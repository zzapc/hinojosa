*----------------------------------------------------------------------*
***INCLUDE LBSPLF02 .
*----------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  BSPL_GRID_TOTALS_CALCULATE
*&---------------------------------------------------------------------*
FORM bspl_grid_totals_calculate
                      TABLES pt_node     TYPE      tt_rsthie
                             pt_data     TYPE      tt_bspl_data
                             pt_totals   TYPE      tt_grid_totals
                       USING is_settings STRUCTURE rfbila_alv_settings.
* local data declarations
  DATA: l_tfill_from      LIKE sy-tabix,
        l_tfill_to        LIKE sy-tabix,
        l_node_level      LIKE rsthie-tlevel,
        l_high_node_level LIKE rsthie-tlevel,
        ls_node_ergsl     TYPE ts_node_ergsl.

* notice highest node level
  LOOP AT pt_node ASSIGNING <rsthie>.
    IF l_high_node_level < <rsthie>-tlevel.
      l_high_node_level = <rsthie>-tlevel.
    ENDIF.
  ENDLOOP.

* calculate totals
  l_node_level = l_high_node_level + 1.
  DO l_high_node_level TIMES.
    l_node_level = l_node_level - 1.
    LOOP AT pt_node ASSIGNING <rsthie>.
      CHECK <rsthie>-type   = con_leaf.
      CHECK <rsthie>-tlevel = l_node_level.

      READ TABLE pt_data WITH KEY
                              id = <rsthie>-id
                              BINARY SEARCH.
      IF sy-subrc = 0.
        pt_totals-id       = pt_data-id.
        pt_totals-ergsl    = pt_data-ergsl.
        pt_totals-type     = <rsthie>-type.
        pt_totals-tlevel   = <rsthie>-tlevel.
        pt_totals-curtp    = pt_data-curtp.
        pt_totals-waers    = pt_data-waers.
        pt_totals-repval   = pt_data-repval.
        pt_totals-compval  = pt_data-compval.
        COLLECT pt_totals.                                  "n1696778
      ENDIF.
    ENDLOOP.

    DESCRIBE TABLE pt_totals LINES sy-tfill.
    IF sy-tfill > l_tfill_to.
      l_tfill_from = l_tfill_to + 1.
      l_tfill_to   = sy-tfill.
    ELSE.
      CONTINUE.               " >>>>  Next DO  >>>>
    ENDIF.
    LOOP AT pt_totals FROM l_tfill_from
                      TO   l_tfill_to.
*     Parent of node ID get
      READ TABLE pt_node INDEX pt_totals-id.
      READ TABLE pt_node INDEX pt_node-parent.
      IF pt_node-type = con_bpos
      OR pt_node-type = con_top.
        ls_node_ergsl = pt_node-name.
        pt_totals-ergsl = ls_node_ergsl-ergsl.
      ENDIF.
      pt_totals-id     = pt_node-id.
      pt_totals-type   = pt_node-type.
      pt_totals-tlevel = pt_node-tlevel.
      COLLECT pt_totals.
    ENDLOOP.
  ENDDO.
  SORT pt_totals.

  IF is_settings-zeroacct = con_x.
*.. accounts with zero balance required
    l_node_level = l_high_node_level + 1.
    DO l_high_node_level TIMES.
      l_node_level = l_node_level - 1.
      LOOP AT pt_node ASSIGNING <rsthie>.
        CHECK <rsthie>-type   = con_acct
           OR <rsthie>-type   = con_fber.
        CHECK <rsthie>-tlevel = l_node_level.

        READ TABLE pt_data WITH KEY
                                id = <rsthie>-id
                                BINARY SEARCH.
        IF sy-subrc = 0.
          pt_totals-id       = pt_data-id.
          pt_totals-ergsl    = pt_data-ergsl.
          pt_totals-type     = <rsthie>-type.
          pt_totals-tlevel   = <rsthie>-tlevel.
          pt_totals-curtp    = pt_data-curtp.
          pt_totals-waers    = pt_data-waers.
          pt_totals-repval   = pt_data-repval.
          pt_totals-compval  = pt_data-compval.
          COLLECT pt_totals.                                "n1696778
        ENDIF.
      ENDLOOP.

      DESCRIBE TABLE pt_totals LINES sy-tfill.
      IF sy-tfill > l_tfill_to.
        l_tfill_from = l_tfill_to + 1.
        l_tfill_to   = sy-tfill.
      ELSE.
        CONTINUE.               " >>>>  Next DO  >>>>
      ENDIF.
      LOOP AT pt_totals FROM l_tfill_from
                        TO   l_tfill_to.
*.....  Parent of node ID get
        READ TABLE pt_node INDEX pt_totals-id.
        READ TABLE pt_node INDEX pt_node-parent.
        IF pt_node-type = con_bpos
        OR pt_node-type = con_top.
          ls_node_ergsl = pt_node-name.
          pt_totals-ergsl = ls_node_ergsl-ergsl.
        ENDIF.
        pt_totals-id     = pt_node-id.
        pt_totals-type   = pt_node-type.
        pt_totals-tlevel = pt_node-tlevel.
        COLLECT pt_totals.
      ENDLOOP.
    ENDDO.
    SORT pt_totals.
  ENDIF.
ENDFORM.                    " BSPL_GRID_TOTALS_CALCULATE

*&---------------------------------------------------------------------*
*&      Form  BSPL_GRID_OUTTAB_FILL
*&---------------------------------------------------------------------*
FORM bspl_grid_outtab_fill
                        TABLES pt_node      TYPE tt_rsthie
                               pt_data      TYPE tt_bspl_data
                               pt_totals    TYPE tt_grid_totals
                               pt_text      TYPE tt_ergsl_text
                               pt_edit      TYPE tt_edit_settings
                               pt_outtab    TYPE tt_grid_outtab
                   USING VALUE(ps_settings) LIKE rfbila_alv_settings.

*     local data declaration
  DATA: lt_predecessor TYPE tt_grid_predecessor WITH HEADER LINE.
  DATA: l_id           LIKE rsthie-id.
  DATA: l_fberflg      LIKE con_x.
  DATA: l_fber_tlevel  LIKE rsthie-tlevel.
  DATA: ls_node_saknr  TYPE ts_node_saknr.
  DATA: lf_node_bukrs  LIKE t001-bukrs.
  DATA: lf_node_fkber  LIKE tfkb-fkber.

  IF gts_data IS INITIAL.                         "begin "n2084383
    PERFORM gts_data_fill
      USING
        pt_data[]
        ps_settings-altacct
        t011-xergs
      CHANGING
        gts_data[].
  ENDIF.                                          "end "n2084383

  LOOP AT pt_node ASSIGNING <rsthie>.
    CASE <rsthie>-type.
      WHEN con_top.
      WHEN con_bpos.
        CLEAR: l_fberflg.
        PERFORM bspl_grid_bpos_to_outtab TABLES pt_totals
                                                pt_text
                                                pt_edit
                                                pt_outtab
                                                lt_predecessor
                                         USING  <rsthie>
                                                ps_settings
                                       CHANGING l_id.
      WHEN con_acct.
        ls_node_saknr = <rsthie>-name.
        CHECK: g_list_state = con_acct.
        CHECK: l_fberflg     IS INITIAL
           OR  l_fber_tlevel =  <rsthie>-tlevel.
        PERFORM bspl_grid_acct_to_outtab TABLES pt_totals
                                                pt_outtab
                                                pt_data
                                         USING  <rsthie>
                                                ps_settings
                                                gts_data    "n2084383
                                       CHANGING l_id.

      WHEN con_fber.
        lf_node_fkber = <rsthie>-name.
        l_fberflg = con_x.
        l_fber_tlevel = <rsthie>-tlevel.
        CHECK: g_list_state = con_acct.
        PERFORM bspl_grid_fber_to_outtab TABLES pt_totals
                                                pt_outtab
                                                pt_data
                                         USING  <rsthie>
                                                ps_settings
                                       CHANGING l_id.
      WHEN con_ccod.
        lf_node_bukrs = <rsthie>-name.
        CHECK: g_list_state = con_ccod.
        PERFORM bspl_grid_ccod_to_outtab TABLES pt_totals
                                                pt_outtab
                                                pt_data
                                         USING  <rsthie>
                                                ps_settings
                                                ls_node_saknr
                                                lf_node_fkber
                                                l_fberflg
                                                gts_data    "n2084383
                                       CHANGING l_id.
      WHEN con_busa.
      WHEN con_leaf.
        CHECK: g_list_state = con_leaf.
        PERFORM bspl_grid_leaf_to_outtab TABLES pt_totals
                                                pt_outtab
                                                pt_data
                                         USING  <rsthie>
                                                ps_settings
                                                ls_node_saknr
                                                lf_node_bukrs
                                                lf_node_fkber
                                                l_fberflg
                                                gts_data    "n2084383
                                       CHANGING l_id.
    ENDCASE.
  ENDLOOP.

  IF NOT ( lt_predecessor[] IS INITIAL ).
*   put totals of remaining predecessors to OUTTAB
    PERFORM bspl_grid_predecessor_2_outtab
                                    TABLES pt_totals
                                           pt_text
                                           pt_edit
                                           pt_outtab
                                           lt_predecessor
                                    USING  '01'      "<<< TLEVEL
                                           ps_settings
                                 CHANGING  l_id.
  ENDIF.

  CLEAR gts_data.                                           "n2388560
  FREE gts_data.                                            "n2388560
  IF  x_bukrs_unique <> space                     "n2487300 "n2528042
  AND x_rbusa_unique <> space                     "n2528042 "n2736342
  AND x_racct_unique <> space.                              "n2736342
    CLEAR gt_rsthie. FREE gt_rsthie.
  CLEAR gt_bspldata. FREE gt_bspldata.
  CLEAR gt_gridtotals. FREE gt_gridtotals.
  DATA lr_ref TYPE REF TO data.
  FIELD-SYMBOLS <lt_tree> TYPE STANDARD TABLE.
  CREATE DATA lr_ref TYPE STANDARD TABLE OF snode.
  ASSIGN lr_ref->* TO <lt_tree>.
  IF sy-subrc = 0.
    ASSIGN ('(SAPLSEUT)tree[]') TO <lt_tree>.
    IF sy-subrc = 0.

      CLEAR <lt_tree>. FREE <lt_tree>.
    ENDIF.
  ENDIF.
  ENDIF.                                                    "n2487300

ENDFORM.                    " BSPL_GRID_OUTTAB_FILL

*&---------------------------------------------------------------------*
*&      Form  BSPL_GRID_ACCT_TO_OUTTAB
*&---------------------------------------------------------------------*
FORM bspl_grid_acct_to_outtab
                       TABLES pt_totals    TYPE tt_grid_totals
                              pt_outtab    TYPE tt_grid_outtab
                              pt_data      TYPE tt_bspl_data
                 USING  VALUE(ps_node)     LIKE rsthie
                        VALUE(ps_settings) LIKE rfbila_alv_settings
                              its_data     TYPE ttx_data    "n2084383
              CHANGING  VALUE(p_id)        LIKE rsthie-id.
*----------------------------------------------------------------------*
*     local data declarations
  DATA: ls_node_saknr       TYPE ts_node_saknr.
  DATA ld_id                TYPE seu_id.                    "n2084383
  DATA ld_tabix             TYPE sytabix.                   "n2084383
*----------------------------------------------------------------------*
  FIELD-SYMBOLS <lsx_data>  TYPE tsx_data.                  "n2084383
*----------------------------------------------------------------------*
  CLEAR: pt_outtab.
  ls_node_saknr = ps_node-name.
  CALL FUNCTION 'READ_HAUPTBUCH_TEXT'
    EXPORTING
      kontenplan     = ls_node_saknr-ktopl
      sachkonto      = ls_node_saknr-saknr
      sprache        = is_settings-fs_language
    IMPORTING
      text_wa        = skat
    EXCEPTIONS
      text_not_found = 1
      OTHERS         = 2.
  IF sy-subrc = 0.
*.. fix account number to relevant length
    PERFORM bspl_account_length_fix
                           CHANGING ls_node_saknr.
    CONCATENATE ls_node_saknr-saknr
                skat-txt50
           INTO pt_outtab-text SEPARATED BY ' '.
  ELSE.
*.. fix account number to relevant length
    PERFORM bspl_account_length_fix
                           CHANGING ls_node_saknr.
    CONCATENATE ls_node_saknr-ktopl
                ls_node_saknr-saknr
           INTO pt_outtab-text SEPARATED BY ' '.
  ENDIF.

  p_id = p_id + 1.
  pt_outtab-id      = p_id.        .
  pt_outtab-subid   = 1.
* read totals of account number
  READ TABLE pt_totals WITH KEY id = ps_node-id
                                     BINARY SEARCH.
  IF sy-subrc = 0.
    pt_outtab-ergsl   = pt_totals-ergsl.
    pt_outtab-curtp   = pt_totals-curtp.
    pt_outtab-waers   = pt_totals-waers.
    pt_outtab-repval  = pt_totals-repval.
    pt_outtab-compval = pt_totals-compval.
    pt_outtab-tlevel  = pt_totals-tlevel.
*.. calculate variance
    PERFORM bspl_variance_calculate USING ps_settings-comptype
                                          pt_outtab-repval
                                          pt_outtab-compval
                                 CHANGING pt_outtab-absvar
                                          pt_outtab-relvar_d "n2812892
                                          pt_outtab-relvar.

*.. provide fields like ccode, busarea, funcarea, account no etc.
    ls_node_saknr = ps_node-name.
    IF NOT ( ps_settings-altacct IS INITIAL ).
*.... alternative account number is used
      ASSIGN: pt_data-ktop2 TO <ktopl>,
              pt_data-altkt TO <racct>.
    ELSEIF NOT ( t011-xergs IS INITIAL ).
*.... group account number is used
      ASSIGN: pt_data-kktpl TO <ktopl>,
              pt_data-bilkt TO <racct>.
    ELSE.
*.... default normal accounts used
      ASSIGN: pt_data-ktopl TO <ktopl>,
              pt_data-racct TO <racct>.
    ENDIF.


    READ TABLE its_data                         "begin "n2084383
      WITH KEY
        ergsl = pt_totals-ergsl
        racct = ls_node_saknr-saknr
      BINARY SEARCH
      TRANSPORTING NO FIELDS.
    IF sy-subrc = 0.
      ld_tabix = sy-tabix.
*      LOOP AT pt_data WHERE ergsl = pt_totals-ergsl.
*     CHECK <KTOPL> = LS_NODE_SAKNR-KTOPL. "n1559627
      LOOP AT its_data
        ASSIGNING <lsx_data>
        FROM ld_tabix.

        IF <lsx_data>-ergsl <> pt_totals-ergsl
        OR <lsx_data>-racct <> ls_node_saknr-saknr.
          EXIT.
        ENDIF.

        LOOP AT <lsx_data>-t_id
          INTO ld_id.
          READ TABLE pt_data
            WITH KEY
              id = ld_id
            BINARY SEARCH.                          "end "n2084383
          CHECK <racct> = ls_node_saknr-saknr.
          pt_outtab-ktopl  = pt_data-ktopl.
*     provide operational account no
          IF pt_outtab-racct IS INITIAL.
            pt_outtab-racct  = pt_data-racct.
          ELSEIF pt_outtab-racct <> pt_data-racct.
*       1:n assignment between group account no
*                    and operational account no
            pt_outtab-racct = con_stars.
          ENDIF.
          pt_outtab-kktpl  = pt_data-kktpl.
          pt_outtab-bilkt  = pt_data-bilkt.
          pt_outtab-ktop2  = pt_data-ktop2.
          pt_outtab-altkt  = pt_data-altkt.
          pt_outtab-ryear  = pt_data-ryear.
          pt_outtab-poper  = pt_data-poper.
*     provide company code
          IF pt_outtab-rbukrs IS INITIAL.
            pt_outtab-rbukrs = pt_data-rbukrs.
          ELSEIF pt_outtab-rbukrs <> pt_data-rbukrs.
*       more than one ccode, mark it with stars
            pt_outtab-rbukrs = con_stars.
            CLEAR x_bukrs_unique.
          ENDIF.
*     provide business area
          IF pt_outtab-rbusa IS INITIAL
         AND pt_data-rbusa   IS INITIAL.
            pt_outtab-rbusa  = sy-vline.
          ELSEIF pt_outtab-rbusa IS INITIAL.
            pt_outtab-rbusa  = pt_data-rbusa.
          ELSEIF pt_outtab-rbusa <> pt_data-rbusa.
*       more than one busarea, mark it with stars
            pt_outtab-rbusa = con_stars.
            CLEAR x_rbusa_unique.
          ENDIF.
*     privide function area
          IF pt_outtab-rfarea IS INITIAL.
            pt_outtab-rfarea = pt_data-rfarea.
          ELSEIF pt_outtab-rfarea <> pt_data-rfarea.
*       more than one function area, mark it with stars
            pt_outtab-rfarea = con_stars.
          ENDIF.
        ENDLOOP.
      ENDLOOP.                                              "n2084383
    ENDIF.                                                  "n2084383

    IF pt_outtab-rbusa  = sy-vline.
*.... only initial business area
      CLEAR: pt_outtab-rbusa.
    ENDIF.
    PERFORM bspl_grid_cell_color_set
                            TABLES pt_outtab-cell_color.
  ENDIF.

  APPEND pt_outtab.

ENDFORM.                    " BSPL_GRID_ACCT_TO_OUTTAB

*&---------------------------------------------------------------------*
*&      Form  BSPL_GRID_BPOS_TO_OUTTAB
*&---------------------------------------------------------------------*
FORM bspl_grid_bpos_to_outtab
                       TABLES pt_totals      TYPE tt_grid_totals
                              pt_text        TYPE tt_ergsl_text
                              pt_edit        TYPE tt_edit_settings
                              pt_outtab      TYPE tt_grid_outtab
                              pt_predecessor TYPE tt_grid_predecessor
                   USING  VALUE(ps_node)     LIKE rsthie
                          VALUE(ps_settings) LIKE rfbila_alv_settings
                CHANGING  VALUE(p_id)        LIKE rsthie-id.
*     local data declarations
  DATA: ls_predecessor_info LIKE snodetext,
        ls_node_ergsl       TYPE ts_node_ergsl,
        l_tabix             LIKE sy-tabix.

* check if current node (PS_NODE) has a predecessor ?
* ---------------------------------------------------
  CALL FUNCTION 'RS_TREE_GET_PREDECESSOR'
    EXPORTING
      node_id          = ps_node-id
    IMPORTING
      predecessor_info = ls_predecessor_info
    EXCEPTIONS
      id_not_found     = 1
      no_predecessor   = 2
      OTHERS           = 3.

  IF sy-subrc = 0.
*   save predecessor's info
    ls_node_ergsl         = ls_predecessor_info-name.
    pt_predecessor-tlevel = ls_predecessor_info-tlevel.
    pt_predecessor-id     = ls_predecessor_info-id.
    pt_predecessor-ergsl  = ls_node_ergsl-ergsl.
    APPEND pt_predecessor.
*   put totals of predecessors to OUTTAB
    PERFORM bspl_grid_predecessor_2_outtab
                                    TABLES pt_totals
                                           pt_text
                                           pt_edit
                                           pt_outtab
                                           pt_predecessor
                                    USING  ps_node-tlevel
                                           ps_settings
                                 CHANGING  p_id.
  ENDIF.

* check if current node (PS_NODE) has a successor ?
* -------------------------------------------------
  IF ps_node-next IS INITIAL.
*   there is no successor
*   save the ID because you don't get it as a predecessor
    ls_node_ergsl = ps_node-name.
    pt_predecessor-tlevel = ps_node-tlevel.
    pt_predecessor-id     = ps_node-id.
    pt_predecessor-ergsl  = ls_node_ergsl-ergsl.
    APPEND pt_predecessor.
  ENDIF.

* read group begin text of current node
  ls_node_ergsl = ps_node-name.
  READ TABLE pt_text WITH KEY ergsl = ls_node_ergsl-ergsl
                              txtyp = con_a
                              BINARY SEARCH.
  l_tabix = sy-tabix + 1.
  IF sy-subrc = 0.
*   add first line (group begin text) of actual node
    p_id = p_id + 1.
    CLEAR: pt_outtab.
    pt_outtab-id         = p_id.        .
    pt_outtab-subid      = pt_text-zeile.
    pt_outtab-ergsl      = ls_node_ergsl-ergsl.
    pt_outtab-text       = pt_text-txt45.
    pt_outtab-tlevel     = ps_node-tlevel.
    PERFORM bspl_grid_cell_color_set
                          TABLES pt_outtab-cell_color.
    APPEND pt_outtab.

*   add the rest lines (group begin text) of actual node
    CLEAR: pt_outtab.
    LOOP AT pt_text FROM l_tabix
                   WHERE ergsl = ls_node_ergsl-ergsl
                     AND txtyp = con_a.
      pt_outtab-id         = p_id.
      pt_outtab-subid      = pt_text-zeile.
      pt_outtab-ergsl      = ls_node_ergsl-ergsl.
      pt_outtab-text       = pt_text-txt45.
      pt_outtab-tlevel     = ps_node-tlevel.

      PERFORM bspl_grid_cell_color_set
                          TABLES pt_outtab-cell_color.
      APPEND pt_outtab.
    ENDLOOP.
  ENDIF.
ENDFORM.                    " BSPL_GRID_BPOS_TO_OUTTAB

*&---------------------------------------------------------------------*
*&      Form  BSPL_GRID_FBER_TO_OUTTAB
*&---------------------------------------------------------------------*
FORM bspl_grid_fber_to_outtab
                       TABLES pt_totals    TYPE tt_grid_totals
                              pt_outtab    TYPE tt_grid_outtab
                              pt_data      TYPE tt_bspl_data
                 USING  VALUE(ps_node)     LIKE rsthie
                        VALUE(ps_settings) LIKE rfbila_alv_settings
              CHANGING  VALUE(p_id)        LIKE rsthie-id.


  CLEAR: pt_outtab.
  SELECT SINGLE fkbtx INTO pt_outtab-text
                      FROM tfkbt WHERE spras = is_settings-fs_language
                                   AND fkber = ps_node-name.
  IF sy-subrc = 0.
    CONCATENATE ps_node-name
                pt_outtab-text
           INTO pt_outtab-text SEPARATED BY ' '.
  ELSE.
    pt_outtab-text = ps_node-name.
  ENDIF.

  p_id = p_id + 1.
  pt_outtab-id      = p_id.        .
  pt_outtab-subid   = 1.
* read totals of function area
  READ TABLE pt_totals WITH KEY id = ps_node-id
                                     BINARY SEARCH.
  pt_outtab-ergsl   = pt_totals-ergsl.
  pt_outtab-curtp   = pt_totals-curtp.
  pt_outtab-waers   = pt_totals-waers.
  pt_outtab-repval  = pt_totals-repval.
  pt_outtab-compval = pt_totals-compval.
  pt_outtab-tlevel  = pt_totals-tlevel.
* calculate variance
  PERFORM bspl_variance_calculate USING ps_settings-comptype
                                        pt_outtab-repval
                                        pt_outtab-compval
                               CHANGING pt_outtab-absvar
                                        pt_outtab-relvar_d  "n2812892
                                        pt_outtab-relvar.

* provide fields like ccode, busarea, funcarea, account no etc.
  LOOP AT pt_data WHERE ergsl  = pt_totals-ergsl
                    AND rfarea = ps_node-name.
    pt_outtab-rfarea = pt_data-rfarea.
    pt_outtab-ryear  = pt_data-ryear.
    pt_outtab-poper  = pt_data-poper.
*   provide group gl account
    pt_outtab-ktopl  = pt_data-ktopl.
    IF pt_outtab-racct IS INITIAL.
      pt_outtab-racct  = pt_data-racct.
    ELSEIF pt_outtab-racct <> pt_data-racct.
*     more than one, mark it with stars
      pt_outtab-racct  = con_stars.
      CLEAR x_racct_unique.                                 "n2499736
    ENDIF.
*   provide gl account
    pt_outtab-kktpl  = pt_data-kktpl.
    IF pt_outtab-bilkt IS INITIAL.
      pt_outtab-bilkt  = pt_data-bilkt.
    ELSEIF pt_outtab-bilkt <> pt_data-bilkt.
*     more than one, mark it with stars
      pt_outtab-bilkt  = con_stars.
    ENDIF.
*   provide alternative gl account
    pt_outtab-ktop2  = pt_data-ktop2.
    IF pt_outtab-altkt IS INITIAL.
      pt_outtab-altkt  = pt_data-altkt.
    ELSEIF pt_outtab-altkt <> pt_data-altkt.
*     more than one, mark it with stars
      pt_outtab-altkt  = con_stars.
    ENDIF.
*   provide company code
    IF pt_outtab-rbukrs IS INITIAL.
      pt_outtab-rbukrs = pt_data-rbukrs.
    ELSEIF pt_outtab-rbukrs <> pt_data-rbukrs.
*     more than one ccode, mark it with stars
      pt_outtab-rbukrs = con_stars.
      CLEAR x_bukrs_unique.
    ENDIF.
*   provide business area
    IF pt_outtab-rbusa IS INITIAL
   AND pt_data-rbusa   IS INITIAL.
      pt_outtab-rbusa  = sy-vline.
    ELSEIF pt_outtab-rbusa IS INITIAL.
      pt_outtab-rbusa  = pt_data-rbusa.
    ELSEIF pt_outtab-rbusa <> pt_data-rbusa.
*     more than one busarea, mark it with stars
      pt_outtab-rbusa = con_stars.
      CLEAR x_rbusa_unique.
    ENDIF.
  ENDLOOP.

  IF pt_outtab-rbusa  = sy-vline.
*   only initial business area
    CLEAR: pt_outtab-rbusa.
  ENDIF.

  PERFORM bspl_grid_cell_color_set
                          TABLES pt_outtab-cell_color.
  APPEND pt_outtab.

ENDFORM.                    " BSPL_GRID_FBER_TO_OUTTAB

*&---------------------------------------------------------------------*
*&      Form  BSPL_GRID_PREDECESSOR_2_OUTTAB
*&---------------------------------------------------------------------*
FORM bspl_grid_predecessor_2_outtab
                       TABLES pt_totals       TYPE tt_grid_totals
                              pt_text         TYPE tt_ergsl_text
                              pt_edit         TYPE tt_edit_settings
                              pt_outtab       TYPE tt_grid_outtab
                              pt_predecessor  TYPE tt_grid_predecessor
                    USING  VALUE(p_tlevel)    LIKE rsthie-tlevel
                           VALUE(ps_settings) LIKE rfbila_alv_settings
                 CHANGING  VALUE(p_id)        LIKE rsthie-id.

*     local data declarations
  DATA: l_tabix LIKE sy-tabix.

  SORT pt_predecessor BY tlevel DESCENDING.
  LOOP AT pt_predecessor WHERE tlevel GE p_tlevel.
*   read edit infos of predecessor
    READ TABLE pt_edit WITH KEY ergsl = pt_predecessor-ergsl
                                        BINARY SEARCH.
*   check, if totals output is required ??
    IF pt_edit-total = con_x.
*     read totals of predecessor
      READ TABLE pt_totals WITH KEY id = pt_predecessor-id
                                         BINARY SEARCH.
      IF sy-subrc <> 0.
        CONTINUE.
      ENDIF.
*     read group end text of predecessor
      CLEAR: pt_outtab.
      READ TABLE pt_text WITH KEY ergsl = pt_predecessor-ergsl
                                  txtyp = con_e
                                  BINARY SEARCH.
      l_tabix = sy-tabix + 1.

      IF sy-subrc <> 0.
*       READ TABLE PT_TEXT WITH KEY ERGSL = PT_PREDECESSOR-ERGSL
*                                   TXTYP = CON_K
*                                   BINARY SEARCH.
*       CONCATENATE TEXT-004 PT_TEXT-TXT45
*              INTO PT_OUTTAB-TEXT SEPARATED BY ' '.
      ELSE.
        pt_outtab-text = pt_text-txt45.
      ENDIF.
*     add first line (group end text) and totals
*     of predecessor to PT_OUTTAB
      p_id = p_id + 1.
      pt_outtab-id         = p_id.        .
      pt_outtab-subid      = pt_text-zeile.
      pt_outtab-ergsl      = pt_predecessor-ergsl.
      pt_outtab-curtp      = pt_totals-curtp.
      pt_outtab-waers      = pt_totals-waers.
      pt_outtab-repval     = pt_totals-repval.
      pt_outtab-compval    = pt_totals-compval.
      pt_outtab-tlevel     = pt_predecessor-tlevel.
*     calculate variance
      PERFORM bspl_variance_calculate USING ps_settings-comptype
                                            pt_outtab-repval
                                            pt_outtab-compval
                                   CHANGING pt_outtab-absvar
                                            pt_outtab-relvar_d "n2812892
                                            pt_outtab-relvar.

      PERFORM bspl_grid_line_color_set
                          CHANGING pt_outtab-line_color.
      pt_outtab-totals_level = pt_outtab-tlevel - 1.        "n1486648
      PERFORM bspl_grid_cell_color_set
                            TABLES pt_outtab-cell_color.
      APPEND pt_outtab.

*     add the rest lines  (group end text)
*     of predecessor to PT_OUTTAB
      CLEAR: pt_outtab.
      LOOP AT pt_text FROM l_tabix
                     WHERE ergsl = pt_predecessor-ergsl
                       AND txtyp = con_e.
        pt_outtab-id         = p_id.
        pt_outtab-subid      = pt_text-zeile.
        pt_outtab-ergsl      = pt_predecessor-ergsl.
        pt_outtab-text       = pt_text-txt45.
        pt_outtab-tlevel     = pt_predecessor-tlevel.
*       PERFORM BSPL_GRID_LINE_COLOR_SET
*                           CHANGING PT_OUTTAB-LINE_COLOR.
        PERFORM bspl_grid_cell_color_set
                              TABLES pt_outtab-cell_color.
        APPEND pt_outtab.
      ENDLOOP.
    ENDIF.

*   check, if graduated totals output is required ??
    IF pt_edit-gradt = con_x.
      PERFORM bspl_grid_graduated_t_2_outtab
                                      TABLES pt_totals
                                             pt_text
                                             pt_outtab
                                      USING  pt_predecessor
                                             pt_edit
                                             p_tlevel
                                             ps_settings
                                   CHANGING  p_id.
    ENDIF.
  ENDLOOP.

  DELETE pt_predecessor
         WHERE tlevel GE p_tlevel.
ENDFORM.                    " BSPL_GRID_PREDECESSOR_2_OUTTAB

*&---------------------------------------------------------------------*
*&      Form  BSPL_GRID_ALV_INTERFACE_SET
*&---------------------------------------------------------------------*
FORM bspl_grid_alv_interface_set
              TABLES pt_fieldcat         TYPE      slis_t_fieldcat_alv
               USING VALUE(ps_settings)  STRUCTURE rfbila_alv_settings
            CHANGING VALUE(p_cb_program) LIKE      sy-repid
                     VALUE(ps_variant)   STRUCTURE disvariant
                     VALUE(ps_layout)    TYPE      slis_layout_alv.

* call back settings
*  p_cb_program       = 'SAPLBSPL'. "jjr
  p_cb_program       = 'SAPLZFI_BSPL'. "jjr

* variant settings
  ps_variant-report   = ps_settings-repid.
  ps_variant-handle   = con_grid.
  ps_variant-username = sy-uname.
  ps_variant-variant  = ps_settings-grid_vari.
* layout settings
  ps_layout-info_fieldname   = 'LINE_COLOR'.
  ps_layout-coltab_fieldname = 'CELL_COLOR'.
* fieldcat settings
  LOOP AT pt_fieldcat ASSIGNING <fieldcat>.
    CASE <fieldcat>-fieldname.
      WHEN 'ID'
        OR 'SUBID'
        OR 'NPAGE'
        OR 'TLEVEL'
        OR 'LINE_COLOR'.
        <fieldcat>-no_out = con_x.

      WHEN 'TEXT'.
        PERFORM bspl_grid_column_color_set
                              CHANGING <fieldcat>-emphasize.
      WHEN 'RELVAR'.
        <fieldcat>-just = 'R'.
    ENDCASE.
  ENDLOOP.
ENDFORM.                    " BSPL_GRID_ALV_INTERFACE_SET

*&---------------------------------------------------------------------*
*&      Form  BSPL_GRID_CELL_COLOR_SET
*&---------------------------------------------------------------------*
FORM bspl_grid_cell_color_set
                   TABLES pt_cell_color TYPE shp_slis_t_specialcol_alv.
*     local data declarations
  DATA: ls_cell_color TYPE shp_slis_specialcol_alv.

  ls_cell_color-fieldname = 'TEXT'.
  ls_cell_color-color-col =  4.
  ls_cell_color-color-int =  0.
  APPEND ls_cell_color TO pt_cell_color.

ENDFORM.                    " BSPL_GRID_CELL_COLOR_SET

*&---------------------------------------------------------------------*
*&      Form  BSPL_GRID_COLUMN_COLOR_SET
*&---------------------------------------------------------------------*
FORM bspl_grid_column_color_set
                   CHANGING VALUE(p_emphasize).
* momentan nich aktiv, da bei Spalteneinfärbung
* die Zelleneinfärbung nicht funktioniert hat
* P_EMPHASIZE  = 'C410'.

ENDFORM.                    " BSPL_GRID_COLUMN_COLOR_SET

*&---------------------------------------------------------------------*
*&      Form  BSPL_GRID_LINE_COLOR_SET
*&---------------------------------------------------------------------*
FORM bspl_grid_line_color_set
                 CHANGING VALUE(p_outtab_line_color).

  p_outtab_line_color = 'C30'.

ENDFORM.                    " BSPL_GRID_LINE_COLOR_SET

*&---------------------------------------------------------------------*
*&      Form  BSPL_GRID_TOP
*&---------------------------------------------------------------------*
FORM bspl_grid_top.
* local adata declaration
  STATICS: lt_list_commentary TYPE slis_t_listheader.
  DATA:    ls_list_commentary TYPE slis_listheader.
  DATA:    lf_xnewgl   TYPE c,
           lf_tablines TYPE i,
           lf_start    TYPE i   VALUE 2.

  IF lt_list_commentary[] IS INITIAL.
*.. check if list commentary is filled by caller
    IF NOT it_list_commentary[] IS INITIAL.
*.... yes, take it over
      lt_list_commentary[] = it_list_commentary[].
    ELSE.
*.... no, then set default list commentary
      ls_list_commentary-typ  = 'H'.
      ls_list_commentary-info = t011t-vstxt.
      APPEND ls_list_commentary
          TO lt_list_commentary.
    ENDIF.
  ENDIF.

* fill standard page header
  bhdgd-inifl = con_0.
  bhdgd-lines = sy-linsz.
  bhdgd-uname = sy-uname.
  bhdgd-repid = is_settings-repid.
  READ TABLE lt_list_commentary INDEX 1
                                 INTO ls_list_commentary.
  bhdgd-line1 = ls_list_commentary-info.
  bhdgd-line2 = is_settings-allgline.
  bhdgd-bukrs = is_settings-bukrs.
  bhdgd-domai = 'BUKRS'.
  bhdgd-start_pagno = is_settings-page_no.

* write standard page header
  PERFORM batch-heading1(rsbtchh0) USING bhdgd.

* If form routine is called from RFBILA00 and NewGL is active:
* Do not write the ledger in the header of the list (ledger is
* on index 2 in table LT_LIST_COMMENTARY in this case). The ledger
* is already displayed by the BATCH-HEADING routine from log. DB.
*  IF is_settings-repid = 'RFBILA00'.
  IF is_settings-repid = 'ZFI_RFBILA00'. "JJR 08.06.2021
    CALL FUNCTION 'FAGL_CHECK_GLFLEX_ACTIVE'
      EXPORTING
        id_bukrs        = is_settings-bukrs
      IMPORTING
        e_glflex_active = lf_xnewgl.
    IF NOT lf_xnewgl IS INITIAL.
      lf_start = 2.                                         "n1489749
    ENDIF.
  ENDIF.
  SKIP 1.
  DESCRIBE TABLE lt_list_commentary LINES lf_tablines.
  IF lf_start > lf_tablines. lf_start = lf_tablines. ENDIF.
  LOOP AT lt_list_commentary FROM lf_start TO lf_tablines
                             INTO ls_list_commentary.
    CHECK ls_list_commentary-info <> is_settings-allgline.  "n1489749
    WRITE: / ls_list_commentary-key,
             ls_list_commentary-info.
  ENDLOOP.

ENDFORM.                    " BSPL_GRID_TOP
*&---------------------------------------------------------------------*
*&      Form  BSPL_GRID_TOP_HTML
*&---------------------------------------------------------------------*
FORM bspl_grid_top_html USING p_dyndoc_id
                              TYPE REF TO cl_dd_document.
* local adata declaration
  STATICS: lt_list_commentary TYPE slis_t_listheader.
  DATA:    ls_list_commentary TYPE slis_listheader.

  IF lt_list_commentary[] IS INITIAL.
*.. check if list commentary is filled by caller
    IF NOT it_list_commentary[] IS INITIAL.
*.... yes, take it over
      lt_list_commentary[] = it_list_commentary[].
    ELSE.
*.... no, then set default list commentary
      ls_list_commentary-typ  = 'H'.
      ls_list_commentary-info = t011t-vstxt.
      APPEND ls_list_commentary
          TO lt_list_commentary.
    ENDIF.
  ENDIF.

  EXPORT it_list_commentary
    FROM lt_list_commentary TO MEMORY ID 'DYNDOS_FOR_ALV'.

  CALL FUNCTION 'REUSE_ALV_GRID_COMMENTARY_SET'
    EXPORTING
      document = p_dyndoc_id
      bottom   = space.

ENDFORM.                    " BSPL_GRID_TOP_HTML

*&---------------------------------------------------------------------*
*&      Form  BSPL_GRID_GRADUATED_T_2_OUTTAB
*&---------------------------------------------------------------------*
FORM bspl_grid_graduated_t_2_outtab
                       TABLES pt_totals       TYPE tt_grid_totals
                              pt_text         TYPE tt_ergsl_text
                              pt_outtab       TYPE tt_grid_outtab
                  USING VALUE(ps_predecessor) TYPE ts_grid_predecessor
                        VALUE(ps_edit)        TYPE ts_edit_settings
                        VALUE(p_tlevel)       LIKE rsthie-tlevel
                        VALUE(ps_settings)    LIKE rfbila_alv_settings
               CHANGING VALUE(p_id)           LIKE rsthie-id.

* local data declarations
  DATA: l_tabix LIKE sy-tabix.

  CLEAR: pt_outtab.
* calculate graduated totals
  PERFORM bspl_grid_graduated_t_calc TABLES pt_totals
                                      USING ps_predecessor
                                            p_tlevel
                                   CHANGING pt_outtab-repval
                                            pt_outtab-compval.

* read text of graduated totals
  READ TABLE pt_text WITH KEY ergsl = ps_predecessor-ergsl
                              txtyp = con_s
                              BINARY SEARCH.
  IF sy-subrc = 0.
    pt_outtab-text = pt_text-txt45.
  ENDIF.
  l_tabix = sy-tabix + 1.

* add first line (text) and totals
* of graduated totals to PT_OUTTAB
  p_id = p_id + 1.
  pt_outtab-id         = p_id.        .
  pt_outtab-subid      = pt_text-zeile.
  pt_outtab-ergsl      = ps_predecessor-ergsl.
  pt_outtab-curtp      = pt_totals-curtp.
  pt_outtab-waers      = pt_totals-waers.
  pt_outtab-tlevel     = ps_predecessor-tlevel.             "n1486648
* PT_OUTTAB-REPVAL     = PT_TOTALS-REPVAL.
* PT_OUTTAB-COMPVAL    = PT_TOTALS-COMPVAL.
* calculate variance
  PERFORM bspl_variance_calculate USING ps_settings-comptype
                                        pt_outtab-repval
                                        pt_outtab-compval
                               CHANGING pt_outtab-absvar
                                        pt_outtab-relvar_d  "n2812892
                                        pt_outtab-relvar.

  PERFORM bspl_grid_line_color_set
                      CHANGING pt_outtab-line_color.
  pt_outtab-totals_level = pt_outtab-tlevel - 1.            "n1486648
  PERFORM bspl_grid_cell_color_set
                        TABLES pt_outtab-cell_color.
  APPEND pt_outtab.

* add the rest lines  (text)
* of graduated totals to PT_OUTTAB
  CLEAR: pt_outtab.
  LOOP AT pt_text FROM l_tabix
                 WHERE ergsl = ps_predecessor-ergsl
                   AND txtyp = con_s.
    pt_outtab-id         = p_id.
    pt_outtab-subid      = pt_text-zeile.
    pt_outtab-ergsl      = ps_predecessor-ergsl.
    pt_outtab-text       = pt_text-txt45.
    pt_outtab-tlevel     = p_tlevel.
*   PERFORM BSPL_GRID_LINE_COLOR_SET
*                       CHANGING PT_OUTTAB-LINE_COLOR.
    PERFORM bspl_grid_cell_color_set
                          TABLES pt_outtab-cell_color.
    APPEND pt_outtab.
  ENDLOOP.

ENDFORM.                    " BSPL_GRID_GRADUATED_T_2_OUTTAB


*&---------------------------------------------------------------------*
*&      Form  BSPL_GRID_GRADUATED_T_CALC
*&---------------------------------------------------------------------*
FORM bspl_grid_graduated_t_calc
                       TABLES pt_totals       TYPE tt_grid_totals
                  USING VALUE(ps_predecessor) TYPE ts_grid_predecessor
                        VALUE(p_tlevel)       LIKE rsthie-tlevel
               CHANGING VALUE(p_repval)
                        VALUE(p_compval).

* local data declarations
  DATA: ls_predecessor_info LIKE snodetext,
        ls_node_info        LIKE snodetext,
        l_subrc             LIKE sy-subrc.
  STATICS: lr_blnce_sheet_id LIKE RANGE OF snodetext-id.

  IF lr_blnce_sheet_id[] IS INITIAL.
*   fill balance sheet ID's to LR_BLNCE_SHEET_ID
*   dieser Aufruf muss bei Gelegenheit durch einen FB
*   ersetzt werden ( die Zeit , die Zeit)
    PERFORM gr_blnce_sheet_id_fill(saplrgre)
                                   TABLES gt_rsthie
                                          lr_blnce_sheet_id
                                    USING t011-versn.
  ENDIF.

* read totals of current predecessor
  READ TABLE pt_totals WITH KEY id = ps_predecessor-id
                                     BINARY SEARCH.
  p_repval  = p_repval  + pt_totals-repval.
  p_compval = p_compval + pt_totals-compval.

* get totals of all other predecessors
  ls_predecessor_info-id = ps_predecessor-id.
  WHILE l_subrc = 0.
    CALL FUNCTION 'RS_TREE_GET_PREDECESSOR'
      EXPORTING
        node_id          = ls_predecessor_info-id
      IMPORTING
        predecessor_info = ls_predecessor_info
      EXCEPTIONS
        id_not_found     = 1
        no_predecessor   = 2
        OTHERS           = 3.
    IF sy-subrc = 0.
      IF NOT ( ls_predecessor_info-id IN lr_blnce_sheet_id )
         OR lr_blnce_sheet_id IS INITIAL.
*       take only totals of p&l area
*       read totals of predecessor
        READ TABLE pt_totals WITH KEY id = ls_predecessor_info-id
                                           BINARY SEARCH.
        p_repval  = p_repval  + pt_totals-repval.
        p_compval = p_compval + pt_totals-compval.
      ELSE.
        l_subrc = 4.
      ENDIF.
    ELSE.
*     look, if there is a parent
      CALL FUNCTION 'RS_TREE_GET_NODE'
        EXPORTING
          node_id      = ls_predecessor_info-id
        IMPORTING
          node_info    = ls_node_info
        EXCEPTIONS
          id_not_found = 1
          OTHERS       = 2.

      IF sy-subrc = 0.
        ls_predecessor_info-id = ls_node_info-parent.
      ELSE.
        l_subrc = sy-subrc.
      ENDIF.
    ENDIF.
  ENDWHILE.

ENDFORM.                    " BSPL_GRID_GRADUATED_T_CALC
*&---------------------------------------------------------------------*
*&      Form  BSPL_GRID_SORTINFOS_SET
*&---------------------------------------------------------------------*
FORM bspl_grid_sortinfos_set
                      TABLES pt_sort TYPE slis_t_sortinfo_alv.
* local data declaration
  DATA: ls_sort TYPE slis_sortinfo_alv.

  CLEAR: ls_sort.
  ls_sort-spos       = con_01.
  ls_sort-fieldname  = 'NPAGE'.
  ls_sort-tabname    = 'GT_GRIDOUTTAB'.
  ls_sort-up         = con_x.
  ls_sort-obligatory = con_x.
* set new page
  ls_sort-group      = '*'.
  APPEND ls_sort TO pt_sort.

  CLEAR: ls_sort.
  ls_sort-spos       = con_02.
  ls_sort-fieldname  = 'ID'.
  ls_sort-tabname    = 'GT_GRIDOUTTAB'.
  ls_sort-up         = con_x.
  ls_sort-obligatory = con_x.
  APPEND ls_sort TO pt_sort.

  CLEAR: ls_sort.
  ls_sort-spos       = con_03.
  ls_sort-fieldname  = 'SUBID'.
  ls_sort-tabname    = 'GT_GRIDOUTTAB'.
  ls_sort-up         = con_x.
  ls_sort-obligatory = con_x.
  APPEND ls_sort TO pt_sort.

ENDFORM.                    " BSPL_GRID_SORTINFOS_SET
*&---------------------------------------------------------------------*
*&      Form  BSPL_GRID_NEWPAGE_SET
*&---------------------------------------------------------------------*
FORM bspl_grid_newpage_set TABLES pt_outtab TYPE tt_grid_outtab.
* local data declarations
  DATA: l_ergsl LIKE rf011p-ergsl.
  DATA: l_npage TYPE bspl_grid_fieldcat-npage.

  LOOP AT pt_outtab ASSIGNING <outtab>.
*.. skip to a new page on every level 02
    IF <outtab>-tlevel =  con_02
   AND <outtab>-ergsl <> l_ergsl.
      l_ergsl = <outtab>-ergsl.
      l_npage = l_npage + 1.
    ENDIF.
    <outtab>-npage = l_npage.
  ENDLOOP.

ENDFORM.                    " BSPL_GRID_NEWPAGE_SET
*&---------------------------------------------------------------------*
*&      Form  BSPL_GRID_PRINTINFOS_SET
*&---------------------------------------------------------------------*
FORM bspl_grid_printinfos_set USING ps_print TYPE slis_print_alv.
  ps_print-no_print_listinfos = con_x.
ENDFORM.                    " BSPL_GRID_PRINTINFOS_SET
*&---------------------------------------------------------------------*
*&      Form  BSPL_GRID_CCOD_TO_OUTTAB
*&---------------------------------------------------------------------*
FORM bspl_grid_ccod_to_outtab
                       TABLES pt_totals      TYPE tt_grid_totals
                              pt_outtab      TYPE tt_grid_outtab
                              pt_data        TYPE tt_bspl_data
                 USING  VALUE(ps_node)       LIKE rsthie
                        VALUE(ps_settings)   LIKE rfbila_alv_settings
                        VALUE(ps_node_saknr) TYPE ts_node_saknr
                        VALUE(p_node_fkber)  LIKE gt_fkber_id-fkber
                        VALUE(p_fberflg)     LIKE con_x
                              its_data     TYPE ttx_data    "n2084383
              CHANGING  VALUE(p_id)          LIKE rsthie-id.

*----------------------------------------------------------------------*
*     local data declarations
  DATA: ls_node_saknr       TYPE ts_node_saknr.
  DATA: l_bukrs             LIKE t001-bukrs.
  DATA ld_id                TYPE seu_id.                    "n2084383
  DATA ld_tabix             TYPE sytabix.                   "n2084383
*----------------------------------------------------------------------*
  FIELD-SYMBOLS <lsx_data>  TYPE tsx_data.                  "n2084383
*----------------------------------------------------------------------*

  CLEAR: pt_outtab.
  l_bukrs = ps_node-name.

  p_id = p_id + 1.
  pt_outtab-id      = p_id.        .
  pt_outtab-subid   = 1.
* read totals of account number
  READ TABLE pt_totals WITH KEY id = ps_node-id
                                     BINARY SEARCH.
  IF sy-subrc = 0.
    pt_outtab-ergsl   = pt_totals-ergsl.
    pt_outtab-curtp   = pt_totals-curtp.
    pt_outtab-waers   = pt_totals-waers.
    pt_outtab-repval  = pt_totals-repval.
    pt_outtab-compval = pt_totals-compval.
    pt_outtab-tlevel  = pt_totals-tlevel.
*.. calculate variance
    PERFORM bspl_variance_calculate USING ps_settings-comptype
                                          pt_outtab-repval
                                          pt_outtab-compval
                                 CHANGING pt_outtab-absvar
                                          pt_outtab-relvar_d "n2812892
                                          pt_outtab-relvar.

*.. provide fields like ccode, busarea, funcarea, account no etc.
    READ TABLE its_data                         "begin "n2084383
      WITH KEY
        ergsl  = pt_totals-ergsl
        racct  = ps_node_saknr-saknr
        rbukrs = l_bukrs
      BINARY SEARCH
      TRANSPORTING NO FIELDS.
    IF sy-subrc = 0.
      ld_tabix = sy-tabix.
*      LOOP AT pt_data WHERE ergsl = pt_totals-ergsl.
*     CHECK <KTOPL> = LS_NODE_SAKNR-KTOPL. "n1559627
      LOOP AT its_data
        ASSIGNING <lsx_data>
        FROM ld_tabix.

        IF <lsx_data>-ergsl  <> pt_totals-ergsl
        OR <lsx_data>-racct  <> ps_node_saknr-saknr
        OR <lsx_data>-rbukrs <> l_bukrs.
          EXIT.
        ENDIF.

        LOOP AT <lsx_data>-t_id
          INTO ld_id.
          READ TABLE pt_data
            WITH KEY
              id = ld_id
            BINARY SEARCH.
*    LOOP AT pt_data WHERE ergsl = pt_totals-ergsl."end "n2084383

          CHECK pt_data-rbukrs = l_bukrs.

          IF NOT ( ps_settings-altacct IS INITIAL ).
*...... alternative account number is used
            ASSIGN: pt_data-ktop2 TO <ktopl>,
                    pt_data-altkt TO <racct>.
          ELSEIF NOT ( t011-xergs IS INITIAL ).
*...... group account number is used
            ASSIGN: pt_data-kktpl TO <ktopl>,
                    pt_data-bilkt TO <racct>.
          ELSE.
*...... default normal accounts used
            ASSIGN: pt_data-ktopl TO <ktopl>,
                    pt_data-racct TO <racct>.
          ENDIF.

          ls_node_saknr-ktopl = <ktopl>.
          ls_node_saknr-saknr = <racct>.
*     CHECK <KTOPL> = PS_NODE_SAKNR-KTOPL. "n1559627
          CHECK <racct> = ps_node_saknr-saknr.
          IF p_fberflg NE space.
            CHECK pt_data-rfarea = p_node_fkber.
          ENDIF.
*...... get name of account
          IF p_fberflg = space.
            CALL FUNCTION 'READ_HAUPTBUCH_TEXT'
              EXPORTING
                kontenplan     = <ktopl>
                sachkonto      = <racct>
                sprache        = is_settings-fs_language
              IMPORTING
                text_wa        = skat
              EXCEPTIONS
                text_not_found = 1
                OTHERS         = 2.
            IF sy-subrc = 0.
*...... fix account number to relevant length
              PERFORM bspl_account_length_fix
                                     CHANGING ls_node_saknr.
              CONCATENATE ls_node_saknr-saknr
                          skat-txt50
                     INTO pt_outtab-text SEPARATED BY ' '.
            ELSE.
*...... fix account number to relevant length
              PERFORM bspl_account_length_fix
                                     CHANGING ls_node_saknr.
              CONCATENATE ls_node_saknr-ktopl
                          ls_node_saknr-saknr
                       INTO pt_outtab-text SEPARATED BY ' '.
            ENDIF.
*...... get name of functional area
          ELSE.
            SELECT SINGLE fkbtx INTO pt_outtab-text
                                FROM tfkbt
                                WHERE spras = is_settings-fs_language
                                  AND fkber = p_node_fkber.
            IF sy-subrc = 0.
              CONCATENATE p_node_fkber
                          pt_outtab-text
                     INTO pt_outtab-text SEPARATED BY ' '.
            ELSE.
              pt_outtab-text = ps_node-name.
            ENDIF.
          ENDIF.

          pt_outtab-ktopl  = pt_data-ktopl.
*     provide operational account no
          IF pt_outtab-racct IS INITIAL.
            pt_outtab-racct  = pt_data-racct.
          ELSEIF pt_outtab-racct <> pt_data-racct.
*       1:n assignment between group account no
*                    and operational account no
            pt_outtab-racct = con_stars.
            CLEAR x_racct_unique.                           "n2499736
          ENDIF.
          pt_outtab-kktpl  = pt_data-kktpl.
          pt_outtab-bilkt  = pt_data-bilkt.
          pt_outtab-ktop2  = pt_data-ktop2.
          pt_outtab-altkt  = pt_data-altkt.
          pt_outtab-ryear  = pt_data-ryear.
          pt_outtab-poper  = pt_data-poper.
*     provide company code
          IF pt_outtab-rbukrs IS INITIAL.
            pt_outtab-rbukrs = pt_data-rbukrs.
          ELSEIF pt_outtab-rbukrs <> pt_data-rbukrs.
*       more than one ccode, mark it with stars
            pt_outtab-rbukrs = con_stars.
          ENDIF.
*     provide business area
          IF pt_outtab-rbusa IS INITIAL
         AND pt_data-rbusa   IS INITIAL.
            pt_outtab-rbusa  = sy-vline.
          ELSEIF pt_outtab-rbusa IS INITIAL.
            pt_outtab-rbusa  = pt_data-rbusa.
          ELSEIF pt_outtab-rbusa <> pt_data-rbusa.
*       more than one busarea, mark it with stars
            pt_outtab-rbusa = con_stars.
            CLEAR x_rbusa_unique.                           "n2678600
          ENDIF.
*     privide function area
          IF pt_outtab-rfarea IS INITIAL.
            pt_outtab-rfarea = pt_data-rfarea.
          ELSEIF pt_outtab-rfarea <> pt_data-rfarea.
*       more than one function area, mark it with stars
            pt_outtab-rfarea = con_stars.
          ENDIF.
        ENDLOOP.
      ENDLOOP.                                              "n2084383
    ENDIF.                                                  "n2084383

    IF pt_outtab-rbusa  = sy-vline.
*.... only initial business area
      CLEAR: pt_outtab-rbusa.
    ENDIF.
    PERFORM bspl_grid_cell_color_set
                            TABLES pt_outtab-cell_color.
  ENDIF.

  APPEND pt_outtab.

ENDFORM.                    " BSPL_GRID_CCOD_TO_OUTTAB

*&---------------------------------------------------------------------*
*&      Form  BSPL_GRID_PF_STATUS_SET
*&---------------------------------------------------------------------*
FORM bspl_grid_pf_status_set USING t_exuc TYPE slis_t_extab.
* local data declarations
  DATA: lt_exuc TYPE slis_t_extab.
  DATA: ls_exuc TYPE slis_extab.

  lt_exuc = t_exuc.                                         "n2017704
  APPEND '&ALL'     TO lt_exuc.
  APPEND '&SAL'     TO lt_exuc.
  APPEND '&REFRESH' TO lt_exuc.
  APPEND '&EB9'     TO lt_exuc.

*  LOOP AT t_exuc INTO ls_exuc WHERE fcode = '&XINT'.        "n2017704
*    COLLECT ls_exuc INTO lt_exuc.                           "n2017704
*  ENDLOOP.                                                  "n2017704

  CASE g_list_state.
    WHEN con_acct.
      APPEND 'COMP'          TO lt_exuc.
      APPEND 'COMP_BUSA'     TO lt_exuc.
      IF x_bukrs_unique <> space.
        APPEND 'EXPA'        TO lt_exuc.
        IF x_rbusa_unique <> space.
          APPEND 'EXPA_BUSA' TO lt_exuc.
        ENDIF.
      ELSE.
        APPEND 'EXPA_BUSA'   TO lt_exuc.
      ENDIF.
      APPEND 'COMP_ACCT'     TO lt_exuc.          "begin "n2499736
      IF x_racct_unique <> space
      OR x_bukrs_unique =  space.
        APPEND 'EXPA_ACCT'   TO lt_exuc.
      ENDIF.
    WHEN con_ccod.
      APPEND 'EXPA'          TO lt_exuc.
      APPEND 'COMP_BUSA'     TO lt_exuc.
      IF x_bukrs_unique <> space.
        APPEND 'COMP'        TO lt_exuc.
      ENDIF.
      IF x_rbusa_unique <> space.
        APPEND 'EXPA_BUSA'   TO lt_exuc.
      ENDIF.
      APPEND 'EXPA_ACCT'     TO lt_exuc.
      IF x_racct_unique <> space
      OR x_bukrs_unique =  space.
        APPEND 'COMP_ACCT'   TO lt_exuc.
      ENDIF.
    WHEN con_leaf.
      IF x_bukrs_unique <> space.
        APPEND 'COMP'          TO lt_exuc.
      ENDIF.
      APPEND 'EXPA'          TO lt_exuc.
      APPEND 'EXPA_BUSA'     TO lt_exuc.
      APPEND 'EXPA_ACCT'     TO lt_exuc.
      IF x_racct_unique <> space
      OR x_bukrs_unique =  space.
        APPEND 'COMP_ACCT'   TO lt_exuc.
      ENDIF.                                      "end "n2499736
  ENDCASE.

*  SET PF-STATUS 'GRID_FULLSCREEN' jjr
  SET PF-STATUS 'ZGRID_FULLSCREEN'
                           EXCLUDING lt_exuc.


ENDFORM.                    " BSPL_GRID_PF_STATUS_SET

*&---------------------------------------------------------------------*
*&      Form  BSPL_GRID_USER_COMMAND
*&---------------------------------------------------------------------*
FORM bspl_grid_user_command USING p_ucomm     LIKE sy-ucomm
                             ps_selfield TYPE slis_selfield.
*----------------------------------------------------------------------*
  DATA ld_expa_comp.                                        "n2844026
*----------------------------------------------------------------------*
  CASE p_ucomm.
    WHEN 'EXPA'                                             "n2499736
      OR 'EXPA_ACCT'.                                       "n2499736
      g_list_state        = con_ccod.
      ps_selfield-refresh = con_x.
      ld_expa_comp = con_x.                                 "n2844026

    WHEN 'COMP'                                             "n2499736
      OR 'COMP_ACCT'.                                       "n2499736
      g_list_state        = con_acct.
      ps_selfield-refresh = con_x.
      ld_expa_comp = con_x.                                 "n2844026

    WHEN 'EXPA_BUSA'.
      g_list_state        = con_leaf.
      ps_selfield-refresh = con_x.
      ld_expa_comp = con_x.                                 "n2844026

    WHEN 'COMP_BUSA'.
      g_list_state        = con_ccod.
      ps_selfield-refresh = con_x.
      ld_expa_comp = con_x.                                 "n2844026

    WHEN 'SEL'.
      CALL FUNCTION 'FB_SELECTIONS_DISPLAY'
        EXPORTING
          ed_program = sy-cprog
          ed_mode    = '1'.

    WHEN 'REPORT_ASSIGNMENT'.                     "begin "n1696349
      PERFORM assign_report.
      EXIT.                                                 "n1795884

    WHEN 'REPORT_CALL'.
      PERFORM call_report
        USING
          ps_selfield.                            "end "n1696349
      EXIT.
* >> JJR 28.08.2021
    WHEN 'XML' OR 'XML_CREA'.
      PERFORM create_xml TABLES gt_gridouttab.
* < JJR 28.08.2021"n1795884
    WHEN OTHERS. EXIT.
  ENDCASE.

  IF ld_expa_comp = con_x.                                  "n2844026
    REFRESH: gt_gridouttab.
  CLEAR:   gt_gridouttab.
  PERFORM bspl_grid_outtab_fill      TABLES gt_rsthie
                                            gt_bspldata
                                            gt_gridtotals
                                            gt_ergsl_text
                                            gt_edit_settings
                                            gt_gridouttab
                                      USING gs_settings.

  PERFORM bspl_grid_newpage_set      TABLES gt_gridouttab.
  ENDIF.                                                    "n2844026

ENDFORM.                    " BSPL_GRID_USER_COMMAND
*&---------------------------------------------------------------------*
*&      Form  BSPL_GRID_LEAF_TO_OUTTAB  (Geschäftsbereich)
*&---------------------------------------------------------------------*
FORM bspl_grid_leaf_to_outtab
                       TABLES pt_totals      TYPE tt_grid_totals
                              pt_outtab      TYPE tt_grid_outtab
                              pt_data        TYPE tt_bspl_data
                 USING  VALUE(ps_node)       LIKE rsthie
                        VALUE(ps_settings)   LIKE rfbila_alv_settings
                        VALUE(ps_node_saknr) TYPE ts_node_saknr
                        VALUE(p_node_bukrs)  LIKE t001-bukrs
                        VALUE(p_node_fkber)  LIKE gt_fkber_id-fkber
                        VALUE(p_fberflg)     LIKE con_x
                              its_data     TYPE ttx_data    "n2084383
              CHANGING  VALUE(p_id)          LIKE rsthie-id.
*----------------------------------------------------------------------*
*     local data declarations
  DATA: ls_node_saknr       TYPE ts_node_saknr.
  DATA: l_busa              TYPE gsber.
  DATA ld_id                TYPE seu_id.                    "n2084383
  DATA ld_tabix             TYPE sytabix.                   "n2084383
*----------------------------------------------------------------------*
  FIELD-SYMBOLS <lsx_data>  TYPE tsx_data.                  "n2084383
*----------------------------------------------------------------------*

  CLEAR: pt_outtab.
  l_busa = ps_node-name.

  p_id = p_id + 1.
  pt_outtab-id      = p_id.        .
  pt_outtab-subid   = 1.
* read totals of account number
  READ TABLE pt_totals WITH KEY id = ps_node-id
                                     BINARY SEARCH.
  IF sy-subrc = 0.
    pt_outtab-ergsl   = pt_totals-ergsl.
    pt_outtab-curtp   = pt_totals-curtp.
    pt_outtab-waers   = pt_totals-waers.
    pt_outtab-repval  = pt_totals-repval.
    pt_outtab-compval = pt_totals-compval.
    pt_outtab-tlevel  = pt_totals-tlevel.
*.. calculate variance
    PERFORM bspl_variance_calculate USING ps_settings-comptype
                                          pt_outtab-repval
                                          pt_outtab-compval
                                 CHANGING pt_outtab-absvar
                                          pt_outtab-relvar_d "n2812892
                                          pt_outtab-relvar.

*.. provide fields like ccode, busarea, funcarea, account no etc.
    READ TABLE its_data                         "begin "n2084383
      WITH KEY
        ergsl  = pt_totals-ergsl
        racct  = ps_node_saknr-saknr
        rbukrs = p_node_bukrs
      BINARY SEARCH
      TRANSPORTING NO FIELDS.
    IF sy-subrc = 0.
      ld_tabix = sy-tabix.
*      LOOP AT pt_data WHERE ergsl = pt_totals-ergsl.
*     CHECK <KTOPL> = LS_NODE_SAKNR-KTOPL. "n1559627
      LOOP AT its_data
        ASSIGNING <lsx_data>
        FROM ld_tabix.

        IF <lsx_data>-ergsl  <> pt_totals-ergsl
        OR <lsx_data>-racct  <> ps_node_saknr-saknr
        OR <lsx_data>-rbukrs <> p_node_bukrs.
          EXIT.
        ENDIF.

        LOOP AT <lsx_data>-t_id
          INTO ld_id.
          READ TABLE pt_data
            WITH KEY
              id = ld_id
            BINARY SEARCH.
*    LOOP AT pt_data WHERE ergsl = pt_totals-ergsl."end "n2084383
          CHECK pt_data-rbusa = l_busa.

          IF NOT ( ps_settings-altacct IS INITIAL ).
*...... alternative account number is used
            ASSIGN: pt_data-ktop2 TO <ktopl>,
                    pt_data-altkt TO <racct>.
          ELSEIF NOT ( t011-xergs IS INITIAL ).
*...... group account number is used
            ASSIGN: pt_data-kktpl TO <ktopl>,
                    pt_data-bilkt TO <racct>.
          ELSE.
*...... default normal accounts used
            ASSIGN: pt_data-ktopl TO <ktopl>,
                    pt_data-racct TO <racct>.
          ENDIF.

          ls_node_saknr-ktopl = <ktopl>.
          ls_node_saknr-saknr = <racct>.
*     CHECK <KTOPL> = PS_NODE_SAKNR-KTOPL. "n1559627
          CHECK <racct> = ps_node_saknr-saknr.
          CHECK pt_data-rbukrs = p_node_bukrs.
          IF p_fberflg NE space.
            CHECK pt_data-rfarea = p_node_fkber.
          ENDIF.
*...... get name of account
          IF p_fberflg = space.
            CALL FUNCTION 'READ_HAUPTBUCH_TEXT'
              EXPORTING
                kontenplan     = <ktopl>
                sachkonto      = <racct>
                sprache        = is_settings-fs_language
              IMPORTING
                text_wa        = skat
              EXCEPTIONS
                text_not_found = 1
                OTHERS         = 2.
            IF sy-subrc = 0.
*...... fix account number to relevant length
              PERFORM bspl_account_length_fix
                                     CHANGING ls_node_saknr.
              CONCATENATE ls_node_saknr-saknr
                          skat-txt50
                     INTO pt_outtab-text SEPARATED BY ' '.
            ELSE.
*...... fix account number to relevant length
              PERFORM bspl_account_length_fix
                                     CHANGING ls_node_saknr.
              CONCATENATE ls_node_saknr-ktopl
                          ls_node_saknr-saknr
                     INTO pt_outtab-text SEPARATED BY ' '.
            ENDIF.
*...... get name of functional area
          ELSE.
            SELECT SINGLE fkbtx INTO pt_outtab-text
                                FROM tfkbt
                                WHERE spras = is_settings-fs_language
                                  AND fkber = p_node_fkber.
            IF sy-subrc = 0.
              CONCATENATE p_node_fkber
                          pt_outtab-text
                     INTO pt_outtab-text SEPARATED BY ' '.
            ELSE.
              pt_outtab-text = ps_node-name.
            ENDIF.
          ENDIF.

          pt_outtab-ktopl  = pt_data-ktopl.
*     provide operational account no
          IF pt_outtab-racct IS INITIAL.
            pt_outtab-racct  = pt_data-racct.
          ELSEIF pt_outtab-racct <> pt_data-racct.
*       1:n assignment between group account no
*                    and operational account no
            pt_outtab-racct = con_stars.
          ENDIF.
          pt_outtab-kktpl  = pt_data-kktpl.
          pt_outtab-bilkt  = pt_data-bilkt.
          pt_outtab-ktop2  = pt_data-ktop2.
          pt_outtab-altkt  = pt_data-altkt.
          pt_outtab-ryear  = pt_data-ryear.
          pt_outtab-poper  = pt_data-poper.
*     provide company code
          IF pt_outtab-rbukrs IS INITIAL.
            pt_outtab-rbukrs = pt_data-rbukrs.
          ELSEIF pt_outtab-rbukrs <> pt_data-rbukrs.
*       more than one ccode, mark it with stars
            pt_outtab-rbukrs = con_stars.
          ENDIF.

*     provide business area
          IF pt_outtab-rbusa IS INITIAL
         AND pt_data-rbusa   IS INITIAL.
            pt_outtab-rbusa  = sy-vline.
          ELSEIF pt_outtab-rbusa IS INITIAL.
            pt_outtab-rbusa  = pt_data-rbusa.
          ELSEIF pt_outtab-rbusa <> pt_data-rbusa.
*       more than one busarea, mark it with stars
            pt_outtab-rbusa = con_stars.
          ENDIF.

*     privide function area
          IF pt_outtab-rfarea IS INITIAL.
            pt_outtab-rfarea = pt_data-rfarea.
          ELSEIF pt_outtab-rfarea <> pt_data-rfarea.
*       more than one function area, mark it with stars
            pt_outtab-rfarea = con_stars.
          ENDIF.
        ENDLOOP.
      ENDLOOP.                                              "n2084383
    ENDIF.                                                  "n2084383

    IF pt_outtab-rbusa  = sy-vline.
*.... only initial business area
      CLEAR: pt_outtab-rbusa.
    ENDIF.
    PERFORM bspl_grid_cell_color_set
                            TABLES pt_outtab-cell_color.
  ENDIF.

  APPEND pt_outtab.

ENDFORM.                    " BSPL_GRID_BUSA_TO_OUTTAB

*&---------------------------------------------------------------------*
*&      Form  CREATE_XML
*&---------------------------------------------------------------------*

FORM create_xml TABLES pt_outtab    TYPE tt_grid_outtab.

* Declaracion variables
  TYPES: xmlline(1024) TYPE x.

  DATA: xml_result        TYPE  STANDARD TABLE OF xmlline,
        lt_source         TYPE zfi_xml_mod_200,
        lv_name           TYPE string,
        xml_result2       TYPE xstring,  "xstring ensures UTF-8 encoding
*        xml_result_string TYPE string,  "xstring ensures UTF-8 encoding
        lv_amount         TYPE p DECIMALS 2,
        lt_result_xml     TYPE STANDARD TABLE OF abap_trans_resbind,
        lt_fields         TYPE TABLE OF sval,
        lw_fields         TYPE sval,
        ls_result_xml     TYPE abap_trans_resbind,
        lv_ucomm          TYPE sy-ucomm,
        lv_path           TYPE string,
        lv_fullpath       TYPE string,
        lc_c              TYPE string,
        lv_transformation TYPE string.

  FIELD-SYMBOLS: <fs_source>         TYPE zfi_xml_mod_200.
  FIELD-SYMBOLS <fs_field> TYPE any.


  lw_fields-tabname = 'BKPF'.
  lw_fields-fieldname = 'GJAHR'.
  lw_fields-field_obl = 'X'.
  APPEND lw_fields TO lt_fields.

* Popup para informar año del formato XML (MOD200YYYY)

  lv_ucomm = sy-ucomm.
  CALL FUNCTION 'POPUP_GET_VALUES'
    EXPORTING
      popup_title     = 'Año Modelo 200'
    TABLES
      fields          = lt_fields
    EXCEPTIONS
      error_in_fields = 1
      OTHERS          = 2.

  READ TABLE lt_fields INTO DATA(ls_fields) INDEX 1.

* Tipo de documento (Normal o abreviado)
  IF t011-versn = '200A'.
    lt_source-tipo = 'ABREVIADO'.
    CONCATENATE 'ZFI_XML_MOD200_AEAT_ABREV'  ls_fields-value INTO lv_transformation   SEPARATED BY '_'.
  ELSE.
    lt_source-tipo = 'NORMAL'.
    CONCATENATE 'ZFI_XML_MOD200_AEAT_NORM'  ls_fields-value INTO lv_transformation   SEPARATED BY '_'.
  ENDIF.
  CONCATENATE '200' ls_fields-value INTO lt_source-year.


* Si no existe la transformacion dará error
  SELECT SINGLE * FROM tadir INTO @DATA(ls_tadir) WHERE
              pgmid = 'R3TR' AND
              object = 'XSLT' AND
              obj_name = @lv_transformation.

  IF sy-subrc <> 0.
    MESSAGE e026(zgfi) WITH lv_transformation.
*   No está configurado el XML &1
    EXIT.
  ENDIF.

* Rellenar con cero todos los campos
  SELECT tabname, fieldname FROM dd03l
    INTO TABLE @DATA(lt_dd03l)
    WHERE tabname LIKE 'ZST_TIPO_PAGINA%'.
  LOOP AT lt_dd03l INTO DATA(ls_dd03l).
    CONCATENATE 'LT_SOURCE' '-'  ls_dd03l-tabname+4(13) '-' ls_dd03l-fieldname INTO lv_name.
    ASSIGN (lv_name) TO <fs_field>.
    <fs_field> = '0.00'.
  ENDLOOP.

* Rellenar la tabla SOURCE
  LOOP AT pt_outtab  INTO DATA(ls_outtab) WHERE ergsl(1) = 'T' AND totals_level <> ''.


    CONCATENATE 'LT_SOURCE' '-' 'TIPO_PAGINA03' '-' ls_outtab-ergsl INTO lv_name.
    ASSIGN (lv_name) TO <fs_field>.
    IF <fs_field> IS ASSIGNED AND sy-subrc = 0.
      <fs_field> = ls_outtab-repval.
    ELSE.
      CONCATENATE 'LT_SOURCE' '-' 'TIPO_PAGINA04' '-' ls_outtab-ergsl INTO lv_name.
      ASSIGN (lv_name) TO <fs_field>.
      IF <fs_field> IS ASSIGNED AND sy-subrc = 0.
        <fs_field> =  ls_outtab-repval.
      ELSE.
        IF ls_outtab-ergsl = 'T00199P' OR ls_outtab-ergsl = 'T00199G'.
          CONCATENATE 'LT_SOURCE' '-' 'TIPO_PAGINA05' '-' 'T00199' INTO lv_name.
        ELSE.
          CONCATENATE 'LT_SOURCE' '-' 'TIPO_PAGINA05' '-' ls_outtab-ergsl INTO lv_name.
        ENDIF.
        ASSIGN (lv_name) TO <fs_field>.

        IF <fs_field> IS ASSIGNED AND sy-subrc = 0.
          <fs_field> = ls_outtab-repval * -1.
        ELSE.
          CONCATENATE 'LT_SOURCE' '-' 'TIPO_PAGINA06' '-' ls_outtab-ergsl INTO lv_name.
          ASSIGN (lv_name) TO <fs_field>.
          IF <fs_field> IS ASSIGNED AND sy-subrc = 0.
            <fs_field> = ls_outtab-repval * -1.
          ELSE.
            CONCATENATE 'LT_SOURCE' '-' 'TIPO_PAGINA07' '-' ls_outtab-ergsl INTO lv_name.
            ASSIGN (lv_name) TO <fs_field>.
            IF <fs_field> IS ASSIGNED AND sy-subrc = 0.
              <fs_field> = ls_outtab-repval * -1.
            ELSE.
              CONCATENATE 'LT_SOURCE' '-' 'TIPO_PAGINA08' '-' ls_outtab-ergsl INTO lv_name.
              ASSIGN (lv_name) TO <fs_field>.
              IF <fs_field> IS ASSIGNED AND sy-subrc = 0.
                <fs_field> = ls_outtab-repval * -1.
              ENDIF.
            ENDIF.
          ENDIF.
        ENDIF.
      ENDIF.
    ENDIF.

    lv_amount = <fs_field>.
* El negativo estará delante
    IF lv_amount < 0.
      lv_amount = abs( lv_amount ).
      <fs_field> = lv_amount.
      CONDENSE <fs_field> NO-GAPS.
      CONCATENATE '-' <fs_field> INTO <fs_field>.
    ELSE.
      CONDENSE <fs_field> NO-GAPS.
    ENDIF.
  ENDLOOP.

  DATA: lexc_group       TYPE REF TO cx_st_group_missing_case,
        lexc_trans_error TYPE REF TO cx_transformation_error.

  IF lv_ucomm = 'XML'. "visualiazar XML

    TRY.
        CALL TRANSFORMATION (lv_transformation)
        SOURCE mod_200 = lt_source
        RESULT XML  xml_result2.
      CATCH cx_st_group_missing_case INTO lexc_group.
        RAISE xml_error.
      CATCH cx_transformation_error INTO lexc_trans_error.
        RAISE xml_error.
    ENDTRY.

    cl_abap_browser=>show_html( html_string =
      cl_abap_codepage=>convert_from( xml_result2 )
      buttons = 'X'
      printing = 'X'
       ).
  ELSE. "DOWNLOAD XML

    TRY.
        CALL TRANSFORMATION (lv_transformation)
        SOURCE mod_200 = lt_source
        RESULT XML  xml_result.
      CATCH cx_st_group_missing_case INTO lexc_group.
        RAISE xml_error.
      CATCH cx_transformation_error INTO lexc_trans_error.
        RAISE xml_error.
    ENDTRY.


* Download XML
    CALL METHOD cl_gui_frontend_services=>file_save_dialog
      EXPORTING
        window_title      = 'Fichero XML'
        default_extension = 'XML'
        default_file_name = lv_path
        initial_directory = lc_c
      CHANGING
        filename          = lv_path
        path              = lc_c
        fullpath          = lv_fullpath
      EXCEPTIONS
        cntl_error        = 1
        error_no_gui      = 2
        OTHERS            = 3.


    CALL METHOD cl_gui_frontend_services=>gui_download
      EXPORTING
        filename = lv_fullpath
        filetype = 'BIN'
      CHANGING
        data_tab = xml_result.
  ENDIF.


ENDFORM.
