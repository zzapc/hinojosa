
class ZCL_BCAI_CONFIGURADOR definition
  public
  final
  create public .

public section.

  class-methods MODIFY_SCREEN
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IT_CONDREC type /VCXI/CKX_TT_OBJECT optional .
  class-methods VALIDAR_CAMPOS_OBLIGATORIOS
    importing
      !DATOS type ANY .
protected section.
private section.
endclass. "ZCL_BCAI_CONFIGURADOR definition
class ZCL_BCAI_CONFIGURADOR implementation.
  METHOD modify_screen.
    DATA: i_callstack TYPE sys_callst.

    CALL FUNCTION 'SYSTEM_CALLSTACK'
      IMPORTING
        et_callstack = i_callstack.

    READ TABLE i_callstack ASSIGNING FIELD-SYMBOL(<stack>) WITH KEY eventtype = 'FORM'
                                                                    eventname = '0100_PBO'.
    IF sy-subrc NE 0.
      READ TABLE i_callstack ASSIGNING <stack> WITH KEY eventtype = 'FORM'
                                                                      eventname = '0100_UPDATE'.
      IF sy-subrc NE 0.
        READ TABLE i_callstack ASSIGNING <stack> WITH KEY eventtype = 'FORM'
                                                                        eventname = '0100_MODIFY_SCREEN'.
      ENDIF.
    ENDIF.

    CHECK <stack> IS ASSIGNED.

    SELECT * FROM ztbcai0001
      INTO TABLE @DATA(i_campos)
     WHERE dynpprog = @<stack>-progname
       AND dynpro   = @sy-dynnr.

    LOOP AT i_campos ASSIGNING FIELD-SYMBOL(<campo>).
      LOOP AT SCREEN.
        IF screen-name = <campo>-dynprofld.
          IF NOT <campo>-atr_input IS INITIAL.
            screen-input = <campo>-atr_input.
            MODIFY SCREEN.
          ENDIF.
          IF NOT <campo>-atr_invisible IS INITIAL.
            screen-invisible = <campo>-atr_invisible.
            MODIFY SCREEN.
          ENDIF.
          IF NOT <campo>-atr_required IS INITIAL AND screen-input = '1' AND screen-invisible = '0'.
            screen-required = <campo>-atr_required.
            MODIFY SCREEN.
          ENDIF.
        ENDIF.
      ENDLOOP.
    ENDLOOP.
  ENDMETHOD.
  METHOD validar_campos_obligatorios.
    DATA: i_callstack TYPE sys_callst.

    CALL FUNCTION 'SYSTEM_CALLSTACK'
      IMPORTING
        et_callstack = i_callstack.

    READ TABLE i_callstack ASSIGNING FIELD-SYMBOL(<stack>) WITH KEY eventtype = 'FORM'
                                                                    eventname = '0100_UPDATE'.
    CHECK sy-subrc = 0.

    SELECT * FROM ztbcai0001
      INTO TABLE @DATA(i_campos)
     WHERE dynpprog = @<stack>-progname
       AND dynpro   = @sy-dynnr
       AND atr_required = '1'.

    LOOP AT i_campos ASSIGNING FIELD-SYMBOL(<campo>).
      SPLIT <campo>-dynprofld AT '-' INTO DATA(l_estructura) DATA(l_campo).
      ASSIGN COMPONENT l_campo OF STRUCTURE datos TO FIELD-SYMBOL(<fs>).
      IF sy-subrc = 0.
        IF <fs> IS INITIAL.
          select single ROLLNAME from dd03l
            into @data(l_elem)
           where tabname = @l_estructura
             and fieldname = @l_campo.
          if sy-subrc = 0 and not l_elem is initial.
            select reptext from dd04t
              into @data(l_reptext)
             where rollname = @l_elem
               and ddlanguage in ('S', 'E')
             ORDER BY ddlanguage DESCENDING.
            endselect.
          endif.
          if l_reptext is initial.
            l_reptext = l_campo.
          endif.
          MESSAGE |El campo { l_reptext } es obligatorio| TYPE 'E'.
        ENDIF.
      ENDIF.
    ENDLOOP.
  ENDMETHOD.
endclass. "ZCL_BCAI_CONFIGURADOR implementation
