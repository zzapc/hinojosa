
class ZCL_FICHEROS definition
  public
  final
  create public .

public section.

  constants C_FILTRO_XLS type STRING value 'Excel |*.XLS*'. "#EC NOTEXT

  type-pools ABAP .
  class-methods LISTA_FICHEROS
    importing
      !DIRECTORY type STRING
      !FILTER type STRING default '*.*'
      !RECURSIVO type ABAP_BOOL default ''
    returning
      value(FILE_TABLE) type RSTT_T_FILES .
  class-methods LEE_FICH_SERVIDOR
    importing
      !FICHERO type STRING
      !MODO_TEXTO type ABAP_BOOL default 'X'
      !MOSTRAR_MENSAJES type ABAP_BOOL default ''
      !LEGACY type ABAP_BOOL default ''
    exporting
      !LONGITUD type I
    changing
      !TABLA type TABLE .
  class-methods LIMPIAR_FICHERO
    importing
      !FICHERO type STRING
    returning
      value(SALIDA) type STRING .
  class-methods ULTIMA_LETRA_RUTA
    importing
      !DIRECTORIO type STRING
    returning
      value(LETRA) type CHAR1 .
  class-methods LEE_FICHEROS_SERVIDOR
    importing
      !DIR_NAME type ANY
      !FILE_MASK type ANY
      !RECURSION type ABAP_BOOL default ''
      !SOLO_DIR type ZT_LISTA_FICHEROS
    changing
      !DIR_LIST type ZT_LISTA_FICHEROS
    exceptions
      READ_DIRECTORY_FAILED
      EMPTY_DIRECTORY_LIST .
  class-methods LEER_XML
    importing
      !FICHERO type ANY
      !MOSTRAR_ERROR type ABAP_BOOL default 'X'
    exporting
      !STRING type STRING
      !NUM_EXCEPCION type I .
  class-methods POPUP_SELECT_FICHERO
    importing
      !DEFAULT_EXTENSION type STRING
      !INITIAL_DIRECTORY type STRING
      !FILE_FILTER type STRING
    returning
      value(FICHERO) type STRING .
  class-methods GET_EXTENSION
    importing
      !FICHERO type ANY
    returning
      value(EXTENSION) type STRING .
  class-methods GET_NOMBRE_FICHERO
    importing
      !FICHERO type ANY
      !CON_EXTENSION type ANY default ''
    returning
      value(NOMBRE) type STRING .
  class-methods GET_DIRECTORIO_FICHERO
    importing
      !FICHERO type ANY
    returning
      value(DIRECTORIO) type STRING .
  class-methods CONCAT_RUTA
    importing
      !DIRECTORIO type ANY
      !FICHERO type ANY
      !EXTENSION type ANY optional
    returning
      value(RUTA) type STRING .
  class-methods GRABAR
    importing
      !FICHERO type ANY
      !TIPO type CHAR10 default 'ASC'
      !MOSTRAR_ERROR type ABAP_BOOL default 'X'
      !DIALOGO type ABAP_BOOL default ''
      !TRUNC type ABAP_BOOL default 'X'
    exporting
      !NUM_EXCEPCION type I
    changing
      !TABLA type TABLE
      !FICHERO_DIALOGO type ANY default '' .
  class-methods GRABAR_XML
    importing
      !FICHERO type ANY
      !STRING type STRING
      !MOSTRAR_ERROR type ABAP_BOOL default 'X'
    exporting
      !NUM_EXCEPCION type I .
  class-methods LEER
    importing
      !FICHERO type ANY
      !TIPO type CHAR10 default 'ASC'
      !MOSTRAR_ERROR type ABAP_BOOL default 'X'
    exporting
      !NUM_EXCEPCION type I
    changing
      !TABLA type TABLE .
  class-methods GRABA_FICH_SERVIDOR
    importing
      !FICHERO type STRING
      !MODO_TEXTO type ABAP_BOOL default 'X'
      !MOSTRAR_MENSAJES type ABAP_BOOL default ''
      !UNICODE type ABAP_BOOL default ''
    changing
      !TABLA type TABLE
    exceptions
      ERROR_ABRIR_FICHERO
      ERROR_TRANSFER
      ERROR_CERRAR_FICHERO .
protected section.
private section.
endclass. "ZCL_FICHEROS definition
class ZCL_FICHEROS implementation.
METHOD concat_ruta.
  DATA: l_ext(30),
        l_directorio TYPE string.

  IF directorio IS INITIAL.
    ruta = fichero.
  ELSE.
    l_directorio = directorio.
    IF ultima_letra_ruta( l_directorio ) = '\'.
      CONCATENATE l_directorio fichero INTO ruta.
    ELSE.
      CONCATENATE l_directorio '\' fichero INTO ruta.
    ENDIF.
  ENDIF.

  IF NOT extension IS INITIAL.
    CONCATENATE '.' extension INTO l_ext.
    IF NOT fichero CS l_ext.
      CONCATENATE ruta l_ext INTO ruta.
    ENDIF.
  ENDIF.

ENDMETHOD.
method GET_DIRECTORIO_FICHERO.

  DATA: w_file type string,
        name TYPE string,
        name_with_ext type string.

  DATA: one  type pcfile-drive,
        two  type string.

  w_file = fichero.

  CALL FUNCTION 'CH_SPLIT_FILENAME'
    EXPORTING
      complete_filename = w_file
    IMPORTING
      drive             = one
      path              = directorio
      name              = name
      name_with_ext     = name_with_ext
    EXCEPTIONS
      invalid_drive     = 1
*      invalid_extension = 2
*      invalid_name      = 3
*      invalid_path      = 4
*      OTHERS            = 5.
      invalid_path      = 2
      OTHERS            = 3.

endmethod.
method GET_EXTENSION.
  data: l_long_filename type dbmsgora-filename,
         l_extension type sdbad-funct.
  l_long_filename = fichero.

  data l_long type i.
  l_long = strlen( fichero ).
  l_long = l_long - 4.
  if l_long > 0.
    if fichero+l_long(1) = '.'.
      add 1 to l_long.
      l_extension = fichero+l_long(3).
    else.
      call function 'SPLIT_FILENAME'
        exporting
          long_filename        = l_long_filename
        importing
*        PURE_FILENAME        =
         pure_extension       = l_extension.
    endif.
  else.
    call function 'SPLIT_FILENAME'
      exporting
        long_filename        = l_long_filename
      importing
*        PURE_FILENAME        =
       pure_extension       = l_extension.
  endif.
  extension = l_extension.
  translate extension to upper case.


endmethod.
method GET_NOMBRE_FICHERO.

  DATA: w_file type string,
        name TYPE string,
        name_with_ext type string.

  DATA: one  type pcfile-drive,
        two  type string.

  w_file = fichero.

  CALL FUNCTION 'CH_SPLIT_FILENAME'
    EXPORTING
      complete_filename = w_file
    IMPORTING
      drive             = one
      path              = two
      name              = name
      name_with_ext     = name_with_ext
    EXCEPTIONS
      invalid_drive     = 1
*      invalid_extension = 2
*      invalid_name      = 3
*      invalid_path      = 4
*      OTHERS            = 5.
      invalid_path      = 2
      OTHERS            = 3.

  IF con_extension IS INITIAL.
    nombre = name.
  ELSE.
    nombre = name_with_ext.
  ENDIF.

endmethod.
METHOD graba_fich_servidor.
  DATA: l_linea(65535), l_long TYPE i,
        l_string TYPE string.

  IF modo_texto = 'X'.
    IF unicode IS INITIAL.
      OPEN DATASET fichero FOR OUTPUT IN TEXT MODE ENCODING NON-UNICODE.
    ELSE.
      OPEN DATASET fichero FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
    ENDIF.
  ELSE.
    OPEN DATASET fichero FOR OUTPUT IN BINARY MODE.
  ENDIF.
  IF sy-subrc NE 0.
    IF mostrar_mensajes = 'X'.
      MESSAGE e093(ch) WITH fichero.
    ENDIF.
    RAISE error_abrir_fichero.
  ENDIF.

  LOOP AT tabla INTO l_linea.
    TRY.
        TRANSFER l_linea TO fichero.
      CATCH cx_sy_conversion_codepage.
        l_string = l_linea.
        zcl_ap_string=>quitar_caracteres_extranos( CHANGING string = l_string ).
        l_linea = l_string.
        TRANSFER l_linea TO fichero.
    ENDTRY.
    IF sy-subrc NE 0.
      IF mostrar_mensajes = 'X'.
        MESSAGE e094(ch) WITH fichero.
      ENDIF.
      RAISE error_transfer.
    ENDIF.
  ENDLOOP.

  CLOSE DATASET fichero.
  IF sy-subrc NE 0.
    IF mostrar_mensajes = 'X'.
      MESSAGE e411(tl) WITH fichero.
    ENDIF.
    RAISE error_cerrar_fichero.
  ENDIF.


ENDMETHOD.
method GRABAR.
  DATA: l_fichero TYPE string,
        l_path TYPE string,
        l_fullpath TYPE string,
        l_extension TYPE string,
        l_dat_mode.

  l_fichero = fichero.
  IF dialogo = 'X'.
    l_extension = get_extension( l_fichero ).
    CALL METHOD cl_gui_frontend_services=>file_save_dialog
       EXPORTING
*        window_title         =
         default_extension    = l_extension
*        default_file_name    =
*        with_encoding        =
*        file_filter          =
*        initial_directory    =
*        prompt_on_overwrite  = 'X'
      CHANGING
        filename             = l_fichero
        path                 = l_path
        fullpath             = l_fullpath
*        user_action          =
*        file_encoding        =
      EXCEPTIONS
        cntl_error           = 1
        error_no_gui         = 2
        not_supported_by_gui = 3
        OTHERS               = 4.
    IF sy-subrc <> 0.
      num_excepcion = sy-subrc.
      IF mostrar_error = 'X'.
        MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
                   WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
      ELSE.
        EXIT.
      ENDIF.
    ELSE.
      l_fichero = l_fullpath.
      fichero_dialogo = l_fullpath.
      IF L_FULLPATH IS INITIAL.
        NUM_EXCEPCION = 99.
        EXIT.
      ENDIF.
    ENDIF.
  ENDIF.

  if tipo = 'DAT'.
    l_dat_mode = 'X'.
  endif.

  CALL METHOD cl_gui_frontend_services=>gui_download
    EXPORTING
*      bin_filesize              =
      filename                  = l_fichero
      filetype                  = tipo
*      append                    = SPACE
*      write_field_separator     = SPACE
*      header                    = '00'
*      trunc_trailing_blanks     = SPACE
*      write_lf                  = 'X'
*      col_select                = SPACE
*      col_select_mask           = SPACE
       dat_mode                  = l_dat_mode
*      confirm_overwrite         = SPACE
*      no_auth_check             = SPACE
*      codepage                  = SPACE
*      ignore_cerr               = ABAP_TRUE
*      replacement               = '#'
*      write_bom                 = SPACE
       trunc_trailing_blanks_eol = TRUNC
*      wk1_n_format              = SPACE
*      wk1_n_size                = SPACE
*      wk1_t_format              = SPACE
*      wk1_t_size                = SPACE
*    IMPORTING
*      filelength                =
    CHANGING
      data_tab                  = tabla
    EXCEPTIONS
      file_write_error          = 1
      no_batch                  = 2
      gui_refuse_filetransfer   = 3
      invalid_type              = 4
      no_authority              = 5
      unknown_error             = 6
      header_not_allowed        = 7
      separator_not_allowed     = 8
      filesize_not_allowed      = 9
      header_too_long           = 10
      dp_error_create           = 11
      dp_error_send             = 12
      dp_error_write            = 13
      unknown_dp_error          = 14
      access_denied             = 15
      dp_out_of_memory          = 16
      disk_full                 = 17
      dp_timeout                = 18
      file_not_found            = 19
      dataprovider_exception    = 20
      control_flush_error       = 21
      not_supported_by_gui      = 22
      error_no_gui              = 23
      OTHERS                    = 24.

  num_excepcion = sy-subrc.

  IF mostrar_error = 'X'.
    IF num_excepcion NE 0.
      CASE num_excepcion.
        WHEN 1.
          MESSAGE e153(14) WITH fichero.
        WHEN OTHERS.
          MESSAGE e153(14) WITH fichero.
      ENDCASE.
    ENDIF.
  ENDIF.

endmethod.
method GRABAR_XML.
  DATA: temptable_char TYPE table_of_strings,
        tempstring TYPE string.

  SPLIT string AT cl_abap_char_utilities=>newline
  INTO TABLE temptable_char.

  grabar( EXPORTING fichero = fichero
                    tipo    = 'DAT'
                    mostrar_error = 'X'
          IMPORTING num_excepcion = num_excepcion
          CHANGING  tabla = temptable_char ).

endmethod.
  method LEE_FICH_SERVIDOR.
      DATA: l_linea(65535), l_long TYPE i.

  IF modo_texto = 'X'.
    IF legacy is initial.
      OPEN DATASET fichero FOR INPUT IN TEXT MODE ENCODING DEFAULT.
    ELSE.
      OPEN DATASET fichero FOR INPUT IN LEGACY TEXT MODE.
    ENDIF.
  ELSE.
    OPEN DATASET fichero FOR INPUT IN BINARY MODE.
  ENDIF.
  IF sy-subrc NE 0.
    IF mostrar_mensajes = 'X'.
      MESSAGE e025(ba) WITH fichero.
    ENDIF.
  ENDIF.

  FREE tabla.
  DO.
    CLEAR l_long.
    READ DATASET fichero INTO l_linea LENGTH l_long.
    IF sy-subrc NE 0 AND l_long = 0.
      EXIT.
    ELSE.
      APPEND l_linea TO tabla.
      ADD l_long TO longitud.
    ENDIF.
  ENDDO.

  CLOSE DATASET fichero.
  IF sy-subrc NE 0.
    IF mostrar_mensajes = 'X'.
      MESSAGE e804(fu) WITH fichero.
    ENDIF.
  ENDIF.

  endmethod.
  method LEE_FICHEROS_SERVIDOR.
      DATA file_counter TYPE i.
  DATA: BEGIN OF file,
          dirname(75) TYPE c, " name of directory. (possibly truncated.)
          name(75)    TYPE c, " name of entry. (possibly truncated.)
          type(10)    TYPE c, " type of entry.
          len(8)      TYPE p, " length in bytes.
          owner(8)    TYPE c, " owner of the entry.
        mtime(6)    TYPE p, " last modification date, seconds since 1970
          mode(9)     TYPE c, " like "rwx-r-x--x": protection mode.
          errno(3)    TYPE c,
          errmsg(40)  TYPE c,
        END OF file,
        l_dir TYPE zlista_ficheros,
        l_time(8).

  DATA: lv_eps_subdir     TYPE epsf-epssubdir.

* get directory listing
  CALL 'C_DIR_READ_FINISH'                  " just to be sure
        ID 'ERRNO'  FIELD file-errno
        ID 'ERRMSG' FIELD file-errmsg.

  CALL 'C_DIR_READ_START'
        ID 'DIR'    FIELD dir_name
        ID 'FILE'   FIELD file_mask
        ID 'ERRNO'  FIELD file-errno
        ID 'ERRMSG' FIELD file-errmsg.
  IF sy-subrc <> 0.
     RAISE read_directory_failed.
  ENDIF.

  CLEAR dir_list.
  DO.
    CLEAR file.
    CLEAR l_dir.
    CALL 'C_DIR_READ_NEXT'
          ID 'TYPE'   FIELD file-type
          ID 'NAME'   FIELD file-name
          ID 'LEN'    FIELD file-len
          ID 'OWNER'  FIELD file-owner
          ID 'MTIME'  FIELD file-mtime
          ID 'MODE'   FIELD file-mode
          ID 'ERRNO'  FIELD file-errno
          ID 'ERRMSG' FIELD file-errmsg.

    IF sy-subrc = 0.
*      IF file-type(1) = 'f' OR              " regular file
*         file-type(1) = 'F'.
      IF file-name(1) NE '.'.
        IF file-type = 'directory'.
          TRANSLATE file-name TO UPPER CASE.
          READ TABLE solo_dir TRANSPORTING NO FIELDS
                       WITH KEY dirname = file-name.
          IF sy-subrc NE 0 AND NOT solo_dir[] IS INITIAL.
            CONTINUE.
          ENDIF.
        ENDIF.

        MOVE-CORRESPONDING file TO l_dir.
        PERFORM p6_to_date_time_tz IN PROGRAM rstr0400 USING file-mtime
                                                   l_time
                                                   l_dir-mod_date.
        IF l_time CS ':'.
          REPLACE ':' WITH '' INTO l_time.
          REPLACE ':' WITH '' INTO l_time.
          CONDENSE l_time NO-GAPS.
        ENDIF.
        l_dir-mod_time = l_time.

        ADD 1 TO file_counter.
        l_dir-dirname = dir_name.
        l_dir-procesado = 'X'.
* APC170108 Para homogeneizar, devolvemos los ficheros siempre
* en mayúsculas
        TRANSLATE l_dir-name TO UPPER CASE.
        TRANSLATE l_dir-dirname TO UPPER CASE.
        APPEND l_dir TO dir_list.

      ENDIF.
    ELSEIF sy-subrc = 1.
      EXIT.
    ELSE.
* APC170108 Para homogeneizar, devolvemos los ficheros siempre
* en mayúsculas
      TRANSLATE l_dir-name TO UPPER CASE.
      TRANSLATE l_dir-dirname TO UPPER CASE.
      APPEND l_dir TO dir_list.
    ENDIF.
  ENDDO.

  CALL 'C_DIR_READ_FINISH'
        ID 'ERRNO'  FIELD file-errno
        ID 'ERRMSG' FIELD file-errmsg.



  IF file_counter > 0.
    SORT dir_list BY name ASCENDING.
  ELSE.
    RAISE empty_directory_list.
  ENDIF.

  IF recursion = 'X'.
    DATA l_dir_name(75).
    DATA: l_dir_list TYPE zt_lista_ficheros,
          i_dir TYPE zt_lista_ficheros.

    LOOP AT dir_list INTO l_dir WHERE type = 'directory'
                                  AND procesado = 'X'.
      TRANSLATE l_dir-name TO UPPER CASE.
      TRANSLATE l_dir-dirname TO UPPER CASE.
      APPEND l_dir TO i_dir.
      CLEAR l_dir-procesado.
      MODIFY dir_list FROM l_dir.
    ENDLOOP.
    LOOP AT i_dir INTO l_dir.
      CONCATENATE dir_name '\' l_dir-name INTO l_dir_name.
      REFRESH l_dir_list.
      CALL FUNCTION 'Z_LEE_FICHEROS_SERVIDOR'
        EXPORTING
          dir_name               = l_dir_name
          file_mask              = file_mask
          recursion              = 'X'
        TABLES
          dir_list               = l_dir_list
          solo_dir               = solo_dir
        EXCEPTIONS
          invalid_eps_subdir     = 1
          sapgparam_failed       = 2
          build_directory_failed = 3
          no_authorization       = 4
          read_directory_failed  = 5
          too_many_read_errors   = 6
          empty_directory_list   = 7
          OTHERS                 = 8.
      IF sy-subrc = 0.
        LOOP AT l_dir_list INTO l_dir.
          TRANSLATE l_dir-name TO UPPER CASE.
          TRANSLATE l_dir-dirname TO UPPER CASE.
          APPEND l_dir TO dir_list.
        ENDLOOP.
      ENDIF.
    ENDLOOP.
  ENDIF.

  endmethod.
method LEER.
data: l_sep,
      l_tipo type CHAR10.

  l_tipo = tipo.
  if tipo = 'DAT'.
    l_sep = 'X'.
    l_tipo = 'ASC'.
  endif.

  CALL METHOD cl_gui_frontend_services=>gui_upload
     EXPORTING
       filename                = fichero
       filetype                = l_tipo
     has_field_separator     = l_sep
*    header_length           = 0
*    read_by_line            = 'X'
     dat_mode                = l_sep
*    codepage                = SPACE
*    ignore_cerr             = ABAP_TRUE
*    replacement             = '#'
*    virus_scan_profile      =
*  IMPORTING
*    filelength              =
*    header                  =
    CHANGING
      data_tab                = tabla
    EXCEPTIONS
      file_open_error         = 1
      file_read_error         = 2
      no_batch                = 3
      gui_refuse_filetransfer = 4
      invalid_type            = 5
      no_authority            = 6
      unknown_error           = 7
      bad_data_format         = 8
      header_not_allowed      = 9
      separator_not_allowed   = 10
      header_too_long         = 11
      unknown_dp_error        = 12
      access_denied           = 13
      dp_out_of_memory        = 14
      disk_full               = 15
      dp_timeout              = 16
      not_supported_by_gui    = 17
      error_no_gui            = 18
      OTHERS                  = 19.
  num_excepcion = sy-subrc.

  IF mostrar_error = 'X'.
    IF num_excepcion NE 0.
      CASE num_excepcion.
        WHEN 1.
          MESSAGE e503(0u) WITH fichero.
        when 8.
          message 'Error en formato de datos' type 'E'.
        WHEN OTHERS.
          MESSAGE e503(0u) WITH fichero.
      ENDCASE.
    ENDIF.
  ENDIF.

endmethod.
  method LEER_XML.
      DATA: temptable TYPE table_of_strings,
        tempstring TYPE string.

  CLEAR string.

  leer( EXPORTING fichero = fichero
                  tipo    = 'DAT'
                  mostrar_error = mostrar_error
          IMPORTING num_excepcion = num_excepcion
          CHANGING  tabla = temptable ).

  LOOP AT temptable INTO tempstring.
    CONCATENATE string tempstring cl_abap_char_utilities=>newline
      INTO string.
  ENDLOOP.

  endmethod.
  method LIMPIAR_FICHERO.
      DATA l_linea(10000).

  l_linea = fichero.
  REPLACE '\\' WITH '\' INTO l_linea+1.
  salida = l_linea.

  endmethod.
  method LISTA_FICHEROS.
      DATA: i_directorios TYPE TABLE OF file_info,
        l_directorio TYPE file_info,
        i_ficheros TYPE TABLE OF file_info,
        l_fichero TYPE file_info,
        l_count TYPE i,
        l_aux(20),
        l_existe,
        l_file(1000),
        l_dir TYPE string,
        l_string TYPE string,
        l_files_aux TYPE TABLE OF file_info,
        l_dir_aux TYPE string.

  REFRESH file_table.
  CALL METHOD cl_gui_frontend_services=>directory_list_files
    EXPORTING
      directory                   = directory
      filter                      = filter
      directories_only            = recursivo
    CHANGING
      file_table                  = i_directorios
      count                       = l_count
    EXCEPTIONS
      cntl_error                  = 1
      directory_list_files_failed = 2
      wrong_parameter             = 3
      error_no_gui                = 4
      not_supported_by_gui        = 5
      OTHERS                      = 6.
  IF recursivo IS INITIAL.
    file_table = i_directorios.
    LOOP AT file_table INTO l_fichero.
      CONCATENATE directory '\' l_fichero-filename
         INTO l_fichero-filename.
      REPLACE '\\' WITH '\' INTO l_fichero-filename+2.
      MODIFY file_table FROM l_fichero.
    ENDLOOP.
  ELSE.
    CALL METHOD cl_gui_frontend_services=>directory_list_files
      EXPORTING
        directory                   = directory
        filter                      = filter
      CHANGING
        file_table                  = file_table
        count                       = l_count
      EXCEPTIONS
        cntl_error                  = 1
        directory_list_files_failed = 2
        wrong_parameter             = 3
        error_no_gui                = 4
        not_supported_by_gui        = 5
        OTHERS                      = 6.

    LOOP AT file_table INTO l_fichero.
      IF l_fichero-isdir = 1.
**        DELETE file_table.
        CONCATENATE directory '\' l_fichero-filename
               INTO l_dir_aux.
        REPLACE '\\' WITH '\' INTO l_dir_aux.
        l_files_aux = zcl_ficheros=>lista_ficheros(
                          directory = l_dir_aux
                          filter = filter
                          recursivo = recursivo
                          ).
        LOOP AT l_files_aux INTO l_fichero.
*          CONCATENATE directory '\' l_fichero-filename
*               INTO l_fichero-filename.
*          REPLACE '\\' WITH '\' INTO l_fichero-filename+2.
        CONCATENATE directory '\'  INTO l_dir_aux.
          replace l_dir_aux with '' into l_fichero-filename.
          l_fichero-filename = l_fichero-filename+1.
          APPEND l_fichero TO file_table.
        ENDLOOP.
      ELSE.
        CONCATENATE directory '\' l_fichero-filename
           INTO l_fichero-filename.
        REPLACE '\\' WITH '\' INTO l_fichero-filename+2.
        MODIFY file_table FROM l_fichero.
      ENDIF.
    ENDLOOP.

    LOOP AT i_directorios INTO l_directorio.
      CLEAR l_dir.
      CONCATENATE directory '\' l_directorio-filename INTO l_file.
      REPLACE '\\' WITH '\' INTO l_file+2.
      l_dir = l_file.
      REFRESH i_ficheros.
      CALL METHOD cl_gui_frontend_services=>directory_list_files
        EXPORTING
          directory                   = l_dir
          files_only                  = 'X'
        CHANGING
          file_table                  = i_ficheros
          count                       = l_count
        EXCEPTIONS
          cntl_error                  = 1
          directory_list_files_failed = 2
          wrong_parameter             = 3
          error_no_gui                = 4
          not_supported_by_gui        = 5
          OTHERS                      = 6.

      LOOP AT i_ficheros INTO l_fichero.
        CONCATENATE l_dir '\' l_fichero-filename
           INTO l_fichero-filename.
        REPLACE '\\' WITH '\' INTO l_fichero-filename+2.
        APPEND l_fichero TO file_table.
      ENDLOOP.
    ENDLOOP.
  ENDIF.

  IF recursivo = 'X'.
    DELETE file_table WHERE isdir = 1.
  ENDIF.


  endmethod.
  method POPUP_SELECT_FICHERO.
      DATA: i_ficheros TYPE filetable,
        l_fichero TYPE file_table,
        l_rc TYPE i.

  cl_gui_frontend_services=>file_open_dialog(
                 EXPORTING default_extension = default_extension
                           INITIAL_DIRECTORY = INITIAL_DIRECTORY
                           FILE_FILTER       = FILE_FILTER
                 CHANGING  rc = l_rc
                           file_table = i_ficheros ).

  READ TABLE i_ficheros INTO l_fichero INDEX 1.
  IF sy-subrc = 0.
    fichero = l_fichero.
  ENDIF.


  endmethod.
  method ULTIMA_LETRA_RUTA.
    data: l_lon type i,
      l_char(1000).

  l_char = directorio.
  l_lon = strlen( l_char ).
  if l_lon > 1.
    l_lon = l_lon - 1.
  endif.
  letra = l_char+l_lon(1).
  endmethod.
