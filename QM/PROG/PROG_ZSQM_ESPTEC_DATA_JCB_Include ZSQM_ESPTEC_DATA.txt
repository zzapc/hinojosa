*&---------------------------------------------------------------------*
*&  Include           ZSQM_ESPTEC_DATA
*&---------------------------------------------------------------------*

TABLES: mara, kna1, vbap.

DATA: wl_fm_name TYPE rs38l_fnam.
DATA: wl_params TYPE sfpoutputparams.
DATA: wl_docparams TYPE sfpdocparams.

DATA gt_output      TYPE TABLE OF zstqm_esptec.
DATA ls_output      TYPE zstqm_esptec.
DATA alv            TYPE REF TO cl_gui_alv_grid.
DATA container      TYPE REF TO cl_gui_custom_container.
DATA fieldcat       TYPE lvc_t_fcat.
DATA layout         TYPE lvc_s_layo.
DATA variant        LIKE disvariant.
DATA excludin       TYPE ui_functions.
DATA or_doc         TYPE REF TO cl_gui_docking_container.
DATA event_receiver TYPE REF TO lcl_event_receiver.
DATA lt_rows        TYPE lvc_t_row.
DATA ls_rows        TYPE lvc_s_row.


"""""""""""""""""
" Pantalla de selección
"""""""""""""""""


SELECTION-SCREEN BEGIN OF BLOCK b01 WITH FRAME TITLE text-b01.
PARAMETERS: rb_espst RADIOBUTTON GROUP rb01 DEFAULT 'X' USER-COMMAND rb01,
            rb_esppr RADIOBUTTON GROUP rb01.
SELECTION-SCREEN END OF BLOCK b01.


SELECTION-SCREEN BEGIN OF BLOCK a WITH FRAME TITLE text-001.

PARAMETERS: p_werks TYPE werks_d DEFAULT '2001'     MODIF ID std.
SELECT-OPTIONS: p_matnr FOR mara-matnr NO INTERVALS MODIF ID std,
                p_kunnr FOR kna1-kunnr NO INTERVALS MODIF ID std.

SELECT-OPTIONS: s_vbeln   FOR vbap-vbeln  NO INTERVALS MODIF ID prv,
                s_posnr   FOR vbap-posnr  NO INTERVALS MODIF ID prv,
                s_zstatp  FOR vbap-zstatp NO INTERVALS MODIF ID prv.
SELECTION-SCREEN END OF BLOCK a.




SELECTION-SCREEN BEGIN OF BLOCK b02 WITH FRAME TITLE text-b02.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(22) text-005 FOR FIELD cbx_bct.
PARAMETERS: cbx_bct AS CHECKBOX USER-COMMAND cbx.

SELECTION-SCREEN COMMENT 28(8) text-008 FOR FIELD cbx_bctm.
PARAMETERS: p_bct_um  TYPE t006-msehi DEFAULT 'KG'.
SELECTION-SCREEN COMMENT 42(15) text-006 FOR FIELD cbx_bctm.
PARAMETERS: cbx_bctm AS CHECKBOX USER-COMMAND cbx.
SELECTION-SCREEN COMMENT 64(8) text-007 FOR FIELD p_bct.
PARAMETERS: p_bct     TYPE dec12_2.


SELECTION-SCREEN END OF LINE.


SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(22) text-002 FOR FIELD cbx_rea.
PARAMETERS: cbx_rea AS CHECKBOX USER-COMMAND cbx.
SELECTION-SCREEN COMMENT 28(8) text-004 FOR FIELD cbx_rev.
PARAMETERS: p_rea TYPE zsqmt_esptec_rea.
SELECTION-SCREEN END OF LINE.

SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(22) text-003 FOR FIELD cbx_rev.
PARAMETERS: cbx_rev AS CHECKBOX USER-COMMAND cbx.
SELECTION-SCREEN COMMENT 28(8) text-004 FOR FIELD cbx_rev.
PARAMETERS: p_rev TYPE zsqmt_esptec_rev.
SELECTION-SCREEN END OF LINE.

SELECTION-SCREEN END OF BLOCK b02.


SELECTION-SCREEN BEGIN OF BLOCK b03 WITH FRAME TITLE text-b03.
PARAMETERS: rb_esp RADIOBUTTON GROUP idio DEFAULT 'X',
            rb_ing RADIOBUTTON GROUP idio,
            rb_fra RADIOBUTTON GROUP idio.
SELECTION-SCREEN END OF BLOCK b03.



DATA: vg_werks_ant LIKE p_werks.


"""""""""""""""""""""
" Eventos
"""""""""""""""""""""

INITIALIZATION.
  s_zstatp[] = VALUE #( sign = 'E' option = 'EQ' ( low = 'A1' ) ( low = 'A2' ) ).


AT SELECTION-SCREEN OUTPUT.
  IF p_werks <> vg_werks_ant.
    vg_werks_ant = p_werks.
    IF p_werks IS NOT INITIAL.
      SELECT SINGLE mostrar_bct
        INTO cbx_bct
        FROM zqm0002_centros
      WHERE werks = p_werks.
      IF sy-subrc <> 0.
        CLEAR cbx_bct.
      ENDIF.
    ENDIF.
  ENDIF.


  LOOP AT SCREEN.
    IF screen-name CP '*P_REA*'.
      screen-input = SWITCH #( cbx_rea WHEN 'X' THEN 1 ELSE 0 ).
    ENDIF.
    IF screen-name CP '*P_REV*'.
      screen-input = SWITCH #( cbx_rev WHEN 'X' THEN 1 ELSE 0 ).
    ENDIF.
    IF screen-name CP '*CBX_BCTM*'.
      screen-input = SWITCH #( cbx_bct WHEN 'X' THEN 1 ELSE 0 ).
    ENDIF.
    IF screen-name CP '*P_BCT*'.
      screen-input = COND #( WHEN cbx_bct = 'X' AND cbx_bctm = 'X' THEN 1 ELSE 0 ).
    ENDIF.
    IF screen-name CP '*P_BCT_UM*'.
      screen-input = COND #( WHEN cbx_bct = 'X' THEN 1 ELSE 0 ).
    ENDIF.

    CASE screen-group1.
      WHEN 'STD'.
        IF screen-name NP '%_*'.  "%_S_POSNR_%_APP_%-OPTI_PUSH
          screen-input     = COND #( WHEN rb_espst = 'X' THEN 1 ELSE 0 ).
        ENDIF.
        screen-invisible = COND #( WHEN rb_espst = space THEN 1 ELSE 0 ).
      WHEN 'PRV'.
        IF screen-name NP '%_*'.  "%_S_POSNR_%_APP_%-OPTI_PUSH
          screen-input  = COND #( WHEN rb_esppr = 'X' THEN 1 ELSE 0 ).
        ENDIF.
        screen-invisible = COND #( WHEN rb_esppr = space THEN 1 ELSE 0 ).
    ENDCASE.

    MODIFY SCREEN.
  ENDLOOP.


AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_bct_um.
  DATA: tl_field_tab  TYPE TABLE OF dfies,
        tl_return_tab TYPE TABLE OF ddshretval.

  CLEAR: tl_field_tab, tl_return_tab.

  SELECT t006~msehi, t006a~msehl
    INTO TABLE @DATA(tl_meins_bct)
    FROM t006 LEFT OUTER JOIN t006a ON t006a~msehi = t006~msehi AND
                                       t006a~spras = @sy-langu
  WHERE t006~msehi IN ('KG', 'N').



  CALL FUNCTION 'F4IF_INT_TABLE_VALUE_REQUEST'
    EXPORTING
      retfield        = 'MSEHI'
      value_org       = 'S'
    TABLES
      value_tab       = tl_meins_bct
      return_tab      = tl_return_tab
    EXCEPTIONS
      parameter_error = 1
      no_values_found = 2
      OTHERS          = 3.

  READ TABLE tl_return_tab ASSIGNING FIELD-SYMBOL(<fs_return_tab>) INDEX 1.
  IF sy-subrc = 0.
    p_bct_um = <fs_return_tab>-fieldval.
  ENDIF.


AT SELECTION-SCREEN ON p_werks.
  CHECK sy-ucomm = 'ONLI'.
  IF rb_espst = 'X' AND p_werks IS INITIAL.
    MESSAGE 'Debe indicar el centro' TYPE 'E'.
  ENDIF.


AT SELECTION-SCREEN.
  CHECK sy-ucomm = 'ONLI'.
  IF rb_esppr = 'X' AND s_vbeln[] IS INITIAL.
    MESSAGE 'Debe indicar alguna oferta' TYPE 'E'.
  ENDIF.
