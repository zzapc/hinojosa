*&---------------------------------------------------------------------*
*& Report  ZRMM0017
*&
*&---------------------------------------------------------------------*
*& Autor: Carlos Vivó Jiménez
*& Fecha: 05.09.2018
*& Proyecto: Implantación WM Xàtiva
*&
*& Comentarios: Programa para la creación autom. de las vistas de ventas
*&    para las materias primas. Es una tarea mecánica que puede auto-
*&    matizarse.
*&
*&---------------------------------------------------------------------*
REPORT zrmm0017.

INCLUDE zrmm0017_top.

SELECTION-SCREEN BEGIN OF BLOCK 0001. " pantalla inicial
PARAMETERS: p_werks LIKE marc-werks OBLIGATORY,
            p_vkorg LIKE mvke-vkorg OBLIGATORY,
            p_vtweg LIKE mvke-vtweg OBLIGATORY.
SELECT-OPTIONS: s_mtart FOR mara-mtart,
                s_matnr FOR mara-matnr.
SELECTION-SCREEN END OF BLOCK 0001.

START-OF-SELECTION.

  SELECT * FROM tvarvc
    WHERE name EQ 'ZRMM0017_MVKE'
    INTO TABLE @DATA(t_tvarvc).

  SELECT * FROM tvarvc
    WHERE name EQ 'ZRMM0017_MLAN'
    INTO TABLE @DATA(t_tvarvc_mlan).

  SELECT * FROM marc
    INNER JOIN mara
    ON marc~matnr EQ mara~matnr
    INTO CORRESPONDING FIELDS OF TABLE t_marc
    WHERE mara~mtart IN s_mtart
      AND marc~matnr IN s_matnr
      AND marc~werks EQ p_werks.

  SELECT * FROM mvke
    INTO TABLE t_mvke
    FOR ALL ENTRIES IN t_marc
    WHERE matnr EQ t_marc-matnr
      AND vkorg EQ p_vkorg
      AND vtweg EQ p_vtweg.

  LOOP AT t_mvke. " quitamos los que ya están creados
    DELETE t_marc WHERE matnr EQ t_mvke-matnr.
  ENDLOOP.

END-OF-SELECTION.

  LOOP AT t_marc.
    CLEAR: headdata, salesdatax, salesdata, return, field, fieldx, taxclassifications, text.
    REFRESH: taxclassifications.

    headdata-material = t_marc-matnr.

    salesdata-sales_org = p_vkorg.
    salesdatax-sales_org = p_vkorg.
    salesdata-distr_chan = p_vtweg.
    salesdatax-distr_chan = p_vtweg.

    taxclassifications-depcountry = 'ES'.

    LOOP AT t_tvarvc INTO tvarvc.
      CONCATENATE 'salesdata-' tvarvc-low INTO field.
      ASSIGN (field) TO <value>.
      IF sy-subrc EQ 0.
        WRITE tvarvc-high TO <value>.
      ENDIF.

      CONCATENATE 'salesdatax-' tvarvc-low INTO fieldx.
      ASSIGN (fieldx) TO <value>.
      IF sy-subrc EQ 0.
        WRITE 'X' TO <value>.
      ENDIF.
    ENDLOOP.

    LOOP AT t_tvarvc_mlan INTO tvarvc.
      CLEAR field.

      CONCATENATE 'taxclassifications-' tvarvc-low INTO field.
      ASSIGN (field) TO <value>.
      IF sy-subrc EQ 0.
        WRITE tvarvc-high TO <value>.
      ENDIF.
    ENDLOOP.

    APPEND taxclassifications.

    CALL FUNCTION 'BAPI_MATERIAL_SAVEDATA'
      EXPORTING
        headdata           = headdata
        salesdata          = salesdata
        salesdatax         = salesdatax
      IMPORTING
        return             = return
      TABLES
        taxclassifications = taxclassifications[].

    IF return-type NE 'E' AND sy-subrc EQ 0.
      WRITE: / t_marc-matnr, ' modificado'.
      COMMIT WORK AND WAIT.
    ELSE.
      WRITE: / '                        ', t_marc-matnr, ' error al modificar'.
    ENDIF.
  ENDLOOP.
