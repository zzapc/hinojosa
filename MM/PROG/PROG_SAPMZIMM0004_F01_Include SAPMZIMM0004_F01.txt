*&---------------------------------------------------------------------*
*&  Include           SAPMZIMM0004_F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  INICIALIZAR_DATOS
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM inicializar_datos .

  IF zemm_0004_scr-lgort IS INITIAL.
    zemm_0004_scr-lgort = '1000'.
  ENDIF.

ENDFORM.                    " INICIALIZAR_DATOS
*&---------------------------------------------------------------------*
*&      Form  CREAR_ALV
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM crear_alv.

  DATA is_layout TYPE lvc_s_layo.
  DATA it_toolbar_excluding TYPE ui_functions.
  DATA ls_variant TYPE disvariant.

  CREATE OBJECT g_cont_9000
    EXPORTING
      container_name              = 'CUSTOM_CONTAINER'
    EXCEPTIONS
      cntl_error                  = 1
      cntl_system_error           = 2
      create_error                = 3
      lifetime_error              = 4
      lifetime_dynpro_dynpro_link = 5
      OTHERS                      = 6.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
               WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

  CREATE OBJECT g_alv_9000
    EXPORTING
      i_parent          = g_cont_9000
    EXCEPTIONS
      error_cntl_create = 1
      error_cntl_init   = 2
      error_cntl_link   = 3
      error_dp_create   = 4
      OTHERS            = 5.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
               WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

  PERFORM crear_catalogo_campos.
  PERFORM excluir_botones CHANGING it_toolbar_excluding.

  IF event_rec IS INITIAL.
    CREATE OBJECT event_rec.
  ENDIF.
  SET HANDLER event_rec->handle_data_changed FOR g_alv_9000.
  CALL METHOD g_alv_9000->register_edit_event EXPORTING i_event_id = cl_gui_alv_grid=>mc_evt_modified.

  CREATE OBJECT gcl_event_handler.
  SET HANDLER gcl_event_handler->handle_toolbar      FOR g_alv_9000.
  SET HANDLER gcl_event_handler->handle_user_command FOR g_alv_9000.

  CLEAR ls_variant.
  ls_variant-report = sy-repid.

  g_alv_9000->set_table_for_first_display(
    EXPORTING
       i_save                        = 'A'
       is_layout                     = is_layout
       it_toolbar_excluding          = it_toolbar_excluding
       is_variant                    = ls_variant
    CHANGING
      it_outtab                     = gt_regtb
      it_fieldcatalog               = gt_fcat
    EXCEPTIONS
      invalid_parameter_combination = 1
      program_error                 = 2
      too_many_lines                = 3
      OTHERS                        = 4
         ).

ENDFORM.                    " CREAR_ALV
*&---------------------------------------------------------------------*
*&      Form  CREAR_CATALOGO_CAMPOS
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM crear_catalogo_campos .

  DATA lw_fcat TYPE lvc_s_fcat.

  CALL FUNCTION 'LVC_FIELDCATALOG_MERGE'
    EXPORTING
      i_structure_name       = 'ZEMM_0004_REGTB_ALV'
    CHANGING
      ct_fieldcat            = gt_fcat
    EXCEPTIONS
      inconsistent_interface = 1
      program_error          = 2
      OTHERS                 = 3.

  LOOP AT gt_fcat INTO lw_fcat.
    CASE lw_fcat-fieldname.
      WHEN 'CHECK_BOX'.
        lw_fcat-coltext = text-001.
        lw_fcat-edit = 'X'.
        lw_fcat-checkbox = 'X'.
*        lw_fcat-outputlen = 7.
        lw_fcat-col_opt = 'X'.
      WHEN 'MATNR'.
        lw_fcat-coltext = text-002.
        lw_fcat-outputlen = 13.
*        lw_fcat-col_opt = 'X'.
      WHEN 'MAKTX'.
        lw_fcat-coltext = text-003.
        lw_fcat-outputlen = 23.
*        lw_fcat-col_opt = 'X'.
      WHEN 'CLABS'.
        lw_fcat-no_out = 'X'.
      WHEN 'MEINS'.
        lw_fcat-no_out = 'X'.
      WHEN 'CANT_TXT'.
        lw_fcat-coltext = text-004.
        lw_fcat-outputlen = 12.
*        lw_fcat-col_opt = 'X'.
      WHEN 'CHARG'.
        lw_fcat-coltext = text-005.
        lw_fcat-outputlen = 10.
*        lw_fcat-col_opt = 'X'.
      WHEN 'LICHA'.
        lw_fcat-coltext = text-006.
        lw_fcat-outputlen = 15.
*        lw_fcat-col_opt = 'X'.
      WHEN 'LIFNR'.
        lw_fcat-coltext = text-007.
        lw_fcat-outputlen = 6.
*        lw_fcat-col_opt = 'X'.
      WHEN 'NAME1'.
        lw_fcat-coltext = text-008.
        lw_fcat-outputlen = 20.
*        lw_fcat-col_opt = 'X'.
      WHEN 'LIFNR_TXT'.
        lw_fcat-no_out = 'X'.
      WHEN 'XBLNR'.
        lw_fcat-coltext = text-009.
*        lw_fcat-outputlen = 16.
        lw_fcat-col_opt = 'X'.
      WHEN 'WERKS'.
        lw_fcat-coltext = text-010.
        lw_fcat-outputlen = 4.
*        lw_fcat-col_opt = 'X'.
      WHEN 'LGORT_ORIG'.
        lw_fcat-coltext = text-011.
        lw_fcat-outputlen = 4.
*        lw_fcat-col_opt = 'X'.
      WHEN 'LGORT_ORIG_DESC'.
        lw_fcat-coltext = text-014.
        lw_fcat-outputlen = 16.
*        lw_fcat-col_opt = 'X'.
      WHEN 'LGORT_DEST'.
        lw_fcat-coltext = text-012.
        lw_fcat-outputlen = 4.
*        lw_fcat-col_opt = 'X'.
      WHEN 'LGORT_DEST_DESC'.
        lw_fcat-coltext = text-015.
        lw_fcat-outputlen = 16.
*        lw_fcat-col_opt = 'X'.
      WHEN OTHERS.
    ENDCASE.
    MODIFY gt_fcat FROM lw_fcat.
  ENDLOOP.

ENDFORM.                    " CREAR_CATALOGO_CAMPOS
*&---------------------------------------------------------------------*
*&      Form  EXCLUIR_BOTONES
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      <--P_IT_TOOLBAR_EXCLUDING  text
*----------------------------------------------------------------------*
FORM excluir_botones  CHANGING pt_toolbar_excl TYPE ui_functions.

  REFRESH pt_toolbar_excl.

  APPEND cl_gui_alv_grid=>mc_fc_auf TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_average TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_back_classic TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_call_abc TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_call_chain TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_call_crbatch TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_call_crweb TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_call_lineitems TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_call_master_data TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_call_more TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_call_report TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_call_xint TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_call_xxl TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_check TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_col_invisible TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_count TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_detail TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_expcrdata TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_expcrdesig TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_expcrtempl TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_expmdb TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_extend TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_help TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_html TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_info TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_loc_append_row TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_loc_copy TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_loc_copy_row TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_loc_cut TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_loc_delete_row TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_loc_insert_row TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_loc_move_row TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_loc_paste TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_loc_paste_new_row TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_loc_undo TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_maximum TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_minimum TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_pc_file TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_print TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_print_back TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_print_prev TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_reprep TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_send TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_separator TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_subtot TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_to_office TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_to_rep_tree TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_unfix_columns TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_url_copy_to_clipboard TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_views TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_view_crystal TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_view_excel TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_view_grid TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_view_lotus TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_word_processor TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_mb_export TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_mb_paste TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_mb_subtot TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_mb_view TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fg_edit TO pt_toolbar_excl.
  APPEND cl_gui_alv_grid=>mc_fc_filter TO pt_toolbar_excl.

ENDFORM.                    " EXCLUIR_BOTONES
*&---------------------------------------------------------------------*
*&      Form  DATA_CHANGED
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_ER_DATA_CHANGED  text
*----------------------------------------------------------------------*
FORM data_changed   USING p_er_chg TYPE REF TO cl_alv_changed_data_protocol.

ENDFORM.                    " DATA_CHANGED
*&---------------------------------------------------------------------*
*&      Form  EXIT_COMMAND_9000
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM exit_command_9000 .

  DATA l_continuar TYPE bool.
  DATA numreg TYPE i.
  DATA respuesta TYPE c.

  DESCRIBE TABLE gt_regtb LINES numreg.
  IF numreg > 0.
    CALL FUNCTION 'POPUP_TO_CONFIRM'
      EXPORTING
        titlebar              = 'Traslados registrados'
        text_question         = 'Existen posiciones de traslado sin contabilizar. ¿Desea realmente salir?'
        default_button        = '2'
        display_cancel_button = ' '
      IMPORTING
        answer                = respuesta.

    IF respuesta = 'J' OR respuesta = 'Y' OR respuesta = '1'.
      l_continuar = si.
    ELSE.
      l_continuar = no.
    ENDIF.
  ELSE.
    l_continuar = si.
  ENDIF.

  CHECK l_continuar = si.

  CLEAR zemm_0004_scr-xblnr.
  CLEAR zemm_0004_scr-lgort.
  CLEAR gv_header_txt.

  CLEAR okcode_9000.
  LEAVE TO SCREEN 0.

ENDFORM.                    " EXIT_COMMAND_9000
*&---------------------------------------------------------------------*
*&      Form  USER_COMMAND_9000
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM user_command_9000 .

  DATA l_okcode TYPE sy-ucomm.
  DATA l_continuar TYPE bool.
  DATA numreg TYPE i.
  DATA respuesta TYPE c.

  l_okcode = okcode_9000.
  CLEAR okcode_9000.
  CASE l_okcode.
    WHEN 'OBTB'.  " Obtener bobinas puerto
      PERFORM obtener_bobinas.
    WHEN 'CONT'.  " Contabilizar
      PERFORM contabilizar_traslados.
    WHEN 'BORR'. " Borrar registro si el flag OK marcado
      PERFORM borrar_sin_ok.
    WHEN OTHERS.
      IF sy-ucomm = 'BACK' OR sy-ucomm = 'EXIT' OR sy-ucomm = 'CANCEL'.
        DESCRIBE TABLE gt_regtb LINES numreg.
        IF numreg > 0.
          CALL FUNCTION 'POPUP_TO_CONFIRM'
            EXPORTING
              titlebar              = 'Traslados registrados'
              text_question         = 'Existen posiciones de traslado sin contabilizar. ¿Desea realmente salir?'
              default_button        = '2'
              display_cancel_button = ' '
            IMPORTING
              answer                = respuesta.

          IF respuesta = 'J' OR respuesta = 'Y' OR respuesta = '1'.
            l_continuar = si.
          ELSE.
            l_continuar = no.
          ENDIF.
        ELSE.
          l_continuar = si.
        ENDIF.

        CHECK l_continuar = si.

        CLEAR zemm_0004_scr-xblnr.
        CLEAR zemm_0004_scr-lgort.
        CLEAR gv_header_txt.
        REFRESH: gt_regtb.

        CLEAR okcode_9000.
        LEAVE TO SCREEN 0.
      ELSEIF sy-ucomm IS INITIAL.
        PERFORM obtener_bobinas.
      ENDIF.
  ENDCASE.

ENDFORM.                    " USER_COMMAND_9000
*&---------------------------------------------------------------------*
*&      Form  OBTENER_BOBINAS
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM obtener_bobinas .

  DATA: l_continuar TYPE bool VALUE si,
        lv_lgort    TYPE lgort_d.
  DATA: BEGIN OF lt_bob OCCURS 0,
          xblnr TYPE xblnr1,
          lifnr TYPE elifn,
          matnr TYPE matnr,
          clabs TYPE labst,
          charg TYPE charg_d,
          werks TYPE werks_d,
          lgort TYPE lgort_d,
        END OF lt_bob.

  DATA: ls_bob       LIKE LINE OF lt_bob,
        ls_regtb     LIKE LINE OF gt_regtb,
        ls_regtb_aux LIKE LINE OF gt_regtb.

  IF zemm_0004_scr-xblnr IS INITIAL OR zemm_0004_scr-lgort IS INITIAL.
    MESSAGE i072(zmm).
    l_continuar = no.
  ENDIF.
  CHECK l_continuar = si.

  CLEAR lv_lgort.
  SELECT SINGLE lgort INTO lv_lgort
    FROM t001l
    WHERE lgort = zemm_0004_scr-lgort.
  IF lv_lgort IS INITIAL.
    MESSAGE i076(zmm) WITH zemm_0004_scr-lgort.
    l_continuar = no.
  ENDIF.
  CHECK l_continuar = si.

  CLEAR: ls_bob, lt_bob.
  REFRESH: lt_bob.

  SELECT a~xblnr b~lifnr c~matnr c~clabs c~charg c~werks c~lgort
    INTO CORRESPONDING FIELDS OF TABLE lt_bob
    FROM mkpf AS a
    INNER JOIN mseg AS b ON ( b~mblnr = a~mblnr AND
                              b~mjahr = a~mjahr )
    INNER JOIN mchb AS c ON ( c~matnr = b~matnr AND
                              c~werks = b~werks AND
                              c~charg = b~charg )
    WHERE a~xblnr = zemm_0004_scr-xblnr
    AND c~lgort LIKE '2%'
    AND c~clabs > 0
    AND c~lvorm = ' '.

  IF lt_bob[] IS INITIAL.
    MESSAGE i073(zmm) WITH zemm_0004_scr-xblnr.
    l_continuar = no.
  ELSE.
* Chequeo centro entradas seleccionadas:
    LOOP AT lt_bob INTO ls_bob.
      AUTHORITY-CHECK OBJECT 'M_MSEG_WMB'
               ID 'ACTVT' FIELD '03'
               ID 'WERKS' FIELD ls_bob-werks.

      IF sy-subrc <> 0.
        MESSAGE s075(zmm) WITH ls_bob-werks DISPLAY LIKE 'E'.
        "Sin autorización a visualizar documentos de material del centro.
        LEAVE TO SCREEN 9000.
      ENDIF.
    ENDLOOP.


* Recuperamos los datos que faltan para el listado:
    LOOP AT lt_bob INTO ls_bob.
      MOVE-CORRESPONDING ls_bob TO ls_regtb.
      ls_regtb-lgort_orig = ls_bob-lgort.

      " Almacén destino:
      ls_regtb-lgort_dest = zemm_0004_scr-lgort.

      CLEAR ls_regtb_aux.
      READ TABLE gt_regtb INTO ls_regtb_aux
                 WITH KEY matnr = ls_bob-matnr
                          charg = ls_bob-charg.
*                          clabs = ls_bob-clabs
*                          lifnr = ls_bob-lifnr
*                          xblnr = ls_bob-xblnr
*                          werks = ls_bob-werks
*                          lgort_orig = ls_bob-lgort
*                          lgort_dest = zemm_0004_scr-lgort.

      IF ls_regtb_aux IS INITIAL.

        " Lote proveedor:
        SELECT SINGLE licha INTO ls_regtb-licha
          FROM mch1
          WHERE matnr = ls_bob-matnr
          AND charg = ls_bob-charg.

        " Descripción de material:
        SELECT SINGLE maktx INTO ls_regtb-maktx
          FROM makt
          WHERE matnr = ls_bob-matnr
          AND spras = sy-langu.

        " Cantidad y unidades:
        SELECT SINGLE meins INTO ls_regtb-meins
          FROM mara
          WHERE matnr = ls_bob-matnr.
        WRITE ls_regtb-clabs TO ls_regtb-cant_txt.
        CONDENSE ls_regtb-cant_txt.
        CONCATENATE ls_regtb-cant_txt ls_regtb-meins
                    INTO ls_regtb-cant_txt
                    SEPARATED BY space.

        " Nombre del proveedor:
        SELECT SINGLE name1 INTO ls_regtb-name1
          FROM lfa1
          WHERE lifnr = ls_bob-lifnr.

        IF ls_regtb-name1 IS NOT INITIAL.
          CONCATENATE ls_bob-lifnr ls_regtb-name1 INTO
                      ls_regtb-lifnr_txt SEPARATED BY ' - '.
        ELSE.
          ls_regtb-lifnr_txt = ls_bob-lifnr.
        ENDIF.

        " Descripción almacén origen:
        SELECT SINGLE lgobe INTO ls_regtb-lgort_orig_desc
          FROM t001l
          WHERE werks = ls_regtb-werks
          AND lgort = ls_regtb-lgort_orig.

        " Descripción almacén destino:
        SELECT SINGLE lgobe INTO ls_regtb-lgort_dest_desc
          FROM t001l
          WHERE werks = ls_regtb-werks
          AND lgort = ls_regtb-lgort_dest.

        APPEND ls_regtb TO gt_regtb.
      ENDIF.

    ENDLOOP.
  ENDIF.

  CLEAR: zemm_0004_scr-xblnr, zemm_0004_scr-lgort.
  CLEAR gv_header_txt.

ENDFORM.                    " OBTENER_BOBINAS
*&---------------------------------------------------------------------*
*&      Form  CONTABILIZAR_TRASLADOS
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM contabilizar_traslados.

  DATA: ls_header TYPE         bapi2017_gm_head_01,
        ls_code   TYPE         bapi2017_gm_code,
        lt_item   LIKE         bapi2017_gm_item_create OCCURS 0,
        ls_head   TYPE         bapi2017_gm_head_ret,
        lv_doc    LIKE         bapi2017_gm_head_ret-mat_doc,
        lv_year   LIKE         bapi2017_gm_head_ret-doc_year,
        ls_item   TYPE         bapi2017_gm_item_create,
        lt_return LIKE         bapiret2 OCCURS 0,
        ls_return TYPE         bapiret2,
        ls_regtb  LIKE LINE OF gt_regtb,
        lv_index  LIKE         sy-tabix.

  CLEAR ls_header.
  ls_header-pstng_date = sy-datum.
  ls_header-doc_date = sy-datum.
  ls_header-header_txt = gv_header_txt.
  ls_header-ver_gr_gi_slip = '1'.
  ls_header-ver_gr_gi_slipx = 'X'.
  ls_header-pr_uname = sy-uname.

  CLEAR ls_code.
  ls_code-gm_code = '04'.

  CLEAR: lt_item, ls_item.
  REFRESH: lt_item.
  LOOP AT gt_regtb INTO ls_regtb WHERE check_box = 'X'.
    CLEAR ls_item.
** GST - Autorización centro ->
*    AUTHORITY-CHECK OBJECT 'M_MSEG_WMB'
*             ID 'ACTVT' FIELD '03'
*             ID 'WERKS' FIELD ls_regtb-werks.
*
*    IF sy-subrc <> 0.
*      MESSAGE s202(12) WITH ls_regtb-werks display like 'E'.
*      "Sin autorización a visualizar documentos de material del centro.
*      LEAVE TO SCREEN 9000.
*    ELSE.
** GST - Autorización centro <-
    ls_item-material = ls_regtb-matnr.
    ls_item-batch = ls_regtb-charg.
    ls_item-plant = ls_regtb-werks.
    ls_item-stge_loc = ls_regtb-lgort_orig.
    ls_item-move_type = '311'.
    ls_item-entry_qnt = ls_regtb-clabs.
    ls_item-entry_uom = ls_regtb-meins.
    ls_item-move_stloc = ls_regtb-lgort_dest.
    APPEND ls_item TO lt_item.
*    ENDIF.
  ENDLOOP.

  CLEAR: ls_head, lv_doc, lv_year, lt_return, ls_return.
  REFRESH: lt_return.

  SET PARAMETER ID 'NDR' FIELD 'X'.

  CALL FUNCTION 'BAPI_GOODSMVT_CREATE'
    EXPORTING
      goodsmvt_header  = ls_header
      goodsmvt_code    = ls_code
*     TESTRUN          = ' '
*     GOODSMVT_REF_EWM =
    IMPORTING
      goodsmvt_headret = ls_head
      materialdocument = lv_doc
      matdocumentyear  = lv_year
    TABLES
      goodsmvt_item    = lt_item
*     GOODSMVT_SERIALNUMBER         =
      return           = lt_return
*     GOODSMVT_SERV_PART_DATA       =
*     EXTENSIONIN      =
    .

*   Comprobamos errores
  CLEAR ls_return.
  READ TABLE lt_return INTO ls_return
  WITH KEY type = 'E'.
  IF sy-subrc = 0.
    MESSAGE ls_return-message TYPE 'S'.
*    MESSAGE ls_return-message display like 'E'.
  ELSE.

    CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
      EXPORTING
        wait = 'X'.
*     Volvemos a la dynpro inicial

    MESSAGE s074(zmm) WITH lv_doc lv_year.

    CLEAR: zemm_0004_scr-xblnr, zemm_0004_scr-lgort.
    LOOP AT lt_item INTO ls_item.
      READ TABLE gt_regtb INTO ls_regtb
                 WITH KEY matnr = ls_item-material
                          charg = ls_item-batch.
      lv_index = sy-tabix.
      IF ls_regtb IS NOT INITIAL.
        DELETE gt_regtb INDEX lv_index.
      ENDIF.
    ENDLOOP.

    LEAVE TO SCREEN 9000.

  ENDIF.

ENDFORM.                    " CONTABILIZAR_TRASLADOS
*&---------------------------------------------------------------------*
*&      Form  MARCAR_TODAS
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM marcar_todas .

  DATA: ls_regtb     LIKE LINE OF           gt_regtb,
        lv_index     LIKE                   sy-tabix,
        gt_regtb_aux TYPE STANDARD TABLE OF zemm_0004_regtb_alv.

  CLEAR gt_regtb_aux.
  REFRESH: gt_regtb_aux.
  gt_regtb_aux[] = gt_regtb[].

  LOOP AT gt_regtb_aux INTO ls_regtb.
    lv_index = sy-tabix.
    ls_regtb-check_box = 'X'.
    MODIFY gt_regtb FROM ls_regtb INDEX lv_index.
  ENDLOOP.

  g_alv_9000->refresh_table_display( ).

ENDFORM.                    " MARCAR_TODAS

*&---------------------------------------------------------------------*
*&      Form  DESMARCAR_TODAS
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM desmarcar_todas .

  DATA: ls_regtb     LIKE LINE OF           gt_regtb,
        lv_index     LIKE                   sy-tabix,
        gt_regtb_aux TYPE STANDARD TABLE OF zemm_0004_regtb_alv.

  CLEAR gt_regtb_aux.
  REFRESH: gt_regtb_aux.
  gt_regtb_aux[] = gt_regtb[].

  LOOP AT gt_regtb_aux INTO ls_regtb.
    lv_index = sy-tabix.
    CLEAR ls_regtb-check_box.
    MODIFY gt_regtb FROM ls_regtb INDEX lv_index.
  ENDLOOP.

  g_alv_9000->refresh_table_display( ).

ENDFORM.                    " DESMARCAR_TODAS
*&---------------------------------------------------------------------*
*&      Form  BORRAR_SIN_OK
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM borrar_sin_ok .

  DATA: ls_regtb LIKE LINE OF gt_regtb,
        lv_index LIKE         sy-tabix.
*        gt_regtb_aux TYPE STANDARD TABLE OF ZEMM_0004_REGTB_ALV.

*  CLEAR gt_regtb_aux.
*  REFRESH: gt_regtb_aux.
*  gt_regtb_aux[] = gt_regtb[].

  LOOP AT gt_regtb INTO ls_regtb WHERE check_box <> 'X'.
    lv_index = sy-tabix.
    DELETE gt_regtb INDEX lv_index.
  ENDLOOP.

  g_alv_9000->refresh_table_display( ).

ENDFORM.                    " BORRAR_SIN_OK
