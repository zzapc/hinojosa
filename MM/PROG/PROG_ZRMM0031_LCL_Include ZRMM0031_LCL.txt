*&---------------------------------------------------------------------*
*&  Include           ZRMM0031_LCL
*&---------------------------------------------------------------------*
CLASS lcl_po_update DEFINITION.

  PUBLIC SECTION.

    "Tipos para rango de n. pedidos
    TYPES: BEGIN OF _ty_ebeln,
      sign(1)      TYPE c,
      option(2)    TYPE c,
      low          TYPE ekko-ebeln,
      high         TYPE ekko-ebeln,
    END OF _ty_ebeln.

    "Tipos para sociedad
    TYPES: BEGIN OF _ty_bukrs,
      sign(1)      TYPE c,
      option(2)    TYPE c,
      low          TYPE ekko-bukrs,
      high         TYPE ekko-bukrs,
    END OF _ty_bukrs.

    "Tipos para rango de org. de compra
    TYPES: BEGIN OF _ty_ekorg,
      sign(1)      TYPE c,
      option(2)    TYPE c,
      low          TYPE ekko-ekorg,
      high         TYPE ekko-ekorg,
    END OF _ty_ekorg.

    "Tipos para rango de grupos de compra
    TYPES: BEGIN OF _ty_ekgrp,
      sign(1)      TYPE c,
      option(2)    TYPE c,
      low          TYPE ekko-ekgrp,
      high         TYPE ekko-ekgrp,
    END OF _ty_ekgrp.

    "Tipos para rango de tipos de material
    TYPES: BEGIN OF _ty_mtart,
      sign(1)      TYPE c,
      option(2)    TYPE c,
      low          TYPE mara-mtart,
      high         TYPE mara-mtart,
    END OF _ty_mtart.

    "Tipos para pedidos
    TYPES: BEGIN OF _ty_podata,
      ebeln TYPE ebeln,
    END OF  _ty_podata.

    TYPES: BEGIN OF _ty_polog,
      ebeln       TYPE ekpo-ebeln,
      ebelp       TYPE ekpo-ebelp,
      stge_loc    TYPE ekpo-lgort,
      quantity    TYPE ekpo-menge,
      new_menge   TYPE ekpo-menge,
      no_more_gr  TYPE ekpo-elikz,
      conf_ctrl   TYPE ekpo-bstae,
      message     TYPE bapi_msg,
      type        TYPE bapi_mtype,
      id          TYPE symsgid,
      number      TYPE symsgno,
    END OF _ty_polog.

    CLASS-DATA: lcl_t_po_log TYPE TABLE OF _ty_polog.

    TYPES: _tty_ebeln TYPE TABLE OF _ty_ebeln,
           _tty_bukrs TYPE TABLE OF _ty_bukrs,
           _tty_ekorg TYPE TABLE OF _ty_ekorg,
           _tty_ekgrp TYPE TABLE OF _ty_ekgrp,
           _tty_mtart TYPE TABLE OF _ty_mtart,

           _tty_return TYPE TABLE OF bapiret2,

           _tty_podata TYPE TABLE OF _ty_podata,
           _tty_polog  TYPE TABLE OF _ty_polog.

    DATA: _gr_ebeln TYPE _tty_ebeln,
          _gr_bukrs TYPE _tty_bukrs,
          _gr_ekorg TYPE _tty_ekorg,
          _gr_ekgrp TYPE _tty_ekgrp,
          _gr_mtart TYPE _tty_mtart,

          _gt_podata TYPE _tty_podata,
          _gt_polog  TYPE _tty_polog.

    METHODS: constructor IMPORTING ir_ebeln TYPE _tty_ebeln
                                   ir_bukrs TYPE _tty_bukrs
                                   ir_ekorg TYPE _tty_ekorg
                                   ir_ekgrp TYPE _tty_ekgrp OPTIONAL
                                   ir_mtart TYPE _tty_mtart OPTIONAL,

             update_mass_po_values IMPORTING iv_testrun   TYPE boolean
                                             is_poheader  TYPE bapimepoheader
                                             is_poheaderx TYPE bapimepoheaderx
                                             is_poitem    TYPE bapimepoitem
                                             is_poitemx   TYPE bapimepoitemx
                                   EXPORTING et_return    TYPE _tty_return.

  PRIVATE SECTION.

    CONSTANTS: _c_f TYPE c VALUE 'F'.

    TYPES: __ty_poheader          TYPE bapimepoheader,
           __ty_poheaderx         TYPE bapimepoheaderx,
           __tty_poitem           TYPE TABLE OF bapimepoitem,
           __tty_poitemx          TYPE TABLE OF bapimepoitemx,
           __tty_poschedule       TYPE TABLE OF bapimeposchedule,
           __tty_poschedulex      TYPE TABLE OF bapimeposchedulx,
           __tty_pocond           TYPE TABLE OF bapimepocond,
           __tty_pocondx          TYPE TABLE OF bapimepocondx,
           __tty_pohistory        TYPE TABLE OF bapiekbe,
           __tty_pohistory_totals TYPE TABLE OF bapiekbes,
           __tty_extensionout     TYPE TABLE OF bapiparex.

             "Selecciona pedidos
    METHODS: select_data,
             "Obtiene detalles del pedido
             get_ebeln_details IMPORTING iv_ebeln             TYPE ekko-ebeln
                               EXPORTING et_return            TYPE _tty_return
                                         et_poheader          TYPE __ty_poheader
                                         et_poitem            TYPE __tty_poitem
                                         et_pocond            TYPE __tty_pocond
                                         et_poschedule        TYPE __tty_poschedule
                                         et_pohistory         TYPE __tty_pohistory
                                         et_pohistory_totals  TYPE __tty_pohistory_totals
                                         et_extensionout      TYPE __tty_extensionout,
             "Modifica los valores de cabecera
             change_poheader_values IMPORTING is_poheader      TYPE bapimepoheader
                                              is_new_poheader  TYPE bapimepoheader
                                              is_poheaderx     TYPE bapimepoheaderx,
             "Modifica los valores de posicion en masa
             change_poitem_values IMPORTING iv_ebeln            TYPE ekko-ebeln
                                            is_new_mepoitem     TYPE bapimepoitem
                                            is_mepoitemx        TYPE bapimepoitemx
                                            it_pocond           TYPE __tty_pocond
                                            it_poschedule       TYPE __tty_poschedule
                                            it_pohistory        TYPE __tty_pohistory
                                            it_pohistory_totals TYPE __tty_pohistory_totals
                                  EXPORTING et_return           TYPE _tty_return
                                            et_mepoitemx        TYPE __tty_poitemx
                                            et_pocondx          TYPE __tty_pocondx
                                            et_poschedule       TYPE __tty_poschedule
                                  CHANGING  ct_mepoitem         TYPE __tty_poitem,
             "Guarda valores pedido
             save_po_data IMPORTING iv_ebeln        TYPE ekko-ebeln
                                    iv_testrun      TYPE boolean
                                    is_poheader     TYPE __ty_poheader
                                    is_poheaderx    TYPE __ty_poheaderx
                                    it_poitem       TYPE __tty_poitem
                                    it_poitemx      TYPE __tty_poitemx
                                    it_pocond       TYPE __tty_pocond
                                    it_pocondx      TYPE __tty_pocondx
                                    it_poschedule   TYPE __tty_poschedule
                                    it_poschedulex  TYPE __tty_poschedulex
                                    it_extensionout TYPE __tty_extensionout
                          EXPORTING et_return     TYPE _tty_return,
             "Crea registro de log
             create_log IMPORTING is_po_log TYPE _ty_polog,
             "Crea nueva posicion en pedido como copia de otra
             create_new_item_as_copy IMPORTING iv_ebeln         TYPE ekko-ebeln
                                               iv_new_ebelp     TYPE ekpo-ebelp
                                               is_poitem        TYPE bapimepoitem
                                               is_pocond        TYPE bapimepocond
                                               is_poschedule    TYPE bapimeposchedule
                                     EXPORTING es_poitem        TYPE bapimepoitem
                                               es_poitemx       TYPE bapimepoitemx
                                               es_pocond        TYPE bapimepocond
                                               es_pocondx       TYPE bapimepocondx
                                               es_poschedule    TYPE bapimeposchedule
                                               es_poschedulex   TYPE bapimeposchedulx.

ENDCLASS.

CLASS lcl_po_update IMPLEMENTATION.

  METHOD constructor.

    me->_gr_ebeln[] = ir_ebeln[].
    me->_gr_bukrs[] = ir_bukrs[].
    me->_gr_ekorg[] = ir_ekorg[].
    me->_gr_ekgrp[] = ir_ekgrp[].
    me->_gr_mtart[] = ir_mtart[].

    me->select_data( ).

  ENDMETHOD.

  METHOD select_data.

    SELECT *
      INTO CORRESPONDING FIELDS OF TABLE _gt_podata
      FROM ekko AS ek
*      INNER JOIN ekpo AS ep ON ek~ebeln EQ ep~ebeln
     WHERE ek~ebeln IN _gr_ebeln
       AND ek~bstyp EQ _c_f
       AND ek~bukrs IN _gr_bukrs
       AND ek~ekorg IN _gr_ekorg
       AND ek~ekgrp IN _gr_ekgrp
*       AND ep~loekz EQ space
*       AND ep~elikz EQ space
     ORDER BY ek~ebeln." ep~ebelp.

     "DELETE ADJACENT DUPLICATES FROM _gt_podata COMPARING ebeln.

  ENDMETHOD.

  METHOD get_ebeln_details.

    DATA: ls_polog    TYPE _ty_polog,
          ls_return   TYPE bapiret2,
          ls_mepoitem TYPE bapimepoitem.

    CALL FUNCTION 'BAPI_PO_GETDETAIL1'
      EXPORTING
        purchaseorder            = iv_ebeln
      IMPORTING
        poheader                 = et_poheader
      TABLES
        return                   = et_return
        poitem                   = et_poitem
*       POADDRDELIVERY           =
        poschedule               = et_poschedule
*       POACCOUNT                =
*       POCONDHEADER             =
        pocond                   = et_pocond
*       POLIMITS                 =
*       POCONTRACTLIMITS         =
*       POSERVICES               =
*       POSRVACCESSVALUES        =
*       POTEXTHEADER             =
*       POTEXTITEM               =
*       POEXPIMPITEM             =
*       POCOMPONENTS             =
*       POSHIPPINGEXP            =
        pohistory                = et_pohistory
        pohistory_totals         = et_pohistory_totals
*       POCONFIRMATION           =
*       ALLVERSIONS              =
*       POPARTNER                =
        extensionout             = et_extensionout
*       SERIALNUMBER             =
*       INVPLANHEADER            =
*       INVPLANITEM              =
*       POHISTORY_MA             =
              .

      "Comprobamos si ha habido errores
      READ TABLE et_return WITH KEY type = 'E' INTO ls_return.

      IF ( sy-subrc EQ 0 ). "Hay errores

        "Creo log y salgo
        MOVE-CORRESPONDING ls_return TO ls_polog.
        ls_polog-ebeln    = iv_ebeln.
        me->create_log( is_po_log = ls_polog ).
        EXIT.

      ELSE.  "No hay errores

      "Elimino posiciones con indicador de borrado o con EM final
      DELETE et_poitem WHERE delete_ind IS NOT INITIAL.
      DELETE et_poitem WHERE no_more_gr IS NOT INITIAL.

      "Compruebo si quedan registros
      IF ( lines( et_poitem  ) LE 0 ). "No quedan registros -> Pedido no modificable

        "Creo log y salgo
        ls_polog-ebeln    = iv_ebeln.
        ls_polog-type     = 'W'.
        ls_polog-message  = 'Pedido sin posiciones modificables'.
        me->create_log( is_po_log = ls_polog ).
        EXIT.

      ENDIF.

      ENDIF.

  ENDMETHOD.

  METHOD update_mass_po_values.

    DATA: ls_polog            TYPE _ty_polog,
          ls_podata           TYPE _ty_podata,
          lt_return           TYPE _tty_return,
          ls_return           TYPE bapiret2,
          ls_poheader         TYPE __ty_poheader,
          ls_poheaderx        TYPE __ty_poheaderx,
          lt_poitems          TYPE __tty_poitem,
          lt_poitemsx         TYPE __tty_poitemx,
          lt_pocond           TYPE __tty_pocond,
          lt_pocondx          TYPE __tty_pocondx,
          lt_poschedule       TYPE __tty_poschedule,
          lt_poschedulex      TYPE __tty_poschedulex,
          lt_pohistory        TYPE __tty_pohistory,
          lt_pohistory_totals TYPE __tty_pohistory_totals,
          lt_extensionout     TYPE __tty_extensionout.

    LOOP AT _gt_podata INTO ls_podata.

      CLEAR: lt_return, ls_poheader, ls_poheaderx, lt_poitems, lt_poitemsx, lt_pohistory, lt_pohistory_totals, lt_extensionout,
             ls_polog, ls_return.

      "Obtenemos detalles del pedido
      me->get_ebeln_details(
        EXPORTING
          iv_ebeln            = ls_podata-ebeln
        IMPORTING
          et_return           = lt_return
          et_poheader         = ls_poheader
          et_poitem           = lt_poitems
          et_pocond           = lt_pocond
          et_poschedule       = lt_poschedule
          et_pohistory        = lt_pohistory
          et_pohistory_totals = lt_pohistory_totals
          et_extensionout     = lt_extensionout ).

      READ TABLE lt_return WITH KEY type = 'E' TRANSPORTING NO FIELDS.

      IF ( sy-subrc EQ 0 ).
        CONTINUE.
      ENDIF.

      "TODO: Marcamos EF y creamos posicion por restante
      me->change_poitem_values(
        EXPORTING
          iv_ebeln            = ls_podata-ebeln
          is_new_mepoitem     = is_poitem
          is_mepoitemx        = is_poitemx
          it_pocond           = lt_pocond
          it_poschedule       = lt_poschedule
          it_pohistory        = lt_pohistory
          it_pohistory_totals = lt_pohistory_totals
        IMPORTING
          et_return           = lt_return
          et_mepoitemx        = lt_poitemsx
        CHANGING
          ct_mepoitem         = lt_poitems ).


     "Guardamos datos
      me->save_po_data(
        EXPORTING
          iv_ebeln        = ls_podata-ebeln
          iv_testrun      = iv_testrun
          is_poheader     = ls_poheader
          is_poheaderx    = ls_poheaderx
          it_poitem       = lt_poitems
          it_poitemx      = lt_poitemsx
          it_pocond       = lt_pocond
          it_pocondx      = lt_pocondx
          it_poschedule   = lt_poschedule
          it_poschedulex  = lt_poschedulex
          it_extensionout = lt_extensionout
        IMPORTING
          et_return    = lt_return ).

    ENDLOOP.

  ENDMETHOD.

  METHOD change_poheader_values.

  ENDMETHOD.

  METHOD change_poitem_values.

    FIELD-SYMBOLS: <fs_field>     TYPE any,
                   <fs_ifield>    TYPE any,
                   <fs_ixfield>   TYPE any,
                   <fs_xfield>    TYPE any,
                   <fs_pfield>    TYPE any,
                   <fs_mepoitem>  TYPE bapimepoitem,
                   <fs_mepoitemx> TYPE bapimepoitemx.

    DATA: ls_polog       TYPE _ty_polog,
          lo_structdescr TYPE REF TO cl_abap_structdescr,
          lt_components  TYPE abap_component_tab,
          ls_components  TYPE LINE OF abap_component_tab.

    DATA: ls_pohistory_totals TYPE bapiekbes,
          ls_mepoitem         TYPE bapimepoitem,
          ls_newpoitem        TYPE bapimepoitem,
          ls_mepoitemx        TYPE bapimepoitemx,
          ls_newpoitemx       TYPE bapimepoitemx,
          ls_pocond           TYPE bapimepocond,
          ls_poschedule       TYPE bapimeposchedule,
          lv_last_ekpo_item   TYPE bapimepoitem-po_item,
          lv_last_ct_poitem   TYPE bapimepoitem-po_item,
          lv_new_qty          TYPE bapimepoitem-quantity.


    lo_structdescr ?= cl_abap_structdescr=>describe_by_data( is_mepoitemx ).
    lt_components = lo_structdescr->get_components( ).

    LOOP AT lt_components ASSIGNING FIELD-SYMBOL(<fs_component>).

      CASE <fs_component>-name.

        WHEN 'CONF_CTRL'. "Ctrl. Conf.

          ASSIGN COMPONENT <fs_component>-name OF STRUCTURE is_mepoitemx TO <fs_field>.
          IF ( <fs_field> IS ASSIGNED ).
            IF ( <fs_field> IS NOT INITIAL ).

              "Última posición
              SELECT SINGLE MAX( ebelp )
                FROM ekpo
                INTO lv_last_ekpo_item
               WHERE ebeln = iv_ebeln.

              CLEAR: lv_last_ct_poitem.

              LOOP AT ct_mepoitem ASSIGNING <fs_mepoitem> WHERE po_item LE lv_last_ekpo_item.

                IF ( <fs_mepoitem>-delete_ind IS NOT INITIAL OR <fs_mepoitem>-no_more_gr IS NOT INITIAL ).
                  CONTINUE.
                ENDIF.

                SELECT COUNT(*) UP TO 1 ROWS
                  FROM mara
                 WHERE matnr EQ <fs_mepoitem>-material
                   AND mtart IN _gr_mtart.

                IF ( sy-subrc NE 0 ).
                  CONTINUE.
                ENDIF.

                READ TABLE it_pohistory_totals INTO ls_pohistory_totals WITH KEY po_item = <fs_mepoitem>-po_item.

                "Comprobamos si tiene EM
                IF ( sy-subrc EQ 0 ).

                  "Comprobamos cantidad pendiente de EM.
                  IF ( <fs_mepoitem>-quantity GT ls_pohistory_totals-deliv_qty ).

                    "CASO 1: Existen entregas parciales de la posición
                    "        la posición se desdobla.

                    "1. Calculamos numero de nueva posición
                    IF ( lv_last_ct_poitem IS INITIAL OR lv_last_ct_poitem EQ 0 ).
                      lv_last_ct_poitem = lv_last_ekpo_item + 10.
                    ELSE.
                      lv_last_ct_poitem = lv_last_ct_poitem + 10.
                    ENDIF.

                    "2. Creamos una nueva posición como copia de la original
                    CLEAR: ls_newpoitem, ls_newpoitemx.
                    me->create_new_item_as_copy(
                      EXPORTING
                        iv_ebeln       = iv_ebeln
                        iv_new_ebelp   = lv_last_ct_poitem
                        is_poitem      = <fs_mepoitem>
                        is_pocond      = ls_pocond "TODO
                        is_poschedule  = ls_poschedule "TODO
                      IMPORTING
                        es_poitem      = ls_newpoitem
                        es_poitemx     = ls_newpoitemx
*                        es_pocond      =
*                        es_pocondx     =
*                        es_poschedule  =
*                        es_poschedulex =
                    ).

                    "3. Tratamos la posición original

                    "Calculo cantidad de nueva posición
                    lv_new_qty = <fs_mepoitem>-quantity - ls_pohistory_totals-deliv_qty.

                    ""Corrijo posición
                    <fs_mepoitem>-quantity    = ls_pohistory_totals-deliv_qty.
                    <fs_mepoitem>-no_more_gr  = abap_true.

                    "Comprobamos si ya tenemos registro.
                    READ TABLE et_mepoitemx WITH KEY po_item = <fs_mepoitem>-po_item ASSIGNING <fs_mepoitemx>.
                    IF ( <fs_mepoitemx> IS ASSIGNED AND sy-subrc = 0 ).

                      <fs_mepoitemx>-quantity   = abap_true.
                      <fs_mepoitemx>-no_more_gr = abap_true.

                    ELSE.

                      CLEAR: ls_mepoitemx.
                      ls_mepoitemx-po_item  = <fs_mepoitem>-po_item.
                      ls_mepoitemx-po_itemx = abap_true.
                      ls_mepoitemx-stge_loc = abap_true.
                      APPEND ls_mepoitemx TO et_mepoitemx.

                    ENDIF.

                    "LOG
                    CLEAR: ls_polog.
                    ls_polog-ebeln      = iv_ebeln.
                    ls_polog-ebelp      = <fs_mepoitem>-po_item.
                    ls_polog-quantity   = <fs_mepoitem>-quantity + lv_new_qty.
                    ls_polog-new_menge  = <fs_mepoitem>-quantity.
                    ls_polog-no_more_gr = <fs_mepoitem>-no_more_gr.
                    ls_polog-type       = 'S'.
                    ls_polog-message    = text-002.
                    me->create_log( is_po_log = ls_polog ).

                    "3. Tratamos la nueva posición
                    ls_newpoitem-quantity   = lv_new_qty.
                    ls_newpoitem-conf_ctrl  = is_new_mepoitem-conf_ctrl.
                    APPEND ls_newpoitem TO ct_mepoitem.

                    ls_mepoitemx-quantity   = abap_true.
                    ls_newpoitemx-conf_ctrl = abap_true.
                    APPEND ls_newpoitemx TO et_mepoitemx.

                    "LOG
                    CLEAR: ls_polog.
                    ls_polog-ebeln      = iv_ebeln.
                    ls_polog-ebelp      = ls_newpoitem-po_item.
                    ls_polog-quantity   = ls_newpoitem-quantity.
                    ls_polog-type       = 'S'.
                    ls_polog-conf_ctrl  = ls_newpoitem-conf_ctrl.
                    CONCATENATE 'Posición creada con cantidad restante de posición' <fs_mepoitem>-po_item INTO ls_polog-message SEPARATED BY space.
                    me->create_log( is_po_log = ls_polog ).

                  ELSE. "Cantidad EM igual o mayor a la de pedido --> No corrijo posición pedido

                    "CASO 2: No se puede corregir pq ya se ha entregado todo o se ha entregado más de lo previsto

                    "LOG
                    CLEAR: ls_polog.
                    ls_polog-ebeln      = iv_ebeln.
                    ls_polog-type       = 'E'.
                    ls_polog-ebelp      = <fs_mepoitem>-po_item.
                    ls_polog-message    = 'Cantidad EM igual o mayor a cantidad posición pedido'.
                    me->create_log( is_po_log = ls_polog ).

                    DELETE ct_mepoitem WHERE po_item EQ <fs_mepoitem>-po_item.

                    CONTINUE.

                  ENDIF. "Fin ctd pendiente EM

                ELSE. "No tiene EM realizada

                  <fs_mepoitem>-conf_ctrl   = is_new_mepoitem-conf_ctrl.

                  "Comprobamos si ya tenemos registro.
                  READ TABLE et_mepoitemx WITH KEY po_item = <fs_mepoitem>-po_item ASSIGNING <fs_mepoitemx>.
                  IF ( <fs_mepoitemx> IS ASSIGNED AND sy-subrc = 0 ).

                    <fs_mepoitemx>-conf_ctrl = abap_true.

                  ELSE.

                    CLEAR: ls_mepoitemx.
                    ls_mepoitemx-po_item    = <fs_mepoitem>-po_item.
                    ls_mepoitemx-po_itemx   = abap_true.
                    ls_mepoitemx-conf_ctrl  = abap_true.
                    APPEND ls_mepoitemx TO et_mepoitemx.

                  ENDIF.

                  "LOG
                  CLEAR: ls_polog.
                  ls_polog-ebeln      = iv_ebeln.
                  ls_polog-ebelp      = <fs_mepoitem>-po_item.
                  ls_polog-type       = 'S'.
                  ls_polog-conf_ctrl      = <fs_mepoitem>-conf_ctrl.
                  ls_polog-message    = text-002.
                  me->create_log( is_po_log = ls_polog ).

                  CONTINUE.

                ENDIF. "Fin tiene EM

              ENDLOOP.

            ENDIF.

          ENDIF.

        WHEN OTHERS.

          ASSIGN COMPONENT <fs_component>-name OF STRUCTURE is_mepoitemx TO <fs_ixfield>.
          IF ( <fs_ixfield> IS ASSIGNED ).
            IF ( <fs_ixfield> IS NOT INITIAL ).

              ASSIGN COMPONENT <fs_component>-name OF STRUCTURE is_new_mepoitem TO <fs_ifield>.

              LOOP AT ct_mepoitem ASSIGNING <fs_mepoitem>.

                IF ( <fs_mepoitem>-delete_ind IS NOT INITIAL OR <fs_mepoitem>-no_more_gr IS NOT INITIAL ).
                  CONTINUE.
                ENDIF.

                SELECT COUNT(*) UP TO 1 ROWS
                  FROM mara
                 WHERE matnr EQ <fs_mepoitem>-material
                   AND mtart IN _gr_mtart.

                IF ( sy-subrc NE 0 ).
                  CONTINUE.
                ENDIF.

                ASSIGN COMPONENT <fs_component>-name OF STRUCTURE <fs_mepoitem> TO <fs_field>.
                <fs_field> = <fs_ifield>.

                CLEAR: ls_mepoitemx.
                READ TABLE et_mepoitemx WITH KEY po_item = <fs_mepoitem>-po_item ASSIGNING <fs_mepoitemx>.
                IF ( <fs_mepoitemx> IS ASSIGNED AND sy-subrc = 0 ).

                  ASSIGN COMPONENT <fs_component>-name OF STRUCTURE <fs_mepoitemx> TO <fs_xfield>.
                  <fs_xfield> = abap_true.
                  MODIFY et_mepoitemx FROM ls_mepoitemx.

                ELSE.

                  CLEAR: ls_mepoitemx.
                  ASSIGN COMPONENT <fs_component>-name OF STRUCTURE ls_mepoitemx TO <fs_xfield>.
                  ls_mepoitemx-po_item  = <fs_mepoitem>-po_item.
                  ls_mepoitemx-po_itemx = abap_true.
                  <fs_xfield> = abap_true.
                  APPEND ls_mepoitemx TO et_mepoitemx.

                ENDIF.

                CLEAR: ls_polog.
                ASSIGN COMPONENT <fs_component>-name OF STRUCTURE ls_polog TO <fs_pfield>.

                "LOG
                ls_polog-ebeln      = iv_ebeln.
                ls_polog-ebelp      = <fs_mepoitem>-po_item.
                <fs_pfield>         = <fs_field>.
                ls_polog-type       = 'S'.
                ls_polog-message    = text-002.
                me->create_log( is_po_log = ls_polog ).

              ENDLOOP.

            ENDIF.
          ENDIF.

      ENDCASE.

      UNASSIGN: <fs_mepoitem>, <fs_mepoitemx>, <fs_field>, <fs_ixfield>, <fs_ifield>, <fs_xfield>.

    ENDLOOP.

  ENDMETHOD.

  METHOD save_po_data.

    DATA: ls_polog  TYPE _ty_polog,
          ls_return TYPE bapiret2.

    DATA: lt_mepoitem   TYPE TABLE OF bapimepoitem,
          lt_mepoitemx  TYPE TABLE OF bapimepoitemx.

    MOVE-CORRESPONDING it_poitem  TO lt_mepoitem.
    MOVE-CORRESPONDING it_poitemx TO lt_mepoitemx.

    CALL FUNCTION 'BAPI_PO_CHANGE'
      EXPORTING
        purchaseorder                = is_poheader-po_number
        poheader                     = is_poheader
        poheaderx                    = is_poheaderx
        testrun                      = iv_testrun
        no_authority                 = abap_true
      TABLES
        return                       = et_return
        poitem                       = lt_mepoitem
        poitemx                      = lt_mepoitemx
*       POADDRDELIVERY               =
*       POSCHEDULE                   =
*       POSCHEDULEX                  =
*       POACCOUNT                    =
*       POACCOUNTPROFITSEGMENT       =
*       POACCOUNTX                   =
*       POCONDHEADER                 =
*       POCONDHEADERX                =
*       POCOND                       =
*       POCONDX                      =
*       POLIMITS                     =
*       POCONTRACTLIMITS             =
*       POSERVICES                   =
*       POSRVACCESSVALUES            =
*       POSERVICESTEXT               =
*       EXTENSIONIN                  =
*      extensionout                 = it_extensionout
*       POEXPIMPITEM                 =
*       POEXPIMPITEMX                =
*       POTEXTHEADER                 =
*       POTEXTITEM                   =
*       ALLVERSIONS                  =
*       POPARTNER                    =
*       POCOMPONENTS                 =
*       POCOMPONENTSX                =
*       POSHIPPING                   =
*       POSHIPPINGX                  =
*       POSHIPPINGEXP                =
*        POHISTORY                    =
*        POHISTORY_TOTALS             =
*       POCONFIRMATION               =
*       SERIALNUMBER                 =
*       SERIALNUMBERX                =
*       INVPLANHEADER                =
*       INVPLANHEADERX               =
*       INVPLANITEM                  =
*       INVPLANITEMX                 =
*       POHISTORY_MA                 =
              .

    READ TABLE et_return WITH KEY type = 'E' INTO ls_return.

    IF ( sy-subrc EQ 0 ).

      IF ( iv_testrun IS INITIAL ).
        ROLLBACK WORK.
      ENDIF.

      DELETE _gt_polog WHERE ebeln = iv_ebeln.
      CLEAR: ls_polog.
      ls_polog-ebeln = iv_ebeln.
      MOVE-CORRESPONDING ls_return TO ls_polog.
      me->create_log( is_po_log = ls_polog ).

    ELSE.

      IF ( iv_testrun IS INITIAL ).

        COMMIT WORK AND WAIT.

      ENDIF.

    ENDIF.

  ENDMETHOD.

  METHOD create_log.

    FIELD-SYMBOLS: <fs_ifield> TYPE any,
                   <fs_lfield> TYPE any,
                   <fs_polog> TYPE _ty_polog.

    DATA: lo_structdescr  TYPE REF TO cl_abap_structdescr,
          lt_components   TYPE abap_component_tab.

    lo_structdescr ?= cl_abap_structdescr=>describe_by_data( is_po_log ).
    lt_components = lo_structdescr->get_components( ).

    READ TABLE _gt_polog WITH KEY ebeln = is_po_log-ebeln
                                  ebelp = is_po_log-ebelp
                             ASSIGNING <fs_polog>.

    IF ( sy-subrc = 0 ). "Tenemos log 'ebeln-ebelp'

      "Modificamos log
      LOOP AT lt_components ASSIGNING FIELD-SYMBOL(<fs_components>).

        ASSIGN COMPONENT <fs_components>-name OF STRUCTURE <fs_polog> TO <fs_lfield>.
        ASSIGN COMPONENT <fs_components>-name OF STRUCTURE is_po_log TO <fs_ifield>.
        IF ( <fs_ifield> IS ASSIGNED AND <fs_lfield> IS ASSIGNED ).
          IF ( <fs_ifield> IS NOT INITIAL ).

            CASE <fs_components>-name.
              WHEN 'EBELN'.
                CONTINUE.
              WHEN 'EBELP'.
                CONTINUE.
              WHEN OTHERS.
                IF ( <fs_ifield> NE <fs_lfield> ).
                  <fs_lfield> = <fs_ifield>.
                ENDIF.
            ENDCASE.

          ENDIF.
        ENDIF.

      ENDLOOP.

      UNASSIGN: <fs_ifield>, <fs_lfield>, <fs_polog>.

    ELSE. "No tenemos log 'ebeln-ebelp'

      "Añadimos log
      APPEND is_po_log TO me->_gt_polog.

    ENDIF.

  ENDMETHOD.

  METHOD create_new_item_as_copy.

    FIELD-SYMBOLS: <fs_field> TYPE any,
                   <fs_fieldx> TYPE any.

    DATA: lo_structdescr  TYPE REF TO cl_abap_structdescr,
          lt_components   TYPE abap_component_tab,
          lv_max_position TYPE ekpo-ebelp.

    CHECK is_poitem IS NOT INITIAL.
    CHECK iv_ebeln IS NOT INITIAL.

    MOVE-CORRESPONDING is_poitem TO es_poitem.

    IF ( iv_new_ebelp IS INITIAL OR iv_new_ebelp LE 0 ).

      SELECT SINGLE MAX( ebelp )
        FROM ekpo
        INTO lv_max_position
       WHERE ebeln EQ iv_ebeln.

      IF ( sy-subrc NE 0 ).
        CLEAR: es_poitem, es_poitemx.
        EXIT.
      ENDIF.

      es_poitem-po_item   = lv_max_position + 10.
      es_poitemx-po_item  = lv_max_position + 10.

    ELSE.

      es_poitem-po_item   = iv_new_ebelp.
      es_poitemx-po_item  = iv_new_ebelp.

    ENDIF.


    es_poitemx-po_itemx = abap_true.

    lo_structdescr ?= cl_abap_structdescr=>describe_by_data( es_poitem ).
    lt_components = lo_structdescr->get_components( ).

    LOOP AT lt_components ASSIGNING FIELD-SYMBOL(<fs_components>).

      ASSIGN COMPONENT <fs_components>-name OF STRUCTURE es_poitem TO <fs_field>.
      IF ( <fs_field> IS ASSIGNED ).
        IF ( <fs_field> IS NOT INITIAL ).
          ASSIGN COMPONENT <fs_components>-name OF STRUCTURE es_poitemx TO <fs_fieldx>.
          IF ( <fs_fieldx> IS ASSIGNED ).
            IF ( <fs_fieldx> IS INITIAL ).
              <fs_fieldx> = abap_true.
            ENDIF.
          ENDIF.
        ENDIF.
      ENDIF.

    ENDLOOP.

  ENDMETHOD.

ENDCLASS.
