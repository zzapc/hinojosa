*&---------------------------------------------------------------------*
*& Report  ZRMM0024
*&
*&---------------------------------------------------------------------*
*&
*&
*&---------------------------------------------------------------------*
REPORT zrmm0024.

TABLES: kona.

*----------------------------------------------------------------------*
*       CLASS cl_event_handler DEFINITION
*----------------------------------------------------------------------*
CLASS lcl_handle_events DEFINITION.
  PUBLIC SECTION.

    CLASS-METHODS on_link_click                   " LINK_CLICK
      FOR EVENT if_salv_events_actions_table~link_click
                  OF cl_salv_events_table
      IMPORTING row column.
ENDCLASS.                    "cl_event_handler DEFINITION

TYPES: BEGIN OF ty_purchase_orders,
         knuma LIKE kona-knuma,
         boart LIKE kona-boart,
         bolif LIKE kona-bolif,
         datab LIKE kona-datab,
         datbi LIKE kona-datbi.
        INCLUDE STRUCTURE purchwere.
TYPES: END OF ty_purchase_orders.

DATA: t_kona            TYPE TABLE OF kona WITH HEADER LINE,
      t_vake            TYPE TABLE OF vake,
      t_konp            TYPE TABLE OF konp,
      t_arrang_val      TYPE TABLE OF arrang_val,
      t_enachweis       TYPE TABLE OF enachweis,
      t_purchwere       TYPE TABLE OF purchwere,
      t_purchase_orders TYPE TABLE OF ty_purchase_orders WITH HEADER LINE.

DATA: gr_table         TYPE REF TO cl_salv_table,
      layout_settings  TYPE REF TO cl_salv_layout,
      layout_key       TYPE salv_s_layout_key,
      columns          TYPE REF TO cl_salv_columns_table,
      column           TYPE REF TO cl_salv_column,
      column_list      TYPE REF TO cl_salv_column_list,
      not_found        TYPE REF TO cx_salv_not_found,
      functions        TYPE REF TO cl_salv_functions_list,
      display_settings TYPE REF TO cl_salv_display_settings,
      gr_events        TYPE REF TO lcl_handle_events,
      lr_events        TYPE REF TO cl_salv_events_table.

*----------------------------------------------------------------------*
*       CLASS cl_event_handler IMPLEMENTATION
*----------------------------------------------------------------------*
CLASS lcl_handle_events IMPLEMENTATION.
  METHOD on_link_click.
    READ TABLE t_purchase_orders INTO DATA(purchase) INDEX row.
    SET PARAMETER ID 'BES' FIELD purchase-ebeln.
    CALL TRANSACTION 'ME23N' AND SKIP FIRST SCREEN.
  ENDMETHOD.                    "on_link_click
ENDCLASS.                    "cl_event_handler IMPLEMENTATION

SELECTION-SCREEN BEGIN OF BLOCK b0 WITH FRAME TITLE text-s00.
SELECT-OPTIONS: s_knuma FOR kona-knuma OBLIGATORY.
SELECTION-SCREEN END OF BLOCK b0.

START-OF-SELECTION.

  SELECT * FROM kona
    INTO TABLE t_kona
    WHERE knuma IN s_knuma.

  LOOP AT t_kona.
    REFRESH: t_vake, t_konp, t_purchwere, t_enachweis.

    CALL FUNCTION 'MM_ARRANG_CHECK_PUR_ORD_ARRANG'
      EXPORTING
        i_arrangement              = t_kona
        i_check_we                 = space
        i_check_re                 = space
        i_read_kona                = 'X'
      TABLES
        t_variable_key             = t_vake
        t_condition_record         = t_konp
        t_purchase_orders          = t_purchwere
        t_index_purchase           = t_enachweis
      EXCEPTIONS
        condition_record_not_found = 1
        open_document_exist        = 2
        arrangement_not_found      = 3
        OTHERS                     = 4.

    IF sy-subrc EQ 0.
      LOOP AT t_purchwere INTO DATA(purchwere).
        CLEAR t_purchase_orders.
        MOVE-CORRESPONDING t_kona TO t_purchase_orders.
        MOVE-CORRESPONDING purchwere TO t_purchase_orders.
        APPEND t_purchase_orders.
      ENDLOOP.
    ENDIF.
  ENDLOOP.

END-OF-SELECTION.
* Mostrar ALV
* Instanciar ALV desde tabla interna
  cl_salv_table=>factory( IMPORTING r_salv_table = gr_table CHANGING t_table = t_purchase_orders[] ).

* Disposición ALV
  layout_settings = gr_table->get_layout( ).
  layout_key-report = sy-repid.
  layout_settings->set_key( layout_key ).
  layout_settings->set_save_restriction( if_salv_c_layout=>restrict_none ).

* Optimizar columnas
  columns = gr_table->get_columns( ).
  columns->set_optimize( ).

  lr_events = gr_table->get_event( ).
  SET HANDLER lcl_handle_events=>on_link_click FOR lr_events.

** Modificar/ocultar columnas
  TRY.
      column_list ?= columns->get_column( 'EBELN' ).
      column_list->set_cell_type( if_salv_c_cell_type=>hotspot ).
    CATCH cx_salv_not_found INTO not_found.
  ENDTRY.

* Funciones ALV activar
  functions = gr_table->get_functions( ).
  functions->set_all( ).

* Opciones generales ALV
  display_settings = gr_table->get_display_settings( ).
  display_settings->set_striped_pattern( if_salv_c_bool_sap=>true ).
  display_settings->set_list_header( 'Resultado' ).

  gr_table->display( ).
