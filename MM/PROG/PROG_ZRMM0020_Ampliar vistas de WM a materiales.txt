*&---------------------------------------------------------------------*
*& Report  ZRMM0020
*&
*&---------------------------------------------------------------------*
*& Autor: Carlos Vivó Jiménez
*& Fecha: 05.04.2019
*& Proyecto: Implantación WM Xàtiva - Mejoras
*&
*& Comentarios: Programa para la creación autom. de las vistas de WM
*&    para las materias primas. Es una tarea mecánica que puede auto-
*&    matizarse.
*&
*&---------------------------------------------------------------------*
REPORT zrmm0020.

INCLUDE zrmm0020_top.

SELECTION-SCREEN BEGIN OF BLOCK 0001. " pantalla inicial
PARAMETERS: p_werks LIKE marc-werks OBLIGATORY,
            p_lgnum LIKE mlgn-lgnum OBLIGATORY.
SELECT-OPTIONS: s_mtart FOR mara-mtart,
                s_matnr FOR mara-matnr.
SELECTION-SCREEN END OF BLOCK 0001.

START-OF-SELECTION.

  " para diferenciar por tipos de material
  so_name-sign = 'I'.
  so_name-option = 'CP'.
  so_name-low = 'ZRMM0017_MLGN*'.
  APPEND so_name.

  SELECT * FROM tvarvc
    WHERE name IN @so_name
    INTO TABLE @DATA(t_tvarvc_mlgn).

  SELECT * FROM marc
    INNER JOIN mara
    ON marc~matnr EQ mara~matnr
    INTO CORRESPONDING FIELDS OF TABLE t_mara
    WHERE mara~mtart IN s_mtart
      AND marc~matnr IN s_matnr
      AND marc~werks EQ p_werks.

  SELECT * FROM mlgn
    INTO TABLE t_mlgn
    FOR ALL ENTRIES IN t_mara
    WHERE matnr EQ t_mara-matnr
      AND lgnum EQ p_lgnum.

  LOOP AT t_mlgn. " quitamos los que ya están creados
    DELETE t_mara WHERE matnr EQ t_mlgn-matnr.
  ENDLOOP.

END-OF-SELECTION.

  LOOP AT t_mara.
    CLEAR: headdata, return, field, fieldx, text, wmdata, wmdatax.

    CONCATENATE '*' t_mara-mtart INTO field.

    headdata-material = t_mara-matnr.

    LOOP AT t_tvarvc_mlgn INTO tvarvc WHERE name CP field.
      wmdata-whse_no = p_lgnum.
      wmdatax-whse_no = p_lgnum.

      CONCATENATE 'wmdata-' tvarvc-low INTO field.
      ASSIGN (field) TO <value>.
      IF sy-subrc EQ 0.
        WRITE tvarvc-high TO <value>.
      ENDIF.

      CONCATENATE 'wmdatax-' tvarvc-low INTO fieldx.
      ASSIGN (fieldx) TO <value>.
      IF sy-subrc EQ 0.
        WRITE 'X' TO <value>.
      ENDIF.
    ENDLOOP.

    IF wmdata IS NOT INITIAL.
      CALL FUNCTION 'BAPI_MATERIAL_SAVEDATA'
        EXPORTING
          headdata             = headdata
          warehousenumberdata  = wmdata
          warehousenumberdatax = wmdatax
        IMPORTING
          return               = return.

      IF return-type NE 'E' AND sy-subrc EQ 0.
        WRITE: / t_mara-matnr, ' modificado'.
        COMMIT WORK AND WAIT.
      ELSE.
        WRITE: / '                        ', t_mara-matnr, ' error al modificar'.
      ENDIF.
    ENDIF.
  ENDLOOP.
