REPORT zrmm0029 .
*======================================================================*
*  The report RMMD06NEW provides an EXAMPLE for a user-specific
*  entrance to the collective display MD06/MS06 of mrp lists
*  for one or more plants.
*  The new report includes a new selection function module with
*  ranges.
*  REMARK:  The ranges should be set in a suitable way to avoid
*           performance problems because of a selection range, that
*           is too large !!!
*  -----------------------------------------------------------
*  INPUT :  Internal Table MDMWX has to be filled with
*           material / plant combination of selected materials
*  -----------------------------------------------------------
*  OUTPUT:  MD06 overview of these materials to be processed
*           like in MD06
*
*======================================================================*
*ENHANCEMENT-POINT RMMD06NEW_G4 SPOTS ES_RMMD06NEW STATIC .

*ENHANCEMENT-POINT RMMD06NEW_G5 SPOTS ES_RMMD06NEW.

*ENHANCEMENT-POINT RMMD06NEW_G6 SPOTS ES_RMMD06NEW STATIC .

*ENHANCEMENT-POINT RMMD06NEW_G7 SPOTS ES_RMMD06NEW.

*--> Type-Pools
TYPE-POOLS: m61x.

*--> Data definitions
CONSTANTS:  memo_id_md06(10) TYPE c VALUE 'START_MD06'.

TABLES:     rm61r, mdkp, mt61d, cr61t, cm61x, t450n,
            t000md, am61r, mdlv.

*--> Table of selected materials
DATA: BEGIN OF mdmwx OCCURS 0.
        INCLUDE STRUCTURE mdmw.        "material / plant
DATA: END OF mdmwx.
*--> Table of selected materials
DATA: BEGIN OF mdkex OCCURS 0.
        INCLUDE STRUCTURE mdke.        "material / plant
DATA: END OF mdkex.

*--> Hilfsfelder für die Sammelanzeige aktuelle BBLs
DATA: BEGIN OF matov,
        werks    LIKE rm61r-werks,        "Werk aus dem Einstiegsbild
        matnr    LIKE rm61r-matnr,        "Material aus dem Einstiegsbild
        berid    LIKE rm61r-berid,    "Dispobereich aus dem Einstiegsbild
        plscn    LIKE rm61r-plscn,        "Szenario aus dem Einstiegsbild
        initf(1) TYPE c,           "Initialisierung wurde durchgeführt
        disvi(1) TYPE c,               "Disponenten-Feinsel. sichtbar
        lifvi(1) TYPE c,               "Lieferanten-Feinsel. sichtbar
        flivi(1) TYPE c,               "Fert.Linien-Feinsel. sichtbar
        plavi(1) TYPE c,               "Planungsabschnittsel. sichtbar
        seqvi(1) TYPE c,               "Sequenzplanungs-Details sichtbar
        modus(1) TYPE c,               "Materialdaten Ermittelungsmodus
        error(1) TYPE c,               "Fehler beim Ermitteln ABBL
        dsper(1) TYPE c,           "Ausgabe von Fehlern: 'X', wenn ja
        refre(1) TYPE c,               "Auffrischmodus für die ABBL
        refpt(1) TYPE c,               "Auffrischmodus Planungstableau
        detli(1) TYPE c,           "Detail Linie (REMPT)      "99a_rm
        detdi(1) TYPE c,           "Detail Disponent (REMPT)  "99a_rm
        ndisp(1) TYPE c,           "Uebersicht ueberspringen  "RE 4.6A
        newrb(1) TYPE c,           "Wert von neu gesetzem Radiobutton SA
        oldrb(1) TYPE c,               "Wert von altem Radiobutton SA
      END OF matov.

DATA: lv_valid TYPE boolean.

DATA gr_table TYPE REF TO cl_salv_table.
DATA layout_settings TYPE REF TO cl_salv_layout.
DATA layout_key      TYPE salv_s_layout_key.
DATA columns TYPE REF TO cl_salv_columns_table.
DATA column  TYPE REF TO cl_salv_column.
DATA not_found TYPE REF TO cx_salv_not_found.
DATA functions TYPE REF TO cl_salv_functions_list.
DATA display_settings TYPE REF TO cl_salv_display_settings.

*----------------------------------------------------------------------*
*   Includes for MRP Area and Constants
*----------------------------------------------------------------------*
INCLUDE mm61xdbr.
INCLUDE mm61xval.


************** Start of customer-specific changes *********************
*----------------------------------------------------------
* Selection Screen
*
* REMARK: Please delete unnecessary parameters !!
*----------------------------------------------------------
*--> Planning Scenario for LTP
SELECTION-SCREEN BEGIN OF BLOCK sel_psc WITH FRAME TITLE text-f00.
PARAMETERS: pv_plscn LIKE plpt-plscn.
SELECTION-SCREEN END OF BLOCK sel_psc.

*--> Organization Units
SELECTION-SCREEN BEGIN OF BLOCK sel_wrk WITH FRAME TITLE text-f01.
SELECT-OPTIONS: so_werks FOR rm61r-werks MEMORY ID wrk,
                so_berid FOR rm61r-berid MODIF ID dbr.
SELECTION-SCREEN END OF BLOCK sel_wrk.

*--> Selection Paths
SELECTION-SCREEN BEGIN OF BLOCK sel_path WITH FRAME TITLE text-f02.
SELECT-OPTIONS: so_dispo FOR rm61r-dispo,
                so_lifnr FOR rm61r-lifnr,
                so_line1 FOR rm61r-line1,
                so_prgrp FOR rm61r-prgrp.
SELECTION-SCREEN END OF BLOCK sel_path.

*--> Parameters regarding Material Master
SELECTION-SCREEN BEGIN OF BLOCK sel_mat WITH FRAME TITLE text-f03.
SELECT-OPTIONS: so_matnr FOR rm61r-matnr,
                so_disgr FOR mt61d-disgr,
                so_beskz FOR rm61r-beskz,
                so_sobsl FOR rm61r-sobsl,
                so_dismm FOR rm61r-dismm,
                so_mtart FOR rm61r-mtart,
                so_maabc FOR rm61r-maabc,
                so_mmsta FOR rm61r-mmsta.
PARAMETERS:     pv_nolvm TYPE md_nolvm AS CHECKBOX.
SELECTION-SCREEN END OF BLOCK sel_mat.

*--> Parameters regarding days' supply limits
SELECTION-SCREEN BEGIN OF BLOCK sel_krit WITH FRAME TITLE text-f04.
SELECT-OPTIONS: so_dsdat FOR mdkp-dsdat,
                so_beada FOR mdkp-beada,
                so_berw1 FOR mdkp-berw1,
                so_berw2 FOR mdkp-berw2,
                so_berw4 FOR mdkp-berw4.
SELECTION-SCREEN END OF BLOCK sel_krit.

**--> Parameters regarding Planning File Entries
*SELECTION-SCREEN BEGIN OF BLOCK sel_pfe WITH FRAME TITLE text-f06.
*PARAMETERS:     pv_algsa LIKE rm61r-algsa RADIOBUTTON GROUP pfe DEFAULT 'X',
*                pv_gsaen LIKE rm61r-gsaen RADIOBUTTON GROUP pfe,
*                pv_nogsa LIKE rm61r-nogsa RADIOBUTTON GROUP pfe.
*SELECTION-SCREEN END OF BLOCK sel_pfe.

*--> Parameters regarding Processing Indicator
SELECTION-SCREEN BEGIN OF BLOCK sel_prind WITH FRAME TITLE text-f08.
PARAMETERS:     pv_adbkz LIKE rm61r-adbkz RADIOBUTTON GROUP pri DEFAULT 'X',
                pv_bdbkz LIKE rm61r-bdbkz RADIOBUTTON GROUP pri,
                pv_nbekz LIKE rm61r-nbekz RADIOBUTTON GROUP pri,
                pv_naukz LIKE rm61r-naukz AS CHECKBOX.
SELECTION-SCREEN END OF BLOCK sel_prind.

*--> Parameters regarding Exception Groups
SELECTION-SCREEN BEGIN OF BLOCK sel_exgrp WITH FRAME TITLE text-f09.
PARAMETERS:     pv_slkz1 LIKE rm61r-slkz1 AS CHECKBOX,
                pv_slkz2 LIKE rm61r-slkz2 AS CHECKBOX,
                pv_slkz3 LIKE rm61r-slkz3 AS CHECKBOX,
                pv_slkz4 LIKE rm61r-slkz4 AS CHECKBOX,
                pv_slkz5 LIKE rm61r-slkz5 AS CHECKBOX,
                pv_slkz6 LIKE rm61r-slkz6 AS CHECKBOX,
                pv_slkz7 LIKE rm61r-slkz7 AS CHECKBOX,
                pv_slkz8 LIKE rm61r-slkz8 AS CHECKBOX.
SELECTION-SCREEN END OF BLOCK sel_exgrp.

*--> Parameters regarding filtering
SELECTION-SCREEN BEGIN OF BLOCK sel_fil WITH FRAME TITLE text-f07.
PARAMETERS:     pv_dfilt LIKE rm61r-dfilt DEFAULT 'X',
                pv_filbz LIKE rm61r-filbz.
SELECTION-SCREEN END OF BLOCK sel_fil.

*---------------------------------------------------------
* Other necessary Data Declarations
*---------------------------------------------------------
DATA: range_werks TYPE md_range_t_werks,
      range_berid TYPE md_range_t_berid,
      range_dispo TYPE md_range_t_dispo,
      range_lifnr TYPE md_range_t_lifnr,
      range_line1 TYPE md_range_t_line1,
      range_prgrp TYPE md_range_t_prgrp,
      range_matnr TYPE md_range_t_matnr,
      range_disgr TYPE md_range_t_disgr,
      range_mtart TYPE md_range_t_mtart,
      range_beskz TYPE md_range_t_beskz,
      range_sobsl TYPE md_range_t_sobsl,
      range_dismm TYPE md_range_t_dismm,
      range_maabc TYPE md_range_t_maabc,
      range_mmsta TYPE md_range_t_mmsta.
DATA: range_berw1 TYPE md_range_t_berw1,
      range_berw2 TYPE md_range_t_berw2,
      range_berw4 TYPE md_range_t_berw4.

DATA: lv_dspkz LIKE rm61r-dspkz,
      lv_liekz LIKE rm61r-liekz,
      lv_flikz LIKE rm61r-flikz,
      lv_prgkz LIKE rm61r-prgkz,
      lv_tcode LIKE sy-tcode.
DATA: lv_nolvm TYPE md_nolvm.

*-------------------------------------------------------
* Consistency Checks: Plant is mandatory !!
*-------------------------------------------------------
AT SELECTION-SCREEN ON so_werks.
  IF so_werks[] IS INITIAL.
    MESSAGE e453(61).
  ENDIF.

*-------------------------------------------------------
* Activate or Deactivate MRP Area Input Fields
*-------------------------------------------------------
AT SELECTION-SCREEN OUTPUT.
*--> Falls Dispobereich nicht aktiv ist DB Feld ausblenden
  PERFORM read_t000md.
  IF t000md-disfg IS INITIAL.     "Dispobereich nicht aktiv
    LOOP AT SCREEN.
      IF screen-group1 = 'DBR'.
        screen-invisible = '1'.
        screen-input = '0'.
        MODIFY SCREEN.
      ENDIF.
    ENDLOOP.
  ENDIF.

*---------------------------------------------------
*--> Transfer of Parameters (Ranges necessary
*    because of header lines)
*---------------------------------------------------
START-OF-SELECTION.
  range_werks[] = so_werks[].
  range_berid[] = so_berid[].
  range_dispo[] = so_dispo[].
  range_lifnr[] = so_lifnr[].
  range_line1[] = so_line1[].
  range_prgrp[] = so_prgrp[].
  range_matnr[] = so_matnr[].
  range_disgr[] = so_disgr[].
  range_beskz[] = so_beskz[].
  range_sobsl[] = so_sobsl[].
  range_dismm[] = so_dismm[].
  range_mtart[] = so_mtart[].
  range_maabc[] = so_maabc[].
  range_mmsta[] = so_mmsta[].
  range_berw1[] = so_berw1[].
  range_berw2[] = so_berw2[].
  range_berw4[] = so_berw4[].

  rm61r-dfilt   = pv_dfilt.
  rm61r-filbz   = pv_filbz.
  rm61r-adbkz   = pv_adbkz.
  rm61r-bdbkz   = pv_bdbkz.
  rm61r-nbekz   = pv_nbekz.
  rm61r-naukz   = pv_naukz.
  rm61r-plscn   = pv_plscn.
  rm61r-dsdav   = so_dsdat-low.
  rm61r-dsdab   = so_dsdat-high.
  rm61r-beadv   = so_beada-low.
  rm61r-beadb   = so_beada-high.
  rm61r-slkz1   = pv_slkz1.
  rm61r-slkz2   = pv_slkz2.
  rm61r-slkz3   = pv_slkz3.
  rm61r-slkz4   = pv_slkz4.
  rm61r-slkz5   = pv_slkz5.
  rm61r-slkz6   = pv_slkz6.
  rm61r-slkz7   = pv_slkz7.
  rm61r-slkz8   = pv_slkz8.
  lv_nolvm      = pv_nolvm.

*-------------------------------------------------------
*  Selection of Materials by Function Module
*-------------------------------------------------------
*--> Determine Selection Path
  IF     NOT range_prgrp IS INITIAL.
    lv_prgkz = 'X'.
  ELSEIF NOT range_line1 IS INITIAL.
    lv_flikz = 'X'.
  ELSEIF NOT range_lifnr IS INITIAL.
    lv_liekz = 'X'.
  ELSEIF NOT range_dispo IS INITIAL.
    lv_dspkz = 'X'.
  ELSE.
    lv_dspkz = 'X'.
  ENDIF.

*--> read T450N for authority-check 'M_MTDI_ORG' "Note 1558474
  IF rm61r-plscn IS INITIAL.
    lv_tcode = 'MDC6'.
  ELSE.
    lv_tcode = 'MSC6'.
  ENDIF.
  SELECT SINGLE * FROM  t450n
                  WHERE tcode EQ lv_tcode.

*--> New Selection Function Module
  CALL FUNCTION 'MD_SELECTION_MAT_LIST_RANGE'
    EXPORTING
      iv_plscn      = rm61r-plscn
      ir_werks      = range_werks
      ir_berid      = range_berid
      iv_dspkz      = lv_dspkz
      ir_dispo      = range_dispo
      iv_liekz      = lv_liekz
      ir_lifnr      = range_lifnr
      iv_flikz      = lv_flikz
      ir_line1      = range_line1
      iv_prgkz      = lv_prgkz
      ir_prgrp      = range_prgrp
      ir_matnr      = range_matnr
      ir_disgr      = range_disgr
      ir_beskz      = range_beskz
      ir_dismm      = range_dismm
      ir_mtart      = range_mtart
      ir_sobsl      = range_sobsl
      ir_maabc      = range_maabc
      ir_mmsta      = range_mmsta
      iv_gsaen      = rm61r-gsaen
      iv_nogsa      = rm61r-nogsa
      iv_nolvm      = lv_nolvm
      iv_mdakt      = t450n-mdakt
    TABLES
      mdmwx         = mdmwx
    EXCEPTIONS
      error_message = 01.

  IF sy-subrc NE 0.
    MESSAGE ID     sy-msgid
            TYPE   sy-msgty
            NUMBER sy-msgno
            WITH   sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

*-----------------------------------------------
* Further Selection Criteria to restrict
* MDMWX-list of materials from above
*-----------------------------------------------
*
*

************** Ende of customer-specific changes **********************

END-OF-SELECTION.
*------------------------------------------------
* Check: Did the Selection find Materials?
*------------------------------------------------
  IF mdmwx[] IS INITIAL.
    MESSAGE s085(61).
    EXIT.
  ELSE.
    DATA: range_werks_line TYPE LINE OF md_range_t_werks.
*--> relevant plant
    CLEAR range_werks[].
    CLEAR range_werks_line.
    range_werks_line-option = 'I'.
    range_werks_line-sign   = 'EQ'.
    range_werks_line-low    = rm61r-werks.
    APPEND range_werks_line TO range_werks.

*--> Einstellungen aus T463
    DATA: afilt TYPE afilt.

    SELECT SINGLE * FROM t463
    WHERE afibz EQ @pv_filbz
      INTO @DATA(t463).
    IF sy-subrc EQ 0.
      MOVE-CORRESPONDING t463 TO afilt.
    ELSE.
      CLEAR afilt.
*   message xxx
    ENDIF.


*--> Building the Material Overview Table MDKEX for MRP Lists
    CALL FUNCTION 'MD_SELECTION_MDKP_RANGE'
      EXPORTING
        iv_plscn = rm61r-plscn
        ir_werks = range_werks
*       IR_BERID =
        iv_bdbkz = rm61r-bdbkz
        iv_nbekz = rm61r-nbekz
        iv_naukz = rm61r-naukz
        iv_slkz1 = rm61r-slkz1
        iv_slkz2 = rm61r-slkz2
        iv_slkz3 = rm61r-slkz3
        iv_slkz4 = rm61r-slkz4
        iv_slkz5 = rm61r-slkz5
        iv_slkz6 = rm61r-slkz6
        iv_slkz7 = rm61r-slkz7
        iv_slkz8 = rm61r-slkz8
        iv_dsdav = rm61r-dsdav
        iv_dsdab = rm61r-dsdab
        iv_beadv = rm61r-beadv
        iv_beadb = rm61r-beadb
        ir_berw1 = range_berw1
        ir_berw2 = range_berw2
        ir_berw4 = range_berw4
        iv_t450n = t450n             "note 1882696
        eafilt   = afilt            "note 1953755
      TABLES
        mdmwx    = mdmwx
        mdkex    = mdkex.

*--> Check if Selection Table is empty
    IF mdkex[] IS INITIAL.
      MESSAGE s085(61).
    ENDIF.

* instanciar alv desde tabla interna
    cl_salv_table=>factory( IMPORTING r_salv_table = gr_table CHANGING t_table = mdkex[] ).

* Disposición ALV
    layout_settings = gr_table->get_layout( ).
    layout_key-report = sy-repid.
    layout_settings->set_key( layout_key ).
    layout_settings->set_default( abap_true ).
    layout_settings->set_save_restriction( if_salv_c_layout=>restrict_none ).

* Optimizar columnas
    columns = gr_table->get_columns( ).
    columns->set_optimize( ).

* Modificar/ocultar columnas
    TRY.
        column = columns->get_column( 'MANDT' ).
        column->set_visible( if_salv_c_bool_sap=>false ). "
      CATCH cx_salv_not_found INTO not_found.
    ENDTRY.
    TRY.
        column ?= columns->get_column( 'COL_CHAR1_CONT' ).
        column->set_short_text( 'Pronóstico' ).
        column->set_medium_text( 'Pronóstico' ).
        column->set_long_text( 'Pronóstico' ).
      CATCH cx_salv_not_found.                          "#EC NO_HANDLER
    ENDTRY.
    TRY.
        column ?= columns->get_column( 'COL_DEC1_CONT' ).
        column->set_short_text( 'Sol.' ).
        column->set_medium_text( 'Solicitudes' ).
        column->set_long_text( 'Solicitudes' ).
      CATCH cx_salv_not_found.                          "#EC NO_HANDLER
    ENDTRY.
    TRY.
        column ?= columns->get_column( 'COL_DEC2_CONT' ).
        column->set_short_text( 'Pedidos' ).
        column->set_medium_text( 'Pedidos' ).
        column->set_long_text( 'Pedidos' ).
      CATCH cx_salv_not_found.                          "#EC NO_HANDLER
    ENDTRY.

* Funciones ALV activar
    functions = gr_table->get_functions( ).
    functions->set_all( ).

* Opciones generales ALV
    display_settings = gr_table->get_display_settings( ).
    display_settings->set_striped_pattern( if_salv_c_bool_sap=>true ).
    display_settings->set_list_header( 'Listas MRP seleccionadas' ).

    gr_table->display( ).

  ENDIF.
