FUNCTION zmm_cambiar_pronost_calendario.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     REFERENCE(I_PROGDAT) LIKE  PROP-PRDAT
*"     REFERENCE(I_WERKS) TYPE  WERKS_D
*"  TABLES
*"      T_PROGNOSE STRUCTURE  IPROG
*"----------------------------------------------------------------------
  DATA: t_period TYPE TABLE OF ztmm0028,
        period   TYPE ztmm0028,
        semana   TYPE scal-week,
        index    TYPE sytabix,
        date     TYPE prop-prdat.
  " cvivo - 61615 Cambiar valores de consumo pronosticado en base a calendario

  " --------------------------------------------------------------------------------------------------------------------------------------
  " Primero actualizamos la tabla
  " 1. Borramos entradas antiguas
  DELETE FROM ztmm0028 WHERE fin LT sy-datum.

  " 2. Vemos los centros relevantes
  SELECT * FROM tvarvc
    WHERE name EQ 'ZMM_CENTRO_PRONOSTICO'
    INTO TABLE @DATA(t_centros).

  " 3. Generamos entradas hasta 1 año vista
  SELECT * FROM ztmm0028
    INTO TABLE t_period.

  CALL FUNCTION 'GET_WEEK_INFO_BASED_ON_DATE'
    EXPORTING
      date   = sy-datum
    IMPORTING
      week   = semana
      monday = period-ini
      sunday = period-fin.

  period-semana = semana+4.
  period-parte = 1.

  DO 52 TIMES. " por ejemplo, la cuestión es tener un periodo largo cubierto
    READ TABLE t_period WITH KEY ini = period-ini TRANSPORTING NO FIELDS.

    IF sy-subrc NE 0.
      LOOP AT t_centros INTO DATA(centro).
        period-werks = centro-low.
        INSERT ztmm0028 FROM period.
      ENDLOOP.
    ENDIF.

    ADD 7 TO: period-ini, period-fin. " validamos siguiente semana

    CALL FUNCTION 'GET_WEEK_INFO_BASED_ON_DATE'
      EXPORTING
        date = period-ini
      IMPORTING
        week = semana.

    period-semana = semana+4.
  ENDDO.

  " --------------------------------------------------------------------------------------------------------------------------------------
  " Ahora aplicamos los factores de la tabla
  SELECT * FROM ztmm0028
   INTO TABLE t_period
    WHERE werks EQ i_werks.

  IF sy-subrc EQ 0.
    CLEAR index.
    date = i_progdat. " empezamos por este periodo

    LOOP AT t_prognose ASSIGNING FIELD-SYMBOL(<prog>) WHERE eflag EQ space. " recorremos uno a uno los valores de pronóstico, eflag no porque esos son expost

      READ TABLE t_period INTO period WITH KEY ini = date.
      IF sy-subrc EQ 0.
        <prog>-prw01 = round( val = ( <prog>-prw01 * period-parte ) dec = 0 ). " aplicamos el factor
      ENDIF.

      ADD 7 TO date. " siguiente semana
    ENDLOOP.
  ENDIF.
ENDFUNCTION.
