*----------------------------------------------------------------------*
*   INCLUDE J_3RMOBVEDF02                                              *
*----------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  compare_selection_parameters
*&---------------------------------------------------------------------*
form compare_selection_parameters
    changing p_xnot_calc type type_1c.

  clear p_xnot_calc.

  call function 'RS_REFRESH_FROM_SELECTOPTIONS'
    exporting
      curr_report     = g_repid
    tables
      selection_table = seltab_new
    exceptions
      not_found       = 1
      no_report       = 2
      others          = 3.
  if sy-subrc is initial.
    delete seltab_new
        where selname = 'PA_VARNT'
           or selname = 'PA_DOCS'
           or selname = 'g_bkgrd'.                          "#EC NOTEXT
    if seltab_new[] = seltab_cur[].
      p_xnot_calc = gc_true.
    endif.
  endif.

endform.                    " compare_selection_parameters
*&---------------------------------------------------------------------*
*&      Form  restore_from_db_month
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_SO_WERKS_LOW  text
*      -->P_PA_BUKRS  text
*      -->P_SO_SAKNR_LOW  text
*      -->P_SO_BUDAT_LOW  text
*      -->P_PA_REVER  text
*      -->P_PA_ADCST  text
*      <--P_G_NOT_CALC  text
*----------------------------------------------------------------------*
form restore_from_db_month
    tables
          p_listmat type tt_listmat
          p_docmat type tt_docmat
    using p_werks type werks_d
          p_bukrs type bukrs
          p_hkont type hkont
          p_budat type budat
          p_rever type j_3rmoptved3
          p_adcst type j_3rmoptved5
    changing p_not_calc type type_1c.

  data: l_ndxkey like indx-srtfd,
        l_mode(1) type c.                                      "1430397 start

  import g_erase from memory id g_key_memory.

  perform fill_indxkey
    using p_werks
          p_bukrs
          p_hkont
          p_budat
          p_rever
          p_adcst
    changing l_ndxkey
             l_mode.                                           "1430397 end

  if not l_ndxkey is initial.
    import p_listmat p_docmat
        from database indx(im) id l_ndxkey.
    if sy-subrc is initial or not g_erase is initial.          "1430397
      p_not_calc = gc_true.
    endif.                                                     "1430397
  endif.

endform.                    " restore_from_db_month
*&---------------------------------------------------------------------*
*&      Form  restore_report_from_database
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_LISTMAT  text
*      -->P_DOCMAT  text
*      <--P_G_NOT_CALC  text
*----------------------------------------------------------------------*
form restore_report_from_database
    tables p_listmat type tt_listmat
           p_docmat type tt_docmat
    changing p_not_calc type type_1c.

  import p_listmat p_docmat gd_made gt_made
      from database indx(im) id g_ndxkeyalc.
  if not sy-subrc is initial.
    clear p_not_calc.
  endif.

endform.                    " restore_report_from_database
*&---------------------------------------------------------------------*
*&      Form  save_to_db_month
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_LISTMAT  text
*      -->P_DOCMAT  text
*      -->P_SO_WERKS_LOW  text
*      -->P_PA_BUKRS  text
*      -->P_SO_SAKNR_LOW  text
*      -->P_SO_BUDAT_LOW  text
*      -->P_PA_REVER  text
*      -->P_PA_ADCST  text
*----------------------------------------------------------------------*
form save_to_db_month
    tables p_listmat type tt_listmat
           p_docmat type tt_docmat
    using p_werks type werks_d
          p_bukrs type bukrs
          p_hkont type hkont
          p_budat type budat
          p_rever type j_3rmoptved3
          p_adcst type j_3rmoptved5.

  data: l_ndxkey    type indx_srtfd,
        l_relid     type indx_relid,
        l_mode(1)   type c.                                    "1430397

  import g_erase from memory id g_key_memory.                  "1163961

  perform fill_indxkey                                         "1430397 start
    using p_werks
          p_bukrs
          p_hkont
          p_budat
          p_rever
          p_adcst
    changing l_ndxkey
             l_mode.                                           "1430397 end

  if not l_ndxkey is initial.
    select single relid
      into l_relid
      from indx
      where relid = 'IM'
        and srtfd = l_ndxkey.                               "#EC *

    if sy-subrc is initial.
      if not g_erase is initial.
        delete from indx
            where relid = 'IM'
              and srtfd = l_ndxkey.
        commit work and wait.                                  "1163961
      else.
        exit.
      endif.
    endif.

    export p_listmat p_docmat
        to database indx(im) id l_ndxkey.
  endif.

endform.                    " save_to_db_month

*&---------------------------------------------------------------------*
*&      Form  SAVE_CURRENT_RESULT_DATABASE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form save_current_result_database
    tables p_listmat type tt_listmat
           p_docmat type tt_docmat.

  indx-aedat = sy-datum.
  indx-usera = sy-uname.
* Export data.
* free database space
  delete from indx
      where relid = 'IM'
        and srtfd = g_ndxkeyalc.

  export p_listmat p_docmat gd_made gt_made to
         database indx(im) id g_ndxkeyalc.

endform.                    " SAVE_CURRENT_RESULT_DATABASE
*&---------------------------------------------------------------------*
*&      Form  save_selection_parameters
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form save_selection_parameters.

  refresh seltab_cur.
  call function 'RS_REFRESH_FROM_SELECTOPTIONS'
    exporting
      curr_report     = g_repid
    tables
      selection_table = seltab_cur
    exceptions
      not_found       = 1
      no_report       = 2
      others          = 3.
  if sy-subrc is initial.
    delete seltab_cur
          where selname = 'PA_VARNT'
             or selname = 'PA_DOCS'
             or selname = 'g_bkgrd'.                        "#EC NOTEXT
    export seltab_cur to memory id g_repid.
  endif.

endform.                    " save_selection_parameters
*&---------------------------------------------------------------------*
*&      Form  fill_indxkey
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form fill_indxkey                                              "1430397 start
    using p_werks type werks_d
          p_bukrs type bukrs
          p_hkont type hkont
          p_budat type budat
          p_rever type j_3rmoptved3
          p_adcst type j_3rmoptved5
    changing p_ndxkey type indx_srtfd
             p_mode   type c.

  data: l_date(8)   type c,
        l_rever(1)  type c,
        l_adcst(1)  type c,
        l_keyac(1)  type c,
        l_key        type i.

  l_date = p_budat.

  if p_rever is initial.
    l_rever = gc_symbol_0.
  else.
    l_rever = gc_symbol_1.
    l_key   = 1.
  endif.

  if p_adcst is initial.
    l_adcst = gc_symbol_0.
  else.
    l_adcst = gc_symbol_1.
    l_key   = l_key + 2.
  endif.

  write l_key to l_keyac.

  if not p_werks is initial
      and p_bukrs is initial and p_hkont is initial.
    concatenate
        gc_keywerk p_werks l_rever l_adcst l_date(6)
        into p_ndxkey.
    p_mode = 1.                "on plant level
  elseif p_werks is initial
      and not p_bukrs is initial and not p_hkont is initial.
    concatenate
        gc_keybukr p_bukrs p_hkont l_keyac l_date(6)
        into p_ndxkey.
    p_mode = 2.                "on company code and G/L account level
  else.
    clear p_ndxkey.
    p_mode = 0.                "incorrect mode
  endif.

endform.                    " fill_indxkey                     "1430397 end
