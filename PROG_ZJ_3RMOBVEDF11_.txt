*----------------------------------------------------------------------*
*   INCLUDE J_3RMOBVEDF11                                              *
*&---------------------------------------------------------------------*
*&      Form  reversal_doc_processing
*&---------------------------------------------------------------------*
form reversal_doc_processing
    tables p_listmat type tt_listmat
           p_docmat  type tt_docmat
           p_budat   type tt_so_budat
    using  ppa_rever type type_1c.                          "#EC *

  data:
      l_prcntcur   type i,                 " for scale
      l_prcntall   type i,                 " for scale
      l_prcntshw   type i,                 " for scale
      l_prcnttmp   type i,                 " for scale
*    l_textsc     TYPE type_text70,
      l_mensh_in   type j_3rfmin,
      l_mensh_out  type j_3rfmout,
      l_dmshb_in   type j_3rfinsum,
      l_dmshb_out  type j_3rfoutsum,
      l_dmshb4_in  type j_3rfinsum,                            "1095460
      l_dmshb4_out type j_3rfoutsum,                           "1095460
      l_rec        type sytabix,
      l_rec1       type sytabix,
* tables
      lt_ind4_docmat  type tt_ind4_docmat,
      lt_erase_storno type tt_erase_storno,
* headers
      lw_erase_storno type type_erase_storno.

  field-symbols:
      <fs_docmat>   like docmat,
      <fs_docmat4>  like docmat,
      <fs_docmat_t> like docmat,
      <fs_erase_storno> type type_erase_storno.                "1259372

  describe table p_docmat lines l_prcntall.
  if l_prcntall is initial.
    exit.
  endif.
* fill index table
  lt_ind4_docmat[] = p_docmat[].
  sort p_listmat by field_link.                                "1557283

* looking-for reversal document
  loop at p_docmat assigning <fs_docmat>.
    l_rec = sy-tabix.
    add 1 to l_prcntcur.
* if p_docmat-smblp isn't empty, it's reversal document
    if <fs_docmat>-smblp ne g_fourzero.
*    IF p_docmat-sjahr NE g_fourzero.
* search recversed document
      clear l_rec1.
      loop at lt_ind4_docmat assigning <fs_docmat_t>
          where sjhnr = <fs_docmat>-sjahr
            and sblnr = <fs_docmat>-smbln
            and szile = <fs_docmat>-smblp.                  "#EC CI_XYZ
        check <fs_docmat_t>-color_banner is initial.
        check <fs_docmat_t>-lgort = <fs_docmat>-lgort.         "1251769
        check <fs_docmat_t>-xmatnr = <fs_docmat>-xmatnr.       "2097423
* reversal for reversal
* OSS message number : 0020079747 0000383385 2003
        check <fs_docmat_t>-smbln is initial.
        l_rec1 = sy-tabix.
        read table lt_ind4_docmat assigning <fs_docmat4>
            index l_rec1.
        exit.
      endloop.
      if sy-subrc is initial and not l_rec1 is initial.
* item and number reversal documet
        lw_erase_storno-matnr = <fs_docmat>-xmatnr.       "2097423
        lw_erase_storno-gjahr = <fs_docmat>-sjhnr.
        lw_erase_storno-mblnr = <fs_docmat>-sblnr.
        lw_erase_storno-zeile = <fs_docmat>-szile.
* item and number canceled documet
        lw_erase_storno-sjahr = <fs_docmat4>-sjhnr.
        lw_erase_storno-smbln = <fs_docmat4>-sblnr.
        lw_erase_storno-smblp = <fs_docmat4>-szile.
* check date range for canceled and reversal documents
        check <fs_docmat4>-budat >= p_budat-low
          and <fs_docmat4>-budat <= p_budat-high
          and <fs_docmat>-budat >= p_budat-low
          and <fs_docmat>-budat <= p_budat-high.
        if not ppa_rever is initial.
* check and correct amount for reversal document               "1095460
          clear: l_dmshb_in, l_dmshb_out,                      "1095460
                 l_dmshb4_in, l_dmshb4_out.                    "1095460
          l_dmshb_in   = abs( <fs_docmat>-xdmshb_in ).         "1095460
          l_dmshb_out  = abs( <fs_docmat>-xdmshb_out ).        "1095460
          l_dmshb4_in  = abs( <fs_docmat4>-xdmshb_in ).        "1095460
          l_dmshb4_out = abs( <fs_docmat4>-xdmshb_out ).       "1095460
          If ( l_dmshb4_in <> l_dmshb_out Or l_dmshb4_out <> l_dmshb_in )
          and <fs_docmat>-xbelnr is initial.                   "1506654
*           reversal document without FI only                  "1506654
            Read Table lt_ind4_docmat With Table Key           "1095460
                 sjahr = <fs_docmat>-sjahr                     "1095460
                 sblnr = <fs_docmat>-sblnr                     "1095460
                 szile = <fs_docmat>-szile                     "1095460
                 lgort = <fs_docmat>-lgort                     "1095460
                 Assigning <fs_docmat_t>.                      "1095460
            If sy-subrc EQ 0.                                  "1095460
              <fs_docmat_t>-xdmshb_in = - <fs_docmat4>-xdmshb_out.
              <fs_docmat_t>-xdmshb_out = - <fs_docmat4>-xdmshb_in.
            Endif.                                             "1095460
          Endif.                                               "1095460
          <fs_docmat4>-color_banner = s_color_storno.
        else.
* recalc account balances
          perform the_mateial_turnover_recalc
              using p_listmat[]                                "1557283
                    <fs_docmat4>-field_link
                    <fs_docmat4>-xmensh_in
                    <fs_docmat4>-xmensh_out
                    <fs_docmat4>-xdmshb_in
                    <fs_docmat4>-xdmshb_out.

          <fs_docmat4>-color_banner = s_color_storno.
          delete lt_ind4_docmat index l_rec1.

        endif.
* story reference to reversal document
        append lw_erase_storno to lt_erase_storno.
      endif.
    endif.
*   recording a step 'Processing reversal documents 1'         "1417445
    if g_bkgrd is initial.
      check gv_wan is initial.                                 "1417445
      perform run_scale
          using l_prcntall l_prcntcur text-017
          changing l_prcntshw l_prcnttmp.
    endif.
  endloop.

* erase or colour reversal document
  describe table lt_erase_storno lines l_prcntall.
  clear: l_prcntcur, l_prcnttmp.

  loop at lt_erase_storno assigning <fs_erase_storno>.         "1259372

    add 1 to l_prcntcur.
    clear l_rec.

    loop at lt_ind4_docmat assigning <fs_docmat_t>
        where xmatnr = <fs_erase_storno>-matnr                 "2097423
          and sjhnr = <fs_erase_storno>-gjahr
          and sblnr = <fs_erase_storno>-mblnr
          and szile = <fs_erase_storno>-zeile.                 "#EC CI_XYZ

      check <fs_docmat_t>-color_banner is initial.
      l_rec = sy-tabix.
      read table lt_ind4_docmat assigning <fs_docmat4>
          index l_rec.
      exit.
    endloop.
    if sy-subrc is initial and not l_rec is initial.
      if not ppa_rever is initial.
        check <fs_docmat4>-budat >= p_budat-low
            and <fs_docmat4>-budat <= p_budat-high.

        <fs_docmat4>-color_banner = s_color_storno.
        clear: l_mensh_in, l_mensh_out, l_dmshb_in, l_dmshb_out.
        l_mensh_in  = <fs_docmat4>-xmensh_out.
        l_mensh_out = <fs_docmat4>-xmensh_in.
        l_dmshb_in  = <fs_docmat4>-xdmshb_out.
        l_dmshb_out = <fs_docmat4>-xdmshb_in.
        <fs_docmat4>-xmensh_out = l_mensh_out.
        <fs_docmat4>-xmensh_in  = l_mensh_in.
        <fs_docmat4>-xdmshb_out = l_dmshb_out.
        <fs_docmat4>-xdmshb_in  = l_dmshb_in.
      else.
* recalc account balances
        check <fs_docmat4>-budat >= p_budat-low
            and <fs_docmat4>-budat <= p_budat-high.

* recalc account balances
        perform the_mateial_turnover_recalc
            using p_listmat[]                                  "1557283
                  <fs_docmat4>-field_link
                  <fs_docmat4>-xmensh_in
                  <fs_docmat4>-xmensh_out
                  <fs_docmat4>-xdmshb_in
                  <fs_docmat4>-xdmshb_out.

        <fs_docmat4>-color_banner = s_color_storno.
        delete lt_ind4_docmat index l_rec.
      endif.
    endif.
  endloop.

  p_docmat[]  = lt_ind4_docmat[].

endform.                    " reversal_doc_processing
*&---------------------------------------------------------------------*
*&      Form  the_mateial_turnover_recalc
*&---------------------------------------------------------------------*
form the_mateial_turnover_recalc
    using pt_ind4_listmat     type tt_listmat                  "1557283
          p_docmat_field_link type type_link
          p_docmat_xmensh_in  type j_3rfmin
          p_docmat_xmensh_out type j_3rfmout
          p_docmat_xdmshb_in  type j_3rfinsum
          p_docmat_xdmshb_out type j_3rfoutsum.

  data: l_xmensh type j_3rfmout,
        l_xdmshb type j_3rfoutsum.

  field-symbols:
      <fs_listmat4> like listmat.

  read table pt_ind4_listmat assigning <fs_listmat4>
      with key field_link = p_docmat_field_link
      binary search.

  if sy-subrc is initial.
    if not p_docmat_xmensh_in is initial.
      subtract p_docmat_xmensh_in from <fs_listmat4>-deltlbkumd.
    endif.
    if not p_docmat_xmensh_out is initial.
      l_xmensh = abs( p_docmat_xmensh_out ).
      subtract l_xmensh from <fs_listmat4>-deltlbkumk.
    endif.
    if not p_docmat_xdmshb_in is initial.
      subtract p_docmat_xdmshb_in from <fs_listmat4>-deltsalkd.
    endif.
    if not p_docmat_xdmshb_out is initial.
      l_xdmshb = abs( p_docmat_xdmshb_out ).
      subtract l_xdmshb from <fs_listmat4>-deltsalkk.
    endif.
  endif.


endform.                    " the_mateial_turnover_recalc
*&---------------------------------------------------------------------*
*&      Form  reversal_invoice_processing
*&---------------------------------------------------------------------*
form reversal_invoice_processing
       tables p_listmat type tt_listmat
           p_docmat  type tt_docmat
           p_budat   type tt_so_budat
    using p_pa_rever type j_3rmoptved3.                     "#EC *

  data:
      l_prcntcur   type i,                 " for scale
      l_prcntall   type i,                 " for scale
      l_prcntshw   type i,                 " for scale
      l_prcnttmp   type i,                 " for scale
*    l_textsc     TYPE type_text70,
      l_mensh_in   type j_3rfmin,
      l_mensh_out  type j_3rfmout,
      l_dmshb_in   type j_3rfinsum,
      l_dmshb_out  type j_3rfoutsum,
      l_rec        type sytabix,
      l_rec1       type sytabix,

* tables
      lt_ind4_docmat  type tt_ind4_docmat,
      lt_erase_storno type tt_erase_storno,
* headers
      lw_erase_storno type type_erase_storno.

  field-symbols:
      <fs_docmat>    like docmat,
      <fs_docmat_t>  like docmat,
      <fs_docmat4>   like docmat,
      <fs_erase_storno> type type_erase_storno.                "1259372

  describe table p_docmat lines l_prcntall.
  if l_prcntall is initial.
    exit.
  endif.
* fill index table
  lt_ind4_docmat[]  = p_docmat[].
  sort p_listmat by field_link.                                "1557283

  loop at p_docmat assigning <fs_docmat>.
    l_rec = sy-tabix.
    add 1 to l_prcntcur.
    if <fs_docmat>-sjahr ne g_fourzero and
       <fs_docmat>-belegnr is initial and
       <fs_docmat>-smbln < <fs_docmat>-sblnr.
* canceled document search
      clear l_rec1.
      loop at lt_ind4_docmat assigning <fs_docmat_t>
          where sjhnr = <fs_docmat>-sjahr
            and sblnr = <fs_docmat>-smbln
            and szile = <fs_docmat>-smblp.                  "#EC CI_XYZ
        check <fs_docmat_t>-color_banner is initial.
        check <fs_docmat_t>-lgort = <fs_docmat>-lgort.             "2097423
        check <fs_docmat_t>-xmatnr = <fs_docmat>-xmatnr.           "2097423
        l_rec1 = sy-tabix.
        read table lt_ind4_docmat assigning <fs_docmat4>
            index l_rec1.
        exit.
      endloop.
      if sy-subrc is initial and not l_rec1 is initial.
* item and number reversal documet
        lw_erase_storno-matnr = <fs_docmat>-xmatnr.                "2097423
        lw_erase_storno-gjahr = <fs_docmat>-sjhnr.
        lw_erase_storno-mblnr = <fs_docmat>-sblnr.
* item and number canceled documet
        lw_erase_storno-sjahr = <fs_docmat4>-sjhnr.
        lw_erase_storno-smbln = <fs_docmat4>-sblnr.
* check date range for canceled and reversal documents
        check <fs_docmat4>-budat >= p_budat-low
          and <fs_docmat4>-budat <= p_budat-high
          and <fs_docmat>-budat >= p_budat-low
          and <fs_docmat>-budat <= p_budat-high.
        if not p_pa_rever is initial.
          <fs_docmat4>-color_banner = s_color_storno.
        else.
* recalc account balances
          perform the_mateial_turnover_recalc
              using p_listmat[]                                "1557283
                    <fs_docmat4>-field_link
                    <fs_docmat4>-xmensh_in
                    <fs_docmat4>-xmensh_out
                    <fs_docmat4>-xdmshb_in
                    <fs_docmat4>-xdmshb_out.
          <fs_docmat4>-color_banner = s_color_storno.
          delete lt_ind4_docmat index l_rec1.

        endif.
* story reference to reversal document
        append lw_erase_storno to lt_erase_storno .
      endif.
    endif.
*   recording a step 'Processing reversal documents 2'         "1417445
    if g_bkgrd is initial.
      check gv_wan is initial.                                 "1417445
      perform run_scale
          using l_prcntall l_prcntcur text-018                 "1417445
          changing l_prcntshw l_prcnttmp.
    endif.
  endloop.

* erase or colour reversal document
  describe table lt_erase_storno lines l_prcntall.
  clear: l_prcntcur, l_prcnttmp.

  loop at lt_erase_storno assigning <fs_erase_storno>.         "1259372

    add 1 to l_prcntcur.
    clear l_rec.
    loop at lt_ind4_docmat assigning <fs_docmat_t>
        where xmatnr = <fs_erase_storno>-matnr                 "2097423
          and sjhnr = <fs_erase_storno>-gjahr
          and sblnr = <fs_erase_storno>-mblnr.                 "#EC CI_XYZ

      check <fs_docmat_t>-color_banner is initial.
      l_rec = sy-tabix.
      read table lt_ind4_docmat assigning <fs_docmat4>
          index l_rec.
      exit.
    endloop.

    if sy-subrc is initial and not l_rec is initial.
      if not p_pa_rever is initial.
        check <fs_docmat4>-budat >= p_budat-low
            and <fs_docmat4>-budat <= p_budat-high.

        <fs_docmat4>-color_banner = s_color_storno.
        clear: l_mensh_in, l_mensh_out, l_dmshb_in, l_dmshb_out.
        l_mensh_in  = <fs_docmat4>-xmensh_out.
        l_mensh_out = <fs_docmat4>-xmensh_in.
        l_dmshb_in  = <fs_docmat4>-xdmshb_out.
        l_dmshb_out = <fs_docmat4>-xdmshb_in.
        <fs_docmat4>-xmensh_out = l_mensh_out.
        <fs_docmat4>-xmensh_in  = l_mensh_in.
        <fs_docmat4>-xdmshb_out = l_dmshb_out.
        <fs_docmat4>-xdmshb_in  = l_dmshb_in.
      else.
* recalc account balances
        check <fs_docmat4>-budat >= p_budat-low
            and <fs_docmat4>-budat <= p_budat-high.

        perform the_mateial_turnover_recalc
            using p_listmat[]                                  "1557283
                  <fs_docmat4>-field_link
                  <fs_docmat4>-xmensh_in
                  <fs_docmat4>-xmensh_out
                  <fs_docmat4>-xdmshb_in
                  <fs_docmat4>-xdmshb_out.
        <fs_docmat4>-color_banner = s_color_storno.

        delete lt_ind4_docmat index l_rec.
      endif.
    endif.
  endloop.

  p_docmat[]  = lt_ind4_docmat[].

endform.                    " reversal_invoice_processing
