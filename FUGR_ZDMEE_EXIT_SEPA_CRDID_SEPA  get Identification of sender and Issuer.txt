FUNCTION zdmee_exit_sepa_crdid.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     VALUE(I_TREE_TYPE) TYPE  DMEE_TREETYPE
*"     VALUE(I_TREE_ID) TYPE  DMEE_TREEID
*"     VALUE(I_ITEM)
*"     VALUE(I_PARAM)
*"     VALUE(I_UPARAM)
*"     REFERENCE(I_EXTENSION) TYPE  DMEE_EXIT_INTERFACE
*"  EXPORTING
*"     REFERENCE(O_VALUE)
*"     REFERENCE(C_VALUE)
*"     REFERENCE(N_VALUE)
*"     REFERENCE(P_VALUE)
*"  TABLES
*"      I_TAB
*"----------------------------------------------------------------------

*  TABLES: T001Z.
*
  DATA: lwa_item       TYPE dmee_paym_if_type,
        ls_fpayh       TYPE fpayh,
        ls_fpayhx      TYPE fpayhx,
        lv_rec_crdid   TYPE sepa_crdid,
        lv_modalidad   TYPE zfi_modalidad,
        lv_util_efecto TYPE zfi_efecto,
        lv_wverw       TYPE wverw,
        lv_gjahr       TYPE gjahr,
        lv_belnr      TYPE belnr_d.

  lwa_item = i_item.
  ls_fpayh  = lwa_item-fpayh.
  ls_fpayhx = lwa_item-fpayhx.

  CLEAR lv_rec_crdid.

  MOVE ls_fpayh-doc1r+14 TO lv_gjahr.

  CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
    EXPORTING
      input         = ls_fpayhx-ovbln
   IMPORTING
     output        =  lv_belnr.

* select a la bseg para obtener el valor de la wverw
  SELECT SINGLE wverw INTO lv_wverw
    FROM bseg
    WHERE bukrs  = ls_fpayh-zbukr
     AND  belnr  = lv_belnr
     AND  gjahr =  lv_gjahr
     AND ( wverw = 'D' OR  wverw = 'I' ).

  SELECT SINGLE rec_crdid modalidad util_efecto
  INTO (lv_rec_crdid, lv_modalidad, lv_util_efecto)
    FROM zfi001
      WHERE bukrs = ls_fpayh-zbukr
      AND   hbkid = ls_fpayh-hbkid
      AND   modalidad = ls_fpayhx-inst_code
      AND   util_efecto = lv_wverw.

  IF lv_rec_crdid IS NOT INITIAL.
    c_value = lv_rec_crdid.
  ELSE.
    c_value = ls_fpayhx-rec_crdid.
  ENDIF.

ENDFUNCTION.
