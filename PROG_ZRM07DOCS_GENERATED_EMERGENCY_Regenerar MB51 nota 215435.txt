*&---------------------------------------------------------------------*
*& Report  ZRM07DOCS_GENERATED_EMERGENCY                               *
*&                                                                     *
*&---------------------------------------------------------------------*
*&                                                                     *
*& This report creates the emergency source code for include report    *
*& RM07DOCS_GENERATED according note 215435.                           *
*&                                                                     *
*& Run this report in the case transaction MB51 cannot run due syntax  *
*& error in include report RM07DOCS_GENERATED. Then check the settings *
*& in customizing view V_MMIM_REP_CUST and correct then if necessary.  *
*&                                                                     *
*& !!! Do not delete or change comment lines !!!                       *
*&                                                                     *
*&---------------------------------------------------------------------*

REPORT  zrm07docs_generated_emergency.

TYPES : BEGIN OF stype_code,
          line(72) TYPE c,
        END OF stype_code,

        stab_code TYPE STANDARD TABLE OF stype_code
                             WITH DEFAULT KEY.

DATA  : g_s_code TYPE stype_code,
        g_t_code TYPE stab_code.

TYPES : BEGIN OF stype_fields,
          tabname   LIKE  mmim_rep_cust-tabname,
          fieldname LIKE  mmim_rep_cust-fieldname,
          counter   TYPE i,
        END OF stype_fields,

        stab_fields TYPE STANDARD TABLE OF stype_fields
                             WITH DEFAULT KEY.

DATA  : g_s_fields       TYPE stype_fields,
        g_t_fields       TYPE stab_fields,
        g_s_fields_short TYPE stype_fields,
        g_t_fields_short TYPE stab_fields.

*----------------------------------------------------------------------*

AT SELECTION-SCREEN.

* Check that the current user has debug authorization.
  AUTHORITY-CHECK OBJECT 'S_DEVELOP'
    ID 'DEVCLASS' DUMMY
    ID 'OBJTYPE'  FIELD 'DEBUG'
    ID 'OBJNAME'  DUMMY
    ID 'P_GROUP'  DUMMY
    ID 'ACTVT'    FIELD '03'.

  IF  sy-subrc <> 0.
    MESSAGE e895(m7) WITH 'Sorry, no authorization'.
  ENDIF.

*----------------------------------------------------------------------*

START-OF-SELECTION.

* check whether report RM07DOCS_GENERATED is available
  READ REPORT 'RM07DOCS_GENERATED' INTO g_t_code.

  IF sy-subrc <> 0.
    MESSAGE e895(m7)
      WITH 'Error during reading report RM07DOCS_GENERATED'.
  ELSE.
    REFRESH                  g_t_code.
  ENDIF.

* read source code of this report into working table
  READ REPORT 'ZRM07DOCS_GENERATED_EMERGENCY' INTO g_t_code.

  IF sy-subrc <> 0.
    MESSAGE e895(m7)
      WITH 'Error during reading source code'.
  ENDIF.

* extract the source code for report RM07DOCS_GENERATED
* only the line who contain "*/" will be inserted into the
* source
* the leading digits "*/" will be deleted
  LOOP AT g_t_code           INTO  g_s_code.
    IF  g_s_code-line+0(02) = '*/'.
      SHIFT  g_s_code-line   BY 2 PLACES.
      MODIFY  g_t_code       FROM  g_s_code-line.
    ELSE.
      DELETE                 g_t_code.
    ENDIF.
  ENDLOOP.

  IF  g_t_code[] IS INITIAL.
    MESSAGE e895(m7)
      WITH 'No new source code available for insert'.
  ENDIF.

* insert the emergency coding into report
  INSERT REPORT 'RM07DOCS_GENERATED' FROM g_t_code.

  IF sy-subrc IS INITIAL.
    MESSAGE s895(m7) WITH 'Report RM07DOCS_GENERATED updated'.
  ELSE.
    MESSAGE e895(m7)
      WITH 'Error during insert report RM07DOCS_GENERATED'.
  ENDIF.

* check the field names for selection whether there are ducplicates
* when the long fieldnames are
  SELECT tabname fieldname   FROM mmim_rep_cust
    INTO CORRESPONDING FIELDS OF TABLE g_t_fields
      WHERE  selpos <> space.

  IF sy-subrc IS INITIAL.
    LOOP AT g_t_fields       INTO  g_s_fields.
      MOVE : g_s_fields-tabname
                             TO  g_s_fields_short-tabname,
             g_s_fields-fieldname(08)
                             TO  g_s_fields_short-fieldname,
             1               TO  g_s_fields_short-counter.
      COLLECT  g_s_fields_short
                             INTO  g_t_fields_short.
    ENDLOOP.

    LOOP AT g_t_fields_short INTO  g_s_fields_short.
      CHECK  g_s_fields_short-counter > 1.
      WRITE : /1 'Table =',  g_s_fields_short-tabname,
                 'Duplicate short fieldname =',
                             g_s_fields_short-fieldname.
      WRITE : / 'Please check and correct entries in view',
                'V_MMIM_REP_CUST'.

      SKIP                   1.
    ENDLOOP.
  ENDIF.

* the following lines with "*/" will be inserted into the source
* code of RM07DOCS_GENERATED

*/*99999999999999  <- Timestamp
*/********************************************************************
*/*
*/* Emergency restart coding for the generated part of RM07DOCS.
*/*
*/* Only needed to ensure an initial compilability.
*/*
*/********************************************************************
*/*
*/* The ranges need to be there for the call to MMIM_ENTRYCHECK_MAIN
*/*
*/RANGES:
*/  matnr FOR mseg-matnr,
*/  werks FOR mseg-werks,
*/  lgort FOR mseg-lgort,
*/  lifnr FOR mseg-lifnr,
*/  kunnr FOR mseg-kunnr,
*/  bwart FOR mseg-bwart,
*/  sobkz FOR mseg-sobkz,
*/  vgart FOR mkpf-vgart,
*/  usnam FOR mkpf-usnam.
*/*
*/DATA: BEGIN OF itab OCCURS 0.
*/*   The entire MSEG and some additional fields from the header
*/*   which are accessed literally in the coding.
*/    INCLUDE STRUCTURE mseg.
*/    DATA: budat LIKE mkpf-budat.
*/DATA: END OF itab.
*/*
*/*
*/selection-screen begin of block mkpf with frame title text-002.
*/  select-options BUDAT for MKPF-BUDAT.
*/selection-screen end of block mkpf.
*/*
*/* Form routines just to be there
*/*
*/FORM data_selection_new.
*/ENDFORM.
*/*
*/FORM data_selection_via_matnr.
*/ENDFORM.
*/*
*/FORM data_selection_via_budat.
*/ENDFORM.
*/*
*/FORM data_selection.
*/ENDFORM.
*/*
*/FORM shortdocument_read.
*/ENDFORM.
*/*
*/FORM archive_check_selections CHANGING rc.
*/ENDFORM.
*/*
*/FORM build_runtimetable.
*/ENDFORM.
*/*
*/FORM fill_frange.
*/ENDFORM.
*/*
*/FORM check_select_mkpf.
*/ENDFORM.
*/*
*/FORM check_select_mseg.
*/ENDFORM.
*/*
*/FORM check_restrictions.
*/ENDFORM.
*/*
*/FORM select_maa_data.
*/ENDFORM.
*/*
*/FORM select_single_mm_doc_item.
*/ENDFORM.
*/*
*/FORM check_restrictions_maa.
*/ENDFORM.
