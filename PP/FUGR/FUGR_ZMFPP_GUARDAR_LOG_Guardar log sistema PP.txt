FUNCTION zmfpp_guardar_log.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     REFERENCE(OBJETO) TYPE  BALOBJ_D
*"     REFERENCE(SUBOBJETO) TYPE  BALSUBOBJ
*"     REFERENCE(MENSAJES) TYPE  BAPIRET2_T
*"     REFERENCE(EXTNUMBER) TYPE  BALNREXT
*"----------------------------------------------------------------------

  DATA: wa_mensajes TYPE bapiret2.
  DATA: clave TYPE balnrext.
  DATA: linea TYPE balnrext.

* Variables para la creacion del log
  DATA:
    l_log_handle TYPE balloghndl,
    l_s_log      TYPE bal_s_log,
    l_s_msg      TYPE bal_s_msg,
    l_msgno      TYPE symsgno,
    ta_loghandle TYPE bal_t_logh.

* Creamos el log de incidencias

  IF NOT extnumber IS INITIAL.
    l_s_log-extnumber       = extnumber.
  ENDIF.

  l_s_log-object          = objeto.
  l_s_log-subobject       = subobjeto.
  l_s_log-aldate_del      = sy-datum + 100.
  l_s_log-del_before      = space.

  CALL FUNCTION 'BAL_LOG_CREATE'
    EXPORTING
      i_s_log      = l_s_log
    IMPORTING
      e_log_handle = l_log_handle
    EXCEPTIONS
      OTHERS       = 1.

  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
             WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

  LOOP AT mensajes INTO wa_mensajes.

    l_s_msg-msgty    = wa_mensajes-type.
    l_s_msg-msgid    = wa_mensajes-id.
    l_s_msg-msgno    = wa_mensajes-number.
    l_s_msg-msgv1    = wa_mensajes-message_v1.
    l_s_msg-msgv2    = wa_mensajes-message_v2.
    l_s_msg-msgv3    = wa_mensajes-message_v3.
    l_s_msg-msgv4    = wa_mensajes-message_v4.
*    l_s_msg-detlevel = wa_mensajes-detlevel.

    case l_s_msg-msgty.
      when 'E'.
        l_s_msg-probclass = '1'.
      when others.
          l_s_msg-probclass = '3'.
    endcase.

* Añadimos la incidencia al log
    call function 'BAL_LOG_MSG_ADD'
      exporting
        i_log_handle = l_log_handle
        i_s_msg      = l_s_msg
      exceptions
        others       = 1.
    if sy-subrc <> 0.
      message id sy-msgid type sy-msgty number sy-msgno
               with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    endif.

  ENDLOOP.

  refresh ta_loghandle.
  append l_log_handle to ta_loghandle.

  call function 'BAL_DB_SAVE'
       exporting
         i_client               = sy-mandt
         i_in_update_task       = ''
         i_save_all             = ''
         i_t_log_handle         = ta_loghandle
*      IMPORTING
*        E_NEW_LOGNUMBERS       =
       exceptions
         log_not_found          = 1
         save_not_allowed       = 2
         numbering_error        = 3
         others                 = 4.

* Refrescamos el log de incidencias
  call function 'BAL_LOG_REFRESH'
    exporting
      i_log_handle  = l_log_handle
    exceptions
      log_not_found = 1
      others        = 2.
  if sy-subrc <> 0.
    message id sy-msgid type sy-msgty number sy-msgno
            with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  endif.

ENDFUNCTION.
