*&---------------------------------------------------------------------*
*& Report  ZRPP0058
*& CMS 26-08-2019
*&---------------------------------------------------------------------*
*& Control receta MP por fecha y turno de producción Tx. ZPP062
*&
*&---------------------------------------------------------------------*
REPORT zrpp0059.

INCLUDE zrpp0059_top.
INCLUDE zrpp0059_class.
INCLUDE zrpp0059_f01.

* Obtención del centro asociado al Usuario
INITIALIZATION.

  SELECT SINGLE parva FROM usr05
    INTO p_werks
    WHERE bname = sy-uname AND
          parid = 'WRK'.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_rece.

*Se realizan las búsquedas y se anexan los valores a la tabla interna
  SELECT id_receta , z40~zcalidad , descrip , descrip_rec , fecha_ini , hora_ini , fecha_fin , hora_fin
     FROM ztpp0040 as z40
      INNER JOIN ZTPP0035 as z35 on z35~werks = z40~werks and z35~zcalidad = z40~zcalidad
    INTO TABLE @DATA(it_recetasf4)
        WHERE z40~werks EQ @p_werks
        ORDER BY fecha_ini DESCENDING , hora_ini DESCENDING.

*Se hace uso de la función que convoca al matchcode y transforma nuestra tabla interna
  CALL FUNCTION 'F4IF_INT_TABLE_VALUE_REQUEST'
    EXPORTING
      retfield        = 'ID_RECETA'
      window_title    = 'Recetas MP'
      value_org       = 'S'
    TABLES
      value_tab       = it_recetasf4
      return_tab      = it_match
    EXCEPTIONS
      parameter_error = 1
      no_values_found = 2
      OTHERS          = 3.

  IF sy-subrc = 0.
    TRY.
        p_rece = CONV zid_receta( it_match[ 1 ]-fieldval ).
      CATCH cx_root .
        CLEAR p_rece.
    ENDTRY.
  ENDIF.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_lay06.
  PERFORM buscar_variante CHANGING p_lay06.


AT SELECTION-SCREEN.
  IF p_t1 IS INITIAL AND
     p_t2 IS INITIAL AND
     p_t3 IS INITIAL.
    MESSAGE 'Se debe marcar al menos un turno.' TYPE 'E' DISPLAY LIKE 'I'.

  ENDIF.

* Campo centro abierto, validación:
  IF p_werks <> '3000' AND p_werks <> '3020'.
    MESSAGE 'Seleccione un centro correcto' TYPE 'I'.
    RETURN.

  ENDIF.

  IF p_rece IS INITIAL.
    MESSAGE 'Introduzca una receta' TYPE 'E' DISPLAY LIKE 'I'.

  ENDIF.

START-OF-SELECTION.

  DATA: l_rc TYPE char02.

  CALL FUNCTION 'ZMPP_MOSTRAR_DATOS_TURNO_RECET'
    EXPORTING
      i_werks      = p_werks
      i_id_receta  = p_rece
*      i_fecha_fabr = p_fec
      ir_fecha_fabr = s_fec[]
      i_t1         = p_t1
      i_t2         = p_t2
      i_t3         = p_t3
      i_layout     = p_lay06
    IMPORTING
      o_rc         = l_rc.

  CASE l_rc.
    WHEN '01'. MESSAGE 'No se han encontrado datos.' TYPE 'I'.
  ENDCASE.
