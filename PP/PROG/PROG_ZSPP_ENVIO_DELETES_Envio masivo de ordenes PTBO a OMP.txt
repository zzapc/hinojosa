*&---------------------------------------------------------------------*
*& Report  ZSPP_ENVIO_DELETES
*&
*&---------------------------------------------------------------------*
*&
*&
*&---------------------------------------------------------------------*
REPORT zspp_envio_deletes.

DATA: lv_aufnr TYPE aufnr,
      lv_date  TYPE aufk-aedat,
      lv_auart TYPE auart.

SELECTION-SCREEN BEGIN OF BLOCK b1.
SELECT-OPTIONS: so_aufnr FOR lv_aufnr,
so_date FOR lv_date,
so_auart FOR lv_auart.

SELECTION-SCREEN END OF BLOCK b1.

START-OF-SELECTION.
  DATA it_aufnr TYPE TABLE OF bapi_order_key.

  SELECT aufnr AS order_number INTO TABLE @it_aufnr
    FROM jest AS t1 INNER JOIN aufk AS t2
    ON t1~objnr EQ t2~objnr
    WHERE aufnr IN @so_aufnr
    AND aedat IN @so_date
    AND auart IN @so_auart
    AND inact = @space
    AND stat EQ 'I0076' . "PTBO

  "INI Obtenemos los IDOCS enviados en el día de hoy, con status verde para no reenviarlos
  SELECT t2~* INTO TABLE @DATA(it_idocs)
    FROM edidc AS t1 INNER JOIN edid4 AS t2
    ON t1~docnum EQ t2~docnum
    WHERE t2~segnam EQ 'ZCCORDERDATA'
    AND t1~mestyp EQ 'ZIF_OMP_ORDER_DELETE'
    AND t1~credat IN @so_date
    AND t1~status EQ '03'.

  "Obtenemos todos aquellos que ya se han enviado correctamente
  DATA rg_auf TYPE RANGE OF aufnr.
  FREE rg_auf.
  DATA wa_segmento TYPE zccorderdata.
  LOOP AT it_idocs ASSIGNING FIELD-SYMBOL(<idocs>).
    wa_segmento = <idocs>-sdata.
    APPEND INITIAL LINE TO rg_auf ASSIGNING FIELD-SYMBOL(<auf>).
    <auf> = 'IEQ'.
    <auf>-low = wa_segmento-orderid.
    CONDENSE <auf>-low  NO-GAPS.
    <auf>-low = |{ <auf>-low ALPHA = IN }|.

    APPEND INITIAL LINE TO rg_auf ASSIGNING <auf>.
    <auf> = 'IEQ'.
    <auf>-low = wa_segmento-orderid.
    CONDENSE <auf>-low  NO-GAPS.
    <auf>-low = |{ <auf>-low ALPHA = OUT }|.

  ENDLOOP.

  DELETE it_aufnr WHERE order_number IN rg_auf.
  "FJVM

  LOOP AT it_aufnr ASSIGNING FIELD-SYMBOL(<fs_aufnr>).

    CALL FUNCTION 'ZSPP_IDOC_ORDERS_DELETE'
      EXPORTING
        i_aufnr = <fs_aufnr>-order_number.

  ENDLOOP.
