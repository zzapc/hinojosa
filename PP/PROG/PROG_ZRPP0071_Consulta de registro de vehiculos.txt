*&---------------------------------------------------------------------*
*& Report  ZRPP0068
*& CMS 12-07-2020
*&---------------------------------------------------------------------*
*& Consulta de registros de vehículo
*&  Tx. ZPP0077
*&
*&---------------------------------------------------------------------*
REPORT zrpp0071.

TABLES: ztpp0059 , ztpp0051.

TYPES: BEGIN OF ty_dest,
         id_destino    TYPE ztpp0051-id_destino,
         descr_destino TYPE ztpp0051-descr_destino,
       END OF ty_dest.
DATA: gss_dest TYPE ty_dest,
      gts_dest TYPE TABLE OF ty_dest.
DATA: gt_return  TYPE TABLE OF ddshretval,
      gwa_return TYPE ddshretval.

SELECTION-SCREEN BEGIN OF BLOCK b4 WITH FRAME TITLE text-003.
SELECTION-SCREEN SKIP 1.

PARAMETER: p_werks LIKE ztpp0059-werks.

SELECTION-SCREEN SKIP 1.

SELECT-OPTIONS: s_reg    FOR  ztpp0059-id_reg_vehiculo MATCHCODE OBJECT zhpp_reg_vehiculo.
SELECT-OPTIONS: s_fec    FOR  ztpp0059-fecha_ins.
PARAMETER:      p_alb    LIKE ztpp0059-albaran.
PARAMETER:      p_matri  LIKE ztpp0059-zmatricula1.
PARAMETER:      p_dest   TYPE zid_destino.
SELECTION-SCREEN SKIP 1.

PARAMETER: p_anul   AS CHECKBOX DEFAULT ' '.
PARAMETER: p_oper   LIKE ztpp0059-usuario_anul NO-DISPLAY.

SELECTION-SCREEN END OF BLOCK b4.

SELECTION-SCREEN SKIP 1.
SELECTION-SCREEN BEGIN OF BLOCK b2.
PARAMETER:
p_lay06 LIKE disvariant-variant.

SELECTION-SCREEN END OF BLOCK b2.


INCLUDE zrpp0071_top.
INCLUDE zrpp0071_class.
INCLUDE zrpp0071_f01.
INCLUDE zrpp0071_status_0100o01.
INCLUDE zrpp0071_user_command_0100i01.

* Obtención del centro asociado al Usuario
INITIALIZATION.

  SELECT SINGLE parva FROM usr05
    INTO p_werks
    WHERE bname = sy-uname AND
          parid = 'WRK'.

  IF sy-tcode = 'ZPP0077V'. " Valores iniciales para ZPP0077V
    s_fec[] =  VALUE #( ( sign = 'I'  option = 'EQ'  low = sy-datum ) ).
    p_dest = '02'.
    p_lay06 = '/EXPEDIC'.
  ENDIF.


AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_lay06.
  PERFORM buscar_variante CHANGING p_lay06.


AT SELECTION-SCREEN OUTPUT.
* 28/01/2021 - CMS - Campo centro NO editable
**** 28.02.2022 **** 74514 LER - MODIFICACION CAMPO CENTRO MODIFICABLE EN PANTALLA SELECCION
  IF sy-tcode = 'ZPP0077V'.
    LOOP AT SCREEN.
      IF screen-name = 'P_WERKS' OR
         screen-name = 'P_ANUL' OR
         screen-name = 'P_DEST' .

        screen-input = 1.
        MODIFY SCREEN.
      ENDIF.
    ENDLOOP.
  ENDIF.

  IF p_oper IS NOT INITIAL.

    LOOP AT SCREEN.
      IF screen-name = 'P_WERKS'.
        screen-input = '0'.
        MODIFY SCREEN.
      ENDIF.
    ENDLOOP.

  ENDIF.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_dest.

  REFRESH gts_dest.
  LOOP AT zclpp_reg_vehiculo_master=>gt_destinos
          INTO DATA(ls_destino) WHERE werks = p_werks.

    gss_dest-id_destino = ls_destino-id_destino.
    gss_dest-descr_destino = ls_destino-descr_destino.
    APPEND gss_dest TO gts_dest.
  ENDLOOP.

  CALL FUNCTION 'F4IF_INT_TABLE_VALUE_REQUEST'
    EXPORTING
      retfield        = 'ID_DESTINO'
      value_org       = 'S'
    TABLES
      value_tab       = gts_dest
      return_tab      = gt_return
    EXCEPTIONS
      parameter_error = 1
      no_values_found = 2
      OTHERS          = 3.

  CHECK sy-subrc = 0.
  TRY.
      p_dest = gt_return[ 1 ]-fieldval.
    CATCH cx_root .
  ENDTRY.



AT SELECTION-SCREEN.
* Campo centro abierto, validación:
  IF p_werks <> '3000' AND p_werks <> '3020'.
    MESSAGE 'Seleccione un centro correcto' TYPE 'E' DISPLAY LIKE 'I'.
    RETURN.
  ENDIF.

  IF s_fec IS INITIAL and s_reg is INITIAL.
    MESSAGE 'Informar registro de vehículo o fecha de registro.' TYPE 'E' DISPLAY LIKE 'I'.
    EXIT.
  ENDIF.

START-OF-SELECTION.
  PERFORM obtener_datos.

END-OF-SELECTION.
  IF gt_datos IS NOT INITIAL.

    CALL SCREEN 0100.
  ELSE.
    MESSAGE |No se han encontrado datos| TYPE 'I'. "DISPLAY LIKE 'I'.
  ENDIF.
