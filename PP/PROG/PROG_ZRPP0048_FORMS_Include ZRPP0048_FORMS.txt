*&---------------------------------------------------------------------*
*&  Include           ZRPP0048_FORMS
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  WERKS_NOT_EDITABLE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM init_sel_screen .

  SELECT SINGLE parva FROM usr05 INTO p_werks
                                 WHERE bname = sy-uname AND
                                       parid = 'WRK'.
  IF sy-subrc NE 0.
    MESSAGE 'No se ha podido determinar el centro a partir del usuario'(001) TYPE 'S' DISPLAY LIKE 'E'.
  ENDIF.

  IF s_dates[] IS INITIAL.
    s_dates-sign = 'I'.
    s_dates-option = 'EQ'.
    s_dates-low = sy-datum.
    APPEND s_dates.
  ENDIF.

*  LOOP AT SCREEN.
*    CHECK screen-name EQ 'P_WERKS'.
*    screen-input = 0.
*    MODIFY SCREEN.
*  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM select_data .

  SELECT * FROM ztpp0027 INTO CORRESPONDING FIELDS OF TABLE gt_docs WHERE werks EQ p_werks
                                              AND creation_date IN s_dates
                                             .
  IF sy-subrc NE 0.
    MESSAGE 'Sin datos para mostrar'(002) TYPE 'S' DISPLAY LIKE 'E'.
    SET SCREEN 0.
    EXIT.
  ENDIF.

  SELECT * FROM ztpp0029 INTO TABLE gt_29 FOR ALL ENTRIES IN gt_docs WHERE mblnr = gt_docs-mblnr.


  LOOP AT gt_docs ASSIGNING FIELD-SYMBOL(<fs>).

    LOOP AT gt_29 INTO DATA(gs_29) WHERE mblnr = <fs>-mblnr .
      IF  gs_29-obsv IS NOT INITIAL." OR <fs>-obsv_gen IS NOT INITIAL.
        <fs>-obsv = icon_alarm.
        <fs>-obsv_button = icon_annotation.
        APPEND VALUE lvc_s_styl( fieldname = 'OBSV_BUTTON' style = cl_gui_alv_grid=>mc_style_button  )
                  TO <fs>-style.
        EXIT.
*      ELSE.
*        CLEAR <fs>-obsv.
      ENDIF.
    ENDLOOP.
  ENDLOOP.

  SORT gt_docs BY creation_date DESCENDING creation_time DESCENDING.


ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  CREATE_OBJECTS
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM create_objects .
  CREATE OBJECT lo_docking_container
    EXPORTING
      side                        = cl_gui_docking_container=>dock_at_left
      extension                   = 8000
    EXCEPTIONS
      cntl_error                  = 1
      cntl_system_error           = 2
      create_error                = 3
      lifetime_error              = 4
      lifetime_dynpro_dynpro_link = 5
      OTHERS                      = 6.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
               WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    EXIT.
  ENDIF.

  CREATE OBJECT lo_alv
    EXPORTING
      i_parent          = lo_docking_container
    EXCEPTIONS
      error_cntl_create = 1
      error_cntl_init   = 2
      error_cntl_link   = 3
      error_dp_create   = 4
      OTHERS            = 5.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
               WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    EXIT.
  ENDIF.

  APPEND: cl_gui_alv_grid=>mc_fc_loc_append_row  TO gt_excluding,
          cl_gui_alv_grid=>mc_fc_loc_insert_row  TO gt_excluding,
          cl_gui_alv_grid=>mc_fc_loc_delete_row  TO gt_excluding,
          cl_gui_alv_grid=>mc_fc_loc_copy        TO gt_excluding,
          cl_gui_alv_grid=>mc_fc_loc_copy_row    TO gt_excluding,
          cl_gui_alv_grid=>mc_fc_loc_paste       TO gt_excluding,
          cl_gui_alv_grid=>mc_fc_loc_paste_new_row TO gt_excluding,
          cl_gui_alv_grid=>mc_fc_loc_undo        TO gt_excluding,
          cl_gui_alv_grid=>mc_fc_loc_cut         TO gt_excluding,
          cl_gui_alv_grid=>mc_fc_check           TO gt_excluding.
ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  SET_FIELD_CATALOG
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM set_field_catalog .

  CALL FUNCTION 'LVC_FIELDCATALOG_MERGE'
    EXPORTING
      i_structure_name       = 'ZTPP0027'
    CHANGING
      ct_fieldcat            = gt_fieldcat
    EXCEPTIONS
      inconsistent_interface = 1
      program_error          = 2
      OTHERS                 = 3.
  IF sy-subrc <> 0.
    EXIT.
  ENDIF.

  APPEND VALUE lvc_s_fcat( fieldname = 'OBSV' outputlen = 2 scrtext_l = 'Observaciones' ) TO gt_fieldcat.
*  APPEND VALUE lvc_s_fcat( fieldname = 'OBSV_BUTTON' outputlen = 2 scrtext_l = 'Obsrv.' datatype = 'ICON' ) TO gt_fieldcat.
  LOOP AT gt_fieldcat ASSIGNING FIELD-SYMBOL(<fs>).
    <fs>-col_pos = 30.
  ENDLOOP.
  LOOP AT gt_fieldcat ASSIGNING <fs>.
    CASE <fs>-fieldname.

      WHEN 'WERKS'.
        <fs>-col_pos    = 01.
        <fs>-f4availabl = abap_false.
        <fs>-ref_field = <fs>-ref_table = <fs>-domname = space.

      WHEN 'LGORT'.
        <fs>-col_pos = 02.
      WHEN 'CREATION_DATE' .
        <fs>-key     = abap_true.
        <fs>-col_pos = 03.
      WHEN 'CREATION_TIME'.
        <fs>-key     = abap_true.
        <fs>-col_pos = 04.
      WHEN 'MBLNR'.
        <fs>-key     = abap_true.
        <fs>-col_pos = 05.
      WHEN 'MJAHR'.
        <fs>-col_pos = 06.
      WHEN 'OBSV'.
        <fs>-col_pos = 07.
      WHEN 'OBSV_BUTTON'.
        <fs>-col_pos = 08.
      WHEN 'ALLOWED_TWGT'.
        <fs>-scrtext_l = <fs>-scrtext_s = <fs>-scrtext_m =  'Peso Total'.
        <fs>-col_pos = 09.
      WHEN 'SIGNI'.
        <fs>-edit    = abap_false.
        <fs>-col_pos = 10.
      WHEN 'TPBEZ'.
        <fs>-edit    = abap_false.
        <fs>-col_pos = 11.
      WHEN 'TNDR_TRKID'.
        <fs>-edit    = abap_false.
        <fs>-col_pos = 12.
      WHEN 'TNDR_CRNM'.
        <fs>-edit    = abap_false.
        <fs>-col_pos = 13.
      WHEN 'OBSV'.
        <fs>-edit    = abap_false.
        <fs>-col_pos = 14.
      WHEN 'OBSV_GEN'.
        <fs>-scrtext_l = <fs>-scrtext_s = <fs>-scrtext_m =  'Observaciones'.
        <fs>-edit    = abap_false.
        <fs>-col_pos = 15.
      WHEN OTHERS.
        DELETE gt_fieldcat INDEX sy-tabix.
        CONTINUE.
    ENDCASE.



  ENDLOOP.


ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  SAVE_TRANSPORT_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM save_transport_data .

  DATA lt_orig_data TYPE HASHED TABLE OF ztpp0027 WITH UNIQUE KEY mblnr mjahr.

  DATA ls_27 TYPE ztpp0027.
  READ TABLE gt_docs ASSIGNING FIELD-SYMBOL(<fs>)
            WITH KEY mandt = sy-mandt
                     mblnr = gs_docs-mblnr.
  IF sy-subrc EQ 0.
    MOVE-CORRESPONDING gs_docs TO ls_27.

    MODIFY ztpp0027 FROM ls_27.
    COMMIT WORK AND WAIT.
    MESSAGE 'Datos guardados' TYPE 'S'.


    DATA it_29 TYPE TABLE OF ztpp0029 .

    it_29[] = CORRESPONDING #( gt_bobinas ).

    MODIFY ztpp0029 FROM TABLE it_29.

    RETURN.
  ENDIF.



** Comprobar que registros se han modificado y se han de guardar.
*
*  lt_orig_data[] = gt_docs[].
*
*  lo_alv->check_changed_data( ).
*
*  LOOP AT gt_docs INTO DATA(ls_docs).
*    READ TABLE lt_orig_data INTO DATA(ls_orig_data) WITH TABLE KEY mblnr = ls_docs-mblnr
*                                                                   mjahr = ls_docs-mjahr.
*    CHECK sy-subrc EQ 0.
*
**   Guardar los campos modificados del registro si no es igual
*    IF ls_docs NE ls_orig_data.
*
*      UPDATE ztpp0027 SET signi      = ls_docs-signi
*                          tpbez      = ls_docs-tpbez
*                          tndr_trkid = ls_docs-tndr_trkid
*                          tndr_crnm  = ls_docs-tndr_crnm
*                      WHERE mblnr = ls_docs-mblnr AND
*                            mjahr = ls_docs-mjahr.
*
*    ENDIF.
*
*  ENDLOOP.
*
*  COMMIT WORK AND WAIT.
*
*  MESSAGE 'Datos guardados'(003) TYPE 'S' .

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  PRINT_TRANSPORT_DOCUMENT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM print_transport_document .

  DATA: fm_name         TYPE rs38l_fnam,
        fp_docparams    TYPE sfpdocparams,
        fp_outputparams TYPE sfpoutputparams.


  DATA: lt_valuesnum   TYPE TABLE OF bapi1003_alloc_values_num,
        lt_valueschar  TYPE TABLE OF bapi1003_alloc_values_char,
        lt_valuescurr  TYPE TABLE OF bapi1003_alloc_values_curr,
        lt_return      TYPE bapiret2_t,
        ls_direccion   TYPE zstsd_albaran_dir,
        ls_dir_almacen TYPE zspp_direccion_almacen,
        ls_datos_cab   TYPE zspp_doc_transporte_cab,
        lt_posiciones  TYPE zspp_doc_transporte_pos_tt,
        ls_posiciones  LIKE LINE OF lt_posiciones,
        ls_usr01       TYPE usr01,
        ls_lopd        TYPE zstbc_textolargo.

  DATA: lv_diametro TYPE atflv,
        lv_longitud TYPE atflv,
        lv_peso     TYPE atflv,
        lv_motivo   TYPE atwrt,
        lv_adrnr    TYPE adrnr,
        lv_fsc      TYPE zesd_fsc,      "DTM
        lv_cod_cert TYPE zesd_cod_cert, "DTM
        lv_cert_fsc TYPE zcertif_fsc.   "DTM

  CONSTANTS: lc_objecttable TYPE tabelle VALUE 'MCH1',
             lc_bobina      TYPE klasse_d VALUE 'Z_BOBINA',
             lc_classtype   TYPE klassenart VALUE '023',
             lc_diametro    TYPE atnam VALUE 'Z_DIAMETRO',
             lc_longitud    TYPE atnam VALUE 'Z_LONGITUD_B',
             lc_peso        TYPE atnam VALUE 'Z_PESO',
             lc_motivo      TYPE atnam VALUE 'ZPPI_MOTIVO_REPROCESO_BOBINA'.

  PERFORM save_transport_data.

  lo_alv->get_selected_rows( IMPORTING et_row_no = DATA(lt_row) ).

  IF lt_row[] IS INITIAL.

    MESSAGE 'Seleccione una linea para imprimir'(004) TYPE 'S' DISPLAY LIKE 'W'.
    RETURN.

  ENDIF.

  READ TABLE lt_row INTO DATA(ls_row) INDEX 1.

  fp_outputparams-immexit = abap_true.
  fp_outputparams-reqimm  = abap_true.


  READ TABLE gt_docs INTO DATA(ls_docs) INDEX ls_row-row_id.



  CALL FUNCTION 'FP_JOB_OPEN'
    CHANGING
      ie_outputparams = fp_outputparams
    EXCEPTIONS
      cancel          = 1
      usage_error     = 2
      system_error    = 3
      internal_error  = 4
      OTHERS          = 5.

  IF sy-subrc <> 0.

    IF sy-subrc EQ 1.

*      MESSAGE 'Proceso cancelado' TYPE 'S'.

    ELSE.

      MESSAGE 'Error FP_JOB_OPEN' TYPE 'S' DISPLAY LIKE 'E'.

    ENDIF.

    EXIT.

  ENDIF.

  CALL FUNCTION 'FP_FUNCTION_MODULE_NAME'           "& Form Processing Generation
    EXPORTING
      i_name     = 'ZPPSF_DOC_TRANSPORTE'
    IMPORTING
      e_funcname = fm_name.



  SELECT SINGLE bukrs FROM t001k INTO @DATA(lv_bukrs) WHERE bwkey = @ls_docs-werks.

  SELECT SINGLE adrnr stceg INTO (ls_direccion-adrnr, ls_direccion-stceg)
    FROM t001
    WHERE bukrs = lv_bukrs.

  PERFORM address_data USING ls_direccion-adrnr CHANGING ls_direccion.

  CLEAR ls_dir_almacen.

  DATA(ls_dir_aux) = ls_direccion.

  SELECT SINGLE adrnr FROM twlad INTO lv_adrnr
                           WHERE werks = ls_docs-werks AND
                                 lgort = ls_docs-lgort.

  IF sy-subrc EQ 0.

    PERFORM address_data USING lv_adrnr CHANGING ls_direccion.

  ENDIF.

  ls_dir_almacen = CORRESPONDING #( ls_direccion ).

  ls_dir_almacen-lgort = ls_docs-lgort.
  ls_dir_almacen-lgobe = ls_direccion-name1.

  ls_direccion = ls_dir_aux.

  ls_datos_cab = CORRESPONDING #( ls_docs ).

  CONCATENATE 'ZLOPD_' ls_docs-werks INTO ls_lopd-tdname.
  ls_lopd-tdobject = 'TEXT'.
  ls_lopd-tdid     = 'ST'.
  ls_lopd-tdspras  = sy-langu.

*  CASE ls_docs-werks.
*    WHEN '3000'.
*      PERFORM obtener_logo USING ls_docs-werks CHANGING ls_datos_cab-logo.
*    WHEN '3020'.
*      PERFORM obtener_logo USING ls_docs-werks CHANGING ls_datos_cab-logo.
*  ENDCASE.

  CALL FUNCTION 'ZRECUPERA_LOGOS_SOCIEDAD'
    EXPORTING
      iv_bukrs = ls_docs-werks
    IMPORTING
      ev_path  = ls_datos_cab-logo.



  SELECT * FROM ztpp0029 INTO CORRESPONDING FIELDS OF TABLE gt_bobinas.

  "81925 - Albarán reproceso
  SELECT werks, matnr, charg, SUM( menge ) AS menge
    INTO TABLE @DATA(lt_mseg)
    FROM mseg
    WHERE mblnr = @ls_docs-mblnr AND
          mjahr = @ls_docs-mjahr AND
          lgort = @ls_docs-lgort
    GROUP BY werks, matnr, charg.

*  DELETE ADJACENT DUPLICATES FROM lt_mseg COMPARING matnr charg.

  LOOP AT lt_mseg INTO DATA(ls_mseg).

    CLEAR: ls_posiciones.
    ls_posiciones-matnr = ls_mseg-matnr.
    ls_posiciones-charg = ls_mseg-charg.

    SELECT SINGLE maktx INTO ls_posiciones-maktx FROM makt WHERE spras = sy-langu AND
                                                                 matnr = ls_mseg-matnr.


    DATA(lv_objectkey) = CONV objnum( |{ ls_mseg-matnr }{ ls_mseg-charg }| ).

    CALL FUNCTION 'BAPI_OBJCL_GETDETAIL'
      EXPORTING
        objectkey       = lv_objectkey
        objecttable     = lc_objecttable
        classnum        = lc_bobina
        classtype       = lc_classtype
      TABLES
        allocvaluesnum  = lt_valuesnum
        allocvalueschar = lt_valueschar
        allocvaluescurr = lt_valuescurr
        return          = lt_return.

    TRY.

        lv_diametro = lt_valuesnum[ charact = lc_diametro ]-value_from.
      CATCH cx_sy_itab_line_not_found.

    ENDTRY.

    TRY.

        lv_longitud = lt_valuesnum[ charact = lc_longitud ]-value_from.
      CATCH cx_sy_itab_line_not_found.

    ENDTRY.

    "81925 - Albarán reproceso
    lv_peso = ls_mseg-menge.
*    SELECT SINGLE clabs FROM mchb INTO lv_peso
*                       WHERE matnr = ls_mseg-matnr
*                         AND werks = ls_mseg-werks
*                         AND lgort = ls_docs-lgort
*                         AND charg = ls_mseg-charg.
*    IF sy-subrc NE 0.
*
*      MESSAGE text-024 TYPE 'I'.
**      PERFORM limpiar_campos.
*      RETURN.
*
*    ENDIF.

    TRY.

        CLEAR: lv_motivo.
        lv_motivo = lt_valueschar[ charact = lc_motivo ]-value_char.
      CATCH cx_sy_itab_line_not_found.

    ENDTRY.


    ls_posiciones-diametro = |{ lv_diametro DECIMALS = 0 }|.
    ls_posiciones-longitud = |{ lv_longitud DECIMALS = 0 }|.
    ls_posiciones-peso     = |{ lv_peso     DECIMALS = 0 }|.

    APPEND ls_posiciones-maktx TO ls_posiciones-descripciones.

* DTM - Se recupera el código de registro FSC

    CLEAR: lv_fsc.

    SELECT SINGLE * INTO @DATA(ls_ztsd0003)
      FROM ztsd0003
      WHERE bukrs EQ @lv_bukrs
        AND endda GE @sy-datum
        AND begda LE @sy-datum.

    IF ls_ztsd0003 IS NOT INITIAL.

      lv_fsc      = ls_ztsd0003-fsc.
      lv_cod_cert = ls_ztsd0003-cod_cert.
      CONDENSE lv_cod_cert.

      CONCATENATE lv_cod_cert ' FSC Recycled'
                  lv_fsc '%' INTO lv_cert_fsc SEPARATED BY space.

      "Se añade el texto del código FSC
      APPEND lv_cert_fsc TO ls_posiciones-descripciones.

    ENDIF.

* DTM

    IF lv_motivo IS NOT INITIAL.

      CONCATENATE 'Motivo:' lv_motivo INTO gs_bobinas-obsv SEPARATED BY space.
      APPEND gs_bobinas-obsv TO ls_posiciones-descripciones.

    ENDIF.

    READ TABLE gt_bobinas INTO
    gs_bobinas WITH KEY matnr = ls_mseg-matnr charg = ls_mseg-charg.

    IF sy-subrc EQ 0 AND gs_bobinas-obsv IS NOT INITIAL.

*      CLEAR ls_posiciones.
*      ls_posiciones-maktx = gs_bobinas-obsv.
*      APPEND ls_posiciones TO lt_posiciones.
      APPEND gs_bobinas-obsv TO  ls_posiciones-descripciones.

** CMS 29-04-2020 Si no hay observaciones de producción, buscamos nota insp. y si hay la trasladamos al ALV.
*    elseif gs_bobinas-obsv IS INITIAL.

    ENDIF.

    APPEND ls_posiciones TO lt_posiciones.

  ENDLOOP.

  CONDENSE ls_datos_cab-allowed_twgt.
  ls_datos_cab-allowed_twgt = |{ ls_datos_cab-allowed_twgt  } KG|.

  ls_datos_cab-tndr_crnm = |{ ls_datos_cab-tndr_trkid } { ls_datos_cab-tndr_crnm }|.

  fp_docparams-langu   = sy-langu.
  fp_docparams-country = 'ES'.

  IF ls_direccion-tel_number IS NOT INITIAL.

    ls_direccion-tel_number = |TEL: { ls_direccion-tel_number }|.

  ENDIF.

  ls_datos_cab-tknum = ls_docs-mblnr.

*  WRITE ls_datos_Cab-creation_date to ls_da

  TRANSLATE: ls_dir_almacen TO UPPER CASE,
             ls_direccion   TO UPPER CASE .


  CALL FUNCTION fm_name
    EXPORTING
      /1bcdwb/docparams    = fp_docparams
      posiciones           = lt_posiciones
      direccion_almacen    = ls_dir_almacen
      direccion            = ls_direccion
      datos_transporte_cab = ls_datos_cab
      texto_lopd           = ls_lopd
    EXCEPTIONS
      usage_error          = 1
      system_error         = 2
      internal_error       = 3.


  CALL FUNCTION 'FP_JOB_CLOSE'
*   IMPORTING
*     E_RESULT             =
    EXCEPTIONS
      usage_error    = 1
      system_error   = 2
      internal_error = 3
      OTHERS         = 4.
  IF sy-subrc <> 0.
* Implement suitable error handling here
  ENDIF.

ENDFORM.

FORM address_data USING p_adrnr CHANGING ps_direccion TYPE zstsd_albaran_dir.

  DATA: lv_adrnr TYPE adrnr,
        ls_adrc  TYPE adrc.

*  CLEAR: ps_direccion, ls_adrc, lv_adrnr.
*
*  SELECT SINGLE bukrs FROM t001k  INTO @DATA(lv_bukrs) WHERE bwkey = @p_werks.
*
*  SELECT SINGLE adrnr stceg INTO (ps_direccion-adrnr, ps_direccion-stceg)
*    FROM t001
*    WHERE bukrs = l_bukrs.
*
*  IF ps_direccion-adrnr IS NOT INITIAL.
  IF p_adrnr IS NOT INITIAL.
    ps_direccion-adrnr = p_adrnr.

    SELECT SINGLE * INTO ls_adrc
      FROM adrc
      WHERE addrnumber = ps_direccion-adrnr.

    IF ls_adrc IS NOT INITIAL.

      MOVE-CORRESPONDING ls_adrc TO ps_direccion.
      TRANSLATE ps_direccion-name1 TO UPPER CASE.
      " Dirección 1:
      CONDENSE: ps_direccion-street, ps_direccion-house_num1.
      IF ps_direccion-house_num1 IS NOT INITIAL.
        CONCATENATE ps_direccion-street ps_direccion-house_num1
                    INTO ps_direccion-direc_1 SEPARATED BY ', '.
      ELSE.
        WRITE ps_direccion-street TO ps_direccion-direc_1.
      ENDIF.
      TRANSLATE ps_direccion-direc_1 TO UPPER CASE.
      " Dirección 2:
      CONDENSE: ps_direccion-post_code1, ps_direccion-city1.
      CONCATENATE ps_direccion-post_code1 ps_direccion-city1
                INTO ps_direccion-direc_2 SEPARATED BY space.
      TRANSLATE ps_direccion-direc_2 TO UPPER CASE.
      IF ps_direccion-region IS NOT INITIAL.
        TRANSLATE ps_direccion-city1 TO UPPER CASE.
        SELECT SINGLE bezei INTO ps_direccion-bezei
          FROM t005u
          WHERE bland = ls_adrc-region
          AND spras = ls_adrc-langu
          AND land1 = ls_adrc-country.
        TRANSLATE ps_direccion-bezei TO UPPER CASE.
        IF ps_direccion-bezei IS NOT INITIAL AND ps_direccion-bezei NE ps_direccion-city1.
          CONCATENATE ps_direccion-direc_2 ps_direccion-bezei
                      INTO ps_direccion-direc_2 SEPARATED BY ' - '.
        ENDIF.
      ENDIF.
    ENDIF.
    " FAX:
    SELECT SINGLE fax_number INTO ps_direccion-fax_number
      FROM adr3
      WHERE addrnumber = ps_direccion-adrnr.
  ENDIF.

ENDFORM.
FORM obtener_logo  USING    p_werks
                   CHANGING logo TYPE xstringval.

*  CALL METHOD cl_ssf_xsf_utilities=>get_bds_graphic_as_bmp
*    EXPORTING
*      p_object       = 'GRAPHICS'
*      p_name         = p_nombre
*      p_id           = 'BMAP'
*      p_btype        = 'BCOL'
*    RECEIVING
*      p_bmp          = logo       " g_logo must be typed xstring
*    EXCEPTIONS
*      not_found      = 1
*      internal_error = 2
*      OTHERS         = 3.

  DATA l_file TYPE string.

  l_file = |/logos/{ p_werks }.jpg|.

  OPEN DATASET l_file FOR INPUT IN BINARY MODE.
  DATA:l_img TYPE xstring, l_hex TYPE x.
  DATA lt_img TYPE TABLE OF xstring.
  DO .

    READ DATASET  l_file INTO logo .
    IF sy-subrc EQ 0.
*    APPEND l_img to lt_img.
      EXIT.
    ELSE.
      EXIT.
    ENDIF.

  ENDDO.
  CLOSE DATASET l_file.

ENDFORM.                    " OBTENER_LOGO          " CARGAR_LOGO_URL

FORM obtener_logo_url  USING    p_bukrs
                      CHANGING p_logo_url.

  " Obtenemos la ruta del logo
  CONCATENATE 'E:\logos\' p_bukrs '.jpg' INTO p_logo_url.
  CONDENSE p_logo_url.

ENDFORM.                    " CARGAR_LOGO_URL
*&---------------------------------------------------------------------*
*&      Form  OBSV_CHARG
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM obsv_charg .



  lo_alv->get_selected_rows( IMPORTING et_row_no = DATA(lt_row) ).

  IF lt_row[] IS INITIAL.
    MESSAGE 'Seleccione una linea'(005) TYPE 'S' DISPLAY LIKE 'W'.
    RETURN.
  ENDIF.

  READ TABLE lt_row INTO DATA(ls_row) INDEX 1.

  READ TABLE gt_docs INTO gs_docs INDEX ls_row-row_id.
  CHECK sy-subrc EQ 0.

  SELECT * FROM ztpp0029 INTO CORRESPONDING FIELDS OF TABLE gt_bobinas
    WHERE mblnr = gs_docs-mblnr AND
                           mjahr = gs_docs-mjahr.
  IF sy-subrc NE 0.
    SELECT * FROM mseg
             INTO CORRESPONDING FIELDS OF TABLE gt_bobinas
                       WHERE mblnr = gs_docs-mblnr AND
                             mjahr = gs_docs-mjahr.

    SORT gt_bobinas BY matnr charg.
    DELETE ADJACENT DUPLICATES FROM gt_bobinas COMPARING matnr charg.
  ENDIF.

  IF gt_bobinas IS NOT INITIAL.
    LOOP AT gt_bobinas ASSIGNING FIELD-SYMBOL(<fs>).
      SELECT SINGLE maktx FROM makt INTO <fs>-maktx
        WHERE spras = sy-langu AND matnr = <fs>-matnr.
      IF <fs>-obsv IS INITIAL.
        PERFORM obtener_nota_insp_lote CHANGING <fs>.

      ENDIF.
    ENDLOOP.
  ELSE.
    MESSAGE 'Error:movimiento de material sin materiales'(006) TYPE 'S' DISPLAY LIKE 'E'.
    RETURN.
  ENDIF.

  CALL SCREEN 0300 STARTING AT 5 5 ENDING AT  130 20.


ENDFORM.

FORM obtener_nota_insp_lote USING ps_bob LIKE gs_bobinas.

  DATA lt_allocvaluesnum TYPE STANDARD TABLE OF
  bapi1003_alloc_values_num WITH HEADER LINE.
  DATA lt_allocvalueschar TYPE STANDARD TABLE OF
  bapi1003_alloc_values_char WITH HEADER LINE.
  DATA lt_allocvaluescurr TYPE STANDARD TABLE OF
  bapi1003_alloc_values_curr WITH HEADER LINE.
  DATA lt_return TYPE STANDARD TABLE OF bapiret2.
  DATA l_objectkey TYPE objnum.

  l_objectkey = |{ ps_bob-matnr }{ ps_bob-charg }|.


  CALL FUNCTION 'BAPI_OBJCL_GETDETAIL'
    EXPORTING
      objectkey       = l_objectkey
      objecttable     = 'MCH1'
      classnum        = 'Z_BOBINA'
      classtype       = '023'
    TABLES
      allocvaluesnum  = lt_allocvaluesnum
      allocvalueschar = lt_allocvalueschar
      allocvaluescurr = lt_allocvaluescurr
      return          = lt_return.

  DATA lv_diametro       TYPE atflv.
  DATA lv_longitud       TYPE atflv.

  TRY.
      ps_bob-obsv = lt_allocvalueschar[ charact = 'Z_NOTA_INSP' ]-value_char.
    CATCH cx_sy_itab_line_not_found.
  ENDTRY.

ENDFORM.

FORM obsv_generales.

  lo_alv->get_selected_rows( IMPORTING et_row_no = DATA(lt_row) ).

  IF lt_row[] IS INITIAL.
    MESSAGE 'Seleccione una linea'(005) TYPE 'S' DISPLAY LIKE 'W'.
    RETURN.
  ENDIF.

  READ TABLE lt_row INTO DATA(ls_row) INDEX 1.

  READ TABLE gt_docs INTO gs_docs INDEX ls_row-row_id.
  CHECK sy-subrc EQ 0.

  CALL SCREEN 0400 STARTING AT 5 5 ENDING AT  130 17.

ENDFORM.

* INI 06/06/2019 CMS Nuevo botón ELIMINAR:
* 1. Borramos el documento de transporte
* 2. Borramos tabla de transportes ztpp0027
* 3. Borramos tabla de observaciones de transportes ztpp0029
* 4. Borramos registro ALV y refrescamos

FORM eliminar.
  DATA: t_return    TYPE TABLE OF bapiret2.
  DATA: lv_answer    TYPE c.

  lo_alv->get_selected_rows( IMPORTING et_row_no = DATA(lt_row) ).
  IF lt_row[] IS INITIAL.
    MESSAGE 'Seleccione una linea'(005) TYPE 'S' DISPLAY LIKE 'W'.
    RETURN.
  ENDIF.
  DATA(ls_reg) = gt_docs[ lt_row[ 1 ]-row_id ].

*  REPLACE FIRST OCCURRENCE OF '&' IN  text-007 WITH ls_reg-mblnr.
  DATA(l_msg) = |¿Desea eliminar el documento { ls_reg-mblnr ALPHA = OUT }?|.

  CALL FUNCTION 'POPUP_TO_CONFIRM'
    EXPORTING
      titlebar              = text-008
*     DIAGNOSE_OBJECT       = ' '
      text_question         = l_msg
      text_button_1         = 'Sí' "(008)
*     ICON_BUTTON_1         = ' '
      text_button_2         = 'No' "(009)
*     ICON_BUTTON_2         = ' '
      default_button        = '2'
      display_cancel_button = ''
*     USERDEFINED_F1_HELP   = ' '
*     START_COLUMN          = 25
*     START_ROW             = 6
*     POPUP_TYPE            =
*     IV_QUICKINFO_BUTTON_1 = ' '
*     IV_QUICKINFO_BUTTON_2 = ' '
    IMPORTING
      answer                = lv_answer
*           TABLES
*     parameter             = lt_param
    EXCEPTIONS
      text_not_found        = 1
      OTHERS                = 2.
  IF sy-subrc <> 0.
*           Implement suitable error handling here
  ENDIF.

  CHECK lv_answer IS INITIAL OR lv_answer = '1'.

  CALL FUNCTION 'BAPI_GOODSMVT_CANCEL'
    EXPORTING
      materialdocument = ls_reg-mblnr
      matdocumentyear  = ls_reg-mjahr
*     GOODSMVT_PSTNG_DATE       =
*     GOODSMVT_PR_UNAME         =
* IMPORTING
*     GOODSMVT_HEADRET =
    TABLES
      return           = t_return
*     GOODSMVT_MATDOCITEM       =
    .
  IF line_exists( t_return[ type = 'E' ] ).
    DATA(wa) = t_return[ type = 'E' ].
    MESSAGE wa-message TYPE 'S' DISPLAY LIKE 'W'. " DISPLAY LIKE 'W'.
*    MESSAGE wa-message TYPE 'E'. " DISPLAY LIKE 'W'. " DISPLAY LIKE 'W'.
    RETURN.
  ELSE.
    CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
      EXPORTING
        wait = 'X'.

    DELETE FROM ztpp0027
    WHERE mblnr = ls_reg-mblnr  AND
          mjahr = ls_reg-mjahr .
    IF sy-subrc <> 0.
      MESSAGE 'Documento de transporte no encontrado para borrar' TYPE 'W' DISPLAY LIKE 'E'.
      RETURN.
    ENDIF.

* Borramos observaciones (si las hay)
    SELECT SINGLE *
      FROM ztpp0029
      INTO @DATA(ls_ztpp0029)
    WHERE mblnr = @ls_reg-mblnr  AND
          mjahr = @ls_reg-mjahr.
    IF sy-subrc = 0.
      DELETE FROM ztpp0029
      WHERE mblnr = ls_reg-mblnr  AND
            mjahr = ls_reg-mjahr.
    ENDIF.

    DELETE gt_docs INDEX lt_row[ 1 ]-row_id.
    lo_alv->refresh_table_display( ).
  ENDIF.

ENDFORM.
* FIN 06/06/2019 CMS Nuevo botón ELIMINAR:
