*&---------------------------------------------------------------------*
*&  Include           ZRPP0034_ALV
*&---------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      Form  MOSTRAR_ALV
*&---------------------------------------------------------------------*
FORM mostrar_alv.

  DATA: lti_fieldcat TYPE lvc_t_fcat,
        lti_exctool  TYPE ui_functions.

  DATA: ls_layout  TYPE lvc_s_layo,
        ls_variant TYPE disvariant.

* Preparamos el catalogo de campos
  PERFORM prepare_fielcat CHANGING lti_fieldcat.

* Preparamos la tabla para excluir botones
  PERFORM prepare_toolbar CHANGING lti_exctool.

* Preparamos el layout
  PERFORM alv_prepare_layout CHANGING ls_layout.

* Mostramos el ALV
  CALL METHOD g_grid->set_table_for_first_display
    EXPORTING
      i_buffer_active      = ' '
      i_bypassing_buffer   = 'X'
      is_layout            = ls_layout
      is_variant           = ls_variant
      i_save               = 'A'
      it_toolbar_excluding = lti_exctool
    CHANGING
      it_outtab            = gti_datos
      it_fieldcatalog      = lti_fieldcat.

* Volvemos a ajustar el ancho de las columnas
  PERFORM refrescar_alv.

ENDFORM.                    " MOSTRAR_ALV

*&---------------------------------------------------------------------*
*&      Form  REFRESCAR_ALV
*&---------------------------------------------------------------------*
FORM refrescar_alv .

  DATA: ls_layout TYPE lvc_s_layo,
        ls_stable TYPE lvc_s_stbl.

* Sacamos el layout del ALV
  CALL METHOD g_grid->get_frontend_layout
    IMPORTING
      es_layout = ls_layout.

* Ponemos de nuevo que optimice las columnas
*  ls_layout-cwidth_opt = 'X'.

* Cargamos de nuevo el layout
  CALL METHOD g_grid->set_frontend_layout
    EXPORTING
      is_layout = ls_layout.

* Refrescamos el ALV
  ls_stable-row = 'X'.
  ls_stable-col = 'X'.

  CALL METHOD g_grid->refresh_table_display
    EXPORTING
      is_stable = ls_stable.

ENDFORM.                    " REFRESCAR_ALV

*&---------------------------------------------------------------------*
*&      Form  PREPARE_FIELCAT
*&---------------------------------------------------------------------*
FORM prepare_fielcat  CHANGING cti_fieldcat TYPE lvc_t_fcat.

  DATA lr_rtti_struc TYPE REF TO cl_abap_structdescr.

  DATA: ls_fcat TYPE          lvc_s_fcat,
        aux     LIKE LINE OF  lr_rtti_struc->components,
        auxt    LIKE TABLE OF aux.

  DATA: dref TYPE REF TO data.

  FIELD-SYMBOLS: <fs> TYPE any.

* Now I want to build a field catalog
* First get your data structure into a field symbol
  CREATE DATA dref TYPE ty_datos.
  ASSIGN dref->* TO <fs>.

  lr_rtti_struc ?= cl_abap_structdescr=>describe_by_data( <fs> ).

  auxt[]  = lr_rtti_struc->components.

  LOOP AT auxt INTO aux.

    CLEAR ls_fcat.

    ls_fcat-fieldname = aux-name.
    ls_fcat-datatype  = aux-type_kind.
    ls_fcat-inttype   = aux-type_kind.
    ls_fcat-intlen    = aux-length.
    ls_fcat-decimals  = aux-decimals.
    ls_fcat-coltext   = aux-name.
    ls_fcat-lowercase = 'X'.

    CASE ls_fcat-fieldname.
      WHEN 'CENTRO'.
        ls_fcat-ref_field  = 'WERKS'.
        ls_fcat-ref_table  = 'MCHB'.
        ls_fcat-auto_value = 'X'.
        ls_fcat-f4availabl = ' '.
        ls_fcat-checktable = '!'.
*        ls_fcat-no_out     = 'X'.
        ls_fcat-outputlen  = 10.
        ls_fcat-coltext   = 'Centro'.
      WHEN 'MATERIAL'.
        ls_fcat-ref_field  = 'MATNR'.
        ls_fcat-ref_table  = 'MCHB'.
        ls_fcat-f4availabl = 'X'.
        ls_fcat-checktable = '!'.
*        ls_fcat-edit       = 'X'.
        ls_fcat-outputlen  = 20.
        ls_fcat-coltext   = 'Material'.
      WHEN 'LOTE'.
        ls_fcat-ref_field  = 'CHARG'.
        ls_fcat-ref_table  = 'MCHB'.
        ls_fcat-f4availabl = 'X'.
        ls_fcat-checktable = '!'.
*        ls_fcat-edit       = 'X'.
        ls_fcat-outputlen  = 20.
        ls_fcat-coltext   = 'Lote'.
      WHEN 'CANTIDAD'.
        ls_fcat-outputlen  = 15.
*        ls_fcat-edit       = 'X'.
        ls_fcat-coltext   = 'Cantidad'.
      WHEN 'ALMACEN' OR 'QTY_DISP' OR 'CLASS' OR 'MEINS'.
        ls_fcat-no_out = 'X'.
    ENDCASE.

    APPEND ls_fcat TO cti_fieldcat .

  ENDLOOP.

ENDFORM.                    " PREPARE_FIELCAT

*&---------------------------------------------------------------------*
*&      Form  PREPARE_TOOLBAR
*&---------------------------------------------------------------------*
FORM prepare_toolbar  CHANGING cti_exctool TYPE ui_functions.

* Quitamos la botonera del ALV editable EXCEPTO el boton ELIMINAR
*  APPEND cl_gui_alv_grid=>mc_fg_edit TO cti_exctool.
*  APPEND cl_gui_alv_grid=>mc_fc_loc_append_row    TO cti_exctool.
  APPEND cl_gui_alv_grid=>mc_fc_loc_copy          TO cti_exctool.
  APPEND cl_gui_alv_grid=>mc_fc_loc_copy_row      TO cti_exctool.
  APPEND cl_gui_alv_grid=>mc_fc_loc_cut           TO cti_exctool.
  APPEND cl_gui_alv_grid=>mc_fc_loc_insert_row    TO cti_exctool.
  APPEND cl_gui_alv_grid=>mc_fc_loc_move_row      TO cti_exctool.
  APPEND cl_gui_alv_grid=>mc_fc_loc_paste         TO cti_exctool.
  APPEND cl_gui_alv_grid=>mc_fc_loc_paste_new_row TO cti_exctool.
  APPEND cl_gui_alv_grid=>mc_fc_loc_undo          TO cti_exctool.
  APPEND cl_gui_alv_grid=>mc_fc_maximum           TO cti_exctool.
  APPEND cl_gui_alv_grid=>mc_fc_minimum           TO cti_exctool.
  APPEND cl_gui_alv_grid=>mc_fc_subtot            TO cti_exctool.
  APPEND cl_gui_alv_grid=>mc_fc_sum               TO cti_exctool.
  APPEND cl_gui_alv_grid=>mc_fg_sort              TO cti_exctool.
  APPEND cl_gui_alv_grid=>mc_fc_filter            TO cti_exctool.
  APPEND cl_gui_alv_grid=>mc_fc_detail            TO cti_exctool.
  APPEND cl_gui_alv_grid=>mc_fc_refresh           TO cti_exctool.
  APPEND cl_gui_alv_grid=>mc_fc_print             TO cti_exctool.
  APPEND cl_gui_alv_grid=>mc_fc_graph             TO cti_exctool.
  APPEND cl_gui_alv_grid=>mc_fc_views             TO cti_exctool.
  APPEND cl_gui_alv_grid=>mc_fc_info              TO cti_exctool.
  APPEND cl_gui_alv_grid=>mc_fc_check             TO cti_exctool.
  APPEND cl_gui_alv_grid=>mc_fc_average           TO cti_exctool.
  APPEND cl_gui_alv_grid=>mc_fc_find              TO cti_exctool.
  APPEND cl_gui_alv_grid=>mc_mb_export            TO cti_exctool.
  APPEND cl_gui_alv_grid=>mc_mb_variant           TO cti_exctool.

ENDFORM.                    " PREPARE_TOOLBAR

*&---------------------------------------------------------------------*
*&      Form  ALV_PREPARE_LAYOUT
*&---------------------------------------------------------------------*
FORM alv_prepare_layout CHANGING ls_lvc_s_layo TYPE lvc_s_layo.

* Para que muestre el ancho de las columnas optimizado
  ls_lvc_s_layo-edit = 'X'.
*  ls_lvc_s_layo-cwidth_opt = 'X'.
  ls_lvc_s_layo-sel_mode   = 'A'.
  ls_lvc_s_layo-info_fname = 'ROW_COLOR'.

ENDFORM.                    " ALV_PREPARE_LAYOUT

*&---------------------------------------------------------------------*
*&      Form  INI_TAB_ALV
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM ini_tab_alv.

  DATA: ls_datos TYPE ty_datos.

  CLEAR: ls_datos.

  REFRESH: gti_datos.

  ls_datos-centro = so_werks-low. "'4000'.

  APPEND ls_datos TO gti_datos.

ENDFORM.
