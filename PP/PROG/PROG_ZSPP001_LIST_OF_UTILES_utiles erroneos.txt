*&---------------------------------------------------------------------*
*& Report  ZSPP001_LIST_OF_UTILES
*&
*&---------------------------------------------------------------------*
*&
*&
*&---------------------------------------------------------------------*
REPORT zspp001_list_of_utiles.
DATA lv_matnr TYPE matnr.
SELECT-OPTIONS so_matnr FOR lv_matnr.

START-OF-SELECTION.

  SELECT t2~vornr, t3~aufnr, t4~matnr, t4~werks, pzlfh
    INTO TABLE @DATA(pt_util)
    FROM affh AS t1 INNER JOIN afvc AS t2 ON t1~aufpl EQ t2~aufpl AND t1~aplzl EQ t2~aplzl
    INNER JOIN afko AS t3 ON t2~aufpl EQ t3~aufpl
    INNER JOIN aufk AS t5 ON t3~aufnr EQ t5~aufnr
    INNER JOIN crvm_b AS t4 ON t1~objid EQ t4~objid AND t1~objty EQ t4~objty AND t5~werks EQ t4~werks
    WHERE matnr IN @so_matnr.

PERFORM show_alv USING pt_util 'Listado de útiles'.

FORM show_alv USING it_datos TYPE STANDARD TABLE
                  VALUE(i_title) TYPE lvc_title.

  DATA: l_alv_table   TYPE REF TO cl_salv_table.
  DATA: l_layout      TYPE REF TO cl_salv_layout.
  DATA: key           TYPE salv_s_layout_key.
  DATA: lr_selections TYPE REF TO cl_salv_selections.
  DATA: lo_display    TYPE REF TO cl_salv_display_settings.

  TRY.
      CALL METHOD cl_salv_table=>factory
        IMPORTING
          r_salv_table = l_alv_table
        CHANGING
          t_table      = it_datos.
    CATCH cx_salv_msg .
      RETURN.
  ENDTRY.

  l_layout = l_alv_table->get_layout( ).
  key-report = sy-repid.
  l_layout->set_key( key ).
  l_layout->set_save_restriction( if_salv_c_layout=>restrict_none ).


*... Set selection mode
  lr_selections = l_alv_table->get_selections( ).
  lr_selections->set_selection_mode( if_salv_c_selection_mode=>row_column ).


*... Enable Generic ALV functions
  DATA: l_functions TYPE REF TO cl_salv_functions_list.
  l_functions = l_alv_table->get_functions( ).
*gr_functions->set_default( ).
  l_functions->set_all( ).

  lo_display = l_alv_table->get_display_settings( ).
  lo_display->set_striped_pattern( 'X' ).

*   Title to ALV
  IF i_title IS NOT INITIAL.
    lo_display->set_list_header( i_title ).
  ENDIF.

  l_alv_table->display( ).

ENDFORM. " SHOW_ALV
