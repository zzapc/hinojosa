*&---------------------------------------------------------------------*
*& Report  ZRPP0079
*&
*&---------------------------------------------------------------------*
*& Mantenimiento de datos de residuos
*  09/09/2021 - CMS
*&---------------------------------------------------------------------*
REPORT zrpp0079.

INCLUDE zrpp0079_class.
*INCLUDE zrpp0078_class.

TABLES: mara , ztpp0059.

DATA: g_rc TYPE char2.

DATA: BEGIN OF gs_residuos,
        cir                 TYPE lifnr,
        desc_cir            TYPE  char40,
        dir                 TYPE zdir,
        ler                 TYPE zler,
        id_entrada_residuos TYPE zpp_num_entrada_residuos,
        ok_code             LIKE sy-tcode,
      END OF gs_residuos.

SELECTION-SCREEN BEGIN OF BLOCK b4 WITH FRAME TITLE text-003.

PARAMETER: p_werks LIKE ztpp0041-werks. "Centro
SELECTION-SCREEN SKIP 1.
PARAMETER: p_idreg LIKE ztpp0059-id_reg_vehiculo MATCHCODE OBJECT zhpp_reg_vehiculo. " obligatory.

SELECTION-SCREEN SKIP 1.
SELECTION-SCREEN END OF BLOCK b4.

INITIALIZATION.

  SELECT SINGLE parva FROM usr05
    INTO p_werks
    WHERE bname = sy-uname AND
          parid = 'WRK'.

AT SELECTION-SCREEN.

  IF p_werks IS INITIAL.
    MESSAGE 'Obligatorio informar centro' TYPE 'E'.
    RETURN.
  ENDIF.

  IF p_idreg IS INITIAL.
    MESSAGE 'Obligatorio informar tiquet de registro de vehículo' TYPE 'E'.
    RETURN.
  ENDIF.

START-OF-SELECTION.

  SELECT SINGLE trans_residuos , dir , ler
    INTO @DATA(ls_datos)
    FROM ztmm0006
    WHERE werks           = @p_werks AND
          id_reg_vehiculo = @p_idreg.

  IF sy-subrc <> 0.
    MESSAGE |Número de tiquet incorrecto| TYPE 'I' DISPLAY LIKE 'E'.
    EXIT.
  ELSE.
    gs_residuos     = CORRESPONDING #( ls_datos ).
    gs_residuos-cir = ls_datos-trans_residuos.
    SELECT SINGLE nombre
      INTO  @gs_residuos-desc_cir
      FROM ztpp0065
      WHERE tp_gestor = 'TR'       AND
            werks     = @p_werks  AND
            cir       = @gs_residuos-cir AND
            lvorm     = ' '.
  ENDIF.

  CALL SCREEN 0100.



FORM guardar_datos.


  UPDATE ztmm0006
    SET trans_residuos    = @gs_residuos-cir,
        ler               = @gs_residuos-ler,
        dir               = @gs_residuos-dir
    WHERE werks           = @p_werks AND
          id_reg_vehiculo = @p_idreg.
  IF sy-subrc <> 0.
    MESSAGE |Error al actualizar registro.| TYPE 'I' DISPLAY LIKE 'E'.
    g_rc = '01'.
  ELSE.
    g_rc = '00'.
    MESSAGE |Registro de vehículo { p_idreg ALPHA = OUT } actualizado correctamente.| TYPE 'S'.
  ENDIF.
ENDFORM.


FORM validar_cir.

  g_rc = '00'.

  SELECT SINGLE nombre
    INTO  @gs_residuos-desc_cir
    FROM ztpp0065
    WHERE tp_gestor = 'TR'       AND
          werks     = @p_werks  AND
          cir       = @gs_residuos-cir AND
          lvorm     = ' '.
  IF sy-subrc <> 0.
    CLEAR gs_residuos-desc_cir.
    MESSAGE |Código CIR incorrecto.| TYPE 'I' DISPLAY LIKE 'E'.
    g_rc = '01'.
  ENDIF.

ENDFORM.

FORM f4_trans_residuos.

  DATA: lt_return  TYPE TABLE OF ddshretval.
  DATA:lt_dynpro_fields TYPE STANDARD TABLE OF dynpread.

  SELECT  nombre , cir
    INTO TABLE @DATA(lt_transport)
    FROM ztpp0065
    WHERE tp_gestor = 'TR'       AND
          werks     = @p_werks  AND
          lvorm = ' '.


  CHECK lt_transport IS NOT INITIAL.

  CALL FUNCTION 'F4IF_INT_TABLE_VALUE_REQUEST'
    EXPORTING
      retfield        = 'GS_RESIDUOS-CIR'
      value_org       = 'S'
    TABLES
      value_tab       = lt_transport
      return_tab      = lt_return
    EXCEPTIONS
      parameter_error = 1
      no_values_found = 2
      OTHERS          = 3.

  IF sy-subrc = 0.
    TRY.
        DATA(ls_selec) = lt_return[ 1 ] .
        gs_residuos-cir = |{ ls_selec-fieldval ALPHA = OUT }|.

      CATCH cx_root .
    ENDTRY.

  ENDIF.

ENDFORM.

INCLUDE zrpp0079_status_0100o01.

INCLUDE zrpp0079_user_command_0100i01.
