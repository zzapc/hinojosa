*&---------------------------------------------------------------------*
*&  Include           ZPPRF001_CLASSES
*&---------------------------------------------------------------------*
CLASS zcl_app DEFINITION.
  PUBLIC SECTION.

    CONSTANTS: c_zpap            TYPE mtart VALUE 'ZPAP',
               c_zsem            TYPE mtart VALUE 'ZSEM',
               c_material_pulper TYPE matnr VALUE '000000000002001764'.

    DATA: matnr              TYPE matnr READ-ONLY,
          maktx              TYPE maktx READ-ONLY,
          mtart              TYPE mtart READ-ONLY,
          matkl              TYPE matkl READ-ONLY,
          gramaje            TYPE zzgramaje READ-ONLY,
          ancho              TYPE mara-zzancho READ-ONLY,
          barcode            TYPE char28 READ-ONLY,
          peso               TYPE p DECIMALS 3 READ-ONLY,
          peso_text          TYPE char28 READ-ONLY,
          peso_acumulado     TYPE p DECIMALS 3 READ-ONLY,
          peso_acumulado_txt TYPE char12 READ-ONLY,
          werks              TYPE werks_d READ-ONLY,
          lgort              TYPE lgort_d READ-ONLY,
          lgort_origen       TYPE lgort_d READ-ONLY,
          lgort_dstino       TYPE lgort_d,
          lgort_origen_txt   TYPE lgobe READ-ONLY,
          lgort_dstino_txt   TYPE lgobe READ-ONLY,
          kunnr              TYPE kunnr READ-ONLY,
          gramaje_ancho      TYPE char30 READ-ONLY,
          calidad            TYPE c LENGTH 16 READ-ONLY,
          lgobe              TYPE lgobe READ-ONLY,
          charg              TYPE charg_d READ-ONLY,
          decision_empleo    TYPE c LENGTH 13 READ-ONLY,
*          mostrar_botones    TYPE abap_bool READ-ONLY,
*          pagina             TYPE i READ-ONLY,
*          max_paginas        TYPE i READ-ONLY,
          lgort_active       TYPE abap_bool VALUE abap_true READ-ONLY,
*          dynpro             TYPE sy-dynnr READ-ONLY,
*          titulo_311         TYPE char25 READ-ONLY,
*          nueva_ubicacion    TYPE ztpp0023-zona_ubicacion READ-ONLY,
          ubicacion          TYPE ztpp0023-zona_ubicacion READ-ONLY.

    DATA classnum TYPE bapi1003_key-classnum.



    DATA:matnr_non_standard_paper TYPE matnr READ-ONLY,
         maktx_non_standard_paper TYPE maktx READ-ONLY,
         new_kunnr                TYPE kunnr READ-ONLY,
         new_kunnr_name           TYPE name1_gp READ-ONLY.

    TYPES: BEGIN OF t_bobinas_leidas,
             matnr           TYPE matnr,
             charg           TYPE charg_d,
             werks           TYPE werks_d,
             peso            TYPE char28,
             peso_real       TYPE mchb-clabs,
             kunnr           TYPE kunnr,
             decision_empleo TYPE c LENGTH 13,
             gramaje_ancho   TYPE char30,
             calidad         TYPE t023t-wgbez60,
             lgort           TYPE lgort_d,
           END OF t_bobinas_leidas.

    DATA tabla_bobinas_leidas TYPE TABLE OF t_bobinas_leidas.
    DATA: error_messages TYPE bapiret2_t.

    METHODS:
      constructor,

      borrar_campos_leidos,
      validar_bobina_zpap   EXCEPTIONS no_es_zpap,
      validar_bobina_pulper EXCEPTIONS no_es_pulper,
      imprimir_etiquetas_bobinas,
      anadir_bobina_a_lista,
      validar_almacen_gestion EXCEPTIONS error_almacen,
      cambiar_char_tabla_bobinas IMPORTING  charact   TYPE atnam
                                            new_value TYPE string
                                 EXCEPTIONS error_actualizar ,
      borrar_lista_bobinas,
      eliminar_bobina_tabla IMPORTING charg TYPE charg_d,
      cambiar_ubicacion IMPORTING  nueva_ubicacion TYPE zzona_ubicacion
                        EXCEPTIONS lote_incorrecto
                                   error_actualizar
                                   error_nueva_ubicacion
                                   error_ubicacion_almacen,

      traspasar_bobinas_almacen IMPORTING  p_lgort_destino TYPE lgort_d
                                           es_externo      TYPE abap_bool
                                EXCEPTIONS error_traspaso,

      convertir_tabla_bobinas_a_nsp IMPORTING  matnr_nsp TYPE matnr
                                    EXCEPTIONS error_movimiento,
      convertir_tabla_nsp_a_pulper "IMPORTING  matnr_nsp TYPE matnr
        EXCEPTIONS error_movimiento,

      leer_bobina IMPORTING  con_material TYPE abap_bool
                  CHANGING   bar_code     TYPE char28
                  EXCEPTIONS lote_sin_existencias
                             lote_incorrecto,
*                             err_cambio_cli,
      leer_bobina_hjnsp_a_pulper IMPORTING  con_material TYPE abap_bool
                                 CHANGING   bar_code     TYPE char28
                                 EXCEPTIONS lote_sin_existencias
                                            lote_incorrecto,

      limpiar_material_peso.
    .

  PRIVATE SECTION.

ENDCLASS.
CLASS zcl_app IMPLEMENTATION.
  METHOD constructor.

    SELECT SINGLE parva INTO me->werks FROM usr05
                                WHERE bname = sy-uname AND parid = 'WRK'.
  ENDMETHOD.

  METHOD borrar_campos_leidos.

    CLEAR:  matnr,
            maktx,
            mtart,
            matkl,
            gramaje,
            ancho,
            barcode,
            peso,
            peso_text,
*            peso_acumulado,
*            peso_acumulado_txt,
            lgort,
            lgort_origen,
            lgort_dstino,
            lgort_origen_txt,
            lgort_dstino_txt,
            kunnr,
            gramaje_ancho,
            calidad,
            lgobe,
            charg,
            decision_empleo,
            lgort_active,
            ubicacion,
            g_ubicacion.

    REFRESH error_messages.

  ENDMETHOD.
  METHOD eliminar_bobina_tabla.
    DELETE tabla_bobinas_leidas WHERE charg = charg.

    CLEAR me->peso_acumulado.

    LOOP AT tabla_bobinas_leidas INTO DATA(ls_bobinas).
      ADD ls_bobinas-peso_real TO me->peso_acumulado.
    ENDLOOP.

    me->peso_acumulado_txt = |{ me->peso_acumulado DECIMALS = 0 } KG|.
  ENDMETHOD.
  METHOD validar_bobina_zpap.
    IF me->mtart NE me->c_zpap.
      RAISE no_es_zpap.
    ENDIF.
  ENDMETHOD.

  METHOD validar_almacen_gestion.
    SELECT SINGLE lgort INTO @DATA(l_lgort_aux)
             FROM ztpp0024
            WHERE werks = @me->werks AND lgort = @me->lgort.
    IF sy-subrc NE 0.
      RAISE error_almacen.
    ENDIF.

  ENDMETHOD.
  METHOD validar_bobina_pulper.
    IF me->mtart NE me->c_zsem.
      RAISE no_es_pulper.
    ENDIF.
    IF me->matnr NE me->c_material_pulper.
      RAISE no_es_pulper.
    ENDIF.
  ENDMETHOD.



******************
  METHOD leer_bobina.

    DATA: BEGIN OF ls_matnr,
            matnr TYPE matnr,
            ersda TYPE ersda,
          END OF ls_matnr,
          lt_matnr  LIKE STANDARD TABLE OF ls_matnr,
          l_obj_key TYPE ausp-objek.
    DATA: lt_valuesnum  TYPE TABLE OF bapi1003_alloc_values_num,
          lt_valueschar TYPE TABLE OF bapi1003_alloc_values_char,
          lt_valuescurr TYPE TABLE OF bapi1003_alloc_values_curr,
          lt_return     TYPE bapiret2_t.

    borrar_campos_leidos( ).

    me->barcode = bar_code.

    TRANSLATE me->barcode TO UPPER CASE.
*   Obtener el numero de lote, puede que se lea con una extension posterior
*   o bien que introduzcan simplemente el lote a mano
    DATA(l_len) = strlen( me->barcode ).

    IF con_material EQ abap_true.
*     El codigo de barras es con formatp MATERIAL_CON_CEROS+LOTE
      IF l_len <= 10 .
        RAISE lote_incorrecto.
      ENDIF.
      DATA(l_len_charg) = l_len - 10.

      me->charg = me->barcode+l_len_charg(10).
      me->matnr = |{ me->barcode(l_len_charg) }|.
      me->matnr = |{ me->matnr ALPHA = IN }|.
    ELSE.
*     El codigo de barras es con formato LOTE+DIGITOS

      IF l_len > 10.
        l_len_charg   = l_len - 10.
        me->charg     = me->barcode(10).
      ELSE.
        me->charg     = me->barcode.
      ENDIF.

*     Determina el material como el ultimo material que aparece en la tabla
*     de maestros de lote MCHA

      SELECT mcha~matnr mcha~ersda
                   INTO TABLE lt_matnr
                   FROM mcha JOIN mara ON mcha~matnr = mara~matnr
                   WHERE mtart = 'ZPAP' AND werks = me->werks AND charg = me->charg
                   ORDER BY mcha~ersda_tmstp DESCENDING. "mcha~ersda DESCENDING.
      IF sy-subrc NE 0.
        RAISE lote_incorrecto.
      ENDIF.

      DATA(lt_matnr_aux) = lt_matnr.

      DELETE ADJACENT DUPLICATES FROM lt_matnr_aux COMPARING ersda.

      IF lines( lt_matnr_aux ) NE lines( lt_matnr ).
        "Significa que hay un material con fecha duplicada
        LOOP AT lt_matnr INTO ls_matnr.
          SELECT SINGLE @abap_true INTO @DATA(l_var) FROM  mara
                                   WHERE matnr = @ls_matnr-matnr AND
                                         mtart = 'ZPAP'.
          IF sy-subrc NE 0.
            DELETE lt_matnr WHERE matnr = ls_matnr-matnr.
          ENDIF.
        ENDLOOP.
      ENDIF.

      TRY.
          me->matnr = |{ lt_matnr[ 1 ]-matnr ALPHA = IN }| .
        CATCH cx_sy_itab_line_not_found.
          RAISE lote_incorrecto.
      ENDTRY.
    ENDIF.



*   Determina el peso y el almacen donde el par material/lote se encuentra
*   El peso es el stock libre en KG
    SELECT SINGLE lgort clabs
             INTO ( me->lgort, me->peso )
             FROM mchb
            WHERE matnr EQ me->matnr AND
                  charg EQ me->charg AND
                  werks EQ me->werks AND
                  clabs GT 0.
    IF sy-subrc EQ 0.
      me->peso_text = |{ me->peso DECIMALS = 0 } KG |.
    ELSE.

      RAISE lote_sin_existencias.
    ENDIF.

    SELECT SINGLE maktx INTO me->maktx FROM makt WHERE matnr = me->matnr AND spras = sy-langu.

*   Datos de la bobina que estane en el maestro
    SELECT SINGLE mtart matkl zzgramaje zzancho
                              FROM mara
                              INTO ( me->mtart, me->matkl, me->gramaje ,me->ancho )
                              WHERE matnr = me->matnr.

    IF me->mtart EQ 'ZSEM'.
      me->classnum    = 'Z_BOBINA_PULPER'.
    ELSE.
      me->classnum    = 'Z_BOBINA'.
    ENDIF.

*   Leer caracteeristicas del lote
    l_obj_key = |{ me->matnr }{ me->charg }|.

    CALL FUNCTION 'BAPI_OBJCL_GETDETAIL'
      EXPORTING
        objectkey       = l_obj_key
        objecttable     = 'MCH1'
        classnum        = me->classnum
        classtype       = '023'
      TABLES
        allocvaluesnum  = lt_valuesnum
        allocvalueschar = lt_valueschar
        allocvaluescurr = lt_valuescurr
        return          = lt_return.

    IF line_exists( lt_return[ type = 'E' ] ).
      me->error_messages = lt_return.
      RAISE lote_incorrecto.
    ENDIF.

    TRY.
        me->decision_empleo = lt_valueschar[ charact = 'LOBM_UDCODE' ]-value_char.
        me->decision_empleo = me->decision_empleo+9(1).

***        IF g_ind_bob_no_at = 'X' AND  ( me->decision_empleo = 'A' OR me->decision_empleo = ' ' ) .
***          RAISE err_cambio_cli.
***        ENDIF.
      CATCH cx_sy_itab_line_not_found.
    ENDTRY.
    TRY.
        me->kunnr           = lt_valueschar[ charact = 'Z_CLIENTE' ]-value_char.
      CATCH cx_sy_itab_line_not_found.
    ENDTRY.

    TRY.
        me->ubicacion         = lt_valueschar[ charact = 'Z_UBIC_HPAP' ]-value_char.
      CATCH cx_sy_itab_line_not_found.
    ENDTRY.

*   Texto de la calidad
    SELECT SINGLE wgbez60
             FROM t023t
             INTO me->calidad
            WHERE matkl = me->matkl AND spras = sy-langu.

    me->gramaje_ancho = |{ me->gramaje }/{ me->ancho }|.

    CLEAR bar_code.

  ENDMETHOD.
******************

  METHOD leer_bobina_hjnsp_a_pulper.

    DATA: BEGIN OF ls_matnr,
            matnr TYPE matnr,
            ersda TYPE ersda,
          END OF ls_matnr,
          lt_matnr  LIKE STANDARD TABLE OF ls_matnr,
          l_obj_key TYPE ausp-objek.
    DATA: lt_valuesnum  TYPE TABLE OF bapi1003_alloc_values_num,
          lt_valueschar TYPE TABLE OF bapi1003_alloc_values_char,
          lt_valuescurr TYPE TABLE OF bapi1003_alloc_values_curr,
          lt_return     TYPE bapiret2_t.

    borrar_campos_leidos( ).

    me->barcode = bar_code.

    TRANSLATE me->barcode TO UPPER CASE.
*   Obtener el numero de lote, puede que se lea con una extension posterior
*   o bien que introduzcan simplemente el lote a mano
    DATA(l_len) = strlen( me->barcode ).
    IF l_len = 10.
      me->barcode = |{ gs_dyn450-matnr_origen ALPHA = IN }{ me->barcode }|.
    ENDIF.

* Volver a calcular la longitud
    l_len = strlen( me->barcode ).

    IF con_material EQ abap_true.
*     El codigo de barras es con formatp MATERIAL_CON_CEROS+LOTE
      IF l_len <= 10 .
        RAISE lote_incorrecto.
      ENDIF.
      DATA(l_len_charg) = l_len - 10.

      me->charg = me->barcode+l_len_charg(10).
*      me->matnr = |{ me->barcode(l_len_charg) }|.
*      me->matnr = |{ me->matnr ALPHA = IN }|.
      me->matnr = gs_dyn450-matnr_origen.
    ELSE.
*     El codigo de barras es con formato LOTE+DIGITOS

      IF l_len > 10.
        l_len_charg   = l_len - 10.
        me->charg     = me->barcode(10).
      ELSE.
        me->charg     = me->barcode.
      ENDIF.

*     Determina el material como el ultimo material que aparece en la tabla
*     de maestros de lote MCHA

      SELECT mcha~matnr mcha~ersda
                   INTO TABLE lt_matnr
                   FROM mcha JOIN mara ON mcha~matnr = mara~matnr
                   WHERE mtart = 'ZPAP' AND werks = me->werks AND charg = me->charg
                   ORDER BY mcha~ersda_tmstp DESCENDING. "mcha~ersda DESCENDING.
      IF sy-subrc NE 0.
        RAISE lote_incorrecto.
      ENDIF.

      DATA(lt_matnr_aux) = lt_matnr.

      DELETE ADJACENT DUPLICATES FROM lt_matnr_aux COMPARING ersda.

      IF lines( lt_matnr_aux ) NE lines( lt_matnr ).
        "Significa que hay un material con fecha duplicada
        LOOP AT lt_matnr INTO ls_matnr.
          SELECT SINGLE @abap_true INTO @DATA(l_var) FROM  mara
                                   WHERE matnr = @ls_matnr-matnr AND
                                         mtart = 'ZPAP'.
          IF sy-subrc NE 0.
            DELETE lt_matnr WHERE matnr = ls_matnr-matnr.
          ENDIF.
        ENDLOOP.
      ENDIF.

      TRY.
          me->matnr = |{ lt_matnr[ 1 ]-matnr ALPHA = IN }| .
        CATCH cx_sy_itab_line_not_found.
          RAISE lote_incorrecto.
      ENDTRY.
    ENDIF.



*   Determina el peso y el almacen donde el par material/lote se encuentra
*   El peso es el stock libre en KG
    SELECT SINGLE lgort clabs
             INTO ( me->lgort, me->peso )
             FROM mchb
            WHERE matnr EQ me->matnr AND
                  charg EQ me->charg AND
                  werks EQ me->werks AND
                  clabs GT 0.
    IF sy-subrc EQ 0.
      me->peso_text = |{ me->peso DECIMALS = 0 } KG |.
    ELSE.

      RAISE lote_sin_existencias.
    ENDIF.

    SELECT SINGLE maktx INTO me->maktx FROM makt WHERE matnr = me->matnr AND spras = sy-langu.

*   Datos de la bobina que estane en el maestro
    SELECT SINGLE mtart matkl zzgramaje zzancho
                              FROM mara
                              INTO ( me->mtart, me->matkl, me->gramaje ,me->ancho )
                              WHERE matnr = me->matnr.

    IF me->mtart EQ 'ZSEM'.
      me->classnum    = 'Z_BOBINA_PULPER'.
    ELSE.
      me->classnum    = 'Z_BOBINA'.
    ENDIF.

*   Leer caracteeristicas del lote
    l_obj_key = |{ me->matnr }{ me->charg }|.

    CALL FUNCTION 'BAPI_OBJCL_GETDETAIL'
      EXPORTING
        objectkey       = l_obj_key
        objecttable     = 'MCH1'
        classnum        = me->classnum
        classtype       = '023'
      TABLES
        allocvaluesnum  = lt_valuesnum
        allocvalueschar = lt_valueschar
        allocvaluescurr = lt_valuescurr
        return          = lt_return.

    IF line_exists( lt_return[ type = 'E' ] ).
      me->error_messages = lt_return.
      RAISE lote_incorrecto.
    ENDIF.

    TRY.
        me->decision_empleo = lt_valueschar[ charact = 'LOBM_UDCODE' ]-value_char.
        me->decision_empleo = me->decision_empleo+9(1).
      CATCH cx_sy_itab_line_not_found.
    ENDTRY.
    TRY.
        me->kunnr           = lt_valueschar[ charact = 'Z_CLIENTE' ]-value_char.
      CATCH cx_sy_itab_line_not_found.
    ENDTRY.

    TRY.
        me->ubicacion         = lt_valueschar[ charact = 'Z_UBIC_HPAP' ]-value_char.
      CATCH cx_sy_itab_line_not_found.
    ENDTRY.

*   Texto de la calidad
    SELECT SINGLE wgbez60
             FROM t023t
             INTO me->calidad
            WHERE matkl = me->matkl AND spras = sy-langu.

    me->gramaje_ancho = |{ me->gramaje }/{ me->ancho }|.

    CLEAR bar_code.

  ENDMETHOD.
  METHOD anadir_bobina_a_lista.
*   Añade la ultima bobina leida a una tabla de bobinas
    CHECK NOT line_exists( tabla_bobinas_leidas[ charg = me->charg ] ).
    APPEND VALUE t_bobinas_leidas(
               matnr         = me->matnr
               charg         = me->charg
               werks         = me->werks
               peso          = me->peso_text
               peso_real     = me->peso
               kunnr         = me->kunnr
               decision_empleo = me->decision_empleo
               gramaje_ancho = me->gramaje_ancho
               calidad       = me->calidad
               lgort         = me->lgort
           ) TO me->tabla_bobinas_leidas.

*   Acumular pesos leidos -> necesario para reproceso externo
    ADD me->peso TO me->peso_acumulado.

    me->peso_acumulado_txt = |{ me->peso_acumulado DECIMALS = 0 } KG|.

  ENDMETHOD.
  METHOD borrar_lista_bobinas.
    REFRESH me->tabla_bobinas_leidas.
    me->peso_acumulado     = 0.
    me->peso_acumulado_txt = |{ me->peso_acumulado DECIMALS = 0 } KG|.
  ENDMETHOD.
  METHOD traspasar_bobinas_almacen.
*   Realiza traspasos de las bobinas leidas al almacen correspondiente

    DATA: ls_header TYPE bapi2017_gm_head_01,
          ##NEEDED
          l_mblnr   TYPE bapi2017_gm_head_ret-mat_doc,
          l_mjahr   TYPE gjahr,
          lt_item   TYPE TABLE OF bapi2017_gm_item_create,
          ls_item   TYPE bapi2017_gm_item_create,
          lt_return TYPE TABLE OF bapiret2.

*   Acumular todos los movimientos de bobina para la bapi de creacion de movs
    LOOP AT tabla_bobinas_leidas INTO DATA(ls_bobinas).

      ls_item-material    = |{ ls_bobinas-matnr ALPHA = IN }|.
      ls_item-batch       = ls_bobinas-charg.
      ls_item-plant       = me->werks.
      ls_item-stge_loc    = ls_bobinas-lgort.
      ls_item-move_type   = '311'.
      ls_item-entry_qnt   = ls_bobinas-peso_real.

      SELECT SINGLE meins FROM mara INTO @DATA(l_meins)
                                    WHERE matnr EQ @ls_bobinas-matnr AND
                                          lvorm EQ @space.

      ls_item-entry_uom   = l_meins.
      ls_item-move_stloc  = p_lgort_destino.
      APPEND ls_item TO lt_item.
    ENDLOOP.


    ls_header-pstng_date = sy-datum.
    ls_header-doc_date   = sy-datum.
    ls_header-header_txt = 'ZPPRF'.

*   Crea el 311
    CALL FUNCTION 'BAPI_GOODSMVT_CREATE'
      EXPORTING
        goodsmvt_header  = ls_header
        goodsmvt_code    = CONV bapi2017_gm_code( '04' )
      IMPORTING
        materialdocument = l_mblnr
        matdocumentyear  = l_mjahr
      TABLES
        goodsmvt_item    = lt_item
        return           = lt_return.

    IF line_exists( lt_return[ type = 'E' ] ).
      CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
      me->error_messages = lt_return.
      RAISE error_traspaso.
    ENDIF.

    CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
      EXPORTING
        wait = 'X'.

*   Si el traspaso es a un almacen de reproceso externo se graba un
*   registro en la tabal ZTPP0027 para luego poder completar los datos
*   de transporte con la transaccion ZPP0052

    IF es_externo EQ abap_true.
      DATA ls_ztpp0027 TYPE ztpp0027.

      LOOP AT lt_bobinas INTO ls_bobinas.
        ADD ls_bobinas-peso_real TO  ls_ztpp0027-allowed_twgt .
      ENDLOOP.


      ls_ztpp0027-creation_date = sy-datum.
      ls_ztpp0027-creation_time = sy-uzeit.
      ls_ztpp0027-mblnr         = l_mblnr.
      ls_ztpp0027-mjahr         = l_mjahr.
      ls_ztpp0027-werks         = g_werks.
      ls_ztpp0027-meins         = 'KG'.
      ls_ztpp0027-lgort         = g_lgort_dstino.

      MODIFY ztpp0027 FROM ls_ztpp0027.
    ENDIF.

    borrar_campos_leidos( ).
    borrar_lista_bobinas( ).

  ENDMETHOD.
  METHOD cambiar_ubicacion .


    DATA: l_obj_key TYPE ausp-objek.

    DATA: lt_valuesnum  TYPE TABLE OF bapi1003_alloc_values_num,
          lt_valueschar TYPE TABLE OF bapi1003_alloc_values_char,
          ls_valueschar LIKE LINE OF lt_valueschar,
          lt_valuescurr TYPE TABLE OF bapi1003_alloc_values_curr,
          lt_return     TYPE bapiret2_t.

    DATA ls_ubicacion TYPE ztpp0023.

    SELECT SINGLE * FROM ztpp0023 INTO ls_ubicacion
                    WHERE werks          = me->werks AND
                          zona_ubicacion = nueva_ubicacion AND
                          tipo_ubicacion = ''.
    IF sy-subrc NE 0.
      RAISE error_nueva_ubicacion.
    ENDIF.

    IF ls_ubicacion-lgort NE me->lgort.
      RAISE error_ubicacion_almacen.
    ENDIF.


    l_obj_key = |{ g_matnr ALPHA = IN }{ g_charg }|.


    CALL FUNCTION 'BAPI_OBJCL_GETDETAIL'
      EXPORTING
        objectkey       = l_obj_key
        objecttable     = 'MCH1'
        classnum        = me->classnum
        classtype       = '023'
      TABLES
        allocvaluesnum  = lt_valuesnum
        allocvalueschar = lt_valueschar
        allocvaluescurr = lt_valuescurr
        return          = lt_return.

    IF line_exists( lt_return[ type = 'E' ] ).
      RAISE lote_incorrecto.
    ENDIF.

    READ TABLE lt_valueschar ASSIGNING FIELD-SYMBOL(<fs>) WITH KEY charact = 'Z_UBIC_HPAP'.
    IF sy-subrc EQ 0.
      <fs>-value_char = <fs>-value_neutral = g_nueva_ubicacion.
    ELSE.
      ls_valueschar-charact    = 'Z_UBIC_HPAP'.
      ls_valueschar-value_char = ls_valueschar-value_neutral = g_nueva_ubicacion.
      APPEND ls_valueschar TO lt_valueschar.
    ENDIF.

    CALL FUNCTION 'BAPI_OBJCL_CHANGE'
      EXPORTING
        objectkey          = l_obj_key
        objecttable        = 'MCH1'
        classnum           = me->classnum
        classtype          = '023'
      TABLES
        allocvaluesnumnew  = lt_valuesnum
        allocvaluescharnew = lt_valueschar
        allocvaluescurrnew = lt_valuescurr
        return             = lt_return.
    IF line_exists( lt_return[ type = 'E' ] ).
      DATA(ls_return) = lt_return[ type = 'E' ] .
      APPEND ls_return TO error_messages.
      RAISE error_actualizar.
    ENDIF.

    COMMIT WORK AND WAIT.

  ENDMETHOD.
  METHOD cambiar_char_tabla_bobinas.
    DATA: l_obj_key TYPE ausp-objek.

    DATA: lt_valuesnum      TYPE TABLE OF bapi1003_alloc_values_num,
          lt_valueschar     TYPE TABLE OF bapi1003_alloc_values_char,
          ls_valueschar     LIKE LINE OF lt_valueschar,
          lt_valuescurr     TYPE TABLE OF bapi1003_alloc_values_curr,
          lt_return         TYPE TABLE OF bapiret2,
          ls_bobinas_leidas LIKE LINE OF tabla_bobinas_leidas,
          lv_objectkey      TYPE cuobn.

    DATA l_error TYPE abap_bool.

    LOOP AT tabla_bobinas_leidas INTO DATA(ls_bobinas).

      l_obj_key = |{ ls_bobinas-matnr ALPHA = IN }{ ls_bobinas-charg }|.

*
      CALL FUNCTION 'BAPI_OBJCL_GETDETAIL'
        EXPORTING
          objectkey       = l_obj_key
          objecttable     = 'MCH1'
          classnum        = 'Z_BOBINA'
          classtype       = '023'
        TABLES
          allocvaluesnum  = lt_valuesnum
          allocvalueschar = lt_valueschar
          allocvaluescurr = lt_valuescurr
          return          = lt_return.



      READ TABLE lt_valueschar ASSIGNING FIELD-SYMBOL(<fs>) WITH KEY charact = charact.
      IF sy-subrc NE 0.
        ls_valueschar-charact    = charact.
        ls_valueschar-value_char = ls_valueschar-value_neutral = new_value.
        APPEND ls_valueschar TO lt_valueschar.
      ELSE.
        <fs>-value_char = <fs>-value_neutral = new_value.
      ENDIF.

      CALL FUNCTION 'BAPI_OBJCL_CHANGE'
        EXPORTING
          objectkey          = l_obj_key
          objecttable        = 'MCH1'
          classnum           = 'Z_BOBINA'
          classtype          = '023'
        TABLES
          allocvaluesnumnew  = lt_valuesnum
          allocvaluescharnew = lt_valueschar
          allocvaluescurrnew = lt_valuescurr
          return             = lt_return.

      IF  line_exists( lt_return[ type = 'E' ] ).
        l_error = abap_true.
      ENDIF.

    ENDLOOP.

    IF l_error EQ abap_false.

      CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
        EXPORTING
          wait = 'X'.

"EAB T76941 28.06.2023 Integración E80: se llama a la función de envio de datos de bobina a E80 para cada cambio de cliente en las bobianas
      LOOP AT tabla_bobinas_leidas INTO ls_bobinas_leidas.

        CONCATENATE ls_bobinas_leidas-matnr ls_bobinas_leidas-charg INTO lv_objectkey.

        CALL FUNCTION 'Z_E80_ENVIO_STOCK_BOBINA'
          EXPORTING
            i_werks          = g_werks
*           I_CHARG          =
            i_objek          = lv_objectkey
            i_par_origen     = '5'
          EXCEPTIONS
            lote_inexistente = 1
            OTHERS           = 2.
        IF sy-subrc <> 0.
* Implement suitable error handling here
        ENDIF.

      ENDLOOP.

*      MESSAGE 'Cambio de cliente realizado'(500) TYPE 'S'.

*      CALL SCREEN 515.
      SET SCREEN 515.

*      IF g_imprimir_515 = 'X'.
*
*        imprimir_etiquetas_bobinas( ).
*
*      ENDIF.
*
*      borrar_lista_bobinas( ).

    ELSE.
      RAISE error_actualizar.
    ENDIF.

  ENDMETHOD.

  METHOD convertir_tabla_bobinas_a_nsp .

    DATA: l_obj_key TYPE ausp-objek.

    DATA: lt_valuesnum  TYPE TABLE OF bapi1003_alloc_values_num,
          lt_valueschar TYPE TABLE OF bapi1003_alloc_values_char,
          ls_valueschar LIKE LINE OF lt_valueschar,
          lt_valuescurr TYPE TABLE OF bapi1003_alloc_values_curr.

    DATA: ls_header TYPE bapi2017_gm_head_01,
          ##NEEDED
          l_mblnr   TYPE bapi2017_gm_head_ret-mat_doc,
          l_mjahr   TYPE gjahr,
          lt_item   TYPE TABLE OF bapi2017_gm_item_create,
          ls_item   TYPE bapi2017_gm_item_create,
          lt_return TYPE TABLE OF bapiret2.

    LOOP AT tabla_bobinas_leidas INTO DATA(ls_bobinas).

      ls_item-material    = |{ ls_bobinas-matnr ALPHA = IN }|.
      ls_item-batch       = ls_bobinas-charg.
      ls_item-plant       = ls_bobinas-werks.
*      ls_item-stge_loc    = '1000'.
      ls_item-stge_loc    = '4005'.
      ls_item-move_type   = '309'.
      ls_item-entry_qnt   = ls_bobinas-peso_real.

      SELECT SINGLE meins FROM mara INTO @DATA(l_meins)
                                    WHERE matnr EQ @ls_bobinas-matnr AND
                                          lvorm EQ @space.

      ls_item-entry_uom   = l_meins.
      ls_item-move_stloc  = '1040'.
      ls_item-move_batch  = ls_bobinas-charg.
      ls_item-move_mat    = |{ matnr_nsp ALPHA = IN }|.
      APPEND ls_item TO lt_item.

    ENDLOOP.


    ls_header-pstng_date = sy-datum.
    ls_header-doc_date   = sy-datum.
    ls_header-header_txt = 'ZPPRF'.


    CALL FUNCTION 'BAPI_GOODSMVT_CREATE'
      EXPORTING
        goodsmvt_header  = ls_header
        goodsmvt_code    = CONV bapi2017_gm_code( '04' )
      IMPORTING
        materialdocument = l_mblnr
        matdocumentyear  = l_mjahr
      TABLES
        goodsmvt_item    = lt_item
        return           = lt_return.


    IF line_exists( lt_return[ type = 'E' ] ).
      CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
      me->error_messages = lt_return.
      RAISE error_movimiento.
    ELSE.

      COMMIT WORK AND WAIT.


      LOOP AT tabla_bobinas_leidas INTO ls_bobinas.

        l_obj_key = |{ matnr_nsp ALPHA = IN }{ ls_bobinas-charg }|.

*
        CALL FUNCTION 'BAPI_OBJCL_GETDETAIL'
          EXPORTING
            objectkey       = l_obj_key
            objecttable     = 'MCH1'
            classnum        = 'Z_BOBINA'
            classtype       = '023'
          TABLES
            allocvaluesnum  = lt_valuesnum
            allocvalueschar = lt_valueschar
            allocvaluescurr = lt_valuescurr
            return          = lt_return.



        READ TABLE lt_valueschar ASSIGNING FIELD-SYMBOL(<fs>) WITH KEY charact = 'LOBM_UDCODE'.
        IF sy-subrc NE 0.
          ls_valueschar-charact    = 'LOBM_UDCODE'.
          ls_valueschar-value_char = ls_valueschar-value_neutral = 'DE       R'.
          APPEND ls_valueschar TO lt_valueschar.
        ELSE.
          <fs>-value_char = <fs>-value_neutral = 'DE       R'.
        ENDIF.

        READ TABLE lt_valueschar ASSIGNING <fs> WITH KEY charact = 'Z_CLIENTE'.
        IF sy-subrc NE 0.
          ls_valueschar-charact    = 'Z_CLIENTE'.
          ls_valueschar-value_char = ls_valueschar-value_neutral = |C{ me->werks }|.
          APPEND ls_valueschar TO lt_valueschar.
        ELSE.
          <fs>-value_char = <fs>-value_neutral = |C{ me->werks }|.
        ENDIF.


        CALL FUNCTION 'BAPI_OBJCL_CHANGE'
          EXPORTING
            objectkey          = l_obj_key
            objecttable        = 'MCH1'
            classnum           = 'Z_BOBINA'
            classtype          = '023'
          TABLES
            allocvaluesnumnew  = lt_valuesnum
            allocvaluescharnew = lt_valueschar
            allocvaluescurrnew = lt_valuescurr
            return             = lt_return.

        COMMIT WORK AND WAIT.
      ENDLOOP.



    ENDIF.
  ENDMETHOD.

  METHOD convertir_tabla_nsp_a_pulper .

    DATA: l_obj_key TYPE ausp-objek.

    DATA: lt_valuesnum  TYPE TABLE OF bapi1003_alloc_values_num,
          lt_valueschar TYPE TABLE OF bapi1003_alloc_values_char,
          ls_valueschar LIKE LINE OF lt_valueschar,
          lt_valuescurr TYPE TABLE OF bapi1003_alloc_values_curr.

    DATA: ls_header TYPE bapi2017_gm_head_01,
          ##NEEDED
          l_mblnr   TYPE bapi2017_gm_head_ret-mat_doc,
          l_mjahr   TYPE gjahr,
          lt_item   TYPE TABLE OF bapi2017_gm_item_create,
          ls_item   TYPE bapi2017_gm_item_create,
          lt_return TYPE TABLE OF bapiret2.

    LOOP AT tabla_bobinas_leidas INTO DATA(ls_bobinas).

      ls_item-material    = |{ ls_bobinas-matnr ALPHA = IN }|.
      ls_item-batch       = ls_bobinas-charg.
      ls_item-plant       = ls_bobinas-werks.
      ls_item-stge_loc    = '1040'.
      ls_item-move_type   = '309'.
      ls_item-entry_qnt   = ls_bobinas-peso_real.
      ls_item-entry_uom   = 'KG'.
      ls_item-move_stloc  = '1000'.
      ls_item-move_batch  = ls_bobinas-charg.
      ls_item-move_mat    = |{ me->c_material_pulper ALPHA = IN }|. " convertir a PULPER
      APPEND ls_item TO lt_item.

    ENDLOOP.


    ls_header-pstng_date = sy-datum.
    ls_header-doc_date   = sy-datum.
    ls_header-header_txt = 'ZPPRF'.


    CALL FUNCTION 'BAPI_GOODSMVT_CREATE'
      EXPORTING
        goodsmvt_header  = ls_header
        goodsmvt_code    = CONV bapi2017_gm_code( '04' )
      IMPORTING
        materialdocument = l_mblnr
        matdocumentyear  = l_mjahr
      TABLES
        goodsmvt_item    = lt_item
        return           = lt_return.


    IF line_exists( lt_return[ type = 'E' ] ).
      CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
      me->error_messages = lt_return.
      RAISE error_movimiento.
    ELSE.

      COMMIT WORK AND WAIT.

    ENDIF.
  ENDMETHOD.


  METHOD imprimir_etiquetas_bobinas.
    DATA: l_obj_key TYPE ausp-objek.
    DATA: ls_etiqueta TYPE zspp_etiq_bobina,
          l_padest    TYPE rspopname.

    DATA: lt_valuesnum  TYPE TABLE OF bapi1003_alloc_values_num,
          lt_valueschar TYPE TABLE OF bapi1003_alloc_values_char,
          ls_valueschar LIKE LINE OF lt_valueschar,
          lt_valuescurr TYPE TABLE OF bapi1003_alloc_values_curr,
          lt_return     TYPE bapiret2_t.


*    SELECT SINGLE padest INTO l_padest
*           FROM ztpp0007 WHERE werks = me->werks AND uname = sy-uname.

    l_padest = zcl_utils_hpap_pp=>determinar_disp_impresion( ).

    LOOP AT tabla_bobinas_leidas INTO DATA(ls_bobinas).

      l_obj_key = |{ ls_bobinas-matnr ALPHA = IN }{ ls_bobinas-charg }|.

      CALL FUNCTION 'BAPI_OBJCL_GETDETAIL'
        EXPORTING
          objectkey       = l_obj_key
          objecttable     = 'MCH1'
          classnum        = 'Z_BOBINA'
          classtype       = '023'
        TABLES
          allocvaluesnum  = lt_valuesnum
          allocvalueschar = lt_valueschar
          allocvaluescurr = lt_valuescurr
          return          = lt_return.

      LOOP AT lt_valueschar  INTO ls_valueschar.
        CASE ls_valueschar-charact.
          WHEN 'Z_CLIENTE'.
            IF ls_etiqueta-kunnr IS INITIAL.
              CONDENSE ls_valueschar-value_char.
              ls_etiqueta-kunnr = ls_valueschar-value_char.
            ENDIF.
          WHEN 'Z_PEDIDO'.
            IF ls_etiqueta-pedido IS INITIAL.
              CONDENSE ls_valueschar-value_char.
              ls_etiqueta-pedido = ls_valueschar-value_char.
            ENDIF.
          WHEN 'Z_POSICION'.
            IF ls_etiqueta-posicion IS INITIAL.
              CONDENSE ls_valueschar-value_char.
              ls_etiqueta-posicion = ls_valueschar-value_char.
            ENDIF.

        ENDCASE.
      ENDLOOP.

      ls_etiqueta-fec_fabricacion = sy-datum.
      ls_etiqueta-lgort           = ls_bobinas-lgort.
      ls_etiqueta-charg           = ls_bobinas-charg.
      ls_etiqueta-matnr           = ls_bobinas-matnr.
      ls_etiqueta-werks           = me->werks.
      ls_etiqueta-werks_ent       = me->werks.


      CALL FUNCTION 'ZMFPP_ETIQ_BOBINA'
        EXPORTING
          i_bobina   = ls_etiqueta
          i_nodialog = abap_true
          i_padest   = l_padest.

    ENDLOOP.

  ENDMETHOD.
  METHOD limpiar_material_peso.
    CLEAR: me->matnr ,
           me->charg ,
           me->maktx ,
           me->maktx_non_standard_paper ,
           me->peso ,
           me->peso_text
           .

  ENDMETHOD.
ENDCLASS.

DATA o_app TYPE REF TO zcl_app.
