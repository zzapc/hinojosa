
class ZCL_UTILS_HPAP_PP definition
  public
  final
  create public .

public section.

  types:
    BEGIN OF ty_sociedad,
        werks TYPE werks_d,
        name1 TYPE name1,
      END OF ty_sociedad .
  types:
    tty_sociedad TYPE STANDARD TABLE OF ty_sociedad .
  types:
    BEGIN OF ty_proveedor,
        lifnr TYPE lifnr,
        name1 TYPE name1_gp,
      END OF ty_proveedor .
  types:
    tty_proveedor TYPE STANDARD TABLE OF ty_proveedor .
  types:
    BEGIN OF ty_cli,
         kunnr TYPE kunnr,
         name1 TYPE name1_gp,
         pstlz TYPE pstlz,
         land1 TYPE land1,
       END OF ty_cli .
  types:
    tty_cli TYPE STANDARD TABLE OF ty_cli .
  types:
    tyr_lgort TYPE RANGE OF lgort_d .
  types:
    ttyr_lgort TYPE STANDARD TABLE OF tyr_lgort .

  class-data G_WERKS type WERKS_D .
  class-data GTR_ALM_REPROCESO type TYR_LGORT .

  class-methods CLASS_CONSTRUCTOR .
  class-methods CONECTAR_BBDD_BOBINADORA
    importing
      !I_WERKS type WERKS_D
    exporting
      !O_RC type CHAR2
      !O_PESO type NUMC06
      !O_PESO2 type NUMC06
      !O_LONGITUD type NUMC07
      !O_DIAMETRO type NUMC07
      !O_ANCHO type ZANCHO .
  class-methods OBTENER_LISTA_DEFECTOS
    exporting
      !OT_LISTA_DEFECTOS type TT_BAPICHARACTVALUESDESCR .
  class-methods ES_CABO
    importing
      !I_MATNR type MATNR
      !I_KUNNR type KUNNR
    returning
      value(R_ES_CABO) type FLAG .
  class-methods OBTENER_DEST_ENTREGA_SALIDA
    importing
      !I_WERKS type WERKS_D
      !I_DEST_MERCANCIAS type KUNNR
    exporting
      !OT_DESTINATARIOS type BCSY_SMTPA .
  class-methods DETERMINAR_DISP_IMPRESION
    returning
      value(R_PADEST) type RSPOPNAME .
  class-methods GET_DESCRIP_WERKS
    importing
      !I_WERKS type WERKS_D
    returning
      value(R_NAME1) type NAME1 .
  class-methods GET_DESCRIP_PROVEEDOR
    importing
      !I_LIFNR type LIFNR
    returning
      value(R_NAME1) type NAME1_GP .
  class-methods GET_DATOS_CLIENTE
    importing
      !I_KUNNR type KUNNR
    exporting
      !O_NAME1 type NAME1_GP
      !O_PSTLZ type PSTLZ
      !O_LAND1 type LAND1 .
  class-methods GET_DESCR_MATERIAL
    importing
      !I_MATNR type MATNR
    returning
      value(R_MAKTX) type MAKTX .
  class-methods GET_WERKS_FROM_USER
    returning
      value(R_WERKS) type WERKS_D .
  class-methods GET_LOGO_FROM_WERKS
    importing
      !I_WERKS type WERKS_D
    returning
      value(O_LOGO) type XSTRINGVAL .
  class-methods GET_DATOS_MATERIAL
    importing
      !I_MATNR type MATNR
    exporting
      !E_MAKTX type MAKTX
      !E_ANCHO type ZANCHO
      !E_GRAMAJE type ZGRAMAJE
      !E_CALIDAD type WGBEZ60 .
  class-methods GET_CARACTERISTICAS_LOTE
    importing
      !I_MATNR type MATNR
      !I_CHARG type CHARG_D
      !I_TP_LOTE type CHAR1
    exporting
      !O_DE type ATWRT
      !O_DE_CHAR1 type CHAR1
      !O_KUNNR type KUNNR
      !O_FECHA_FABRIC type DATUM
      !O_VBELN type VBELN
      !O_POSNR type POSNR
      !O_UBICACION type CHAR4
      !O_REPROCESO type CHAR1
      !O_MOTIVO_REPROC type ATWRT
      !O_RC type CHAR02 .
  class-methods GET_FLAG_ZTPP0032
    importing
      !I_WERKS type WERKS_D
      !I_APL type ZAPLICACION
    returning
      value(R_FLAG) type Z_FLAG_INACTIVO .
  class-methods DELETE_OLD_USER_SESSIONS
    importing
      !I_USER type UNAME .
  class-methods GET_MATERIAL_CLIENTE_EXTERNO
    importing
      !I_WERKS type WERKS_D
      !I_MATNR type MATNR
      !I_KUNNR type KUNNR
    exporting
      !O_MATNR type KDMAT .
protected section.
private section.

  class-data GT_SOCIEDADES type TTY_SOCIEDAD .
  class-data GT_PROVEEDORES type TTY_PROVEEDOR .
  class-data GT_DESCR_MATERIAL type MAKT_TAB .
  class-data GT_CLIENTES type TTY_CLI .
  class-data GT_DATOS_MATERIAL type ISM_MARA_TAB .
endclass. "ZCL_UTILS_HPAP_PP definition
class ZCL_UTILS_HPAP_PP implementation.
  METHOD class_constructor.

    g_werks = zcl_utils_hpap_pp=>get_werks_from_user( ).


    SELECT 'I'    AS sign,
           'EQ'   AS option,
           lgort  AS low,
           @space AS high
          INTO TABLE @gtr_alm_reproceso
          FROM ztpp0024
          WHERE werks         = @g_werks AND
                tipo_almacen  IN ( 03 , 04 ).

  ENDMETHOD.
  METHOD conectar_bbdd_bobinadora.

    DATA: l_env_ok   TYPE flag,
          l_peso     TYPE erfmg,
          l_peso2    TYPE erfmg,
          l_longitud TYPE zzdiametro,
          l_diametro TYPE zzdiametro,
          l_ample    TYPE zzdiametro.

    CASE i_werks.
      WHEN '3000'.
        EXEC SQL.
          CONNECT TO 'CONECT_BOBINADORA_3000' AS 'V'
        ENDEXEC.
      WHEN '3020'.
        EXEC SQL.
          CONNECT TO 'CONECT_BOBINADORA' AS 'V'
        ENDEXEC.
      WHEN OTHERS.
    ENDCASE.

*    DATA lv_dbmsg TYPE string.
*    IF sy-subrc <> 0.
*      EXEC SQL.
*        GET DIAGNOSTICS CONDITION 1
*          :lv_dbmsg = MESSAGE_TEXT
*      ENDEXEC.
*
*      MESSAGE lv_dbmsg TYPE 'I'.
*      RETURN.
*    ENDIF.

    EXEC SQL.
      SET CONNECTION 'V'
    ENDEXEC.
*
**- Get the data from MS-SQL Server
    CASE i_werks.
      WHEN '3000'.
        EXEC SQL.
          open C1 for
          select pes , pes as pes2 , longitud , diametre , enviament_ok , ample
          from NOTIF_BOBI01
        ENDEXEC.

      WHEN '3020'.
*          select pes , longitud , diametre , enviament_ok , ample
        EXEC SQL.
          open C1 for
          select pes , pes2 , longitud , diametre , enviament_ok , ample
          from NOTIF_BOBI10
        ENDEXEC.
    ENDCASE.


    EXEC SQL.
      FETCH NEXT C1 into :l_peso, :l_peso2, :l_longitud, :l_diametro, :l_env_ok, :l_ample
    ENDEXEC.

    IF sy-subrc <> 0.
      o_rc = sy-subrc.
    ELSE.

      CALL FUNCTION 'ROUND'
        EXPORTING
          decimals      = 0
          input         = l_peso
          sign          = ' '
        IMPORTING
          output        = l_peso
        EXCEPTIONS
          input_invalid = 1
          overflow      = 2
          type_invalid  = 3
          OTHERS        = 4.
      IF sy-subrc <> 0.
* Implement suitable error handling here
      ELSE.
        o_peso = l_peso.
      ENDIF.

      CALL FUNCTION 'ROUND'
        EXPORTING
          decimals      = 0
          input         = l_peso2
          sign          = ' '
        IMPORTING
          output        = l_peso2
        EXCEPTIONS
          input_invalid = 1
          overflow      = 2
          type_invalid  = 3
          OTHERS        = 4.
      IF sy-subrc <> 0.
* Implement suitable error handling here
      ELSE.
        o_peso2 = l_peso2.
      ENDIF.

      CALL FUNCTION 'ROUND'
        EXPORTING
          decimals      = 0
          input         = l_diametro
          sign          = ' '
        IMPORTING
          output        = l_diametro
        EXCEPTIONS
          input_invalid = 1
          overflow      = 2
          type_invalid  = 3
          OTHERS        = 4.
      IF sy-subrc <> 0.
* Implement suitable error handling here
      ELSE.
        o_diametro = l_diametro.
      ENDIF.

      o_ancho = l_ample.

* Per treure decimals
      o_longitud = CONV numc7( floor( l_longitud ) ).
    ENDIF.

    EXEC SQL.

      CLOSE C1

    ENDEXEC.

  ENDMETHOD.
  METHOD delete_old_user_sessions.

    DATA: p_jobcnt     TYPE btcjobcnt,
          l_release(1) TYPE c.
    DATA: l_terminal TYPE xuterminal,
          l_ip       TYPE string,
          resto      TYPE string.

* Obtener la IP actual.
**    CALL FUNCTION 'TERMINAL_ID_GET'
**      EXPORTING
**        username             = sy-uname
**      IMPORTING
**        terminal             = l_terminal
**      EXCEPTIONS
**        multiple_terminal_id = 1
**        no_terminal_found    = 2
**        OTHERS               = 3.
**    IF sy-subrc <> 0.
*** Implement suitable error handling here
**      EXIT.
**    ENDIF.

*    CALL METHOD cl_gui_frontend_services=>get_ip_address
*      RECEIVING
*        ip_address           = l_terminal
*      EXCEPTIONS
*        cntl_error           = 1
*        error_no_gui         = 2
*        not_supported_by_gui = 3
*        OTHERS               = 4.
*
*    IF sy-subrc <> 0.
*      WRITE:/'address not received'.
*    ELSE.
*      WRITE:/'ip adress = ',l_terminal.
*    ENDIF.


**    SPLIT l_terminal AT '-' INTO l_ip resto.


* Llamada ONLINE para pruebas.
*    SUBMIT zrpp_kill_sessions WITH  p_user EQ i_user
*                              WITH  p_ip   EQ l_ip
*                              AND RETURN.

    CALL FUNCTION 'JOB_OPEN'
      EXPORTING
        jobname          = 'ZPP_KILL_SESSIONS_HTTP'
      IMPORTING
        jobcount         = p_jobcnt
      EXCEPTIONS
        cant_create_job  = 1
        invalid_job_data = 2
        jobname_missing  = 3
        OTHERS           = 4.
    IF sy-subrc <> 0.
* Implement suitable error handling here
    ENDIF.

    SUBMIT zrpp_kill_sessions WITH  p_user EQ i_user
*                              WITH  p_ip   EQ l_ip
                              VIA JOB 'ZPP_KILL_SESSIONS_HTTP'
                              NUMBER p_jobcnt
                              USER 'SAPJOB'
                              WITH immediately = space
                              WITH keep_in_spool = 'X' AND RETURN.

    DATA: l_job_released TYPE btcchar1.

    CALL FUNCTION 'JOB_CLOSE'
      EXPORTING
        jobcount             = p_jobcnt
        jobname              = 'ZPP_KILL_SESSIONS_HTTP'
        strtimmed            = 'X'
      IMPORTING
        job_was_released     = l_job_released
      EXCEPTIONS
        cant_start_immediate = 1
        invalid_startdate    = 2
        jobname_missing      = 3
        job_close_failed     = 4
        job_nosteps          = 5
        job_notex            = 6
        lock_failed          = 7
        invalid_target       = 8
        OTHERS               = 9.
    IF sy-subrc <> 0.
* Implement suitable error handling here
    ENDIF.

  ENDMETHOD.
  METHOD determinar_disp_impresion.

    clear r_padest.

    SELECT SINGLE parva FROM usr05
      INTO @DATA(l_werks)
      WHERE bname = @sy-uname AND
            parid = 'WRK'.


    SELECT SINGLE padest
              INTO @r_padest
              FROM ztpp0007
              WHERE werks = @l_werks AND
                    uname = @sy-uname AND
                    tcode = @sy-tcode.

    IF sy-subrc <> 0.

      SELECT SINGLE padest
                INTO @r_padest
                FROM ztpp0007
                WHERE werks = @l_werks AND
                      uname = @sy-uname and
                      tcode = ' '.
    ENDIF.

    IF r_padest IS INITIAL.
      SELECT SINGLE padest
        INTO @r_padest
        FROM ztpp0007
        WHERE werks = @l_werks AND
              uname = ' ' AND tcode = ' '.
    ENDIF.

    IF r_padest IS INITIAL.
      r_padest = 'LOCA'.
    ENDIF.

  ENDMETHOD.
  method ES_CABO.

    data(l_matnr) = |{ i_matnr ALPHA = in }|.

    clear r_es_cabo.

    select single zzancho
      from mara
      into @data(l_ancho)
      where matnr = @l_matnr and
            zzancho <= 1340.

    IF sy-subrc <> 0.
    elseif i_kunnr in zcl_secuenciador=>gct_clientes_propios.
      r_es_cabo = 'X'.
    ENDIF.

  endmethod.
  METHOD get_caracteristicas_lote.

    DATA l_objkey           TYPE bapi1003_key-object.
    DATA l_classnum         TYPE klasse_d.
    DATA lt_allocvaluesnum  TYPE STANDARD TABLE OF bapi1003_alloc_values_num.
    DATA lt_allocvalueschar TYPE STANDARD TABLE OF bapi1003_alloc_values_char.
    DATA lt_allocvaluescurr TYPE STANDARD TABLE OF bapi1003_alloc_values_curr.
    DATA lt_return          TYPE STANDARD TABLE OF bapiret2.
    DATA: l_num   TYPE n LENGTH 8,              l_fecha TYPE datum,
          l_int   TYPE i,
          l_kunnr type kunnr.

    o_rc = '00'.

    CASE i_tp_lote.
      WHEN 'B'. l_classnum = 'Z_BOBINA'.
      WHEN 'R'. l_classnum = 'Z_ROLLO'.
      WHEN OTHERS.
    ENDCASE.

    l_objkey = |{ i_matnr }{ i_charg }|.

    CALL FUNCTION 'BAPI_OBJCL_GETDETAIL'
      EXPORTING
        objectkey       = l_objkey
        objecttable     = 'MCH1'
        classnum        = 'Z_BOBINA'
        classtype       = '023'
*       KEYDATE         = SY-DATUM
*       UNVALUATED_CHARS       = ' '
*       LANGUAGE        = SY-LANGU
* IMPORTING
*       STATUS          =
*       STANDARDCLASS   =
      TABLES
        allocvaluesnum  = lt_allocvaluesnum
        allocvalueschar = lt_allocvalueschar
        allocvaluescurr = lt_allocvaluescurr
        return          = lt_return.

    IF line_exists( lt_return[ type = 'E' ] ).
      o_rc = '01'.
      EXIT.
    ENDIF.


    TRY .
        l_int          = lt_allocvaluesnum[ charact = 'Z_FECHA_FABRICACION' ]-value_from.
        l_num          = CONV #( l_int ).
        o_fecha_fabric = CONV #( l_num ).

      CATCH cx_root.
    ENDTRY.


    TRY .
        o_kunnr = lt_allocvalueschar[ charact = 'Z_CLIENTE' ]-value_neutral.
        CONDENSE o_kunnr.
        o_kunnr = |{ o_kunnr ALPHA = in }|.
      CATCH cx_root.
    ENDTRY.


    TRY .
        o_de    = lt_allocvalueschar[ charact = 'LOBM_UDCODE' ]-value_neutral.
        o_de_char1 = o_de+9(1).


      CATCH cx_root.
    ENDTRY.

    TRY.
        o_vbeln = lt_allocvalueschar[ charact = 'Z_PEDIDO' ]-value_neutral.
        CONDENSE o_vbeln.
      CATCH cx_root .
    ENDTRY.

    TRY.
        o_posnr = lt_allocvalueschar[ charact = 'Z_POSICION' ]-value_neutral.
        CONDENSE o_posnr.
      CATCH cx_root .
    ENDTRY.

    TRY.
        o_ubicacion = lt_allocvalueschar[ charact = 'Z_UBIC_HPAP' ]-value_neutral(4).
      CATCH cx_root .
    ENDTRY.

    TRY.
        o_reproceso = lt_allocvalueschar[ charact = 'Z_REPROCESO' ]-value_neutral(1).
      CATCH cx_root .
    ENDTRY.

    TRY.
        o_motivo_reproc = lt_allocvalueschar[ charact = 'ZPPI_MOTIVO_REPROCESO_BOBINA' ]-value_neutral.
      CATCH cx_root .
    ENDTRY.


  ENDMETHOD.

* CARACTERÍSTICAS BOBINA -  LT_ALLOCVALUESCHAR
*  LOBM_UDCODE        DE       A
*  Z_CLIENTE          C2030
*  Z_CONTRAMAESTRE    NUNO MIGUEL MACHADO
*  Z_LOTE_ORIGEN      125AS00190
*  Z_OPERARIO	        LORENZO ESPINOSA
*  Z_PEDIDO	          44003489
*  Z_POSICION	        60
*  Z_PUESTO_TRABAJO	  BOBI_10
*  Z_ROLLO_ORIGEN_V2  2000924
*  Z_UBIC_HPAP        0118

* CARACTERÍSTICAS BOBINA -  LT_ALLOCVALUESNUM

* Z_ANCHO_REAL        0.0000000000000000E+00
* Z_DIAMETRO          1.4290000000000000E+03
* Z_EMPALMES          0.0000000000000000E+00
* Z_FECHA_FABRICACION	2.0210627000000000E+07
* Z_FSC_PORCENTAJE    1.0000000000000000E+02
* Z_LONGITUD_B        5.9520000000000000E+03
* Z_PESO              2.8940000000000000E+03
  METHOD get_datos_cliente.

    TRY.
        DATA(ls_cli) = gt_clientes[ kunnr = i_kunnr ].
        o_name1   = ls_cli-name1.
        o_pstlz   = ls_cli-pstlz.
        o_land1   = ls_cli-land1.

      CATCH cx_root .

        SELECT SINGLE kunnr , name1 , pstlz , land1
          INTO CORRESPONDING FIELDS OF @ls_cli
          FROM kna1
          WHERE kunnr = @i_kunnr.

        IF sy-subrc = 0.
          APPEND INITIAL LINE TO gt_clientes ASSIGNING FIELD-SYMBOL(<fs_cli>).
          <fs_cli>-kunnr  = i_kunnr.

          <fs_cli>-name1  = ls_cli-name1.
          <fs_cli>-pstlz  = ls_cli-pstlz.
          <fs_cli>-land1  = ls_cli-land1.

          o_name1  = ls_cli-name1.
          o_pstlz  = ls_cli-pstlz.
          o_land1  = ls_cli-land1.

        ELSE.
          CLEAR:  o_name1 ,  o_pstlz , o_pstlz.
        ENDIF.

    ENDTRY.

  ENDMETHOD.
  METHOD get_datos_material.


    TRY.

*        e_maktx =    gt_descr_material[ matnr = i_matnr ]-maktx.
*        e_ancho =    gt_datos_material[ matnr = i_matnr ]-zzancho.



        e_maktx = gt_descr_material[ matnr = i_matnr ].

        DATA(ls_mara) = gt_datos_material[ matnr = i_matnr ].
        e_gramaje     = ls_mara-zzgramaje.
        e_ancho       = ls_mara-zzancho.

*   Texto de la calidad
        SELECT SINGLE wgbez60
                 FROM t023t
                 INTO e_calidad
                WHERE matkl = ls_mara-matkl AND spras = sy-langu.


      CATCH cx_root .
        SELECT SINGLE maktx INTO     e_maktx

          FROM makt
          WHERE matnr = i_matnr AND
                spras = sy-langu.
        IF sy-subrc = 0.
          APPEND INITIAL LINE TO gt_descr_material ASSIGNING FIELD-SYMBOL(<fs_makt>).
          <fs_makt>-matnr = i_matnr.
          <fs_makt>-maktx = e_maktx.

          SELECT SINGLE *
            INTO @ls_mara
            FROM mara
            WHERE matnr = @i_matnr.
          APPEND INITIAL LINE TO gt_datos_material ASSIGNING FIELD-SYMBOL(<fs_mara>).
          <fs_mara> = CORRESPONDING #( ls_mara ).
          e_gramaje = <fs_mara>-zzgramaje.
          e_ancho = <fs_mara>-zzancho.
*   Texto de la calidad
          SELECT SINGLE wgbez60
                   FROM t023t
                   INTO e_calidad
                  WHERE matkl = <fs_mara>-matkl AND spras = sy-langu.


        ELSE.
          CLEAR: e_maktx , e_ancho , e_gramaje , e_calidad.
        ENDIF.

    ENDTRY.


  ENDMETHOD.
  METHOD GET_DESCR_MATERIAL.


    TRY.
        r_maktx =    gt_descr_material[ matnr = i_matnr ]-maktx.

      CATCH cx_root .
        SELECT SINGLE maktx INTO     r_maktx

          FROM makt
          WHERE matnr = i_matnr AND
                spras = sy-langu.
        IF sy-subrc = 0.
          APPEND INITIAL LINE TO gt_descr_material ASSIGNING FIELD-SYMBOL(<fs_makt>).
          <fs_makt>-matnr = i_matnr.
          <fs_makt>-maktx = r_maktx.
        ELSE.
          CLEAR r_maktx.
        ENDIF.

    ENDTRY.


  ENDMETHOD.
  method GET_DESCRIP_PROVEEDOR.

* Obtener descripción de proveedor.
      TRY.
          r_name1 = GT_PROVEEDORES[ lifnr      = i_lifnr ]-name1.
        CATCH cx_root .

          select single name1
            into @r_name1
            from lfa1
            where lifnr = @i_lifnr.
            IF sy-subrc = 0.
              APPEND INITIAL LINE TO GT_PROVEEDORES ASSIGNING FIELD-SYMBOL(<fs_pro>).
              <fs_pro>-lifnr  = i_lifnr.
              <fs_pro>-name1  = r_name1.
            ENDIF.

      ENDTRY.





*    * Obtenemos proveedores del centro.
*    SELECT lifnr , name1
*      INTO TABLE @DATA(lt_lifnr)
*      FROM lfa1
*      WHERE werks = @i_werks.
*    IF sy-subrc = 0.
*      LOOP AT lt_lifnr INTO DATA(ls_lifnr).
*        lr_lifnr = VALUE #( ( sign = 'I'  option = 'EQ'  low = ls_lifnr-lifnr )
*                           ).
*      ENDLOOP.
*    ENDIF.
*



  endmethod.
  method GET_DESCRIP_WERKS.

* Obtener descripción de centro.
      TRY.
          r_name1 = GT_SOCIEDADES[ werks      = i_werks ]-name1.
        CATCH cx_root .

          select single name1
            into @r_name1
            from t001w
            where werks = @i_werks.
            IF sy-subrc = 0.
              APPEND INITIAL LINE TO gt_sociedades ASSIGNING FIELD-SYMBOL(<fs_soc>).
              <fs_soc>-werks  = i_werks.
              <fs_soc>-name1  = r_name1.
            ENDIF.

      ENDTRY.




  endmethod.
  METHOD get_flag_ztpp0032.

    SELECT SINGLE flag_inactivo
      FROM ztpp0032
      INTO @r_flag
      WHERE werks = @i_werks AND
            apl   = @i_apl.

  ENDMETHOD.
  method GET_LOGO_FROM_WERKS.

    data: l_name  TYPE TDOBNAME.

    CASE i_werks.
      WHEN '3000'. l_name = 'ZLOGO_ALQUERIA'.
      WHEN '3020'. l_name = 'ZLOGO_SARRIA'.
      WHEN OTHERS.
    ENDCASE.
  CALL METHOD cl_ssf_xsf_utilities=>get_bds_graphic_as_bmp
    EXPORTING
      p_object       = 'GRAPHICS'
      p_name         = l_name
      p_id           = 'BMAP'
      p_btype        = 'BCOL'
    RECEIVING
      p_bmp          = o_logo       " g_logo must be typed xstring
    EXCEPTIONS
      not_found      = 1
      internal_error = 2
      OTHERS         = 3.



  endmethod.
  METHOD get_material_cliente_externo.
  data: l_kunnr type kunnr.
  data: l_matnr type matnr.

    o_matnr = i_matnr.

* No tratar clientes grupo
    CHECK NOT i_kunnr CP  'C*'.

  l_kunnr = |{ i_kunnr ALPHA = in }|.
  l_matnr = |{ i_matnr ALPHA = in }|.

    SELECT SINGLE kdmat
      INTO @o_matnr
      FROM knmt
      WHERE vkorg = @i_werks
      AND   vtweg = '10'
      AND   matnr = @l_matnr
      AND   kunnr = @l_kunnr.

    IF sy-subrc <> 0.
      o_matnr = i_matnr.
    ENDIF.

  ENDMETHOD.
  METHOD get_werks_from_user.

    SELECT SINGLE parva
      INTO r_werks
      FROM usr05
      WHERE bname = sy-uname AND
            parid = 'WRK'.

  ENDMETHOD.
  METHOD obtener_dest_entrega_salida.

    SELECT SINGLE * INTO @DATA(ls_ztpp0047)
      FROM ztpp0047
      WHERE werks     = @i_werks
        AND cliente   = @i_dest_mercancias
        and inactivo  = ' '.

    CHECK sy-subrc = 0.

    IF ls_ztpp0047-email1 IS NOT INITIAL.
      APPEND INITIAL LINE TO ot_destinatarios ASSIGNING FIELD-SYMBOL(<fs_dest>).
      <fs_dest> = ls_ztpp0047-email1.
    ENDIF.

    IF ls_ztpp0047-email2 IS NOT INITIAL.
      APPEND INITIAL LINE TO ot_destinatarios ASSIGNING <fs_dest>.
      <fs_dest> = ls_ztpp0047-email2.
    ENDIF.


    IF ls_ztpp0047-email3 IS NOT INITIAL.
      APPEND INITIAL LINE TO ot_destinatarios ASSIGNING <fs_dest>.
      <fs_dest> = ls_ztpp0047-email2.
    ENDIF.


    IF ls_ztpp0047-email4 IS NOT INITIAL.
      APPEND INITIAL LINE TO ot_destinatarios ASSIGNING <fs_dest>.
      <fs_dest> = ls_ztpp0047-email2.
    ENDIF.

  ENDMETHOD.
  METHOD obtener_lista_defectos.

    DATA:  lt_bapiret2       TYPE STANDARD TABLE OF bapiret2.

    clear ot_lista_defectos[].
    CALL FUNCTION 'BAPI_CHARACT_GETDETAIL'
      EXPORTING
        charactname        = 'Z_DEFECTOS'
        keydate            = sy-datum
        language           = sy-langu
*   IMPORTING
*       CHARACTDETAIL      =
      TABLES
*       CHARACTDESCR       =
*       CHARACTVALUESNUM   =
*       CHARACTVALUESCHAR  =
*       CHARACTVALUESCURR  =
        charactvaluesdescr = ot_lista_defectos
*       CHARACTREFERENCES  =
*       CHARACTRESTRICTIONS       =
        return             = lt_bapiret2.




  ENDMETHOD.
