
class ZCLPP_RECETAS_MP definition
  public
  final
  create public .

public section.

  types:
    BEGIN OF ty_calidad,
        zcalidad TYPE zzcalidad,
        descrip  TYPE zdescripcion,
      END   OF ty_calidad .
  types:
    tty_calidad TYPE STANDARD TABLE OF ty_calidad .
  types:
    tty_cab_recetas_std TYPE STANDARD TABLE OF ztpp0038 .
  types:
    tty_cab_recetas_mp TYPE STANDARD TABLE OF ZSPP0040_ALV .

  class-data GT_MOTIVOS type ZTTP0042 .
  class-data GT_DESCR_MP type MAKT_TAB .

*  types:
*    BEGIN OF ty_rec_std,
*        matnr      TYPE matnr,
*        maktx      TYPE maktx,
*        kg_ciclo   TYPE ztpp0039-kg_ciclo,
*        porc_ciclo TYPE ztpp0039-porc_ciclo,
*      END   OF ty_rec_std .
*  types:
*    tty_rec_std TYPE STANDARD TABLE OF ty_rec_std .
  class-methods OBTENER_SOCIEDAD_USUARIO
    returning
      value(R_WERKS) type WERKS_D .
  class-methods OBTENER_CALIDADES_RECETA_STD
    importing
      !I_WERKS type WERKS_D
    exporting
      !OT_CALIDADES type TTY_CALIDAD .
  class-methods OBTENER_LISTA_RECETAS_STANDARD
    importing
      !I_WERKS type WERKS_D
      !I_CALIDAD type ZZCALIDAD optional
    exporting
      value(OT_CAB_RECETAS_STD) type TTY_CAB_RECETAS_STD .
  class-methods OBTENER_ID_RECETA_NUEVO
    importing
      !I_WERKS type WERKS_D
      !I_TP_RECETA type CHAR1
    returning
      value(R_ID_RECETA) type ZID_RECETA .
  class-methods OBTENER_LISTA_MP
    importing
      !I_WERKS type WERKS_D
    exporting
      value(OT_LISTA_MP) type ZTPP0056_ALV300 .
  class-methods GUARDAR_RECETA_STD
    importing
      !I_ID_RECETA_STD type ZID_RECETA optional
      !I_WERKS type WERKS_D
      !I_ZCALIDAD type ZZCALIDAD
      !I_DESCRIP_REC type ZDESCRIP_REC
      !IT_MATERIAS_PRIMAS type ZTPP0056_ALV300
    exporting
      !O_ID_RECETA type ZID_RECETA
      !O_FECHA_REG type DATUM
      !O_HORA_REG type UZEIT .
  class-methods BORRAR_RECETAS_STD
    importing
      !I_WERKS type WERKS_D
      !IT_RECETAS type ZRRID_RECETA .
  class-methods OBTENER_POSICIONES_RECETA
    importing
      !I_WERKS type WERKS_D
      !I_ID_RECETA_STD type ZID_RECETA
    exporting
      !OT_LISTA_MP type ZTPP0056_ALV300 .
  class-methods OBTENER_LISTA_RECETAS_MP
    importing
      !I_WERKS type WERKS_D
      !I_CALIDAD type ZZCALIDAD optional
      !I_CONTRAMA type ZNOMBRE_OPER optional
      !I_UP_TO type NUMC10 optional
    exporting
      !OT_CAB_RECETAS_MP type TTY_CAB_RECETAS_MP .
  class-methods OBTENER_DATOS_RECETA_STD
    importing
      !I_WERKS type WERKS_D
      !I_ID_RECETA type ZID_RECETA optional
    exporting
      !O_CAB_REC_STD type ZTPP0038
      !OT_POS_REC_STD type ZTPP0056_ALV300 .
  class-methods VALIDAR_TABLA_KO_A_CERO
    importing
      !IT_LISTA_MP type ZTPP0056_ALV300
    returning
      value(R_OK) type FLAG .
  class-methods VALIDAR_FECHAS_ALTA_REC_MP
    importing
      !I_WERKS type WERKS_D
      !I_FECHA type DATUM
      !I_HORA type UZEIT
    exporting
      value(O_RETURN) type CHAR2
      !O_ULT_FECHA type DATUM
      !O_ULT_HORA type UZEIT .
  class-methods GUARDAR_RECETA_MP
    importing
      !I_ID_RECETA type ZID_RECETA
      !I_WERKS type WERKS_D
      !I_CALIDAD type ZZCALIDAD
      !I_DESCRIP_REC type ZDESCRIP_REC
      !I_OBSERVACIONES type ZOBSERVACIONES
      !I_FECHA_INI type DATUM
      !I_HORA_INI type UZEIT
      !I_CONTRAMA type ZNOMBRE_OPER
      !I_COD_MOTIVO type ZCOD_MOT_REC
      !IT_POS_KG_MP type ZTPP0056_ALV300
    exporting
      !O_ID_RECETA type ZID_RECETA
      !O_RC type CHAR2 .
  class-methods OBTENER_DESCR_CALIDAD
    importing
      !I_WERKS type WERKS_D
      !I_CALIDAD type ZZCALIDAD
    returning
      value(R_DESCR) type ZDESCRIPCION .
  class-methods OBTENER_TEMPORALIDAD_RECETA
    importing
      !I_FECHA_INI type DATUM
      !I_HORA_INI type UZEIT
      !I_FECHA_FIN type DATUM
      !I_HORA_FIN type UZEIT
    returning
      value(R_RETURN) type CHAR1 .
  class-methods OBTENER_DATOS_FORM_RECETA_MP
    importing
      !I_WERKS type WERKS_D
      !I_ID_RECETA type ZID_RECETA
    exporting
      !OS_DATOS_FORM_RECETA_MP type ZSPP_FORM_RECETA_MP
      !O_RC type CHAR02 .
  class-methods VALIDAR_SUPRIMIR_RECETA_MP
    importing
      !I_WERKS type WERKS_D
      !I_ID_RECETA type ZID_RECETA
    exporting
      !O_RC type CHAR02 .
  class-methods BORRAR_RECETA_MP
    importing
      !I_WERKS type WERKS_D
      !I_ID_RECETA type ZID_RECETA
    exporting
      !O_RC type CHAR02 .
  class-methods CLASS_CONSTRUCTOR .
  class-methods MODIFICAR_FECHA_FIN_RECETA_MP
    importing
      !I_WERKS type WERKS_D
      !I_ID_RECETA type ZID_RECETA
      !I_NUEVA_FECHA_FIN type DATUM
      !I_NUEVA_HORA_FIN type UZEIT
      !I_NEXT_RECETA type ZID_RECETA
      !I_OPERARIO type ZNOMBRE_OPER
    exporting
      !O_RC type CHAR02 .
  class-methods VALIDAR_NUEVA_FECHA_FIN_RECETA
    importing
      !I_WERKS type WERKS_D
      !I_ID_RECETA type ZID_RECETA
      !I_NUEVA_FECHA_FIN type DATUM
      !I_NUEVA_HORA_FIN type UZEIT
    exporting
      !O_NEXT_RECETA type ZID_RECETA
      !O_RC type CHAR02 .
  PROTECTED SECTION.
private section.
endclass. "ZCLPP_RECETAS_MP definition
class ZCLPP_RECETAS_MP implementation.
  METHOD borrar_receta_mp.

* Obtenemos datos de la receta que se desea borrar.
    SELECT SINGLE *
      INTO @DATA(ls_rec_borr)
      FROM ztpp0040
      WHERE werks     = @i_werks AND
            id_receta = @i_id_receta.

* Restamos 1 seg. para obtener la receta inmediatamente anterior.
    IF ls_rec_borr-hora_ini = '000000' . " Si la hora de inicio son las 24h, le restaremos 1 dia a la fecha.
      ls_rec_borr-hora_ini  = '235959'.
      ls_rec_borr-fecha_ini = ls_rec_borr-fecha_ini - 1.
    ELSE.
      ls_rec_borr-hora_ini  = ls_rec_borr-hora_ini - 1.
    ENDIF.

* Obtenmos la receta inmediatamente anterior, a la cual le actualizaremos la fecha_fin
    SELECT SINGLE *
      INTO @DATA(ls_rec_cam_ff)
      FROM ztpp0040
      WHERE werks      = @i_werks AND
            fecha_fin  = @ls_rec_borr-fecha_ini AND
            hora_fin   = @ls_rec_borr-hora_ini.

    IF sy-subrc <> 0.
      o_rc = '01'.
      EXIT.
    ENDIF.

    ls_rec_cam_ff-fecha_fin   = ls_rec_borr-fecha_fin.
    ls_rec_cam_ff-hora_fin    = ls_rec_borr-hora_fin.
    ls_rec_cam_ff-fecha_upd   = sy-datum.
    ls_rec_cam_ff-hora_upd    = sy-uzeit.
    UPDATE ztpp0040 FROM ls_rec_cam_ff.
    IF sy-subrc <> 0.
      o_rc = '02'.
      EXIT.
    ENDIF.

* Borramos cabecera de receta
    DELETE FROM ztpp0040
      WHERE werks     = i_werks AND
            id_receta = i_id_receta.
    IF sy-subrc <> 0.
      o_rc = '03'.
      EXIT.
    ELSE.
      DELETE FROM ztpp0039 " Borramos las posiciones de los Kg.Ciclo
      WHERE werks     = i_werks AND
      id_receta = i_id_receta.
      IF sy-subrc <> 0.
        o_rc = '04'.
        EXIT.
      else.
        o_rc = '00'.
      ENDIF.
    ENDIF.

  ENDMETHOD.
  METHOD borrar_recetas_std.

* Borramos posicones
    DELETE FROM ztpp0039
    WHERE id_receta IN @it_recetas AND
          werks   = @i_werks.

* Borramos cabeceras.
    DELETE FROM ztpp0038
    WHERE id_receta IN @it_recetas AND
          werks   = @i_werks.

  ENDMETHOD.
  METHOD class_constructor.

    CLEAR : gt_motivos , gt_descr_mp.

    SELECT *
      INTO TABLE gt_motivos
      FROM ztpp0042.

    SELECT m~* INTO TABLE @gt_descr_mp
      FROM ztpp0036 AS z
      INNER JOIN makt AS m ON m~matnr = z~matnr
        WHERE spras = @sy-langu.

    SELECT m~* APPENDING TABLE @gt_descr_mp
      FROM ztpp0037 AS z
      INNER JOIN makt AS m ON m~matnr = z~matnr_mp
        WHERE spras = @sy-langu.

  ENDMETHOD.
  METHOD guardar_receta_mp.
    DATA: ls_ztpp0040     TYPE  ztpp0040,
          ls_ztpp0039     TYPE  ztpp0039,
          l_posnr         TYPE  posnr,
          l_fecha_fin_ant TYPE  datum,
          l_hora_fin_ant  LIKE  sy-uzeit.
    .

    IF i_id_receta IS INITIAL.
      CALL METHOD zclpp_recetas_mp=>obtener_id_receta_nuevo
        EXPORTING
          i_werks     = i_werks
          i_tp_receta = '1' "Tipo receta MP
        RECEIVING
          r_id_receta = o_id_receta.

* Obtenemos la receta con la fecha final 9991231.
      SELECT SINGLE id_receta
        INTO @DATA(l_receta_max)
         FROM ztpp0040
         WHERE fecha_fin = '99991231' AND
               werks     = @i_werks.


* Grabamos cabecera.
      CLEAR ls_ztpp0040.
      ls_ztpp0040-werks             = i_werks.
      ls_ztpp0040-id_receta         = o_id_receta.
      ls_ztpp0040-zcalidad          = i_calidad.
      ls_ztpp0040-descrip_rec       = i_descrip_rec.
      ls_ztpp0040-observaciones     = i_observaciones.

      ls_ztpp0040-fecha_ini         = i_fecha_ini.
      ls_ztpp0040-hora_ini          = i_hora_ini.
      ls_ztpp0040-fecha_fin         = '99991231'.
      ls_ztpp0040-hora_fin          = '235959'.
      ls_ztpp0040-cod_motivo        = i_cod_motivo.
      ls_ztpp0040-fecha_ins         = sy-datum.
      ls_ztpp0040-hora_ins          = sy-uzeit.
      ls_ztpp0040-contr_ins         = i_contrama.

      INSERT INTO ztpp0040 VALUES @ls_ztpp0040.

      IF i_hora_ini = '000000'. "si la fecha inicial es 0h, restamos 1 a la fecha y hora=23:59:59
        l_hora_fin_ant   = '235959'.
        l_fecha_fin_ant  = i_fecha_ini - 1.
      ELSE.
        l_hora_fin_ant   = i_hora_ini - 1.
        l_fecha_fin_ant  = i_fecha_ini.

      ENDIF.


      IF sy-subrc = 0.
* Modificar fecha/hora final de la receta anteriormente planificada
        UPDATE ztpp0040 SET fecha_fin = l_fecha_fin_ant
                            hora_fin  = l_hora_fin_ant
                            WHERE id_receta = l_receta_max.
      ENDIF.

* Grabamos posiciones
      CLEAR l_posnr.
      LOOP AT it_pos_kg_mp INTO DATA(ls_mp) WHERE kg_ciclo > 0.

        CLEAR ls_ztpp0039.
        l_posnr = l_posnr + 1.

        ls_ztpp0039-werks           = i_werks.
        ls_ztpp0039-id_receta       = o_id_receta.
        ls_ztpp0039-posnr           = l_posnr.
        ls_ztpp0039-matnr           = ls_mp-matnr.
        ls_ztpp0039-kg_ciclo        = ls_mp-kg_ciclo.
        ls_ztpp0039-meins           = 'KG'.
        ls_ztpp0039-porc_ciclo      = ls_mp-porc_ciclo.
        INSERT INTO ztpp0039 VALUES @ls_ztpp0039.

      ENDLOOP.

    ENDIF.

  ENDMETHOD.
  METHOD guardar_receta_std.

    DATA: ls_ztpp0038 TYPE ztpp0038, "Cabecera
          ls_ztpp0039 TYPE ztpp0039, "Posiciones
          l_posnr     TYPE posnr.

    IF i_id_receta_std IS INITIAL.

      CALL METHOD zclpp_recetas_mp=>obtener_id_receta_nuevo
        EXPORTING
          i_werks     = i_werks
          i_tp_receta = '2'
        RECEIVING
          r_id_receta = o_id_receta.

      o_fecha_reg = sy-datum.
      o_hora_reg  = sy-uzeit.

* Grabamos cabecera.
      CLEAR ls_ztpp0038.
      ls_ztpp0038-werks             = i_werks.
      ls_ztpp0038-id_receta         = o_id_receta.
      ls_ztpp0038-zcalidad          = i_zcalidad.
      ls_ztpp0038-descrip_rec       = i_descrip_rec.
      ls_ztpp0038-fecha_reg         = o_fecha_reg.
      ls_ztpp0038-hora_reg          = o_hora_reg.
      ls_ztpp0038-contramaestre     = sy-uname.
      INSERT INTO ztpp0038 VALUES @ls_ztpp0038.

* Grabamos posiciones
      CLEAR l_posnr.
      LOOP AT it_materias_primas INTO DATA(ls_mp) WHERE kg_ciclo > 0.

        CLEAR ls_ztpp0039.
        l_posnr = l_posnr + 1.

        ls_ztpp0039-werks           = i_werks.
        ls_ztpp0039-id_receta       = o_id_receta.
        ls_ztpp0039-posnr           = l_posnr.
        ls_ztpp0039-matnr           = ls_mp-matnr.
        ls_ztpp0039-kg_ciclo        = ls_mp-kg_ciclo.
        ls_ztpp0039-meins           = 'KG'.
        ls_ztpp0039-porc_ciclo      = ls_mp-porc_ciclo.
        INSERT INTO ztpp0039 VALUES @ls_ztpp0039.

      ENDLOOP.

    ELSE.

      SELECT SINGLE *
        FROM ztpp0038
        INTO @DATA(ls_038)
        WHERE id_receta = @i_id_receta_std AND
              werks     = @i_werks.
      IF sy-subrc = 0.
        IF ls_038-descrip_rec <> i_descrip_rec.
          ls_038-descrip_rec = i_descrip_rec.
          UPDATE ztpp0038 FROM @ls_038.
        ENDIF.


        CHECK it_materias_primas IS NOT INITIAL.

        DELETE FROM ztpp0039
        WHERE id_receta = @i_id_receta_std AND
              werks     = @i_werks.

        CLEAR l_posnr.
        LOOP AT it_materias_primas INTO ls_mp WHERE kg_ciclo > 0.

          CLEAR ls_ztpp0039.
          l_posnr = l_posnr + 1.

          ls_ztpp0039-werks           = i_werks.
          ls_ztpp0039-id_receta       = o_id_receta.
          ls_ztpp0039-posnr           = l_posnr.
          ls_ztpp0039-matnr           = ls_mp-matnr.
          ls_ztpp0039-kg_ciclo        = ls_mp-kg_ciclo.
          ls_ztpp0039-meins           = 'KG'.
          ls_ztpp0039-porc_ciclo      = ls_mp-porc_ciclo.
          ls_ztpp0038-contramaestre     = sy-uname.
          INSERT INTO ztpp0039 VALUES @ls_ztpp0039.

        ENDLOOP.

      ENDIF.

    ENDIF.


  ENDMETHOD.
  METHOD modificar_fecha_fin_receta_mp.

    DATA: l_hora  TYPE uzeit,
          l_fecha TYPE datum.

    UPDATE ztpp0040 SET fecha_fin = i_nueva_fecha_fin
                        hora_fin  = i_nueva_hora_fin
                        fecha_upd = sy-datum
                        hora_upd  = sy-uzeit
                        contr_upd = i_operario
                  WHERE werks     = i_werks AND
                        id_receta = i_id_receta.
    IF sy-subrc <> 0.
      o_rc = '01'. "Error de actualización
    ENDIF.

    l_hora = i_nueva_hora_fin + 1.
    IF l_hora = '000000'.
      l_fecha = i_nueva_fecha_fin + 1.
    ELSE.
      l_fecha = i_nueva_fecha_fin.
    ENDIF.

    UPDATE ztpp0040 SET fecha_ini = l_fecha
                        hora_ini  = l_hora
                        fecha_upd = sy-datum
                        hora_upd  = sy-uzeit
                        contr_upd = i_operario
                  WHERE werks     = i_werks AND
                        id_receta = i_next_receta.
    IF sy-subrc <> 0.
      o_rc = '02'.
    ELSE.
      o_rc = '00'.
    ENDIF.
  ENDMETHOD.
  METHOD obtener_calidades_receta_std.

    CLEAR ot_calidades[].

    SELECT zcalidad descrip
      INTO TABLE ot_calidades
      FROM ztpp0035
      WHERE lvorm = ' ' AND
            werks = i_werks.
  ENDMETHOD.
  METHOD obtener_datos_form_receta_mp.

    o_rc = '00'.

    SELECT SINGLE *
      INTO @DATA(ls_ztpp0040)
      FROM ztpp0040
      WHERE werks     = @i_werks AND
            id_receta = @i_id_receta.
    IF sy-subrc <> 0.
      o_rc = '01'. " Receta no encontrada
      EXIT.
    ENDIF.

    SELECT *
      INTO  TABLE @DATA(lt_ztpp0039)
      FROM  ztpp0039
      WHERE werks     = @i_werks AND
            id_receta = @i_id_receta
      ORDER BY posnr.
    IF sy-subrc <> 0.
      o_rc = '02'. " Receta sin posiciones
      EXIT.
    ENDIF.

    CLEAR: os_datos_form_receta_mp , os_datos_form_receta_mp-t_materiales_mp[].
    os_datos_form_receta_mp-werks             = ls_ztpp0040-werks.
    os_datos_form_receta_mp-id_receta         = ls_ztpp0040-id_receta.
    os_datos_form_receta_mp-id_rec_cab        = ls_ztpp0040-id_receta.
    os_datos_form_receta_mp-codbar_idrec      = ls_ztpp0040-id_receta.
    os_datos_form_receta_mp-zcalidad          = ls_ztpp0040-zcalidad.
    os_datos_form_receta_mp-descr_calidad     = zclpp_recetas_mp=>obtener_descr_calidad( i_werks   = i_werks
                                                                                         i_calidad = ls_ztpp0040-zcalidad ).
    os_datos_form_receta_mp-descr_rec         = ls_ztpp0040-descrip_rec.
    os_datos_form_receta_mp-observaciones     = ls_ztpp0040-observaciones.
    os_datos_form_receta_mp-contramaestre     = ls_ztpp0040-contr_ins.
    os_datos_form_receta_mp-fecha_ini         = ls_ztpp0040-fecha_ini.
    os_datos_form_receta_mp-hora_ini          = ls_ztpp0040-hora_ini.
    os_datos_form_receta_mp-fecha_fin         = ls_ztpp0040-fecha_fin.
    os_datos_form_receta_mp-hora_fin          = ls_ztpp0040-hora_fin.
    os_datos_form_receta_mp-cod_motivo        = ls_ztpp0040-cod_motivo.
    TRY.
        os_datos_form_receta_mp-desc_motivo        = zclpp_recetas_mp=>gt_motivos[ cod_motivo = ls_ztpp0040-cod_motivo ]-desc_motivo.

      CATCH cx_root .
    ENDTRY.

    os_datos_form_receta_mp-fecha_actual      = sy-datum.
    os_datos_form_receta_mp-hora_actual       = sy-uzeit.

    LOOP AT lt_ztpp0039 INTO DATA(ls_z39).

      APPEND INITIAL LINE TO os_datos_form_receta_mp-t_materiales_mp ASSIGNING FIELD-SYMBOL(<fs_mat_prima>).
      <fs_mat_prima>-matnr        = ls_z39-matnr.
      <fs_mat_prima>-kg_ciclo     = ls_z39-kg_ciclo.
      <fs_mat_prima>-porc_ciclo   = ls_z39-porc_ciclo.

* Descrip.Material.
      TRY.
          <fs_mat_prima>-maktx = zclpp_recetas_mp=>gt_descr_mp[ matnr = <fs_mat_prima>-matnr ]-maktx.
        CATCH cx_root .
      ENDTRY.

      os_datos_form_receta_mp-total_kg   = os_datos_form_receta_mp-total_kg   + ls_z39-kg_ciclo.
      os_datos_form_receta_mp-total_porc = os_datos_form_receta_mp-total_porc + ls_z39-porc_ciclo.

    ENDLOOP.

  ENDMETHOD.
  METHOD obtener_datos_receta_std.

* Obtener datos cabecera
    SELECT SINGLE *
      INTO @o_cab_rec_std
      FROM ztpp0038
      WHERE werks     = @i_werks AND
            id_receta = @i_id_receta.

* Obtener posiciones
    CALL METHOD zclpp_recetas_mp=>obtener_posiciones_receta
      EXPORTING
        i_werks         = i_werks
        i_id_receta_std = i_id_receta
      IMPORTING
        ot_lista_mp     = ot_pos_rec_std.

  ENDMETHOD.
  method OBTENER_DESCR_CALIDAD.

  SELECT SINGLE descrip
    INTO  @r_descr
    FROM ztpp0035
    WHERE werks     = @i_werks and
          zcalidad  = @i_calidad.

  endmethod.
  METHOD obtener_id_receta_nuevo.

    DATA l_tp_rango TYPE nrnr.

    CASE i_tp_receta.
      WHEN '2'. "STD
        CASE i_werks.
          WHEN '3000'. l_tp_rango = '03'.
          WHEN '3020'. l_tp_rango = '04'.
          WHEN OTHERS.
        ENDCASE.

      WHEN '1'. "MP
        CASE i_werks.
          WHEN '3000'. l_tp_rango = '01'.
          WHEN '3020'. l_tp_rango = '02'.
          WHEN OTHERS.
        ENDCASE.

      WHEN OTHERS.
    ENDCASE.

    CALL FUNCTION 'NUMBER_GET_NEXT'
      EXPORTING
        nr_range_nr = l_tp_rango
        object      = 'ZPP_RECEMP'
        quantity    = '1'
*       SUBOBJECT   = ' '
*       TOYEAR      = '0000'
       IGNORE_BUFFER                 = 'X'
      IMPORTING
        number      = r_id_receta
*       QUANTITY    =
*       RETURNCODE  =
*     EXCEPTIONS
*       INTERVAL_NOT_FOUND            = 1
*       NUMBER_RANGE_NOT_INTERN       = 2
*       OBJECT_NOT_FOUND              = 3
*       QUANTITY_IS_0                 = 4
*       QUANTITY_IS_NOT_1             = 5
*       INTERVAL_OVERFLOW             = 6
*       BUFFER_OVERFLOW               = 7
*       OTHERS      = 8
      .
    IF sy-subrc <> 0.
* Implement suitable error handling here
    ENDIF.


  ENDMETHOD.
  METHOD obtener_lista_mp.

    SELECT *
      INTO TABLE @DATA(lt_mat_primas)
      FROM ztpp0036
      WHERE lvorm = ' ' and
            werks = @i_werks.

    CLEAR ot_lista_mp.

    LOOP AT lt_mat_primas INTO DATA(ls_mat_prima).
      APPEND INITIAL LINE TO ot_lista_mp ASSIGNING FIELD-SYMBOL(<fs>).
      <fs>-matnr    = ls_mat_prima-matnr.

* Descripción de material
      SELECT SINGLE maktx
        INTO <fs>-maktx
        FROM makt
        WHERE matnr = ls_mat_prima-matnr AND
              spras = sy-langu.
      IF sy-subrc <> 0.
        CLEAR <fs>-maktx.
      ENDIF.

    ENDLOOP.

  ENDMETHOD.
  METHOD obtener_lista_recetas_mp.

    CLEAR ot_cab_recetas_mp[].


    SELECT *
      INTO CORRESPONDING FIELDS OF TABLE @ot_cab_recetas_mp
      FROM ztpp0040
      WHERE  werks        = @i_werks
      ORDER BY fecha_ini , hora_ini DESCENDING
      .

    IF i_calidad IS NOT INITIAL.
      DELETE ot_cab_recetas_mp WHERE zcalidad <> i_calidad.
    ENDIF.
    IF i_contrama IS NOT INITIAL.
      DELETE ot_cab_recetas_mp WHERE contr_ins <> i_contrama.
    ENDIF.

    LOOP AT ot_cab_recetas_mp ASSIGNING FIELD-SYMBOL(<fs>).
      <fs>-descr_calidad = obtener_descr_calidad( i_werks   = <fs>-werks
                                                  i_calidad = <fs>-zcalidad ).
      TRY.
          <fs>-desc_motivo = zclpp_recetas_mp=>gt_motivos[ cod_motivo = <fs>-cod_motivo ]-desc_motivo.
        CATCH cx_root .
      ENDTRY.

    ENDLOOP.
    SORT ot_cab_recetas_mp BY fecha_ini DESCENDING
                              hora_ini  DESCENDING.

    CHECK i_up_to IS NOT INITIAL.

    DATA(l_lin_tabl) = lines( ot_cab_recetas_mp ).

    IF l_lin_tabl > i_up_to.
      DATA(l_ini_del) = i_up_to + 1.

      DELETE  ot_cab_recetas_mp FROM  l_ini_del.
    ENDIF.
  ENDMETHOD.
  METHOD obtener_lista_recetas_standard.

    CLEAR ot_cab_recetas_std[].

    IF i_calidad is not SUPPLIED.

    SELECT *
      INTO TABLE ot_cab_recetas_std
      FROM ztpp0038
      WHERE werks       = i_werks "AND
*            zcalidad    = i_calidad
      ORDER BY id_receta.

    else.

    SELECT *
      INTO TABLE ot_cab_recetas_std
      FROM ztpp0038
      WHERE werks       = i_werks AND
            zcalidad    = i_calidad
      ORDER BY id_receta.

    ENDIF.



  ENDMETHOD.
  METHOD obtener_posiciones_receta.

    CLEAR: ot_lista_mp[].

    SELECT *
      FROM ztpp0039
      INTO TABLE @DATA(lt_pos_rec_std)
      WHERE id_receta = @i_id_receta_std AND
            werks     = @i_werks.
    IF sy-subrc = 0.
      CALL METHOD zclpp_recetas_mp=>obtener_lista_mp
        EXPORTING
          i_werks     = i_werks
        IMPORTING
          ot_lista_mp = ot_lista_mp.

      LOOP AT ot_lista_mp ASSIGNING FIELD-SYMBOL(<fs>).

        TRY.
            <fs>-kg_ciclo   = lt_pos_rec_std[ matnr = <fs>-matnr ]-kg_ciclo.
            <fs>-porc_ciclo = lt_pos_rec_std[ matnr = <fs>-matnr ]-porc_ciclo.

          CATCH cx_root .
        ENDTRY.

      ENDLOOP.


    ENDIF.

  ENDMETHOD.
  METHOD obtener_sociedad_usuario.

* obtenemos los parámetros del usuario para rellenar el centro
    SELECT SINGLE  parva FROM usr05
      INTO r_werks
      WHERE bname = sy-uname AND
            parid = 'WRK'.

  ENDMETHOD.
  METHOD OBTENER_TEMPORALIDAD_RECETA.

    DATA:  l_data_act TYPE datum,
           l_hora_act TYPE uzeit.

    CALL FUNCTION 'DB_LOCAL_TIME'
      IMPORTING
        time = l_hora_act
        date = l_data_act.

    IF I_FECHA_INI > l_data_act OR
      ( I_FECHA_INI = l_data_act AND i_hora_ini > l_hora_act ).

      r_return = 'F'.  "receta Futura o planificada
      exit.
    ENDIF.

    IF ( I_FECHA_INI < l_data_act OR
         ( I_FECHA_INI = l_data_act AND i_hora_ini < l_hora_act ) ) AND
       ( I_FECHA_fin > l_data_act OR
         ( I_FECHA_fin = l_data_act AND i_hora_fin > l_hora_act ) )
        .
      r_return = 'A'. "receta Actual

    ELSE.

      r_return = 'P'. "receta Pasada

    ENDIF.

  ENDMETHOD.
  METHOD validar_fechas_alta_rec_mp.
    DATA: l_msg     TYPE string,
          l_answer  TYPE c,
          l_tmp_now TYPE timestampl,
          l_tmp_in  TYPE timestampl.

* Conversión a timestamp
    GET TIME STAMP FIELD l_tmp_now.
    CONVERT DATE i_fecha TIME i_hora INTO TIME STAMP l_tmp_in TIME ZONE sy-zonlo.

    IF i_fecha IS INITIAL. "  or  i_hora IS INITIAL.
      o_return = '01'.
      EXIT.
    ENDIF.

* Fecha/hora siempre a futuro
    IF  l_tmp_in < l_tmp_now." OR
      o_return = '02'.
      EXIT.
    ENDIF.


    SELECT id_receta , fecha_ini , hora_ini ", timestmp_ini
      INTO TABLE @DATA(lt_rec_futuras)
      FROM ztpp0040
      WHERE werks      = @i_werks and
            (   fecha_ini > @i_fecha or
              ( fecha_ini = @i_Fecha and
                ( hora_ini > @i_hora or hora_ini = @i_hora )
              )
             )

      ORDER BY fecha_ini , hora_ini DESCENDING.
      IF sy-subrc = 0.
        o_return ='03'. "error existe una receta con fecha & y hora &
         sort lt_rec_futuras by fecha_ini DESCENDING
                             hora_ini  DESCENDING.
        o_ult_fecha = lt_rec_futuras[ 1 ]-fecha_ini.
        o_ult_hora  = lt_rec_futuras[ 1 ]-hora_ini.
        return.
      ENDIF.

    IF lt_rec_futuras IS NOT INITIAL.


      TRY.
          DATA(l_prox_fecha) = lt_rec_futuras[ 1 ]-fecha_ini.
          DATA(l_prox_hora)  = lt_rec_futuras[ 1 ]-hora_ini.

          IF lt_rec_futuras[ 1 ]-fecha_ini > i_fecha OR
             ( lt_rec_futuras[ 1 ]-fecha_ini = i_fecha AND
               lt_rec_futuras[ 1 ]-hora_ini > i_hora       ) . "
            o_return = '03'. "error existe una receta con fecha & y hora &
            EXIT.

          ENDIF.

        CATCH cx_root .
          l_prox_fecha = 99991231.
          l_prox_hora  = 235959.
      ENDTRY.

    ENDIF.

    o_return = '00'.

  ENDMETHOD.
  METHOD validar_nueva_fecha_fin_receta.

    DATA: l_hora  TYPE uzeit,
          l_fecha type datum.

    o_rc = '00'.

    CALL METHOD zclpp_recetas_mp=>obtener_datos_form_receta_mp
      EXPORTING
        i_werks                 = i_werks
        i_id_receta             = i_id_receta
      IMPORTING
        os_datos_form_receta_mp = DATA(ls_datos_rec)
        o_rc                    = DATA(l_rc).


* Validamos que la f/h fin de la receta no sea menor que la f/h inicial
    IF  i_nueva_fecha_fin < ls_datos_rec-fecha_ini OR
      ( i_nueva_fecha_fin = ls_datos_rec-fecha_ini AND
        i_nueva_hora_fin <= ls_datos_rec-hora_ini           ).
      o_rc = '01'.
      EXIT.
    ENDIF.

    l_hora = ls_datos_rec-hora_fin + 1.
    IF l_hora = '000000'.
      l_fecha = ls_datos_rec-fecha_fin + 1.
    ELSE.
      l_fecha = ls_datos_rec-fecha_fin.
    ENDIF.

* Buscar siguiente receta.
    SELECT SINGLE * INTO @DATA(l_next_rec)
      FROM ztpp0040
      WHERE werks     = @i_werks AND
            fecha_ini = @l_fecha AND
            hora_ini  = @l_hora.
    IF sy-subrc <> 0.
      o_rc = '02'.  " Siguiente receta no encontrada.
      EXIT.
    ENDIF.

    IF  i_nueva_fecha_fin > l_next_rec-fecha_fin OR
      ( i_nueva_fecha_fin = l_next_rec-fecha_fin AND
        i_nueva_hora_fin >= l_next_rec-hora_fin       ).
      o_rc  = '03'.
    ELSE.
      o_next_receta = l_next_rec-id_receta.
    ENDIF.

  ENDMETHOD.
  METHOD validar_suprimir_receta_mp.
    DATA: l_answer TYPE char1.
    DATA: lv_answer(1) TYPE c.

    CALL METHOD zclpp_recetas_mp=>obtener_datos_form_receta_mp
      EXPORTING
        i_werks                 = i_werks
        i_id_receta             = i_id_receta
      IMPORTING
        os_datos_form_receta_mp = DATA(ls_datos_rec)
        o_rc                    = o_rc.

    CHECK o_rc = '00'.

    TRY.
        DATA(l_tp_rec) = zclpp_recetas_mp=>obtener_temporalidad_receta(
                i_fecha_ini = ls_datos_rec-fecha_ini
                i_hora_ini  = ls_datos_rec-hora_ini
                i_fecha_fin = ls_datos_rec-fecha_fin
                i_hora_fin  = ls_datos_rec-hora_fin
                                                                   ).
        CASE l_tp_rec.
          WHEN 'P'.
            o_rc = '03'.
            EXIT.

          WHEN 'A'. " Si se trata de la receta Actual, comprovamos si se han leido lotes asociados a esta receta.
            SELECT SINGLE mandt INTO sy-mandt
              FROM ztpp0041
              WHERE werks     = i_werks
                AND id_receta = i_id_receta.
            IF sy-subrc = 0.
              o_rc = '05'.
              EXIT.
            ENDIF.

          WHEN OTHERS.
        ENDCASE.


*        IF l_tp_rec = 'P'.  "<> 'F'.   11/11/19 CMS Se permite borrar recetas Actuales.
*          o_rc = '03'.
*          EXIT.
*        ENDIF.

      CATCH cx_root .
    ENDTRY.

* Ventana de confirmación
    DATA(l_msg) = |¿Desea eliminar la receta { i_id_receta }?|.
    CALL FUNCTION 'POPUP_TO_CONFIRM'
      EXPORTING
        titlebar              = 'Borrado de receta MP'
        text_question         = l_msg
        text_button_1         = 'Aceptar'
        text_button_2         = 'Cancelar'
        default_button        = '2'
        display_cancel_button = ' '
      IMPORTING
        answer                = lv_answer
      EXCEPTIONS
        text_not_found        = 1
        OTHERS                = 2.
*
    IF lv_answer EQ 1.
      o_rc = '00'.
    ELSE.
      o_rc = '04'.
    ENDIF.

  ENDMETHOD.
  METHOD validar_tabla_ko_a_cero.

    r_ok = ' '.

*Validar que no esté todo a ceros.
    LOOP AT it_lista_mp INTO DATA(ls_mp) WHERE kg_ciclo > 0.
      r_ok = 'X'.
      exit.
    ENDLOOP.

  ENDMETHOD.
