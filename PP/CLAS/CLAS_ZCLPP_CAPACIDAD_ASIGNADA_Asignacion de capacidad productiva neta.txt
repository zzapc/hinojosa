
CLASS zclpp_capacidad_asignada DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.

    TYPES:
      tty_kweek TYPE RANGE OF kweek .
    TYPES:
      BEGIN OF ty_capac_asignada,
        tp_pedido  TYPE ztp_pedido,
        vbeln      TYPE vbeln,
        posnr      TYPE posnr,
        etenr      TYPE etenr,
        kunnr      TYPE kunnr,
        desc_cli   TYPE char30,
        matnr      TYPE matnr,
        maktx      TYPE maktx,
        matkl      TYPE matkl,
        gramaje    TYPE zgramaje,
        ancho      TYPE zancho,
        menge      TYPE z_cantidad_sin_dec,
        meins      TYPE meins,
        fecha_ent  TYPE datum,
        cant_pend  TYPE z_cantidad_sin_dec,
        meins_pend TYPE meins,
        werks      TYPE werks_d,
        aufnr      TYPE aufnr,
        semana     TYPE kweek,
        vbeln_cli  TYPE bstnk,
      END OF ty_capac_asignada .
**  types:
**    BEGIN OF ty_capac_asignada,
**        werks      TYPE werks_d,
**        kunnr      TYPE kunnr,
**        desc_cli   TYPE char30,
**        vbeln      TYPE vbeln,
**        posnr      TYPE posnr,
**        etenr      TYPE etenr,
**        tp_pedido  TYPE ztp_pedido,
**        matnr      TYPE matnr,
**        maktx      TYPE maktx,
**        matkl      TYPE matkl,
**        ancho      type zancho,
**        gramaje    type zgramaje,
**        menge      TYPE z_cantidad_sin_dec,
**        meins      TYPE meins,
**        fecha_ent  TYPE datum,
**        cant_pend  TYPE z_cantidad_sin_dec,
**        meins_pend TYPE meins,
**        aufnr      TYPE aufnr,
**        semana     type kweek,
**      END OF ty_capac_asignada .
    TYPES:
      tty_capac_asig     TYPE STANDARD TABLE OF ty_capac_asignada .
    TYPES:
      BEGIN OF ty_capac_sin_asig,
        tp_pedido  TYPE ztp_pedido,
        vbeln      TYPE vbeln,
        posnr      TYPE posnr,
        etenr      TYPE etenr,
        kunnr      TYPE kunnr,
        desc_cli   TYPE char30,
        matnr      TYPE matnr,
        maktx      TYPE maktx,
        matkl      TYPE matkl,
        gramaje    TYPE zgramaje,
        ancho      TYPE zancho,
        menge      TYPE z_cantidad_sin_dec,
        meins      TYPE meins,
        fecha_ent  TYPE datum,
        cant_pend  TYPE z_cantidad_sin_dec,
        meins_pend TYPE meins,
        werks      TYPE werks_d,
      END OF ty_capac_sin_asig .
**    TYPES:
**      BEGIN OF ty_capac_sin_asig,
**        werks      TYPE werks_d,
**        kunnr      TYPE kunnr,
**        desc_cli   TYPE char30,
**        vbeln      TYPE vbeln,
**        posnr      TYPE posnr,
**        etenr      TYPE etenr,
**        tp_pedido  TYPE ztp_pedido,
**        matnr      TYPE matnr,
**        maktx      TYPE maktx,
**        matkl      TYPE matkl,
**        ancho      TYPE zancho,
**        gramaje    TYPE zgramaje,
**        menge      TYPE z_cantidad_sin_dec,
**        meins      TYPE meins,
**        fecha_ent  TYPE datum,
**        cant_pend  TYPE z_cantidad_sin_dec,
**        meins_pend TYPE meins,
**      END OF ty_capac_sin_asig .
    TYPES:
      tty_capac_sin_asig TYPE STANDARD TABLE OF ty_capac_sin_asig .
    TYPES:
      tty_ztpp0072 TYPE STANDARD TABLE OF ztpp0072 .
    TYPES:
      BEGIN OF ty_clientes,
        kunnr TYPE kunnr,
        name1 TYPE name1_gp,
      END OF ty_clientes .
    TYPES:
      tty_clientes TYPE STANDARD TABLE OF ty_clientes .
    TYPES:
      BEGIN OF ty_pedidos,
        vbeln  TYPE vbeln,
        posnr  TYPE posnr,
        etenr  TYPE etenr,
        tp_ped TYPE ztp_pedido,
      END OF ty_pedidos .
    TYPES:
      tty_pedidos TYPE STANDARD TABLE OF ty_pedidos .
    TYPES:
      BEGIN OF ty_ancho_gram,
        matnr   TYPE matnr,
        ancho   TYPE zancho,
        gramaje TYPE zgramaje,
        matkl   TYPE matkl,
      END OF ty_ancho_gram .
    TYPES:
      tty_ancho_gram TYPE STANDARD TABLE OF ty_ancho_gram .

    DATA gc_dias_margen_pedido TYPE numc2 .
    CLASS-DATA gc_dias_maximos TYPE numc2 VALUE 30 ##NO_TEXT.
    DATA g_werks TYPE werks_d .
    DATA g_semana TYPE kweek .
    DATA g_semana_actual TYPE kweek .
    DATA g_semana_ant TYPE kweek .
    DATA g_semana_sig TYPE kweek .
    DATA g_dia_ini_semana TYPE datum .
    DATA g_dia_fin_semana TYPE datum .
    DATA g_kg_netos_presup TYPE z_cantidad_sin_dec .
    DATA g_kg_capac_asig TYPE z_cantidad_sin_dec .
    DATA g_kg_ajust_capac TYPE z_cantidad_sin_dec .
    DATA g_kg_capac_disp TYPE z_cantidad_sin_dec .
    DATA g_porc_disp TYPE z_cantidad_sin_dec .
    DATA g_icono_disp TYPE icon_d .
    DATA gt_pedidos_asig TYPE tty_capac_asig .
    DATA gt_pedidos_sin_asig TYPE tty_capac_sin_asig .
    DATA gc_gramaje_medio TYPE zgramaje .
    DATA gc_ancho_medio TYPE zancho .

    METHODS constructor
      IMPORTING
        !i_werks  TYPE werks_d
        !i_mjahr  TYPE mjahr
        !i_semana TYPE numc2 .
    CLASS-METHODS instanciar_semana
      IMPORTING
        !i_werks                 TYPE werks_d
        !i_mjahr                 TYPE mjahr
        !i_semana                TYPE numc2
        !i_sw_no_act_sin_asig    TYPE flag OPTIONAL
        !i_sw_no_rep_asig_con_of TYPE flag DEFAULT ' '
      EXPORTING
        !or_semana               TYPE REF TO zclpp_capacidad_asignada
        !o_rc                    TYPE char2 .
    CLASS-METHODS cambiar_semana
      IMPORTING
        !i_tp_cambio TYPE char1
      EXPORTING
        !o_rc        TYPE char02
      CHANGING
        !cr_semana   TYPE REF TO zclpp_capacidad_asignada .
    METHODS refrescar_datos_ped_sin_asig .
    CLASS-METHODS get_orden_fabric_pedido
      IMPORTING
        !i_pedido      TYPE ebeln
        !i_posicion    TYPE ebelp
        !i_reparto     TYPE eeten
      EXPORTING
        !o_of_asignada TYPE aufnr
        !o_estat       TYPE j_status
        !o_kg_plan     TYPE z_cantidad_sin_dec
        !o_kg_fabr     TYPE z_cantidad_sin_dec
        !o_meins       TYPE meins
        !o_fecha_fabr  TYPE datum .
    METHODS asignar_pedidos_a_semana
      IMPORTING
        !it_pedidos TYPE tty_pedidos
        !i_semana   TYPE kweek
      EXPORTING
        !o_rc       TYPE char2 .
    CLASS-METHODS get_informe_resumen
      IMPORTING
        !i_werks          TYPE werks_d
        !it_kweek         TYPE tty_kweek
      EXPORTING
        !ot_repartos_asig TYPE tty_capac_asig .
    CLASS-METHODS get_descripcion_cliente
      IMPORTING
        !i_kunnr               TYPE kunnr
      RETURNING
        VALUE(r_descr_cliente) TYPE name1_gp .
    CLASS-METHODS get_ancho_gramaje_material
      IMPORTING
        !i_matnr   TYPE matnr
      EXPORTING
        !o_ancho   TYPE zancho
        !o_gramaje TYPE zgramaje
        !o_matkl   TYPE matkl .
    METHODS get_pedidos_asignados
      IMPORTING
        !iw_no_ofs TYPE flag DEFAULT ' ' .
protected section.
private section.

  class-data GT_CLIENTES type TTY_CLIENTES .
  class-data GT_MATNR_ANCHO_GRAM type TTY_ANCHO_GRAM .

  class-methods VALIDAR_SEMANA
    importing
      !I_WERKS type WERKS_D
      !I_MJAHR type MJAHR
      !I_SEMANA type NUMC2
    exporting
      !O_RC type CHAR2 .
  class-methods GET_PEDIDOS_COMPRA_SIN_ASIGNAR
    importing
      !I_WERKS type WERKS_D
    exporting
      value(RT_PEDIDOS_COMPRA_NO_ASIG) type TTY_CAPAC_SIN_ASIG .
  class-methods GET_PEDIDOS_VENTA_SIN_ASIGNAR
    importing
      !I_WERKS type WERKS_D
    changing
      value(OT_PEDIDOS_VENTA_NO_ASIG) type TTY_CAPAC_SIN_ASIG .
  methods GET_PEDIDOS_SIN_ASIGNAR .
  methods GET_KG_CAPAC_PROD_NETA .
endclass. "ZCLPP_CAPACIDAD_ASIGNADA definition
class ZCLPP_CAPACIDAD_ASIGNADA implementation.
  METHOD ASIGNAR_PEDIDOS_A_SEMANA.

    DATA: l_ztpp72 TYPE ztpp0072.

    o_rc = '00'.

    LOOP AT it_pedidos INTO DATA(ls_pedido).

      IF i_semana IS NOT INITIAL.

        CLEAR l_ztpp72.
        l_ztpp72-werks      = me->g_werks.
        l_ztpp72-vbeln      = ls_pedido-vbeln.
        l_ztpp72-posnr      = ls_pedido-posnr.
        l_ztpp72-etenr      = ls_pedido-etenr.
        l_ztpp72-tp_pedido  = ls_pedido-tp_ped.
        l_ztpp72-semana     = i_semana.
        MODIFY ztpp0072 FROM l_ztpp72.

      else.

        delete from ztpp0072 where werks      = me->g_werks     and
                                   vbeln      = ls_pedido-vbeln and
                                   posnr      = ls_pedido-posnr and
                                   etenr      = ls_pedido-etenr and
                                   tp_pedido  = ls_pedido-tp_ped.

      ENDIF.

    ENDLOOP.

    CALL METHOD me->get_pedidos_asignados( ).

* Si pasa de SIN_ASIG(alv inf) a ASIGNADO(alv sup), refrescamos pedidos sin asignar.
    IF g_semana = i_semana or
       i_semana is INITIAL.
      CALL METHOD me->get_pedidos_sin_asignar( ).
    ENDIF.

  ENDMETHOD.
  METHOD cambiar_semana.

    DATA: l_fecha_ini TYPE datum,
          l_werks     TYPE werks_d,
          l_kweek     TYPE kweek.

    l_werks = cr_semana->g_werks.

    CASE i_tp_cambio.
      WHEN '-'.
        l_fecha_ini = cr_semana->g_dia_ini_semana - 7.
      WHEN '+'.
        l_fecha_ini = cr_semana->g_dia_ini_semana + 7.
      WHEN '='.
        l_fecha_ini = sy-datum.
      WHEN OTHERS.
    ENDCASE.

    CALL FUNCTION 'DATE_GET_WEEK'
      EXPORTING
        date         = l_fecha_ini
      IMPORTING
        week         = l_kweek
      EXCEPTIONS
        date_invalid = 1
        OTHERS       = 2.
    IF sy-subrc <> 0.
* Implement suitable error handling here
    ENDIF.

    FREE cr_semana.
    CALL METHOD zclpp_capacidad_asignada=>instanciar_semana
      EXPORTING
        i_werks              = l_werks
        i_mjahr              = l_kweek(4)
        i_semana             = l_kweek+4(2)
        i_sw_no_act_sin_asig = 'X'
      IMPORTING
        or_semana            = cr_semana
        o_rc                 = o_rc.


  ENDMETHOD.
  METHOD constructor.

    g_semana = |{ i_mjahr }{ i_semana }|.
    g_werks  = i_werks.

    CALL FUNCTION 'WEEK_GET_FIRST_DAY'
      EXPORTING
        week         = g_semana
      IMPORTING
        date         = g_dia_ini_semana
      EXCEPTIONS
        week_invalid = 1
        OTHERS       = 2.
    IF sy-subrc <> 0.
* Implement suitable error handling here
    ELSE.
* Asignamos el dia final de la semana.
      g_dia_fin_semana = g_dia_ini_semana + 6.
    ENDIF.

*Obtener semana anterior, siguiente y actual.
    DATA: l_fecha TYPE datum.
    l_fecha = g_dia_ini_semana + 7.
    CALL FUNCTION 'DATE_GET_WEEK'
      EXPORTING
        date         = l_fecha
      IMPORTING
        week         = g_semana_sig
      EXCEPTIONS
        date_invalid = 1
        OTHERS       = 2.

    l_fecha = g_dia_ini_semana - 7.
    CALL FUNCTION 'DATE_GET_WEEK'
      EXPORTING
        date         = l_fecha
      IMPORTING
        week         = g_semana_ant
      EXCEPTIONS
        date_invalid = 1
        OTHERS       = 2.

    CALL FUNCTION 'DATE_GET_WEEK'
      EXPORTING
        date         = sy-datum
      IMPORTING
        week         = g_semana_actual
      EXCEPTIONS
        date_invalid = 1
        OTHERS       = 2.

* Obtenemos los kg.presupuestados
    SELECT SINGLE kg_netos_presup
      INTO @g_kg_netos_presup
      FROM ztpp0070
      WHERE werks  = @i_werks AND
            semana = @g_semana.

    SELECT SINGLE valor
      INTO @DATA(l_dias)
      FROM ztpp0033
      WHERE werks = @i_werks AND
            id_parm = 'DIAS_MARG'.

    gc_dias_margen_pedido = CONV #( l_dias ).

* Obtenemos el gramaje y ancho medios según planta y ejercicio.
    SELECT SINGLE gramaje_medio , ancho_medio
      INTO ( @gc_gramaje_medio , @gc_ancho_medio )
      FROM ztpp0071
      WHERE werks = @i_werks and
            mjahr = @i_mjahr.

* Obtenemos datos asignados a esta semana.
    SELECT *
      INTO TABLE @DATA(gt_asig)
      FROM ztpp0072
      WHERE werks  = @i_werks AND
            semana = @g_semana.


  ENDMETHOD.
  METHOD get_ancho_gramaje_material.

    TRY.

        o_ancho   = gt_matnr_ancho_gram[ matnr = i_matnr ]-ancho.
        o_gramaje = gt_matnr_ancho_gram[ matnr = i_matnr ]-gramaje.
        o_matkl   = gt_matnr_ancho_gram[ matnr = i_matnr ]-matkl.

      CATCH cx_root .

        SELECT SINGLE matkl , zzancho , zzgramaje
          INTO @DATA(ls_mat)
          FROM mara
          WHERE matnr = @i_matnr.
        IF sy-subrc = 0.

          APPEND INITIAL LINE TO gt_matnr_ancho_gram ASSIGNING FIELD-SYMBOL(<fs_mat>).
          <fs_mat>-matnr    = i_matnr.
          <fs_mat>-ancho    = o_ancho   = ls_mat-zzancho.
          <fs_mat>-gramaje  = o_gramaje = ls_mat-zzgramaje.
          <fs_mat>-matkl    = o_matkl   = ls_mat-matkl.
*          o_ancho           = ls_mat-zzancho.
*          o_gramaje         = ls_mat-zzgramaje.
*          o_matkl           = ls_mat-matkl.

        ENDIF.

    ENDTRY.




  ENDMETHOD.
  METHOD get_descripcion_cliente.

    TRY.
        r_descr_cliente =    gt_clientes[ kunnr = i_kunnr ]-name1.

      CATCH cx_root .

        SELECT SINGLE name1
          INTO @r_descr_cliente
          FROM kna1
          WHERE kunnr = @i_kunnr.

        IF sy-subrc = 0.
          APPEND INITIAL LINE TO gt_clientes ASSIGNING FIELD-SYMBOL(<fs_kna1>).
          <fs_kna1>-kunnr = i_kunnr.
          <fs_kna1>-name1 = r_descr_cliente.
        ELSE.
          CLEAR r_descr_cliente.
        ENDIF.

    ENDTRY.


  ENDMETHOD.
  METHOD get_informe_resumen.

    DATA: l_mjahr   TYPE mjahr,
          l_sem     TYPE numc2,
          lr_semana TYPE REF TO zclpp_capacidad_asignada,
          lt_rep_asig type TTY_CAPAC_ASIG .



    SELECT DISTINCT semana
      INTO TABLE @DATA(lt_semanas)
      FROM ztpp0072
      WHERE werks = @i_werks AND
            semana IN @it_kweek.

    clear ot_repartos_asig.

    LOOP AT lt_semanas INTO DATA(ls_semana).

      CLEAR: l_mjahr , l_sem.

      l_mjahr = ls_semana(4).
      l_sem   = ls_semana+4(2).

      CALL METHOD zclpp_capacidad_asignada=>instanciar_semana
        EXPORTING
          i_werks              = i_werks
          i_mjahr              = l_mjahr
          i_semana             = l_sem
          i_sw_no_act_sin_asig = 'X'
        IMPORTING
          or_semana            = lr_semana.

      append lines of lr_semana->gt_pedidos_asig[] to ot_repartos_asig[].

    ENDLOOP.




  ENDMETHOD.
  METHOD get_kg_capac_prod_neta.

    data: l_aj_ancho type z_cantidad_sin_dec,
          l_aj_gram  type z_cantidad_sin_dec.

    CLEAR: g_kg_capac_asig , g_kg_ajust_capac.

* Acumulamos kg. pendientes de servir, y se calcula el ajuste.
    LOOP AT gt_pedidos_asig INTO DATA(ls_ped_asig).

      g_kg_capac_asig = g_kg_capac_asig + ls_ped_asig-menge.

      CALL METHOD zclpp_capacidad_asignada=>get_ancho_gramaje_material
        EXPORTING
          i_matnr   = ls_ped_asig-matnr
        IMPORTING
          o_ancho   = data(l_ancho)
          o_gramaje = data(l_gram).

*      clear: l_aj_ancho , l_aj_gram.
      l_aj_ancho = ls_ped_asig-menge - ( ( gc_ancho_medio    * ls_ped_asig-menge ) /  l_ancho ).
      l_aj_gram  = ls_ped_asig-menge - ( ( gc_gramaje_medio  * ls_ped_asig-menge ) / l_gram   ).

      g_kg_ajust_capac = g_kg_ajust_capac + l_aj_ancho + l_aj_gram.

    ENDLOOP.


    IF g_kg_netos_presup = 0.
      g_porc_disp = 0.
    ELSE.
      g_kg_capac_asig = g_kg_capac_asig * ( -1 ).
      g_kg_capac_disp = g_kg_netos_presup + g_kg_capac_asig + g_kg_ajust_capac .
      g_porc_disp     = ( g_kg_capac_disp * 100 ) / g_kg_netos_presup.
    ENDIF.

    IF g_porc_disp <= 0.
      WRITE icon_red_light AS ICON TO g_icono_disp.
    ELSEIF g_porc_disp < 20.
      WRITE icon_yellow_light AS ICON TO g_icono_disp.
    ELSE.
      WRITE icon_green_light AS ICON TO g_icono_disp.
    ENDIF.


  ENDMETHOD.
  METHOD GET_ORDEN_FABRIC_PEDIDO.

    CONSTANTS:  lc_aufa_bob TYPE aufart VALUE 'ZI02'.

    DATA: l_wemng TYPE menge_d.
    DATA: l_posnr TYPE ZZEBELP1,
          l_kg_plan type menge_d,
          l_kg_fabr type menge_d.



    CLEAR: o_of_asignada , o_estat.

    SELECT SINGLE a~aufnr , b~stat , c~wemng , ko~gltrp , ko~gmein , ko~gamng , ko~igmng
    INTO ( @o_of_asignada , @o_estat , @l_wemng , @o_fecha_fabr , @o_meins , @l_kg_plan , @l_kg_fabr )
    FROM aufk AS a
    INNER JOIN afko AS ko ON ko~aufnr = a~aufnr
    INNER JOIN jest AS b ON b~objnr = a~objnr
         INNER JOIN afpo AS c ON a~aufnr = c~aufnr
            WHERE ( a~auart = @lc_aufa_bob )
              AND ( ( a~zzebeln1 = @i_pedido AND a~zzebelp1 = @i_posicion AND a~zzurgt1  = @i_reparto ) OR
                    ( a~zzebeln2 = @i_pedido AND a~zzebelp2 = @i_posicion AND a~zzurgt2  = @i_reparto ) OR
                    ( a~zzebeln3 = @i_pedido AND a~zzebelp3 = @i_posicion AND a~zzurgt3  = @i_reparto ) )
              AND b~inact = @space
              AND ( ( stat = 'I0045' AND wemng <> 0 ) OR
                      stat = 'I0046' OR "uDefine - Ticket 45747 - Cosiderar CERR como CTEC
                      stat = 'I0002' OR
                      stat = 'I0001'
                  ).

      o_kg_fabr = l_kg_fabr.
      o_kg_plan = l_kg_plan.
*      SELECT SINGLE a~aufnr , b~gmein , b~plnbez , b~gltrp , b~gamng , b~igmng " GAMNG->kg plan rollo  // IGMNG->kg.fabricados rollo // ZZAUFNR_ROLLO
*        INTO @DATA(ls_of_rollo)
*        FROM aufk AS a
*        INNER JOIN afko AS b ON a~aufnr = b~aufnr
*        WHERE b~aufnr = @ls_of_bobi-zzaufnr_rollo AND
*              a~auart = @c_zi01.


  ENDMETHOD.
  METHOD get_pedidos_asignados.

    DATA: l_ebelp         TYPE ebelp,
          lv_tot_entregas TYPE menge_d.

    CLEAR: gt_pedidos_asig[].

    SELECT *
      INTO TABLE @DATA(lt_zpp72)
      FROM ztpp0072
      WHERE werks  = @me->g_werks AND
            semana = @me->g_semana.

    LOOP AT lt_zpp72 INTO DATA(ls_zpp72).

      APPEND INITIAL LINE TO gt_pedidos_asig ASSIGNING FIELD-SYMBOL(<fs_ped_asig>).
      <fs_ped_asig>-werks       = ls_zpp72-werks.
      <fs_ped_asig>-vbeln       = ls_zpp72-vbeln.
      <fs_ped_asig>-posnr       = ls_zpp72-posnr.
      <fs_ped_asig>-etenr       = ls_zpp72-etenr.
      <fs_ped_asig>-tp_pedido   = ls_zpp72-tp_pedido.
      <fs_ped_asig>-semana      = ls_zpp72-semana.


* Datos complementarios
      CASE ls_zpp72-tp_pedido.
        WHEN 'C'.

          SELECT SINGLE  e~glmng ,  e~eindt , e~menge , v~kunnr , p~matnr , p~meins , p~matkl
            INTO @DATA(ls_compra)
            FROM eket AS e
            INNER JOIN ekpo AS p ON e~ebeln = p~ebeln AND e~ebelp = p~ebelp
            INNER JOIN ekpv AS v ON e~ebeln = v~ebeln AND e~ebelp = v~ebelp
            WHERE e~ebeln  = @ls_zpp72-vbeln AND
                  e~ebelp  = @ls_zpp72-posnr AND
                  e~etenr  = @ls_zpp72-etenr.
          IF sy-subrc = 0.
            <fs_ped_asig>-matnr      = ls_compra-matnr.
            <fs_ped_asig>-maktx      =  zclpp_recetas_reparto_pulper=>obtener_descr_material( <fs_ped_asig>-matnr ).

            <fs_ped_asig>-matkl      = ls_compra-matkl.
            <fs_ped_asig>-meins      = ls_compra-meins.
            <fs_ped_asig>-meins_pend = ls_compra-meins.
            <fs_ped_asig>-cant_pend  = ls_compra-glmng.
            <fs_ped_asig>-fecha_ent  = ls_compra-eindt.
            <fs_ped_asig>-menge      = ls_compra-menge.
            <fs_ped_asig>-kunnr      = ls_compra-kunnr.
            <fs_ped_asig>-desc_cli   =  get_descripcion_cliente( <fs_ped_asig>-kunnr ).

* Cálculo de Kg.pendientes
            IF ls_compra-glmng >= ls_compra-menge.
              <fs_ped_asig>-cant_pend = 0.
            ELSE.
              <fs_ped_asig>-cant_pend         =  <fs_ped_asig>-menge - ls_compra-glmng. "GLMNG= cant.suministrada
            ENDIF.

          ENDIF.

        WHEN 'V'.

          SELECT SINGLE k~vbeln , k~kunnr, k~vbtyp, p~posnr , p~matnr , p~matkl ,
                        p~kwmeng, r~etenr , r~lifsp , r~edatu , r~wmeng , r~vrkme , r~bmeng
            INTO @DATA(ls_venta)
            FROM vbak AS k
            INNER JOIN vbap AS p  ON p~vbeln  = k~vbeln
            INNER JOIN vbep AS r  ON r~vbeln  = p~vbeln AND r~posnr = p~posnr
*            INTO TABLE @DATA(lt_datos_ventas)
            WHERE k~vbeln  = @ls_zpp72-vbeln AND
                  p~posnr  = @ls_zpp72-posnr AND
                  r~etenr  = @ls_zpp72-etenr.

          IF sy-subrc = 0.
* DTM - Flujo de documentos: buscar  pedido->entrega para calcular cantidad pendiente
            SELECT vbfa~vbelv , vbfa~posnv , vbfa~vbeln , vbfa~posnn , rfmng , lfimg , kcmeng
              FROM vbfa
              INNER JOIN lips ON lips~vbeln = vbfa~vbeln AND lips~posnr = vbfa~posnn
            INTO TABLE @DATA(lt_vbfa)
             WHERE vbfa~vbelv = @ls_venta-vbeln
               AND vbfa~posnv = @ls_venta-posnr
               AND vbfa~posnn < '900000'
               AND vbtyp_n    IN ('J','T')
               AND vbtyp_v    = @ls_venta-vbtyp
               AND stufe      = '00'.

            SORT lt_vbfa BY vbelv posnv vbeln posnn.

* Cantidad pendiente
            CLEAR lv_tot_entregas.
            READ TABLE lt_vbfa ASSIGNING FIELD-SYMBOL(<lfs_vbfa>) WITH KEY vbelv = ls_venta-vbeln
                                                                           posnv = ls_venta-posnr BINARY SEARCH.

            IF sy-subrc EQ 0.
              IF <lfs_vbfa>-kcmeng IS NOT INITIAL.
                lv_tot_entregas = lv_tot_entregas + <lfs_vbfa>-kcmeng.
              ELSEIF <lfs_vbfa>-lfimg IS NOT INITIAL.
                lv_tot_entregas = lv_tot_entregas + <lfs_vbfa>-lfimg.
              ENDIF.
            ENDIF.

* Si la cantidad entregada >= cantidad pedido, eliminar linea.
            IF lv_tot_entregas >= ls_venta-kwmeng.
              CLEAR ls_venta.
              CONTINUE.
            ENDIF.
* DTM

            <fs_ped_asig>-kunnr      =  ls_venta-kunnr.
            <fs_ped_asig>-desc_cli   =  get_descripcion_cliente( <fs_ped_asig>-kunnr ).
            <fs_ped_asig>-matnr      =  ls_venta-matnr.
            <fs_ped_asig>-maktx      =  zclpp_recetas_reparto_pulper=>obtener_descr_material( <fs_ped_asig>-matnr ).
            <fs_ped_asig>-matkl      =  ls_venta-matkl.
            <fs_ped_asig>-fecha_ent  =  ls_venta-edatu.
            <fs_ped_asig>-menge      =  ls_venta-wmeng.
            <fs_ped_asig>-meins      =  ls_venta-vrkme.
            <fs_ped_asig>-meins_pend =  ls_venta-vrkme.

            IF lv_tot_entregas >= <fs_ped_asig>-menge. "ls_venta-bmeng DTM
              <fs_ped_asig>-cant_pend = 0.
            ELSE.
              <fs_ped_asig>-cant_pend = <fs_ped_asig>-menge - lv_tot_entregas. "ls_venta-bmeng. DTM
            ENDIF.

          ENDIF.

        WHEN OTHERS.
      ENDCASE.

      CLEAR: l_ebelp , <fs_ped_asig>-aufnr.
      l_ebelp = <fs_ped_asig>-posnr.

      CALL METHOD zclpp_capacidad_asignada=>get_orden_fabric_pedido
        EXPORTING
          i_pedido      = <fs_ped_asig>-vbeln
          i_posicion    = l_ebelp
          i_reparto     = <fs_ped_asig>-etenr
        IMPORTING
          o_of_asignada = <fs_ped_asig>-aufnr.

      IF iw_no_ofs = 'X' AND <fs_ped_asig>-aufnr IS NOT INITIAL.
        CLEAR: <fs_ped_asig>.
      ENDIF.

      CALL METHOD zclpp_capacidad_asignada=>get_ancho_gramaje_material
        EXPORTING
          i_matnr   = <fs_ped_asig>-matnr
        IMPORTING
          o_ancho   = <fs_ped_asig>-ancho
          o_gramaje = <fs_ped_asig>-gramaje.


    ENDLOOP.

* Borramos registros reseteados por tener OF.
    DELETE gt_pedidos_asig WHERE vbeln IS INITIAL.

    SORT gt_pedidos_asig BY fecha_ent ASCENDING.

    CALL METHOD get_kg_capac_prod_neta.

  ENDMETHOD.
  METHOD get_pedidos_compra_sin_asignar.

    CONSTANTS: gc_zint     TYPE char4  VALUE 'ZINT',
               gc_aufa_rol TYPE aufart VALUE 'ZI01',
               gc_aufa_bob TYPE aufart VALUE 'ZI02'.

    DATA: lr_lifnr  TYPE RANGE OF lifnr,
          lrs_lifnr LIKE LINE OF lr_lifnr.

    CLEAR: rt_pedidos_compra_no_asig[].

* Obtenemos proveedores del centro.
    SELECT lifnr , name1
      INTO TABLE @DATA(lt_lifnr)
      FROM lfa1
      WHERE werks = @i_werks.
    IF sy-subrc = 0.
      LOOP AT lt_lifnr INTO DATA(ls_lifnr).
        CLEAR lrs_lifnr.
        lrs_lifnr-sign = 'I'.
        lrs_lifnr-option = 'EQ'.
        lrs_lifnr-low = ls_lifnr-lifnr.
        APPEND lrs_lifnr TO lr_lifnr .
      ENDLOOP.
    ENDIF.

    SELECT a~ebeln , "a~kunnr ,
           b~ebelp , b~meins , b~matnr , b~matkl ,
           c~etenr , c~glmng , c~eindt , c~menge ,
           v~kunnr
      INTO TABLE @DATA(lt_datos_compra)
     FROM ekko AS a
      INNER JOIN ekpo AS b ON a~ebeln = b~ebeln
      INNER JOIN eket AS c ON b~ebeln = c~ebeln AND b~ebelp = c~ebelp
      INNER JOIN ekpv AS v ON b~ebeln = v~ebeln AND b~ebelp = v~ebelp
      WHERE a~lifnr       IN @lr_lifnr
        AND a~bsart       EQ @gc_zint
        AND a~reswk       <> @space
        AND b~elikz       EQ @space
        AND b~loekz       EQ @space
        AND b~zzbloq_plan EQ @space
        AND ( a~zzsegcal <> '2' AND a~zzsegcal <> '3' )
        AND b~zzbloq_fabr <> 'X'
        AND b~eglkz       <> 'X'
        AND b~zzabgru     EQ @space
        AND b~retpo       <> 'X'
        .

    CHECK NOT lt_datos_compra[] IS INITIAL.

    LOOP AT lt_datos_compra INTO DATA(ls_compras).

      CALL METHOD zclpp_capacidad_asignada=>get_orden_fabric_pedido
        EXPORTING
          i_pedido      = ls_compras-ebeln
          i_posicion    = ls_compras-ebelp
          i_reparto     = ls_compras-etenr
        IMPORTING
          o_of_asignada = DATA(l_of).

      IF l_of IS NOT INITIAL.
        CONTINUE.
      ENDIF.

      APPEND INITIAL LINE TO rt_pedidos_compra_no_asig ASSIGNING FIELD-SYMBOL(<fs_pedido_no_asig>).

      <fs_pedido_no_asig>-werks             =  i_werks.
      <fs_pedido_no_asig>-kunnr             =  ls_compras-kunnr.
      <fs_pedido_no_asig>-desc_cli          =  get_descripcion_cliente( <fs_pedido_no_asig>-kunnr ).
      <fs_pedido_no_asig>-vbeln             =  ls_compras-ebeln.
      <fs_pedido_no_asig>-posnr             =  ls_compras-ebelp.
      <fs_pedido_no_asig>-etenr             =  ls_compras-etenr.
      <fs_pedido_no_asig>-tp_pedido         =  'C'.
      <fs_pedido_no_asig>-matnr             =  ls_compras-matnr.
      <fs_pedido_no_asig>-maktx             =  zclpp_recetas_reparto_pulper=>obtener_descr_material( <fs_pedido_no_asig>-matnr ).
      <fs_pedido_no_asig>-matkl             =  ls_compras-matkl.

      CALL METHOD zclpp_capacidad_asignada=>get_ancho_gramaje_material
        EXPORTING
          i_matnr   = <fs_pedido_no_asig>-matnr
        IMPORTING
          o_ancho   = <fs_pedido_no_asig>-ancho
          o_gramaje = <fs_pedido_no_asig>-gramaje.


      <fs_pedido_no_asig>-fecha_ent         =  ls_compras-eindt.
      <fs_pedido_no_asig>-menge             =  ls_compras-menge.
      <fs_pedido_no_asig>-meins             =  ls_compras-meins.
      <fs_pedido_no_asig>-meins_pend        =  ls_compras-meins.

* Cálculo de Kg.pendientes
      IF ls_compras-glmng > ls_compras-menge.
        <fs_pedido_no_asig>-cant_pend = 0.
      ELSE.
        <fs_pedido_no_asig>-cant_pend         =  <fs_pedido_no_asig>-menge - ls_compras-glmng. "GLMNG= cant.suministrada
      ENDIF.
    ENDLOOP.

  ENDMETHOD.
  METHOD get_pedidos_sin_asignar.

    DATA: l_semana TYPE kweek.

    CALL METHOD zclpp_capacidad_asignada=>get_pedidos_compra_sin_asignar
      EXPORTING
        i_werks                   = me->g_werks
      IMPORTING
        rt_pedidos_compra_no_asig = me->gt_pedidos_sin_asig[].

    CALL METHOD zclpp_capacidad_asignada=>get_pedidos_venta_sin_asignar
      EXPORTING
        i_werks                  = me->g_werks
      CHANGING
        ot_pedidos_venta_no_asig = me->gt_pedidos_sin_asig[].

* Selección de pedidos asignados
    SELECT werks , vbeln , posnr , etenr , semana , tp_pedido
      INTO TABLE @DATA(lt_asig)
      FROM ztpp0072
      FOR ALL ENTRIES IN @gt_pedidos_sin_asig
      WHERE werks = @gt_pedidos_sin_asig-werks AND
            vbeln = @gt_pedidos_sin_asig-vbeln AND
            posnr = @gt_pedidos_sin_asig-posnr AND
            etenr = @gt_pedidos_sin_asig-etenr AND
            semana IS NOT NULL.

* Descartar pedidos ya asignados.
    LOOP AT gt_pedidos_sin_asig ASSIGNING FIELD-SYMBOL(<fs_ped_sin>).

      CLEAR l_semana.

      TRY.
          l_semana = lt_asig[ werks     = <fs_ped_sin>-werks
                              vbeln     = <fs_ped_sin>-vbeln
                              posnr     = <fs_ped_sin>-posnr
                              etenr     = <fs_ped_sin>-etenr
                              tp_pedido = <fs_ped_sin>-tp_pedido
                             ]-semana.
            CLEAR <fs_ped_sin>.
            continue.
        CATCH cx_root .
      ENDTRY.



    ENDLOOP.

    DELETE gt_pedidos_sin_asig WHERE vbeln IS INITIAL.

    SORT gt_pedidos_sin_asig BY fecha_ent ASCENDING.

  ENDMETHOD.
  METHOD get_pedidos_venta_sin_asignar.

    CONSTANTS: gc_zta      TYPE char3 VALUE 'ZTA'.

    DATA: l_spebe           TYPE spebe.

    DATA: lr_vkorg        TYPE RANGE OF vbak-vkorg,
          lrs_vkorg       LIKE LINE OF lr_vkorg,
          lv_tot_entregas TYPE menge_d.

    SELECT bwkey
      FROM t001w
      INTO TABLE @DATA(lt_bwkey)
      WHERE werks = @i_werks.

    IF NOT lt_bwkey[] IS INITIAL.

      SELECT bukrs
        INTO TABLE @DATA(lt_bukrs)
        FROM t001k
        FOR ALL ENTRIES IN @lt_bwkey
        WHERE bwkey = @lt_bwkey-bwkey.

      IF NOT lt_bukrs[] IS INITIAL.

        SELECT vkorg
          FROM tvko
          INTO TABLE @DATA(lt_vkorg)
          FOR ALL ENTRIES IN @lt_bukrs
          WHERE bukrs = @lt_bukrs-bukrs.

        LOOP AT lt_vkorg INTO DATA(ls_vkorg).
          CLEAR: lrs_vkorg.
          lrs_vkorg-sign = 'I'.
          lrs_vkorg-option = 'EQ'.
          lrs_vkorg-low = ls_vkorg-vkorg.
          APPEND lrs_vkorg TO lr_vkorg.
        ENDLOOP.

      ENDIF.
    ENDIF.


    SELECT k~vbeln , k~kunnr, k~vbtyp , p~posnr , p~matnr , p~matkl,
           p~kwmeng, r~etenr , r~lifsp , r~edatu , r~wmeng , r~vrkme , r~bmeng
      FROM vbak AS k
      INNER JOIN vbap AS p  ON p~vbeln  = k~vbeln
      INNER JOIN vbep AS r  ON r~vbeln  = p~vbeln AND r~posnr = p~posnr
      INTO TABLE @DATA(lt_datos_ventas)
      WHERE k~vkorg       IN @lr_vkorg
        AND k~auart       EQ @gc_zta "OR auart EQ gc_ztas )
        AND k~lifsk       EQ @space
        AND k~zzsegcal    = ' '
*        AND st~lfsta      EQ 'C'
        AND p~pstyv = 'TAN'
        AND p~abgru EQ @space
        AND r~wmeng > 0.

    SELECT * FROM vbup INTO TABLE @DATA(lt_vbup)
                       FOR ALL ENTRIES IN @lt_datos_ventas
                       WHERE vbeln = @lt_datos_ventas-vbeln AND lfsta EQ 'C'. "estatus de entrega de la posicion
    IF sy-subrc EQ 0.
      LOOP AT lt_vbup INTO DATA(ls_vbup).
        DELETE lt_datos_ventas WHERE vbeln = ls_vbup-vbeln AND posnr = ls_vbup-posnr.
      ENDLOOP.
    ENDIF.

* DTM - Flujo de documentos: buscar  pedido->entrega para calcular cantidad pendiente
    SELECT vbfa~vbelv , vbfa~posnv , vbfa~vbeln , vbfa~posnn , rfmng , lfimg , kcmeng
      FROM vbfa
      INNER JOIN lips ON lips~vbeln = vbfa~vbeln AND lips~posnr = vbfa~posnn
    INTO TABLE @DATA(lt_vbfa)
      FOR ALL ENTRIES IN @lt_datos_ventas
     WHERE vbfa~vbelv = @lt_datos_ventas-vbeln
       AND vbfa~posnv = @lt_datos_ventas-posnr
       AND vbfa~posnn < '900000'
       AND vbtyp_n    IN ('J','T')
       AND vbtyp_v    = @lt_datos_ventas-vbtyp
       AND stufe      = '00'.

    SORT lt_vbfa BY vbelv posnv vbeln posnn.
* DTM

    LOOP AT lt_datos_ventas INTO DATA(ls_venta).
      DATA l_ebelp TYPE  ebelp.

      l_ebelp = ls_venta-posnr.

      CALL METHOD zclpp_capacidad_asignada=>get_orden_fabric_pedido
        EXPORTING
          i_pedido      = ls_venta-vbeln
          i_posicion    = l_ebelp
          i_reparto     = ls_venta-etenr
        IMPORTING
          o_of_asignada = DATA(l_of).
      IF l_of IS NOT INITIAL.
        CONTINUE.
      ENDIF.

      CLEAR l_spebe.
      IF ls_venta-lifsp IS NOT INITIAL.

        SELECT SINGLE spebe
          INTO @l_spebe
          FROM tvls
          WHERE lifsp = @ls_venta-lifsp.

      ENDIF.

      CHECK l_spebe <> 'X'.

* DTM - Cantidad pendiente
      CLEAR lv_tot_entregas.
      READ TABLE lt_vbfa ASSIGNING FIELD-SYMBOL(<lfs_vbfa>) WITH KEY vbelv = ls_venta-vbeln
                                                                     posnv = ls_venta-posnr BINARY SEARCH.

      IF sy-subrc EQ 0.
        IF <lfs_vbfa>-kcmeng IS NOT INITIAL.
          lv_tot_entregas = lv_tot_entregas + <lfs_vbfa>-kcmeng.
        ELSEIF <lfs_vbfa>-lfimg IS NOT INITIAL.
          lv_tot_entregas = lv_tot_entregas + <lfs_vbfa>-lfimg.
        ENDIF.
      ENDIF.

* Si la cantidad entregada >= cantidad pedido, eliminar linea.
      IF lv_tot_entregas >= ls_venta-kwmeng.
        CLEAR ls_venta.
        CONTINUE.
**      ELSE.
**        ls_venta-bmeng = lv_tot_entregas.
      ENDIF.
* DTM


      APPEND INITIAL LINE TO ot_pedidos_venta_no_asig ASSIGNING FIELD-SYMBOL(<fs_pedido_no_asig>).

      <fs_pedido_no_asig>-werks     =  i_werks.

* 09/12/2021 - CMS - Obtener destinatario de mercancias como cliente dentro de la aplicación.
      SELECT SINGLE kunnr
        INTO @DATA(l_kunwe)
        FROM vbpa
        WHERE vbeln = @ls_venta-vbeln AND
              parvw = 'WE'.
      IF sy-subrc <> 0.
        <fs_pedido_no_asig>-kunnr     =  ls_venta-kunnr.
      ELSE.
        <fs_pedido_no_asig>-kunnr = l_kunwe.
      ENDIF.
*      <fs_pedido_no_asig>-kunnr     =  ls_venta-kunnr.


      <fs_pedido_no_asig>-desc_cli  =  get_descripcion_cliente( <fs_pedido_no_asig>-kunnr ).
      <fs_pedido_no_asig>-vbeln             =  ls_venta-vbeln.
      <fs_pedido_no_asig>-posnr             =  ls_venta-posnr.
      <fs_pedido_no_asig>-etenr             =  ls_venta-etenr.
      <fs_pedido_no_asig>-tp_pedido         =  'V'.
      <fs_pedido_no_asig>-matnr             =  ls_venta-matnr.
      <fs_pedido_no_asig>-maktx             =  zclpp_recetas_reparto_pulper=>obtener_descr_material( <fs_pedido_no_asig>-matnr ).
      <fs_pedido_no_asig>-matkl             =  ls_venta-matkl.

      CALL METHOD zclpp_capacidad_asignada=>get_ancho_gramaje_material
        EXPORTING
          i_matnr   = <fs_pedido_no_asig>-matnr
        IMPORTING
          o_ancho   = <fs_pedido_no_asig>-ancho
          o_gramaje = <fs_pedido_no_asig>-gramaje.

      <fs_pedido_no_asig>-fecha_ent         =  ls_venta-edatu.
      <fs_pedido_no_asig>-menge             =  ls_venta-wmeng.
      <fs_pedido_no_asig>-meins             =  ls_venta-vrkme.
      <fs_pedido_no_asig>-meins_pend        =  ls_venta-vrkme.

      IF <fs_pedido_no_asig>-menge > lv_tot_entregas. "ls_venta-bmeng. DTM
        <fs_pedido_no_asig>-cant_pend = <fs_pedido_no_asig>-menge - lv_tot_entregas. "ls_venta-bmeng. DTM
      ELSE.
        <fs_pedido_no_asig>-cant_pend = 0.
      ENDIF.

    ENDLOOP.


  ENDMETHOD.
  METHOD instanciar_semana.
    DATA: l_cap_any    TYPE datum,
          l_12_31      TYPE char4 VALUE '1231',
          l_aaaass     TYPE kweek,
          l_semana_max TYPE numc2.

    o_rc = '00'.

*  Validaciones previas a crear la instancia.
    CALL METHOD zclpp_capacidad_asignada=>validar_semana
      EXPORTING
        i_werks  = i_werks
        i_mjahr  = i_mjahr
        i_semana = i_semana
      IMPORTING
        o_rc     = o_rc.

    CHECK o_rc = '00'.

    or_semana = NEW zclpp_capacidad_asignada( i_werks  = i_werks
                                              i_mjahr  = i_mjahr
                                              i_semana = i_semana ).

    CALL METHOD or_semana->get_pedidos_asignados( I_SW_NO_REP_ASIG_CON_OF ).

    IF i_sw_no_act_sin_asig <> 'X'.


      CALL METHOD or_semana->get_pedidos_sin_asignar( ).


    ENDIF.



  ENDMETHOD.
  METHOD refrescar_datos_ped_sin_asig.


    CLEAR: me->gt_pedidos_sin_asig[].

    CALL METHOD get_pedidos_sin_asignar( ).

  ENDMETHOD.
  METHOD validar_semana.

    DATA: l_cap_any    TYPE datum,
          l_12_31      TYPE char4 VALUE '1231',
          l_aaaass     TYPE kweek,
          l_semana_max TYPE numc2.

    o_rc = '00'.

* Validar planta/centro
    CASE i_werks.
      WHEN '3000' OR '3020'.

      WHEN OTHERS.
        o_rc = '01'. " Centro incorrecto
        EXIT.
    ENDCASE.

***Validacion de planta
*  SELECT bwkey FROM t001w INTO TABLE lt_bwkey WHERE werks IN s_werks.
*  IF NOT lt_bwkey[] IS INITIAL.
*    SELECT bukrs INTO TABLE lt_bukrs FROM t001k
*      FOR ALL ENTRIES IN lt_bwkey WHERE bwkey = lt_bwkey-bwkey.

* Obtener la última semana del año, para validar que la semana es OK.
    l_cap_any = |{ i_mjahr }{ l_12_31 }|.

    CALL FUNCTION 'DATE_GET_WEEK'
      EXPORTING
        date         = l_cap_any
      IMPORTING
        week         = l_aaaass
      EXCEPTIONS
        date_invalid = 1
        OTHERS       = 2.
    IF sy-subrc <> 0.
      o_rc = '02'. " Año incorrcto
      EXIT.
    ELSE.

* Determinar la última semana válida del año recibido por parámetro.
      IF i_mjahr = l_aaaass(4).
        l_semana_max = l_aaaass+4(2).
      ELSE.
        l_semana_max = 53.
      ENDIF.

      IF l_semana_max < i_semana.
        o_rc = '03'. " Semana incorrecta
        EXIT.
      ENDIF.
    ENDIF.



  ENDMETHOD.
