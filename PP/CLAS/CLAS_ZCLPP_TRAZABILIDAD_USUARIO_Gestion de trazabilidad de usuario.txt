
class ZCLPP_TRAZABILIDAD_USUARIO definition
  public
  final
  create public .

public section.

  constants TP_OP_BOBINADOR type ZTP_OPERARIO value 'BOBINADOR' ##NO_TEXT.
  constants TP_OP_AYUDANPOPE type ZTP_OPERARIO value 'AYUDANPOPE' ##NO_TEXT.
  constants TP_OP_CONTRAMAES type ZTP_OPERARIO value 'CONTRAMAES' ##NO_TEXT.
  constants TP_OP_CARRETILLERO type ZTP_OPERARIO value 'CARRET MP' ##NO_TEXT.
  constants TP_OP_BASCULA_VEHICULOS type ZTP_OPERARIO value 'RECEPCION' ##NO_TEXT.

  class-methods OBTENER_DATOS_TURNO
    exporting
      !O_TURNO type ZZTURNO
      !O_DESC_TURNO type CHAR20 .
  class-methods OBTENER_OPERARIOS_PUESTO_TRAB
    importing
      !I_WERKS type WERKS_D
      !I_TP_OPERARIO type ZTP_OPERARIO
      !I_ARBPL type ZARBPL optional
    exporting
      !OT_OPERARIOS type ZTNOMBRE_OPER .
  class-methods VALIDAR_TURNO_ACTUAL
    importing
      !I_TURNO type ZZTURNO
    returning
      value(R_TURNO_OK) type FLAG .
  PROTECTED SECTION.
  PRIVATE SECTION.
endclass. "ZCLPP_TRAZABILIDAD_USUARIO definition
class ZCLPP_TRAZABILIDAD_USUARIO implementation.
  METHOD obtener_datos_turno.
    DATA: l_hora_desde TYPE char8,
          l_hora_hasta TYPE char8,
          l_uzeit      LIKE sy-uzeit.

    CALL FUNCTION 'DB_LOCAL_TIME'
      IMPORTING
        time = l_uzeit.

    IF l_uzeit BETWEEN '060000' AND '135959'.
      o_turno = 'T1'.
    ELSEIF l_uzeit BETWEEN '140000' AND '215959'.
      o_turno = 'T2'.
    ELSEIF ( l_uzeit BETWEEN '220000' AND '235959' ) OR ( l_uzeit BETWEEN '000000' AND '055959' ).
      o_turno = 'T3'.
    ENDIF.

* Obtener descriptivo del turno
    SELECT SINGLE turno , hora_desde , hora_hasta FROM zpp_turno
      INTO @DATA(wa)
      WHERE turno = @o_turno.
    IF sy-subrc = 0.
      WRITE wa-hora_desde TO l_hora_desde USING EDIT MASK '__:__:__'.
      WRITE wa-hora_hasta TO l_hora_hasta USING EDIT MASK '__:__:__'.
      CONCATENATE l_hora_desde l_hora_hasta INTO o_desc_turno
          SEPARATED BY '-'.

    ENDIF.
*    o_desc_turno = |{ wa-hora_desde }-{ wa-hora_hasta }|.

  ENDMETHOD.
  METHOD obtener_operarios_puesto_trab.

    CLEAR: ot_operarios.

    IF i_arbpl IS SUPPLIED.

      IF i_arbpl = 'MAQ%'.
* Selección utilizada para Notificación de Rollo(POPE) se desea obtenr tanto Contramaestres, como ayudantes de Pope.
        SELECT nombre
              FROM ztpp0034
              INTO TABLE @ot_operarios
              WHERE werks       =    @i_werks       AND
                    tp_operario =    @i_tp_operario AND
                    arbpl       LIKE @i_arbpl       AND
                    lvorm       = ' '.
      ELSE.
        SELECT nombre
              FROM ztpp0034
              INTO TABLE @ot_operarios
              WHERE werks       = @i_werks       AND
                    tp_operario = @i_tp_operario AND
                    arbpl       = @i_arbpl       AND
                    lvorm       = ' '.
      ENDIF.
    ELSE.

      SELECT nombre
        FROM ztpp0034
        INTO TABLE @ot_operarios
        WHERE werks       = @i_werks       AND
              tp_operario = @i_tp_operario AND
              lvorm       = ' '.

    ENDIF.

  ENDMETHOD.
  METHOD validar_turno_actual.

    CLEAR r_turno_ok.

    CALL METHOD zclpp_trazabilidad_usuario=>obtener_datos_turno
      IMPORTING
        o_turno = DATA(l_turno)
*       o_desc_turno =
      .

* Si el turno actual no se corresponde con el turno de la notificación, mostramos ventana error.
    IF l_turno <> i_turno.
      CALL FUNCTION 'ZPP_PANTALLA_AVISO'
        EXPORTING
          titulo   = 'Error'
          tp_icono = 'E'
          text1    = 'ATENCION CAMBIO DE TURNO. '
          text2    = 'Informar el nuevo operario del turno.'.
      r_turno_ok = 'E'.
    ELSE.
      r_turno_ok = 'X'.
    ENDIF.

  ENDMETHOD.
