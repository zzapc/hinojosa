
class ZCL_ZUI5_WM_CONSUMOS_DPC_IMP definition
  public
  inheriting from ZCL_ZUI5_WM_CONSUMOS_DPC
  final
  create public .

public section.

  constants CG_OBJECT_LOG type BAL_S_LOG-OBJECT value 'CONSUMO_WM_UI5' ##NO_TEXT.

  methods MAQUINAS_GETLIST
    importing
      value(VP_WERKS) type ZUI5_WM_S_MAQUINAS-WERKS
      value(VP_GRUPO) type ZUI5_WM_S_GRUPOS_MAQUINAS-GRUPO
    returning
      value(TP_MAQUINAS) type ZUI5_WM_T_MAQUINAS
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods MAQUINAS_GETDETAIL
    importing
      value(VP_WERKS) type ZUI5_WM_S_MAQUINAS-WERKS
      value(VP_LGPLA) type ZUI5_WM_S_MAQUINAS-LGPLA
    returning
      value(WP_MAQUINA) type ZUI5_WM_S_MAQUINAS
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods GRUPOS_MAQUINAS_GETDETAIL
    importing
      value(VP_GRUPO) type ZUI5_WM_S_GRUPOS_MAQUINAS-GRUPO
      value(VP_WERKS) type ZUI5_WM_S_GRUPOS_MAQUINAS-WERKS
    returning
      value(WP_GRUPO_MAQUINAS) type ZUI5_WM_S_GRUPOS_MAQUINAS
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods GET_CENTROS_LOGO
    importing
      !VP_WERKS type WERKS_D
    returning
      value(VP_LOGO) type XSTRING
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods ESTACIONES_GETLIST
    importing
      value(VP_WERKS) type ZUI5_WM_S_ESTACIONES-WERKS
      value(VP_LGPLA_MAQUINA) type ZUI5_WM_S_ESTACIONES-LGPLA_MAQUINA
    returning
      value(TP_ESTACIONES) type ZUI5_WM_T_ESTACIONES
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods EMPLEADOS_GETDETAIL
    importing
      value(VP_OPERARIO) type ZUI5_WM_S_EMPLEADOS-OPERARIO
      value(VP_WERKS) type ZUI5_WM_S_EMPLEADOS-WERKS
    returning
      value(WP_EMPLEADO) type ZUI5_WM_S_EMPLEADOS
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods ACTUALIZAR_RESB_CONSUMOS_PP
    importing
      value(VP_AUFNR) type AUFNR optional
      value(VP_FECHA) type DATS optional
      value(VP_WERKS) type WERKS_D
      value(VP_MATNR) type MATNR optional
      value(VP_LGPLA) type LGPLA optional
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods ALTA_UA_PARTE_REBOB
    importing
      value(VP_COD_PARTE_REBOB) type ZWM_NT_UI5_S_PARTE_REBOB_CAB-COD_PARTE_REBOB
      value(VP_ID_MAQUINA) type ZWM_NT_UI5_S_PARTE_REBOB_CAB-ID_MAQUINA
      value(VP_ID_PEDIDO) type ZUI5_WM_S_PEDIDOS_CONSUMO-ID
      value(VP_WERKS) type ZWM_NT_UI5_S_PARTE_REBOB_CAB-WERKS
    returning
      value(WP_RETURN) type BAPIRET2
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods CONSTRUCTOR .
  methods VALIDAR_REGULARIZAR_UA
    importing
      value(VP_WERKS) type WERKS_D
      value(VP_LGPLA) type LGPLA
      value(VP_UA) type LTAP-VLENR
    returning
      value(WP_BOBINA) type ZUI5_WM_S_BOBINAS
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods VALIDAR_LECTURA_BOBINA_PREIMP
    importing
      value(VP_WERKS) type WERKS_D
      value(VP_LGPLA) type LGPLA
      value(VP_ID_BOBINA) type ZUI5_WM_S_BOBINAS-ID
    returning
      value(WP_RETURN) type BAPIRET2
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods VALIDAR_LECTURA_CONSUMO_TRAZA
    importing
      value(VP_WERKS) type WERKS_D
      value(VP_LGPLA) type LGPLA
      value(VP_ID_BOBINA) type ZUI5_WM_S_BOBINAS-ID
    returning
      value(WP_BOBINA) type ZUI5_WM_S_BOBINAS
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods CONSUMIR_BOBINA_CON_TRAZA
    importing
      value(VP_WERKS) type WERKS_D
      value(VP_LGPLA) type LGPLA
      value(VP_ID_BOBINA) type ZUI5_WM_S_BOBINAS-ID
      value(VP_BENUM) type LTBK-BENUM
      value(VP_PEDIDO1) type ZWM_LTBK_ADIT-UPPER_ORDERID
      value(VP_PEDIDO2) type ZWM_LTBK_ADIT-LOWER_ORDERID
      value(VP_CONSUMO) type INT4
    returning
      value(WP_RETURN) type BAPIRET2
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods VALIDAR_LECTURA_BOBINA
    importing
      value(VP_WERKS) type WERKS_D
      value(VP_LGPLA) type LGPLA
      value(VP_ID_PEDIDO) type ZUI5_WM_S_PEDIDOS_CONSUMO-ID
      value(VP_ID_BOBINA) type ZUI5_WM_S_BOBINAS-ID
    returning
      value(TP_RETURN) type BAPIRET2_T
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods CONSUMIR_BOBINA_PREIMP
    importing
      value(VP_WERKS) type WERKS_D
      value(VP_LGPLA) type LGPLA
      value(VP_EBELN) type EBELN
      value(VP_ID_BOBINA) type ZUI5_WM_S_BOBINAS-ID
    returning
      value(WP_RETURN) type BAPIRET2
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods VALIDAR_CONSUMO_BOBINA_POST
    importing
      value(VP_WERKS) type WERKS_D
      value(VP_LGPLA) type LGPLA
      value(VP_ID_PEDIDO) type ZUI5_WM_S_PEDIDOS_CONSUMO-ID
      value(VP_ID_BOBINA) type LTAP-VLENR
      value(VP_CANTIDAD) type EKPO-MENGE
    returning
      value(WP_RETURN) type BAPIRET2
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods VALIDAR_CONSUMO_BOBINA
    importing
      value(VP_WERKS) type WERKS_D
      value(VP_LGPLA) type LGPLA
      value(VP_ID_BOBINA) type LTAP-VLENR
      value(VP_ID_PEDIDO) type ZUI5_WM_S_PEDIDOS_CONSUMO-ID
      value(VP_CANTIDAD) type EKPO-MENGE
    returning
      value(WP_RETURN) type BAPIRET2
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods REGULARIZAR_UA
    importing
      value(VP_WERKS) type WERKS_D
      value(VP_LGPLA) type LGPLA
      value(VP_UA) type LTAP-VLENR
      value(VP_MENGE) type EKPO-MENGE
      value(VP_MEINS) type EKPO-MEINS
      value(VP_CLA_APROV) type XFELD default SPACE
    returning
      value(WP_RETURN) type BAPIRET2
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods IMPRIMIR_UA
    importing
      value(VP_LGPLA) type LGPLA
      value(VP_WERKS) type WERKS_D
      value(VP_UA) type LTAP-VLENR
      value(VP_CLA_APROV) type XFELD default SPACE
    returning
      value(WP_RETURN) type BAPIRET2
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods REFRESH_INTERFASE_NEMESIS
    importing
      value(VP_WERKS) type WERKS_D
    returning
      value(WP_RETURN) type BAPIRET2
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods PEDIDOS_CONSUMO_PP_GETLIST
    importing
      value(VP_WERKS) type ZUI5_WM_S_PEDIDOS_CONSUMO-WERKS
      value(VP_LGPLA) type ZUI5_WM_S_PEDIDOS_CONSUMO-LGPLA
    returning
      value(TP_PEDIDOS) type ZUI5_WM_T_PEDIDOS_CONSUMO
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods PEDIDOS_CONSUMO_GETLIST
    importing
      value(VP_WERKS) type ZUI5_WM_S_PEDIDOS_CONSUMO-WERKS
      value(VP_LGPLA) type ZUI5_WM_S_PEDIDOS_CONSUMO-LGPLA
    returning
      value(TP_PEDIDOS) type ZUI5_WM_T_PEDIDOS_CONSUMO
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods PEDIDOS_CONSUMO_GETDETAIL
    importing
      value(VP_WERKS) type ZUI5_WM_S_PEDIDOS_CONSUMO-WERKS
      value(VP_LGPLA) type ZUI5_WM_S_PEDIDOS_CONSUMO-LGPLA
      value(VP_ID) type ZUI5_WM_S_PEDIDOS_CONSUMO-ID
    returning
      value(WP_PEDIDO) type ZUI5_WM_S_PEDIDOS_CONSUMO
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods GET_RANGO_TIPOS_MAT_PALET
    returning
      value(TP_RANGO) type MD_RANGE_T_MTART .
  methods GET_DATOS_CONEXION
    importing
      value(VP_WERKS) type WERKS_D
    returning
      value(WP_DATOS_CONEXION) type ZUI5_WM_S_DATOS_CONEXION
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods ESTACIONES_GETDETAIL
    importing
      value(VP_WERKS) type ZUI5_WM_S_ESTACIONES-WERKS
      value(VP_LGPLA) type ZUI5_WM_S_ESTACIONES-LGPLA
    returning
      value(WP_ESTACION) type ZUI5_WM_S_ESTACIONES
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods CONSUMIR_BOBINA
    importing
      value(VP_WERKS) type WERKS_D
      value(VP_LGPLA) type LGPLA
      value(VP_ID_PEDIDO) type ZUI5_WM_S_PEDIDOS_CONSUMO-ID
      value(VP_TBNUM) type ZUI5_WM_S_PEDIDOS_CONSUMO-TBNUM
      value(VP_ID_BOBINA) type ZUI5_WM_S_BOBINAS-ID
      value(VP_CANTIDAD) type ZUI5_WM_S_BOBINAS-DIAMETRO
    returning
      value(WP_BOBINA) type ZUI5_WM_S_BOBINAS
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods CONSUMIR_BOBINA_PP
    importing
      value(VP_WERKS) type WERKS_D
      value(VP_LGPLA) type LGPLA
      value(VP_ID_PEDIDO) type ZUI5_WM_S_PEDIDOS_CONSUMO-ID
      value(VP_ID_BOBINA) type ZUI5_WM_S_BOBINAS-ID
      value(VP_STOCK_BOBINA) type ZUI5_WM_S_BOBINAS-DIAMETRO optional
      value(VP_CANTIDAD) type ZUI5_WM_S_BOBINAS-DIAMETRO
      value(VP_TEST) type XFELD default SPACE
      value(VP_INACTIVOS) type XFELD default SPACE
      value(VP_MANUAL) type XFELD default SPACE
      !TP_LISTA_APROV type ZWM_NT_UI5_APROV_LIST_TAB optional
    exporting
      !WP_BOBINA type ZUI5_WM_S_BOBINAS
      !WP_RETURN type BAPIRET2
      !TP_LINK_CONF_GOODSMOV type BAPI_LINK_CONF_GOODSMOV_TT
      !TP_GOODSMOVEMENTS type BAPI2017_GM_ITEM_CREATE_T
      !TP_TIMETICKETS type BAPI_PP_TIMETICKET_TT
      !TP_CONSUMOS_PP type ZWM_T_CONSUMOS_PP
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods CENTROS_GETDETAIL
    importing
      value(VP_WERKS) type ZUI5_WM_S_CENTROS-WERKS
    returning
      value(WP_CENTRO) type ZUI5_WM_S_CENTROS
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods BOBINAS_GETLIST
    importing
      value(VP_WERKS) type ZUI5_WM_S_ESTACIONES-WERKS
      value(VP_LGPLA) type ZUI5_WM_S_ESTACIONES-LGPLA
    returning
      value(TP_BOBINAS) type ZUI5_WM_T_BOBINAS
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods BOBINAS_GETDETAIL
    importing
      value(VP_ID) type ZUI5_WM_S_BOBINAS-ID
      value(VP_ID_PEDPROG) type ZUI5_WM_S_BOBINAS-ID_PEDPROG
    returning
      value(WP_BOBINA) type ZUI5_WM_S_BOBINAS
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods BOBINAS_DELETE
    importing
      !WP_BOBINA type ZUI5_WM_S_BOBINAS
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods BOBINAS_CREATE
    changing
      value(WP_BOBINA) type ZUI5_WM_S_BOBINAS
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods ASIGNAR_BOBINA
    importing
      value(VP_WERKS) type WERKS_D
      value(VP_LGPLA) type LGPLA
      value(VP_ID_PEDIDO) type ZUI5_WM_S_PEDIDOS_CONSUMO-ID
      value(VP_ID_BOBINA) type ZUI5_WM_S_BOBINAS-ID
      value(VP_POSICION) type ZWM_ECM_BOBINE-POSICION optional
    returning
      value(WP_BOBINA) type ZWM_ECM_BOBINE
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods ALTA_UA
    importing
      value(VP_WERKS) type WERKS_D
      value(VP_LGPLA) type LGPLA
      value(VP_ID_PEDIDO) type ZUI5_WM_S_PEDIDOS_CONSUMO-ID
      value(VP_CANTIDAD) type ZUI5_WM_S_BOBINAS-DIAMETRO
      value(VP_MEINS) type MEINS default 'ST'
      value(VP_MATNR) type MATNR optional
    returning
      value(WP_BOBINA_PROD) type ZUI5_WM_S_BOBINAS
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods MOVER_BOBINA
    importing
      value(VP_WERKS) type WERKS_D
      value(VP_LGPLA) type LGPLA
      value(VP_BOBINA) type LENUM
    returning
      value(WP_LTAK) type LTAK
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .

  methods /IWBEP/IF_MGW_APPL_SRV_RUNTIME~EXECUTE_ACTION
    redefinition .
  methods /IWBEP/IF_MGW_APPL_SRV_RUNTIME~GET_ENTITY
    redefinition .
  methods /IWBEP/IF_MGW_APPL_SRV_RUNTIME~GET_STREAM
    redefinition .
  methods /IWBEP/IF_MGW_CORE_SRV_RUNTIME~CREATE_ENTITY
    redefinition .
  methods /IWBEP/IF_MGW_CORE_SRV_RUNTIME~DELETE_ENTITY
    redefinition .
  methods /IWBEP/IF_MGW_CORE_SRV_RUNTIME~EXEC_SERVICE_OPERATION
    redefinition .
  methods /IWBEP/IF_MGW_CORE_SRV_RUNTIME~READ_ENTITY
    redefinition .
  methods /IWBEP/IF_MGW_CORE_SRV_RUNTIME~READ_ENTITYSET
    redefinition .
  methods /IWBEP/IF_MGW_CORE_SRV_RUNTIME~UPDATE_ENTITY
    redefinition .
protected section.

  methods CREAR_OT_CONSUMO_PP
    changing
      !WP_BOBINA type ZWM_ECM_BOBINE
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods CREAR_OT_CONSUMO
    changing
      !WP_BOBINA type ZWM_ECM_BOBINE
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods VALIDAR_DIAMETRO_BOBINA
    importing
      value(VP_WERKS) type WERKS_D
      value(VP_LGPLA) type LGPLA
      value(VP_ID_BOBINA) type LTAP-VLENR
      value(VP_CANTIDAD) type EKPO-MENGE
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods GET_BWLVS_OT
    importing
      !WP_BOBINA type ZWM_ECM_BOBINE
    returning
      value(VP_BWLVS) type LTAK-BWLVS .

  methods BOBINAS_CREATE_ENTITY
    redefinition .
  methods BOBINAS_DELETE_ENTITY
    redefinition .
  methods BOBINAS_GET_ENTITY
    redefinition .
  methods BOBINAS_GET_ENTITYSET
    redefinition .
  methods CENTROS_GET_ENTITY
    redefinition .
  methods EMPLEADOS_GET_ENTITY
    redefinition .
  methods ESTACIONES_GET_ENTITY
    redefinition .
  methods ESTACIONES_GET_ENTITYSET
    redefinition .
  methods MAQUINAS_GET_ENTITY
    redefinition .
  methods MAQUINAS_GET_ENTITYSET
    redefinition .
  methods PARTESREBOBBOBPR_CREATE_ENTITY
    redefinition .
  methods PARTESREBOBBOBPR_DELETE_ENTITY
    redefinition .
  methods PARTESREBOBBOBPR_GET_ENTITY
    redefinition .
  methods PARTESREBOBBOBPR_GET_ENTITYSET
    redefinition .
  methods PARTESREBOBBOBPR_UPDATE_ENTITY
    redefinition .
  methods PARTESREBOBCAB_CREATE_ENTITY
    redefinition .
  methods PARTESREBOBCAB_DELETE_ENTITY
    redefinition .
  methods PARTESREBOBCAB_GET_ENTITY
    redefinition .
  methods PARTESREBOBCAB_GET_ENTITYSET
    redefinition .
  methods PARTESREBOBCAB_UPDATE_ENTITY
    redefinition .
  methods PEDIDOSCONSUMO_GET_ENTITY
    redefinition .
  methods PEDIDOSCONSUMO_GET_ENTITYSET
    redefinition .
  methods GRUPOSMAQUINAS_GET_ENTITY
    redefinition .
private section.

  data RG_WM_NT type ref to ZCL_ZUI5_WM_NT_DPC_EXT .
  data VG_OPERARIO type ZLNUM_OPER .

  methods ACT_TABLA_INTERFASE_BHS
    importing
      !WP_INTF_BHS type ZWM_INTF_BHS
      !VP_TEST type XFELD default 'X'
      !RP_LOG type ref to ZCL_AP_LOG optional
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods TRATAR_KEY_ENTITY
    importing
      !IO_TECH_REQUEST_CONTEXT type ref to /IWBEP/IF_MGW_REQ_ENTITY
    exporting
      !WP_ENTITY type DATA
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods TRATAR_KEY_ENTITYSET
    importing
      !IO_TECH_REQUEST_CONTEXT type ref to /IWBEP/IF_MGW_REQ_ENTITYSET
    exporting
      !WP_ENTITY type DATA
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
endclass. "ZCL_ZUI5_WM_CONSUMOS_DPC_IMP definition
class ZCL_ZUI5_WM_CONSUMOS_DPC_IMP implementation.
  METHOD /iwbep/if_mgw_appl_srv_runtime~execute_action.
    CASE io_tech_request_context->get_function_import_name( ).
      WHEN 'getDatosConexion'.
        DATA: BEGIN OF  wl_get_datos_conexion_params,
                werks TYPE werks_d,
              END OF wl_get_datos_conexion_params.

        io_tech_request_context->get_converted_parameters( IMPORTING es_parameter_values = wl_get_datos_conexion_params ).
        copy_data_to_ref( EXPORTING is_data = get_datos_conexion( wl_get_datos_conexion_params-werks )
                          CHANGING  cr_data = er_data ).

      WHEN 'validarLecturaBobina'.
        DATA: BEGIN OF  wl_validar_lec_bobina_params,
                werks     TYPE werks_d,
                lgpla     TYPE lgpla,
                id_pedido TYPE zui5_wm_s_pedidos_consumo-id,
                id_bobina TYPE zui5_wm_s_bobinas-id,
              END OF wl_validar_lec_bobina_params.

        io_tech_request_context->get_converted_parameters( IMPORTING es_parameter_values = wl_validar_lec_bobina_params ).
        copy_data_to_ref( EXPORTING is_data = validar_lectura_bobina( vp_werks = wl_validar_lec_bobina_params-werks
                                                                      vp_lgpla = wl_validar_lec_bobina_params-lgpla
                                                                      vp_id_pedido = wl_validar_lec_bobina_params-id_pedido
                                                                      vp_id_bobina = wl_validar_lec_bobina_params-id_bobina )
                          CHANGING  cr_data = er_data ).

      WHEN 'consumirBobina'.
        DATA: BEGIN OF  wl_asignar_bobina_params,
                werks     TYPE werks_d,
                lgpla     TYPE lgpla,
                id_pedido TYPE zui5_wm_s_pedidos_consumo-id,
                tbnum     TYPE ltbk-tbnum,
                id_bobina TYPE zui5_wm_s_bobinas-id,
                cantidad  TYPE zui5_wm_s_bobinas-diametro,
              END OF wl_asignar_bobina_params.

        io_tech_request_context->get_converted_parameters( IMPORTING es_parameter_values = wl_asignar_bobina_params ).
        copy_data_to_ref( EXPORTING is_data = consumir_bobina( vp_werks = wl_asignar_bobina_params-werks
                                                               vp_lgpla = wl_asignar_bobina_params-lgpla
                                                               vp_id_pedido = wl_asignar_bobina_params-id_pedido
                                                               vp_tbnum     = wl_asignar_bobina_params-tbnum
                                                               vp_id_bobina = wl_asignar_bobina_params-id_bobina
                                                               vp_cantidad  = wl_asignar_bobina_params-cantidad )
                          CHANGING  cr_data = er_data ).


      WHEN 'validarConsumoBobina'.
        DATA: BEGIN OF wl_params_validar_consumo_pre,
                werks     TYPE werks_d,
                lgpla     TYPE lgpla,
                id_bobina TYPE ltap-vlenr,
                id_pedido TYPE zui5_wm_s_pedidos_consumo-id,
                cantidad  TYPE ekpo-menge,
              END OF wl_params_validar_consumo_pre.
        io_tech_request_context->get_converted_parameters( IMPORTING es_parameter_values = wl_params_validar_consumo_pre ).
*
        CALL FUNCTION 'CONVERSION_EXIT_LENUM_INPUT'
          EXPORTING
            input           = wl_params_validar_consumo_pre-id_bobina
          IMPORTING
            output          = wl_params_validar_consumo_pre-id_bobina
          EXCEPTIONS
            check_failed    = 1
            not_numeric     = 2
            t344_get_failed = 3
            wrong_length    = 4
            OTHERS          = 5.
        copy_data_to_ref( EXPORTING is_data = validar_consumo_bobina(  vp_werks     = wl_params_validar_consumo_pre-werks
                                                                       vp_lgpla     = wl_params_validar_consumo_pre-lgpla
                                                                       vp_cantidad  = wl_params_validar_consumo_pre-cantidad
                                                                       vp_id_bobina = wl_params_validar_consumo_pre-id_bobina
                                                                       vp_id_pedido = wl_params_validar_consumo_pre-id_pedido )
                          CHANGING  cr_data = er_data ).




      WHEN 'validarConsumoBobinaPost'.
        DATA: BEGIN OF wl_params_validar_consumo,
                werks     TYPE werks_d,
                lgpla     TYPE lgpla,
                id_bobina TYPE ltap-vlenr,
                id_pedido TYPE zui5_wm_s_pedidos_consumo-id,
                cantidad  TYPE ekpo-menge,
              END OF wl_params_validar_consumo.
        io_tech_request_context->get_converted_parameters( IMPORTING es_parameter_values = wl_params_validar_consumo ).
*
        CALL FUNCTION 'CONVERSION_EXIT_LENUM_INPUT'
          EXPORTING
            input           = wl_params_validar_consumo-id_bobina
          IMPORTING
            output          = wl_params_validar_consumo-id_bobina
          EXCEPTIONS
            check_failed    = 1
            not_numeric     = 2
            t344_get_failed = 3
            wrong_length    = 4
            OTHERS          = 5.

        copy_data_to_ref( EXPORTING is_data = validar_consumo_bobina_post( vp_werks     = wl_params_validar_consumo-werks
                                                                           vp_lgpla     = wl_params_validar_consumo-lgpla
                                                                           vp_cantidad  = wl_params_validar_consumo-cantidad
                                                                           vp_id_bobina = wl_params_validar_consumo-id_bobina
                                                                           vp_id_pedido = wl_params_validar_consumo-id_pedido )
                          CHANGING  cr_data = er_data ).




      WHEN 'validarRegularizarUA'.
        DATA: BEGIN OF wl_params_validar_regul_ua,
                ua    TYPE ltap-vlenr,
                werks TYPE werks_d,
                lgpla TYPE lgpla,
              END OF wl_params_validar_regul_ua.
        io_tech_request_context->get_converted_parameters( IMPORTING es_parameter_values = wl_params_validar_regul_ua ).
        CALL FUNCTION 'CONVERSION_EXIT_LENUM_INPUT'
          EXPORTING
            input           = wl_params_validar_regul_ua-ua
          IMPORTING
            output          = wl_params_validar_regul_ua-ua
          EXCEPTIONS
            check_failed    = 1
            not_numeric     = 2
            t344_get_failed = 3
            wrong_length    = 4
            OTHERS          = 5.



        copy_data_to_ref( EXPORTING is_data = validar_regularizar_ua( vp_werks = wl_params_validar_regul_ua-werks
                                                                      vp_lgpla = wl_params_validar_regul_ua-lgpla
                                                                      vp_ua = wl_params_validar_regul_ua-ua )
                          CHANGING  cr_data = er_data ).


      WHEN 'regularizarUA'.
        DATA: BEGIN OF wl_params_regularizar_ua,
                werks TYPE werks_d,
                lgpla TYPE lgpla,
                ua    TYPE ltap-vlenr,
                menge TYPE ekpo-menge,
                meins TYPE ekpo-meins,
              END OF wl_params_regularizar_ua.
        io_tech_request_context->get_converted_parameters( IMPORTING es_parameter_values = wl_params_regularizar_ua ).

        CALL FUNCTION 'CONVERSION_EXIT_LENUM_INPUT'
          EXPORTING
            input           = wl_params_regularizar_ua-ua
          IMPORTING
            output          = wl_params_regularizar_ua-ua
          EXCEPTIONS
            check_failed    = 1
            not_numeric     = 2
            t344_get_failed = 3
            wrong_length    = 4
            OTHERS          = 5.

        copy_data_to_ref( EXPORTING is_data = regularizar_ua( vp_werks  = wl_params_regularizar_ua-werks
                                                              vp_lgpla  = wl_params_regularizar_ua-lgpla
                                                              vp_ua     = wl_params_regularizar_ua-ua
                                                              vp_menge  = wl_params_regularizar_ua-menge
                                                              vp_meins  = wl_params_regularizar_ua-meins )
                          CHANGING  cr_data = er_data ).



      WHEN 'imprimirUA'.
        DATA: BEGIN OF wl_params_imprimir_ua,
                werks TYPE werks_d,
                lgpla TYPE lgpla,
                ua    TYPE ltap-vlenr,
              END OF wl_params_imprimir_ua.
        io_tech_request_context->get_converted_parameters( IMPORTING es_parameter_values = wl_params_imprimir_ua ).

        CALL FUNCTION 'CONVERSION_EXIT_LENUM_INPUT'
          EXPORTING
            input           = wl_params_imprimir_ua-ua
          IMPORTING
            output          = wl_params_imprimir_ua-ua
          EXCEPTIONS
            check_failed    = 1
            not_numeric     = 2
            t344_get_failed = 3
            wrong_length    = 4
            OTHERS          = 5.

        copy_data_to_ref( EXPORTING is_data = imprimir_ua( vp_werks  = wl_params_imprimir_ua-werks
                                                           vp_lgpla  = wl_params_imprimir_ua-lgpla
                                                           vp_ua     = wl_params_imprimir_ua-ua )
                          CHANGING  cr_data = er_data ).




      WHEN 'refreshInterfaseNemesis'.
        DATA: BEGIN OF  wl_refresh_if_nemesis_params,
                werks TYPE werks_d,
              END OF wl_refresh_if_nemesis_params.
        io_tech_request_context->get_converted_parameters( IMPORTING es_parameter_values = wl_refresh_if_nemesis_params ).
        copy_data_to_ref( EXPORTING is_data = refresh_interfase_nemesis( wl_refresh_if_nemesis_params-werks )
                          CHANGING  cr_data = er_data ).


      WHEN 'altaUA'.
        DATA wl_return TYPE bapiret2.
        DATA: BEGIN OF  wl_alta_ua_params,
                werks     TYPE werks_d,
                lgpla     TYPE lgpla,
                id_pedido TYPE zui5_wm_s_pedidos_consumo-id,
                cantidad  TYPE zui5_wm_s_bobinas-diametro,
              END OF wl_alta_ua_params.

        io_tech_request_context->get_converted_parameters( IMPORTING es_parameter_values = wl_alta_ua_params ).
        alta_ua(  vp_werks      = wl_alta_ua_params-werks
                  vp_lgpla      = wl_alta_ua_params-lgpla
                  vp_id_pedido  = wl_alta_ua_params-id_pedido
                  vp_cantidad   = wl_alta_ua_params-cantidad ).

        copy_data_to_ref( EXPORTING is_data = wl_return
                          CHANGING  cr_data = er_data ).

      WHEN 'altaUAParteRebob'.
        DATA: BEGIN OF  wl_alta_ua_parte_rebob,
                werks           TYPE werks_d,
                id_maquina      TYPE lgpla,
                cod_parte_rebob TYPE zwm_cod_parte_rebob,
                id_pedido       TYPE zui5_wm_s_pedidos_consumo-id,
              END OF wl_alta_ua_parte_rebob.

        io_tech_request_context->get_converted_parameters( IMPORTING es_parameter_values = wl_alta_ua_parte_rebob ).
        copy_data_to_ref( EXPORTING is_data = alta_ua_parte_rebob(  vp_werks            = wl_alta_ua_parte_rebob-werks
                                                                    vp_id_maquina       = wl_alta_ua_parte_rebob-id_maquina
                                                                    vp_cod_parte_rebob  = wl_alta_ua_parte_rebob-cod_parte_rebob
                                                                    vp_id_pedido        = wl_alta_ua_parte_rebob-id_pedido )
                          CHANGING  cr_data = er_data ).


      WHEN 'validarLecturaBobinaPreimp'.
        DATA: BEGIN OF  wl_validar_lec_bobina_preimp,
                werks     TYPE werks_d,
                lgpla     TYPE lgpla,
                id_bobina TYPE zui5_wm_s_bobinas-id,
              END OF wl_validar_lec_bobina_preimp.

        io_tech_request_context->get_converted_parameters( IMPORTING es_parameter_values = wl_validar_lec_bobina_preimp ).
        copy_data_to_ref( EXPORTING is_data = validar_lectura_bobina_preimp(  vp_werks = wl_validar_lec_bobina_preimp-werks
                                                                              vp_lgpla = wl_validar_lec_bobina_preimp-lgpla
                                                                              vp_id_bobina = wl_validar_lec_bobina_preimp-id_bobina )
                          CHANGING  cr_data = er_data ).


      WHEN 'consumirBobinaPreimp'.
        DATA: BEGIN OF  wl_consumir_bobina_preimp,
                werks     TYPE werks_d,
                lgpla     TYPE lgpla,
                id_bobina TYPE zui5_wm_s_bobinas-id,
                ebeln     TYPE ebeln,
              END OF wl_consumir_bobina_preimp.

        io_tech_request_context->get_converted_parameters( IMPORTING es_parameter_values = wl_consumir_bobina_preimp ).
        copy_data_to_ref( EXPORTING is_data = consumir_bobina_preimp(  vp_werks = wl_consumir_bobina_preimp-werks
                                                                       vp_lgpla = wl_consumir_bobina_preimp-lgpla
                                                                       vp_ebeln = wl_consumir_bobina_preimp-ebeln
                                                                       vp_id_bobina = wl_consumir_bobina_preimp-id_bobina )
                          CHANGING  cr_data = er_data ).



      WHEN 'parteRebobCerrarParte'.
        DATA(rl_wm_nt) = NEW zcl_zui5_wm_nt_dpc_ext( ).
        DATA: BEGIN OF wl_params_parterebob_gen_nec,
                werks           TYPE zwm_nt_ui5_s_parte_rebob_cab-werks,
                id_maquina      TYPE zwm_nt_ui5_s_parte_rebob_cab-id_maquina,
                cod_parte_rebob TYPE zwm_nt_ui5_s_parte_rebob_cab-cod_parte_rebob,
              END OF wl_params_parterebob_gen_nec.
        io_tech_request_context->get_converted_parameters( IMPORTING es_parameter_values = wl_params_parterebob_gen_nec ).

        copy_data_to_ref( EXPORTING is_data = rl_wm_nt->parte_rebob_cerrar_parte( vp_werks           = wl_params_parterebob_gen_nec-werks
                                                                       vp_id_maquina      = wl_params_parterebob_gen_nec-id_maquina
                                                                       vp_cod_parte_rebob = wl_params_parterebob_gen_nec-cod_parte_rebob )
                          CHANGING  cr_data = er_data ).

      WHEN 'parteRebobReabrirParte'.
        io_tech_request_context->get_converted_parameters( IMPORTING es_parameter_values = wl_params_parterebob_gen_nec ).

        copy_data_to_ref( EXPORTING is_data = rl_wm_nt->parte_rebob_reabrir_parte( vp_werks           = wl_params_parterebob_gen_nec-werks
                                                                       vp_id_maquina      = wl_params_parterebob_gen_nec-id_maquina
                                                                       vp_cod_parte_rebob = wl_params_parterebob_gen_nec-cod_parte_rebob )
                          CHANGING  cr_data = er_data ).




      WHEN 'validarLecturaBobinaConsumoTraza'.
        DATA: BEGIN OF  wl_validar_lec_consumo_traza,
                werks     TYPE werks_d,
                lgpla     TYPE lgpla,
                id_bobina TYPE zui5_wm_s_bobinas-id,
              END OF wl_validar_lec_consumo_traza.

        io_tech_request_context->get_converted_parameters( IMPORTING es_parameter_values = wl_validar_lec_consumo_traza ).
        copy_data_to_ref( EXPORTING is_data = validar_lectura_consumo_traza(  vp_werks      = wl_validar_lec_consumo_traza-werks
                                                                              vp_lgpla      = wl_validar_lec_consumo_traza-lgpla
                                                                              vp_id_bobina  = wl_validar_lec_consumo_traza-id_bobina )
                          CHANGING  cr_data = er_data ).



      WHEN 'consumirBobinaConTraza'.
        DATA: BEGIN OF  wl_consumir_bobina_traza,
                werks     TYPE werks_d,
                lgpla     TYPE lgpla,
                id_bobina TYPE zui5_wm_s_bobinas-id,
                benum     TYPE ltbk-benum,
                pedido1   TYPE zwm_ltbk_adit-upper_orderid,
                pedido2   TYPE zwm_ltbk_adit-lower_orderid,
                consumo   TYPE int4,
              END OF wl_consumir_bobina_traza.

        io_tech_request_context->get_converted_parameters( IMPORTING es_parameter_values = wl_consumir_bobina_traza ).
        copy_data_to_ref( EXPORTING is_data = consumir_bobina_con_traza(  vp_werks      = wl_consumir_bobina_traza-werks
                                                                          vp_lgpla      = wl_consumir_bobina_traza-lgpla
                                                                          vp_id_bobina  = wl_consumir_bobina_traza-id_bobina
                                                                          vp_benum      = wl_consumir_bobina_traza-benum
                                                                          vp_pedido1    = wl_consumir_bobina_traza-pedido1
                                                                          vp_pedido2    = wl_consumir_bobina_traza-pedido2
                                                                          vp_consumo    = wl_consumir_bobina_traza-consumo
                                                                          )
                          CHANGING  cr_data = er_data ).




      WHEN OTHERS.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDCASE.
  ENDMETHOD.
  METHOD /iwbep/if_mgw_appl_srv_runtime~get_entity.



  ENDMETHOD.
  METHOD /iwbep/if_mgw_appl_srv_runtime~get_stream.
    DATA: wl_stream  TYPE ty_s_media_resource.
    DATA: wl_header  TYPE ihttpnvp.


    CASE io_tech_request_context->get_entity_type_name( ).
      WHEN 'CentrosLogo'.
        DATA: wl_centros_logo TYPE zui5_wm_s_centros_logo.
        io_tech_request_context->get_converted_keys( IMPORTING es_key_values = wl_centros_logo ).
        IF wl_centros_logo IS INITIAL.
          io_tech_request_context->get_converted_source_keys( IMPORTING es_key_values = wl_centros_logo ).
        ENDIF.


        wl_stream-value = get_centros_logo( wl_centros_logo-werks ).
        wl_stream-mime_type = 'image/jpeg'.

        wl_header-name  = 'content-disposition'.
        wl_header-value = |inline; filename=logo.jpg|.

    ENDCASE.

    copy_data_to_ref( EXPORTING is_data = wl_stream
                      CHANGING  cr_data = er_stream ).


*    IF rg_servicio IS BOUND AND wl_header IS NOT INITIAL.
*      rg_servicio->/iwbep/if_mgw_conv_srv_runtime~set_header( wl_header ).
*    ENDIF.

  ENDMETHOD.
  METHOD /iwbep/if_mgw_core_srv_runtime~create_entity.
    READ TABLE is_request_details-technical_request-request_header ASSIGNING FIELD-SYMBOL(<fs_header>) WITH KEY name = 'operario'.
    IF sy-subrc = 0.
      vg_operario = <fs_header>-value.
    ENDIF.


    SET PARAMETER ID 'ZNUM_OPER' FIELD vg_operario.

  ENDMETHOD.
  METHOD /iwbep/if_mgw_core_srv_runtime~delete_entity.
    READ TABLE is_request_details-technical_request-request_header ASSIGNING FIELD-SYMBOL(<fs_header>) WITH KEY name = 'operario'.
    IF sy-subrc = 0.
      vg_operario = <fs_header>-value.
    ENDIF.

    SET PARAMETER ID 'ZNUM_OPER' FIELD vg_operario.

  ENDMETHOD.
  METHOD /iwbep/if_mgw_core_srv_runtime~exec_service_operation.
    READ TABLE is_request_details-technical_request-request_header ASSIGNING FIELD-SYMBOL(<fs_header>) WITH KEY name = 'operario'.
    IF sy-subrc = 0.
      vg_operario = <fs_header>-value.
    ENDIF.

    SET PARAMETER ID 'ZNUM_OPER' FIELD vg_operario.

  ENDMETHOD.
  METHOD /iwbep/if_mgw_core_srv_runtime~read_entity.
    READ TABLE is_request_details-technical_request-request_header ASSIGNING FIELD-SYMBOL(<fs_header>) WITH KEY name = 'operario'.
    IF sy-subrc = 0.
      vg_operario = <fs_header>-value.
    ENDIF.

    SET PARAMETER ID 'ZNUM_OPER' FIELD vg_operario.
  ENDMETHOD.
  METHOD /iwbep/if_mgw_core_srv_runtime~read_entityset.
    READ TABLE is_request_details-technical_request-request_header ASSIGNING FIELD-SYMBOL(<fs_header>) WITH KEY name = 'operario'.
    IF sy-subrc = 0.
      vg_operario = <fs_header>-value.
    ENDIF.

    SET PARAMETER ID 'ZNUM_OPER' FIELD vg_operario.

  ENDMETHOD.
  METHOD /iwbep/if_mgw_core_srv_runtime~update_entity.
    READ TABLE is_request_details-technical_request-request_header ASSIGNING FIELD-SYMBOL(<fs_header>) WITH KEY name = 'operario'.
    IF sy-subrc = 0.
      vg_operario = <fs_header>-value.
    ENDIF.

    SET PARAMETER ID 'ZNUM_OPER' FIELD vg_operario.

  ENDMETHOD.
  METHOD act_tabla_interfase_bhs.

    IF vp_test = space.
      IF wp_intf_bhs-message IS NOT INITIAL.
        CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
      ENDIF.

      IF wp_intf_bhs-id IS NOT INITIAL.
        MODIFY zwm_intf_bhs FROM wp_intf_bhs.
        CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
          EXPORTING
            wait = 'X'.
      ENDIF.
    ENDIF.

    IF wp_intf_bhs-message IS NOT INITIAL.
      IF rp_log IS BOUND.
        rp_log->log( msgty = 'E' message = wp_intf_bhs-message ).
      ENDIF.

      zcl_seis_odata_utils=>lanzar_excepcion( wp_intf_bhs-message ).
    ENDIF.

  ENDMETHOD.
  METHOD actualizar_resb_consumos_pp.

    DATA(wl_centro) = centros_getdetail( vp_werks ).

    IF wl_centro-lista_apro_ui5_pp = space.
      zcl_seis_odata_utils=>lanzar_excepcion( 'El centro no está configurado con consumos por PP' ).
    ENDIF.

    IF vp_aufnr IS INITIAL AND vp_fecha IS INITIAL.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Debe indicar fecha u orden' ).
    ENDIF.

    IF vp_aufnr IS NOT INITIAL.
      DATA(tl_aufnr_rg) = VALUE /iwbep/t_cod_select_options( ( sign = 'I' option = 'EQ' low = vp_aufnr ) ).
    ENDIF.

    IF vp_matnr IS NOT INITIAL.
      DATA(tl_matnr_rg) = VALUE /iwbep/t_cod_select_options( ( sign = 'I' option = 'EQ' low = vp_matnr ) ).
    ENDIF.
    IF vp_lgpla IS NOT INITIAL.
      DATA(tl_lgpla_rg) = VALUE /iwbep/t_cod_select_options( ( sign = 'I' option = 'EQ' low = vp_lgpla ) ).
    ENDIF.

    "Si se indica fecha, busco órdenes con algún movimiento en esa fecha
    IF vp_fecha IS NOT INITIAL.
      SELECT DISTINCT 'I' AS sign, 'EQ' AS option, aufnr AS low
        APPENDING CORRESPONDING FIELDS OF TABLE @tl_aufnr_rg
        FROM mseg INNER JOIN mkpf ON mkpf~mblnr = mseg~mblnr AND
                                     mkpf~mjahr = mseg~mjahr
        WHERE mseg~werks =  @vp_werks       AND
              mkpf~bldat =  @vp_fecha       AND
              mseg~matnr IN @tl_matnr_rg    AND
              mseg~bwart IN ('261', '262')  AND
              mseg~aufnr <> ''.
    ENDIF.

    IF tl_aufnr_rg IS INITIAL.
      zcl_seis_odata_utils=>lanzar_excepcion( 'No se han seleccionado órdenes' ).
    ENDIF.

    DATA: tl_resb         TYPE TABLE OF resb,
          wl_resb         LIKE LINE OF tl_resb,
          tl_tokens_sgtxt TYPE TABLE OF text10.
    SELECT mseg~werks, mseg~mblnr, mseg~mjahr, mseg~shkzg, mseg~aufnr, mseg~matnr,
           mseg~menge, mseg~meins, mseg~dmbtr, mseg~sgtxt
      INTO TABLE @DATA(tl_mseg)
      FROM mseg
      WHERE mseg~werks =  @vp_werks     AND
            mseg~matnr IN @tl_matnr_rg  AND
            mseg~aufnr IN @tl_aufnr_rg  AND
            mseg~bwart IN ('261', '262').
    LOOP AT tl_mseg ASSIGNING FIELD-SYMBOL(<fs_mseg>).
      CLEAR: wl_resb, tl_tokens_sgtxt.
      "SGTXT: CDE/100/OND1
      "???/LGTYP/LGPLA
      SPLIT <fs_mseg>-sgtxt AT '/' INTO TABLE tl_tokens_sgtxt.
      CHECK lines( tl_tokens_sgtxt ) >= 3.

      wl_resb-aufnr = <fs_mseg>-aufnr.
*      wl_resb-matnr = <fs_mseg>-matnr.
      wl_resb-sortf = tl_tokens_sgtxt[ 3 ].
      CHECK wl_resb-sortf IN tl_lgpla_rg.

      wl_resb-enmng = <fs_mseg>-menge.
      wl_resb-meins = <fs_mseg>-meins.
      wl_resb-enwrt = <fs_mseg>-dmbtr.
      IF <fs_mseg>-shkzg = 'S'.
        MULTIPLY: wl_resb-enmng BY -1,
                  wl_resb-enwrt BY -1.
      ENDIF.

      COLLECT wl_resb  INTO tl_resb.
    ENDLOOP.

    "Busco la reserva a actualizar
    CHECK tl_resb IS NOT INITIAL.

    SELECT *
      INTO TABLE @DATA(tl_resb_bd)
      FROM resb
      FOR ALL ENTRIES IN @tl_resb
      WHERE aufnr = @tl_resb-aufnr AND
*            matnr = @tl_resb-matnr AND
            sortf = @tl_resb-sortf AND
            xloek = @space.

    LOOP AT tl_resb ASSIGNING FIELD-SYMBOL(<fs_resb>).
      READ TABLE tl_resb_bd ASSIGNING FIELD-SYMBOL(<fs_resb_bd>) WITH KEY aufnr = <fs_resb>-aufnr
*                                                                          matnr = <fs_resb>-matnr
                                                                          sortf = <fs_resb>-sortf.
      CHECK sy-subrc = 0.
      IF <fs_resb>-meins <> <fs_resb_bd>-meins.
        CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
          EXPORTING
            i_matnr              = <fs_resb_bd>-matnr
            i_in_me              = <fs_resb>-meins
            i_out_me             = <fs_resb_bd>-meins
            i_menge              = <fs_resb>-enmng
          IMPORTING
            e_menge              = <fs_resb>-enmng
          EXCEPTIONS
            error_in_application = 1
            error                = 2
            OTHERS               = 3.
        IF sy-subrc <> 0.
          zcl_seis_odata_utils=>lanzar_excepcion( ).
        ENDIF.

        <fs_resb>-meins = <fs_resb_bd>-meins.
      ENDIF.

      UPDATE resb
        SET enmng = <fs_resb>-enmng
            enwrt = <fs_resb>-enwrt
        WHERE rsnum = <fs_resb_bd>-rsnum AND
              rspos = <fs_resb_bd>-rspos AND
              rsart = <fs_resb_bd>-rsart.
    ENDLOOP.

  ENDMETHOD.
  METHOD alta_ua.



    " cvivo - Cambios fase 2 UI5 CB - 58678
    DATA: valor        TYPE p DECIMALS 2,
          return       TYPE TABLE OF bapiret2,
          items        TYPE TABLE OF bapi2017_gm_item_create,
          portabobinas TYPE zwm_intf_bhs-porta_bobinas,
          orden        TYPE zwm_intf_bhs-orden_bhs,
          t_consumo    TYPE TABLE OF zwm_intf_bhs,
          cantidad     TYPE mseg-menge,
          texto        TYPE bapi_msg,
          var          TYPE char10,
          mblnr        TYPE mseg-mblnr,
          mjahr        TYPE mseg-mjahr,
          t_trite      TYPE l03b_trite_t,
          trite        LIKE LINE OF t_trite,
          tanum        TYPE ltak-tanum,
          t_ltak       TYPE TABLE OF ltak_vb,
          t_ltap       TYPE TABLE OF ltap_vb,
          vp_kgs       LIKE vp_cantidad.

    SELECT SINGLE lgnum FROM t320 " 11.03.2019 - cvivo - WM CB
    INTO @DATA(lgnum)
    WHERE werks EQ @vp_werks.

    SELECT SINGLE benum FROM ltbk " primero recuperamos el número de necesidad
      WHERE rsnum EQ @vp_id_pedido
        AND lgnum EQ @lgnum
        AND betyp EQ 'O'
      INTO @DATA(benum).

    orden = benum.

    MOVE: vp_lgpla TO portabobinas.

    " recuperamos los consumos pendientes de asignar hasta 50 anteriores, lo hacemos descendentemente para evitar saltos
    " es decir, una vez encontramos deddscendentemente un consumo asignado, paramos
    SELECT * FROM zwm_intf_bhs
      WHERE werks EQ @vp_werks
        AND porta_bobinas EQ @portabobinas
      ORDER BY id DESCENDING " lo hacemos descendente por seguridad, para evitar chapuzas
        INTO @DATA(consumo)
      UP TO 50 ROWS.

      IF consumo-ua_producida IS INITIAL AND consumo-orden_bhs EQ orden.
        APPEND consumo TO t_consumo.
      ELSEIF consumo-ua_producida IS NOT INITIAL.
        EXIT. " cuando encontramos una asignación paramos
      ENDIF.
    ENDSELECT.

    IF t_consumo IS INITIAL.
      zcl_seis_odata_utils=>lanzar_excepcion( 'No se han encontrado consumos para la asignación del alta, registre primero los consumos' ).
      RETURN.
    ENDIF.

    " ahora identificamos el material de alta, para el cual se puede sustituir el grupo de artículos y gramaje
    " si finalmente se han consumido otras bobinas diferentes a las planificadas
    IF vp_matnr IS INITIAL.
      SELECT SINGLE matnr_alta
        FROM zwm_ltbk_adit " el material de alta se pinta sobre la tabla adicional
        INTO @DATA(matnr)
        WHERE benum EQ @benum AND
              lgnum EQ @lgnum.
    ELSE.
      matnr = vp_matnr.
    ENDIF.

    IF matnr IS INITIAL.
      zcl_seis_odata_utils=>lanzar_excepcion( 'No se ha encontrado material para el alta' ).
      RETURN.
    ENDIF.

    " verificamos si ha habido cambio, tomamos la última bobina como referencia
    SORT t_consumo BY id DESCENDING.
    LOOP AT t_consumo INTO consumo WHERE matnr IS NOT INITIAL.
      EXIT.
    ENDLOOP.

    consumo-matnr = |{ consumo-matnr ALPHA = OUT }|.
    matnr = |{ matnr ALPHA = OUT }|.
    matnr = COND #( WHEN matnr(9) EQ consumo-matnr(9) THEN |{ matnr ALPHA = IN }| ELSE |{ consumo-matnr(9) }{ matnr+9 }| ).
    matnr = |{ matnr ALPHA = IN }|.

    " como hemos cambiado el material, puede que no esté dado de alta, verificamos
    zcl_si_prod_replenishment_ui5=>check_material_exists( EXPORTING
                                                            iv_matnr = matnr
                                                            iv_werks = vp_werks
                                                          EXCEPTIONS
                                                            error_creacion = 1
                                                            material_vacio = 5 ).

    " transformamos a KG, que es como gestionamos los movimientos IM
    CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
      EXPORTING
        i_matnr              = matnr
        i_in_me              = vp_meins
        i_out_me             = 'KG'
        i_menge              = vp_cantidad
      IMPORTING
        e_menge              = vp_kgs
      EXCEPTIONS
        error_in_application = 1
        error                = 2
        OTHERS               = 3.


    " verificamos que las cantidades alta vs bajas concuerdan
    SELECT * FROM mseg
      FOR ALL ENTRIES IN @t_consumo
      WHERE mblnr EQ @t_consumo-mblnr
        AND mjahr EQ @t_consumo-mjahr
        AND werks EQ @t_consumo-werks
        AND matnr EQ @t_consumo-matnr
        AND charg EQ @t_consumo-charg
        AND meins EQ 'KG' " se asume que los consumos vienen en KG
        AND bwart EQ '291'
      INTO @DATA(mseg).

      ADD mseg-menge TO cantidad. " se asume que siempre se hacen los 291 con KG
      ADD mseg-dmbtr TO valor.
    ENDSELECT.

    IF cantidad < ( ( vp_kgs * 90 ) / 100 ). " si kgs consumo menor a kgs alta +10%, no se permite
      CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
        EXPORTING
          i_matnr              = matnr
          i_in_me              = 'KG'
          i_out_me             = vp_meins
          i_menge              = cantidad
        IMPORTING
          e_menge              = cantidad
        EXCEPTIONS
          error_in_application = 1
          error                = 2
          OTHERS               = 3.

      WRITE cantidad TO var DECIMALS 0.
      texto = |Los consumos asignados permiten dar de alta hasta { var } unidades|.
      zcl_seis_odata_utils=>lanzar_excepcion( texto ).
      RETURN.
    ENDIF.

    " con los consumos asignados obtenemos el importe del alta
    DATA(precio_tn) = valor / ( cantidad / 1000 ).
    valor = ( vp_kgs * precio_tn / 1000 ).

    DATA(header) = VALUE bapi2017_gm_head_01( pstng_date = sy-datum
                                              header_txt = 'Alta UA desde UI5'
                                              ref_doc_no = orden ).

    SELECT SINGLE valor1, valor2 FROM ztwm001
      WHERE cprog EQ 'INTF_BHS_BOBINA'
        AND param1 EQ 'IMPUTACION_ALTA_UA'
        AND param2 EQ @vp_lgpla
      INTO @DATA(imputacion).


    items = VALUE #(  material = matnr
                      plant = vp_werks
                      stge_loc = '1000'
                      move_type = '961'
                      entry_qnt = vp_kgs
                      entry_uom = 'KG' " movimiento preferentemente en kilos para poder verlo bien en mb51
                      item_text = vp_id_pedido
                      gl_account = CONV #( imputacion-valor1 )
                      orderid = CONV #( imputacion-valor2 )
                      ( amount_lc = valor ) ).

    CALL FUNCTION 'BAPI_GOODSMVT_CREATE'
      EXPORTING
        goodsmvt_header  = header
        goodsmvt_code    = '05' " otras EM
      IMPORTING
        materialdocument = mblnr
        matdocumentyear  = mjahr
      TABLES
        goodsmvt_item    = items
        return           = return.

    IF NOT line_exists( return[ type = 'E' ] ).
      COMMIT WORK AND WAIT.

      WAIT UP TO 1 SECONDS.

      SELECT SINGLE * FROM mseg
        WHERE mblnr EQ @mblnr
          AND mjahr EQ @mjahr
        INTO @DATA(alta).

      IF sy-subrc NE 0.
        zcl_seis_odata_utils=>lanzar_excepcion( 'Se ha detenido el proceso por un problema en la selección en base de datos' ).
        RETURN.
      ELSE.
        SELECT SINGLE * FROM ltbp
          WHERE tbnum EQ @alta-tbnum AND
                lgnum EQ @alta-lgnum
          INTO @DATA(ltbp).

        "JCB: 14.02.22. Añado LGNUM como sufijo al parámetro
        DATA(vl_cprog) = |INTF_BHS_BOBINA_{ lgnum }|.
        SELECT SINGLE valor1 FROM ztwm001
          WHERE cprog EQ  @vl_cprog
            AND param1 EQ 'UBIC_ALTA_UA'
            AND param2 EQ @vp_lgpla
          INTO @DATA(destino).

        IF sy-subrc EQ 0.
          SELECT SINGLE * FROM lagp
            WHERE lgpla EQ @destino
            INTO @DATA(lagp).
        ENDIF.

        t_trite = VALUE #( tbpos = 1 letyp = 'PL' nltyp = lagp-lgtyp anfme = ltbp-menga altme = ltbp-altme
                              ( nlpla = destino ) ).

        CALL FUNCTION 'L_TO_CREATE_TR'
          EXPORTING
            i_lgnum                        = ltbp-lgnum
            i_tbnum                        = alta-tbnum
*           i_bname                        = gst_zwm005-bname
            i_squit                        = abap_true
            i_tbeli                        = abap_true
            it_trite                       = t_trite
          IMPORTING
            e_tanum                        = tanum
          TABLES
            t_ltak                         = t_ltak
            t_ltap_vb                      = t_ltap
          EXCEPTIONS
            foreign_lock                   = 1
            qm_relevant                    = 2
            tr_completed                   = 3
            xfeld_wrong                    = 4
            ldest_wrong                    = 5
            drukz_wrong                    = 6
            tr_wrong                       = 7
            squit_forbidden                = 8
            no_to_created                  = 9
            update_without_commit          = 10
            no_authority                   = 11
            preallocated_stock             = 12
            partial_transfer_req_forbidden = 13
            input_error                    = 14
            error_message                  = 16
            OTHERS                         = 15.

        IF tanum IS NOT INITIAL.
          COMMIT WORK AND WAIT.

          DATA(ltap) = t_ltap[ 1 ].
        ELSE.
          DATA(key) = |Error generación OT alta UA: { sy-datum } / { sy-uzeit } / { sy-uname }|.
          LOG-POINT ID zwm_ui5_vatan SUBKEY key FIELDS sy-msgid sy-msgno sy-msgty sy-msgv1 sy-msgv2 t_trite alta.

*          zcl_seis_odata_utils=>lanzar_excepcion( 'Error al generar la OT para el alta de la unidad de almacén' ).
          zcl_seis_odata_utils=>lanzar_excepcion( ).
        ENDIF.

      ENDIF.

      LOOP AT t_consumo ASSIGNING FIELD-SYMBOL(<consumo>).
        <consumo>-ua_producida = ltap-nlenr.
        <consumo>-matnr_prod = matnr.
      ENDLOOP.

      MODIFY zwm_intf_bhs FROM TABLE t_consumo.
      COMMIT WORK AND WAIT.

    ELSE.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al generar el alta, notifique al responsable' ).
      RETURN.
    ENDIF.

    CHECK ltap-nlenr IS NOT INITIAL.

    "Impresión
    DATA(vl_cod_maq_portabo) = CONV zwm_param-maquina( vp_lgpla ).
    DATA tl_selection_table TYPE TABLE OF rsparamsl_255.
    EXPORT lv_vengo_inter_consumo = 'X'        TO MEMORY ID 'VENGO_CONSUMO' . " cvivo
    EXPORT lv_cod_maq_portabo     = vl_cod_maq_portabo  TO MEMORY ID 'MAQUINA' .

    " create label for remaining
    tl_selection_table = VALUE #( ( selname = 'P_LGNUM' kind = 'P' sign = 'I' option = 'EQ' low = ltbp-lgnum )
    ( selname = 'S_LENUM' kind = 'S' sign = 'I' option = 'EQ' low = ltap-nlenr ) ).
    SUBMIT zimpresion_ua WITH SELECTION-TABLE tl_selection_table AND RETURN.
    FREE MEMORY ID: 'VENGO_CONSUMO', 'MAQUINA'.

    wp_bobina_prod-werks  = vp_werks.
    wp_bobina_prod-id     = ltap-nlenr.
    wp_bobina_prod-matnr  = matnr.
    wp_bobina_prod-lgpla  = destino.
    wp_bobina_prod-meins  = vp_meins.
    wp_bobina_prod-verme  = vp_cantidad.


  ENDMETHOD.
  METHOD alta_ua_parte_rebob.

    DATA(rl_wm_nt) = NEW zcl_zui5_wm_nt_dpc_ext( ).

    DATA(wl_centro)   = zcl_wm_nt_generic=>get_instance( )->centros_getdetail( vp_werks ).
    DATA(wl_maquina)  = zcl_wm_nt_generic=>get_instance( )->maquinas_getdetail( vp_id_maquina ).
    DATA(wl_parte_cab) = rl_wm_nt->parte_rebob_cab_getdetail( vp_werks            = vp_werks
                                                              vp_id_maquina       = vp_id_maquina
                                                              vp_cod_parte_rebob  = vp_cod_parte_rebob ).

    DATA(tl_parte_bob_prod) = rl_wm_nt->parte_rebob_bob_prod_getlist( vp_cod_parte_rebob  = vp_cod_parte_rebob
                                                                      vp_id_maquina       = vp_id_maquina
                                                                      vp_werks            = vp_werks ).

    DATA vl_msg_error TYPE string.

    DELETE tl_parte_bob_prod WHERE cla_alta_ua = 'X'.

    DATA: tl_contador_rg TYPE RANGE OF zwm_parte_reb_bp-contador.
    LOOP AT tl_parte_bob_prod ASSIGNING FIELD-SYMBOL(<fs_parte_bob_prod>).
      APPEND INITIAL LINE TO tl_contador_rg ASSIGNING FIELD-SYMBOL(<fs_contador_rg>).
      <fs_contador_rg>-sign = 'I'. <fs_contador_rg>-option = 'EQ'. <fs_contador_rg>-low = <fs_parte_bob_prod>-contador.
    ENDLOOP.


    LOOP AT tl_parte_bob_prod ASSIGNING <fs_parte_bob_prod>.
      DATA: vp_lgpla       TYPE lgpla,
            vp_cantidad    TYPE zui5_wm_s_bobinas-diametro,
            vp_meins       TYPE meins,
            vp_matnr       TYPE matnr,
            wp_bobina_prod TYPE zui5_wm_s_bobinas.

      vp_lgpla     = <fs_parte_bob_prod>-id_maquina.
      vp_id_pedido = vp_id_pedido.
      vp_matnr     = <fs_parte_bob_prod>-matnr.
      vp_cantidad  = <fs_parte_bob_prod>-menge_prod.
      vp_meins     = <fs_parte_bob_prod>-meins.

      " cvivo - Cambios fase 2 UI5 CB - 58678
      DATA: valor        TYPE p DECIMALS 2,
            return       TYPE TABLE OF bapiret2,
            items        TYPE TABLE OF bapi2017_gm_item_create,
            portabobinas TYPE zwm_intf_bhs-porta_bobinas,
            orden        TYPE zwm_intf_bhs-orden_bhs,
            t_consumo    TYPE TABLE OF zwm_intf_bhs,
            cantidad     TYPE mseg-menge,
            texto        TYPE bapi_msg,
            var          TYPE char10,
            mblnr        TYPE mseg-mblnr,
            mjahr        TYPE mseg-mjahr,
            t_trite      TYPE l03b_trite_t,
            trite        LIKE LINE OF t_trite,
            tanum        TYPE ltak-tanum,
            t_ltak       TYPE TABLE OF ltak_vb,
            t_ltap       TYPE TABLE OF ltap_vb,
            vp_kgs       LIKE vp_cantidad.

      CLEAR: valor, return, items, portabobinas, orden, t_consumo, cantidad,
             texto, var, mblnr, mjahr, t_trite, trite, tanum, t_ltak, t_ltap, vp_kgs.

      SELECT SINGLE lgnum FROM t320 " 11.03.2019 - cvivo - WM CB
      INTO @DATA(lgnum)
      WHERE werks EQ @vp_werks.

*      SELECT SINGLE benum FROM ltbk " primero recuperamos el número de necesidad
*        WHERE rsnum EQ @vp_id_pedido
*          AND lgnum EQ @lgnum
*          AND betyp EQ 'O'
*        INTO @DATA(benum).
      DATA benum TYPE ltbk-benum.
      orden = benum = <fs_parte_bob_prod>-cod_parte_rebob && <fs_parte_bob_prod>-contador.

      MOVE: vp_lgpla TO portabobinas.

      " recuperamos los consumos pendientes de asignar hasta 50 anteriores, lo hacemos descendentemente para evitar saltos
      " es decir, una vez encontramos deddscendentemente un consumo asignado, paramos
      SELECT * FROM zwm_intf_bhs
        WHERE werks EQ @vp_werks
          AND porta_bobinas EQ @portabobinas
        ORDER BY id DESCENDING " lo hacemos descendente por seguridad, para evitar chapuzas
          INTO @DATA(consumo)
        UP TO 50 ROWS.

*        IF consumo-ua_producida IS INITIAL AND consumo-orden_bhs EQ orden.
        IF ( consumo-cod_parte_rebob IS INITIAL OR consumo-cod_parte_rebob = vp_cod_parte_rebob ) AND consumo-orden_bhs = orden.
          APPEND consumo TO t_consumo.
        ELSEIF consumo-ua_producida IS NOT INITIAL.
          EXIT. " cuando encontramos una asignación paramos
        ENDIF.
      ENDSELECT.

      IF t_consumo IS INITIAL.
        vl_msg_error = 'No se han encontrado consumos para la asignación del alta, registre primero los consumos'.
        EXIT.
      ENDIF.

      " ahora identificamos el material de alta, para el cual se puede sustituir el grupo de artículos y gramaje
      " si finalmente se han consumido otras bobinas diferentes a las planificadas
      IF vp_matnr IS INITIAL.
        SELECT SINGLE matnr_alta
          FROM zwm_ltbk_adit " el material de alta se pinta sobre la tabla adicional
          INTO @DATA(matnr)
          WHERE benum EQ @benum AND
                lgnum EQ @lgnum.
      ELSE.
        matnr = vp_matnr.
      ENDIF.

      IF matnr IS INITIAL.
        vl_msg_error = 'No se ha encontrado material para el alta'.
        EXIT.
      ENDIF.

      " verificamos si ha habido cambio, tomamos la última bobina como referencia
      SORT t_consumo BY id DESCENDING.
      LOOP AT t_consumo INTO consumo WHERE matnr IS NOT INITIAL.
        EXIT.
      ENDLOOP.

      consumo-matnr = |{ consumo-matnr ALPHA = OUT }|.
      matnr = |{ matnr ALPHA = OUT }|.
      matnr = COND #( WHEN matnr(9) EQ consumo-matnr(9) THEN |{ matnr ALPHA = IN }| ELSE |{ consumo-matnr(9) }{ matnr+9 }| ).
      matnr = |{ matnr ALPHA = IN }|.

      " como hemos cambiado el material, puede que no esté dado de alta, verificamos
      zcl_si_prod_replenishment_ui5=>check_material_exists( EXPORTING
                                                              iv_matnr = matnr
                                                              iv_werks = vp_werks
                                                            EXCEPTIONS
                                                              error_creacion = 1
                                                              material_vacio = 5 ).

      " transformamos a KG, que es como gestionamos los movimientos IM
      CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
        EXPORTING
          i_matnr              = matnr
          i_in_me              = vp_meins
          i_out_me             = 'KG'
          i_menge              = vp_cantidad
        IMPORTING
          e_menge              = vp_kgs
        EXCEPTIONS
          error_in_application = 1
          error                = 2
          OTHERS               = 3.


      " verificamos que las cantidades alta vs bajas concuerdan
      SELECT * FROM mseg
        FOR ALL ENTRIES IN @t_consumo
        WHERE mblnr EQ @t_consumo-mblnr
          AND mjahr EQ @t_consumo-mjahr
          AND werks EQ @t_consumo-werks
          AND matnr EQ @t_consumo-matnr
          AND charg EQ @t_consumo-charg
          AND meins EQ 'KG' " se asume que los consumos vienen en KG
          AND bwart EQ '291'
        INTO @DATA(mseg).

        ADD mseg-menge TO cantidad. " se asume que siempre se hacen los 291 con KG
        ADD mseg-dmbtr TO valor.
      ENDSELECT.

      IF cantidad < ( ( vp_kgs * 90 ) / 100 ). " si kgs consumo menor a kgs alta +10%, no se permite
        CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
          EXPORTING
            i_matnr              = matnr
            i_in_me              = 'KG'
            i_out_me             = vp_meins
            i_menge              = cantidad
          IMPORTING
            e_menge              = cantidad
          EXCEPTIONS
            error_in_application = 1
            error                = 2
            OTHERS               = 3.

        DATA vl_meins_out TYPE text10.
        CALL FUNCTION 'CONVERSION_EXIT_CUNIT_OUTPUT'
          EXPORTING
            input          = vp_meins
            language       = sy-langu
          IMPORTING
            output         = vl_meins_out
          EXCEPTIONS
            unit_not_found = 1
            OTHERS         = 2.



        WRITE cantidad TO var DECIMALS 0.
        vl_msg_error = |Los consumos asignados permiten dar de alta hasta { var } { vl_meins_out }|.
        EXIT.
      ENDIF.

      " con los consumos asignados obtenemos el importe del alta
      DATA(precio_tn) = valor / ( cantidad / 1000 ).
      valor = ( vp_kgs * precio_tn / 1000 ).

      DATA(header) = VALUE bapi2017_gm_head_01( pstng_date = sy-datum
                                                header_txt = 'Alta UA desde UI5'
                                                ref_doc_no = orden ).

      SELECT SINGLE valor1, valor2 FROM ztwm001
        WHERE cprog EQ 'INTF_BHS_BOBINA'
          AND param1 EQ 'IMPUTACION_ALTA_UA'
          AND param2 EQ @vp_lgpla
        INTO @DATA(imputacion).


      items = VALUE #(  material = matnr
                        plant = vp_werks
                        stge_loc = '1000'
                        move_type = '961'
                        entry_qnt = vp_kgs
                        entry_uom = 'KG' " movimiento preferentemente en kilos para poder verlo bien en mb51
                        item_text = vp_id_pedido
                        gl_account = CONV #( imputacion-valor1 )
                        orderid = CONV #( imputacion-valor2 )
                        ( amount_lc = valor ) ).

      CALL FUNCTION 'BAPI_GOODSMVT_CREATE'
        EXPORTING
          goodsmvt_header  = header
          goodsmvt_code    = '05' " otras EM
        IMPORTING
          materialdocument = mblnr
          matdocumentyear  = mjahr
        TABLES
          goodsmvt_item    = items
          return           = return.

      IF NOT line_exists( return[ type = 'E' ] ).
        COMMIT WORK AND WAIT.

        WAIT UP TO 1 SECONDS.

        SELECT SINGLE * FROM mseg
          WHERE mblnr EQ @mblnr
            AND mjahr EQ @mjahr
          INTO @DATA(alta).

        IF sy-subrc NE 0.
          vl_msg_error = 'Se ha detenido el proceso por un problema en la selección en base de datos'.
          EXIT.

        ELSE.
          SELECT SINGLE * FROM ltbp
            WHERE tbnum EQ @alta-tbnum AND
                  lgnum EQ @alta-lgnum
            INTO @DATA(ltbp).

          "JCB: 14.02.22. Añado LGNUM como sufijo al parámetro
          DATA(vl_cprog) = |INTF_BHS_BOBINA_{ lgnum }|.
          SELECT SINGLE valor1 FROM ztwm001
            WHERE cprog EQ  @vl_cprog
              AND param1 EQ 'UBIC_ALTA_UA'
              AND param2 EQ @vp_lgpla
            INTO @DATA(destino).

          IF sy-subrc EQ 0.
            SELECT SINGLE * FROM lagp
              WHERE lgpla EQ @destino
              INTO @DATA(lagp).
          ENDIF.

          t_trite = VALUE #( tbpos = 1 letyp = 'BB' nltyp = lagp-lgtyp anfme = ltbp-menga altme = ltbp-altme
                                ( nlpla = destino ) ).

          CALL FUNCTION 'L_TO_CREATE_TR'
            EXPORTING
              i_lgnum                        = ltbp-lgnum
              i_tbnum                        = alta-tbnum
*             i_bname                        = gst_zwm005-bname
              i_squit                        = abap_true
              i_tbeli                        = abap_true
              it_trite                       = t_trite
            IMPORTING
              e_tanum                        = tanum
            TABLES
              t_ltak                         = t_ltak
              t_ltap_vb                      = t_ltap
            EXCEPTIONS
              foreign_lock                   = 1
              qm_relevant                    = 2
              tr_completed                   = 3
              xfeld_wrong                    = 4
              ldest_wrong                    = 5
              drukz_wrong                    = 6
              tr_wrong                       = 7
              squit_forbidden                = 8
              no_to_created                  = 9
              update_without_commit          = 10
              no_authority                   = 11
              preallocated_stock             = 12
              partial_transfer_req_forbidden = 13
              input_error                    = 14
              error_message                  = 16
              OTHERS                         = 15.

          IF tanum IS NOT INITIAL.
            COMMIT WORK AND WAIT.

            DATA(ltap) = t_ltap[ 1 ].
          ELSE.
            DATA(key) = |Error generación OT alta UA: { sy-datum } / { sy-uzeit } / { sy-uname }|.
            LOG-POINT ID zwm_ui5_vatan SUBKEY key FIELDS sy-msgid sy-msgno sy-msgty sy-msgv1 sy-msgv2 t_trite alta.


            zcl_seis_odata_utils=>lanzar_excepcion( ).
            MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
              WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4
              INTO vl_msg_error.
            EXIT.
          ENDIF.

        ENDIF.

*JCB: Se tiene que actualizar al final
*        LOOP AT t_consumo ASSIGNING FIELD-SYMBOL(<consumo>).
*          <consumo>-ua_producida = ltap-nlenr.
*          <consumo>-matnr_prod = matnr.
*        ENDLOOP.
*
*        MODIFY zwm_intf_bhs FROM TABLE t_consumo.
*        COMMIT WORK AND WAIT.

      ELSE.


        vl_msg_error = 'Error al generar el alta, notifique al responsable'.
        EXIT.

      ENDIF.

      CHECK ltap-nlenr IS NOT INITIAL.

      "Impresión
      DATA(vl_cod_maq_portabo) = CONV zwm_param-maquina( vp_lgpla ).
      DATA tl_selection_table TYPE TABLE OF rsparamsl_255.
      EXPORT lv_vengo_inter_consumo = 'X'        TO MEMORY ID 'VENGO_CONSUMO' . " cvivo
      EXPORT lv_cod_maq_portabo     = vl_cod_maq_portabo  TO MEMORY ID 'MAQUINA' .

      " create label for remaining
      tl_selection_table = VALUE #( ( selname = 'P_LGNUM' kind = 'P' sign = 'I' option = 'EQ' low = ltbp-lgnum )
      ( selname = 'S_LENUM' kind = 'S' sign = 'I' option = 'EQ' low = ltap-nlenr ) ).
      SUBMIT zimpresion_ua WITH SELECTION-TABLE tl_selection_table AND RETURN.
      FREE MEMORY ID: 'VENGO_CONSUMO', 'MAQUINA'.

      wp_bobina_prod-werks  = vp_werks.
      wp_bobina_prod-id     = ltap-nlenr.
      wp_bobina_prod-matnr  = matnr.
      wp_bobina_prod-lgpla  = destino.
      wp_bobina_prod-meins  = vp_meins.
      wp_bobina_prod-verme  = vp_cantidad.



      """""""""""""""""
      "FIN ALTA UA
      """""""""""""""""

      <fs_parte_bob_prod>-lgpla       = wp_bobina_prod-lgpla.
      <fs_parte_bob_prod>-lenum       = wp_bobina_prod-id.
      <fs_parte_bob_prod>-cla_alta_ua = 'X'.
      rl_wm_nt->parte_rebob_bob_prod_update( CHANGING wp_parte_rebob_bob_prod = <fs_parte_bob_prod> ).

      DELETE tl_parte_bob_prod.
    ENDLOOP.

    IF vl_msg_error IS NOT INITIAL.
      DELETE tl_parte_bob_prod WHERE cla_nuevo_consumo = space.
      IF tl_parte_bob_prod IS NOT INITIAL.
        DELETE zwm_parte_reb_bp FROM TABLE tl_parte_bob_prod.
        COMMIT WORK AND WAIT.
      ENDIF.
      zcl_seis_odata_utils=>lanzar_excepcion( vl_msg_error ).
    ENDIF.


    "JCB: Se tiene que actualizar al final
    LOOP AT t_consumo ASSIGNING FIELD-SYMBOL(<consumo>).
*      <consumo>-ua_producida = ltap-nlenr.
*      <consumo>-matnr_prod = matnr.
      <consumo>-cod_parte_rebob = vp_cod_parte_rebob.
    ENDLOOP.

    MODIFY zwm_intf_bhs FROM TABLE t_consumo.
    COMMIT WORK AND WAIT.




    """"""""""""""""""""
    "Definir datos y constantes
    """"""""""""""""""""


*    wl_parte_cab-rsnum       = vl_rsnum.
*    wl_parte_cab-cla_tratado = 'X'.
*    rl_wm_nt->parte_rebob_cab_update( CHANGING wp_parte_rebob_cab = wl_parte_cab ).
  ENDMETHOD.
  METHOD asignar_bobina.
    DATA(wl_centro)   = centros_getdetail( vp_werks ).

    DELETE FROM zwm_ecm_bobine
      WHERE bobine      = vp_id_bobina AND
            id_bob_ecm  = vp_id_pedido.
    DELETE FROM zwm_ecm_bobine
       WHERE bobine             = vp_id_bobina AND
             parent_id_bob_ecm  = vp_id_pedido.

    "update control strct
    DATA: wl_zwm_ecm_bobine TYPE zwm_ecm_bobine.
    GET TIME STAMP FIELD DATA(lv_stmp).

    CALL FUNCTION 'CONVERSION_EXIT_LENUM_INPUT'
      EXPORTING
        input           = vp_id_bobina
      IMPORTING
        output          = wl_zwm_ecm_bobine-bobine
      EXCEPTIONS
        check_failed    = 1
        not_numeric     = 2
        t344_get_failed = 3
        wrong_length    = 4
        OTHERS          = 5.
    wl_zwm_ecm_bobine-id_bob_ecm      = vp_id_pedido.
    wl_zwm_ecm_bobine-lgnum           = wl_centro-lgnum.
    wl_zwm_ecm_bobine-created_by      = sy-uname.
    wl_zwm_ecm_bobine-created_at      = lv_stmp.
    wl_zwm_ecm_bobine-created_at_zon  = sy-zonlo.
    wl_zwm_ecm_bobine-changed_by      = sy-uname.
    wl_zwm_ecm_bobine-changed_at      = lv_stmp.
    wl_zwm_ecm_bobine-changed_at_zon  = sy-zonlo.
    wl_zwm_ecm_bobine-posicion        = vp_posicion.
    MODIFY zwm_ecm_bobine FROM wl_zwm_ecm_bobine.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al asignar bobina' ).
    ENDIF.


    "Se mueve la bobina (mov. 918) si el centro no tiene lectura de bobinas propuestas
    IF wl_centro-lectura_bobinas_propuestas = space.
      TRY .
          mover_bobina( vp_werks  = vp_werks
                        vp_lgpla  = vp_lgpla
                        vp_bobina = wl_zwm_ecm_bobine-bobine ).

        CATCH /iwbep/cx_mgw_busi_exception.
          "Aunque falle no lanzamos la excepción
      ENDTRY.
    ENDIF.


    MOVE-CORRESPONDING wl_zwm_ecm_bobine TO wp_bobina.


  ENDMETHOD.
  METHOD bobinas_create.


    DATA(vl_clave_log) = |{ wp_bobina-id }_{ wp_bobina-id_pedprog }_{ sy-datum } { sy-uzeit }|.
    DATA(rl_log) = NEW zcl_ap_log( object = cg_object_log clave = vl_clave_log ).




    DATA(wl_centro)   = centros_getdetail( wp_bobina-werks ).
    SELECT SINGLE benum
      INTO @DATA(vl_benum)
      FROM ltbk
      WHERE lgnum = @wl_centro-lgnum AND
            rsnum = @wp_bobina-id_pedprog.


    rl_log->log( msgty = 'I' message = |Asignando bobina { wp_bobina-id } en { vl_benum }. Centro { wp_bobina-werks }| ).


    DELETE FROM zwm_ecm_bobine
      WHERE bobine      = wp_bobina-id AND
            id_bob_ecm  = wp_bobina-id_pedprog.
    DELETE FROM zwm_ecm_bobine
       WHERE bobine             = wp_bobina-id AND
             parent_id_bob_ecm  = wp_bobina-id_pedprog.

    "update control strct
    DATA: wl_zwm_ecm_bobine TYPE zwm_ecm_bobine.
    GET TIME STAMP FIELD DATA(vl_timestamp).

    CALL FUNCTION 'CONVERSION_EXIT_LENUM_INPUT'
      EXPORTING
        input           = wp_bobina-id
      IMPORTING
        output          = wl_zwm_ecm_bobine-bobine
      EXCEPTIONS
        check_failed    = 1
        not_numeric     = 2
        t344_get_failed = 3
        wrong_length    = 4
        OTHERS          = 5.
    wl_zwm_ecm_bobine-id_bob_ecm      = wp_bobina-id_pedprog.
    wl_zwm_ecm_bobine-lgnum           = wl_centro-lgnum.
    wl_zwm_ecm_bobine-created_by      = sy-uname.
    wl_zwm_ecm_bobine-created_at      = vl_timestamp.
    wl_zwm_ecm_bobine-created_at_zon  = sy-zonlo.
    wl_zwm_ecm_bobine-changed_by      = sy-uname.
    wl_zwm_ecm_bobine-changed_at      = vl_timestamp.
    wl_zwm_ecm_bobine-changed_at_zon  = sy-zonlo.
    wl_zwm_ecm_bobine-posicion        = wp_bobina-posicion.
    MODIFY zwm_ecm_bobine FROM wl_zwm_ecm_bobine.
    IF sy-subrc <> 0.
      DATA(vl_mensaje) = 'Error al asignar bobina'.
      rl_log->log( msgty = 'E' message = vl_mensaje ).
      zcl_seis_odata_utils=>lanzar_excepcion( vl_mensaje ).
    ENDIF.


    "Se mueve la bobina (mov. 918) si el centro no tiene lectura de bobinas propuestas
    IF wl_centro-lectura_bobinas_propuestas = space.
      TRY .
          mover_bobina( vp_werks  = wp_bobina-werks
                        vp_lgpla  = wp_bobina-lgpla
                        vp_bobina = wl_zwm_ecm_bobine-bobine ).

        CATCH /iwbep/cx_mgw_busi_exception.
          "Aunque falle no lanzamos la excepción
      ENDTRY.
    ENDIF.


    rl_log->log( msgty = 'S' message = |Bobina { wp_bobina-id } asignada en { wp_bobina-id_pedprog }.| ).

  ENDMETHOD.
  METHOD bobinas_create_entity.
    io_data_provider->read_entry_data( IMPORTING es_data = er_entity ).
    bobinas_create( CHANGING wp_bobina = er_entity ).
  ENDMETHOD.
  METHOD bobinas_delete.
    DATA vl_mensaje TYPE bapiret2-message.

    DATA(vl_clave_log) = |{ wp_bobina-id }_{ wp_bobina-id_pedprog }_{ sy-datum } { sy-uzeit }|.
    DATA(rl_log) = NEW zcl_ap_log( object = cg_object_log clave = vl_clave_log ).
    rl_log->log( msgty = 'I' message = |Borrando bobina { wp_bobina-id } en { wp_bobina-id_pedprog }. Centro { wp_bobina-werks }| ).


    DATA(wl_centro) = centros_getdetail( wp_bobina-werks ).

    DATA vl_bobine TYPE zwm_ecm_bobine-bobine.
    CALL FUNCTION 'CONVERSION_EXIT_LENUM_INPUT'
      EXPORTING
        input           = wp_bobina-id
      IMPORTING
        output          = vl_bobine
      EXCEPTIONS
        check_failed    = 1
        not_numeric     = 2
        t344_get_failed = 3
        wrong_length    = 4
        OTHERS          = 5.


*    DATA(tl_bobinas) = zcl_wm_zwm_ecm_bobine_dao=>query_by_bobine(  iv_lgnum  = wl_centro-lgnum
*                                                                    iv_bobine = vl_bobine ).

    SELECT *
      INTO TABLE @DATA(tl_ecm_bobine)
      FROM zwm_ecm_bobine
      WHERE bobine    = @vl_bobine        AND
            lgnum     = @wl_centro-lgnum  AND
            tanum     = @space            AND
            is_tbnum  = @space.
    IF sy-subrc <> 0.
      vl_mensaje = 'No se ha encontrado bobina'.
      rl_log->log( msgty = 'E' message = vl_mensaje ).
      zcl_seis_odata_utils=>lanzar_excepcion( vl_mensaje ).
    ENDIF.


    DELETE zwm_ecm_bobine FROM TABLE tl_ecm_bobine.
    IF sy-subrc <> 0.
      vl_mensaje = 'Error al borrar la bobina'.
      rl_log->log( msgty = 'E' message = vl_mensaje ).
      zcl_seis_odata_utils=>lanzar_excepcion( vl_mensaje ).
    ENDIF.


    rl_log->log( msgty = 'S' message = |Bobina { wp_bobina-id } borrada correctamente| ).

  ENDMETHOD.
  METHOD bobinas_delete_entity.
    DATA: wl_entity TYPE zui5_wm_s_bobinas.
    io_tech_request_context->get_converted_keys( IMPORTING es_key_values  = wl_entity ).
    bobinas_delete( wl_entity ).
  ENDMETHOD.
  METHOD bobinas_get_entity.
    tratar_key_entity( EXPORTING io_tech_request_context = io_tech_request_context
                       IMPORTING wp_entity               = er_entity ).
    er_entity = bobinas_getdetail( vp_id          = er_entity-id
                                   vp_id_pedprog  = er_entity-id_pedprog ).

  ENDMETHOD.
  METHOD bobinas_get_entityset.
    DATA wl_entity LIKE LINE OF et_entityset.

    data wl_estacion type zui5_wm_s_estaciones.
    tratar_key_entityset( EXPORTING io_tech_request_context = io_tech_request_context
                          IMPORTING wp_entity               = wl_estacion ).

    et_entityset =  bobinas_getlist( vp_werks = wl_estacion-werks
                                              vp_lgpla = wl_estacion-lgpla ).

    zcl_seis_odata_utils=>tratar_entityset( EXPORTING io_tech_request_context  = io_tech_request_context
                                            CHANGING et_entityset = et_entityset ).
  ENDMETHOD.
  METHOD bobinas_getdetail.

*    CHECK me->gs_data IS INITIAL.
*    SELECT SINGLE * FROM zwm_ecm_bobine
*    INTO CORRESPONDING FIELDS OF me->gs_data
*    WHERE id_bob_ecm = iv_id_bob_ecm
*    AND bobine = iv_bobine.
*    CHECK sy-subrc NE 0.
*    RAISE EXCEPTION TYPE zcx_wm_exception
*      EXPORTING
*        textid = VALUE #( msgid = 'ZUI5' msgno = '002' attr1 = iv_id_bob_ecm attr2 = iv_bobine )
**       previous =
*      .
*
*
*    DATA(lt_lqua) = zcl_wm_nt_generic=>get_bobine_info(
*    EXPORTING
*      iv_lgnum    =  zcl_wm_nt_generic=>get_lgnum(
*                         iv_werks = CONV #( lv_werks )
**                               iv_lgort =
*                     )
*        iv_bobine    = ls_data-bobine
**            iv_matnr    =
**            iv_qty_only = ABAP_TRUE
*  ).
*    IF lines( lt_lqua ) > 0.
*      DATA(ls_lqua) = lt_lqua[ 1 ].
*    ENDIF.
*
*
*    er_entity = VALUE #(
*  id = ls_data-bobine
*matnr = ls_lqua-matnr
*charg = ls_data-bobine
*werks = lv_werks
*diametro = ls_data-diametro
*meins = ls_lqua-meins
*id_pedprog = ls_data-id_bob_ecm
*update_diam = ls_data-saved
*   ).
*  CATCH cx_root.
*ENDTRY.


ENDMETHOD.
  METHOD bobinas_getlist.

    DATA(wl_centro)   = centros_getdetail( vp_werks ).
    DATA(wl_estacion) = estaciones_getdetail( vp_werks = vp_werks
                                              vp_lgpla = vp_lgpla ).


    DATA(tl_pedidos) = pedidos_consumo_getlist( vp_werks = vp_werks
                                                vp_lgpla = vp_lgpla ).
    CHECK tl_pedidos IS NOT INITIAL.
    LOOP AT tl_pedidos INTO DATA(ls_pedprogid) WHERE editable = abap_true.

      SELECT *
        INTO TABLE @DATA(lt_bob)
        FROM zwm_ecm_bobine
        WHERE id_bob_ecm = @ls_pedprogid-id AND
              lgnum      = @wl_centro-lgnum AND
              saved      = @space.

      CHECK lines( lt_bob ) > 0.

      SELECT lqua~lgnum, lqua~lenum, lqua~matnr, lqua~verme, lqua~meins, lqua~werks, lqua~charg
        INTO TABLE @DATA(tl_lqua)
        FROM lqua
        FOR ALL ENTRIES IN @lt_bob
        WHERE lgnum = @lt_bob-lgnum AND
              lenum = @lt_bob-bobine.

      IF tl_lqua IS NOT INITIAL.
        SELECT *
          INTO TABLE @DATA(tl_mara)
          FROM mara
          FOR ALL ENTRIES IN @tl_lqua
          WHERE matnr = @tl_lqua-matnr.
      ENDIF.

      LOOP AT lt_bob INTO DATA(ls_bob).
        IF line_exists( tp_bobinas[ id = ls_bob-bobine ] ).
          CONTINUE.
        ENDIF.
        APPEND CORRESPONDING #( ls_bob MAPPING id = bobine id_pedprog = id_bob_ecm ) TO tp_bobinas ASSIGNING FIELD-SYMBOL(<fs_ret>).

        READ TABLE tl_lqua ASSIGNING FIELD-SYMBOL(<fs_lqua>) WITH KEY lgnum = ls_bob-lgnum
                                                                      lenum = ls_bob-bobine.
        IF sy-subrc = 0.
          <fs_ret>-matnr = <fs_lqua>-matnr.
          <fs_ret>-verme = <fs_lqua>-verme.
          <fs_ret>-meins = <fs_lqua>-meins.
          <fs_ret>-charg = <fs_lqua>-charg.
          READ TABLE tl_mara ASSIGNING FIELD-SYMBOL(<fs_mara>) WITH KEY matnr = <fs_lqua>-matnr.
          IF sy-subrc = 0.
            <fs_ret>-maktx =  zcl_wm_nt_generic=>get_mat_txt_omp( iv_werks = <fs_lqua>-werks
                                                                  is_mara  = <fs_mara> ).
          ENDIF.
        ENDIF.
      ENDLOOP.
    ENDLOOP.

    DATA(lv_idx) = lines( tp_bobinas ).


    IF lv_idx > 2.
      lv_idx = lv_idx - 2.
      DELETE tp_bobinas TO lv_idx.
    ENDIF.

    LOOP AT tp_bobinas ASSIGNING FIELD-SYMBOL(<fs_bobinas>).
      <fs_bobinas>-werks = vp_werks.
      <fs_bobinas>-lgpla = vp_lgpla.


      IF ( wl_estacion-gestiona_palets = 'X' OR wl_centro-consumos_bobinas_metros = 'X' ) AND <fs_bobinas>-matnr IS NOT INITIAL.
        CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
          EXPORTING
            i_matnr              = <fs_bobinas>-matnr
            i_in_me              = <fs_bobinas>-meins
            i_out_me             = wl_estacion-meins
            i_menge              = <fs_bobinas>-verme
          IMPORTING
            e_menge              = <fs_bobinas>-verme
          EXCEPTIONS
            error_in_application = 1
            error                = 2
            OTHERS               = 3.
        IF sy-subrc = 0.
          <fs_bobinas>-meins = wl_estacion-meins.
        ENDIF.

      ELSE.
        zcl_wm_nt_generic=>conv_mat_diametro( EXPORTING
                                                iv_matnr          = <fs_bobinas>-matnr
                                                iv_qty            = <fs_bobinas>-verme
                                                iv_meins          = <fs_bobinas>-meins
                                                iv_dest_meins     = zcl_wm_nt_generic=>gc_meins_dia
                                                iv_werks          = vp_werks
                                              RECEIVING rv_qty = DATA(lv_diametro)
                                              EXCEPTIONS
                                                um_no_valid       = 1
                                                missing_constants = 2
                                                missing_matnr     = 3
                                                no_base_calc      = 4
                                                OTHERS            = 5 ).
        IF sy-subrc = 0.
          <fs_bobinas>-verme = lv_diametro.
          <fs_bobinas>-meins = zcl_wm_nt_generic=>gc_meins_dia.
        ENDIF.
      ENDIF.

      " cvivo 65606 - Si la UA viene de un proveedor con dígitos de control, no se muestran éstos en negrita
      SELECT SINGLE * FROM mch1
        WHERE charg EQ @<fs_bobinas>-charg
          AND matnr EQ @<fs_bobinas>-matnr
        INTO @DATA(mch1).

      IF sy-subrc EQ 0.
        CALL FUNCTION 'CONVERSION_EXIT_ALPHA_OUTPUT'
          EXPORTING
            input  = mch1-lifnr
          IMPORTING
            output = mch1-lifnr.

        SELECT sign, opti AS option, low, high FROM tvarvc
          WHERE low EQ @mch1-lifnr
            AND name EQ 'ZWM_DC'
          INTO TABLE @DATA(r_dc).

        IF sy-subrc EQ 0.
          LOOP AT r_dc ASSIGNING FIELD-SYMBOL(<dc>).
            <dc>-option = 'CP'.
            <dc>-low = |*{ <dc>-high }|.
            CLEAR <dc>-high.
          ENDLOOP.

*          IF <fs_lqua>-lenum IN r_dc.
          IF <fs_bobinas>-id IN r_dc.
            <fs_bobinas>-estilo_ua_4digitos = abap_true.
          ENDIF.
        ENDIF.
      ENDIF.

      CALL FUNCTION 'CONVERSION_EXIT_LENUM_OUTPUT'
        EXPORTING
          input  = <fs_bobinas>-id
        IMPORTING
          output = <fs_bobinas>-id.
    ENDLOOP.

    IF wl_centro-cla_bobinas_pos = 'X'.
      SORT tp_bobinas BY posicion ASCENDING created_at ASCENDING.
    ELSE.
      SORT tp_bobinas BY created_at ASCENDING.
      LOOP AT tp_bobinas ASSIGNING <fs_bobinas>.
        <fs_bobinas>-posicion = sy-tabix.
      ENDLOOP.
    ENDIF.


  ENDMETHOD.
  method CENTROS_GET_ENTITY.
    tratar_key_entity( EXPORTING io_tech_request_context = io_tech_request_context
                       IMPORTING wp_entity               = er_entity ).

    er_entity = centros_getdetail( er_entity-werks ).
  endmethod.
  METHOD centros_getdetail.

    "Datos de centro
    SELECT SINGLE werks, name1
      INTO CORRESPONDING FIELDS OF @wp_centro
      FROM t001w
      WHERE werks = @vp_werks.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Centro incorrecto'(002) ).
    ENDIF.

    "Buscamos almacen WM
    SELECT SINGLE lgnum
      INTO @wp_centro-lgnum
      FROM t320
      WHERE werks = @vp_werks AND
            lgort = '1000'.
    IF sy-subrc = 0.
      "Buscamos parametros
      DATA(vl_condition) = |LG{ wp_centro-lgnum } = 'X'|.
      SELECT *
        INTO TABLE @DATA(tl_func)
        FROM zwm_rf_func
        WHERE (vl_condition).
    ENDIF.

    "Indicamos datos de parametros leidos
    LOOP AT tl_func ASSIGNING FIELD-SYMBOL(<fs_func>).
      CASE <fs_func>-func.
        WHEN 'LISTA_APRO_UI5_MOSTRAR_PEDIDO'.
          wp_centro-mostrar_pedido = 'X'.
        WHEN 'LISTA_APRO_UI5_LEC_BOBINAS_PRO'.
          wp_centro-lectura_bobinas_propuestas = 'X'.
        WHEN 'LISTA_APRO_UI5_ALG_PROPUES_BER'.
          wp_centro-algoritmo_prop_bobinas_ber = 'X'.
        WHEN 'CONSUMOS_BOBINAS_METROS'.
          wp_centro-consumos_bobinas_metros = 'X'.
*        WHEN 'CONSUMOS_PUEDE_REGISTRAR_ALTAS'.
*          wp_centro-consumos_puede_registrar_altas = 'X'.
        WHEN 'CONSUMOS_PUEDE_REGULARIZAR_UA'.
          wp_centro-consumos_puede_regularizar_ua = 'X'.
        WHEN 'CONSUMOS_ERROR_NO_EXISTE_UA'.
          wp_centro-consumos_error_no_existe_ua = 'X'.
        WHEN 'CONSUMOS_CREAR_OT_DE_NT'.
          wp_centro-consumos_crear_ot_de_nt = 'X'.
        WHEN 'NO_PROPONER_SEGUNDAS_BLOQ_ZROJ'.
          wp_centro-restringir_propuesta = 'X'.
        WHEN 'LISTA_APRO_UI5_PP'.
          wp_centro-lista_apro_ui5_pp = 'X'.
        WHEN 'LOGIN_CONSUMOS_UI5'.
          wp_centro-habilitar_login = 'X'.
      ENDCASE.

    ENDLOOP.

    "Transacción STVARV
    DATA(vl_name_like) = |%_{ wp_centro-lgnum }|.
    SELECT name, low
      INTO TABLE @DATA(tl_tvarvc)
      FROM tvarvc
      WHERE name LIKE @vl_name_like.
    LOOP AT tl_tvarvc ASSIGNING FIELD-SYMBOL(<fs_tvarvc>).
      IF     <fs_tvarvc>-name CP 'ZWM_LISTA_APRO_REFRESH_MIN_*'.
        wp_centro-refresh_auto_mins       = <fs_tvarvc>-low.
      ELSEIF <fs_tvarvc>-name CP 'ZWM_LISTA_APRO_NUM_FILAS_*'.
        wp_centro-lista_apro_num_filas    = <fs_tvarvc>-low.
      ELSEIF <fs_tvarvc>-name CP 'ZUI5_ENTCONS_DIAM_MAX_*'.
        wp_centro-diametro_maximo         = <fs_tvarvc>-low.
      ELSEIF <fs_tvarvc>-name CP 'ZMAX_ROWS_OMP_*'.
        wp_centro-max_filas_omp           = <fs_tvarvc>-low.
      ELSEIF <fs_tvarvc>-name CP 'ZWM_LISTA_APRO_CTD_MIN_*'.
        wp_centro-lista_apro_ctd_min      = <fs_tvarvc>-low.
      ELSEIF <fs_tvarvc>-name CP 'ZWM_APROV_IMPRIMIR_MAQUINA_*'.
        wp_centro-aprov_imprimir_maquina  = <fs_tvarvc>-low.
      ELSEIF <fs_tvarvc>-name CP 'ZWM_APROV_IMPRIMIR_MAQUINA_*'.
        wp_centro-aprov_imprimir_maquina  = <fs_tvarvc>-low.
      ELSEIF <fs_tvarvc>-name CP 'ZWM_LISTA_APRO_MARGEN_SEL_*'.
        wp_centro-lista_apro_margen_sel_ordenes  = <fs_tvarvc>-low.
      ELSEIF <fs_tvarvc>-name CP 'ZWM_CONSUMOS_MARGEN_SEL_*'.
        wp_centro-consumos_margen_sel_ordenes     = <fs_tvarvc>-low.
      ELSEIF <fs_tvarvc>-name CP 'ZWM_CONSUMOS_PORC_EXCESO_*'.
        wp_centro-porc_exceso_consumo     = <fs_tvarvc>-low.
      ENDIF.
    ENDLOOP.

    SELECT SINGLE @abap_true
      INTO @wp_centro-regularizar_bobinas_diam
      FROM tvarvc
      WHERE name  = 'ZWM_REGULARIZAR_BOBINAS_DIAM' AND
            low   = @wp_centro-lgnum.


    IF wp_centro-refresh_auto_mins    IS INITIAL. wp_centro-refresh_auto_mins     = 30. ENDIF.
    IF wp_centro-lista_apro_num_filas IS INITIAL. wp_centro-lista_apro_num_filas  = 15. ENDIF.
    IF wp_centro-max_filas_omp        IS INITIAL. wp_centro-max_filas_omp         = zcl_wm_constants=>gc_max_rows_omp_def. ENDIF.


    "Miramos los datos de intefase Nemesis
    SELECT *
      INTO TABLE @DATA(tl_ztwm001)
      FROM ztwm001
      WHERE cprog  =  'INTF_NEMESIS' AND
            param1 IN ('WERKS', 'REFRESH_INTERFASE').
    READ TABLE tl_ztwm001 ASSIGNING FIELD-SYMBOL(<fs_ztwm001>) WITH KEY param1 = 'WERKS'.
    IF sy-subrc = 0 AND <fs_ztwm001>-valor1 = vp_werks.
      READ TABLE tl_ztwm001 ASSIGNING <fs_ztwm001> WITH KEY param1 = 'REFRESH_INTERFASE'
                                                            param2 = 'URL'.
      IF sy-subrc = 0.
        wp_centro-url_interfase_1 = <fs_ztwm001>-valor1.
        wp_centro-url_interfase_2 = <fs_ztwm001>-valor2.
      ENDIF.
    ENDIF.


    SELECT SINGLE cla_bobinas_pos
      INTO @wp_centro-cla_bobinas_pos
      FROM zwm_param_alm
      WHERE lgnum = @wp_centro-lgnum.

  ENDMETHOD.
  METHOD constructor.
    super->constructor( ).
    rg_wm_nt = NEW zcl_zui5_wm_nt_dpc_ext( ).
  ENDMETHOD.
  METHOD consumir_bobina.

    DATA(wl_centro)   = centros_getdetail( vp_werks ).

    IF wl_centro-lista_apro_ui5_pp = 'X'.
      consumir_bobina_pp( EXPORTING vp_werks     = vp_werks
                                    vp_lgpla     = vp_lgpla
                                    vp_id_pedido = vp_id_pedido
                                    vp_id_bobina = vp_id_bobina
                                    vp_cantidad  = vp_cantidad
                          IMPORTING wp_bobina = wp_bobina ).
    ELSE.

      DATA wl_bobina TYPE zwm_ecm_bobine.

      CALL FUNCTION 'CONVERSION_EXIT_LENUM_INPUT'
        EXPORTING
          input           = vp_id_bobina   " External entry
        IMPORTING
          output          = wl_bobina-bobine   " Edited storage unit number for sto
        EXCEPTIONS
          check_failed    = 1
          not_numeric     = 2
          t344_get_failed = 3
          wrong_length    = 4
          OTHERS          = 5.
      wl_bobina-id_bob_ecm = vp_id_pedido.



      DATA(wl_return) = validar_consumo_bobina( vp_werks     = vp_werks
                                                vp_lgpla     = vp_lgpla
                                                vp_id_bobina = wl_bobina-bobine
                                                vp_id_pedido = vp_id_pedido
                                                vp_cantidad  = vp_cantidad ).
      IF wl_return-type CA 'EA'.
        zcl_seis_odata_utils=>lanzar_excepcion( bapiret2 = wl_return ).
      ENDIF.

      DATA(wl_estacion) = estaciones_getdetail( vp_werks = vp_werks
                                                vp_lgpla = vp_lgpla ).


      SELECT SINGLE *
        INTO CORRESPONDING FIELDS OF wl_bobina
        FROM zwm_ecm_bobine
        WHERE id_bob_ecm  = wl_bobina-id_bob_ecm  AND
              bobine      = wl_bobina-bobine      AND
              lgnum       = wl_centro-lgnum       AND
              tanum       = space.
      IF sy-subrc <> 0.
        zcl_seis_odata_utils=>lanzar_excepcion( id = 'ZUI5' number = '002' message_v1 = vp_id_pedido message_v2 = vp_id_bobina ).
        IF 1 = 0. MESSAGE e002(zui5) WITH vp_id_pedido vp_id_bobina. ENDIF.
      ENDIF.
      IF wl_bobina-saved = 'X' AND wl_bobina-diametro <> vp_cantidad.
        zcl_seis_odata_utils=>lanzar_excepcion( id = 'ZUI5' number = '003' ).
        IF 1 = 0. MESSAGE e003(zui5). ENDIF.
      ENDIF.

      wl_bobina-diametro  = vp_cantidad.
      IF wl_estacion-cla_sin_agrup = space OR vp_cantidad = 0.
        wl_bobina-saved = 'X'.
      ENDIF.


      " cvivo - validar que no está duplicando consumo en modelo "regularizar UA" (donde no debe haber errores)
      IF wl_centro-consumos_puede_regularizar_ua EQ 'X'.
        SELECT SINGLE *
          INTO @DATA(l_consumo)
          FROM zwm_intf_bhs
          WHERE werks             =   @vp_werks           AND
                cod_etiqueta      =   @wl_bobina-bobine   AND
                contabilizado     =   @abap_false         AND
                procesado         =   @abap_false         AND
                metros_restantes  <=  @wl_bobina-diametro.
        " no dejamos consumir mayores al pendiente, de lo contrario al reprocesar quedarán no contabilizados.
        " En ese caso ¿qué sucede si entra un consumo por menor cantidad y sí se va a contabilizar?
        " El pendiente se procesaría primero porque en el programa de consumos está puesto ese control

        IF sy-subrc = 0.
          zcl_seis_odata_utils=>lanzar_excepcion( 'Consumo ya grabado anteriormente, pendiente de contabilizar. Avise al responsable' ).
        ENDIF.
      ENDIF.


      "update control strct
      GET TIME STAMP FIELD DATA(vl_timestamp).
      wl_bobina-changed_by      = sy-uname.
      wl_bobina-changed_at      = vl_timestamp.
      wl_bobina-changed_at_zon  = sy-zonlo.


      " current entry will hold the remaining diametro -> must be then divided by all nt's
      " must get all the NT's that use this bobine
*    DATA(lt_data) =  zcl_wm_zwm_ecm_bobine_dao=>query_by_bobine(
*                                                                  EXPORTING
*                                                                    iv_lgnum         = is_data-lgnum
*                                                                    iv_bobine        = is_data-bobine
*                                                                    iv_id_bob_ecm = is_data-id_bob_ecm
**                                                                   iv_no_tanum_only = ABAP_TRUE
*                                                                ).

      "Buscamos el cuanto
      "Esta selección es rara: Busca primero un cuanto cualquiera,
      "y si no tiene stock disponible busca el que tenga cantidad total... y le pone esa cantidad como stock disponible...
      SELECT *
        INTO TABLE @DATA(tl_lqua)
        FROM lqua
        UP TO 1 ROWS
        WHERE lgnum = @wl_bobina-lgnum  AND
              lenum = @wl_bobina-bobine
        ORDER BY verme DESCENDING, gesme DESCENDING.
      IF sy-subrc = 0.
        DATA(wl_lqua) = tl_lqua[ 1 ].
      ENDIF.
      IF wl_lqua-verme = 0.
        wl_lqua-verme = wl_lqua-gesme.
      ENDIF.


      "Buscamos necesidades de transporte
      DATA tl_tbnum_rg TYPE RANGE OF ltbk-tbnum.
      IF vp_tbnum IS NOT INITIAL.
        tl_tbnum_rg = VALUE #( ( sign = 'I' option = 'EQ' low = vp_tbnum ) ).
      ENDIF.

      SELECT lgnum, tbnum, benum, nlpla, statu, rsnum
        INTO TABLE @DATA(tl_ltbk)
        FROM ltbk
        WHERE rsnum = @wl_bobina-id_bob_ecm AND
              tbnum IN @tl_tbnum_rg         AND
              betyp = 'O'                   AND
              bwlvs = '919'.
      IF sy-subrc = 0.
        "Cogemos la primera necesidad
        READ TABLE tl_ltbk ASSIGNING FIELD-SYMBOL(<fs_ltbk>) INDEX 1.
      ELSE.
        "TODO: ¿Tendremos que lanzar alguna excepción aquí?
        "return.
        zcl_seis_odata_utils=>lanzar_excepcion( 'No se han encontrado necesidades de transporte' ).
      ENDIF.


      DATA(vl_matnr) = wl_lqua-matnr.
      IF vl_matnr IS INITIAL.
*      DATA(lv_unknow) = abap_true.
*      " unknow bobine in SAP
        SELECT SINGLE matnr
          INTO @vl_matnr
          FROM ltbp
          WHERE lgnum = @<fs_ltbk>-lgnum AND
                tbnum = @<fs_ltbk>-tbnum.
      ENDIF.

      " Obtener cantidad restante

      DATA(vl_cantidad_restante) = CONV bstmg( wl_bobina-diametro ).

      IF wl_estacion-gestiona_palets = space AND wl_centro-consumos_bobinas_metros = space AND vl_cantidad_restante <> 0.
        " cvivo - cambio paradigma: si diámetro, obtengo metros restantes, si no, trabajo con los metros el reparto,
        "no con los KG (m equivale a UN si pallet siempre)
        zcl_wm_nt_generic=>conv_mat_diametro(
          EXPORTING iv_matnr          = vl_matnr
                    iv_qty            = vl_cantidad_restante
                    iv_meins          = zcl_wm_nt_generic=>gc_meins_dia   " DIA/KG
                    iv_dest_meins     = zcl_wm_nt_generic=>gc_meins_m     " Unidad de medida base
                    iv_werks          = vp_werks
          RECEIVING rv_qty = vl_cantidad_restante
          EXCEPTIONS um_no_valid       = 1
                     missing_constants = 2
                     missing_matnr     = 3
                     no_base_calc      = 4
                     OTHERS            = 5 ).
      ENDIF.

      "Cantidad consumida
      IF wl_lqua IS NOT INITIAL. " la UA existe
        DATA(vl_cantidad_actual) = zcl_wm_nt_generic=>conv_matnr( iv_matnr      = wl_lqua-matnr " DEL - si no es diámetro, ya viene en M/UN restantes
                                                                  iv_meins_dest = wl_estacion-meins
                                                                  iv_meins_orig = wl_lqua-meins
                                                                  iv_qty        = wl_lqua-verme ).

        "JCB: Haciendo esto se redondea a 0 decimales. Lo dejo así porque así estaba...
        DATA(vl_cantidad_consumida) = vl_cantidad_actual - vl_cantidad_restante.
      ENDIF.


      "Si la estación está configurada para indicar consumido, realmente el restante es el consumido
      "El restante será la diferencia entre el actual y el consumido
      IF wl_estacion-cla_indicar_consumido = 'X' AND vp_cantidad > 0.
        vl_cantidad_consumida = vl_cantidad_restante.
        vl_cantidad_restante = vl_cantidad_actual - vl_cantidad_consumida.
        IF vl_cantidad_restante < 0. vl_cantidad_restante = 0. ENDIF.
      ENDIF.


      "Calcular porcentajes:
*    "TODO: Para mi que esto sobra entero, porque en lt_data solo puede haber un dato,
*    "y la llamada a get_nt_for_resb ya la hemos hecho antes
*    DATA(lt_ltbk_full) = VALUE zcl_wm_constants=>gtty_ltbk( ).
*    LOOP AT lt_data INTO DATA(ls_data).
*      lv_rsnum = zcl_wm_nt_generic=>decode_line_id(   iv_id         = ls_data-id_bob_ecm
*                                                      iv_rsnum_mode = abap_true
*                                                      )-rsnum.
*      " must calc now remaining diametro for each of the nt's
*      lt_ltbk = zcl_wm_nt_generic=>get_nt_for_resb( is_resb = VALUE #( rsnum = lv_rsnum ) ).
*      APPEND LINES OF lt_ltbk TO lt_ltbk_full.
*    ENDLOOP.


      CLEAR tl_tbnum_rg.
      LOOP AT tl_ltbk ASSIGNING <fs_ltbk>.
        APPEND INITIAL LINE TO tl_tbnum_rg ASSIGNING FIELD-SYMBOL(<fs_tbnum_rg>).
        <fs_tbnum_rg>-sign = 'I'. <fs_tbnum_rg>-option = 'EQ'. <fs_tbnum_rg>-low = <fs_ltbk>-tbnum.
      ENDLOOP.

      "TODO: Comprobar si se recogen bien los datos... en principio debería ser que sí
      SELECT lgnum, tbnum, SUM( menge ) AS menge, meins, CAST( 0 AS INT4 ) AS porcentaje
        FROM ltbp
        INTO TABLE @DATA(tl_porcentajes_nt)
        WHERE tbnum IN @tl_tbnum_rg AND
              lgnum = @wl_centro-lgnum
        GROUP BY lgnum, tbnum, meins.

      DATA vl_cantidad_nt_pos_total TYPE ltbp-menge.
      LOOP AT tl_porcentajes_nt ASSIGNING FIELD-SYMBOL(<fs_porcentajes_nt>).
        ADD <fs_porcentajes_nt>-menge TO vl_cantidad_nt_pos_total.
      ENDLOOP.

      IF vl_cantidad_nt_pos_total <> 0.
        LOOP AT tl_porcentajes_nt ASSIGNING <fs_porcentajes_nt>.

*        IF wl_estacion-parte_rebobinado = 'X'.
*          "Para partes de rebobinado, los metros a consumir son exactamente los mismos en todas las bobinas, por lo que el porcentaje es 100
*          <fs_porcentajes_nt>-porcentaje = 100.
*        ELSE.
          <fs_porcentajes_nt>-porcentaje = round( val = <fs_porcentajes_nt>-menge / vl_cantidad_nt_pos_total * 100
                                                  dec = 0 ).
*        ENDIF.
        ENDLOOP.
      ENDIF.

      "Asignamos cantidades
*    DATA wl_ecm_bobine_tbnum      TYPE zwm_ecm_bobine.
      DATA: tl_ecm_bobine_tbnum      TYPE TABLE OF zwm_ecm_bobine,
            vl_cantidad_asignada_acu TYPE zwm_ecm_bobine-assigned_qty,
            vl_cantidad_asignada     TYPE zwm_ecm_bobine-assigned_qty.

      "cvivo - UM en función de tipo de UA
      DATA(vl_unidad_asignada) = SWITCH #( wl_lqua-letyp WHEN 'BB' THEN zcl_wm_nt_generic=>gc_meins_m
                                                         WHEN 'PL' THEN zcl_wm_nt_generic=>gc_meins_unidad ).


      DATA(vl_num_nts) = lines( tl_ltbk ).


      LOOP AT tl_ltbk ASSIGNING <fs_ltbk>.
        DATA(vl_tabix) = sy-tabix.

        READ TABLE tl_porcentajes_nt ASSIGNING <fs_porcentajes_nt> WITH KEY tbnum = <fs_ltbk>-tbnum.
        CHECK sy-subrc = 0.

        vl_cantidad_asignada = vl_cantidad_consumida * ( <fs_porcentajes_nt>-porcentaje / 100 ).
        IF vl_num_nts = vl_tabix AND wl_bobina-diametro = 0.
          " cvivo - última línea, si restantes 0, consumimos todo
          vl_cantidad_asignada_acu = vl_cantidad_actual.
        ELSE.
          ADD vl_cantidad_asignada TO vl_cantidad_asignada_acu.
        ENDIF.

        "Creamos entrada para este TBNUM....
        APPEND INITIAL LINE TO tl_ecm_bobine_tbnum ASSIGNING FIELD-SYMBOL(<fs_ecm_bobine_tbnum>).
        <fs_ecm_bobine_tbnum>-id_bob_ecm = zcl_wm_nt_generic=>code_line_id( is_data       = CORRESPONDING #( <fs_ltbk> )
                                                                            iv_tbnum_mode = abap_true ).
        <fs_ecm_bobine_tbnum>-bobine            = wl_bobina-bobine.
        <fs_ecm_bobine_tbnum>-lgnum             = wl_bobina-lgnum.
        <fs_ecm_bobine_tbnum>-parent_id_bob_ecm = wl_bobina-id_bob_ecm.
        <fs_ecm_bobine_tbnum>-is_tbnum          = 'X'.
        <fs_ecm_bobine_tbnum>-perc_assign       = <fs_porcentajes_nt>-porcentaje.
        <fs_ecm_bobine_tbnum>-assigned_qty      = vl_cantidad_asignada.
        <fs_ecm_bobine_tbnum>-restante          = vl_cantidad_actual - vl_cantidad_asignada_acu. " para que se acumule con las diferentes ctds. parciales
        <fs_ecm_bobine_tbnum>-assigned_qty_um   = vl_unidad_asignada.
        <fs_ecm_bobine_tbnum>-meins_restante    = vl_unidad_asignada.
        <fs_ecm_bobine_tbnum>-saved             = 'X'.

        IF wl_centro-consumos_bobinas_metros = space.
          <fs_ecm_bobine_tbnum>-diametro = wl_bobina-diametro.
        ENDIF.
      ENDLOOP.

      wl_bobina-assigned_qty    = vl_cantidad_asignada_acu.
      wl_bobina-restante        = 0.
      wl_bobina-assigned_qty_um = vl_unidad_asignada.
      wl_bobina-meins_restante  = vl_unidad_asignada.

*  "JCB: ESTO TIENE SENTIDO??? NO SERÁ SIEMPRE EL MISMO???? QUIEN ES EXACTAMENTE LR_REF????
*    IF lr_ref->get_data( )-id_bob_ecm <> is_data-id_bob_ecm.
*      lr_ref->set_parent_id( iv_id = is_data-id_bob_ecm ).
*      " diametro will be handled in top id_bob_ecm only
*      lr_ref->set_diametro(
*        EXPORTING
*          iv_diametro      = 0
*      ).
*
*    ENDIF.

*    lr_ref->save_data(
*      EXPORTING
*        iv_commit        = abap_true
*        iv_no_ot         = abap_true
*    ).
*  CATCH cx_root.
*    CONTINUE.
*ENDTRY.


*" refresh data. JCB... Pa qué?
*lt_data =   zcl_wm_zwm_ecm_bobine_dao=>query_by_bobine(
*EXPORTING
*iv_lgnum         = is_data-lgnum
*iv_bobine        = is_data-bobine
*iv_id_bob_ecm = is_data-id_bob_ecm
**        iv_no_tanum_only = ABAP_TRUE
*).
*
      "JCB: Esto tampoco se por qué... solo debe haber 1 en lt_data
*    SORT lt_data BY parent_id_bob_ecm ASCENDING.
*    " now execute OT creation and conssumption, and print
*    LOOP AT lt_data INTO ls_data.
**      TRY.
*      lr_ref = zcl_wm_zwm_ecm_bobine_dao=>get_entry(
*      EXPORTING
*        iv_id_bob_ecm    = ls_data-id_bob_ecm
*        iv_bobine        =  ls_data-bobine
*      ).
*
*
*      lr_ref->create_ot_v2( ).

      SORT tl_ecm_bobine_tbnum BY restante DESCENDING.
      LOOP AT tl_ecm_bobine_tbnum ASSIGNING <fs_ecm_bobine_tbnum>.
        crear_ot_consumo( CHANGING wp_bobina = <fs_ecm_bobine_tbnum> ).
      ENDLOOP.



      " cvivo - Lanzamos la impresión aquí, 1 etiqueta por consumo
      IF wl_estacion-cla_sin_agrup = space AND wl_bobina-diametro > 0.
        DATA tl_selection_table TYPE TABLE OF rsparamsl_255.
        EXPORT lv_vengo_inter_consumo = abap_true         TO MEMORY ID 'VENGO_CONSUMO' .
        EXPORT lv_cod_maq_portabo     = wl_estacion-lgpla TO MEMORY ID 'MAQUINA'.

        " create label for remaining
        tl_selection_table = VALUE #( ( selname = 'P_LGNUM' kind = 'P' sign = 'I' option = 'EQ' low = wl_bobina-lgnum )
                                      ( selname = 'S_LENUM' kind = 'S' sign = 'I' option = 'EQ' low = wl_bobina-bobine ) ).
        SUBMIT zimpresion_ua WITH SELECTION-TABLE tl_selection_table AND RETURN.
        FREE MEMORY ID: 'VENGO_CONSUMO', 'MAQUINA'.
      ENDIF.

      "Cerramos reserva
      zcl_wm_nt_generic=>close_rsnum( iv_rsnum  = CONV #( wl_bobina-id_bob_ecm )
                                      iv_last   = 'X'
                                      iv_nlpla  = vp_lgpla
                                      iv_lgnum  = wl_centro-lgnum ).


      "Actualizamos tabla Z de bobinas
      IF tl_ecm_bobine_tbnum IS NOT INITIAL.
        SELECT *
          INTO TABLE @DATA(tl_ecm_bobine_tbnum_bd)
          FROM zwm_ecm_bobine
          FOR ALL ENTRIES IN @tl_ecm_bobine_tbnum
          WHERE id_bob_ecm  = @tl_ecm_bobine_tbnum-id_bob_ecm AND
                bobine      = @tl_ecm_bobine_tbnum-bobine.
      ENDIF.

      GET TIME STAMP FIELD vl_timestamp.
      LOOP AT tl_ecm_bobine_tbnum ASSIGNING <fs_ecm_bobine_tbnum>.
*        wl_bobina-changed_by      = wl_bobina-created_by      = sy-uname.
*        wl_bobina-changed_at      = wl_bobina-created_at      = vl_timestamp.
*        wl_bobina-changed_at_zon  = wl_bobina-created_at_zon  = sy-zonlo.
        wl_bobina-changed_by      = sy-uname.
        wl_bobina-changed_at      = vl_timestamp.
        wl_bobina-changed_at_zon  = sy-zonlo.

        READ TABLE tl_ecm_bobine_tbnum_bd ASSIGNING FIELD-SYMBOL(<fs_ecm_bobine_tbnum_bd>)
          WITH KEY id_bob_ecm = <fs_ecm_bobine_tbnum>-id_bob_ecm
                   bobine     = <fs_ecm_bobine_tbnum>-bobine.
        IF sy-subrc = 0 AND <fs_ecm_bobine_tbnum_bd>-created_by IS NOT INITIAL.
          <fs_ecm_bobine_tbnum>-created_by      = <fs_ecm_bobine_tbnum_bd>-created_by.
          <fs_ecm_bobine_tbnum>-created_at      = <fs_ecm_bobine_tbnum_bd>-created_at.
          <fs_ecm_bobine_tbnum>-created_at_zon  = <fs_ecm_bobine_tbnum_bd>-created_at_zon.
        ENDIF.
      ENDLOOP.

      MODIFY zwm_ecm_bobine FROM TABLE tl_ecm_bobine_tbnum.
      IF sy-subrc = 0.
        MODIFY zwm_ecm_bobine FROM wl_bobina.
      ENDIF.
      IF sy-subrc <> 0.
        zcl_seis_odata_utils=>lanzar_excepcion( 'Error actualizando tabla de bobinas' ).
      ENDIF.

      wp_bobina-werks       = vp_werks.
      wp_bobina-id_pedprog  = vp_id_pedido.
      wp_bobina-id          = vp_id_bobina.
      wp_bobina-verme       = vp_cantidad.
      MOVE-CORRESPONDING wl_bobina TO wp_bobina.

    ENDIF.
  ENDMETHOD.
  METHOD consumir_bobina_con_traza.
    DATA(wl_centro) = centros_getdetail( vp_werks ).



    DATA vl_lenum TYPE lenum.
    CALL FUNCTION 'CONVERSION_EXIT_LENUM_INPUT'
      EXPORTING
        input           = vp_id_bobina
      IMPORTING
        output          = vl_lenum
      EXCEPTIONS
        check_failed    = 1
        not_numeric     = 2
        t344_get_failed = 3
        wrong_length    = 4
        OTHERS          = 5.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al convertir valor de lectura' ).
    ENDIF.

    SELECT SINGLE *
      INTO @DATA(wl_lqua)
      FROM lqua
      WHERE werks = @vp_werks AND
            lenum = @vl_lenum.

    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'No existe la UA en el sistema' ).
    ENDIF.



    DATA tl_orderid_rg TYPE RANGE OF zwm_ltbk_adit-upper_orderid.
    IF vp_pedido1 IS NOT INITIAL.
      APPEND INITIAL LINE TO tl_orderid_rg ASSIGNING FIELD-SYMBOL(<fs_orderid_rg>).
      <fs_orderid_rg> = VALUE #( sign = 'I' option = 'EQ' low = vp_pedido1 ).
    ENDIF.

    IF vp_pedido2 IS NOT INITIAL.
      APPEND INITIAL LINE TO tl_orderid_rg ASSIGNING <fs_orderid_rg>.
      <fs_orderid_rg> = VALUE #( sign = 'I' option = 'EQ' low = vp_pedido2 ).
    ENDIF.

    IF tl_orderid_rg IS INITIAL.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Debe indicar al menos un pedido' ).
    ENDIF.

    SELECT  *
      INTO TABLE @DATA(tl_zwm_ltbk_adit)
      FROM zwm_ltbk_adit
      WHERE lgnum = @wl_centro-lgnum AND
            ( upper_orderid IN @tl_orderid_rg OR
              lower_orderid IN @tl_orderid_rg ).
    LOOP AT tl_orderid_rg ASSIGNING <fs_orderid_rg>.
      READ TABLE tl_zwm_ltbk_adit ASSIGNING FIELD-SYMBOL(<fs_zwm_ltbk_adit>) WITH KEY upper_orderid = <fs_orderid_rg>-low.
      IF sy-subrc <> 0.
        zcl_seis_odata_utils=>lanzar_excepcion( |No se ha encontrado el pedido { <fs_orderid_rg>-low }| ).
      ENDIF.
    ENDLOOP.

    vp_benum = tl_zwm_ltbk_adit[ 1 ]-benum.


    DATA(vl_meins_out) = COND meins( WHEN wl_lqua-letyp EQ 'BB' THEN 'M'
                                     WHEN wl_lqua-letyp EQ 'PL' THEN 'ST' ).

    CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
      EXPORTING
        i_matnr              = wl_lqua-matnr
        i_in_me              = wl_lqua-meins
        i_out_me             = vl_meins_out
        i_menge              = wl_lqua-verme
      IMPORTING
        e_menge              = wl_lqua-verme
      EXCEPTIONS
        error_in_application = 1
        error                = 2
        OTHERS               = 3.

    wl_lqua-verme = trunc( wl_lqua-verme ).
    wl_lqua-meins = vl_meins_out.


    PERFORM f_consumo_manual IN PROGRAM zrwm0008 USING vp_lgpla vp_benum wl_lqua vp_pedido1 vp_pedido2 vp_consumo space.
  ENDMETHOD.
METHOD consumir_bobina_pp.

  "JCB 25.11.24. Grabar en tabla interfase BHS
  DATA: wl_intf_bhs TYPE zwm_intf_bhs.


  "JCB 27.11.24. Logs
  DATA(vl_clave_log) = |{ vp_werks }{ vp_lgpla }{ vp_id_pedido }_{ sy-datum } { sy-uzeit }|.


  DATA(rl_log) = NEW zcl_ap_log( object = cg_object_log clave = vl_clave_log ).
  IF vp_test = space.
    rl_log->log( msgty = 'I' message = |Consumo bobina. Inicio. Centro { vp_werks } - Ubicación { vp_lgpla } - Bobina { vp_id_bobina } - Reserva { vp_id_pedido } - Diámetro restante { vp_cantidad }| ).
  ENDIF.

  CLEAR: tp_link_conf_goodsmov, tp_goodsmovements, tp_timetickets.
  DATA wl_bobina TYPE zwm_ecm_bobine.

  CALL FUNCTION 'CONVERSION_EXIT_LENUM_INPUT'
    EXPORTING
      input           = vp_id_bobina   " External entry
    IMPORTING
      output          = wl_bobina-bobine   " Edited storage unit number for sto
    EXCEPTIONS
      check_failed    = 1
      not_numeric     = 2
      t344_get_failed = 3
      wrong_length    = 4
      OTHERS          = 5.
  wl_bobina-id_bob_ecm = vp_id_pedido.




  SELECT SINGLE aufnr
    INTO @DATA(vl_aufnr)
    FROM rkpf
    WHERE rsnum = @vp_id_pedido.
  IF sy-subrc <> 0.
    wl_intf_bhs-message = 'No se ha podido determinar la orden'.
    act_tabla_interfase_bhs( wp_intf_bhs = wl_intf_bhs
                             vp_test     = vp_test
                             rp_log      = rl_log ).
  ENDIF.

  DATA(wl_centro)   = centros_getdetail( vp_werks ).
  DATA(wl_estacion) = estaciones_getdetail( vp_werks = vp_werks
                                            vp_lgpla = vp_lgpla ).
  DATA(wl_estacion_generic) = zcl_wm_nt_generic=>get_instance( )->maquinas_getdetail( vp_machine_id = vp_lgpla ).

  wl_intf_bhs-rsnum         = vp_id_pedido.
  wl_intf_bhs-fecha         = sy-datum.
  wl_intf_bhs-hora          = sy-uzeit.
  wl_intf_bhs-pos_fich      = 1.
  wl_intf_bhs-lgnum         = wl_centro-lgnum.
  wl_intf_bhs-werks         = vp_werks.
  wl_intf_bhs-cod_consumo   = '00'.
  wl_intf_bhs-porta_bobinas = vp_lgpla.
  wl_intf_bhs-cod_etiqueta  = vp_id_bobina.



  SELECT SINGLE *
    INTO @DATA(wl_intf_omp_sap)
    FROM zwm_intf_omp_sap
    WHERE lgnum        = @wl_centro-lgnum AND
          ( upper_orderid = @vl_aufnr OR lower_orderid = @vl_aufnr ).
  IF sy-subrc <> 0.
    wl_intf_bhs-message = 'No se han encontrado los datos de la orden'.
    act_tabla_interfase_bhs( wp_intf_bhs = wl_intf_bhs
                             vp_test     = vp_test
                             rp_log      = rl_log ).
  ENDIF.


  IF vp_manual = 'X'.
    wl_bobina = asignar_bobina( vp_werks      = vp_werks
                                vp_lgpla      = vp_lgpla
                                vp_id_pedido  = vp_id_pedido
                                vp_id_bobina  = vp_id_bobina ).
  ENDIF.

  SELECT SINGLE *
    INTO CORRESPONDING FIELDS OF wl_bobina
    FROM zwm_ecm_bobine
    WHERE id_bob_ecm  = wl_bobina-id_bob_ecm  AND
          bobine      = wl_bobina-bobine      AND
          lgnum       = wl_centro-lgnum       AND
          tanum       = space.
  IF sy-subrc <> 0.
    MESSAGE e002(zui5) WITH vp_id_pedido vp_id_bobina INTO wl_intf_bhs-message.
    act_tabla_interfase_bhs( wp_intf_bhs = wl_intf_bhs
                             vp_test     = vp_test
                             rp_log      = rl_log ).

  ENDIF.
  IF wl_bobina-saved = 'X' AND wl_bobina-diametro <> vp_cantidad.
    MESSAGE e003(zui5) WITH vp_id_pedido vp_id_bobina INTO wl_intf_bhs-message.
    act_tabla_interfase_bhs( wp_intf_bhs = wl_intf_bhs
                             vp_test     = vp_test
                             rp_log      = rl_log ).
  ENDIF.

  wl_bobina-diametro  = vp_cantidad.
  IF wl_estacion-cla_sin_agrup = space OR vp_cantidad = 0.
    wl_bobina-saved     = 'X'.
  ENDIF.



  "update control strct
  GET TIME STAMP FIELD DATA(vl_timestamp).
  wl_bobina-changed_by      = sy-uname.
  wl_bobina-changed_at      = vl_timestamp.
  wl_bobina-changed_at_zon  = sy-zonlo.



  "Buscamos el cuanto
  "Esta selección es rara: Busca primero un cuanto cualquiera,
  "y si no tiene stock disponible busca el que tenga cantidad total... y le pone esa cantidad como stock disponible...
  SELECT *
    INTO TABLE @DATA(tl_lqua)
    FROM lqua
    UP TO 1 ROWS
    WHERE lgnum = @wl_bobina-lgnum  AND
          lenum = @wl_bobina-bobine
    ORDER BY verme DESCENDING, gesme DESCENDING.
  IF sy-subrc = 0.
    DATA(wl_lqua) = tl_lqua[ 1 ].
  ELSE.
    wl_intf_bhs-message = 'No se ha encontrado unidad de almacén'.
    act_tabla_interfase_bhs( wp_intf_bhs = wl_intf_bhs
                             vp_test     = vp_test
                             rp_log      = rl_log ).
  ENDIF.

  IF wl_lqua-verme = 0.
    wl_lqua-verme = wl_lqua-gesme.
  ENDIF.

  DATA(vl_matnr) = wl_lqua-matnr.
  " Obtener cantidad restante
  DATA(vl_cantidad_restante) = CONV bstmg( wl_bobina-diametro ).
  IF wl_estacion-gestiona_palets = space AND wl_centro-consumos_bobinas_metros = space AND vl_cantidad_restante <> 0.
    " cvivo - cambio paradigma: si diámetro, obtengo metros restantes, si no, trabajo con los metros el reparto,
    "no con los KG (m equivale a UN si pallet siempre)
    zcl_wm_nt_generic=>conv_mat_diametro(
      EXPORTING iv_matnr          = vl_matnr
                iv_qty            = vl_cantidad_restante
                iv_meins          = zcl_wm_nt_generic=>gc_meins_dia   " DIA/KG
                iv_dest_meins     = zcl_wm_nt_generic=>gc_meins_m     " Unidad de medida base
                iv_werks          = vp_werks
      RECEIVING rv_qty = vl_cantidad_restante
      EXCEPTIONS um_no_valid       = 1
                 missing_constants = 2
                 missing_matnr     = 3
                 no_base_calc      = 4
                 OTHERS            = 5 ).
  ENDIF.



  "Cantidad consumida
  IF wl_lqua IS NOT INITIAL. " la UA existe
    DATA(vl_cantidad_actual) = zcl_wm_nt_generic=>conv_matnr( iv_matnr      = wl_lqua-matnr " DEL - si no es diámetro, ya viene en M/UN restantes
                                                              iv_meins_dest = wl_estacion-meins
                                                              iv_meins_orig = wl_lqua-meins
                                                              iv_qty        = wl_lqua-verme ).
    IF vp_stock_bobina IS SUPPLIED.
      IF wl_estacion-gestiona_palets = space AND wl_centro-consumos_bobinas_metros = space AND vp_stock_bobina <> 0.
        zcl_wm_nt_generic=>conv_mat_diametro(
          EXPORTING iv_matnr          = wl_lqua-matnr
                    iv_qty            = vp_stock_bobina
                    iv_meins          = zcl_wm_nt_generic=>gc_meins_dia   " DIA/KG
                    iv_dest_meins     = zcl_wm_nt_generic=>gc_meins_m     " Unidad de medida base
                    iv_werks          = vp_werks
          RECEIVING rv_qty = vp_stock_bobina
          EXCEPTIONS um_no_valid       = 1
                     missing_constants = 2
                     missing_matnr     = 3
                     no_base_calc      = 4
                     OTHERS            = 5 ).

        IF vp_stock_bobina = 0.
          wl_intf_bhs-message = 'El stock de la bobina indicado no puede ser cero'.
          act_tabla_interfase_bhs( wp_intf_bhs = wl_intf_bhs
                                   vp_test     = vp_test
                                   rp_log      = rl_log ).

        ELSEIF vl_cantidad_actual < vp_stock_bobina.
          wl_intf_bhs-message = 'El stock de la bobina indicado no puede ser mayor que la cantidad actual'.
          act_tabla_interfase_bhs( wp_intf_bhs = wl_intf_bhs
                                   vp_test     = vp_test
                                   rp_log      = rl_log ).
        ENDIF.
        vl_cantidad_actual = vp_stock_bobina.
      ENDIF.
    ENDIF.

    DATA(vl_cantidad_consumida) = CONV ekpo-menge( vl_cantidad_actual - vl_cantidad_restante ).
  ENDIF.

  "Si la estación está configurada para indicar consumido, realmente el restante es el consumido
  "El restante será la diferencia entre el actual y el consumido
  IF wl_estacion-cla_indicar_consumido = 'X' AND vp_cantidad > 0.
    vl_cantidad_consumida = vl_cantidad_restante.
    vl_cantidad_restante = vl_cantidad_actual - vl_cantidad_consumida.
    IF vl_cantidad_restante < 0. vl_cantidad_restante = 0. ENDIF.
  ENDIF.



  wl_intf_bhs-matnr             = wl_lqua-matnr.
  wl_intf_bhs-stock_sap         = wl_intf_bhs-stock_sap_kg    = vl_cantidad_actual.
  wl_intf_bhs-cant_consumo      = wl_intf_bhs-cant_consumo_kg = vl_cantidad_consumida.
  wl_intf_bhs-cant_dif          = wl_intf_bhs-cant_dif_kg     = vl_cantidad_restante.
  wl_intf_bhs-metros_restantes  = vl_cantidad_restante.
  wl_intf_bhs-diametro_rest     = wl_bobina-diametro.
  wl_intf_bhs-meinh             = zcl_wm_nt_generic=>gc_meins_m.

  IF vp_test = space.
    rl_log->log( msgty = 'I' message = |Consumo bobina. Calculo cantidades: Actual { vl_cantidad_actual } { wl_intf_bhs-meinh } - Consumida { vl_cantidad_consumida } { wl_intf_bhs-meinh } - Restante { vl_cantidad_restante } { wl_intf_bhs-meinh }| ).
  ENDIF.



  DATA: tl_detail_return      TYPE TABLE OF bapi_coru_return,
        tl_return             TYPE bapiret2_t,
        wl_return_prodordconf TYPE bapiret1,
        vl_post_wrong_entries TYPE bapi_coru_param-ins_err VALUE '0',
        vl_testrun            TYPE bapi_coru_param-testrun VALUE ' '.

  DATA(rl_nt) = NEW zcl_zui5_wm_nt_dpc_ext( ).

  DATA tl_lista_apro_aux TYPE zwm_nt_ui5_aprov_list_tab.
  IF tp_lista_aprov IS SUPPLIED.
    tl_lista_apro_aux = tp_lista_aprov.
  ELSE.
    tl_lista_apro_aux = rl_nt->lista_aprov_pp_getlist( vp_werks = vp_werks
                                                       vp_lgpla = vp_lgpla
                                                       vp_mostrar_inactivos = vp_inactivos ).
  ENDIF.

  DATA tl_lista_apro  LIKE tl_lista_apro_aux.
  DATA vl_found_key   TYPE text40.
  DATA(vl_found)        = space.


  LOOP AT tl_lista_apro_aux ASSIGNING FIELD-SYMBOL(<fs_lista_apro>).
    IF vl_found = space AND <fs_lista_apro>-nlpla1_rsnum = vp_id_pedido.
      vl_found = 'X'.
      vl_found_key = <fs_lista_apro>-nlpla1_matnr && <fs_lista_apro>-canal.
    ENDIF.

    IF vl_found = 'X'.
      IF wl_estacion_generic-listado_apro_sin_agrup = 'X'.
        APPEND <fs_lista_apro> TO tl_lista_apro.
        EXIT.
      ENDIF.

      IF vl_found_key = <fs_lista_apro>-nlpla1_matnr && <fs_lista_apro>-canal.
        APPEND <fs_lista_apro> TO tl_lista_apro.
      ELSE.
        EXIT.
      ENDIF.
    ENDIF.
  ENDLOOP.



  IF vl_found = space AND vp_inactivos = 'X'.
    SELECT SINGLE aufnr, rsnum
      INTO @DATA(wl_afko)
      FROM afko
      WHERE rsnum = @vp_id_pedido.
    IF sy-subrc = 0.
      SELECT SINGLE *
        INTO @DATA(wl_intf_omp_sap_aux)
        FROM zwm_intf_omp_sap
        WHERE lgnum  = @wl_centro-lgnum AND
              ( upper_orderid  = @wl_afko-aufnr OR  lower_orderid = @wl_afko-aufnr ).
    ENDIF.
    IF sy-subrc = 0.
      APPEND INITIAL LINE TO tl_lista_apro ASSIGNING <fs_lista_apro>.
      <fs_lista_apro>-id            = |{ wl_centro-lgnum }-{ wl_intf_omp_sap_aux-benum }|.
      <fs_lista_apro>-nlpla1        = vp_lgpla.
      <fs_lista_apro>-nlpla1_matnr  = vl_matnr.
    ENDIF.
  ENDIF.

  TYPES: BEGIN OF st_cinumbers,
           aufnr            TYPE   zsppt_cinumbers-aufnr,
           vornr            TYPE   zsppt_cinumbers-vornr,
           matnr            TYPE   zsppt_cinumbers-matnr,
           ci_number        TYPE   zsppt_cinumbers-ci_number,
           estacion         TYPE   zsppt_cinumbers-estacion,
           cant_step        TYPE   zsppt_cinumbers-cant_step,
           rsnum            TYPE   zsppt_cinumbers-rsnum,
           rspos            TYPE   zsppt_cinumbers-rspos,
           stepcontribution TYPE   zsppt_cinumbers-stepcontribution,
         END OF st_cinumbers.

  DATA: tl_cinumbers TYPE TABLE OF st_cinumbers,
        wl_cinumbers LIKE LINE OF tl_cinumbers.
  DATA: vl_lgnum     TYPE lgnum,
        vl_ci_number TYPE zsppt_cinumbers-ci_number.
  LOOP AT tl_lista_apro ASSIGNING <fs_lista_apro>.
    SPLIT <fs_lista_apro>-id AT '-' INTO vl_lgnum vl_ci_number.

    SELECT *
      INTO TABLE @DATA(tl_intf_omp_sap)
      FROM zwm_intf_omp_sap
      WHERE lgnum  = @wl_centro-lgnum AND
            benum  = @vl_ci_number.
    LOOP AT tl_intf_omp_sap ASSIGNING FIELD-SYMBOL(<fs_intf_omp_sap>).
      rl_log->log( msgty = 'I' message = |Consumo bobina. Determinado pedido { vl_ci_number } ({ <fs_intf_omp_sap>-cinumber })| ).

      DO 6 TIMES.
        DATA(vl_index) = sy-index.
        ASSIGN COMPONENT |NLPLA{ vl_index }|          OF STRUCTURE <fs_intf_omp_sap> TO FIELD-SYMBOL(<fs_intf_nlpla>).
        ASSIGN COMPONENT |MATNR{ vl_index }|          OF STRUCTURE <fs_intf_omp_sap> TO FIELD-SYMBOL(<fs_intf_matnr>).
        ASSIGN COMPONENT |PAPER{ vl_index }LENGTH_M|  OF STRUCTURE <fs_intf_omp_sap> TO FIELD-SYMBOL(<fs_intf_length>).

        CHECK <fs_intf_nlpla> = <fs_lista_apro>-nlpla1.
        CHECK <fs_intf_matnr> = <fs_lista_apro>-nlpla1_matnr.
        CLEAR wl_cinumbers.



        wl_cinumbers-matnr     = <fs_intf_matnr>.
        wl_cinumbers-ci_number = <fs_intf_omp_sap>-benum.
        wl_cinumbers-estacion  = <fs_intf_nlpla>.

        IF <fs_intf_omp_sap>-upper_orderid IS NOT INITIAL.
          wl_cinumbers-aufnr            = <fs_intf_omp_sap>-upper_orderid.
          wl_cinumbers-stepcontribution = <fs_intf_omp_sap>-stepcontribution_upper.
          wl_cinumbers-cant_step        = <fs_intf_length> * wl_cinumbers-stepcontribution.
          APPEND wl_cinumbers TO tl_cinumbers.
          rl_log->log( msgty = 'I' message = |Consumo bobina. Determinada orden { wl_cinumbers-aufnr }| ).
        ENDIF.
        IF <fs_intf_omp_sap>-lower_orderid IS NOT INITIAL.
          wl_cinumbers-aufnr            = <fs_intf_omp_sap>-lower_orderid.
          wl_cinumbers-stepcontribution = <fs_intf_omp_sap>-stepcontribution_lower.
          wl_cinumbers-cant_step        = <fs_intf_length> * wl_cinumbers-stepcontribution.
          APPEND wl_cinumbers TO tl_cinumbers.
          rl_log->log( msgty = 'I' message = |Consumo bobina. Determinada orden { wl_cinumbers-aufnr }| ).
        ENDIF.

        wl_intf_bhs-id        = <fs_intf_omp_sap>-benum.
*        wl_intf_bhs-matnr     = <fs_intf_matnr>.
        wl_intf_bhs-orden_bhs = <fs_intf_omp_sap>-cinumber.
        wl_intf_bhs-aufnr1    = <fs_intf_omp_sap>-upper_orderid.
        wl_intf_bhs-aufnr2    = <fs_intf_omp_sap>-lower_orderid.
        wl_intf_bhs-calidad   = <fs_intf_omp_sap>-quality.
        wl_intf_bhs-cod_maquina_principal = <fs_intf_omp_sap>-machine_code.
      ENDDO.
    ENDLOOP.
  ENDLOOP.







  IF tl_cinumbers IS INITIAL.
    wl_intf_bhs-message = |No se ha podido determinar órdenes de producción para la reserva { vp_id_pedido }. Revise|.
    act_tabla_interfase_bhs( wp_intf_bhs = wl_intf_bhs
                             vp_test     = vp_test
                             rp_log      = rl_log ).
  ENDIF.



  SORT tl_cinumbers BY aufnr vornr matnr ci_number estacion.
  DELETE ADJACENT DUPLICATES FROM tl_cinumbers.



  DATA vl_cantidad_total TYPE ekpo-menge.
  LOOP AT tl_cinumbers ASSIGNING FIELD-SYMBOL(<fs_cinumbers>).
    ADD <fs_cinumbers>-cant_step TO vl_cantidad_total.
  ENDLOOP.


  wl_intf_bhs-charg             = wl_lqua-charg.
  wl_intf_bhs-cod_operario      = vg_operario.
  wl_intf_bhs-bwart             = '261'. "Hardcode sacado de la interfaz
  wl_intf_bhs-manual            = vp_manual.
  wl_intf_bhs-metros_desarrollados = vl_cantidad_total.

  DATA vl_menge TYPE menge_d.
  DATA vl_porc_cantidad TYPE p DECIMALS 6.


  DATA(vl_meins) = wl_estacion-meins.
  vl_index = 0.



  IF vl_meins = zcl_wm_nt_generic=>gc_meins_m.
    CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
      EXPORTING
        i_matnr              = vl_matnr
        i_in_me              = vl_meins
        i_out_me             = 'KG'
        i_menge              = vl_cantidad_consumida
      IMPORTING
        e_menge              = vl_cantidad_consumida
      EXCEPTIONS
        error_in_application = 1
        error                = 2
        OTHERS               = 3.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO wl_intf_bhs-message.
      act_tabla_interfase_bhs( wp_intf_bhs = wl_intf_bhs
                               vp_test     = vp_test
                               rp_log      = rl_log ).
    ENDIF.
    vl_meins = 'KG'.

    wl_intf_bhs-cant_consumo_kg = vl_cantidad_consumida.
    CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
      EXPORTING
        i_matnr              = vl_matnr
        i_in_me              = vl_meins
        i_out_me             = 'KG'
        i_menge              = wl_intf_bhs-stock_sap_kg
      IMPORTING
        e_menge              = wl_intf_bhs-stock_sap_kg
      EXCEPTIONS
        error_in_application = 1
        error                = 2
        OTHERS               = 3.
    CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
      EXPORTING
        i_matnr              = vl_matnr
        i_in_me              = vl_meins
        i_out_me             = 'KG'
        i_menge              = wl_intf_bhs-cant_dif_kg
      IMPORTING
        e_menge              = wl_intf_bhs-cant_dif_kg
      EXCEPTIONS
        error_in_application = 1
        error                = 2
        OTHERS               = 3.



  ENDIF.






  IF tl_cinumbers IS NOT INITIAL.
    DATA tl_xloek_rg TYPE RANGE OF resb-xloek.
    IF vp_inactivos = space.
      tl_xloek_rg = VALUE #( ( sign = 'I' option = 'EQ' low = space ) ).
    ENDIF.


    SELECT resb~rsnum, resb~rspos, resb~rsart, resb~matnr, resb~aufnr, resb~werks,
           resb~bdmng, resb~meins, resb~enmng, resb~sortf, resb~bwart, resb~vornr,
           mara~zzgramaje, mara~zzancho, mara~zzlargo, mara~mtart, t023t~wgbez60
      INTO TABLE @DATA(tl_resb)
      FROM resb INNER JOIN ( mara LEFT OUTER JOIN t023t ON t023t~spras = @sy-langu AND
                                                           t023t~matkl = mara~matkl )
                ON mara~matnr = resb~matnr
      FOR ALL ENTRIES IN @tl_cinumbers
      WHERE resb~aufnr = @tl_cinumbers-aufnr AND
            resb~xloek IN @tl_xloek_rg.
  ENDIF.

  DATA vl_cantidad_notificada TYPE ekpo-menge.
  LOOP AT tl_cinumbers ASSIGNING <fs_cinumbers>.

    "JCB 03.11.22 Corregimos reservas de los cinumbers
    READ TABLE tl_resb ASSIGNING FIELD-SYMBOL(<fs_resb>) WITH KEY aufnr = <fs_cinumbers>-aufnr
                                                                  matnr = <fs_cinumbers>-matnr
                                                                  sortf = <fs_cinumbers>-estacion.
    CHECK sy-subrc = 0.
    <fs_cinumbers>-rsnum = <fs_resb>-rsnum.
    <fs_cinumbers>-rspos = <fs_resb>-rspos.
    <fs_cinumbers>-vornr = <fs_resb>-vornr.

    CALL FUNCTION 'CONVERSION_EXIT_NUMCV_INPUT'
      EXPORTING
        input  = <fs_cinumbers>-vornr
      IMPORTING
        output = <fs_cinumbers>-vornr.

    CLEAR: vl_menge, vl_porc_cantidad.
    IF vl_cantidad_total <> 0.
      vl_porc_cantidad = <fs_cinumbers>-cant_step / vl_cantidad_total.
    ENDIF.



    vl_menge  = vl_cantidad_consumida * vl_porc_cantidad.
    CHECK vl_menge <> 0.
    CALL FUNCTION 'ROUND'
      EXPORTING
        input         = vl_menge
        sign          = '-' "Redondeo hacia abajo
      IMPORTING
        output        = vl_menge
      EXCEPTIONS
        input_invalid = 1
        overflow      = 2
        type_invalid  = 3
        OTHERS        = 4.


    "Voy a comprobar si la cantidad a consumir es excesiva
    DATA: vl_umbral_cantidad_tomada TYPE resb-enmng,
          vl_cantidad_tomada        TYPE resb-enmng.
    IF wl_centro-porc_exceso_consumo <> 0.
      IF <fs_resb>-meins <> vl_meins.
        CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
          EXPORTING
            i_matnr              = <fs_resb>-matnr
            i_in_me              = <fs_resb>-meins
            i_out_me             = vl_meins
            i_menge              = <fs_resb>-bdmng
          IMPORTING
            e_menge              = <fs_resb>-bdmng
          EXCEPTIONS
            error_in_application = 1
            error                = 2
            OTHERS               = 3.
        IF sy-subrc <> 0.
          MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO wl_intf_bhs-message.
          act_tabla_interfase_bhs( wp_intf_bhs = wl_intf_bhs
                                   vp_test     = vp_test
                                   rp_log      = rl_log ).
        ENDIF.
        CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
          EXPORTING
            i_matnr              = <fs_resb>-matnr
            i_in_me              = <fs_resb>-meins
            i_out_me             = vl_meins
            i_menge              = <fs_resb>-enmng
          IMPORTING
            e_menge              = <fs_resb>-enmng
          EXCEPTIONS
            error_in_application = 1
            error                = 2
            OTHERS               = 3.
        IF sy-subrc <> 0.
          MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO wl_intf_bhs-message.
          act_tabla_interfase_bhs( wp_intf_bhs = wl_intf_bhs
                                   vp_test     = vp_test
                                   rp_log      = rl_log ).
        ENDIF.
        <fs_resb>-meins = vl_meins.
      ENDIF.


      vl_umbral_cantidad_tomada = <fs_resb>-bdmng + <fs_resb>-bdmng * ( wl_centro-porc_exceso_consumo / 100 ).
      vl_cantidad_tomada        = <fs_resb>-enmng + vl_menge.
      IF vl_cantidad_tomada > vl_umbral_cantidad_tomada.
        wp_return-type    = 'W'.
        wp_return-message = 'Se va a realizar un consumo mayor que la cantidad necesaria en la orden. ¿Desea continuar?'.
      ENDIF.
    ENDIF.

    APPEND INITIAL LINE TO tp_timetickets ASSIGNING FIELD-SYMBOL(<fs_timetickets>).
    <fs_timetickets>-orderid        = <fs_cinumbers>-aufnr.
    <fs_timetickets>-operation      = <fs_cinumbers>-vornr.
    <fs_timetickets>-postg_date     = sy-datlo.
    <fs_timetickets>-conf_text      = ''.
    <fs_timetickets>-exec_start_date  = sy-datlo.
    <fs_timetickets>-exec_start_time  = sy-timlo.
    <fs_timetickets>-exec_fin_date    = sy-datlo.
    <fs_timetickets>-exec_fin_time    = sy-timlo.
    <fs_timetickets>-ex_created_by    = sy-uname.


    APPEND INITIAL LINE TO tp_goodsmovements ASSIGNING FIELD-SYMBOL(<fs_goodsmovements>).
*  <fs_goodsmovements>-reserv_no = <fs_resb>-rsnum.
*  <fs_goodsmovements>-res_item  = <fs_resb>-rspos.
*  <fs_goodsmovements>-res_type  = <fs_resb>-rsart.
    <fs_goodsmovements>-move_type = <fs_resb>-bwart.

    <fs_goodsmovements>-entry_qnt   = vl_menge.
    <fs_goodsmovements>-entry_uom   = vl_meins.
    ADD vl_menge TO vl_cantidad_notificada.


    <fs_goodsmovements>-material    = wl_lqua-matnr.
    <fs_goodsmovements>-plant       = wl_lqua-werks.
    <fs_goodsmovements>-stge_loc    = wl_lqua-lgort.
    <fs_goodsmovements>-batch       = wl_lqua-charg.
    <fs_goodsmovements>-stge_type   = '100'.
    <fs_goodsmovements>-stge_bin    = vp_lgpla.
*  <fs_goodsmovements>-STGE_BIN_PC = vp_lgpla.
*  <fs_goodsmovements>-stge_bin_st = vp_lgpla.


    IF <fs_timetickets>-orderid = wl_intf_bhs-aufnr1.
      wl_intf_bhs-aufnr1_menge = <fs_goodsmovements>-entry_qnt.
    ENDIF.
    IF <fs_timetickets>-orderid = wl_intf_bhs-aufnr2.
      wl_intf_bhs-aufnr2_menge = <fs_goodsmovements>-entry_qnt.
    ENDIF.
    ADD <fs_goodsmovements>-entry_qnt TO wl_intf_bhs-consumo_notificar_kg.


    ADD 1 TO vl_index.
    APPEND INITIAL LINE TO tp_link_conf_goodsmov ASSIGNING FIELD-SYMBOL(<fs_link_conf_goodsmov>).
    <fs_link_conf_goodsmov>-index_confirm   = vl_index.
    <fs_link_conf_goodsmov>-index_goodsmov  = vl_index.


    DATA vl_meins_aux TYPE mara-meins.
    "JCB 02.10.23. En realidad debería estasr así, pero no me atrevo porque afecta a PACS y estoy cambiando SCY
*  vl_meins_aux = COND #( WHEN wl_estacion-gestiona_palets = space AND wl_centro-consumos_bobinas_metros = space
*                      THEN zcl_wm_nt_generic=>gc_meins_dia
*                      ELSE wl_estacion-meins ).
    vl_meins_aux = COND #( WHEN wl_centro-consumos_bobinas_metros = space THEN zcl_wm_nt_generic=>gc_meins_dia ELSE zcl_wm_nt_generic=>gc_meins_m ).

    APPEND INITIAL LINE TO tp_consumos_pp ASSIGNING FIELD-SYMBOL(<fs_zwm_consumos_pp>).
    <fs_zwm_consumos_pp>-werks      = <fs_goodsmovements>-plant.
    <fs_zwm_consumos_pp>-contador2  = <fs_link_conf_goodsmov>-index_goodsmov.
    <fs_zwm_consumos_pp>-dats       = <fs_timetickets>-exec_start_date.
    <fs_zwm_consumos_pp>-tims       = <fs_timetickets>-exec_start_time.
    <fs_zwm_consumos_pp>-id_bob_ecm = vp_id_pedido.
    <fs_zwm_consumos_pp>-ci_number  = <fs_cinumbers>-ci_number.
    <fs_zwm_consumos_pp>-lenum      = wl_bobina-bobine.
    <fs_zwm_consumos_pp>-aufnr      = <fs_cinumbers>-aufnr.
    <fs_zwm_consumos_pp>-lgpla      = <fs_goodsmovements>-stge_bin.
    <fs_zwm_consumos_pp>-matnr      = <fs_goodsmovements>-material.
    <fs_zwm_consumos_pp>-charg      = <fs_goodsmovements>-batch.
    <fs_zwm_consumos_pp>-restante   = vp_cantidad.
    <fs_zwm_consumos_pp>-meins      = vl_meins_aux.
    <fs_zwm_consumos_pp>-consumo    = <fs_goodsmovements>-entry_qnt.
    <fs_zwm_consumos_pp>-meins_consumo = <fs_goodsmovements>-entry_uom.
    <fs_zwm_consumos_pp>-cla_manual    = vp_manual.
    <fs_zwm_consumos_pp>-ernam         = sy-uname.
    <fs_zwm_consumos_pp>-erdat         = sy-datum.
    <fs_zwm_consumos_pp>-erzet         = sy-uzeit.
  ENDLOOP.

  "Regularizamos resto de cantidad consumida en el último consumo
  IF vl_cantidad_restante = 0 AND <fs_goodsmovements> IS ASSIGNED.
    <fs_goodsmovements>-entry_qnt = <fs_goodsmovements>-entry_qnt + ( vl_cantidad_consumida - vl_cantidad_notificada ).
  ENDIF.



  IF tp_goodsmovements IS INITIAL.
    wl_intf_bhs-message =  'No se ha podido determinar el consumo. Revise'.
    act_tabla_interfase_bhs( wp_intf_bhs = wl_intf_bhs
                             vp_test     = vp_test
                             rp_log      = rl_log ).
  ENDIF.


  "Quitamos los consumos negativos por ahora
  DELETE tp_goodsmovements WHERE entry_qnt < 0.

  wl_intf_bhs-procesado = 'X'.


  IF vp_test = space.
    rl_log->log( msgty = 'I' message = |Consumo bobina. Llamada a BAPI consumos| ).
  ENDIF.

  vl_testrun = vp_test.
  CALL FUNCTION 'BAPI_PRODORDCONF_CREATE_TT'
    EXPORTING
      post_wrong_entries = vl_post_wrong_entries
      testrun            = vl_testrun
    IMPORTING
      return             = wl_return_prodordconf
    TABLES
      timetickets        = tp_timetickets
      goodsmovements     = tp_goodsmovements
      link_conf_goodsmov = tp_link_conf_goodsmov
      detail_return      = tl_detail_return
    EXCEPTIONS
      error_message      = 1.
  IF sy-subrc = 1.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO wl_intf_bhs-message.
    act_tabla_interfase_bhs( wp_intf_bhs = wl_intf_bhs
                             vp_test     = vp_test
                             rp_log      = rl_log ).
  ENDIF.
  MOVE-CORRESPONDING tl_detail_return TO tl_return.
  TRY.
      zcl_seis_odata_utils=>lanzar_excepcion( bapiret2_t = tl_return ).
    CATCH /iwbep/cx_mgw_busi_exception INTO DATA(rl_exc).
      wl_intf_bhs-message =  rl_exc->get_text( ).
      act_tabla_interfase_bhs( wp_intf_bhs = wl_intf_bhs
                               vp_test     = vp_test
                               rp_log      = rl_log ).
  ENDTRY.



  IF vp_test = space.
    CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
      EXPORTING
        wait = 'X'.
    WAIT UP TO 2 SECONDS.
    wl_intf_bhs-contabilizado  = 'X'.


    IF tl_detail_return IS NOT INITIAL.
      SELECT *
        INTO TABLE @DATA(tl_afwi)
        FROM afwi
        FOR ALL ENTRIES IN @tl_detail_return
        WHERE rueck = @tl_detail_return-conf_no AND
              rmzhl = @tl_detail_return-conf_cnt.
    ENDIF.




    SELECT MAX( contador )
      INTO @DATA(vl_contador)
      FROM zwm_consumos_pp
      WHERE werks = @vp_werks.
    ADD 1 TO vl_contador.

    LOOP AT tp_consumos_pp ASSIGNING <fs_zwm_consumos_pp>.
      <fs_zwm_consumos_pp>-contador = vl_contador.
      READ TABLE tl_detail_return ASSIGNING FIELD-SYMBOL(<fs_detail_return>) WITH KEY row = <fs_zwm_consumos_pp>-contador2.
      IF sy-subrc = 0.
        <fs_zwm_consumos_pp>-texto = <fs_detail_return>-message.
        <fs_zwm_consumos_pp>-rueck = <fs_detail_return>-conf_no.
        <fs_zwm_consumos_pp>-rmzhl = <fs_detail_return>-conf_cnt.

        READ TABLE tl_afwi INTO DATA(wl_afwi) WITH KEY rueck = <fs_zwm_consumos_pp>-rueck
                                                       rmzhl = <fs_zwm_consumos_pp>-rmzhl.
        IF sy-subrc <> 0.
          CLEAR wl_afwi.
        ENDIF.
        rl_log->log( msgty = 'S' message = |Notificación { <fs_detail_return>-conf_no }/{ <fs_detail_return>-conf_cnt } en orden { <fs_zwm_consumos_pp>-aufnr } realizada. Doc. material { wl_afwi-mblnr }/{ wl_afwi-mjahr } | ).
      ENDIF.
    ENDLOOP.

    "Actualizamos tablaS Z de bobinas
    MODIFY zwm_consumos_pp  FROM TABLE tp_consumos_pp.
    MODIFY zwm_ecm_bobine   FROM wl_bobina.

    " cvivo - Lanzamos la impresión aquí, 1 etiqueta por consumo
    IF wl_bobina-diametro > 0 AND wl_estacion-no_imprimir_al_consumir = space AND wl_estacion-cla_sin_agrup = space.
      DATA tl_selection_table TYPE TABLE OF rsparamsl_255.
      EXPORT lv_vengo_inter_consumo = abap_true         TO MEMORY ID 'VENGO_CONSUMO' .
      EXPORT lv_cod_maq_portabo     = wl_estacion-lgpla TO MEMORY ID 'MAQUINA'.

      " create label for remaining
      tl_selection_table = VALUE #( ( selname = 'P_LGNUM' kind = 'P' sign = 'I' option = 'EQ' low = wl_bobina-lgnum )
                                    ( selname = 'S_LENUM' kind = 'S' sign = 'I' option = 'EQ' low = wl_bobina-bobine ) ).
      SUBMIT zimpresion_ua WITH SELECTION-TABLE tl_selection_table AND RETURN.
      FREE MEMORY ID: 'VENGO_CONSUMO', 'MAQUINA'.
    ENDIF.
  ENDIF.

  IF vp_test = space.
    MODIFY zwm_intf_bhs FROM wl_intf_bhs.
  ENDIF.


  wp_bobina-werks       = vp_werks.
  wp_bobina-id_pedprog  = vp_id_pedido.
  wp_bobina-id          = vp_id_bobina.
  wp_bobina-verme       = vp_cantidad.
  MOVE-CORRESPONDING wl_bobina TO wp_bobina.

  IF vp_test = space.
    rl_log->log( msgty = 'S' message = |Consumo bobina { vp_id_bobina } terminado| ).
  ENDIF.




*wl_intf_bhs-TOLERANCIA
*wl_intf_bhs-MEINS_TOL
*wl_intf_bhs-MBLNR
*wl_intf_bhs-MJAHR
*wl_intf_bhs-TBNUM
*wl_intf_bhs-TBPOS
*wl_intf_bhs-TANUM
*wl_intf_bhs-TAPOS
*wl_intf_bhs-RSPOS
*wl_intf_bhs-RSART
*wl_intf_bhs-METROS_DES_INICIO_CI
*wl_intf_bhs-CONSUMO_NOTIFICAR



ENDMETHOD.
  METHOD consumir_bobina_preimp.
    DATA(wl_centro) = centros_getdetail( vp_werks ).
    "En wl_centro está el LGNUM

    DATA vl_lenum TYPE lenum.
    CALL FUNCTION 'CONVERSION_EXIT_LENUM_INPUT'
      EXPORTING
        input           = vp_id_bobina
      IMPORTING
        output          = vl_lenum
      EXCEPTIONS
        check_failed    = 1
        not_numeric     = 2
        t344_get_failed = 3
        wrong_length    = 4
        OTHERS          = 5.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al convertir valor de lectura' ).
    ENDIF.

    SELECT SINGLE * FROM lqua
      WHERE lenum EQ @vl_lenum
        AND lgnum EQ @wl_centro-lgnum
      INTO @DATA(wl_lqua).

    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al recuperar datos de la UA' ).
    ENDIF.

    DATA(wl_ltbk) = VALUE ltbk( benum = vp_ebeln
                                nlpla = vp_lgpla ).

    "JCB: Dejo aquí la creación del consumo
    DATA(wl_entry_create_cons) = zcl_wm_nt_generic=>get_instance( )->bobecm_create_consumption(
          iv_lgnum              = wl_centro-lgnum
          is_orig_lqua          = wl_lqua
          is_orig_ltbk          = wl_ltbk
          iv_cons_qty           = 0
          iv_meins              = 'KG'
          iv_calidad            = space
          iv_restante           = 0
          iv_diametro           = 0
          iv_create_entry_only  = 'X' ).

    " 3 - submit consumption program
    DATA(wl_entry_create_cons_fin) = zcl_wm_nt_generic=>get_instance( )->bobecm_create_consumption(
          iv_lgnum              = wl_centro-lgnum
          is_orig_lqua          = wl_lqua
          is_orig_ltbk          = wl_ltbk
          iv_cons_qty           = 0
          iv_meins              = 'KG'
          iv_calidad            = space
          iv_restante           = 0
          iv_diametro           = 0
          iv_create_entry_only  = space
          is_entry              = wl_entry_create_cons ).

    IF wl_entry_create_cons_fin IS INITIAL.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al registrar el consumo' ).
    ENDIF.



  ENDMETHOD.
  METHOD crear_ot_consumo.
    "create ot's
    DATA(wl_tbnum_code) =  zcl_wm_nt_generic=>decode_line_id( iv_id         = wp_bobina-id_bob_ecm
                                                              iv_tbnum_mode = 'X' ).
    " cvivo - Punto de verificación: identificar NTs mal seleccionadas
    SELECT SINGLE *
      FROM ltbk INTO @DATA(wl_ltbk)
      WHERE lgnum = @wl_tbnum_code-lgnum AND
            tbnum = @wl_tbnum_code-tbnum.
    IF wl_ltbk-bwlvs NE '919'.
      DATA(key) = |Error selección NT: { sy-datum } / { sy-uzeit } / { sy-uname }|.
      LOG-POINT ID zwm_ui5 SUBKEY key FIELDS wp_bobina wl_tbnum_code.
    ENDIF.


    SELECT *
      INTO TABLE @DATA(tl_lqua_tbnum)
      FROM lqua
      UP TO 1 ROWS
      WHERE lgnum = @wp_bobina-lgnum  AND
            lenum = @wp_bobina-bobine
      ORDER BY verme DESCENDING, gesme DESCENDING.
    IF sy-subrc = 0.
      DATA(wl_lqua_tbnum) = tl_lqua_tbnum[ 1 ].
    ELSE.
      CLEAR wl_lqua_tbnum.
    ENDIF.
    IF wl_lqua_tbnum-verme = 0.
      wl_lqua_tbnum-verme = wl_lqua_tbnum-gesme.
    ENDIF.

    DATA(vl_no_ot) = space.
    IF wl_lqua_tbnum IS INITIAL.
      SELECT SINGLE matnr, werks
        INTO CORRESPONDING FIELDS OF @wl_lqua_tbnum
        FROM ltbp
        WHERE lgnum = @wl_tbnum_code-lgnum AND
              tbnum = @wl_tbnum_code-tbnum.
      wl_lqua_tbnum-lenum = wp_bobina-bobine.
      vl_no_ot = 'X'.
    ENDIF.

    IF vl_no_ot = space.
      " 1 - create ot's based on NT
      " 919 or 920/921/922
      DATA(vl_bwlvs) = get_bwlvs_ot( wp_bobina = wp_bobina ).

      "JCB: Creo que esta condición también sobra...
*        IF me->gs_data-parent_id_bob_ecm IS INITIAL OR is_parent_rsnum-parent_id_bob_ecm IS INITIAL. " only do it for original nt that triggered consumption

      " 1 - create ot
      zcl_wm_nt_generic=>create_ot_4_nt_sc(
        EXPORTING
          iv_bobine     = wp_bobina-bobine
          iv_dest_nlpla = wl_ltbk-nlpla
          iv_bwlvs      = vl_bwlvs
          is_ltbk       = wl_ltbk
          iv_qty        = wp_bobina-assigned_qty
          iv_meins      = wp_bobina-assigned_qty_um
        IMPORTING
          es_ltak       = DATA(wl_ltak)
          ev_subrc      = DATA(wl_subrc)
      ).

      " 2 - change reserv
      zcl_wm_nt_generic=>change_reserv_4_cons(
          iv_bobine = wp_bobina-bobine
          is_ltbk   = wl_ltbk
          iv_qty    = wp_bobina-assigned_qty
          iv_meins  = wp_bobina-assigned_qty_um ).
*        ENDIF.
    ENDIF.

    DATA wl_data_mat_info TYPE zcl_wm_constants=>gty_aprovis_list.
    wl_data_mat_info-lznum = wl_ltbk-lznum.
    APPEND INITIAL LINE TO wl_data_mat_info-lt_tbnum. "Para que funcione correctamente se requiere que en esta tabla haya una fila en blanco.... ¿?
    DATA(wl_mat_info) = zcl_wm_nt_generic=>get_mat_info_lznum( is_data = wl_data_mat_info ).


    "JCB: Dejo aquí la creación del consumo
    DATA(wl_entry_create_cons) = zcl_wm_nt_generic=>get_instance( )->bobecm_create_consumption(
      iv_lgnum              = wl_ltbk-lgnum
      is_orig_lqua          = wl_lqua_tbnum
      is_orig_ltbk          = wl_ltbk
      iv_cons_qty           = wp_bobina-assigned_qty
      iv_meins              = wp_bobina-assigned_qty_um
      iv_calidad            = wl_mat_info-quality
      iv_restante           = wp_bobina-restante
      iv_diametro           = wp_bobina-diametro
      iv_create_entry_only  = 'X'
      iv_operario           = vg_operario ).

    " 3 - submit consumption program
    DATA(wl_entry_create_cons_fin) = zcl_wm_nt_generic=>get_instance( )->bobecm_create_consumption(
      iv_lgnum              = wl_ltbk-lgnum
      is_orig_lqua          = wl_lqua_tbnum
      is_orig_ltbk          = wl_ltbk
      iv_cons_qty           = wp_bobina-assigned_qty
      iv_meins              = wp_bobina-assigned_qty_um
      iv_calidad            = wl_mat_info-quality
      iv_restante           = wp_bobina-restante
      iv_diametro           = wp_bobina-diametro
      iv_create_entry_only  = space
      is_entry              = wl_entry_create_cons
      iv_operario           = vg_operario ).
    IF wl_entry_create_cons_fin IS INITIAL.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al registrar el consumo' ).
    ENDIF.
    MOVE-CORRESPONDING wl_ltak TO wp_bobina.


  ENDMETHOD.
  METHOD CREAR_OT_CONSUMO_PP.
    "create ot's
    DATA(wl_tbnum_code) =  zcl_wm_nt_generic=>decode_line_id( iv_id         = wp_bobina-id_bob_ecm
                                                              iv_tbnum_mode = 'X' ).
    " cvivo - Punto de verificación: identificar NTs mal seleccionadas
    SELECT SINGLE *
      FROM ltbk INTO @DATA(wl_ltbk)
      WHERE lgnum = @wl_tbnum_code-lgnum AND
            tbnum = @wl_tbnum_code-tbnum.
    IF wl_ltbk-bwlvs NE '919'.
      DATA(key) = |Error selección NT: { sy-datum } / { sy-uzeit } / { sy-uname }|.
      LOG-POINT ID zwm_ui5 SUBKEY key FIELDS wp_bobina wl_tbnum_code.
    ENDIF.


    SELECT *
      INTO TABLE @DATA(tl_lqua_tbnum)
      FROM lqua
      UP TO 1 ROWS
      WHERE lgnum = @wp_bobina-lgnum  AND
            lenum = @wp_bobina-bobine
      ORDER BY verme DESCENDING, gesme DESCENDING.
    IF sy-subrc = 0.
      DATA(wl_lqua_tbnum) = tl_lqua_tbnum[ 1 ].
    ELSE.
      CLEAR wl_lqua_tbnum.
    ENDIF.
    IF wl_lqua_tbnum-verme = 0.
      wl_lqua_tbnum-verme = wl_lqua_tbnum-gesme.
    ENDIF.

    DATA(vl_no_ot) = space.
    IF wl_lqua_tbnum IS INITIAL.
      SELECT SINGLE matnr, werks
        INTO CORRESPONDING FIELDS OF @wl_lqua_tbnum
        FROM ltbp
        WHERE lgnum = @wl_tbnum_code-lgnum AND
              tbnum = @wl_tbnum_code-tbnum.
      wl_lqua_tbnum-lenum = wp_bobina-bobine.
      vl_no_ot = 'X'.
    ENDIF.

    IF vl_no_ot = space.
      " 1 - create ot's based on NT
      " 919 or 920/921/922
      DATA(vl_bwlvs) = get_bwlvs_ot( wp_bobina = wp_bobina ).

      "JCB: Creo que esta condición también sobra...
*        IF me->gs_data-parent_id_bob_ecm IS INITIAL OR is_parent_rsnum-parent_id_bob_ecm IS INITIAL. " only do it for original nt that triggered consumption

      " 1 - create ot
      zcl_wm_nt_generic=>create_ot_4_nt_sc(
        EXPORTING
          iv_bobine     = wp_bobina-bobine
          iv_dest_nlpla = wl_ltbk-nlpla
          iv_bwlvs      = vl_bwlvs
          is_ltbk       = wl_ltbk
          iv_qty        = wp_bobina-assigned_qty
          iv_meins      = wp_bobina-assigned_qty_um
        IMPORTING
          es_ltak       = DATA(wl_ltak)
          ev_subrc      = DATA(wl_subrc)
      ).

      " 2 - change reserv
      zcl_wm_nt_generic=>change_reserv_4_cons(
          iv_bobine = wp_bobina-bobine
          is_ltbk   = wl_ltbk
          iv_qty    = wp_bobina-assigned_qty
          iv_meins  = wp_bobina-assigned_qty_um ).
*        ENDIF.
    ENDIF.

    DATA wl_data_mat_info TYPE zcl_wm_constants=>gty_aprovis_list.
    wl_data_mat_info-lznum = wl_ltbk-lznum.
    APPEND INITIAL LINE TO wl_data_mat_info-lt_tbnum. "Para que funcione correctamente se requiere que en esta tabla haya una fila en blanco.... ¿?
    DATA(wl_mat_info) = zcl_wm_nt_generic=>get_mat_info_lznum( is_data = wl_data_mat_info ).


    "JCB: Dejo aquí la creación del consumo
    DATA(wl_entry_create_cons) = zcl_wm_nt_generic=>get_instance( )->bobecm_create_consumption(
      iv_lgnum              = wl_ltbk-lgnum
      is_orig_lqua          = wl_lqua_tbnum
      is_orig_ltbk          = wl_ltbk
      iv_cons_qty           = wp_bobina-assigned_qty
      iv_meins              = wp_bobina-assigned_qty_um
      iv_calidad            = wl_mat_info-quality
      iv_restante           = wp_bobina-restante
      iv_diametro           = wp_bobina-diametro
      iv_create_entry_only  = 'X' ).

    " 3 - submit consumption program
    DATA(wl_entry_create_cons_fin) = zcl_wm_nt_generic=>get_instance( )->bobecm_create_consumption(
      iv_lgnum              = wl_ltbk-lgnum
      is_orig_lqua          = wl_lqua_tbnum
      is_orig_ltbk          = wl_ltbk
      iv_cons_qty           = wp_bobina-assigned_qty
      iv_meins              = wp_bobina-assigned_qty_um
      iv_calidad            = wl_mat_info-quality
      iv_restante           = wp_bobina-restante
      iv_diametro           = wp_bobina-diametro
      iv_create_entry_only  = space
      is_entry              = wl_entry_create_cons ).
    IF wl_entry_create_cons_fin IS INITIAL.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al registrar el consumo' ).
    ENDIF.
    MOVE-CORRESPONDING wl_ltak TO wp_bobina.


  ENDMETHOD.
  METHOD empleados_get_entity.
    tratar_key_entity( EXPORTING io_tech_request_context = io_tech_request_context
                       IMPORTING wp_entity               = er_entity ).
    er_entity = empleados_getdetail( vp_werks     = er_entity-werks
                                     vp_operario  = er_entity-operario ).
  ENDMETHOD.
  METHOD empleados_getdetail.

    DATA(wl_centro) = centros_getdetail( vp_werks ).
    SELECT SINGLE *
      INTO @DATA(wl_zwm_rf_oper)
      FROM zwm_rf_oper
      WHERE lgnum     = @wl_centro-lgnum AND
            operario  = @vp_operario.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Empleado incorrecto' ).
    ENDIF.

    MOVE-CORRESPONDING wl_zwm_rf_oper TO wp_empleado.

  ENDMETHOD.
  METHOD estaciones_get_entity.
    tratar_key_entity( EXPORTING io_tech_request_context = io_tech_request_context
                       IMPORTING wp_entity               = er_entity ).
    er_entity = estaciones_getdetail( vp_lgpla = er_entity-lgpla
                                      vp_werks = er_entity-werks ).
  ENDMETHOD.
  METHOD estaciones_get_entityset.
    DATA wl_entity LIKE LINE OF et_entityset.

    tratar_key_entityset( EXPORTING io_tech_request_context = io_tech_request_context
                          IMPORTING wp_entity               = wl_entity ).

    et_entityset = estaciones_getlist( vp_werks         = wl_entity-werks
                                       vp_lgpla_maquina = wl_entity-lgpla_maquina ).

    zcl_seis_odata_utils=>tratar_entityset( EXPORTING io_tech_request_context  = io_tech_request_context
                                            CHANGING et_entityset = et_entityset ).

  ENDMETHOD.
  METHOD estaciones_getdetail.
    DATA(wl_centro) = centros_getdetail( vp_werks ).

    wp_estacion-werks = vp_werks.
    wp_estacion-lgpla = vp_lgpla.

    DATA(vl_len) = strlen( vp_lgpla ).
    SUBTRACT 1 FROM vl_len.
    wp_estacion-lgpla_maquina = vp_lgpla(vl_len).

    "Mantenimento en transaccion STVARV
    SELECT SINGLE high
      INTO wp_estacion-name1
      FROM tvarvc
      WHERE name  = 'ZWM_NOMBRE_MAQUINA' AND
            low   = wp_estacion-lgpla.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'No se ha encontrado la máquina'(001) ).
    ENDIF.

    SELECT SINGLE low
      INTO @DATA(vl_low)
      FROM tvarvc
      WHERE name  = 'ZWM_MAQUINAS_GESTION_PALETS' AND
            low   = @wp_estacion-lgpla.
    IF sy-subrc = 0.
      wp_estacion-gestiona_palets = 'X'.
    ENDIF.

    SELECT SINGLE low
      INTO @vl_low
      FROM tvarvc
      WHERE name  = 'ZWM_UI5_PERMITE_ALTA_UA' AND
            low   = @wp_estacion-lgpla.
    IF sy-subrc = 0.
      wp_estacion-permite_alta_ua = 'X'.
    ENDIF.

    SELECT SINGLE high
      INTO @DATA(vl_high)
      FROM tvarvc
      WHERE name  = 'ZWM_CONSUMO_NUM_BOBINAS' AND
            low   = @wp_estacion-lgpla.
    IF sy-subrc <> 0.
      vl_high = 2.
    ENDIF.
    wp_estacion-consumo_num_bobinas = vl_high.


    SELECT SINGLE @abap_true
      INTO @wp_estacion-consumo_zpih
      FROM tvarvc
      WHERE name = 'ZWM_UI5_CONSUMO_ZPIH'
        AND low = @wp_estacion-lgpla.

    SELECT SINGLE @abap_true
      INTO @wp_estacion-parte_rebobinado
      FROM tvarvc
      WHERE name  = 'ZWM_MAQUINAS_PARTE_REBOB' AND
            low   = @wp_estacion-lgpla.

    SELECT SINGLE @abap_true
      INTO @wp_estacion-cla_sin_agrup
      FROM tvarvc
      WHERE name  = 'ZWM_MAQUINAS_APRO_SIN_AGRUP' AND
            low   = @wp_estacion-lgpla.



    wp_estacion-meins = SWITCH meins( wp_estacion-gestiona_palets WHEN 'X' THEN 'ST' ELSE 'M' ).


    SELECT SINGLE imagen, cla_indicar_consumido
      INTO CORRESPONDING FIELDS OF @wp_estacion
      FROM zwm_param_estac
      WHERE lgnum           = @wl_centro-lgnum AND
            lgpla_estacion  = @vp_lgpla.


    SELECT SINGLE cla_consumo_con_traza
      INTO @wp_estacion-cla_consumo_con_traza
      FROM zwm_param_maq
      WHERE lgnum  = @wl_centro-lgnum AND
            lgpla  = @wp_estacion-lgpla_maquina.


    SELECT SINGLE grupo
      INTO @DATA(vl_grupo)
      FROM zwm_param_grmaqd
      WHERE lgnum = @wl_centro-lgnum AND
            lgpla = @vp_lgpla.
    IF sy-subrc = 0.
      SELECT SINGLE no_imprimir_al_consumir
        INTO @wp_estacion-no_imprimir_al_consumir
        FROM zwm_param_grmaq
        WHERE lgnum = @wl_centro-lgnum AND
              grupo = @vl_grupo.
    ENDIF.



  ENDMETHOD.
  METHOD estaciones_getlist.


    DATA(wl_centro) = centros_getdetail( vp_werks ).

    SELECT @vp_werks AS werks, lgpla_estacion AS lgpla, lgpla AS lgpla_maquina, nombre as name1, imagen
      INTO CORRESPONDING FIELDS OF TABLE @tp_estaciones
      FROM zwm_param_estac
      WHERE lgnum = @wl_centro-lgnum AND
            lgpla = @vp_lgpla_maquina.

*    LOOP AT tp_estaciones ASSIGNING FIELD-SYMBOL(<fs_estacion>).
*      <fs_estacion> = estaciones_getdetail( vp_werks = <fs_estacion>-werks
*                                            vp_lgpla = <fs_estacion>-lgpla ).
*    ENDLOOP.



  ENDMETHOD.
  METHOD get_bwlvs_ot.

    DATA(wl_tbnum_code) =  zcl_wm_nt_generic=>decode_line_id( iv_id         = wp_bobina-id_bob_ecm
                                                              iv_tbnum_mode = 'X' ).

    vp_bwlvs = '920'.
    SELECT SINGLE matnr
      INTO @DATA(vl_matnr)
      FROM lqua
      WHERE lenum = @wp_bobina-bobine AND
            lgnum = @wp_bobina-lgnum.

    SELECT SINGLE matnr, werks
      INTO @DATA(wl_ltbp)
      FROM ltbp
      WHERE lgnum = @wl_tbnum_code-lgnum AND
            tbnum = @wl_tbnum_code-tbnum.

    " matnr bobine = matnr nt -> 919
    IF vl_matnr = wl_ltbp-matnr.
      vp_bwlvs = '919'.

    ELSE.
      " check if substitute or compatible
      zcl_wm_nt_generic=>get_mat_subs_comp(
        EXPORTING
          iv_matnr  = wl_ltbp-matnr
          iv_werks  = wl_ltbp-werks
        IMPORTING
          et_subst  = DATA(tl_subst)
          et_compat = DATA(tl_compat) ).

      " compatible -> 922
      READ TABLE tl_compat TRANSPORTING NO FIELDS WITH KEY matnr = vl_matnr.
      IF sy-subrc = 0.
        vp_bwlvs = '922'.
      ENDIF.
    ENDIF.




  ENDMETHOD.
  METHOD get_centros_logo.


    DATA: filename TYPE string,
          buffer   TYPE xstring,
          mlen     TYPE i,
          alen     TYPE i.

    filename = |/logos/{ vp_werks }.jpg|.

    OPEN DATASET filename FOR INPUT IN BINARY MODE.
    IF sy-subrc = 0.
      mlen = 1024.
      alen = 9999.
      WHILE alen <> 0.
        READ DATASET filename INTO buffer MAXIMUM LENGTH mlen ACTUAL LENGTH alen.
        IF sy-subrc = 0.
          CONCATENATE vp_logo buffer INTO vp_logo IN BYTE MODE.
        ENDIF.
      ENDWHILE.
      CLOSE DATASET filename.
    ELSE.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al abrir fichero' ).
    ENDIF.




  ENDMETHOD.
  METHOD get_datos_conexion.

    wp_datos_conexion-sysid = sy-sysid.
    wp_datos_conexion-uname = sy-uname.

    "Miramos los datos de intefase Nemesis
    SELECT *
      INTO TABLE @DATA(tl_ztwm001)
      FROM ztwm001
      WHERE cprog  =  'INTF_NEMESIS' AND
            param1 IN ('WERKS', 'REFRESH_INTERFASE').
    READ TABLE tl_ztwm001 ASSIGNING FIELD-SYMBOL(<fs_ztwm001>) WITH KEY param1 = 'WERKS'.
    IF sy-subrc = 0 AND <fs_ztwm001>-valor1 = vp_werks.
      READ TABLE tl_ztwm001 ASSIGNING <fs_ztwm001> WITH KEY param1 = 'REFRESH_INTERFASE'
                                                            param2 = 'USER'
                                                            param3 = wp_datos_conexion-uname.
      IF sy-subrc = 0.
        wp_datos_conexion-permiso_recargar_interfaz = 'X'.
      ENDIF.

      READ TABLE tl_ztwm001 ASSIGNING <fs_ztwm001> WITH KEY param1 = 'REFRESH_INTERFASE'
                                                            param2 = 'TIMESTAMP'.
      IF sy-subrc = 0.
        DATA(vl_timestamp_actual) = sy-datum && sy-uzeit.
        IF vl_timestamp_actual < <fs_ztwm001>-valor1.
          wp_datos_conexion-proteger_recarga_interfaz = 'X'.
        ENDIF.
      ENDIF.
    ENDIF.

    SELECT SINGLE low
      INTO @DATA(vl_listadoaprv_graph_max)
      FROM tvarvc
      WHERE name = 'ZUI5_LISTADOAPRV_GRAPH_MAX'.
    IF sy-subrc = 0.
      wp_datos_conexion-listado_aprv_graph_max = vl_listadoaprv_graph_max.
    ENDIF.

    IF vp_werks IS NOT INITIAL.
      DATA(wl_centro) = centros_getdetail( vp_werks ).
      wp_datos_conexion-habilitar_login = wl_centro-habilitar_login.
    ENDIF.


  ENDMETHOD.
  method GET_RANGO_TIPOS_MAT_PALET.

    SELECT sign, opti AS option, low, high
      INTO CORRESPONDING FIELDS OF TABLE @tp_rango
      FROM tvarvc
      WHERE name = 'ZWM_TIPO_MAT_PALLET'.

  endmethod.
  METHOD grupos_maquinas_getdetail.

    DATA(wl_centro) = centros_getdetail( vp_werks ).

    SELECT SINGLE *
      INTO CORRESPONDING FIELDS OF wp_grupo_maquinas
      FROM zwm_param_grmaq
      WHERE lgnum = wl_centro-lgnum AND
            grupo = vp_grupo.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Grupo de máquinas incorrecto' ).
    ENDIF.

    wp_grupo_maquinas-werks = wl_centro-werks.

  ENDMETHOD.
  method GRUPOSMAQUINAS_GET_ENTITY.
    tratar_key_entity( EXPORTING io_tech_request_context = io_tech_request_context
                       IMPORTING wp_entity               = er_entity ).
    er_entity = grupos_maquinas_getdetail( vp_grupo = er_entity-grupo
                                           vp_werks = er_entity-werks ).
  endmethod.
  METHOD imprimir_ua.
    DATA(wl_centro) = centros_getdetail( vp_werks ).

    "Impresión
    "Si viene de carretilla, utilizar otra clave para impresión
    IF vp_cla_aprov = space.
      DATA(vl_cod_maq_portabo) = CONV zwm_param-maquina( vp_lgpla ).
    ELSE.
      IF wl_centro-aprov_imprimir_maquina IS NOT INITIAL.
        vl_cod_maq_portabo = wl_centro-aprov_imprimir_maquina.
      ELSE.
        zcl_seis_odata_utils=>lanzar_excepcion( 'Debe parametrizar máquina de impresión para aprovisionamiento' ).
      ENDIF.
    ENDIF.


    SELECT SINGLE @abap_true
      INTO @DATA(vl_existe)
      FROM ltap
      WHERE lgnum = @wl_centro-lgnum AND
            nlenr = @vp_ua.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'No se ha encontrado la UA' ).
    ENDIF.

    SELECT SINGLE @abap_true
      INTO @vl_existe
      FROM lqua
      WHERE lgnum = @wl_centro-lgnum AND
            lenum = @vp_ua.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'No se ha encontrado la UA' ).
    ENDIF.




    "Impresión
    DATA tl_selection_table TYPE TABLE OF rsparamsl_255.
    EXPORT lv_vengo_inter_consumo = abap_true           TO MEMORY ID 'VENGO_CONSUMO' .
    EXPORT lv_cod_maq_portabo     = vl_cod_maq_portabo  TO MEMORY ID 'MAQUINA' .

    " create label for remaining
    tl_selection_table = VALUE #( ( selname = 'P_LGNUM' kind = 'P' sign = 'I' option = 'EQ' low = wl_centro-lgnum )
                                  ( selname = 'S_LENUM' kind = 'S' sign = 'I' option = 'EQ' low = vp_ua ) ).
    SUBMIT zimpresion_ua WITH SELECTION-TABLE tl_selection_table AND RETURN.
    FREE MEMORY ID: 'VENGO_CONSUMO', 'MAQUINA'.

  ENDMETHOD.
  method MAQUINAS_GET_ENTITY.
    tratar_key_entity( EXPORTING io_tech_request_context = io_tech_request_context
                       IMPORTING wp_entity               = er_entity ).
    er_entity = maquinas_getdetail( vp_lgpla = er_entity-lgpla
                                    vp_werks = er_entity-werks ).
  endmethod.
  method MAQUINAS_GET_ENTITYSET.
    DATA wl_entity LIKE LINE OF et_entityset.

    data: wl_key type ZUI5_WM_S_GRUPOS_MAQUINAS.
    tratar_key_entityset( EXPORTING io_tech_request_context = io_tech_request_context
                          IMPORTING wp_entity               = wl_key ).

    et_entityset =  maquinas_getlist( vp_werks = wl_key-werks
                                      vp_grupo = wl_key-grupo ).

    zcl_seis_odata_utils=>tratar_entityset( EXPORTING io_tech_request_context  = io_tech_request_context
                                            CHANGING et_entityset = et_entityset ).
  endmethod.
  METHOD maquinas_getdetail.

    DATA(wl_centro) = centros_getdetail( vp_werks ).

    SELECT SINGLE @vp_werks AS werks, lgpla, nombre AS name1, imagen
      INTO CORRESPONDING FIELDS OF @wp_maquina
      FROM zwm_param_maq
      WHERE lgnum = @wl_centro-lgnum AND
            lgpla = @vp_lgpla.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Máquina incorrecta' ).
    ENDIF.


  ENDMETHOD.
  METHOD maquinas_getlist.
    DATA(wl_centro) = centros_getdetail( vp_werks ).

    DATA: tl_lgpla_rg TYPE RANGE OF lgpla.
    IF vp_grupo IS NOT INITIAL.
      SELECT *
        INTO TABLE @DATA(tl_param_grmaqd)
        FROM zwm_param_grmaqd
        WHERE lgnum = @wl_centro-lgnum AND
              grupo = @vp_grupo.
      CHECK sy-subrc = 0.

      LOOP AT tl_param_grmaqd ASSIGNING FIELD-SYMBOL(<fs_param_grmaqd>).
        APPEND INITIAL LINE TO tl_lgpla_rg ASSIGNING FIELD-SYMBOL(<fs_lgpla_rg>).
        <fs_lgpla_rg>-sign = 'I'. <fs_lgpla_rg>-option = 'EQ'. <fs_lgpla_rg>-low = <fs_param_grmaqd>-lgpla.
      ENDLOOP.
    ENDIF.

    SELECT @vp_werks AS werks, lgpla, nombre AS name1, imagen
      INTO CORRESPONDING FIELDS OF TABLE @tp_maquinas
      FROM zwm_param_maq
      WHERE lgnum =  @wl_centro-lgnum AND
            lgpla IN @tl_lgpla_rg.
    LOOP AT tp_maquinas ASSIGNING FIELD-SYMBOL(<fs_maquinas>).
      READ TABLE tl_param_grmaqd ASSIGNING <fs_param_grmaqd> WITH KEY lgpla = <fs_maquinas>-lgpla.
      IF sy-subrc = 0.
        <fs_maquinas>-num_orden = <fs_param_grmaqd>-num_orden.
      ENDIF.
    ENDLOOP.

    SORT tp_maquinas BY num_orden lgpla.

  ENDMETHOD.
  METHOD mover_bobina.
    DATA(wl_centro) = centros_getdetail( vp_werks ).

    zcl_wm_nt_generic=>movebobine(
      EXPORTING
        iv_bobine               = vp_bobina
        iv_dest_nlpla           = vp_lgpla
        iv_lgnum                = wl_centro-lgnum
        iv_bwlvs                = zcl_wm_nt_generic=>gc_move_type_918
        iv_validar_fsc_recycled = space "Validación FSC Recycled sólo para el carretillero
      IMPORTING
        es_ltak       = wp_ltak
        ev_subrc      = DATA(vl_subrc) ).
    IF vl_subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al mover bobina' ).
    ENDIF.



  ENDMETHOD.
  method PARTESREBOBBOBPR_CREATE_ENTITY.
    DATA(rl_wm_nt) = NEW zcl_zui5_wm_nt_dpc_ext( ).
    io_data_provider->read_entry_data( IMPORTING es_data = er_entity ).
    rl_wm_nt->parte_rebob_bob_prod_create( CHANGING wp_parte_rebob_bob_prod = er_entity ).
  endmethod.
  method PARTESREBOBBOBPR_DELETE_ENTITY.
    DATA(rl_wm_nt) = NEW zcl_zui5_wm_nt_dpc_ext( ).
    DATA: wl_entity TYPE zwm_nt_ui5_s_parte_rebob_bob_p.
    io_tech_request_context->get_converted_keys( IMPORTING es_key_values  = wl_entity ).
    rl_wm_nt->parte_rebob_bob_prod_delete( wl_entity ).
  endmethod.
  method PARTESREBOBBOBPR_GET_ENTITY.
    DATA(rl_wm_nt) = NEW zcl_zui5_wm_nt_dpc_ext( ).
    tratar_key_entity( EXPORTING io_tech_request_context = io_tech_request_context
                       IMPORTING wp_entity               = er_entity ).
    er_entity = rl_wm_nt->parte_rebob_bob_prod_getdetail( vp_werks           = er_entity-werks
                                                          vp_id_maquina      = er_entity-id_maquina
                                                          vp_cod_parte_rebob = er_entity-cod_parte_rebob
                                                          vp_contador        = er_entity-contador ).
  endmethod.
  method PARTESREBOBBOBPR_GET_ENTITYSET.
      DATA(rl_wm_nt) = NEW zcl_zui5_wm_nt_dpc_ext( ).
  DATA wl_entity LIKE LINE OF et_entityset.
    tratar_key_entityset( EXPORTING io_tech_request_context = io_tech_request_context
                          IMPORTING wp_entity               = wl_entity ).

    et_entityset =  rl_wm_nt->parte_rebob_bob_prod_getlist( vp_cod_parte_rebob = wl_entity-cod_parte_rebob
                                                            vp_id_maquina      = wl_entity-id_maquina
                                                            vp_werks           = wl_entity-werks ).

    zcl_seis_odata_utils=>tratar_entityset( EXPORTING io_tech_request_context  = io_tech_request_context
                                            CHANGING et_entityset = et_entityset ).


  endmethod.
  method PARTESREBOBBOBPR_UPDATE_ENTITY.
    DATA(rl_wm_nt) = NEW zcl_zui5_wm_nt_dpc_ext( ).
    io_data_provider->read_entry_data( IMPORTING es_data = er_entity ).
    rl_wm_nt->parte_rebob_bob_prod_update( CHANGING wp_parte_rebob_bob_prod = er_entity ).
  endmethod.
  method PARTESREBOBCAB_CREATE_ENTITY.
    DATA(rl_wm_nt) = NEW zcl_zui5_wm_nt_dpc_ext( ).
    io_data_provider->read_entry_data( IMPORTING es_data = er_entity ).
    rl_wm_nt->parte_rebob_cab_create( CHANGING wp_parte_rebob_Cab = er_entity ).
  endmethod.
  method PARTESREBOBCAB_DELETE_ENTITY.
    DATA(rl_wm_nt) = NEW zcl_zui5_wm_nt_dpc_ext( ).
    DATA: wl_entity TYPE zwm_nt_ui5_s_parte_rebob_cab.
    io_tech_request_context->get_converted_keys( IMPORTING es_key_values  = wl_entity ).
    rl_wm_nt->parte_rebob_cab_delete( wl_entity ).
  endmethod.
  method PARTESREBOBCAB_GET_ENTITY.
    DATA(rl_wm_nt) = NEW zcl_zui5_wm_nt_dpc_ext( ).
    tratar_key_entity( EXPORTING io_tech_request_context = io_tech_request_context
                       IMPORTING wp_entity               = er_entity ).
    er_entity = rl_wm_nt->parte_rebob_cab_getdetail( vp_werks           = er_entity-werks
                                                     vp_id_maquina      = er_entity-id_maquina
                                                     vp_cod_parte_rebob = er_entity-cod_parte_rebob ).
  endmethod.
  method PARTESREBOBCAB_GET_ENTITYSET.
    DATA(rl_wm_nt) = NEW zcl_zui5_wm_nt_dpc_ext( ).
    DATA wl_entity LIKE LINE OF et_entityset.
    tratar_key_entityset( EXPORTING io_tech_request_context = io_tech_request_context
                          IMPORTING wp_entity               = wl_entity ).

    et_entityset =  rl_wm_nt->parte_rebob_cab_getlist( vp_id_maquina = wl_entity-id_maquina
                                                       vp_werks      = wl_entity-werks ).

    zcl_seis_odata_utils=>tratar_entityset( EXPORTING io_tech_request_context  = io_tech_request_context
                                            CHANGING et_entityset = et_entityset ).

  endmethod.
  method PARTESREBOBCAB_UPDATE_ENTITY.
    DATA(rl_wm_nt) = NEW zcl_zui5_wm_nt_dpc_ext( ).
    io_data_provider->read_entry_data( IMPORTING es_data = er_entity ).
    rl_wm_nt->parte_rebob_cab_update( CHANGING wp_parte_rebob_cab = er_entity ).
  endmethod.
  METHOD pedidos_consumo_getdetail.

    DATA(tl_pedidos) = pedidos_consumo_getlist( vp_werks   = vp_werks
                                                vp_lgpla   = vp_lgpla ).
    READ TABLE tl_pedidos INTO wp_pedido WITH KEY id = vp_id.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'No se ha encontrado el pedido' ).
    ENDIF.
  ENDMETHOD.
  METHOD pedidos_consumo_getlist.
    DATA(wl_centro) = centros_getdetail( vp_werks ).

    DATA(wl_estacion) = estaciones_getdetail( vp_werks = vp_werks
                                              vp_lgpla = vp_lgpla ).


    IF wl_centro-lista_apro_ui5_pp = 'X'.
      tp_pedidos = pedidos_consumo_pp_getlist( vp_werks = vp_werks
                                               vp_lgpla = vp_lgpla ).
    ELSE.
      DATA(tl_lista_pedidos) = zcl_wm_nt_generic=>get_instance( )->build_bobecm_list_ui5_v2( iv_lgnum = wl_centro-lgnum
                                                                                             iv_nlpla = vp_lgpla ).
      CHECK tl_lista_pedidos IS NOT INITIAL.
      tp_pedidos = CORRESPONDING #( tl_lista_pedidos ).
    ENDIF.
    CHECK tp_pedidos IS NOT INITIAL.



    DATA tl_rsnum_rg TYPE RANGE OF rsnum.
    DATA wl_rsnum_rg LIKE LINE OF tl_rsnum_rg.

    SELECT *
      INTO TABLE @DATA(tl_ecm_bobine)
      FROM zwm_ecm_bobine
      FOR ALL ENTRIES IN @tp_pedidos
      WHERE id_bob_ecm  = @tp_pedidos-id AND
            saved       = @space.

    IF wl_estacion-parte_rebobinado = 'X' AND  tp_pedidos IS NOT INITIAL.
      LOOP AT tp_pedidos ASSIGNING FIELD-SYMBOL(<fs_pedidos>).
        wl_rsnum_rg-sign = 'I'. wl_rsnum_rg-option = 'EQ'. wl_rsnum_rg-low = <fs_pedidos>-id.
        IF wl_rsnum_rg-low IS NOT INITIAL.
          COLLECT wl_rsnum_rg INTO tl_rsnum_rg.
        ENDIF.
      ENDLOOP.

      SELECT werks, id_maquina, cod_parte_rebob, rsnum
        INTO TABLE @DATA(tl_parte_rebob_c)
        FROM zwm_parte_reb_c
        WHERE werks       = @vp_werks AND
              id_maquina  = @vp_lgpla AND
              rsnum       IN @tl_rsnum_rg.
    ENDIF.

    LOOP AT tp_pedidos ASSIGNING <fs_pedidos>.
      <fs_pedidos>-werks = vp_werks.
      <fs_pedidos>-lgpla = vp_lgpla.
      IF <fs_pedidos>-editable = 'X'.
        READ TABLE tl_ecm_bobine ASSIGNING FIELD-SYMBOL(<fs_ecm_bobine>) WITH KEY id_bob_ecm = <fs_pedidos>-id.
        IF sy-subrc = 0.
          <fs_pedidos>-ultimo_asignado = 'X'.
        ENDIF.
      ENDIF.

      IF wl_estacion-parte_rebobinado = 'X'.
        READ TABLE tl_parte_rebob_c ASSIGNING FIELD-SYMBOL(<fs_parte_rebob_c>) WITH KEY rsnum = <fs_pedidos>-id.
        IF sy-subrc = 0.
          DATA(wl_parte_rebob) = rg_wm_nt->parte_rebob_cab_getdetail( vp_cod_parte_rebob  = <fs_parte_rebob_c>-cod_parte_rebob
                                                                      vp_id_maquina       = <fs_parte_rebob_c>-id_maquina
                                                                      vp_werks            = <fs_parte_rebob_c>-werks ).
          IF wl_parte_rebob-cla_cerrado = 'X'.
            DELETE tp_pedidos. CONTINUE.
          ENDIF.

          <fs_pedidos>-cod_parte_rebob = wl_parte_rebob-cod_parte_rebob.
          <fs_pedidos>-matnr           = wl_parte_rebob-matnr.
          <fs_pedidos>-maktx           = wl_parte_rebob-maktx.
          <fs_pedidos>-bobina_cons     = wl_parte_rebob-bobina_cons.

          <fs_pedidos>-prioridad  = wl_parte_rebob-prioridad.
          <fs_pedidos>-num_orden  = wl_parte_rebob-num_orden.
        ELSE.
          DELETE tp_pedidos. CONTINUE.
        ENDIF.
      ENDIF.
    ENDLOOP.

    IF wl_estacion-parte_rebobinado = 'X'.
      SORT tp_pedidos BY editable prioridad num_orden.
    ENDIF.



    IF tp_pedidos IS NOT INITIAL.
      SELECT mara~matnr, mara~matkl, t023t~wgbez60
        INTO TABLE @DATA(tl_mara)
        FROM mara LEFT OUTER JOIN t023t ON  t023t~spras = @sy-langu AND
                                            t023t~matkl = mara~matkl
        FOR ALL ENTRIES IN @tp_pedidos
        WHERE mara~matnr = @tp_pedidos-matnr.
    ENDIF.

    LOOP AT tp_pedidos ASSIGNING <fs_pedidos>.
      READ TABLE tl_mara ASSIGNING FIELD-SYMBOL(<fs_mara>) WITH KEY matnr = <fs_pedidos>-matnr.
      IF sy-subrc = 0.
        <fs_pedidos>-wgbez60 = <fs_mara>-wgbez60.
      ENDIF.
    ENDLOOP.


  ENDMETHOD.
  METHOD pedidos_consumo_pp_getlist.
    DATA wl_pedido LIKE LINE OF tp_pedidos.

    DATA(rl_nt) = NEW zcl_zui5_wm_nt_dpc_ext( ).

    DATA(wl_centro) = centros_getdetail( vp_werks  ).
    DATA(wl_estacion) = zcl_wm_nt_generic=>get_instance( )->maquinas_getdetail( vp_machine_id = vp_lgpla ).


    "Buscamos la lista de aprovisionamiento de la ubicación superior
    DATA(tl_lista_apro) = rl_nt->lista_aprov_pp_getlist( vp_werks = vp_werks
                                                         vp_lgpla = vp_lgpla
                                                         vp_margen_sel_ordenes = wl_centro-consumos_margen_sel_ordenes ).

    DATA: tl_pedidos              LIKE tp_pedidos,
          tl_pedidos_no_editables LIKE tp_pedidos.

    DATA vl_matnr_ant TYPE matnr.
    FIELD-SYMBOLS: <fs_pedido_ant> LIKE LINE OF tp_pedidos.

    IF tl_lista_apro IS NOT INITIAL.
      SELECT rsnum, matnr, sortf, enmng, meins
        INTO TABLE @DATA(tl_resb)
        FROM resb
        FOR ALL ENTRIES IN @tl_lista_apro
        WHERE rsnum  = @tl_lista_apro-nlpla1_rsnum AND
              matnr  = @tl_lista_apro-nlpla1_matnr AND
              sortf  = @vp_lgpla.
    ENDIF.


    DATA: vl_lgnum     TYPE lgnum,
          vl_ci_number TYPE zsppt_cinumbers-ci_number.

    LOOP AT tl_lista_apro ASSIGNING FIELD-SYMBOL(<fs_lista_apro>).
      CLEAR wl_pedido.
      DATA(vl_idx) = 1.
      ASSIGN COMPONENT |NLPLA{ vl_idx }| OF STRUCTURE <fs_lista_apro> TO FIELD-SYMBOL(<fs_nlpla>).
      ASSIGN COMPONENT |NLPLA{ vl_idx }_MAT_ID|     OF STRUCTURE <fs_lista_apro> TO FIELD-SYMBOL(<fs_mat_id>).
      ASSIGN COMPONENT |NLPLA{ vl_idx }_MATNR|      OF STRUCTURE <fs_lista_apro> TO FIELD-SYMBOL(<fs_matnr>).
      ASSIGN COMPONENT |NLPLA{ vl_idx }_MAKTX|      OF STRUCTURE <fs_lista_apro> TO FIELD-SYMBOL(<fs_maktx>).
      ASSIGN COMPONENT |NLPLA{ vl_idx }_TBNUM|      OF STRUCTURE <fs_lista_apro> TO FIELD-SYMBOL(<fs_tbnum>).
      ASSIGN COMPONENT |NLPLA{ vl_idx }_RSNUM|      OF STRUCTURE <fs_lista_apro> TO FIELD-SYMBOL(<fs_rsnum>).
      ASSIGN COMPONENT |NLPLA{ vl_idx }_MENGE|      OF STRUCTURE <fs_lista_apro> TO FIELD-SYMBOL(<fs_menge>).
      ASSIGN COMPONENT |NLPLA{ vl_idx }_MENGE_ACUM| OF STRUCTURE <fs_lista_apro> TO FIELD-SYMBOL(<fs_menge_acum>).
      IF <fs_matnr> IS INITIAL.
        UNASSIGN <fs_pedido_ant>. CONTINUE.
      ENDIF.

      SPLIT <fs_lista_apro>-id AT '-' INTO vl_lgnum vl_ci_number.


      wl_pedido-ci_number = vl_ci_number.
      wl_pedido-id        = <fs_lista_apro>-nlpla1_rsnum.
      wl_pedido-width     = <fs_lista_apro>-width.
      wl_pedido-ztrim     = <fs_lista_apro>-ztrim.
      wl_pedido-quality   = <fs_lista_apro>-quality.
      wl_pedido-canal     = <fs_lista_apro>-canal.
      wl_pedido-length    = SWITCH #( wl_estacion-listado_apro_sin_agrup WHEN space THEN <fs_menge_acum> ELSE <fs_menge> ).
      wl_pedido-meins     = <fs_lista_apro>-meins.
      wl_pedido-pedido2   = wl_pedido-pedido  = <fs_lista_apro>-pedido.
      wl_pedido-nlpla     = <fs_nlpla>.
      wl_pedido-mat_id    = <fs_mat_id>.
      wl_pedido-matnr     = <fs_matnr>.
      wl_pedido-maktx     = <fs_maktx>.
      wl_pedido-tbnum     = <fs_tbnum>.
      wl_pedido-lgpla     = vp_lgpla.
      wl_pedido-werks     = vp_werks.
      wl_pedido-texto     = <fs_lista_apro>-texto.

      READ TABLE tl_resb ASSIGNING FIELD-SYMBOL(<fs_resb>) WITH KEY rsnum = <fs_rsnum>
                                                                    matnr = <fs_matnr>.
      IF sy-subrc = 0.
        CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
          EXPORTING
            i_matnr              = <fs_resb>-matnr
            i_in_me              = <fs_resb>-meins
            i_out_me             = <fs_lista_apro>-meins
            i_menge              = <fs_resb>-enmng
          IMPORTING
            e_menge              = <fs_resb>-enmng
          EXCEPTIONS
            error_in_application = 1
            error                = 2
            OTHERS               = 3.
        IF sy-subrc <> 0.
          CLEAR <fs_resb>-enmng.
        ENDIF.


        SUBTRACT <fs_resb>-enmng FROM wl_pedido-length.
        IF wl_pedido-length < 0. wl_pedido-length = 0. ENDIF.
        wl_pedido-editable  = COND #( WHEN wl_pedido-length >= 10 THEN 'X' ELSE space ).

        "Vaciamos la cantidad tomada para no volverla a usar
        CLEAR <fs_resb>-enmng.
      ENDIF.

      IF <fs_pedido_ant> IS ASSIGNED AND <fs_pedido_ant>-matnr = wl_pedido-matnr AND <fs_pedido_ant>-canal = wl_pedido-canal AND
         wl_estacion-listado_apro_sin_agrup = space.
        IF <fs_pedido_ant>-pedido2 NP '*#+'.
          <fs_pedido_ant>-pedido2 = |{ <fs_pedido_ant>-pedido2 }+|.
        ENDIF.
        IF <fs_pedido_ant>-quality <> wl_pedido-quality AND <fs_pedido_ant>-quality <> 'Varias'.
          <fs_pedido_ant>-quality = 'Varias'.
        ENDIF.

      ELSE.
        APPEND wl_pedido TO tl_pedidos ASSIGNING <fs_pedido_ant>.
      ENDIF.
    ENDLOOP.


    "De los no editables dejamos los dos últimos
    LOOP AT tl_pedidos ASSIGNING FIELD-SYMBOL(<fs_pedido>) WHERE editable = space.
      DATA(vl_tabix) = sy-tabix.
      IF lines( tl_pedidos_no_editables ) = 2.
        DELETE  tl_pedidos_no_editables INDEX 1.
      ENDIF.
      APPEND <fs_pedido> TO tl_pedidos_no_editables.
      DELETE tl_pedidos INDEX vl_tabix.
    ENDLOOP.

    APPEND LINES OF tl_pedidos_no_editables TO tp_pedidos.
    APPEND LINES OF tl_pedidos              TO tp_pedidos.



  ENDMETHOD.
  method PEDIDOSCONSUMO_GET_ENTITY.
    tratar_key_entity( EXPORTING io_tech_request_context = io_tech_request_context
                       IMPORTING wp_entity               = er_entity ).
    er_entity = pedidos_consumo_getdetail(  vp_werks = er_entity-werks
                                            vp_lgpla = er_entity-lgpla
                                            vp_id    = er_entity-id ).

  endmethod.
  METHOD pedidosconsumo_get_entityset.
    DATA wl_entity LIKE LINE OF et_entityset.
    tratar_key_entityset( EXPORTING io_tech_request_context = io_tech_request_context
                          IMPORTING wp_entity               = wl_entity ).

    et_entityset =  pedidos_consumo_getlist( vp_werks = wl_entity-werks
                                             vp_lgpla = wl_entity-lgpla ).

    zcl_seis_odata_utils=>tratar_entityset( EXPORTING io_tech_request_context  = io_tech_request_context
                                            CHANGING et_entityset = et_entityset ).
  ENDMETHOD.
  METHOD refresh_interfase_nemesis.
    DATA: client        TYPE REF TO if_http_client,
          response_data TYPE string.


    DATA(wl_datos_conexion) = get_datos_conexion( vp_werks ).
    IF wl_datos_conexion-permiso_recargar_interfaz = space.
      zcl_seis_odata_utils=>lanzar_excepcion( 'No tiene permiso para recargar la interfaz Nemesis' ).
    ENDIF.

    DATA vl_authorization TYPE string.
    SELECT SINGLE valor1
      INTO @vl_authorization
      FROM ztwm001
      WHERE cprog  = 'INTF_NEMESIS'       AND
            param1 = 'REFRESH_INTERFASE'  AND
            param2 = 'USER'               AND
            param3 = @wl_datos_conexion-uname.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'No tiene permiso para recargar la interfaz Nemesis' ).
    ENDIF.

    "Comprobamos el timer
    DATA(vl_timestamp_actual) = sy-datum && sy-uzeit.
    SELECT SINGLE valor1
      INTO @DATA(vl_timestamp)
      FROM ztwm001
      WHERE cprog  = 'INTF_NEMESIS'       AND
            param1 = 'REFRESH_INTERFASE'  AND
            param2 = 'TIMESTAMP'.
    IF vl_timestamp_actual < vl_timestamp.
      zcl_seis_odata_utils=>lanzar_excepcion( 'La interfaz no está activa. Pruebe dentro de unos minutos' ).
    ENDIF.


    DATA(wl_centro) = centros_getdetail( vp_werks ).

    "Arrancar el canal
    cl_http_client=>create_by_url( EXPORTING url    = CONV #( wl_centro-url_interfase_1 )
                                   IMPORTING client = client
                                   EXCEPTIONS argument_not_found = 1
                                              plugin_not_active  = 2
                                              internal_error     = 3
                                              OTHERS             = 4 ).
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al generar llamada' ).
    ENDIF.

    client->request->set_header_field(  name  = 'Authorization'
                                        value = vl_authorization ).

    client->send( EXCEPTIONS  http_communication_failure  = 1
                              http_invalid_state          = 2
                              http_processing_failed      = 3
                              http_invalid_timeout        = 4    ).
    IF sy-subrc <> 0.
      client->close( ).
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al enviar petición' ).
    ENDIF.

    client->receive( EXCEPTIONS http_communication_failure  = 1
                                http_invalid_state          = 2
                                http_processing_failed      = 3 ).
    IF sy-subrc <> 0.
      client->close( ).
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al recibir respuesta' ).
    ENDIF.

    response_data = client->response->get_cdata( ).
    client->get_last_error( IMPORTING code           = DATA(vl_code)
                                      message        = DATA(vl_message)
                                      message_class  = DATA(vl_message_class)
                                      message_number = DATA(vl_message_number) ).


    client->close( ).
    IF vl_message IS NOT INITIAL.
      zcl_seis_odata_utils=>lanzar_excepcion( vl_message ).
    ENDIF.



    WAIT UP TO 5 SECONDS.


    "Parar el canal
    cl_http_client=>create_by_url( EXPORTING url    = CONV #( wl_centro-url_interfase_2 )
                                   IMPORTING client = client
                                   EXCEPTIONS argument_not_found = 1
                                              plugin_not_active  = 2
                                              internal_error     = 3
                                              OTHERS             = 4 ).
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al generar llamada' ).
    ENDIF.


    client->request->set_header_field(  name  = 'Authorization'
                                        value = vl_authorization ).
    client->send( EXCEPTIONS  http_communication_failure  = 1
                              http_invalid_state          = 2
                              http_processing_failed      = 3
                              http_invalid_timeout        = 4    ).
    IF sy-subrc <> 0.
      client->close( ).
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al enviar petición' ).
    ENDIF.

    client->receive( EXCEPTIONS http_communication_failure  = 1
                                http_invalid_state          = 2
                                http_processing_failed      = 3 ).
    IF sy-subrc <> 0.
      client->close( ).
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al recibir respuesta' ).
    ENDIF.

    response_data = client->response->get_cdata( ).
    client->get_last_error( IMPORTING message = vl_message ).
    client->close( ).
    IF vl_message IS NOT INITIAL.
      zcl_seis_odata_utils=>lanzar_excepcion( vl_message ).
    ENDIF.

    "Calculamos 5 minutos más para indicar el timestamp
    DATA: vl_enddate TYPE sy-datum,
          vl_endtime TYPE sy-uzeit.
    CALL FUNCTION 'C14B_ADD_TIME'
      EXPORTING
        i_startdate = sy-datum
        i_starttime = sy-uzeit
        i_addtime   = '000500'
      IMPORTING
        e_enddate   = vl_enddate
        e_endtime   = vl_endtime.
    vl_timestamp = vl_enddate && vl_endtime.

    UPDATE ztwm001
      SET valor1 = vl_timestamp
      WHERE cprog  =  'INTF_NEMESIS'      AND
            param1 = 'REFRESH_INTERFASE'  AND
            param2 = 'TIMESTAMP'.




  ENDMETHOD.
  METHOD regularizar_ua.
    CONSTANTS: cl_bhs_bobina TYPE repid   VALUE 'INTF_BHS_BOBINA',
               cl_param1     TYPE zeparam VALUE '01'.
    CONSTANTS: cl_code       TYPE gm_code VALUE '03'. " MB1A - Goods Issue


    DATA(wl_centro) = centros_getdetail( vp_werks  ).
    TRY .
        DATA(wl_estacion) = estaciones_getdetail( vp_werks = vp_werks
                                                  vp_lgpla = vp_lgpla ).
      CATCH /iwbep/cx_mgw_busi_exception.
        CLEAR wl_estacion.
    ENDTRY.


    "Impresión
    DATA(vl_cod_maq_portabo) = CONV zwm_param-maquina( vp_lgpla ).
    "Si viene de carretilla, utilizar otra clave para impresión
    IF vp_cla_aprov = 'X'.
      IF wl_centro-aprov_imprimir_maquina IS NOT INITIAL.
        vl_cod_maq_portabo = wl_centro-aprov_imprimir_maquina.
      ELSE.
        zcl_seis_odata_utils=>lanzar_excepcion( 'Debe parametrizar máquina de impresión para aprovisionamiento' ).
      ENDIF.
    ENDIF.



    SELECT SINGLE lgtyp
      INTO @DATA(vl_lgtyp)
      FROM lagp
      WHERE lgnum = @wl_centro-lgnum AND
            lgpla = @vp_lgpla.

    SELECT  *
      INTO TABLE @DATA(tl_ltap)
      FROM ltap
      UP TO 1 ROWS
      WHERE lgnum = @wl_centro-lgnum AND
            nlenr = @vp_ua
      ORDER BY tanum, tapos.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'No se ha encontrado la UA' ).
    ENDIF.
    READ TABLE tl_ltap ASSIGNING FIELD-SYMBOL(<fs_ltap>) INDEX 1.

    IF <fs_ltap>-brgew EQ 0. " cvivo - si no se ha actualizado peso, manejamos cantidades alternativas
      DATA: l_peso TYPE ekpo-menge.

      CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
        EXPORTING
          i_matnr              = <fs_ltap>-matnr
          i_in_me              = <fs_ltap>-altme
          i_out_me             = <fs_ltap>-gewei
          i_menge              = <fs_ltap>-nista " ctd real hacia
        IMPORTING
          e_menge              = l_peso
        EXCEPTIONS
          error_in_application = 1
          error                = 2
          OTHERS               = 3.

      <fs_ltap>-brgew = CONV #( l_peso ).
    ENDIF.


    SELECT SINGLE *
      INTO @DATA(wl_lqua)
      FROM lqua
      WHERE lgnum = @wl_centro-lgnum AND
            lenum = @vp_ua.
    IF sy-subrc <> 0.
      "Si no se encuentra cuanto tengo que darla de alta, marco destino estación para que la OT lo deje directamente ahí
      wl_lqua-matnr = <fs_ltap>-matnr.
      wl_lqua-werks = <fs_ltap>-werks.
      wl_lqua-lgort = <fs_ltap>-lgort.
      wl_lqua-lgnum = <fs_ltap>-lgnum.
      wl_lqua-charg = <fs_ltap>-charg.
      wl_lqua-lenum = <fs_ltap>-nlenr.
      wl_lqua-letyp = <fs_ltap>-letyp.
      wl_lqua-lgpla = vp_lgpla. " ubicación estación
      wl_lqua-lgtyp = vl_lgtyp. " ubicación estación
      wl_lqua-meins = 'KG'.
    ENDIF.


    "Convertimos cantidades en kilos
    IF vp_meins = zcl_wm_nt_generic=>gc_meins_dia.
      zcl_wm_nt_generic=>conv_mat_diametro(
        EXPORTING iv_matnr          = wl_lqua-matnr
                  iv_qty            = vp_menge
                  iv_meins          = vp_meins
                  iv_dest_meins     = 'KG'
                  iv_werks          = wl_lqua-werks
        RECEIVING rv_qty = vp_menge
        EXCEPTIONS  um_no_valid       = 1
                    missing_constants = 2
                    missing_matnr     = 3
                    no_base_calc      = 4
                    overflow          = 5
                    OTHERS            = 6 ).
      CASE sy-subrc.
        WHEN 0. "Correcto
        WHEN 5. "Overflow
          vp_menge = '9999999999.999'.
        WHEN OTHERS.
          zcl_seis_odata_utils=>lanzar_excepcion( 'Error al convertir diámetro a KG' ).
      ENDCASE.


    ELSE.

      vp_meins = COND #( WHEN vp_meins = 'UN' THEN 'ST' ELSE vp_meins ).

      CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
        EXPORTING
          i_matnr              = wl_lqua-matnr
          i_in_me              = vp_meins
          i_out_me             = 'KG'
          i_menge              = vp_menge
        IMPORTING
          e_menge              = vp_menge
        EXCEPTIONS
          error_in_application = 1
          error                = 2
          OTHERS               = 3.
      IF sy-subrc <> 0.
        zcl_seis_odata_utils=>lanzar_excepcion( 'Error al convertir cantidad a KG' ).
      ENDIF.
    ENDIF.
*    vp_meins = 'KG'.


    "Comprobación cantidad
    DATA(vl_menge) = CONV ekpo-menge( <fs_ltap>-brgew ).
    CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
      EXPORTING
        i_matnr              = wl_lqua-matnr
        i_in_me              = <fs_ltap>-gewei
        i_out_me             = 'KG'
        i_menge              = vl_menge
      IMPORTING
        e_menge              = vl_menge
      EXCEPTIONS
        error_in_application = 1
        error                = 2
        OTHERS               = 3.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( ).
    ENDIF.

    DATA: vl_cantidad_entrada TYPE ekpo-menge,
          vl_unidad_entrada   TYPE meins.
    DATA: vl_cantidad_entrada_c TYPE text20,
          vl_unidad_entrada_c   TYPE text10.

    IF vl_menge < vp_menge. " si la cantidad con la que entró es menor que la que se intenta regularizar, error
      IF vp_meins = zcl_wm_nt_generic=>gc_meins_dia.
        zcl_wm_nt_generic=>conv_mat_diametro(
          EXPORTING iv_matnr          = wl_lqua-matnr
                    iv_qty            = vl_menge
                    iv_meins          = 'KG'
                    iv_dest_meins     = vp_meins
                    iv_werks          = wl_lqua-werks
          RECEIVING rv_qty = vl_cantidad_entrada
          EXCEPTIONS  um_no_valid       = 1
                      missing_constants = 2
                      missing_matnr     = 3
                      no_base_calc      = 4
                      overflow          = 5
                      OTHERS            = 6 ).
        IF sy-subrc <> 0.
          zcl_seis_odata_utils=>lanzar_excepcion( 'Error al convertir unidades' ).
        ENDIF.

        WRITE vl_cantidad_entrada TO vl_cantidad_entrada_c DECIMALS 0. CONDENSE vl_cantidad_entrada_c.
        zcl_seis_odata_utils=>lanzar_excepcion( |Ha introducido un diámetro mayor a la de la recepción inicial de la UA ({ vl_cantidad_entrada_c } cm)| ).

      ELSE.
        vl_unidad_entrada = SWITCH #( wl_estacion-gestiona_palets WHEN 'X' THEN 'ST' ELSE 'M' ).
        CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
          EXPORTING
            i_matnr              = <fs_ltap>-matnr
            i_in_me              = <fs_ltap>-gewei
            i_out_me             = vl_unidad_entrada
            i_menge              = vl_menge
          IMPORTING
            e_menge              = vl_cantidad_entrada
          EXCEPTIONS
            error_in_application = 1
            error                = 2
            OTHERS               = 3.
        IF sy-subrc <> 0.
          zcl_seis_odata_utils=>lanzar_excepcion( ).
        ENDIF.

        CALL FUNCTION 'CONVERSION_EXIT_CUNIT_OUTPUT'
          EXPORTING
            input          = vl_unidad_entrada
          IMPORTING
            output         = vl_unidad_entrada_c
          EXCEPTIONS
            unit_not_found = 1
            OTHERS         = 2.
      ENDIF.

      WRITE vl_cantidad_entrada TO vl_cantidad_entrada_c DECIMALS 0. CONDENSE vl_cantidad_entrada_c.
      zcl_seis_odata_utils=>lanzar_excepcion( |Ha introducido una cantidad mayor a la de la recepción inicial de la UA ({ vl_cantidad_entrada_c } { vl_unidad_entrada_c })| ).
    ENDIF.

    vp_meins = 'KG'.


    DATA: wl_header TYPE bapi2017_gm_head_01,
          wl_code   TYPE bapi2017_gm_code,
          tl_item   TYPE STANDARD TABLE OF bapi2017_gm_item_create,
          tl_return TYPE bapiret2_t.

    SELECT *
      FROM ztwm001
      INTO TABLE @DATA(tl_hardcodes)
      WHERE cprog  = @cl_bhs_bobina AND
            param2 = @cl_param1.



    "codigo de tipo de movimiento de mercancias
    wl_code-gm_code = cl_code.

    "Datos de cabecera
    wl_header-pstng_date = sy-datlo.
    wl_header-doc_date   = sy-datlo.
*  wl_header-ref_doc_no = pi_orden_bhs.
    wl_header-pr_uname   = sy-uname.
    READ TABLE tl_hardcodes ASSIGNING FIELD-SYMBOL(<fs_hardcodes>) WITH KEY param1 = 'HEADER_TEXT'.
    IF sy-subrc = 0.
      wl_header-header_txt = <fs_hardcodes>-valor1.
    ENDIF.

    "Datos de la UA que genera el movimiento
    APPEND INITIAL LINE TO tl_item ASSIGNING FIELD-SYMBOL(<fs_item>).
    <fs_item>-plant     = vp_werks.
    <fs_item>-stge_loc  = wl_lqua-lgort.
    <fs_item>-material  = wl_lqua-matnr.
    <fs_item>-batch     = wl_lqua-charg.
    READ TABLE tl_hardcodes ASSIGNING <fs_hardcodes> WITH KEY param1 = 'MOVEMENT_TYPE_INV'.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Falta parametrizar movimiento de inventario' ).
    ENDIF.
    <fs_item>-move_type = <fs_hardcodes>-valor1.

    CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
      EXPORTING
        i_matnr              = wl_lqua-matnr
        i_in_me              = wl_lqua-meins
        i_out_me             = 'KG'
        i_menge              = wl_lqua-verme
      IMPORTING
        e_menge              = wl_lqua-verme
      EXCEPTIONS
        error_in_application = 1
        error                = 2
        OTHERS               = 3.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al convertir cantidad a KG' ).
    ENDIF.
    wl_lqua-meins = 'KG'.

    "Cantidad del movimiento: Cantidad a regularizar - stock actual
    <fs_item>-entry_qnt = trunc( vp_menge - wl_lqua-verme ).
    <fs_item>-entry_uom = vp_meins.
    CHECK <fs_item>-entry_qnt <> 0.

    <fs_item>-stge_bin  = wl_lqua-lgpla.


    DATA: vl_mblnr TYPE bapi2017_gm_head_ret-mat_doc,
          vl_mjahr TYPE bapi2017_gm_head_ret-doc_year.
    CALL FUNCTION 'BAPI_GOODSMVT_CREATE'
      EXPORTING
        goodsmvt_header  = wl_header
        goodsmvt_code    = wl_code
      IMPORTING
        materialdocument = vl_mblnr
        matdocumentyear  = vl_mjahr
      TABLES
        goodsmvt_item    = tl_item
        return           = tl_return.
    zcl_seis_odata_utils=>lanzar_excepcion( bapiret2_t = tl_return ).


    CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
      EXPORTING
        wait = 'X'.

    " Ahora seleccionamos el cuanto generado por el alta 712, sin UA
    SELECT SINGLE *
      INTO @DATA(wl_lqua_new)
            FROM lqua
            WHERE lgnum = @wl_centro-lgnum AND
            wenum EQ @vl_mblnr. " documento EM


    "Crear orden de transporte
    DATA: tl_trite TYPE l03b_trite_t.
    SELECT SINGLE *
     INTO @DATA(wl_ltbk)
     FROM ltbk
     WHERE lgnum = @wl_lqua-lgnum AND
           mblnr = @vl_mblnr AND
           mjahr = @vl_mjahr.

    IF sy-subrc = 0.
      SELECT *
        INTO TABLE @DATA(tl_ltbp)
        FROM ltbp
        WHERE lgnum = @wl_ltbk-lgnum AND
              tbnum = @wl_ltbk-tbnum.
    ENDIF.

    LOOP AT tl_ltbp ASSIGNING FIELD-SYMBOL(<fs_ltbp>).
      APPEND INITIAL LINE TO tl_trite ASSIGNING FIELD-SYMBOL(<fs_trite>).
      <fs_trite>-tbpos = <fs_ltbp>-tbpos.
      <fs_trite>-altme = <fs_ltbp>-meins.
      <fs_trite>-anfme = <fs_ltbp>-menge.
      <fs_trite>-charg = <fs_ltbp>-charg.
*      <fs_trite>-nlpla = vl_nlpla. " hay que indicar la ubicación de la estación
*      <fs_trite>-nltyp = vl_lgtyp. " hay que indicar la ubicación de la estación
      <fs_trite>-vlpla = wl_lqua_new-lgpla. " ubicación origen
      <fs_trite>-vltyp = wl_lqua_new-lgtyp. " origen
      <fs_trite>-vlenr = wl_lqua_new-lenum. " origen no tiene UA

      <fs_trite>-nlpla = wl_lqua-lgpla. " ubicación estación destino
      <fs_trite>-nltyp = wl_lqua-lgtyp. " tipo alm.estación destino
      <fs_trite>-nlenr = wl_lqua-lenum. " UA input
      <fs_trite>-letyp = wl_lqua-letyp. " tipo UA original
    ENDLOOP.

    DATA vl_tanum TYPE ltak-tanum.
    CALL FUNCTION 'L_TO_CREATE_TR'
      EXPORTING
        i_lgnum                        = wl_ltbk-lgnum
        i_tbnum                        = wl_ltbk-tbnum
        i_squit                        = abap_true
        i_tbeli                        = abap_true
        it_trite                       = tl_trite
      IMPORTING
        e_tanum                        = vl_tanum
      EXCEPTIONS
        foreign_lock                   = 1
        qm_relevant                    = 2
        tr_completed                   = 3
        xfeld_wrong                    = 4
        ldest_wrong                    = 5
        drukz_wrong                    = 6
        tr_wrong                       = 7
        squit_forbidden                = 8
        no_to_created                  = 9
        update_without_commit          = 10
        no_authority                   = 11
        preallocated_stock             = 12
        partial_transfer_req_forbidden = 13
        input_error                    = 14
        error_message                  = 16
        OTHERS                         = 15.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( ).
    ENDIF.


    IF wl_lqua-lgpla <> vp_lgpla. " si la ubicación destino anterior (stock actual de la UA si existe) no es la estación, movemos ahora el cuanto con la ctd total a la estación
      zcl_wm_nt_generic=>movebobine(
              EXPORTING
                iv_bobine           = vp_ua
                iv_dest_nlpla       = vp_lgpla
                iv_lgnum            = wl_ltbk-lgnum
                iv_bwlvs            = '999'
                iv_tipo_mov_bobina  = zcl_wm_nt_generic=>gc_tipo_mov_bobina_reubicar
                iv_validar_fsc_recycled = space "Validación FSC Recycled sólo para el carretillero
              IMPORTING
              es_ltak       = DATA(ls_ltak)
              ev_subrc      = DATA(lv_subrc) ).
      IF lv_subrc <> 0.
        zcl_seis_odata_utils=>lanzar_excepcion( ).
      ENDIF.
    ENDIF.

    DATA tl_selection_table TYPE TABLE OF rsparamsl_255.
    EXPORT lv_vengo_inter_consumo = abap_true           TO MEMORY ID 'VENGO_CONSUMO' .
    EXPORT lv_cod_maq_portabo     = vl_cod_maq_portabo  TO MEMORY ID 'MAQUINA' .

    " create label for remaining
    tl_selection_table = VALUE #( ( selname = 'P_LGNUM' kind = 'P' sign = 'I' option = 'EQ' low = wl_centro-lgnum )
                                  ( selname = 'S_LENUM' kind = 'S' sign = 'I' option = 'EQ' low = vp_ua ) ).
    SUBMIT zimpresion_ua WITH SELECTION-TABLE tl_selection_table AND RETURN.
    FREE MEMORY ID: 'VENGO_CONSUMO', 'MAQUINA'.

  ENDMETHOD.
  METHOD tratar_key_entity.

    DATA(vl_source_entity_type_name)  = io_tech_request_context->get_source_entity_type_name( ).
    DATA(vl_entity_type_name)         = io_tech_request_context->get_entity_type_name( ).


    CASE vl_source_entity_type_name.
      WHEN 'Estaciones'.
        DATA wl_estaciones TYPE zui5_wm_s_estaciones.
        io_tech_request_context->get_converted_source_keys( IMPORTING es_key_values  = wl_estaciones ).
*        wl_estaciones = estaciones_getdetail( vp_werks = wl_estaciones-werks
*                                              vp_lgpla = wl_estaciones-lgpla ).
        MOVE-CORRESPONDING wl_estaciones TO wp_entity.
*
*      WHEN 'AlbaranesLin'.
*        DATA wl_albaranes_lin TYPE zmm_ui5_s_albaranes_lin.
*        io_tech_request_context->get_converted_source_keys( IMPORTING es_key_values  = wl_albaranes_lin ).
*        wl_albaranes_lin = albaranes_lin_getdetail(
*                              vp_werks            = wl_albaranes_lin-werks
*                              vp_contador         = wl_albaranes_lin-contador
*                              vp_posicion_alb     = wl_albaranes_lin-posicion_alb
*                              vp_posicion_alb_lin = wl_albaranes_lin-posicion_alb_lin ).
*        MOVE-CORRESPONDING wl_albaranes_lin TO wp_entity.

      WHEN OTHERS.
        io_tech_request_context->get_converted_keys( IMPORTING es_key_values  = wp_entity ).

    ENDCASE.


  ENDMETHOD.
 METHOD tratar_key_entityset.
   DATA(vl_source_entity_type_name)  = io_tech_request_context->get_source_entity_type_name( ).
   DATA(vl_entity_type_name)         = io_tech_request_context->get_entity_type_name( ).

   CASE vl_source_entity_type_name.
     WHEN 'Maquinas'.
       DATA wl_maquina TYPE zui5_wm_s_maquinas.
       io_tech_request_context->get_converted_source_keys( IMPORTING es_key_values  = wl_maquina ).
       MOVE-CORRESPONDING wl_maquina TO wp_entity.

       IF vl_entity_type_name = 'Estaciones'.
         ASSIGN COMPONENT 'LGPLA_MAQUINA' OF STRUCTURE wp_entity TO FIELD-SYMBOL(<fs_lgpla_maquina>).
         <fs_lgpla_maquina> = wl_maquina-lgpla.
       ENDIF.

     WHEN OTHERS.
       io_tech_request_context->get_converted_source_keys( IMPORTING es_key_values  = wp_entity ).
   ENDCASE.
 ENDMETHOD.
  METHOD validar_consumo_bobina.
    DATA vl_rsnum_initial TYPE rsnum.
    IF vp_id_pedido = vl_rsnum_initial.
      zcl_seis_odata_utils=>lanzar_excepcion( |El código de pedido { vp_id_pedido } es incorrecto| ).
    ENDIF.

    "check diametro vs max
    DATA(wl_centro) = centros_getdetail( vp_werks ).
    IF wl_centro-diametro_maximo <> 0 AND vp_cantidad > wl_centro-diametro_maximo.
      IF wl_centro-consumos_bobinas_metros = 'X'.
        zcl_seis_odata_utils=>lanzar_excepcion( 'Los metros restantes introducidos superan el límite permitido' ).
      ELSE.
        zcl_seis_odata_utils=>lanzar_excepcion( 'El diámetro introducido supera el límite permitido' ).
      ENDIF.
    ENDIF.

    validar_diametro_bobina( vp_werks     = vp_werks
                             vp_lgpla     = vp_lgpla
                             vp_id_bobina = vp_id_bobina
                             vp_cantidad  = vp_cantidad ).


    "Si el centro es de PP, ejecuto el consumo en modo test
    IF wl_centro-lista_apro_ui5_pp = 'X'.
      consumir_bobina_pp( EXPORTING vp_werks                     = vp_werks
                                    vp_lgpla                     = vp_lgpla
                                    vp_id_pedido                 = vp_id_pedido
                                    vp_id_bobina                 = conv #( vp_id_bobina )
                                    vp_cantidad                  = vp_cantidad
                                    vp_test                      = 'X'
                          IMPORTING wp_return                    = wp_return ).
    ENDIF.


  ENDMETHOD.
  METHOD validar_consumo_bobina_post.

    DATA(wl_centro)   = centros_getdetail( vp_werks ).

    IF wl_centro-lista_apro_ui5_pp = space.




      DATA r_metros TYPE RANGE OF zwm_intf_bhs-metros_restantes.
      DATA r_diametro TYPE RANGE OF zwm_intf_bhs-diametro_rest.


      " DEL - cvivo - Lo descarto porque técnicamente no deben consumir n veces la bobina en la reserva
*    CASE wl_centro-consumos_bobinas_metros.
*      WHEN abap_true.
*        r_metros = VALUE #( sign = 'I' option = 'EQ' ( low = vp_cantidad ) ).
*      WHEN abap_false.
*        r_diametro = VALUE #( sign = 'I' option = 'EQ' ( low = vp_cantidad ) ).
*    ENDCASE.

      IF vp_id_pedido IS INITIAL. " cvivo - controlamos esto, no debería pasar
        zcl_seis_odata_utils=>lanzar_excepcion( 'Consumo sin número de reserva' ).
      ELSE.
        DATA r_reserva TYPE RANGE OF rsnum.
        r_reserva = VALUE #( sign = 'I' option = 'EQ' ( low = vp_id_pedido high = space )
                                                      ( low = '0000000000' high = space ) ).
      ENDIF.


      SELECT *
        INTO TABLE @DATA(tl_zwm_intf_bhs)
        UP TO 1 ROWS
        FROM zwm_intf_bhs
        WHERE werks             = @vp_werks     AND
              porta_bobinas     = @vp_lgpla     AND
              cod_etiqueta      = @vp_id_bobina    AND
*            fecha             = @sy-datlo     AND
*            metros_restantes  IN @r_metros   AND
*            diametro_rest     IN @r_diametro AND
              rsnum             IN @r_reserva
      ORDER BY id DESCENDING.
      IF sy-subrc <> 0.
        zcl_seis_odata_utils=>lanzar_excepcion( 'Error al generar el consumo.' ).
      ELSEIF tl_zwm_intf_bhs[ 1 ]-contabilizado EQ abap_false.

        " javier aquí se debería limpiar la UA en UI5, porque el registro ha pasado a tabla, pero está pendiente
        "Para que el sistema haga eso, lo mejor es que no sea una excepción, pero que sí que pase el mensaje...
*      zcl_seis_odata_utils=>lanzar_excepcion( tl_zwm_intf_bhs[ 1 ]-message ).
        wp_return-type    = 'W'.
*      wp_return-message =  tl_zwm_intf_bhs[ 1 ]-message.
        wp_return-message = 'El consumo ha sido registrado pero permanece pendiente de procesar, no repita el consumo'.

      ENDIF.
    ENDIF.


  ENDMETHOD.
  METHOD validar_diametro_bobina.

    DATA(wl_centro)   = centros_getdetail( vp_werks ).
    DATA(wl_estacion) = estaciones_getdetail( vp_werks = vp_werks
                                              vp_lgpla = vp_lgpla ).

    DATA tl_lgort_rg TYPE RANGE OF mchb-lgort.
    SELECT *
      INTO TABLE @DATA(tl_t320)
      FROM t320
      WHERE werks = @vp_werks AND
            lgnum = @wl_centro-lgnum.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'No se ha podido determinar almacén' ).
    ENDIF.
    LOOP AT tl_t320 ASSIGNING FIELD-SYMBOL(<fs_t320>).
      APPEND INITIAL LINE TO tl_lgort_rg ASSIGNING FIELD-SYMBOL(<fs_lgort_rg>).
      <fs_lgort_rg>-sign = 'I'. <fs_lgort_rg>-option = 'EQ'. <fs_lgort_rg>-low = <fs_t320>-lgort.
    ENDLOOP.

    SELECT SINGLE lqua~lenum, lqua~matnr, lqua~meins, lqua~verme, mchb~clabs, mchb~cspem
      INTO @DATA(wl_lqua)
      FROM lqua INNER JOIN mchb ON lqua~charg EQ mchb~charg AND
                                   lqua~werks EQ mchb~werks AND
                                   lqua~matnr EQ mchb~matnr
      WHERE lenum       = @vp_id_bobina     AND
            lgnum       = @wl_centro-lgnum  AND
            mchb~lgort  IN @tl_lgort_rg.


    IF wl_estacion-gestiona_palets = space AND wl_centro-consumos_bobinas_metros = space.
      DATA(vl_diametro_valido) = zcl_wm_nt_generic=>validate_mat_diam( iv_bobine   = vp_id_bobina
                                                                       iv_lgnum    = wl_centro-lgnum
                                                                       iv_diametro = CONV #( vp_cantidad ) ).
    ELSE.

      IF wl_lqua IS INITIAL OR ( wl_centro-consumos_puede_regularizar_ua = 'X' AND wl_lqua-clabs <= 0 AND wl_lqua-cspem <= 0 ).
        zcl_seis_odata_utils=>lanzar_excepcion( 'UA no encontrada, avise al responsable' ).
      ENDIF.

      "Convertir a la unidad de la máquina
      CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
        EXPORTING
          i_matnr              = wl_lqua-matnr
          i_in_me              = wl_lqua-meins
          i_out_me             = wl_estacion-meins
          i_menge              = wl_lqua-verme
        IMPORTING
          e_menge              = wl_lqua-verme
        EXCEPTIONS
          error_in_application = 1
          error                = 2
          OTHERS               = 3.
      IF sy-subrc <> 0.
        zcl_seis_odata_utils=>lanzar_excepcion( 'Error al convertir unidad' ).
      ENDIF.

      CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
        EXPORTING
          i_matnr              = wl_lqua-matnr
          i_in_me              = wl_lqua-meins
          i_out_me             = wl_estacion-meins
          i_menge              = wl_lqua-clabs
        IMPORTING
          e_menge              = wl_lqua-clabs
        EXCEPTIONS
          error_in_application = 1
          error                = 2
          OTHERS               = 3.
      IF sy-subrc <> 0.
        zcl_seis_odata_utils=>lanzar_excepcion( 'Error al convertir unidad' ).
      ENDIF.
      wl_lqua-meins = wl_estacion-meins.

      IF wl_centro-consumos_puede_regularizar_ua EQ abap_true. " en modelo con regularización no se permiten fallos; lo de abajo queda obsoleto, pero lo dejo
        DATA(vl_name_control) = |ZWM_UI5_CONSUMO_CONTROL_{ wl_centro-lgnum }|.

        SELECT sign, opti AS option, low, high
          INTO TABLE @DATA(tl_consumo_control_rg)
          FROM tvarvc
          WHERE name EQ @vl_name_control.

        IF sy-subrc EQ 0.
          IF wl_estacion-cla_indicar_consumido = space OR vp_cantidad = 0.
            DATA(vl_consumo) = wl_lqua-verme - vp_cantidad.
          ELSE.
            vl_consumo = vp_cantidad.
          ENDIF.

          IF vl_consumo NOT IN tl_consumo_control_rg AND vl_consumo GT 0. " si es LE 0 dejamos seguir porque lo parará más adelante con el mensaje correspondiente
            zcl_seis_odata_utils=>lanzar_excepcion( 'El consumo es demasiado pequeño, verifique los datos introducidos' ).
          ENDIF.
        ENDIF.
      ENDIF.

      " cvivo - por la conversión de UMA, es posible que acepte metros restantes mayores que el stock actual (p.e. bobina con 8102m y consumo como m restantes 8100, lo acepta, pero es
      " tan baja la cantidad que en ZWM_CONSUMO no se consume nada) -> como los consumos no son por diferencias tan pequeñas, añadimos 5 metros a la cantidad introducida
      IF wl_estacion-cla_indicar_consumido = space AND vp_cantidad > 0.
        ADD 10 TO vp_cantidad. " puede parecer mucho, pero en bobinas de bajo ancho, 1 kg son muchos metros
      ENDIF.

      IF wl_lqua-verme >= vp_cantidad.
        vl_diametro_valido = 'X'.
      ENDIF.
    ENDIF.


    IF vl_diametro_valido = space.
      IF wl_centro-consumos_puede_regularizar_ua = 'X'.

        DATA vl_cantidad_c  TYPE text20.
        DATA vl_unidad_c    TYPE text5.
        WRITE wl_lqua-clabs TO vl_cantidad_c DECIMALS 0. CONDENSE vl_cantidad_c NO-GAPS.
        CALL FUNCTION 'CONVERSION_EXIT_CUNIT_OUTPUT'
          EXPORTING
            input          = wl_lqua-meins
          IMPORTING
            output         = vl_unidad_c
          EXCEPTIONS
            unit_not_found = 1
            OTHERS         = 2.

        DATA vl_msg TYPE string.
        DATA(vl_msg1) = CONV text120( 'La cantidad actual en el sistema de la UA es %1 %2, el valor introducido es mayor que ésta' ).
        DATA(vl_msg2) = CONV text120( 'Regularice primero la UA con la cantidad correcta y luego registre el consumo de nuevo' ).
        CONCATENATE vl_msg1 vl_msg2 INTO vl_msg SEPARATED BY './n'.
        REPLACE FIRST OCCURRENCE OF '%1' IN vl_msg WITH vl_cantidad_c.
        REPLACE FIRST OCCURRENCE OF '%2' IN vl_msg WITH vl_unidad_c.

        zcl_seis_odata_utils=>lanzar_excepcion( vl_msg ).
      ELSE.
        zcl_seis_odata_utils=>lanzar_excepcion( 'El diámetro introducido es incorrecto' ).
      ENDIF.
    ENDIF.

  ENDMETHOD.
  METHOD validar_lectura_bobina.
    DATA(wl_centro) = centros_getdetail( vp_werks ).
    DATA(wl_estacion) = estaciones_getdetail( vp_werks = vp_werks
                                              vp_lgpla = vp_lgpla ).

    IF strlen( vp_id_bobina ) > 20.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Lectura incorrecta' ).
    ENDIF.

    DATA vl_lenum TYPE lenum.
    CALL FUNCTION 'CONVERSION_EXIT_LENUM_INPUT'
      EXPORTING
        input           = vp_id_bobina
      IMPORTING
        output          = vl_lenum
      EXCEPTIONS
        check_failed    = 1
        not_numeric     = 2
        t344_get_failed = 3
        wrong_length    = 4
        OTHERS          = 5.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al convertir valor de lectura' ).
    ENDIF.

    "Si no tengo reserva, lanzo mensaje para refrescar la pantalla de onduladora
    TRY.
        DATA(wl_pedido) = pedidos_consumo_getdetail(  vp_werks  = vp_werks
                                                      vp_lgpla  = vp_lgpla
                                                      vp_id     = vp_id_pedido ).
      CATCH /iwbep/cx_mgw_busi_exception.
        zcl_seis_odata_utils=>lanzar_excepcion( id = 'ZUI5' number = '004').
        IF 1 = 0. MESSAGE s004(zui5). ENDIF.
    ENDTRY.

    " cvivo - si está activod regul.UA, no se admiten lecturas que no existan en MM+WM
    SELECT SINGLE werks, lenum, matnr, letyp
      FROM lqua
      INTO @DATA(wl_lqua)
      WHERE lgnum = @wl_centro-lgnum AND
            lenum = @vl_lenum.

    IF wl_centro-consumos_puede_regularizar_ua = 'X'.
      IF sy-subrc <> 0.
        "Verificar si ha existido en algún momento
        SELECT SINGLE lgnum, tanum, tapos
          INTO @DATA(wl_ltap)
          FROM ltap
          WHERE vlenr = @vl_lenum.
        IF sy-subrc = 0.
          zcl_seis_odata_utils=>lanzar_excepcion( 'La UA introducida ya está dada de baja./nRegularice la cantidad antes de registrar un consumo' ).
        ELSE.
          zcl_seis_odata_utils=>lanzar_excepcion( 'La UA introducida no existe en el sistema./nVerifique que ha leído el código de barras correcto' ).
        ENDIF.

      ELSE. " AHORA VALIDAMOS EN MM
        SELECT SINGLE lqua~lgnum, lqua~lqnum
          FROM lqua INNER JOIN mchb ON  lqua~charg = mchb~charg AND
                                        lqua~werks = mchb~werks AND
                                        lqua~matnr = mchb~matnr
          WHERE lenum = @vl_lenum         AND
                lgnum = @wl_centro-lgnum  AND
                ( mchb~clabs > 0 OR mchb~cspem > 0 )
          INTO @DATA(wl_lqua_mm).
        IF sy-subrc <> 0.
          zcl_seis_odata_utils=>lanzar_excepcion( 'UA leída no está completamente almacenada, avise al responsable' ).
        ENDIF.
      ENDIF.

    ELSE.
      IF sy-subrc <> 0.
        APPEND INITIAL LINE TO tp_return ASSIGNING FIELD-SYMBOL(<fs_return>).
        <fs_return>-type      = 'W'.
        <fs_return>-id        = 'SY'.
        <fs_return>-number    = '002'.
        <fs_return>-message   = 'La bobina no se encuentra en el sistema ¿Quiere continuar?'.
        RETURN.
      ENDIF.
    ENDIF.

    " cvivo - 58678 - validar tipo de UA correcto
    IF wl_lqua-letyp IS NOT INITIAL.
      IF wl_estacion-gestiona_palets IS INITIAL AND wl_lqua-letyp NE 'BB'.
        zcl_seis_odata_utils=>lanzar_excepcion( 'UA leída no se corresponde con una bobina' ).
      ELSEIF wl_estacion-gestiona_palets IS NOT INITIAL AND wl_lqua-letyp NE 'PL'.
        zcl_seis_odata_utils=>lanzar_excepcion( 'UA leída no se corresponde con un pallet' ).
      ENDIF.
    ENDIF.


    "Si es parte rebobinado, compruebo que la lectura coincida con la declarada en el parte
    IF wl_estacion-parte_rebobinado = 'X'.
      SELECT SINGLE werks, id_maquina, cod_parte_rebob, rsnum
        INTO @DATA(wl_parte_reb_c)
        FROM zwm_parte_reb_c
        WHERE rsnum = @vp_id_pedido.
      IF sy-subrc <> 0.
        zcl_seis_odata_utils=>lanzar_excepcion( 'No se ha encontrado parte de rebobinado' ).
      ENDIF.

      DATA(rl_wm_nt) = NEW zcl_zui5_wm_nt_dpc_ext( ).
      DATA(wl_parte_rebob_cab)  = rl_wm_nt->parte_rebob_cab_getdetail( vp_werks           = wl_parte_reb_c-werks
                                                                       vp_id_maquina      = wl_parte_reb_c-id_maquina
                                                                       vp_cod_parte_rebob = wl_parte_reb_c-cod_parte_rebob ).

      IF wl_parte_rebob_cab-matnr <> wl_lqua-matnr.
        zcl_seis_odata_utils=>lanzar_excepcion( |La bobina leida no es del material del parte de rebobionado ({ wl_parte_rebob_cab-matnr })| ).
      ENDIF.
    ENDIF.




    "Ticket 0000065607. Doble check UA
    IF wl_centro-lectura_bobinas_propuestas = 'X'.
      SELECT ltap~lgnum, ltap~tanum, ltap~tapos, ltbk~rsnum, ltbk~tbnum
        INTO TABLE @DATA(t_ltap)
        UP TO 1 ROWS
        FROM ltap INNER JOIN ltak ON  ltak~lgnum = ltap~lgnum AND
                                      ltak~tanum = ltap~tanum
                  INNER JOIN ltbk ON  ltak~benum = ltbk~benum AND
                                      ltak~lgnum = ltbk~lgnum AND
                                      ltap~nlpla = ltbk~nlpla
        WHERE ltak~lgnum = @wl_centro-lgnum AND
              ltap~vlenr = @vl_lenum        AND
              ltbk~rsnum = @vp_id_pedido
        ORDER BY ltak~tanum DESCENDING.

      IF sy-subrc <> 0.
        SELECT ltap~lgnum, ltap~tanum, ltap~tapos, ltbk~rsnum, ltbk~tbnum
          INTO CORRESPONDING FIELDS OF TABLE @t_ltap
          UP TO 1 ROWS
          FROM ltap INNER JOIN ltak ON  ltak~lgnum = ltap~lgnum AND
                                        ltak~tanum = ltap~tanum
                    INNER JOIN ltbk ON  ltak~benum = ltbk~benum AND
                                        ltak~lgnum = ltbk~lgnum AND
                                        ltap~nlpla = ltbk~nlpla
          WHERE ltak~lgnum = @wl_centro-lgnum AND
                ltap~vlenr = @vl_lenum
          ORDER BY ltak~tanum DESCENDING.

        IF sy-subrc <> 0.
          APPEND INITIAL LINE TO tp_return ASSIGNING <fs_return>.
          <fs_return>-type      = 'W'.
          <fs_return>-id        = 'SY'.
          <fs_return>-number    = '002'.
          <fs_return>-message   = 'La UA introducida no se ha aprovisionado para esta estación ¿Quiere continuar?'.

        ELSEIF wl_centro-lista_apro_ui5_pp = 'X'.
          SELECT ltap~lgnum, ltap~tanum, ltap~tapos, ltbk~rsnum, ltbk~tbnum
            INTO CORRESPONDING FIELDS OF TABLE @t_ltap
            UP TO 1 ROWS
            FROM ltap INNER JOIN ltak ON  ltak~lgnum = ltap~lgnum AND
                                          ltak~tanum = ltap~tanum
                      INNER JOIN ltbk ON  ltak~benum = ltbk~benum AND
                                          ltak~lgnum = ltbk~lgnum AND
                                          ltap~nlpla = ltbk~nlpla
            WHERE ltak~lgnum = @wl_centro-lgnum AND
                  ltap~vlenr = @vl_lenum        AND
                  ltbk~tbnum = @wl_pedido-tbnum
            ORDER BY ltak~tanum DESCENDING.
          IF sy-subrc <> 0.
            APPEND INITIAL LINE TO tp_return ASSIGNING <fs_return>.
            <fs_return>-type      = 'W'.
            <fs_return>-id        = 'SY'.
            <fs_return>-number    = '002'.
            <fs_return>-message   = |La UA introducida no se ha aprovisionado para el pedido actual. ¿Quiere continuar? |.
          ENDIF.

        ELSE.
          DATA(wl_ltap_asig) = t_ltap[ 1 ].

          SELECT SINGLE upper_orderid FROM zwm_ltbk_adit
            INTO @DATA(pedido_aprov)
            WHERE lgnum EQ @wl_centro-lgnum AND
                  tbnum EQ @wl_ltap_asig-tbnum.
          APPEND INITIAL LINE TO tp_return ASSIGNING <fs_return>.
          <fs_return>-type      = 'W'.
          <fs_return>-id        = 'SY'.
          <fs_return>-number    = '002'.
          <fs_return>-message   = |La UA introducida no se ha aprovisionado para el pedido actual, sino para el pedido { pedido_aprov } ¿Quiere continuar? |.
        ENDIF.
      ENDIF.
    ENDIF.

    "Fin Ticket 0000065607. Doble check UA




    "Comprobamos si el material es compatible
    zcl_wm_nt_generic=>get_mat_subs_comp(
      EXPORTING
        iv_matnr  = wl_pedido-matnr
        iv_werks  = wl_pedido-werks
      IMPORTING
        et_subst  = DATA(tl_subst)
        et_compat = DATA(tl_compat) ).

    IF tl_compat IS NOT INITIAL OR tl_subst IS NOT INITIAL. " cvivo - 66986
      READ TABLE tl_subst TRANSPORTING NO FIELDS WITH KEY matnr = wl_lqua-matnr.
      IF sy-subrc <> 0.
        READ TABLE tl_compat TRANSPORTING NO FIELDS WITH KEY matnr = wl_lqua-matnr.
      ENDIF.
      IF sy-subrc <> 0.
        APPEND INITIAL LINE TO tp_return ASSIGNING <fs_return>.
        <fs_return>-type      = 'W'.
        <fs_return>-id        = 'SY'.
        <fs_return>-number    = '002'.
        <fs_return>-message   = 'Ha introducido una bobina de otro papel ¿Quiere continuar?'.
      ENDIF.
    ENDIF.


  ENDMETHOD.
  METHOD validar_lectura_bobina_preimp.
    DATA(wl_centro) = centros_getdetail( vp_werks ).
    "En wl_centro está el LGNUM

    IF strlen( vp_id_bobina ) > 20.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Lectura incorrecta' ).
    ENDIF.

    DATA vl_lenum TYPE lenum.
    CALL FUNCTION 'CONVERSION_EXIT_LENUM_INPUT'
      EXPORTING
        input           = vp_id_bobina
      IMPORTING
        output          = vl_lenum
      EXCEPTIONS
        check_failed    = 1
        not_numeric     = 2
        t344_get_failed = 3
        wrong_length    = 4
        OTHERS          = 5.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al convertir valor de lectura' ).
    ENDIF.

    SELECT SINGLE werks, lenum, matnr
    FROM lqua
    INTO @DATA(wl_lqua)
          WHERE werks EQ @vp_werks AND
          lenum = @vl_lenum.

    IF sy-subrc EQ 0.
      SELECT SINGLE @abap_true FROM mara
        INTO @DATA(zpih)
        WHERE matnr EQ @wl_lqua-matnr
          AND mtart EQ 'ZPIH'.

      IF sy-subrc NE 0.
        zcl_seis_odata_utils=>lanzar_excepcion( 'La UA leída no se corresponde con material preimpreso' ).
      ENDIF.
    ELSE.
      zcl_seis_odata_utils=>lanzar_excepcion( 'No existe la UA en el sistema' ).
    ENDIF.



  ENDMETHOD.
  METHOD validar_lectura_consumo_traza.
    DATA(wl_centro) = centros_getdetail( vp_werks ).
    "En wl_centro está el LGNUM

    IF strlen( vp_id_bobina ) > 20.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Lectura incorrecta' ).
    ENDIF.

    DATA vl_lenum TYPE lenum.
    CALL FUNCTION 'CONVERSION_EXIT_LENUM_INPUT'
      EXPORTING
        input           = vp_id_bobina
      IMPORTING
        output          = vl_lenum
      EXCEPTIONS
        check_failed    = 1
        not_numeric     = 2
        t344_get_failed = 3
        wrong_length    = 4
        OTHERS          = 5.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al convertir valor de lectura' ).
    ENDIF.

    SELECT SINGLE *
      FROM lqua
*      LEFT OUTER JOIN makt ON makt~matnr = lqua~matnr AND
*                                        makt~spras = @sy-langu
      INTO @DATA(wl_lqua)
      WHERE werks = @vp_werks AND
            lenum = @vl_lenum.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'No existe la UA en el sistema' ).
    ENDIF.

    DATA(vl_meins_out) = COND meins( WHEN wl_lqua-letyp EQ 'BB' THEN 'M'
                                     WHEN wl_lqua-letyp EQ 'PL' THEN 'ST' ).

    CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
      EXPORTING
        i_matnr              = wl_lqua-matnr
        i_in_me              = wl_lqua-meins
        i_out_me             = vl_meins_out
        i_menge              = wl_lqua-verme
      IMPORTING
        e_menge              = wl_lqua-verme
      EXCEPTIONS
        error_in_application = 1
        error                = 2
        OTHERS               = 3.

    wl_lqua-verme = trunc( wl_lqua-verme ).
    wl_lqua-meins = vl_meins_out.



    MOVE-CORRESPONDING wl_lqua TO wp_bobina.
    wp_bobina-id = vl_lenum.

    SELECT SINGLE maktx
      INTO wp_bobina-maktx
      FROM makt
      WHERE matnr = wp_bobina-matnr AND
            spras = sy-langu.



  ENDMETHOD.
  METHOD validar_regularizar_ua.
    DATA(wl_centro)   = centros_getdetail( vp_werks ).
    SELECT SINGLE *
      INTO @DATA(wl_ltap)
      FROM ltap
      WHERE lgnum = @wl_centro-lgnum AND
            nlenr = @vp_ua.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'La UA introducida no existe en el sistema./nVerifique que ha leído el código de barras correcto' ).
    ENDIF.

    TRY .
        DATA(wl_unidades_alma) = zcl_wm_nt_generic=>get_instance( )->unidades_alma_getdetail( vp_lgnum = wl_ltap-lgnum
                                                                                              vp_lenum = wl_ltap-nlenr ).
      CATCH /iwbep/cx_mgw_busi_exception.
    ENDTRY.

    DATA(wl_estacion) = estaciones_getdetail( vp_werks = vp_werks
                                              vp_lgpla = vp_lgpla ).

    DATA(vl_meins) = SWITCH ekpo-meins( wl_ltap-letyp WHEN 'BB' THEN 'M' WHEN 'PL' THEN 'ST' ).
    IF vl_meins IS INITIAL.
      zcl_seis_odata_utils=>lanzar_excepcion( 'No se ha podido determinar la unidad de medida' ).
    ENDIF.

    IF vp_lgpla IS NOT INITIAL AND wl_unidades_alma IS NOT INITIAL.
      CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
        EXPORTING
          i_matnr              = wl_unidades_alma-matnr
          i_in_me              = wl_unidades_alma-meins
          i_out_me             = vl_meins
          i_menge              = wl_unidades_alma-verme
        IMPORTING
          e_menge              = wl_unidades_alma-verme
        EXCEPTIONS
          error_in_application = 1
          error                = 2
          OTHERS               = 3.
      IF sy-subrc <> 0.
        zcl_seis_odata_utils=>lanzar_excepcion( 'Error convirtiendo unidad' ).
      ENDIF.

    ELSE.
      wl_unidades_alma-matnr = wl_ltap-matnr.
      wl_unidades_alma-lenum = vp_ua.
      wl_unidades_alma-verme = 0.
    ENDIF.
    wl_unidades_alma-meins = vl_meins.


    IF wl_estacion-gestiona_palets = space AND wl_centro-regularizar_bobinas_diam = 'X'.
      IF wl_unidades_alma IS NOT INITIAL.
        zcl_wm_nt_generic=>conv_mat_diametro(
          EXPORTING iv_matnr          = wl_unidades_alma-matnr
                    iv_qty            = wl_unidades_alma-verme
                    iv_meins          = wl_unidades_alma-meins
                    iv_dest_meins     = zcl_wm_nt_generic=>gc_meins_dia
                    iv_werks          = wl_unidades_alma-werks
          RECEIVING rv_qty = wl_unidades_alma-verme
          EXCEPTIONS  um_no_valid       = 1
                      missing_constants = 2
                      missing_matnr     = 3
                      no_base_calc      = 4
                      OTHERS            = 5 ).
        IF sy-subrc <> 0.
          zcl_seis_odata_utils=>lanzar_excepcion( 'Error convirtiendo unidad' ).
        ENDIF.
      ENDIF.
      wl_unidades_alma-meins = zcl_wm_nt_generic=>gc_meins_dia.
    ENDIF.




    wp_bobina-id    = wl_unidades_alma-lenum.
    wp_bobina-matnr = wl_unidades_alma-matnr.
    wp_bobina-werks = wl_unidades_alma-werks.
    wp_bobina-meins = wl_unidades_alma-meins.
    wp_bobina-verme = wl_unidades_alma-verme.
    wp_bobina-maktx = wl_unidades_alma-maktx.
    wp_bobina-meins = wl_unidades_alma-meins.
    wp_bobina-lgpla = vp_lgpla.
  ENDMETHOD.
