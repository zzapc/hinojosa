
class ZCL_ZUI5_WM_BOBINE__02_DPC_EXT definition
  public
  inheriting from ZCL_ZUI5_WM_BOBINE__02_DPC
  create public .

public section.

  constants GC_ACTION_ASSIGN_BOBINE type STRING value 'assignBobine' ##NO_TEXT.
  constants GC_ACTION_MOVE_BOBINE type STRING value 'moveBobine' ##NO_TEXT.
  constants GC_ACTION_CLOSE_RSNUM type STRING value 'closeRsnum' ##NO_TEXT.
  constants GC_ACTION_GET1STASSIGNED type STRING value 'getFirstPedprogidAssigned' ##NO_TEXT.
  constants GC_ACTION_GETLASTASSIGNED type STRING value 'getLastPedprogidAssigned' ##NO_TEXT.
  constants GC_ACTION_REMOVE_BOBINE type STRING value 'removeBobine' ##NO_TEXT.
  constants GC_ACTION_VAL_BOBINE_DIAM type STRING value 'validateBobineDiam' ##NO_TEXT.
  constants GC_ACTION_CHECK_RSNUM_EXIST type STRING value 'checkRsnumExistence' ##NO_TEXT.
  constants GC_ACTION_GETLASTBOBASSIGNED type STRING value 'getLastBobineAssigned' ##NO_TEXT.
  constants GC_ACTION_VAL_REGULARIZAR_UA type STRING value 'ValidarRegularizarUA' ##NO_TEXT.
  constants GC_ACTION_REGULARIZAR_UA type STRING value 'RegularizarUA' ##NO_TEXT.

  methods GET_DATOS_CONEXION
    returning
      value(WP_DATOS_CONEXION) type ZWM_NT_UI5_DATOS_CONEXION
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods REFRESH_INTERFASE_NEMESIS
    returning
      value(WP_RETURN) type BAPIRET2
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods REGULARIZAR_UA
    importing
      value(VP_UA) type LTAP-VLENR
      value(VP_MENGE) type EKPO-MENGE
      value(VP_MEINS) type EKPO-MEINS
    returning
      value(WP_RETURN) type BAPIRET2
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods VALIDAR_CONSUMO
    importing
      value(VP_BOBINA) type LTAP-VLENR
      value(VP_CANTIDAD) type EKPO-MENGE
      value(VP_RESERVA) type RSNUM
    returning
      value(WP_RETURN) type BAPIRET2
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods VALIDAR_LECTURA_UA
    importing
      value(VP_ID_BOBINA) type LTAP-VLENR
      value(VP_ID_PEDIDO) type ZWM_NT_UI5_BOB_ECM_STR-ID
    returning
      value(WP_RETURN) type BAPIRET2
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods VALIDAR_REGULARIZAR_UA
    importing
      value(VP_UA) type LTAP-VLENR
    returning
      value(WP_UNIDAD_ALMA) type ZWM_NT_UI5_UDS_ALMA_STR
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .

  methods /IWBEP/IF_MGW_APPL_SRV_RUNTIME~EXECUTE_ACTION
    redefinition .
protected section.

  methods EXECUTE_ACTION_CHK_RSNUM_EXIST
    importing
      !IV_ACTION_NAME type STRING optional
      !IT_PARAMETER type /IWBEP/T_MGW_NAME_VALUE_PAIR optional
      !IO_TECH_REQUEST_CONTEXT type ref to /IWBEP/IF_MGW_REQ_FUNC_IMPORT optional
    exporting
      !ER_DATA type ref to DATA
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION
      /IWBEP/CX_MGW_TECH_EXCEPTION .
  methods EXECUTE_ACTION_GET1STASSIGNED
    importing
      !IV_ACTION_NAME type STRING optional
      !IT_PARAMETER type /IWBEP/T_MGW_NAME_VALUE_PAIR optional
      !IO_TECH_REQUEST_CONTEXT type ref to /IWBEP/IF_MGW_REQ_FUNC_IMPORT optional
    exporting
      !ER_DATA type ref to DATA
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION
      /IWBEP/CX_MGW_TECH_EXCEPTION .
  methods EXECUTE_ACTION_GETLASTBASSIGN
    importing
      !IV_ACTION_NAME type STRING optional
      !IT_PARAMETER type /IWBEP/T_MGW_NAME_VALUE_PAIR optional
      !IO_TECH_REQUEST_CONTEXT type ref to /IWBEP/IF_MGW_REQ_FUNC_IMPORT optional
    exporting
      !ER_DATA type ref to DATA
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION
      /IWBEP/CX_MGW_TECH_EXCEPTION .
  methods EXECUTE_ACTION_GETLASTASSIGNED
    importing
      !IV_ACTION_NAME type STRING optional
      !IT_PARAMETER type /IWBEP/T_MGW_NAME_VALUE_PAIR optional
      !IO_TECH_REQUEST_CONTEXT type ref to /IWBEP/IF_MGW_REQ_FUNC_IMPORT optional
    exporting
      !ER_DATA type ref to DATA
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION
      /IWBEP/CX_MGW_TECH_EXCEPTION .
  methods EXECUTE_ACTION_VAL_BOBINE_DIAM
    importing
      !IV_ACTION_NAME type STRING optional
      !IT_PARAMETER type /IWBEP/T_MGW_NAME_VALUE_PAIR optional
      !IO_TECH_REQUEST_CONTEXT type ref to /IWBEP/IF_MGW_REQ_FUNC_IMPORT optional
    exporting
      !ER_DATA type ref to DATA
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION
      /IWBEP/CX_MGW_TECH_EXCEPTION .
  methods EXECUTE_ACTION_VAL_CLOSE_RSNUM
    importing
      !IV_ACTION_NAME type STRING optional
      !IT_PARAMETER type /IWBEP/T_MGW_NAME_VALUE_PAIR optional
      !IO_TECH_REQUEST_CONTEXT type ref to /IWBEP/IF_MGW_REQ_FUNC_IMPORT optional
    exporting
      !ER_DATA type ref to DATA
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION
      /IWBEP/CX_MGW_TECH_EXCEPTION .
  methods EXECUTE_ACTION_ASSIGN_BOBINE
    importing
      !IV_ACTION_NAME type STRING optional
      !IT_PARAMETER type /IWBEP/T_MGW_NAME_VALUE_PAIR optional
      !IO_TECH_REQUEST_CONTEXT type ref to /IWBEP/IF_MGW_REQ_FUNC_IMPORT optional
    exporting
      !ER_DATA type ref to DATA
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION
      /IWBEP/CX_MGW_TECH_EXCEPTION .
  methods EXECUTE_ACTION_MOVE_BOBINE
    importing
      !IV_ACTION_NAME type STRING optional
      !IT_PARAMETER type /IWBEP/T_MGW_NAME_VALUE_PAIR optional
      !IO_TECH_REQUEST_CONTEXT type ref to /IWBEP/IF_MGW_REQ_FUNC_IMPORT optional
    exporting
      !ER_DATA type ref to DATA
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION
      /IWBEP/CX_MGW_TECH_EXCEPTION .
  methods TRATAR_KEY_ENTITYSET
    importing
      !IO_TECH_REQUEST_CONTEXT type ref to /IWBEP/IF_MGW_REQ_ENTITYSET optional
    exporting
      !WP_ENTITY type DATA
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods EXECUTE_ACTION_REMOVE_BOBINE
    importing
      !IV_ACTION_NAME type STRING optional
      !IT_PARAMETER type /IWBEP/T_MGW_NAME_VALUE_PAIR optional
      !IO_TECH_REQUEST_CONTEXT type ref to /IWBEP/IF_MGW_REQ_FUNC_IMPORT optional
    exporting
      !ER_DATA type ref to DATA
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION
      /IWBEP/CX_MGW_TECH_EXCEPTION .
  methods TRATAR_KEY_ENTITY
    importing
      !IO_TECH_REQUEST_CONTEXT type ref to /IWBEP/IF_MGW_REQ_ENTITY optional
    exporting
      !WP_ENTITY type DATA
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .

  methods BOBINESET_CREATE_ENTITY
    redefinition .
  methods BOBINESET_GET_ENTITY
    redefinition .
  methods BOBINESET_GET_ENTITYSET
    redefinition .
  methods CENTROSSET_GET_ENTITY
    redefinition .
  methods MAQUINASSET_GET_ENTITY
    redefinition .
  methods PEDPROGIDSET_GET_ENTITY
    redefinition .
  methods PEDPROGIDSET_GET_ENTITYSET
    redefinition .
  methods TVARVCSET_GET_ENTITY
    redefinition .
  methods TVARVCSET_GET_ENTITYSET
    redefinition .
  methods UNIDADESALMASET_GET_ENTITY
    redefinition .
private section.
endclass. "ZCL_ZUI5_WM_BOBINE__02_DPC_EXT definition
class ZCL_ZUI5_WM_BOBINE__02_DPC_EXT implementation.
  METHOD  /iwbep/if_mgw_appl_srv_runtime~execute_action.
    CASE iv_action_name.
      WHEN gc_action_check_rsnum_exist.
        me->execute_action_chk_rsnum_exist(
                EXPORTING
                  iv_action_name               = iv_action_name
                  it_parameter                 =  it_parameter
                  io_tech_request_context      = io_tech_request_context
                IMPORTING
                  er_data                      = er_data
        ).
      WHEN gc_action_get1stassigned.
        me->execute_action_get1stassigned(
            EXPORTING
              iv_action_name               = iv_action_name
              it_parameter                 =  it_parameter
              io_tech_request_context      = io_tech_request_context
            IMPORTING
              er_data                      = er_data
        ).

      WHEN gc_action_getlastbobassigned.
        me->execute_action_getlastbassign(
            EXPORTING
              iv_action_name               = iv_action_name
              it_parameter                 =  it_parameter
              io_tech_request_context      = io_tech_request_context
            IMPORTING
              er_data                      = er_data
        ).


      WHEN gc_action_getlastassigned.
        me->execute_action_getlastassigned(
            EXPORTING
              iv_action_name               = iv_action_name
              it_parameter                 =  it_parameter
              io_tech_request_context      = io_tech_request_context
            IMPORTING
              er_data                      = er_data
        ).

      WHEN gc_action_close_rsnum.
        me->execute_action_val_close_rsnum(
          EXPORTING
            iv_action_name               = iv_action_name
            it_parameter                 =  it_parameter
            io_tech_request_context      = io_tech_request_context
          IMPORTING
            er_data                      = er_data
        ).
      WHEN gc_action_val_bobine_diam.
        me->execute_action_val_bobine_diam(
          EXPORTING
            iv_action_name               = iv_action_name
            it_parameter                 = it_parameter
            io_tech_request_context      = io_tech_request_context
          IMPORTING
            er_data                      = er_data
        ).
      WHEN gc_action_assign_bobine.
        me->execute_action_assign_bobine(
          EXPORTING
            iv_action_name               = iv_action_name
            it_parameter                 = it_parameter
            io_tech_request_context      = io_tech_request_context
          IMPORTING
            er_data                      = er_data
        ).
      WHEN gc_action_move_bobine.
        me->execute_action_move_bobine(
          EXPORTING
            iv_action_name               = iv_action_name
            it_parameter                 = it_parameter
            io_tech_request_context      = io_tech_request_context
          IMPORTING
            er_data                      = er_data
        ).
      WHEN gc_action_remove_bobine.
        me->execute_action_remove_bobine(
          EXPORTING
            iv_action_name               = iv_action_name
            it_parameter                 = it_parameter
            io_tech_request_context      = io_tech_request_context
          IMPORTING
            er_data                      = er_data
        ).

      WHEN gc_action_val_regularizar_ua.
        DATA: BEGIN OF wl_params_val_regularizar_ua,
                ua TYPE ltap-vlenr,
              END OF wl_params_val_regularizar_ua.
        io_tech_request_context->get_converted_parameters( IMPORTING es_parameter_values = wl_params_val_regularizar_ua ).

        CALL FUNCTION 'CONVERSION_EXIT_LENUM_INPUT'
          EXPORTING
            input           = wl_params_val_regularizar_ua-ua
          IMPORTING
            output          = wl_params_val_regularizar_ua-ua
          EXCEPTIONS
            check_failed    = 1
            not_numeric     = 2
            t344_get_failed = 3
            wrong_length    = 4
            OTHERS          = 5.

        copy_data_to_ref( EXPORTING is_data = validar_regularizar_ua( wl_params_val_regularizar_ua-ua )
                          CHANGING  cr_data = er_data ).


      WHEN gc_action_regularizar_ua.
        DATA: BEGIN OF wl_params_regularizar_ua,
                ua    TYPE ltap-vlenr,
                menge TYPE ekpo-menge,
                meins TYPE ekpo-meins,
              END OF wl_params_regularizar_ua.
        io_tech_request_context->get_converted_parameters( IMPORTING es_parameter_values = wl_params_regularizar_ua ).

        CALL FUNCTION 'CONVERSION_EXIT_LENUM_INPUT'
          EXPORTING
            input           = wl_params_regularizar_ua-ua
          IMPORTING
            output          = wl_params_regularizar_ua-ua
          EXCEPTIONS
            check_failed    = 1
            not_numeric     = 2
            t344_get_failed = 3
            wrong_length    = 4
            OTHERS          = 5.

        copy_data_to_ref( EXPORTING is_data = regularizar_ua( vp_ua     = wl_params_regularizar_ua-ua
                                                              vp_menge  = wl_params_regularizar_ua-menge
                                                              vp_meins  = wl_params_regularizar_ua-meins )
                          CHANGING  cr_data = er_data ).


      WHEN 'ValidarConsumo'.
        DATA: BEGIN OF wl_params_validar_consumo,
                bobina   TYPE ltap-vlenr,
                cantidad TYPE ekpo-menge,
                reserva  TYPE rsnum,
              END OF wl_params_validar_consumo.
        io_tech_request_context->get_converted_parameters( IMPORTING es_parameter_values = wl_params_validar_consumo ).

        CALL FUNCTION 'CONVERSION_EXIT_LENUM_INPUT'
          EXPORTING
            input           = wl_params_validar_consumo-bobina
          IMPORTING
            output          = wl_params_validar_consumo-bobina
          EXCEPTIONS
            check_failed    = 1
            not_numeric     = 2
            t344_get_failed = 3
            wrong_length    = 4
            OTHERS          = 5.
        CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
          EXPORTING
            input  = wl_params_validar_consumo-reserva
          IMPORTING
            output = wl_params_validar_consumo-reserva.



        copy_data_to_ref( EXPORTING is_data = validar_consumo(  vp_reserva   = wl_params_validar_consumo-reserva
                                                                vp_cantidad  = wl_params_validar_consumo-cantidad
                                                                vp_bobina    = wl_params_validar_consumo-bobina )
                          CHANGING  cr_data = er_data ).


      WHEN 'ValidarLecturaUA'.

        DATA: BEGIN OF wl_params_lectura_ua,
                id_bobina TYPE ltap-vlenr,
                id_pedido TYPE zwm_nt_ui5_bob_ecm_str-id,
              END OF wl_params_lectura_ua.
        io_tech_request_context->get_converted_parameters( IMPORTING es_parameter_values = wl_params_lectura_ua ).

        CALL FUNCTION 'CONVERSION_EXIT_LENUM_INPUT'
          EXPORTING
            input           = wl_params_lectura_ua-id_bobina
          IMPORTING
            output          = wl_params_lectura_ua-id_bobina
          EXCEPTIONS
            check_failed    = 1
            not_numeric     = 2
            t344_get_failed = 3
            wrong_length    = 4
            OTHERS          = 5.


        copy_data_to_ref( EXPORTING is_data = validar_lectura_ua(  vp_id_pedido  = wl_params_lectura_ua-id_pedido
                                                                   vp_id_bobina  = wl_params_lectura_ua-id_bobina )
                          CHANGING  cr_data = er_data ).




      when 'getDatosConexion'.
        copy_data_to_ref( EXPORTING is_data = get_datos_conexion( )
                          CHANGING  cr_data = er_data ).

      WHEN 'refreshInterfaseNemesis'.
        copy_data_to_ref( EXPORTING is_data = refresh_interfase_nemesis( )
                          CHANGING  cr_data = er_data ).



      WHEN OTHERS.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception
          EXPORTING
            textid = /iwbep/cx_mgw_not_impl_exc=>method_not_implemented
            action = iv_action_name.
    ENDCASE.
  ENDMETHOD.
  METHOD bobineset_create_entity.
    RETURN. " discontinued
*    DATA ls_data TYPE zcl_zui5_wm_bobine_ecm_mpc=>ts_bobine.
*    io_data_provider->read_entry_data( IMPORTING es_data = ls_data ).
*    DATA(lr_ref) = zcl_wm_nt_generic=>get_instance(
**        iv_langu = SY-LANGU
*).
*    DATA(lv_succ) = lr_ref->bobecm_create_consumption(
*      EXPORTING
**        iv_lgnum = ZCL_WM_CONSTANTS=>GC_DEFAULT_LGNUM
*        is_data  = ls_data
*   ).
*    IF lv_succ = abap_false.
*      RAISE EXCEPTION TYPE /iwbep/cx_mgw_busi_exception
*        EXPORTING
*          textid            = /iwbep/cx_mgw_busi_exception=>business_error_unlimited
*          message_unlimited = CONV #( text-001 ).
*    ENDIF.
*
*    CHECK ls_data-update_diam = abap_true.
*
*    " must create to for remaining diameter
*    lv_succ = lr_ref->create_to_4_diamet_ret(
*      EXPORTING
**        iv_lgnum = ZCL_WM_CONSTANTS=>GC_DEFAULT_LGNUM
*        is_data  = ls_data
*    ).
*
*    IF lv_succ = abap_false.
*      RAISE EXCEPTION TYPE /iwbep/cx_mgw_busi_exception
*        EXPORTING
*          textid            = /iwbep/cx_mgw_busi_exception=>business_error_unlimited
*          message_unlimited = CONV #( text-002 ).
*
*    ENDIF.

  ENDMETHOD.
  METHOD bobineset_get_entity.

    DATA(lv_werks) =  zcl_wm_nt_generic=>read_http_header_val(
        EXPORTING
          iv_name   = zcl_wm_nt_generic=>gc_http_head_werks
          iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) )
      ).


    DATA(lv_lgnum) = zcl_wm_nt_generic=>get_lgnum(
                       iv_werks = CONV #( lv_werks )
*                       iv_lgort =
                   ).

    TRY.
        DATA(lv_bobine) = CONV lqua-lenum( it_key_tab[ name = 'Id' ]-value ).

        CALL FUNCTION 'CONVERSION_EXIT_LENUM_INPUT'
          EXPORTING
            input           = lv_bobine   " External entry
          IMPORTING
            output          = lv_bobine   " Edited storage unit number for sto
          EXCEPTIONS
            check_failed    = 1
            not_numeric     = 2
            t344_get_failed = 3
            wrong_length    = 4
            OTHERS          = 5.

        DATA(ls_data) = zcl_wm_zwm_ecm_bobine_dao=>get_entry(
          EXPORTING
            iv_id_bob_ecm    = CONV #( it_key_tab[ name = 'IdPedprog' ]-value )
            iv_bobine        = lv_bobine
        )->get_data( ).

        DATA(lt_lqua) = zcl_wm_nt_generic=>get_bobine_info(
        EXPORTING
          iv_lgnum    =  zcl_wm_nt_generic=>get_lgnum(
                             iv_werks = CONV #( lv_werks )
*                               iv_lgort =
                         )
            iv_bobine    = ls_data-bobine
*            iv_matnr    =
*            iv_qty_only = ABAP_TRUE
      ).
        IF lines( lt_lqua ) > 0.
          DATA(ls_lqua) = lt_lqua[ 1 ].
        ENDIF.


        er_entity = VALUE #(
      id = ls_data-bobine
matnr = ls_lqua-matnr
charg = ls_data-bobine
werks = lv_werks
diametro = ls_data-diametro
meins = ls_lqua-meins
id_pedprog = ls_data-id_bob_ecm
update_diam = ls_data-saved
       ).
      CATCH cx_root.
    ENDTRY.
**      CATCH zcx_wm_exception.    "
*
*    DATA(lt_data) = zcl_wm_nt_generic=>get_instance(
**        iv_langu = SY-LANGU
*       )->build_bobecm_bob_ui5(
*         EXPORTING
*        iv_lgnum = COND #( WHEN lv_lgnum IS NOT INITIAL THEN lv_lgnum ELSE zcl_wm_constants=>gc_default_lgnum )
*           iv_id    = CONV #( it_key_tab[ name = 'Id' ]-value )
*       ) .
*    CHECK lines( lt_data ) > 0.
    "    er_entity = CORRESPONDING #( lt_data[ 1 ] ).
  ENDMETHOD.
  METHOD bobineset_get_entityset.

    IF iv_filter_string IS NOT INITIAL AND iv_filter_string CS 'IdPedprog'.

      SPLIT iv_filter_string AT 'eq' INTO TABLE DATA(lt_str).
      DATA(lv_id) = CONV zwm_ecm_bobine-id_bob_ecm( lt_str[ 2 ] ).
      REPLACE ALL OCCURRENCES OF '''' IN lv_id WITH space.
      REPLACE ALL OCCURRENCES OF ')' IN lv_id WITH space.
      CONDENSE lv_id NO-GAPS.

    ENDIF.

    IF line_exists( it_navigation_path[ nav_prop = 'PEDPROGID_BOBINE_NAV' ] ).
      lv_id =  it_key_tab[ name = 'Id' ]-value .
    ENDIF.

    DATA(lv_werks) =  zcl_wm_nt_generic=>read_http_header_val(
    EXPORTING
      iv_name   = zcl_wm_nt_generic=>gc_http_head_werks
      iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) )
  ).

    DATA(lt_data) = zcl_wm_zwm_ecm_bobine_dao=>query_by_id_bob_ecm( iv_id_bob_ecm = lv_id ).

    SORT lt_data BY created_at ASCENDING.

    LOOP AT lt_data INTO DATA(ls_data).
      DATA(lt_lqua) = zcl_wm_nt_generic=>get_bobine_info(
         EXPORTING
           iv_lgnum    =  zcl_wm_nt_generic=>get_lgnum(
                              iv_werks = CONV #( lv_werks )
*                               iv_lgort =
                          )
             iv_bobine    = ls_data-bobine
*            iv_matnr    =
*            iv_qty_only = ABAP_TRUE
       ).
      IF lines( lt_lqua ) > 0.
        DATA(ls_lqua) = lt_lqua[ 1 ].
      ENDIF.
      APPEND VALUE #(
      id = ls_data-bobine
matnr = ls_lqua-matnr
charg = ls_data-bobine
werks = lv_werks
diametro = ls_data-diametro
meins = ls_lqua-meins
id_pedprog = ls_data-id_bob_ecm
update_diam = ls_data-saved
       ) TO et_entityset.
    ENDLOOP.

  ENDMETHOD.
  METHOD centrosset_get_entity.


    tratar_key_entity( EXPORTING io_tech_request_context = io_tech_request_context
                       IMPORTING wp_entity               = er_entity ).

    er_entity = zcl_wm_nt_generic=>get_instance( )->centros_getdetail( er_entity-werks ).
  ENDMETHOD.
  METHOD execute_action_assign_bobine.
    DATA ls_ecm TYPE zwm_ecm_bobine.
    DATA ls_bapiret2 TYPE bapiret2.
    FIELD-SYMBOLS: <fs_bapiret> TYPE bapiret2.



    DATA(lv_werks) =  zcl_wm_nt_generic=>read_http_header_val( iv_name = zcl_wm_nt_generic=>gc_http_head_werks
                                                               iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) ) ).
    DATA(lv_nlpla) =  zcl_wm_nt_generic=>read_http_header_val( iv_name = zcl_wm_nt_generic=>gc_http_head_nlpla
                                                               iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) ) ).
    DATA(lv_lgnum)    = zcl_wm_nt_generic=>get_lgnum( iv_werks = CONV #( lv_werks ) ).
    DATA(wl_centro)   = zcl_wm_nt_generic=>get_instance( )->centros_getdetail(  vp_werks      = CONV #( lv_werks ) ).
    DATA(wl_estacion) = zcl_wm_nt_generic=>get_instance( )->maquinas_getdetail( vp_machine_id = CONV #( lv_nlpla ) ).




    IF lines( it_parameter ) < 2.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception
        EXPORTING
          textid = /iwbep/cx_mgw_not_impl_exc=>wrong_data_container
          action = iv_action_name.
    ENDIF.
    TRY.
        ls_ecm-bobine = CONV lqua-lenum( it_parameter[ name = 'Id' ]-value ).
        CALL FUNCTION 'CONVERSION_EXIT_LENUM_INPUT'
          EXPORTING
            input           = ls_ecm-bobine   " External entry
          IMPORTING
            output          = ls_ecm-bobine   " Edited storage unit number for sto
          EXCEPTIONS
            check_failed    = 1
            not_numeric     = 2
            t344_get_failed = 3
            wrong_length    = 4
            OTHERS          = 5.
        ls_ecm-id_bob_ecm =  it_parameter[ name = 'IdPedProg' ]-value .
        ls_ecm-diametro = it_parameter[ name = 'Diametro' ]-value .
        ls_ecm-saved = it_parameter[ name = 'Saved' ]-value .
      CATCH cx_root.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception
          EXPORTING
            textid = /iwbep/cx_mgw_tech_exception=>key_structure_error
            action = iv_action_name.
    ENDTRY.

    IF ls_ecm-saved = abap_true.
      "KYV/RDM - 20200721 - validate diametro - beg
      me->execute_action_val_bobine_diam(
        EXPORTING
          iv_action_name               = gc_action_val_bobine_diam
          it_parameter                 = VALUE #( ( name = 'IV_DIAMETRO' value = ls_ecm-diametro ) ( name = 'IV_BOBINE' value = ls_ecm-bobine ) )
          io_tech_request_context      = io_tech_request_context
        IMPORTING
          er_data                      = er_data
      ).
      IF er_data IS BOUND.
        ASSIGN er_data->* TO <fs_bapiret>.
      ENDIF.

      IF <fs_bapiret> IS ASSIGNED AND <fs_bapiret> IS NOT INITIAL AND <fs_bapiret>-type = 'E'.

        IF <fs_bapiret>-type = 'E' AND wl_centro-consumos_puede_regularizar_ua = 'X'.
          DATA(vl_unidad) = SWITCH meins( wl_estacion-gestiona_palets WHEN 'X' THEN 'ST' ELSE 'M' ).

          SELECT SINGLE lqua~meins, mchb~clabs, mchb~matnr     " cvivo - añado mchb, en el hipotético caso en que hay cuantos descuadrados, manda MM para el stock
            FROM lqua             " el objetivo es evitar consumos no contabilizados
            INNER JOIN mchb ON lqua~charg EQ mchb~charg
                           AND lqua~werks EQ mchb~werks
            INTO @DATA(ls_lqua_mchb)
            WHERE lenum = @ls_ecm-bobine
              AND lgnum = @lv_lgnum.

          CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
            EXPORTING
              i_matnr              = ls_lqua_mchb-matnr
              i_in_me              = ls_lqua_mchb-meins
              i_out_me             = vl_unidad
              i_menge              = ls_lqua_mchb-clabs
            IMPORTING
              e_menge              = ls_lqua_mchb-clabs
            EXCEPTIONS
              error_in_application = 1
              error                = 2
              OTHERS               = 3.
          ls_lqua_mchb-meins = vl_unidad.

          DATA vl_cantidad_c  TYPE text20.
          DATA vl_unidad_c    TYPE text5.
          WRITE ls_lqua_mchb-clabs TO vl_cantidad_c DECIMALS 0. CONDENSE vl_cantidad_c NO-GAPS.
          CALL FUNCTION 'CONVERSION_EXIT_CUNIT_OUTPUT'
            EXPORTING
              input          = ls_lqua_mchb-meins
            IMPORTING
              output         = vl_unidad_c
            EXCEPTIONS
              unit_not_found = 1
              OTHERS         = 2.

          DATA vl_msg TYPE string.
          DATA(vl_msg1) = CONV text120( 'La cantidad actual en el sistema de la UA es %1 %2, el valor introducido es mayor que ésta'(002) ).
          DATA(vl_msg2) = CONV text120( 'Regularice primero la UA con la cantidad correcta y luego registre el consumo de nuevo'(003) ).
          CONCATENATE vl_msg1 vl_msg2 INTO vl_msg SEPARATED BY './n'.
          REPLACE FIRST OCCURRENCE OF '%1' IN vl_msg WITH vl_cantidad_c.
          REPLACE FIRST OCCURRENCE OF '%2' IN vl_msg WITH vl_unidad_c.

          zcl_seis_odata_utils=>lanzar_excepcion( vl_msg ).
        ENDIF.

        <fs_bapiret>-parameter = 'Y'.
        RETURN.
      ENDIF.
      "KYV/RDM - 20200721 - validate diametro - end

      " cvivo - validar que no está duplicando consumo en modelo "regularizar UA" (donde no debe haber errores)
      IF wl_centro-consumos_puede_regularizar_ua EQ 'X'.
        SELECT SINGLE *
        INTO @DATA(l_consumo)
              FROM zwm_intf_bhs
              WHERE werks             = @lv_werks AND
*              porta_bobinas     = @lv_nlpla     AND " no sería relevante, si no podrían engañar intentando en otra estación
              cod_etiqueta      = @ls_ecm-bobine    AND
*              rsnum             EQ @ls_ecm-id_bob_ecm AND " idem, la reserva no importaría sino la bobina
              contabilizado EQ @abap_false AND
              procesado EQ @abap_false AND
              metros_restantes LE @ls_ecm-diametro. " no dejamos consumir mayores al pendiente, de lo contrario al reprocesar quedarán no contabilizados, en
                          " ese caso ¿qué sucede si entra un consumo por menor cantidad y sí se va a contabilizar? el pendiente se procesaría primero porque
                          " en el programa de consumos está puesto ese control

        IF sy-subrc EQ 0.
          zcl_seis_odata_utils=>lanzar_excepcion( 'Consumo ya grabado anteriormente, pendiente de contabilizar, avise al responsable' ).
        ENDIF.
      ENDIF.

      DATA(lr_ref) = zcl_wm_zwm_ecm_bobine_dao=>create_entry( EXPORTING iv_id_bob_ecm = ls_ecm-id_bob_ecm
                                                                        iv_bobine     = ls_ecm-bobine  ).
      TRY.
          lr_ref->set_diametro( iv_diametro = ls_ecm-diametro ).
        CATCH zcx_wm_exception INTO DATA(lr_exc).
          zcl_seis_odata_utils=>lanzar_excepcion( lr_exc->get_text( ) ).
      ENDTRY.
    ELSE.
      lr_ref = zcl_wm_zwm_ecm_bobine_dao=>create_entry(
      EXPORTING
        iv_id_bob_ecm = ls_ecm-id_bob_ecm
        iv_bobine     = ls_ecm-bobine
    ).

      " clear entry if it is persistent already
      lr_ref->delete_entry( iv_commit = abap_true iv_related = abap_true ).

      lr_ref = zcl_wm_zwm_ecm_bobine_dao=>create_entry(
       EXPORTING
         iv_id_bob_ecm = ls_ecm-id_bob_ecm
         iv_bobine     = ls_ecm-bobine
     ).
    ENDIF.


    " now save
*    TRY.
    lr_ref->save_data(
*        iv_commit = ABAP_TRUE
    ).
    CALL FUNCTION 'CONVERSION_EXIT_LENUM_OUTPUT'
      EXPORTING
        input           = ls_ecm-bobine   " Storage unit number (internal form
      IMPORTING
        output          = ls_ecm-bobine   " Storage unit number (output format
      EXCEPTIONS
        t344_get_failed = 1
        OTHERS          = 2.

    ls_bapiret2-type = 'S'.
    ls_bapiret2-message = ls_ecm-id_bob_ecm && '/' && ls_ecm-bobine.
    ls_bapiret2-parameter = ls_ecm-saved.

    IF ls_ecm-saved = abap_true.
      DATA(lt_data) =  zcl_wm_zwm_ecm_bobine_dao=>query_by_bobine( EXPORTING
                                                                    iv_lgnum         = lr_ref->gs_data-lgnum
                                                                    iv_bobine        = lr_ref->gs_data-bobine
                                                                    iv_id_bob_ecm = lr_ref->gs_data-id_bob_ecm ).

      CHECK lines( lt_data ) > 0.

      DATA(lv_rsnum) = zcl_wm_nt_generic=>decode_line_id( iv_id         = lt_data[ 1 ]-id_bob_ecm
                                                          iv_rsnum_mode = abap_true )-rsnum.

      zcl_wm_nt_generic=>close_rsnum( iv_rsnum = lv_rsnum iv_last = abap_true iv_nlpla = CONV #( lv_nlpla ) iv_lgnum = lv_lgnum ).
    ENDIF.

*      CATCH cx_root.
*        ls_bapiret2-type = 'E'.
*    ENDTRY.

    me->copy_data_to_ref(
      EXPORTING
        is_data = ls_bapiret2
      CHANGING
        cr_data = er_data
    ).
  ENDMETHOD.
  METHOD execute_action_chk_rsnum_exist.
    IF lines( it_parameter ) < 1.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception
        EXPORTING
          textid = /iwbep/cx_mgw_not_impl_exc=>wrong_data_container
          action = iv_action_name.
    ENDIF.



    DATA(lv_rsnum) = it_parameter[ name = 'IV_ID' ]-value .
    SELECT SINGLE @abap_true FROM resb
    INTO @DATA(lv_exist)
    WHERE rsnum = @lv_rsnum.

    DATA(ls_bapiret2) = VALUE bapiret2( type = COND #( WHEN lv_exist = abap_true THEN 'S' ELSE 'E ') message = lv_rsnum ).
    me->copy_data_to_ref(
EXPORTING
is_data = ls_bapiret2
CHANGING
cr_data = er_data
).


  ENDMETHOD.
  METHOD execute_action_get1stassigned.
    DATA ls_ret TYPE zcl_zui5_wm_bobine_ecm_mpc=>ts_pedprogid.
    me->pedprogidset_get_entityset(
      EXPORTING
        iv_entity_name               = CONV #( zcl_zui5_wm_bobine_ecm_mpc=>gc_pedprogid )
        iv_entity_set_name           = zcl_zui5_wm_bobine_ecm_mpc=>gc_pedprogid && 'Set'
        iv_source_name               = CONV #( zcl_zui5_wm_bobine_ecm_mpc=>gc_pedprogid )
        it_filter_select_options     = VALUE #( )    " Table of select options
        is_paging                    = VALUE #( )    " Paging structure
        it_key_tab                   = VALUE #( )    " Table for name value pairs
        it_navigation_path           = VALUE #( )    " Table of navigation paths
        it_order                     = VALUE #( )    " The sorting order
        iv_filter_string             = VALUE #( )    " Table for name value pairs
        iv_search_string             = VALUE #( )
        "io_tech_request_context      = io_tech_request_context
      IMPORTING
        et_entityset                 = DATA(lt_pedprogid)    " Returning data
*        es_response_context          =
    ).

    LOOP AT lt_pedprogid INTO DATA(ls_pedprogid)
    WHERE editable = abap_true.
      IF lines( zcl_wm_zwm_ecm_bobine_dao=>query_by_id_bob_ecm(
          EXPORTING
            iv_id_bob_ecm     = ls_pedprogid-id
*            iv_not_saved_only = ABAP_TRUE
        ) ) > 0.
        ls_ret = ls_pedprogid.
        EXIT.
      ENDIF.
    ENDLOOP.

    IF ls_ret IS INITIAL.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_busi_exception
        EXPORTING
          textid            = /iwbep/cx_mgw_busi_exception=>business_error
          message_unlimited = CONV #( text-006 ).
    ENDIF.

    me->copy_data_to_ref(
EXPORTING
is_data = ls_ret
CHANGING
cr_data = er_data
).

  ENDMETHOD.
  METHOD execute_action_getlastassigned.

    DATA ls_ret TYPE zcl_zui5_wm_bobine_ecm_mpc=>ts_pedprogid.
    me->pedprogidset_get_entityset(
      EXPORTING
        iv_entity_name               = CONV #( zcl_zui5_wm_bobine_ecm_mpc=>gc_pedprogid )
        iv_entity_set_name           = zcl_zui5_wm_bobine_ecm_mpc=>gc_pedprogid && 'Set'
        iv_source_name               = CONV #( zcl_zui5_wm_bobine_ecm_mpc=>gc_pedprogid )
        it_filter_select_options     = VALUE #( )    " Table of select options
        is_paging                    = VALUE #( )    " Paging structure
        it_key_tab                   = VALUE #( )    " Table for name value pairs
        it_navigation_path           = VALUE #( )    " Table of navigation paths
        it_order                     = VALUE #( )    " The sorting order
        iv_filter_string             = VALUE #( )    " Table for name value pairs
        iv_search_string             = VALUE #( )
        "io_tech_request_context      = io_tech_request_context
      IMPORTING
        et_entityset                 = DATA(lt_pedprogid)    " Returning data
*        es_response_context          =
    ).

    LOOP AT lt_pedprogid INTO DATA(ls_pedprogid)
    WHERE editable = abap_true.
      IF lines( zcl_wm_zwm_ecm_bobine_dao=>query_by_id_bob_ecm(
          EXPORTING
            iv_id_bob_ecm     = ls_pedprogid-id
*            iv_not_saved_only = ABAP_TRUE
        ) ) > 0.
        ls_ret = ls_pedprogid.
      ENDIF.
    ENDLOOP.

    IF ls_ret IS INITIAL.
*      RAISE EXCEPTION TYPE /iwbep/cx_mgw_busi_exception
*        EXPORTING
*          textid            = /iwbep/cx_mgw_busi_exception=>business_error
*          message_unlimited = CONV #( text-006 ).
    ENDIF.

    me->copy_data_to_ref(
EXPORTING
is_data = ls_ret
CHANGING
cr_data = er_data
).


  ENDMETHOD.
  METHOD execute_action_getlastbassign.

    DATA lt_ret TYPE zcl_zui5_wm_bobine__02_mpc=>tt_bobine.
    me->pedprogidset_get_entityset(
      EXPORTING
        iv_entity_name               = CONV #( zcl_zui5_wm_bobine_ecm_mpc=>gc_pedprogid )
        iv_entity_set_name           = zcl_zui5_wm_bobine_ecm_mpc=>gc_pedprogid && 'Set'
        iv_source_name               = CONV #( zcl_zui5_wm_bobine_ecm_mpc=>gc_pedprogid )
        it_filter_select_options     = VALUE #( )    " Table of select options
        is_paging                    = VALUE #( )    " Paging structure
        it_key_tab                   = VALUE #( )    " Table for name value pairs
        it_navigation_path           = VALUE #( )    " Table of navigation paths
        it_order                     = VALUE #( )    " The sorting order
        iv_filter_string             = VALUE #( )    " Table for name value pairs
        iv_search_string             = VALUE #( )
        "io_tech_request_context      = io_tech_request_context
      IMPORTING
        et_entityset                 = DATA(lt_pedprogid)    " Returning data
*        es_response_context          =
    ).



    LOOP AT lt_pedprogid INTO DATA(ls_pedprogid)
    WHERE editable = abap_true.

      DATA(lt_bob) =  zcl_wm_zwm_ecm_bobine_dao=>query_by_id_bob_ecm(
          EXPORTING
            iv_id_bob_ecm     = ls_pedprogid-id
*            iv_not_saved_only = ABAP_TRUE
        ).

      CHECK lines( lt_bob ) > 0.

      SELECT lqua~lgnum, lqua~lenum, lqua~matnr, lqua~verme, lqua~werks
        INTO TABLE @DATA(tl_lqua)
        FROM lqua
        FOR ALL ENTRIES IN @lt_bob
        WHERE lgnum = @lt_bob-lgnum AND
              lenum = @lt_bob-bobine.

      IF tl_lqua IS NOT INITIAL.
        SELECT *
          INTO TABLE @DATA(tl_mara)
          FROM mara
          FOR ALL ENTRIES IN @tl_lqua
          WHERE matnr = @tl_lqua-matnr.
      ENDIF.

      LOOP AT lt_bob INTO DATA(ls_bob).
        IF line_exists( lt_ret[ id = ls_bob-bobine ] ).
          CONTINUE.
        ENDIF.
        APPEND CORRESPONDING #( ls_bob MAPPING id = bobine id_pedprog = id_bob_ecm ) TO lt_ret ASSIGNING FIELD-SYMBOL(<fs_ret>).

        READ TABLE tl_lqua ASSIGNING FIELD-SYMBOL(<fs_lqua>) WITH KEY lgnum = ls_bob-lgnum
                                                                      lenum = ls_bob-bobine.
        IF sy-subrc = 0.
          <fs_ret>-matnr = <fs_lqua>-matnr.
          <fs_ret>-verme = <fs_lqua>-verme.
          READ TABLE tl_mara ASSIGNING FIELD-SYMBOL(<fs_mara>) WITH KEY matnr = <fs_lqua>-matnr.
          IF sy-subrc = 0.
            <fs_ret>-maktx =  zcl_wm_nt_generic=>get_mat_txt_omp( iv_werks = <fs_lqua>-werks
                                                                  is_mara  = <fs_mara> ).
          ENDIF.
        ENDIF.

      ENDLOOP.
    ENDLOOP.

    DATA(lv_idx) = lines( lt_ret ).

    IF lv_idx = 0.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_busi_exception
        EXPORTING
          textid            = /iwbep/cx_mgw_busi_exception=>business_error
          message_unlimited = CONV #( text-006 ).
    ENDIF.

    IF lv_idx > 2.
      lv_idx = lv_idx - 2.
      DELETE lt_ret TO lv_idx.
    ENDIF.


    me->copy_data_to_ref(
  EXPORTING
  is_data = lt_ret
  CHANGING
  cr_data = er_data
  ).



  ENDMETHOD.
  METHOD execute_action_move_bobine.

    DATA ls_bapiret2 TYPE bapiret2.

    IF lines( it_parameter ) < 2.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception
        EXPORTING
          textid = /iwbep/cx_mgw_not_impl_exc=>wrong_data_container
          action = iv_action_name.
    ENDIF.

    DATA(lv_werks) =  zcl_wm_nt_generic=>read_http_header_val(
    EXPORTING
      iv_name   = zcl_wm_nt_generic=>gc_http_head_werks
      iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) )
  ).


    DATA(lv_lgnum) = zcl_wm_nt_generic=>get_lgnum(
                       iv_werks = CONV #( lv_werks )
*                       iv_lgort =
                   ).

    TRY.
        DATA(lv_bobine) = it_parameter[ name = 'IV_BOBINE' ]-value .
        DATA(lv_nlpla) =  it_parameter[ name = 'IV_DEST_NLPLA' ]-value .
*        DATA(lv_tbnum) =  it_parameter[ name = 'IV_TBNUM' ]-value . " cvivo
      CATCH cx_root.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception
          EXPORTING
            textid = /iwbep/cx_mgw_tech_exception=>key_structure_error
            action = iv_action_name.

    ENDTRY.

*    select single @abap_true from ltbk
*      where tbnum eq @lv_tbnum
*        and lgnum eq @lv_lgnum
*      into @data(result).
*    if sy-subrc ne 0.
*
*    endif.


    CALL FUNCTION 'CONVERSION_EXIT_LENUM_INPUT'
      EXPORTING
        input           = lv_bobine    " External entry
      IMPORTING
        output          = lv_bobine    " Edited storage unit number for sto
      EXCEPTIONS
        check_failed    = 1
        not_numeric     = 2
        t344_get_failed = 3
        wrong_length    = 4
        OTHERS          = 5.

    zcl_wm_nt_generic=>movebobine(
      EXPORTING
        iv_bobine     = CONV #( lv_bobine )
        iv_dest_nlpla = CONV #( lv_nlpla )
        iv_lgnum      = lv_lgnum
        iv_bwlvs = zcl_wm_nt_generic=>gc_move_type_918
        iv_validar_fsc_recycled = space "Validación FSC Recycled sólo para el carretillero
      IMPORTING
        es_ltak       = DATA(ls_ltak)
        ev_subrc      = DATA(lv_subrc)
    ).

    ls_bapiret2-type = COND #( WHEN lv_subrc = 0 THEN 'S' ELSE 'E' ).
    ls_bapiret2-message = ls_ltak-lgnum && '/' && ls_ltak-tanum.

    me->copy_data_to_ref(
  EXPORTING
    is_data = ls_bapiret2
  CHANGING
    cr_data = er_data
).


  ENDMETHOD.
  METHOD execute_action_remove_bobine.

    DATA ls_bapiret2 TYPE bapiret2.

    IF lines( it_parameter ) < 1.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception
        EXPORTING
          textid = /iwbep/cx_mgw_not_impl_exc=>wrong_data_container
          action = iv_action_name.
    ENDIF.

    DATA(lv_werks) =  zcl_wm_nt_generic=>read_http_header_val(
    EXPORTING
      iv_name   = zcl_wm_nt_generic=>gc_http_head_werks
      iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) )
  ).
    DATA(lv_nlpla) =  zcl_wm_nt_generic=>read_http_header_val(
    EXPORTING
      iv_name   = zcl_wm_nt_generic=>gc_http_head_nlpla
      iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) )
  ).


    DATA(lv_lgnum) = zcl_wm_nt_generic=>get_lgnum(
                       iv_werks = CONV #( lv_werks )
*                       iv_lgort =
                   ).

    TRY.
        DATA(lv_bobine) = it_parameter[ name = 'IV_BOBINE' ]-value .
      CATCH cx_root.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception
          EXPORTING
            textid = /iwbep/cx_mgw_tech_exception=>key_structure_error
            action = iv_action_name.

    ENDTRY.

    CALL FUNCTION 'CONVERSION_EXIT_LENUM_INPUT'
      EXPORTING
        input           = lv_bobine    " External entry
      IMPORTING
        output          = lv_bobine    " Edited storage unit number for sto
      EXCEPTIONS
        check_failed    = 1
        not_numeric     = 2
        t344_get_failed = 3
        wrong_length    = 4
        OTHERS          = 5.

    DATA(lt_ecl) = zcl_wm_zwm_ecm_bobine_dao=>query_by_bobine(
                   iv_lgnum         = lv_lgnum
                   iv_bobine        = CONV #( lv_bobine )
*                   iv_no_tanum_only = ABAP_TRUE
               ).

    IF lines( lt_ecl ) = 0.
      ls_bapiret2-type = 'S'.
      ls_bapiret2-message = text-003.
      ls_bapiret2-message_v1 = it_parameter[ name = 'IV_BOBINE' ]-value.
      me->copy_data_to_ref(
EXPORTING
is_data = ls_bapiret2
CHANGING
cr_data = er_data
).
      RETURN.
    ENDIF.

    DATA(lv_cnt) = 0.
    LOOP AT lt_ecl INTO DATA(ls_ecl).
      TRY.
          zcl_wm_zwm_ecm_bobine_dao=>get_entry(
            EXPORTING
              iv_id_bob_ecm    = ls_ecl-id_bob_ecm
              iv_bobine        = ls_ecl-bobine
*          RECEIVING
*            rr_ref           =
          )->delete_entry( iv_commit = abap_true ).
        CATCH zcx_wm_exception.    "
      ENDTRY.
      lv_cnt = lv_cnt + 1.
    ENDLOOP.

    "ls_bapiret2-type = COND #( WHEN lv_cnt > 0 THEN 'S' ELSE 'E' ).
    ls_bapiret2-type = 'S'.
    ls_bapiret2-message = |{ text-004 } { lv_cnt }|.
    ls_bapiret2-message_v1 = it_parameter[ name = 'IV_BOBINE' ]-value.

    me->copy_data_to_ref(
    EXPORTING
    is_data = ls_bapiret2
    CHANGING
    cr_data = er_data
    ).


  ENDMETHOD.
  METHOD execute_action_val_bobine_diam.
    DATA ls_bapiret2 TYPE bapiret2.

    IF lines( it_parameter ) < 2.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception
        EXPORTING
          textid = /iwbep/cx_mgw_not_impl_exc=>wrong_data_container
          action = iv_action_name.
    ENDIF.

    DATA(lv_werks) =  zcl_wm_nt_generic=>read_http_header_val( iv_name = zcl_wm_nt_generic=>gc_http_head_werks
                                                               iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) ) ).
    DATA(lv_nlpla) =  zcl_wm_nt_generic=>read_http_header_val( iv_name = zcl_wm_nt_generic=>gc_http_head_nlpla
                                                               iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) ) ).
    DATA(lv_lgnum)    = zcl_wm_nt_generic=>get_lgnum( iv_werks = CONV #( lv_werks ) ).
    DATA(wl_centro)   = zcl_wm_nt_generic=>get_instance( )->centros_getdetail( vp_werks = CONV #( lv_werks ) ).
    DATA(wl_maquina)  = zcl_wm_nt_generic=>get_instance( )->maquinas_getdetail( vp_machine_id = CONV #( lv_nlpla ) ).




    TRY.
        DATA(lv_bobine) = it_parameter[ name = 'IV_BOBINE' ]-value .
        DATA(lv_diametro) =  it_parameter[ name = 'IV_DIAMETRO' ]-value .
      CATCH cx_root.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception
          EXPORTING
            textid = /iwbep/cx_mgw_tech_exception=>key_structure_error
            action = iv_action_name.

    ENDTRY.

    CHECK lv_bobine IS NOT INITIAL. " cvivo - Evitar DUMP CONV_EXIT_FIELD_TOO_SHORT

    CALL FUNCTION 'CONVERSION_EXIT_LENUM_INPUT'
      EXPORTING
        input           = lv_bobine    " External entry
      IMPORTING
        output          = lv_bobine    " Edited storage unit number for sto
      EXCEPTIONS
        check_failed    = 1
        not_numeric     = 2
        t344_get_failed = 3
        wrong_length    = 4
        OTHERS          = 5.



    IF wl_centro-consumos_bobinas_metros = space.
      ls_bapiret2-type = COND #( WHEN zcl_wm_nt_generic=>validate_mat_diam(
                     iv_bobine   = CONV #( lv_bobine )
                     iv_lgnum    = lv_lgnum
                     iv_diametro = CONV #( lv_diametro )
                 ) = abap_true THEN 'S' ELSE 'E' ).
      ls_bapiret2-message = COND #( WHEN ls_bapiret2-type = 'E' THEN text-005 ELSE VALUE #( ) ).

    ELSE.
      "Convertir a la unidad de la máquina
      DATA(vl_meins) = SWITCH meins( wl_maquina-gestiona_palets WHEN 'X' THEN 'ST' ELSE 'M' ).

      IF wl_centro-consumos_puede_regularizar_ua EQ abap_false. " cvivo - no validamos MCHB
        SELECT SINGLE lqua~* FROM lqua
        INNER JOIN mchb
          ON lqua~charg EQ mchb~charg
         AND lqua~werks EQ mchb~werks
          AND lqua~matnr EQ mchb~matnr
        WHERE lenum = @lv_bobine
        AND   lgnum = @lv_lgnum
        INTO @DATA(wl_lqua).
      ELSE.
        SELECT SINGLE lqua~* FROM lqua
        INNER JOIN mchb
          ON lqua~charg EQ mchb~charg
          AND lqua~werks EQ mchb~werks
          AND lqua~matnr EQ mchb~matnr
        WHERE lenum = @lv_bobine
        AND   lgnum = @lv_lgnum
        AND   ( mchb~clabs GT 0 OR mchb~cspem GT 0 )
        INTO @wl_lqua.
      ENDIF.

      IF sy-subrc <> 0.
        zcl_seis_odata_utils=>lanzar_excepcion( 'UA no encontrada, avise al responsable'(004) ).
      ENDIF.

      CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
        EXPORTING
          i_matnr              = wl_lqua-matnr
          i_in_me              = wl_lqua-meins
          i_out_me             = vl_meins
          i_menge              = wl_lqua-verme
        IMPORTING
          e_menge              = wl_lqua-verme
        EXCEPTIONS
          error_in_application = 1
          error                = 2
          OTHERS               = 3.
      IF sy-subrc <> 0.
        zcl_seis_odata_utils=>lanzar_excepcion( 'Error al convertir unidad' ).
      ENDIF.

      DATA(vl_diametro) = CONV ekpo-menge( lv_diametro ).

      IF wl_centro-consumos_puede_regularizar_ua EQ abap_true. " en modelo con regularización no se permiten fallos; lo de abajo queda obsoleto, pero lo dejo
        DATA(control) = |ZWM_UI5_CONSUMO_CONTROL_{ lv_lgnum }|.

        SELECT sign, opti AS option, low, high FROM tvarvc
        WHERE name EQ @control
        INTO TABLE @DATA(rango).

        IF sy-subrc EQ 0.
          DATA(consumo) = wl_lqua-verme - vl_diametro.

          IF consumo NOT IN rango AND consumo GT 0. " si es LE 0 dejamos seguir porque lo parará más adelante con el mensaje correspondiente
            zcl_seis_odata_utils=>lanzar_excepcion( 'El consumo es demasiado pequeño, verifique los datos introducidos'(006) ).
          ENDIF.
        ENDIF.
      ENDIF.

      " cvivo - por la conversión de UMA, es posible que acepte metros restantes mayores que el stock actual (p.e. bobina con 8102m y consumo como m restantes 8100, lo acepta, pero es
      " tan baja la cantidad que en ZWM_CONSUMO no se consume nada) -> como los consumos no son por diferencias tan pequeñas, añadimos 5 metros a la cantidad introducida
      IF vl_diametro > 0.
        ADD 10 TO vl_diametro. " puede parecer mucho, pero en bobinas de bajo ancho, 1 kg son muchos metros
      ENDIF.

      wl_lqua-meins = vl_meins.

      ls_bapiret2-type    = COND #( WHEN wl_lqua-verme >=  vl_diametro THEN 'S' ELSE 'E' ).
      ls_bapiret2-message = COND #( WHEN ls_bapiret2-type = 'E' THEN text-005 ELSE VALUE #( ) ).

    ENDIF.

    me->copy_data_to_ref( EXPORTING is_data = ls_bapiret2
                          CHANGING  cr_data = er_data ).

  ENDMETHOD.
  METHOD execute_action_val_close_rsnum.
    IF lines( it_parameter ) < 1.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception
        EXPORTING
          textid = /iwbep/cx_mgw_not_impl_exc=>wrong_data_container
          action = iv_action_name.
    ENDIF.

    DATA(lv_werks) =  zcl_wm_nt_generic=>read_http_header_val(
    EXPORTING
      iv_name   = zcl_wm_nt_generic=>gc_http_head_werks
      iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) )
  ).

    TRY.
        DATA(lv_rsnum) = it_parameter[ name = 'Id' ]-value .
        DATA(ls_bapiret2) = zcl_wm_nt_generic=>close_rsnum( iv_rsnum = CONV #( lv_rsnum ) ).
        me->copy_data_to_ref(
EXPORTING
  is_data = ls_bapiret2
CHANGING
  cr_data = er_data
).
      CATCH cx_root.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception
          EXPORTING
            textid = /iwbep/cx_mgw_tech_exception=>key_structure_error
            action = iv_action_name.

    ENDTRY.


  ENDMETHOD.
  METHOD get_datos_conexion.
    TRY .

        DATA(vl_werks) =  zcl_wm_nt_generic=>read_http_header_val( iv_name = zcl_wm_nt_generic=>gc_http_head_werks
                                                                   iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) ) ).
        DATA(vl_nlpla) =  CONV lagp-lgpla( zcl_wm_nt_generic=>read_http_header_val( iv_name = zcl_wm_nt_generic=>gc_http_head_nlpla
                                                                                    iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) ) ) ).
      CATCH /iwbep/cx_mgw_tech_exception.
        zcl_seis_odata_utils=>lanzar_excepcion( 'Error al recuperar datos de cabecera' ).
    ENDTRY.

    wp_datos_conexion-sysid = sy-sysid.
    wp_datos_conexion-uname = sy-uname.

    "Miramos los datos de intefase Nemesis
    SELECT *
      INTO TABLE @DATA(tl_ztwm001)
      FROM ztwm001
      WHERE cprog  =  'INTF_NEMESIS' AND
            param1 IN ('WERKS', 'REFRESH_INTERFASE').
    READ TABLE tl_ztwm001 ASSIGNING FIELD-SYMBOL(<fs_ztwm001>) WITH KEY param1 = 'WERKS'.
    IF sy-subrc = 0 AND <fs_ztwm001>-valor1 = vl_werks.
      READ TABLE tl_ztwm001 ASSIGNING <fs_ztwm001> WITH KEY param1 = 'REFRESH_INTERFASE'
                                                            param2 = 'USER'
                                                            param3 = wp_datos_conexion-uname.
      IF sy-subrc = 0.
        wp_datos_conexion-permiso_recargar_interfaz = 'X'.
      ENDIF.

      READ TABLE tl_ztwm001 ASSIGNING <fs_ztwm001> WITH KEY param1 = 'REFRESH_INTERFASE'
                                                            param2 = 'TIMESTAMP'.
      IF sy-subrc = 0.
        DATA(vl_timestamp_actual) = sy-datum && sy-uzeit.
        IF vl_timestamp_actual < <fs_ztwm001>-valor1.
          wp_datos_conexion-proteger_recarga_interfaz = 'X'.
        ENDIF.
      ENDIF.
    ENDIF.

  ENDMETHOD.
  METHOD maquinasset_get_entity.
    tratar_key_entity( EXPORTING io_tech_request_context = io_tech_request_context
                       IMPORTING wp_entity               = er_entity ).

    er_entity = zcl_wm_nt_generic=>get_instance( )->maquinas_getdetail( er_entity-id ).
  ENDMETHOD.
  METHOD pedprogidset_get_entity.
    CLEAR er_entity.
    IF lines( it_key_tab ) = 0.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception
        EXPORTING
          textid = VALUE #( msgid = 'ZUI5' msgno = '001' ).
    ENDIF.

    " get pars
    DATA(lv_nlpla) =  zcl_wm_nt_generic=>read_http_header_val(
        EXPORTING
          iv_name   = zcl_wm_nt_generic=>gc_http_head_nlpla
          iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) )
      ).

    DATA(lv_werks) =  zcl_wm_nt_generic=>read_http_header_val(
        EXPORTING
          iv_name   = zcl_wm_nt_generic=>gc_http_head_werks
          iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) )
      ).

    DATA(lv_currentid) =  zcl_wm_nt_generic=>read_http_header_val(
    EXPORTING
      iv_name   = zcl_wm_nt_generic=>gc_http_head_currentid
      iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) )
  ).

    DATA(lv_lgnum) = zcl_wm_nt_generic=>get_lgnum(
                       iv_werks = CONV #( lv_werks )
*                       iv_lgort =
                   ).

    DATA(lt_data) = zcl_wm_nt_generic=>get_instance(
*        iv_langu = SY-LANGU
    )->build_bobecm_list_ui5_v2(
      EXPORTING
        iv_lgnum = COND #( WHEN lv_lgnum IS NOT INITIAL THEN lv_lgnum ELSE zcl_wm_constants=>gc_default_lgnum )
        iv_nlpla = CONV #( lv_nlpla )
        iv_current_id = CONV #( lv_currentid )
    ).

    TRY.
        er_entity = CORRESPONDING #( lt_data[ id = it_key_tab[ name = 'Id' ]-value ] ).
      CATCH cx_root.
*        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
        "cvivo - Si no tengo reserva, lanzo mensaje para refrescar la pantalla de onduladora
        IF er_entity IS INITIAL.
          zcl_seis_odata_utils=>lanzar_excepcion( id = 'ZUI5' number = '004').
          IF 1 = 0. MESSAGE s004(zui5). ENDIF.
        ENDIF.
    ENDTRY.

    "er_entityset = CORRESPONDING #( lt_data[ id = ] ).

  ENDMETHOD.
  METHOD pedprogidset_get_entityset.
    CLEAR et_entityset.
    " get pars
    DATA(lv_nlpla) =  zcl_wm_nt_generic=>read_http_header_val(
        EXPORTING
          iv_name   = zcl_wm_nt_generic=>gc_http_head_nlpla
          iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) )
      ).

    DATA(lv_werks) =  zcl_wm_nt_generic=>read_http_header_val(
        EXPORTING
          iv_name   = zcl_wm_nt_generic=>gc_http_head_werks
          iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) )
      ).

    DATA(lv_currentid) =  zcl_wm_nt_generic=>read_http_header_val(
        EXPORTING
          iv_name   = zcl_wm_nt_generic=>gc_http_head_currentid
          iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) )
      ).


    DATA(lv_lgnum) = zcl_wm_nt_generic=>get_lgnum(
                       iv_werks = CONV #( lv_werks )
*                       iv_lgort =
                   ).

    DATA(lt_data) = zcl_wm_nt_generic=>get_instance(
*        iv_langu = SY-LANGU
    )->build_bobecm_list_ui5_v2(
      EXPORTING
        iv_lgnum = COND #( WHEN lv_lgnum IS NOT INITIAL THEN lv_lgnum ELSE zcl_wm_constants=>gc_default_lgnum )
        iv_nlpla = CONV #( lv_nlpla )
        iv_current_id = CONV #( lv_currentid )
    ).


    et_entityset = CORRESPONDING #( lt_data ).

  ENDMETHOD.
  METHOD refresh_interfase_nemesis.
    DATA: client        TYPE REF TO if_http_client,
          response_data TYPE string.

    TRY .
        DATA(vl_werks) =  CONV werks_d( zcl_wm_nt_generic=>read_http_header_val( iv_name = zcl_wm_nt_generic=>gc_http_head_werks
                                                                                 iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) ) ) ).
        DATA(vl_nlpla) =  CONV lagp-lgpla( zcl_wm_nt_generic=>read_http_header_val( iv_name = zcl_wm_nt_generic=>gc_http_head_nlpla
                                                                                    iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) ) ) ).
      CATCH /iwbep/cx_mgw_tech_exception.
        zcl_seis_odata_utils=>lanzar_excepcion( 'Error al recuperar datos de cabecera' ).
    ENDTRY.

    DATA(wl_datos_conexion) = get_datos_conexion( ).
    IF wl_datos_conexion-permiso_recargar_interfaz = space.
      zcl_seis_odata_utils=>lanzar_excepcion( 'No tiene permiso para recargar la interfaz Nemesis' ).
    ENDIF.

    DATA vl_authorization TYPE string.
    SELECT SINGLE valor1
      INTO @vl_authorization
      FROM ztwm001
      WHERE cprog  = 'INTF_NEMESIS'       AND
            param1 = 'REFRESH_INTERFASE'  AND
            param2 = 'USER'               AND
            param3 = @wl_datos_conexion-uname.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'No tiene permiso para recargar la interfaz Nemesis' ).
    ENDIF.

    "Comprobamos el timer
    DATA(vl_timestamp_actual) = sy-datum && sy-uzeit.
    SELECT SINGLE valor1
      INTO @DATA(vl_timestamp)
      FROM ztwm001
      WHERE cprog  = 'INTF_NEMESIS'       AND
            param1 = 'REFRESH_INTERFASE'  AND
            param2 = 'TIMESTAMP'.
    IF vl_timestamp_actual < vl_timestamp.
      zcl_seis_odata_utils=>lanzar_excepcion( 'La interfaz no está activa. Pruebe dentro de unos minutos' ).
    ENDIF.


    DATA(wl_centro) = zcl_wm_nt_generic=>get_instance( )->centros_getdetail( vl_werks ).

    "Arrancar el canal
    cl_http_client=>create_by_url( EXPORTING url    = CONV #( wl_centro-url_interfase_1 )
                                   IMPORTING client = client
                                   EXCEPTIONS argument_not_found = 1
                                              plugin_not_active  = 2
                                              internal_error     = 3
                                              OTHERS             = 4 ).
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al generar llamada' ).
    ENDIF.

    client->request->set_header_field(  name  = 'Authorization'
                                        value = vl_authorization ).

    client->send( EXCEPTIONS  http_communication_failure  = 1
                              http_invalid_state          = 2
                              http_processing_failed      = 3
                              http_invalid_timeout        = 4    ).
    IF sy-subrc <> 0.
      client->close( ).
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al enviar petición' ).
    ENDIF.

    client->receive( EXCEPTIONS http_communication_failure  = 1
                                http_invalid_state          = 2
                                http_processing_failed      = 3 ).
    IF sy-subrc <> 0.
      client->close( ).
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al recibir respuesta' ).
    ENDIF.

    response_data = client->response->get_cdata( ).
*    client->get_last_error( IMPORTING message = DATA(vl_message) ).
    client->get_last_error( IMPORTING code           = DATA(vl_code)
                                      message        = DATA(vl_message)
                                      message_class  = DATA(vl_message_class)
                                      message_number = DATA(vl_message_number) ).


    client->close( ).
    IF vl_message IS NOT INITIAL.
      zcl_seis_odata_utils=>lanzar_excepcion( vl_message ).
    ENDIF.



    WAIT UP TO 5 SECONDS.


    "Parar el canal
    cl_http_client=>create_by_url( EXPORTING url    = CONV #( wl_centro-url_interfase_2 )
                                   IMPORTING client = client
                                   EXCEPTIONS argument_not_found = 1
                                              plugin_not_active  = 2
                                              internal_error     = 3
                                              OTHERS             = 4 ).
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al generar llamada' ).
    ENDIF.


    client->request->set_header_field(  name  = 'Authorization'
                                        value = vl_authorization ).
    client->send( EXCEPTIONS  http_communication_failure  = 1
                              http_invalid_state          = 2
                              http_processing_failed      = 3
                              http_invalid_timeout        = 4    ).
    IF sy-subrc <> 0.
      client->close( ).
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al enviar petición' ).
    ENDIF.

    client->receive( EXCEPTIONS http_communication_failure  = 1
                                http_invalid_state          = 2
                                http_processing_failed      = 3 ).
    IF sy-subrc <> 0.
      client->close( ).
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al recibir respuesta' ).
    ENDIF.

    response_data = client->response->get_cdata( ).
    client->get_last_error( IMPORTING message = vl_message ).
    client->close( ).
    IF vl_message IS NOT INITIAL.
      zcl_seis_odata_utils=>lanzar_excepcion( vl_message ).
    ENDIF.

    "Calculamos 5 minutos más para indicar el timestamp
    DATA: vl_enddate TYPE sy-datum,
          vl_endtime TYPE sy-uzeit.
    CALL FUNCTION 'C14B_ADD_TIME'
      EXPORTING
        i_startdate = sy-datum
        i_starttime = sy-uzeit
        i_addtime   = '000500'
      IMPORTING
        e_enddate   = vl_enddate
        e_endtime   = vl_endtime.
    vl_timestamp = vl_enddate && vl_endtime.

    UPDATE ztwm001
      SET valor1 = vl_timestamp
      WHERE cprog  =  'INTF_NEMESIS'      AND
            param1 = 'REFRESH_INTERFASE'  AND
            param2 = 'TIMESTAMP'.




  ENDMETHOD.
  METHOD regularizar_ua.
    CONSTANTS: cl_bhs_bobina TYPE repid   VALUE 'INTF_BHS_BOBINA',
               cl_param1     TYPE zeparam VALUE '01'.
    CONSTANTS: cl_code       TYPE gm_code VALUE '03'. " MB1A - Goods Issue


    TRY .

        DATA(vl_werks) =  zcl_wm_nt_generic=>read_http_header_val( iv_name = zcl_wm_nt_generic=>gc_http_head_werks
                                                                   iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) ) ).
        DATA(vl_nlpla) =  CONV lagp-lgpla( zcl_wm_nt_generic=>read_http_header_val( iv_name = zcl_wm_nt_generic=>gc_http_head_nlpla
                                                                                    iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) ) ) ).
      CATCH /iwbep/cx_mgw_tech_exception.
        zcl_seis_odata_utils=>lanzar_excepcion( 'Error al recuperar datos de cabecera' ).
    ENDTRY.

    DATA(vl_lgnum)    = zcl_wm_nt_generic=>get_lgnum( iv_werks = CONV #( vl_werks ) ).
    DATA(wl_centro)   = zcl_wm_nt_generic=>get_instance( )->centros_getdetail(  vp_werks      = CONV #( vl_werks ) ).
    TRY .
        DATA(wl_estacion) = zcl_wm_nt_generic=>get_instance( )->maquinas_getdetail( vp_machine_id = vl_nlpla ).
      CATCH /iwbep/cx_mgw_busi_exception.
        CLEAR wl_estacion.
    ENDTRY.

    SELECT SINGLE lgtyp
      INTO @DATA(vl_lgtyp)
      FROM lagp
      WHERE lgnum = @vl_lgnum AND
            lgpla = @vl_nlpla.

    SELECT  *
      INTO TABLE @DATA(tl_ltap)
      FROM ltap
      UP TO 1 ROWS
      WHERE lgnum = @vl_lgnum AND
            nlenr = @vp_ua
      ORDER BY tanum, tapos.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'No se ha encontrado la UA' ).
    ENDIF.
    READ TABLE tl_ltap ASSIGNING FIELD-SYMBOL(<fs_ltap>) INDEX 1.


    SELECT SINGLE *
      INTO @DATA(wl_lqua)
      FROM lqua
      WHERE lgnum = @vl_lgnum AND
            lenum = @vp_ua.
    IF sy-subrc <> 0.
      "Si no se encuentra cuanto tengo que darla de alta, marco destino estación para que la OT lo deje directamente ahí
      wl_lqua-matnr = <fs_ltap>-matnr.
      wl_lqua-werks = <fs_ltap>-werks.
      wl_lqua-lgort = <fs_ltap>-lgort.
      wl_lqua-lgnum = <fs_ltap>-lgnum.
      wl_lqua-charg = <fs_ltap>-charg.
      wl_lqua-lenum = <fs_ltap>-nlenr.
      wl_lqua-letyp = <fs_ltap>-letyp.
      wl_lqua-lgpla = vl_nlpla. " ubicación estación
      wl_lqua-lgtyp = vl_lgtyp. " ubicación estación
      wl_lqua-meins = 'KG'.
    ENDIF.


    "Convertimos cantidades en kilos
    IF vp_meins = zcl_wm_nt_generic=>gc_meins_dia.
      zcl_wm_nt_generic=>conv_mat_diametro(
        EXPORTING iv_matnr          = wl_lqua-matnr
                  iv_qty            = vp_menge
                  iv_meins          = vp_meins
                  iv_dest_meins     = 'KG'
                  iv_werks          = wl_lqua-werks
        RECEIVING rv_qty = vp_menge
        EXCEPTIONS  um_no_valid       = 1
                    missing_constants = 2
                    missing_matnr     = 3
                    no_base_calc      = 4
                    overflow          = 5
                    OTHERS            = 6 ).
      CASE sy-subrc.
        WHEN 0. "Correcto
        WHEN 5. "Overflow
          vp_menge = '9999999999.999'.
        WHEN OTHERS.
          zcl_seis_odata_utils=>lanzar_excepcion( 'Error al convertir diámetro a KG' ).
      ENDCASE.


    ELSE.
      CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
        EXPORTING
          i_matnr              = wl_lqua-matnr
          i_in_me              = vp_meins
          i_out_me             = 'KG'
          i_menge              = vp_menge
        IMPORTING
          e_menge              = vp_menge
        EXCEPTIONS
          error_in_application = 1
          error                = 2
          OTHERS               = 3.
      IF sy-subrc <> 0.
        zcl_seis_odata_utils=>lanzar_excepcion( 'Error al convertir cantidad a KG' ).
      ENDIF.
    ENDIF.
*    vp_meins = 'KG'.


    "Comprobación cantidad
    DATA(vl_menge) = CONV ekpo-menge( <fs_ltap>-brgew ).
    CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
      EXPORTING
        i_matnr              = wl_lqua-matnr
        i_in_me              = <fs_ltap>-gewei
        i_out_me             = 'KG'
        i_menge              = vl_menge
      IMPORTING
        e_menge              = vl_menge
      EXCEPTIONS
        error_in_application = 1
        error                = 2
        OTHERS               = 3.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( ).
    ENDIF.

    DATA: vl_cantidad_entrada TYPE ekpo-menge,
          vl_unidad_entrada   TYPE meins.
    DATA: vl_cantidad_entrada_c TYPE text20,
          vl_unidad_entrada_c   TYPE text10.

    IF vl_menge < vp_menge.
      IF vp_meins = zcl_wm_nt_generic=>gc_meins_dia.
        zcl_wm_nt_generic=>conv_mat_diametro(
          EXPORTING iv_matnr          = wl_lqua-matnr
                    iv_qty            = vl_menge
                    iv_meins          = 'KG'
                    iv_dest_meins     = vp_meins
                    iv_werks          = wl_lqua-werks
          RECEIVING rv_qty = vl_cantidad_entrada
          EXCEPTIONS  um_no_valid       = 1
                      missing_constants = 2
                      missing_matnr     = 3
                      no_base_calc      = 4
                      overflow          = 5
                      OTHERS            = 6 ).
        IF sy-subrc <> 0.
          zcl_seis_odata_utils=>lanzar_excepcion( 'Error al convertir unidades' ).
        ENDIF.

        WRITE vl_cantidad_entrada TO vl_cantidad_entrada_c DECIMALS 0. CONDENSE vl_cantidad_entrada_c.
        zcl_seis_odata_utils=>lanzar_excepcion( |Ha introducido un diámetro mayor a la de la recepción inicial de la UA ({ vl_cantidad_entrada_c } cm)| ).

      ELSE.
        vl_unidad_entrada = SWITCH #( wl_estacion-gestiona_palets WHEN 'X' THEN 'ST' ELSE 'M' ).
        CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
          EXPORTING
            i_matnr              = <fs_ltap>-matnr
            i_in_me              = <fs_ltap>-gewei
            i_out_me             = vl_unidad_entrada
            i_menge              = vl_menge
          IMPORTING
            e_menge              = vl_cantidad_entrada
          EXCEPTIONS
            error_in_application = 1
            error                = 2
            OTHERS               = 3.
        IF sy-subrc <> 0.
          zcl_seis_odata_utils=>lanzar_excepcion( ).
        ENDIF.

        CALL FUNCTION 'CONVERSION_EXIT_CUNIT_OUTPUT'
          EXPORTING
            input          = vl_unidad_entrada
          IMPORTING
            output         = vl_unidad_entrada_c
          EXCEPTIONS
            unit_not_found = 1
            OTHERS         = 2.
      ENDIF.

      WRITE vl_cantidad_entrada TO vl_cantidad_entrada_c DECIMALS 0. CONDENSE vl_cantidad_entrada_c.
      zcl_seis_odata_utils=>lanzar_excepcion( |Ha introducido una cantidad mayor a la de la recepción inicial de la UA ({ vl_cantidad_entrada_c } { vl_unidad_entrada_c })| ).
    ENDIF.

    vp_meins = 'KG'.

    DATA: wl_header TYPE bapi2017_gm_head_01,
          wl_code   TYPE bapi2017_gm_code,
          tl_item   TYPE STANDARD TABLE OF bapi2017_gm_item_create,
          tl_return TYPE bapiret2_t.

    SELECT *
      FROM ztwm001
      INTO TABLE @DATA(tl_hardcodes)
      WHERE cprog  = @cl_bhs_bobina AND
            param2 = @cl_param1.



    "codigo de tipo de movimiento de mercancias
    wl_code-gm_code = cl_code.

    "Datos de cabecera
    wl_header-pstng_date = sy-datlo.
    wl_header-doc_date   = sy-datlo.
*  wl_header-ref_doc_no = pi_orden_bhs.
    wl_header-pr_uname   = sy-uname.
    READ TABLE tl_hardcodes ASSIGNING FIELD-SYMBOL(<fs_hardcodes>) WITH KEY param1 = 'HEADER_TEXT'.
    IF sy-subrc = 0.
      wl_header-header_txt = <fs_hardcodes>-valor1.
    ENDIF.

    "Datos de la UA que genera el movimiento
    APPEND INITIAL LINE TO tl_item ASSIGNING FIELD-SYMBOL(<fs_item>).
    <fs_item>-plant     = vl_werks.
    <fs_item>-stge_loc  = wl_lqua-lgort.
    <fs_item>-material  = wl_lqua-matnr.
    <fs_item>-batch     = wl_lqua-charg.
    READ TABLE tl_hardcodes ASSIGNING <fs_hardcodes> WITH KEY param1 = 'MOVEMENT_TYPE_INV'.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Falta parametrizar movimiento de inventario' ).
    ENDIF.
    <fs_item>-move_type = <fs_hardcodes>-valor1.

    CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
      EXPORTING
        i_matnr              = wl_lqua-matnr
        i_in_me              = wl_lqua-meins
        i_out_me             = 'KG'
        i_menge              = wl_lqua-verme
      IMPORTING
        e_menge              = wl_lqua-verme
      EXCEPTIONS
        error_in_application = 1
        error                = 2
        OTHERS               = 3.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al convertir cantidad a KG' ).
    ENDIF.
    wl_lqua-meins = 'KG'.

    "Cantidad del movimiento: Cantidad a regularizar - stock actual
    <fs_item>-entry_qnt = trunc( vp_menge - wl_lqua-verme ).
    <fs_item>-entry_uom = vp_meins.
    CHECK <fs_item>-entry_qnt <> 0.

    <fs_item>-stge_bin  = wl_lqua-lgpla.


    DATA: vl_mblnr TYPE bapi2017_gm_head_ret-mat_doc,
          vl_mjahr TYPE bapi2017_gm_head_ret-doc_year.
    CALL FUNCTION 'BAPI_GOODSMVT_CREATE'
      EXPORTING
        goodsmvt_header  = wl_header
        goodsmvt_code    = wl_code
      IMPORTING
        materialdocument = vl_mblnr
        matdocumentyear  = vl_mjahr
      TABLES
        goodsmvt_item    = tl_item
        return           = tl_return.
    zcl_seis_odata_utils=>lanzar_excepcion( bapiret2_t = tl_return ).


    CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
      EXPORTING
        wait = 'X'.

    " Ahora seleccionamos el cuanto generado por el alta 712, sin UA
    SELECT SINGLE *
      INTO @DATA(wl_lqua_new)
            FROM lqua
            WHERE lgnum = @vl_lgnum AND
            wenum EQ @vl_mblnr. " documento EM


    "Crear orden de transporte
    DATA: tl_trite TYPE l03b_trite_t.
    SELECT SINGLE *
     INTO @DATA(wl_ltbk)
     FROM ltbk
     WHERE lgnum = @wl_lqua-lgnum AND
           mblnr = @vl_mblnr AND
           mjahr = @vl_mjahr.

    IF sy-subrc = 0.
      SELECT *
        INTO TABLE @DATA(tl_ltbp)
        FROM ltbp
        WHERE lgnum = @wl_ltbk-lgnum AND
              tbnum = @wl_ltbk-tbnum.
    ENDIF.

    LOOP AT tl_ltbp ASSIGNING FIELD-SYMBOL(<fs_ltbp>).
      APPEND INITIAL LINE TO tl_trite ASSIGNING FIELD-SYMBOL(<fs_trite>).
      <fs_trite>-tbpos = <fs_ltbp>-tbpos.
      <fs_trite>-altme = <fs_ltbp>-meins.
      <fs_trite>-anfme = <fs_ltbp>-menge.
      <fs_trite>-charg = <fs_ltbp>-charg.
*      <fs_trite>-nlpla = vl_nlpla. " hay que indicar la ubicación de la estación
*      <fs_trite>-nltyp = vl_lgtyp. " hay que indicar la ubicación de la estación
      <fs_trite>-vlpla = wl_lqua_new-lgpla. " ubicación origen
      <fs_trite>-vltyp = wl_lqua_new-lgtyp. " origen
      <fs_trite>-vlenr = wl_lqua_new-lenum. " origen no tiene UA

      <fs_trite>-nlpla = wl_lqua-lgpla. " ubicación estación destino
      <fs_trite>-nltyp = wl_lqua-lgtyp. " tipo alm.estación destino
      <fs_trite>-nlenr = wl_lqua-lenum. " UA input
      <fs_trite>-letyp = wl_lqua-letyp. " tipo UA original
    ENDLOOP.

    DATA vl_tanum TYPE ltak-tanum.
    CALL FUNCTION 'L_TO_CREATE_TR'
      EXPORTING
        i_lgnum                        = wl_ltbk-lgnum
        i_tbnum                        = wl_ltbk-tbnum
        i_squit                        = abap_true
        i_tbeli                        = abap_true
        it_trite                       = tl_trite
      IMPORTING
        e_tanum                        = vl_tanum
      EXCEPTIONS
        foreign_lock                   = 1
        qm_relevant                    = 2
        tr_completed                   = 3
        xfeld_wrong                    = 4
        ldest_wrong                    = 5
        drukz_wrong                    = 6
        tr_wrong                       = 7
        squit_forbidden                = 8
        no_to_created                  = 9
        update_without_commit          = 10
        no_authority                   = 11
        preallocated_stock             = 12
        partial_transfer_req_forbidden = 13
        input_error                    = 14
        error_message                  = 16
        OTHERS                         = 15.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( ).
    ENDIF.

    IF wl_lqua-lgpla <> vl_nlpla. " si la ubicación destino anterior (stock actual de la UA si existe) no es la estación, movemos ahora el cuanto con la ctd total a la estación
      zcl_wm_nt_generic=>movebobine(
              EXPORTING
                iv_bobine           = vp_ua
                iv_dest_nlpla       = vl_nlpla
                iv_lgnum            = wl_ltbk-lgnum
                iv_bwlvs            = '999'
                iv_tipo_mov_bobina  = zcl_wm_nt_generic=>gc_tipo_mov_bobina_reubicar
                iv_validar_fsc_recycled = space "Validación FSC Recycled sólo para el carretillero
              IMPORTING
              es_ltak       = DATA(ls_ltak)
              ev_subrc      = DATA(lv_subrc)
                    ).

*      CALL FUNCTION 'L_TO_CREATE_SINGLE'
*        EXPORTING
*          i_lgnum               = wl_ltbk-lgnum
*          i_bwlvs               = '999'
*          i_matnr               = wl_lqua-matnr    " Número de material
*          i_werks               = wl_lqua-werks    " Centro
*****        i_bestq               = ls_lqua-bestq    " Diferenciación de stock
*          i_letyp               = wl_lqua-letyp    " Tipo de unidad de almacén
*          i_anfme               = vp_menge    " Ctd. solicitada
*          i_altme               = vp_meins    " Unidad de medida
*          i_lznum               = wl_ltbk-lznum    " Número adicional de ref.
*          i_squit               = abap_true    " Confirm. inmed.
*          i_vltyp               = wl_lqua-lgtyp   " Tipo de almacén 'desde'
*          i_vlpla               = wl_lqua-lgpla   " Ubic. 'desde'
*          i_vlenr               = vp_ua           " Uni-alm procedencia
*          i_nltyp               = vl_lgtyp        " Tipo de almacén 'hacia'
*          i_nlpla               = vl_nlpla        " Ubic. 'hacia'
*          i_nlenr               = vp_ua           " Uni.almacén destino
*          i_kompl               = space    " Crear OT aun si no se cubre la cantidad total -> cvivo - Es necesario desmarcarlo porque si se lanza para UMA, si la conversión
*        IMPORTING
*          e_tanum               = vl_tanum   " Número de orden de transporte
*        EXCEPTIONS
*          no_to_created         = 1
*          bwlvs_wrong           = 2
*          betyp_wrong           = 3
*          benum_missing         = 4
*          betyp_missing         = 5
*          foreign_lock          = 6
*          vltyp_wrong           = 7
*          vlpla_wrong           = 8
*          vltyp_missing         = 9
*          nltyp_wrong           = 10
*          nlpla_wrong           = 11
*          nltyp_missing         = 12
*          rltyp_wrong           = 13
*          rlpla_wrong           = 14
*          rltyp_missing         = 15
*          squit_forbidden       = 16
*          manual_to_forbidden   = 17
*          letyp_wrong           = 18
*          vlpla_missing         = 19
*          nlpla_missing         = 20
*          sobkz_wrong           = 21
*          sobkz_missing         = 22
*          sonum_missing         = 23
*          bestq_wrong           = 24
*          lgber_wrong           = 25
*          xfeld_wrong           = 26
*          date_wrong            = 27
*          drukz_wrong           = 28
*          ldest_wrong           = 29
*          update_without_commit = 30
*          no_authority          = 31
*          material_not_found    = 32
*          lenum_wrong           = 33
*          error_message         = 34
*          OTHERS                = 35.
      IF lv_subrc <> 0.
        zcl_seis_odata_utils=>lanzar_excepcion( ).
      ENDIF.
    ENDIF.


    "Impresión
    DATA(vl_cod_maq_portabo) = CONV zwm_param-maquina( vl_nlpla ).
    DATA tl_selection_table TYPE TABLE OF rsparamsl_255.
    EXPORT lv_vengo_inter_consumo = abap_true           TO MEMORY ID 'VENGO_CONSUMO' .
    EXPORT lv_cod_maq_portabo     = vl_cod_maq_portabo  TO MEMORY ID 'MAQUINA' .

    " create label for remaining

    tl_selection_table = VALUE #( ( selname = 'P_LGNUM' kind = 'P' sign = 'I' option = 'EQ' low = vl_lgnum )
                                  ( selname = 'S_LENUM' kind = 'S' sign = 'I' option = 'EQ' low = vp_ua ) ).
    SUBMIT zimpresion_ua WITH SELECTION-TABLE tl_selection_table AND RETURN.
    FREE MEMORY ID: 'VENGO_CONSUMO', 'MAQUINA'.

  ENDMETHOD.
  METHOD TRATAR_KEY_ENTITY.


    DATA(vl_source_entity_type_name)  = io_tech_request_context->get_source_entity_type_name( ).
    DATA(vl_entity_type_name)         = io_tech_request_context->get_entity_type_name( ).


    CASE vl_source_entity_type_name.
*      WHEN 'AlbaranesCab'.
*        DATA wl_albaranes_cab TYPE zmm_ui5_s_albaranes_cab.
*        io_tech_request_context->get_converted_source_keys( IMPORTING es_key_values  = wl_albaranes_cab ).
*        wl_albaranes_cab = albaranes_cab_getdetail(
*                              vp_werks         = wl_albaranes_cab-werks
*                              vp_contador      = wl_albaranes_cab-contador
*                              vp_posicion_alb  = wl_albaranes_cab-posicion_alb ).
*        MOVE-CORRESPONDING wl_albaranes_cab TO wp_entity.
*
      WHEN OTHERS.
        io_tech_request_context->get_converted_keys( IMPORTING es_key_values  = wp_entity ).
    ENDCASE.

  ENDMETHOD.
  METHOD TRATAR_KEY_ENTITYSET.
    DATA(vl_source_entity_type_name)  = io_tech_request_context->get_source_entity_type_name( ).
    DATA(vl_entity_type_name)         = io_tech_request_context->get_entity_type_name( ).

    CASE vl_source_entity_type_name.
*      WHEN 'TiposFichaje'.
*        DATA wl_tipos_fichaje TYPE zhr_s_ss_tipos_fichaje.
*        io_tech_request_context->get_converted_source_keys( IMPORTING es_key_values  = wl_tipos_fichaje ).
*        wl_tipos_fichaje = tipos_fichaje_getdetail( wl_tipos_fichaje-id ).
*        MOVE-CORRESPONDING wl_tipos_fichaje TO wp_entity.
*
      WHEN OTHERS.
        io_tech_request_context->get_converted_source_keys( IMPORTING es_key_values  = wp_entity ).
    ENDCASE.


  ENDMETHOD.
  METHOD tvarvcset_get_entity.
    IF lines( it_key_tab ) = 0.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception
        EXPORTING
          textid = VALUE #( msgid = 'ZUI5' msgno = '001' ).
    ENDIF.
    TRY.
        DATA(lv_name) = CONV tvarvc-name( it_key_tab[ name = 'Name' ]-value ).
        DATA(lv_type) = CONV tvarvc-type( it_key_tab[ name = 'Type' ]-value ).
        DATA(lv_numb) = CONV tvarvc-numb( it_key_tab[ name = 'Numb' ]-value ).
      CATCH cx_root.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception
          EXPORTING
            textid = VALUE #( msgid = 'ZUI5' msgno = '001' ).

    ENDTRY.

    SELECT SINGLE * FROM tvarvc INTO CORRESPONDING FIELDS OF er_entity
      WHERE name = lv_name AND type = lv_type AND numb = lv_numb.


  ENDMETHOD.
  METHOD tvarvcset_get_entityset.

    SELECT * FROM tvarvc UP TO 10 ROWS
      INTO CORRESPONDING FIELDS OF TABLE et_entityset
      WHERE (iv_filter_string).
  ENDMETHOD.
  METHOD unidadesalmaset_get_entity.
    tratar_key_entity( EXPORTING io_tech_request_context = io_tech_request_context
                       IMPORTING wp_entity               = er_entity ).

    DATA(lv_werks)  =  zcl_wm_nt_generic=>read_http_header_val(  iv_name   = zcl_wm_nt_generic=>gc_http_head_werks
                                                                 iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) ) ).
    DATA(wl_centro)   = zcl_wm_nt_generic=>get_instance( )->centros_getdetail(  vp_werks      = CONV #( lv_werks ) ).

    IF er_entity-lgnum IS INITIAL.
      er_entity-lgnum = zcl_wm_nt_generic=>get_lgnum( iv_werks = wl_centro-werks ).
    ENDIF.

    er_entity = zcl_wm_nt_generic=>get_instance( )->unidades_alma_getdetail( vp_lgnum = er_entity-lgnum
                                                                             vp_lenum = er_entity-lenum ).

    DATA(lv_nlpla) =  zcl_wm_nt_generic=>read_http_header_val( iv_name = zcl_wm_nt_generic=>gc_http_head_nlpla
                                                               iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) ) ).

    IF lv_nlpla IS NOT INITIAL.
      IF wl_centro-consumos_bobinas_metros = 'X'.
        TRY .
            DATA(wl_estacion) = zcl_wm_nt_generic=>get_instance( )->maquinas_getdetail( vp_machine_id = CONV #( lv_nlpla ) ).
          CATCH /iwbep/cx_mgw_busi_exception.
        ENDTRY.
        DATA(vl_meins) = SWITCH meins( wl_estacion-gestiona_palets  WHEN 'X' THEN 'ST' ELSE 'M' ).
        CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
          EXPORTING
            i_matnr              = er_entity-matnr
            i_in_me              = er_entity-meins
            i_out_me             = vl_meins
            i_menge              = er_entity-verme
          IMPORTING
            e_menge              = er_entity-verme
          EXCEPTIONS
            error_in_application = 1
            error                = 2
            OTHERS               = 3.
        IF sy-subrc <> 0.
          zcl_seis_odata_utils=>lanzar_excepcion( 'Error convirtiendo unidad' ).
        ENDIF.
        er_entity-meins = vl_meins.
      ELSE.
        zcl_wm_nt_generic=>conv_mat_diametro( EXPORTING
                                                iv_matnr          = er_entity-matnr
                                                iv_qty            = er_entity-verme    " Cantidad de pedido
                                                iv_meins          = er_entity-meins
                                                iv_dest_meins     = zcl_wm_nt_generic=>gc_meins_dia
                                                iv_werks          = wl_centro-werks
                                              RECEIVING rv_qty = DATA(lv_diametro)
                                              EXCEPTIONS
                                                um_no_valid       = 1
                                                missing_constants = 2
                                                missing_matnr     = 3
                                                no_base_calc      = 4
                                                OTHERS            = 5
                                                ).
        IF sy-subrc EQ 0.
          er_entity-verme = lv_diametro.
          er_entity-meins = zcl_wm_nt_generic=>gc_meins_dia.
        ENDIF.
      ENDIF.
    ENDIF.

  ENDMETHOD.
  METHOD validar_consumo.
    DATA r_metros TYPE RANGE OF zwm_intf_bhs-metros_restantes.
    DATA r_diametro TYPE RANGE OF zwm_intf_bhs-diametro_rest.

    TRY .

        DATA(vl_werks) =  zcl_wm_nt_generic=>read_http_header_val( iv_name = zcl_wm_nt_generic=>gc_http_head_werks
                                                                   iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) ) ).
        DATA(vl_nlpla) =  zcl_wm_nt_generic=>read_http_header_val( iv_name = zcl_wm_nt_generic=>gc_http_head_nlpla
                                                                   iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) ) ).
        DATA(wl_centro)   = zcl_wm_nt_generic=>get_instance( )->centros_getdetail(  vp_werks      = CONV #( vl_werks ) ).
      CATCH /iwbep/cx_mgw_tech_exception.
        zcl_seis_odata_utils=>lanzar_excepcion( 'Error al recuperar datos de cabecera' ).
    ENDTRY.

    " DEL - cvivo - Lo descarto porque técnicamente no deben consumir n veces la bobina en la reserva
*    CASE wl_centro-lista_bobinas_metros.
*      WHEN abap_true.
*        r_metros = VALUE #( sign = 'I' option = 'EQ' ( low = vp_cantidad ) ).
*      WHEN abap_false.
*        r_diametro = VALUE #( sign = 'I' option = 'EQ' ( low = vp_cantidad ) ).
*    ENDCASE.

    IF vp_reserva IS INITIAL. " cvivo - controlamos esto, no debería pasar
      zcl_seis_odata_utils=>lanzar_excepcion( 'Consumo sin número de reserva' ).
    ELSE.
      DATA r_reserva TYPE RANGE OF rsnum.
      r_reserva = VALUE #( sign = 'I' option = 'EQ' ( low = vp_reserva high = space )
                                                    ( low = '0000000000' high = space ) ).
    ENDIF.


    SELECT *
      INTO TABLE @DATA(tl_zwm_intf_bhs)
      UP TO 1 ROWS
      FROM zwm_intf_bhs
      WHERE werks             = @vl_werks     AND
            porta_bobinas     = @vl_nlpla     AND
            cod_etiqueta      = @vp_bobina    AND
*            fecha             = @sy-datlo     AND
*            metros_restantes  IN @r_metros   AND
*            diametro_rest     IN @r_diametro AND
            rsnum             IN @r_reserva
    ORDER BY id DESCENDING.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al generar el consumo.' ).
    ELSEIF tl_zwm_intf_bhs[ 1 ]-contabilizado EQ abap_false.

      " javier aquí se debería limpiar la UA en UI5, porque el registro ha pasado a tabla, pero está pendiente
      "Para que el sistema haga eso, lo mejor es que no sea una excepción, pero que sí que pase el mensaje...
*      zcl_seis_odata_utils=>lanzar_excepcion( tl_zwm_intf_bhs[ 1 ]-message ).
      wp_return-type    = 'W'.
*      wp_return-message =  tl_zwm_intf_bhs[ 1 ]-message.
      wp_return-message = 'El consumo ha sido registrado pero permanece pendiente de procesar, no repita el consumo'.

    ENDIF.


  ENDMETHOD.
  METHOD validar_lectura_ua.

    TRY .
        DATA(vl_werks) =  zcl_wm_nt_generic=>read_http_header_val( iv_name = zcl_wm_nt_generic=>gc_http_head_werks
                                                                   iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) ) ).
        DATA(vl_lgnum)    = zcl_wm_nt_generic=>get_lgnum( iv_werks = CONV #( vl_werks ) ).
        DATA(wl_centro)   = zcl_wm_nt_generic=>get_instance( )->centros_getdetail(  vp_werks      = CONV #( vl_werks ) ).
      CATCH /iwbep/cx_mgw_tech_exception.
        zcl_seis_odata_utils=>lanzar_excepcion( 'Error al recuperar datos de cabecera' ).
    ENDTRY.


    "Ticket 0000065607. Doble check UA
    IF wl_centro-lectura_bobinas_propuestas = 'X'.
      SELECT ltap~lgnum, ltap~tanum, ltap~tapos, ltbk~rsnum, ltbk~tbnum
        INTO TABLE @DATA(t_ltap)
        FROM ltap INNER JOIN ltak ON  ltak~lgnum = ltap~lgnum AND
                                      ltak~tanum = ltap~tanum
                  INNER JOIN ltbk ON  ltak~benum = ltbk~benum AND
                                      ltak~lgnum = ltbk~lgnum AND
                                      ltap~nlpla = ltbk~nlpla
        WHERE ltak~lgnum = @vl_lgnum AND
*              ltbk~rsnum = @vp_id_pedido    AND " cvivo - primero miramos si se ha leído, luego si a la misma reserva
              ltap~vlenr = @vp_id_bobina
        ORDER BY ltak~tanum DESCENDING.

      IF sy-subrc <> 0.
        wp_return-type      = 'W'.
        wp_return-id        = 'SY'.
        wp_return-number    = '002'.
        wp_return-message   = 'La UA introducida no se ha aprovisionado para esta estación ¿Quiere continuar?'.
        RETURN.
      ELSEIF NOT line_exists( t_ltap[ rsnum = vp_id_pedido ] ). " cvivo - se ha leído, pero en otra reserva
        DATA(wl_ltap) = t_ltap[ 1 ].

        SELECT SINGLE upper_orderid FROM zwm_ltbk_adit
          INTO @DATA(pedido_aprov)
          WHERE lgnum EQ @vl_lgnum
            AND tbnum EQ @wl_ltap-tbnum.

        wp_return-type      = 'W'.
        wp_return-id        = 'SY'.
        wp_return-number    = '002'.
        wp_return-message   = |La UA introducida no se ha aprovisionado para el pedido actual, sino para el pedido { pedido_aprov } ¿Quiere continuar? |.
        RETURN.
      ENDIF.
    ENDIF.
    "Fin Ticket 0000065607. Doble check UA



  ENDMETHOD.
  METHOD validar_regularizar_ua.

    TRY .

        DATA(vl_werks) =  zcl_wm_nt_generic=>read_http_header_val( iv_name = zcl_wm_nt_generic=>gc_http_head_werks
                                                                   iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) ) ).
        DATA(vl_nlpla) =  zcl_wm_nt_generic=>read_http_header_val( iv_name = zcl_wm_nt_generic=>gc_http_head_nlpla
                                                                   iv_facade = CAST #( /iwbep/if_mgw_conv_srv_runtime~get_dp_facade( ) ) ).
        DATA(vl_lgnum)    = zcl_wm_nt_generic=>get_lgnum( iv_werks = CONV #( vl_werks ) ).
        DATA(wl_centro)   = zcl_wm_nt_generic=>get_instance( )->centros_getdetail(  vp_werks      = CONV #( vl_werks ) ).
      CATCH /iwbep/cx_mgw_tech_exception.
        zcl_seis_odata_utils=>lanzar_excepcion( 'Error en parámetros' ).
    ENDTRY.


    SELECT SINGLE *
      INTO @DATA(wl_ltap)
      FROM ltap
      WHERE lgnum = @vl_lgnum AND
            nlenr = @vp_ua.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'La UA introducida no existe en el sistema./nVerifique que ha leído el código de barras correcto'(005) ).
    ENDIF.

    TRY .
        wp_unidad_alma = zcl_wm_nt_generic=>get_instance( )->unidades_alma_getdetail( vp_lgnum = wl_ltap-lgnum
                                                                                      vp_lenum = wl_ltap-nlenr ).
      CATCH /iwbep/cx_mgw_busi_exception.
    ENDTRY.

    DATA(wl_estacion) = zcl_wm_nt_generic=>get_instance( )->maquinas_getdetail( vp_machine_id = CONV #( vl_nlpla ) ).



    DATA(vl_meins) = SWITCH meins( wl_estacion-gestiona_palets  WHEN 'X' THEN 'ST' ELSE 'M' ).

    IF vl_nlpla IS NOT INITIAL AND wp_unidad_alma IS NOT INITIAL.
      CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
        EXPORTING
          i_matnr              = wp_unidad_alma-matnr
          i_in_me              = wp_unidad_alma-meins
          i_out_me             = vl_meins
          i_menge              = wp_unidad_alma-verme
        IMPORTING
          e_menge              = wp_unidad_alma-verme
        EXCEPTIONS
          error_in_application = 1
          error                = 2
          OTHERS               = 3.
      IF sy-subrc = 0.
        wp_unidad_alma-meins = vl_meins.
      ELSE.
        zcl_seis_odata_utils=>lanzar_excepcion( 'Error convirtiendo unidad' ).
      ENDIF.
    ENDIF.


    IF wl_centro-regularizar_bobinas_diam = 'X'.
      vl_meins = zcl_wm_nt_generic=>gc_meins_dia.

      IF vl_nlpla IS NOT INITIAL AND wp_unidad_alma IS NOT INITIAL.
        zcl_wm_nt_generic=>conv_mat_diametro(
          EXPORTING iv_matnr          = wp_unidad_alma-matnr
                    iv_qty            = wp_unidad_alma-verme
                    iv_meins          = wp_unidad_alma-meins
                    iv_dest_meins     = vl_meins
                    iv_werks          = wp_unidad_alma-werks
          RECEIVING rv_qty = wp_unidad_alma-verme
          EXCEPTIONS  um_no_valid       = 1
                      missing_constants = 2
                      missing_matnr     = 3
                      no_base_calc      = 4
                      OTHERS            = 5 ).
        IF sy-subrc <> 0.
          zcl_seis_odata_utils=>lanzar_excepcion( 'Error convirtiendo unidad' ).
        ENDIF.
      ENDIF.
    ENDIF.

    IF wp_unidad_alma IS INITIAL.
      wp_unidad_alma-matnr = wl_ltap-matnr.
      wp_unidad_alma-lenum = vp_ua.
      wp_unidad_alma-verme = 0.
    ENDIF.

    wp_unidad_alma-meins = vl_meins.

  ENDMETHOD.
