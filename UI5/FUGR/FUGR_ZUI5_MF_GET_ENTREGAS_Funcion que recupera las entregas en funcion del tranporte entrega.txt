FUNCTION zui5_mf_get_entregas.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     VALUE(I_TRANSPORTE) TYPE  TKNUM OPTIONAL
*"     VALUE(I_ENTREGA) TYPE  VBELN_VL OPTIONAL
*"  EXPORTING
*"     VALUE(O_LISTADO_ENTREGAS) TYPE  ZUI5_TT_ENTREGAS
*"     VALUE(O_BAPI_RETURN) TYPE  BAPIRET2
*"----------------------------------------------------------------------

  TYPES: BEGIN OF ty_listado_entregas,
           vbeln TYPE vbeln_vl,
           erdat TYPE erdat,
           kunag TYPE kunag,
           kunnr TYPE kunnr,
         END OF ty_listado_entregas.

  TYPES: BEGIN OF ty_solicitante,
           kunnr TYPE kunnr,
           name1 TYPE name1_gp,
         END OF ty_solicitante.

  TYPES: BEGIN OF ty_destinatario,
           kunnr TYPE kunnr,
           ort01 TYPE ort01,
           land1 TYPE land1,
           name1 TYPE name1_gp,
         END OF ty_destinatario.

  TYPES: BEGIN OF ty_estado,
           vbeln TYPE vbeln,
           kostk TYPE kostk,
         END OF ty_estado.

  DATA: ls_listado_entregas TYPE ty_listado_entregas,
        lt_listado_entregas TYPE TABLE OF ty_listado_entregas,
        ls_solicitante      TYPE ty_solicitante,
        lt_solicitante      TYPE  TABLE OF ty_solicitante,
        ls_destinatario     TYPE ty_destinatario,
        lt_destinatario     TYPE TABLE OF ty_destinatario,
        lv_estado_picking   TYPE kostk,
        ls_estado           TYPE ty_estado,
        lt_estado           TYPE TABLE OF ty_estado.

  DATA: ls_entrega               TYPE zui5_s_entregas,
        lt_posiciones_transporte TYPE TABLE OF vttp.

*********** CASO1: LECTURA DE UNA ENTREGA ******************
  IF i_entrega <> ''.

    SELECT SINGLE vbeln erdat kunag kunnr
      INTO CORRESPONDING FIELDS OF ls_listado_entregas
      FROM likp
      WHERE vbeln = i_entrega.
    IF sy-subrc <> 0.
*si no existe la entrega devolvemos un error
      o_bapi_return-type = 'S'.
      o_bapi_return-id = 'Z_WM'.
      o_bapi_return-number = '008'.

      CALL FUNCTION 'BAPI_MESSAGE_GETDETAIL'
        EXPORTING
          id         = o_bapi_return-id
          number     = o_bapi_return-number
          language   = sy-langu
          textformat = 'ASC'
        IMPORTING
          message    = o_bapi_return-message.
      EXIT.

    ENDIF.

*Recuperamos el nombre del solicitante
    SELECT SINGLE kunnr name1
      INTO CORRESPONDING FIELDS OF ls_solicitante
      FROM kna1
      WHERE kunnr = ls_listado_entregas-kunag.

*Recuperamos la poblacion y pais del destinatario
    SELECT SINGLE kunnr ort01 land1 name1
      INTO CORRESPONDING FIELDS OF ls_destinatario
      FROM kna1
      WHERE kunnr = ls_listado_entregas-kunnr.

*Recuperamos el estado de picking
    SELECT SINGLE kostk
      INTO lv_estado_picking
      FROM vbuk
  WHERE vbeln = i_entrega.

*Preparamos los datos de salida de la entrega
    ls_entrega-cliente = ls_listado_entregas-kunag.
    ls_entrega-destinatario = ls_listado_entregas-kunnr.
    ls_entrega-entrega = ls_listado_entregas-vbeln.
    ls_entrega-fecha = ls_listado_entregas-erdat.
    ls_entrega-nombre_cliente = ls_solicitante-name1.
    ls_entrega-poblacion_dest = ls_destinatario-ort01.
    ls_entrega-pais_dest = ls_destinatario-land1.
    ls_entrega-estado = lv_estado_picking.
    ls_entrega-nombre_dest = ls_destinatario-name1.

    APPEND ls_entrega TO o_listado_entregas.

  ELSE.
************************* CASO 2: LECTURA DE UN TRANSPORTE ***************************

    SELECT *
      INTO CORRESPONDING FIELDS OF TABLE lt_posiciones_transporte
      FROM vttp
      WHERE tknum = i_transporte.
    IF sy-subrc <> 0.
*si no existe el transporte devolvemos un error
      o_bapi_return-type = 'S'.
      o_bapi_return-id = 'Z_WM'.
      o_bapi_return-number = '009'.

      CALL FUNCTION 'BAPI_MESSAGE_GETDETAIL'
        EXPORTING
          id         = o_bapi_return-id
          number     = o_bapi_return-number
          language   = sy-langu
          textformat = 'ASC'
        IMPORTING
          message    = o_bapi_return-message.

    ENDIF.

IF lt_posiciones_transporte IS NOT INITIAL.

*Recuperamos las entregas asociadas al transporte
    SELECT vbeln erdat kunag kunnr
      INTO CORRESPONDING FIELDS OF TABLE lt_listado_entregas
      FROM likp
      FOR ALL ENTRIES IN lt_posiciones_transporte
      WHERE vbeln = lt_posiciones_transporte-vbeln.

*Recuperamos el nombre de los solicitantes de las entregas
    SELECT kunnr name1
    INTO CORRESPONDING FIELDS OF TABLE lt_solicitante
    FROM kna1
   FOR ALL ENTRIES IN lt_listado_entregas
   WHERE kunnr = lt_listado_entregas-kunag.

*Recuperamos la poblacion y el pais de los destinatarios de mercancia
    SELECT kunnr ort01 land1 name1
     INTO CORRESPONDING FIELDS OF TABLE lt_destinatario
     FROM kna1
     FOR ALL ENTRIES IN lt_listado_entregas
     WHERE kunnr = lt_listado_entregas-kunnr.

*Recuperamos los estados de picking de cada una de las entregas
    SELECT vbeln kostk
      INTO CORRESPONDING FIELDS OF TABLE lt_estado
      FROM vbuk
      FOR ALL ENTRIES IN lt_listado_entregas
      WHERE vbeln = lt_listado_entregas-vbeln.



*Preparamos los datos de salida de las entregas

    LOOP AT lt_listado_entregas INTO ls_listado_entregas.

      CLEAR ls_entrega.

      ls_entrega-entrega = ls_listado_entregas-vbeln.
      ls_entrega-cliente = ls_listado_entregas-kunag.
      ls_entrega-destinatario = ls_listado_entregas-kunnr.
      ls_entrega-fecha = ls_listado_entregas-erdat.

*Recuperamos el estado de la entrega
      READ TABLE lt_estado INTO ls_estado WITH KEY vbeln = ls_listado_entregas-vbeln.
      IF sy-subrc = 0.
        ls_entrega-estado = ls_estado-kostk.
      ENDIF.

*Recuperamos el nombre del solicitante
      READ TABLE lt_solicitante INTO ls_solicitante WITH KEY kunnr = ls_listado_entregas-kunag.
      IF sy-subrc = 0.
        ls_entrega-nombre_cliente = ls_solicitante-name1.
      ENDIF.

*Recuperamos poblacion y pais del destinatario de mercancia
      READ TABLE lt_destinatario INTO ls_destinatario WITH KEY kunnr = ls_listado_entregas-kunnr.
      IF sy-subrc = 0.
        ls_entrega-poblacion_dest = ls_destinatario-ort01.
        ls_entrega-pais_dest = ls_destinatario-land1.
        ls_entrega-nombre_dest = ls_destinatario-name1.
      ENDIF.

      APPEND ls_entrega TO o_listado_entregas.
    ENDLOOP.
  ENDIF.
  ENDIF.







ENDFUNCTION.
