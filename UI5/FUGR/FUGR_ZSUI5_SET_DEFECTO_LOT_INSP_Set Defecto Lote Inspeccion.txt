FUNCTION zsui5_set_defecto_lot_insp.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     VALUE(I_GRUPO_DEFECTO) TYPE  FEGRP
*"     VALUE(I_CODIGO_DEFECTO) TYPE  FECOD
*"     VALUE(I_LOTE) TYPE  QPLOS
*"  EXPORTING
*"     VALUE(ET_RETURN) TYPE  BAPIRET2_T
*"----------------------------------------------------------------------
  DATA: t_qmifetab TYPE TABLE OF  qmife,
        t_qierrtab TYPE TABLE OF  qierr.

  CHECK i_grupo_defecto IS NOT INITIAL AND i_codigo_defecto IS NOT INITIAL.

  APPEND INITIAL LINE TO t_qmifetab ASSIGNING FIELD-SYMBOL(<ls_qmifetab>).
  <ls_qmifetab>-prueflos = i_lote.
  <ls_qmifetab>-satzart = 'Q90'.
  <ls_qmifetab>-fegrp = i_grupo_defecto.
  <ls_qmifetab>-fecod = i_codigo_defecto.
  <ls_qmifetab>-fekat = '9'.
  <ls_qmifetab>-posnr = '0001'.
  <ls_qmifetab>-anzfehler = '0000001'.
  <ls_qmifetab>-fenam = sy-uname.
  <ls_qmifetab>-fedat = sy-datum.
  <ls_qmifetab>-fzeit = sy-uzeit.

  CALL FUNCTION 'QIRF_GET_DEFECT_ITEMS2'
    EXPORTING
      i_subsys      = '000001'
    TABLES
      t_qmifetab    = t_qmifetab
      t_qierrtab    = t_qierrtab
    EXCEPTIONS
      no_authority  = 1
      error_message = 2
      OTHERS        = 3.
  IF sy-subrc <> 0.
    APPEND INITIAL LINE TO et_return ASSIGNING FIELD-SYMBOL(<ls_return>).
    <ls_return>-type = 'W'.
    <ls_return>-id = 'ZSWM_UI5'.
    <ls_return>-number = '208'.
    <ls_return>-message_v1 = i_grupo_defecto.
    <ls_return>-message_v2 = i_codigo_defecto.
    <ls_return>-message_v3 = |{ i_lote ALPHA = OUT }|.

    APPEND INITIAL LINE TO et_return ASSIGNING <ls_return>.
    <ls_return>-type      = sy-msgty.
    <ls_return>-id        = sy-msgid.
    <ls_return>-number    = sy-msgno.
    <ls_return>-message_v1 = sy-msgv1.
    <ls_return>-message_v2 = sy-msgv2.
    <ls_return>-message_v3 = sy-msgv3.
    <ls_return>-message_v4 = sy-msgv4.
    CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
    RETURN.
  ENDIF.

  READ TABLE t_qierrtab TRANSPORTING NO FIELDS WITH KEY msgtype = 'E'.
  IF sy-subrc = 0.
    CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
    LOOP AT t_qierrtab INTO DATA(ls_error).
      APPEND INITIAL LINE TO et_return ASSIGNING <ls_return>.
      <ls_return>-type = 'W'.
      <ls_return>-id = 'ZSWM_UI5'.
      <ls_return>-number = '208'.
      <ls_return>-message_v1 = i_grupo_defecto.
      <ls_return>-message_v2 = i_codigo_defecto.
      <ls_return>-message_v3 = |{ i_lote ALPHA = OUT }|.

      APPEND INITIAL LINE TO et_return ASSIGNING <ls_return>.
      <ls_return>-type = 'W'.
      <ls_return>-id = ls_error-msgid.
      <ls_return>-number = ls_error-msgnr.
      <ls_return>-message = ls_error-msgtext.
    ENDLOOP.
  ELSE.
    CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
      EXPORTING
        wait = 'X'.
  ENDIF.



ENDFUNCTION.
