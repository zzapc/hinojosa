FUNCTION zui5_cm_asignar_ent_transporte.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     VALUE(I_TRANSPORTE) TYPE  TKNUM
*"     VALUE(I_ENTREGA) TYPE  VBELN_VL
*"     VALUE(IT_ENTREGAS) TYPE  BAPISHPDELIVNUMBTAB OPTIONAL
*"  EXPORTING
*"     VALUE(O_RETURN) TYPE  BAPIRET2_T
*"----------------------------------------------------------------------
  CONSTANTS v_veces TYPE i VALUE 10.

  DATA: borrar_transporte TYPE flag,

        lt_return         TYPE bapiret2_t.

  DATA: headerdata       LIKE  bapishipmentheader,
        headerdataaction LIKE  bapishipmentheaderaction,
        itemdata         TYPE TABLE OF bapishipmentitem,
        itemdataaction   TYPE TABLE OF bapishipmentitemaction.
  DATA: r_entrega TYPE RANGE OF vbeln_vl.

  r_entrega = VALUE #( FOR ent IN it_entregas ( sign = 'I' option = 'EQ' low = ent-deliv_numb ) ).

  IF i_entrega IS NOT INITIAL.
    APPEND VALUE #( sign = 'I' option = 'EQ' low = i_entrega ) TO r_entrega.
  ENDIF.

  SELECT a~vbeln, b~vbtyp
    FROM vttp AS a
    INNER JOIN likp AS b ON b~vbeln = a~vbeln
    INTO TABLE @DATA(lt_entregas)
    WHERE tknum = @i_transporte
    AND b~vbeln = @i_entrega.
  IF sy-subrc NE 0.
    CLEAR: headerdata, headerdataaction, lt_return.
    headerdata-shipment_num = i_transporte.

    LOOP AT r_entrega INTO DATA(ls_entrega).
      APPEND INITIAL LINE TO itemdata ASSIGNING FIELD-SYMBOL(<ls_itemdata>).
      <ls_itemdata>-delivery =  ls_entrega-low.
      <ls_itemdata>-itenerary = '0001'.

      APPEND INITIAL LINE TO itemdataaction ASSIGNING FIELD-SYMBOL(<ls_itemdataaction>).
      <ls_itemdataaction>-delivery =  'A'.
      <ls_itemdataaction>-itenerary = 'A'.
    ENDLOOP.

    DO v_veces TIMES.
      CLEAR lt_return.
      CALL FUNCTION 'BAPI_SHIPMENT_CHANGE'
        EXPORTING
          headerdata       = headerdata
          headerdataaction = headerdataaction
        TABLES
          itemdata         = itemdata
          itemdataaction   = itemdataaction
          return           = lt_return.

      READ TABLE lt_return TRANSPORTING NO FIELDS WITH KEY type = 'E'.
      IF sy-subrc NE 0.
        EXIT.
      ELSE.
        WAIT UP TO 1 SECONDS.
        CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
      ENDIF.
    ENDDO.
    READ TABLE lt_return TRANSPORTING NO FIELDS WITH KEY type = 'E'.
    IF sy-subrc = 0.
      CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
    ELSE.
      CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
        EXPORTING
          wait = 'X'.
    ENDIF.
    APPEND LINES OF lt_return TO o_return.


  ELSE.

  ENDIF.




ENDFUNCTION.
