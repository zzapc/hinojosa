FUNCTION zui5_cm_anula_posicion_entrega.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     VALUE(IV_VBELN) TYPE  ZSCM_VBELN_TRASLADO
*"     VALUE(IV_POSNR) TYPE  POSNR_VL
*"  EXPORTING
*"     VALUE(O_RETURN) TYPE  BAPIRET2_T
*"     VALUE(E_ERROR) TYPE  XFELD
*"----------------------------------------------------------------------

  DATA tl_return TYPE bapiret2_t.
  DATA wl_header_data TYPE bapiibdlvhdrchg.
  DATA wl_header_control TYPE bapiibdlvhdrctrlchg.
  DATA tl_item_control TYPE STANDARD TABLE OF bapiibdlvitemctrlchg.
  DATA wl_item_control TYPE bapiibdlvitemctrlchg.
  DATA tl_item_data TYPE STANDARD TABLE OF bapiibdlvitemchg.
  DATA wl_item_data TYPE bapiibdlvitemchg.

  PERFORM mensajes_pasos USING 'ZUI5_CM' 'I' '072' 'ZUI5_CM_ANULA_POSICION_ENTREGA' '' '' ''CHANGING o_return.

  SELECT vbeln FROM likp INTO TABLE @DATA(tv_likp)
    WHERE vbeln = @iv_vbeln.
  IF sy-subrc = 0.

    SELECT vbeln, posnr, matnr, lfimg, vrkme INTO TABLE @DATA(tv_lips)
      FROM lips
      WHERE vbeln = @iv_vbeln
      AND posnr = @iv_posnr.
    IF sy-subrc = 0.

      LOOP AT tv_likp INTO DATA(wv_likp).

        wl_header_data-deliv_numb = wv_likp-vbeln.
        wl_header_control-deliv_numb = wv_likp-vbeln.

        CLEAR tl_item_control.

        LOOP AT tv_lips INTO DATA(wv_lips) WHERE vbeln = wv_likp-vbeln.

          wl_item_data-deliv_numb = wl_item_control-deliv_numb = wv_lips-vbeln.
          wl_item_data-deliv_item =  wl_item_control-deliv_item = wv_lips-posnr.
          wl_item_control-del_item = 'X'.
          wl_item_data-material = wv_lips-matnr.
          wl_item_data-dlv_qty = wv_lips-lfimg.
          wl_item_data-dlv_qty_imunit = wv_lips-lfimg.
          wl_item_data-del_qty_flo = wv_lips-lfimg.
          wl_item_data-dlv_qty_st_flo = wv_lips-lfimg.
          wl_item_data-fact_unit_nom = 1.
          wl_item_data-fact_unit_denom = 1.
          wl_item_data-conv_fact = 1.
          wl_item_data-sales_unit = wv_lips-vrkme.

          INSERT wl_item_control INTO TABLE tl_item_control.
          INSERT wl_item_data INTO TABLE tl_item_data.
          CLEAR wl_item_control.
          CLEAR wl_item_data.

        ENDLOOP.

        CALL FUNCTION 'BAPI_INB_DELIVERY_CHANGE'
          EXPORTING
            header_data    = wl_header_data
            header_control = wl_header_control
            delivery       = wv_likp-vbeln
          TABLES
            item_data      = tl_item_data
            item_control   = tl_item_control
            return         = tl_return.
        IF NOT line_exists( tl_return[ type = 'E' ] ).

          APPEND VALUE #( type = 'S' id = 'ZUI5_CM' number = '073' message_v1 = iv_vbeln
                    message_v2 = space message_v3 = space message_v4 = space ) TO o_return.

          CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
            EXPORTING
              wait = 'X'.

        ELSE.
          e_error = 'X'.
          MOVE-CORRESPONDING tl_return TO o_return KEEPING TARGET LINES.

        ENDIF.

      ENDLOOP.

    ENDIF.


  ENDIF.

  IF NOT o_return IS INITIAL.

    PERFORM guardar_log USING iv_vbeln o_return.

  ENDIF.

ENDFUNCTION.
