FUNCTION zui5_cm_anular_salida_entrega.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     VALUE(I_VBELN) TYPE  VBELN_VL
*"     VALUE(I_PSTYP) TYPE  VBTYP
*"  EXPORTING
*"     VALUE(ET_RETURN) TYPE  BAPIRET2_T
*"----------------------------------------------------------------------

  CONSTANTS v_veces TYPE i VALUE 10.

  DATA: t_mesg   TYPE TABLE OF mesg,
        lv_subrc TYPE sy-subrc.

**** REFRESH DELIVERY BUFFER
  CALL FUNCTION 'LE_DELIVERY_REFRESH_BUFFER'
    EXCEPTIONS
      no_key_specified = 0
      OTHERS           = 0.

  DO v_veces TIMES.
    CLEAR t_mesg.
    CALL FUNCTION 'WS_REVERSE_GOODS_ISSUE'
      EXPORTING
        i_vbeln                   = i_vbeln
        i_budat                   = sy-datum
        i_tcode                   = 'VL09'
        i_vbtyp                   = i_pstyp
      TABLES
        t_mesg                    = t_mesg
      EXCEPTIONS
        error_message             = 3
        error_reverse_goods_issue = 1
        OTHERS                    = 2.

    lv_subrc = sy-subrc.
    IF sy-subrc = 0.
      EXIT.
    ELSE.
      WAIT UP TO 1 SECONDS.
      CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'                  .
    ENDIF.
  ENDDO.
  IF lv_subrc = 0.
    CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
      EXPORTING
        wait = 'X'.
    APPEND INITIAL LINE TO et_return ASSIGNING FIELD-SYMBOL(<ls_return>).
    <ls_return>-type = 'S'.
    <ls_return>-id = 'ZUI5_CM'.
    <ls_return>-number = '009'.
    <ls_return>-message_v1 = |{ i_vbeln ALPHA = OUT }|.
  ELSE.
    CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'                  .

    APPEND INITIAL LINE TO et_return ASSIGNING <ls_return>.
    <ls_return>-type = 'E'.
    <ls_return>-id = 'ZUI5_CM'.
    <ls_return>-number = '010'.
    <ls_return>-message_v1 = |{ i_vbeln ALPHA = OUT }|.

    APPEND INITIAL LINE TO et_return ASSIGNING <ls_return>.
    <ls_return>-type = sy-msgty.
    <ls_return>-id = sy-msgid.
    <ls_return>-number = sy-msgno.
    <ls_return>-message_v1 = sy-msgv1.
    <ls_return>-message_v2 = sy-msgv2.
    <ls_return>-message_v3 = sy-msgv3.
    <ls_return>-message_v4 = sy-msgv4.

    LOOP AT t_mesg INTO DATA(ls_mseg).
      APPEND INITIAL LINE TO et_return ASSIGNING <ls_return>.
      <ls_return>-type = ls_mseg-msgty.
      <ls_return>-id = 'ZUI5_CM'.
      <ls_return>-number = '000'.
      <ls_return>-message_v1 = ls_mseg-text.
    ENDLOOP.
  ENDIF.


ENDFUNCTION.
