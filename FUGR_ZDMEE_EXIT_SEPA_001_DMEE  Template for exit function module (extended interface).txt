FUNCTION ZDMEE_EXIT_SEPA_001.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     VALUE(I_TREE_TYPE) TYPE  DMEE_TREETYPE_ABA
*"     VALUE(I_TREE_ID) TYPE  DMEE_TREEID_ABA
*"     VALUE(I_ITEM)
*"     VALUE(I_PARAM)
*"     VALUE(I_UPARAM)
*"     REFERENCE(I_EXTENSION) TYPE  DMEE_EXIT_INTERFACE_ABA
*"  EXPORTING
*"     REFERENCE(O_VALUE)
*"     REFERENCE(C_VALUE)
*"     REFERENCE(N_VALUE)
*"     REFERENCE(P_VALUE)
*"  TABLES
*"      I_TAB
*"----------------------------------------------------------------------

  DATA: BEGIN OF ti_xblnr OCCURS 0,
          xblnr LIKE regup-xblnr,
        END OF ti_xblnr.

  DATA: payment_doc_no TYPE belnr_d, payment_doc_buk TYPE bukrs,
        payment_doc_gja TYPE gjahr, lineas TYPE i, cadena TYPE string,
        cad(20), lifnr TYPE lifnr, kunnr TYPE kunnr.

  FIELD-SYMBOLS: <f> TYPE fpayh.

  CLEAR: ti_xblnr, ti_xblnr[], cadena, lifnr, kunnr.
  ASSIGN COMPONENT 1 OF STRUCTURE i_item TO <f>.
  payment_doc_buk = <f>-doc1r(4).
  payment_doc_no  = <f>-doc1r+4(10).
  payment_doc_gja = <f>-doc1r+14(4).
  IF <f>-gpa1t = '11'.
    lifnr = <f>-gpa1r.
  ELSEIF <f>-gpa1t = '12'.
    kunnr = <f>-gpa1r.
  ENDIF.

  SELECT xblnr INTO CORRESPONDING FIELDS OF TABLE ti_xblnr
    FROM regup
   WHERE laufd = <f>-laufd
     AND laufi = <f>-laufi
     AND xvorl = <f>-xvorl
     AND zbukr = <f>-zbukr
     AND lifnr = lifnr
     AND kunnr = kunnr
*     and empfg = <f>-...
     AND vblnr = payment_doc_no
     AND bukrs = payment_doc_buk.

  DESCRIBE TABLE ti_xblnr LINES lineas.
  LOOP AT ti_xblnr.
    AT FIRST.
      MOVE lineas TO cad.
      CONDENSE cad NO-GAPS.
      cadena = cad.
      CONCATENATE cadena 'FACTURA' INTO cadena SEPARATED BY space.
      IF lineas > 1.
        CONCATENATE cadena 'S' INTO cadena.
      ENDIF.
      CONCATENATE cadena ':' INTO cadena SEPARATED BY space.
    ENDAT.
    CONDENSE ti_xblnr-xblnr NO-GAPS.
    CONCATENATE cadena ti_xblnr-xblnr INTO cadena SEPARATED BY space.
  ENDLOOP.

  c_value = cadena.

ENDFUNCTION.
