*&---------------------------------------------------------------------*
*&  Include           ZGLO_REINDAT_MAIN
*&---------------------------------------------------------------------*


**********************************************************************
* Main program of ZGLO_REINDAT
**********************************************************************


**********************************************************************
* AT SELECTION-SCREEN
**********************************************************************
AT SELECTION-SCREEN.

* Authority check
PERFORM auth_check.

  IF gv_test = ' '.
    PERFORM testrun_warning.
  ENDIF.

  IF gv_radi4 = 'X'.                  "fill data from header text field
    PERFORM bktxt_filled_check.
  ENDIF.

  PERFORM vat_date_filled_check.


**********************************************************************
*
* START-OF-SELECTION
*
**********************************************************************
START-OF-SELECTION.

* Create application log
  PERFORM create_extnumber
    CHANGING gv_extnumber.

  PERFORM create_log
    USING    gv_extnumber
    CHANGING gv_log_handle.

* Read payment terms table when payment due date selected
  IF gv_radi3 = 'X'.
    PERFORM read_payment_terms.
  ENDIF.


* Start reading bkpf table
  OPEN CURSOR gv_cursor_1 FOR SELECT *
    FROM bkpf
    WHERE bukrs IN gt_compc
      AND gjahr IN gv_fi_ye
      AND blart IN gv_doc_t
      AND belnr IN gv_doc_n.

  DO.

    FETCH NEXT CURSOR gv_cursor_1
      INTO CORRESPONDING FIELDS OF TABLE gt_bkpf
      PACKAGE SIZE 20000.

    IF sy-subrc <> 0.
      EXIT.
    ENDIF.

* When posting date selected
    IF gv_radi1 = 'X'.
      LOOP AT gt_bkpf INTO gs_bkpf.
        IF ( gs_bkpf-REINDAT IS INITIAL ) OR
           ( gs_bkpf-REINDAT EQ '        ' ) .
          gs_bkpf-REINDAT = gs_bkpf-budat.
          PERFORM msg_put USING co_msg_success '000' 'ZGLO_REINDAT' "n2143303
                 text-050 gs_bkpf-bukrs gs_bkpf-belnr gs_bkpf-gjahr. "n2143303
          MODIFY gt_bkpf FROM gs_bkpf.
        ELSE.
          PERFORM msg_put USING co_msg_warning '000' 'ZGLO_REINDAT' "n2143303
            text-053 gs_bkpf-bukrs gs_bkpf-belnr gs_bkpf-gjahr. "n2143303
        ENDIF.
      ENDLOOP.
    ENDIF.

* When document date selected
    IF gv_radi2 = 'X'.
      LOOP AT gt_bkpf INTO gs_bkpf.
        IF ( gs_bkpf-REINDAT IS INITIAL ) OR
           ( gs_bkpf-REINDAT EQ '        ' ).
          gs_bkpf-REINDAT = gs_bkpf-bldat.
          PERFORM msg_put USING co_msg_success '000' 'ZGLO_REINDAT' "n2143303
             text-050 gs_bkpf-bukrs gs_bkpf-belnr gs_bkpf-gjahr. "n2143303

          MODIFY gt_bkpf FROM gs_bkpf.
        ELSE.
          PERFORM msg_put USING co_msg_warning '000' 'ZGLO_REINDAT' "n2143303
            text-053 gs_bkpf-bukrs gs_bkpf-belnr gs_bkpf-gjahr. "n2143303
        ENDIF.
      ENDLOOP.
    ENDIF.

* When payment due date selected
    IF gv_radi3 = 'X'.

* Read line items
      SELECT bukrs belnr gjahr buzei zfbdt zterm
        INTO CORRESPONDING FIELDS OF TABLE gt_line_items
        FROM bseg
        FOR ALL ENTRIES IN gt_bkpf
          WHERE bukrs = gt_bkpf-bukrs
            AND belnr = gt_bkpf-belnr
            AND gjahr = gt_bkpf-gjahr.


      LOOP AT gt_bkpf INTO gs_bkpf.

        CLEAR: gs_line_items,
               gt_line_items_temp[].
        IF gs_bkpf-REINDAT IS INITIAL.

          LOOP AT gt_line_items INTO gs_line_items
            WHERE bukrs = gs_bkpf-bukrs
              AND   belnr = gs_bkpf-belnr
              AND   gjahr = gs_bkpf-gjahr.
            APPEND gs_line_items TO gt_line_items_temp.
          ENDLOOP.

          PERFORM determine_payment_duedate
            TABLES gt_line_items_temp
            CHANGING gs_bkpf-REINDAT.

          IF gs_bkpf-REINDAT IS INITIAL.
            PERFORM msg_put USING co_msg_error '000' 'ZGLO_REINDAT' "n2143303
                 text-051 gs_bkpf-bukrs gs_bkpf-belnr gs_bkpf-gjahr. "n2143303
          ELSE.
            PERFORM msg_put USING co_msg_success '000' 'ZGLO_REINDAT' "n2143303
                 text-050 gs_bkpf-bukrs gs_bkpf-belnr gs_bkpf-gjahr. "n2143303
            MODIFY gt_bkpf FROM gs_bkpf.
          ENDIF.

        ELSE.
          PERFORM msg_put USING co_msg_warning '000' 'ZGLO_REINDAT' "n2143303
            text-053 gs_bkpf-bukrs gs_bkpf-belnr gs_bkpf-gjahr. "n2143303
        ENDIF.
      ENDLOOP.
    ENDIF.

* When header-text selected
    IF gv_radi4 = 'X'.
      LOOP AT gt_bkpf INTO gs_bkpf.
        IF ( gs_bkpf-REINDAT IS INITIAL ) OR
           ( gs_bkpf-REINDAT EQ '        ' ).
          PERFORM fill_date_from_bktxt
            USING gs_bkpf-bktxt
            CHANGING gs_bkpf-REINDAT.
          IF ( gs_bkpf-REINDAT IS INITIAL ) OR
             ( gs_bkpf-REINDAT EQ '        ' ).
            PERFORM msg_put USING co_msg_error '000' 'ZGLO_REINDAT' "n2143303
            text-052 gs_bkpf-bukrs gs_bkpf-belnr gs_bkpf-gjahr."n2143303
          ELSE.
            PERFORM msg_put USING co_msg_success '000' 'ZGLO_REINDAT' "n2143303
               text-050 gs_bkpf-bukrs gs_bkpf-belnr gs_bkpf-gjahr."n2143303
            MODIFY gt_bkpf FROM gs_bkpf.
          ENDIF.
        ELSE.
          PERFORM msg_put USING co_msg_warning '000' 'ZGLO_REINDAT' "n2143303
            text-053 gs_bkpf-bukrs gs_bkpf-belnr gs_bkpf-gjahr."n2143303
        ENDIF.

      ENDLOOP.
    ENDIF.


* When REINDAT selected
    IF gv_radi5 = 'X'.
      LOOP AT gt_bkpf INTO gs_bkpf.
        IF ( gs_bkpf-REINDAT IS INITIAL ) OR
           ( gs_bkpf-REINDAT EQ '        ' ) .
          gs_bkpf-REINDAT = gs_bkpf-VATDATE.
          PERFORM msg_put USING co_msg_success '000' 'ZGLO_REINDAT' "n2143303
                 text-050 gs_bkpf-bukrs gs_bkpf-belnr gs_bkpf-gjahr. "n2143303
          MODIFY gt_bkpf FROM gs_bkpf.
        ELSE.
          PERFORM msg_put USING co_msg_warning '000' 'ZGLO_REINDAT' "n2143303
            text-053 gs_bkpf-bukrs gs_bkpf-belnr gs_bkpf-gjahr. "n2143303
        ENDIF.
      ENDLOOP.
    ENDIF.


    MODIFY bkpf FROM TABLE gt_bkpf.

  ENDDO.


  IF gv_test = ' '.
    COMMIT WORK AND WAIT.
  ELSE.
    ROLLBACK WORK.
  ENDIF.

  CLOSE CURSOR gv_cursor_1.

* Display application log
  PERFORM display_log USING gv_log_handle.                  "n2143303
