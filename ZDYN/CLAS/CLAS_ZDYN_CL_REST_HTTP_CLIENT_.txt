
class ZDYN_CL_REST_HTTP_CLIENT definition
  public
  create public .

public section.

  data MO_HTTP_CLIENT type ref to IF_HTTP_CLIENT .
  data MO_HTTP_ENTITY type ref to IF_HTTP_ENTITY .

  methods CONSTRUCTOR
    importing
      !IO_HTTP_CLIENT type ref to IF_HTTP_CLIENT .
  methods DELETE .
  methods GET .
  methods POST .
  methods PUT .
  methods SET_STRING_DATA
    importing
      !IS_DATA type STRING .
  methods GET_CONTENT_TYPE
    exporting
      !ES_MEDIA_TYPE type STRING .
  methods GET_STATUS
    returning
      value(RI_STATUS) type I .
  methods GET_BINARY_DATA
    returning
      value(RX_DATA) type XSTRING .
  methods GET_STRING_DATA
    returning
      value(RS_DATA) type STRING .
protected section.

  methods SEND_RECEIVE .
private section.
endclass. "ZDYN_CL_REST_HTTP_CLIENT definition
class ZDYN_CL_REST_HTTP_CLIENT implementation.
METHOD CONSTRUCTOR.

  mo_http_client = io_http_client.

ENDMETHOD.
METHOD DELETE.

  mo_http_client->request->set_method( 'DELETE' ).

  send_receive( ).

ENDMETHOD.
METHOD GET.

  mo_http_client->request->set_method( 'GET' ).

  send_receive( ).

ENDMETHOD.
METHOD GET_BINARY_DATA.

  rx_data = mo_http_client->response->get_data( ).

ENDMETHOD.                    "IF_REST_ENTITY~GET_BINARY_DATA
METHOD GET_CONTENT_TYPE.

  DATA: ls_content_type TYPE string,
        ls_parameters   TYPE string.

  ls_content_type = mo_http_client->response->get_header_field( if_http_header_fields=>content_type ).

  SPLIT ls_content_type AT ';' INTO es_media_type ls_parameters.

ENDMETHOD.
METHOD GET_STATUS.

  mo_http_client->response->get_status( IMPORTING code = ri_status ).

ENDMETHOD.
METHOD GET_STRING_DATA.

  rs_data = mo_http_client->response->get_cdata( ).

ENDMETHOD.
METHOD POST.

  mo_http_client->request->set_method( 'POST' ).

  send_receive( ).

ENDMETHOD.
METHOD PUT.

  mo_http_client->request->set_method( 'PUT' ).

  send_receive( ).

ENDMETHOD.
METHOD send_receive.

  mo_http_client->send( EXCEPTIONS OTHERS = 1 ).

  IF sy-subrc = 0.
    mo_http_client->receive( EXCEPTIONS OTHERS = 1 ).
  ENDIF.

ENDMETHOD.
METHOD SET_STRING_DATA.

  mo_http_client->request->set_cdata( is_data ).

ENDMETHOD.
