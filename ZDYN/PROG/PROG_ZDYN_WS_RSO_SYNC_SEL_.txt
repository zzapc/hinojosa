*&---------------------------------------------------------------------*
*&  Include           ZDYN_WS_RSO_SYNC_SEL
*&---------------------------------------------------------------------*

SELECTION-SCREEN BEGIN OF BLOCK cfg WITH FRAME TITLE text-img.

SELECTION-SCREEN PUSHBUTTON /10(50) gc_bgen USER-COMMAND cfggen VISIBLE LENGTH 8.
SELECTION-SCREEN PUSHBUTTON 22(50) gc_bpdm USER-COMMAND cfgpdm VISIBLE LENGTH 8.
SELECTION-SCREEN PUSHBUTTON 34(50) gc_barc USER-COMMAND cfgarc VISIBLE LENGTH 8.
SELECTION-SCREEN PUSHBUTTON 46(50) gc_bcus USER-COMMAND cfgcus VISIBLE LENGTH 8.
SELECTION-SCREEN PUSHBUTTON 58(50) gc_bcuv USER-COMMAND cfgcuv VISIBLE LENGTH 8.
SELECTION-SCREEN PUSHBUTTON 75(50) gc_bver USER-COMMAND cfgver VISIBLE LENGTH 3.

SELECTION-SCREEN END OF BLOCK cfg.

SELECTION-SCREEN BEGIN OF BLOCK communication WITH FRAME TITLE text-com.

PARAMETERS:
  psysid LIKE zdyn_rso_tpdmap-sysid DEFAULT sy-sysid NO-DISPLAY,
  puser  LIKE zdyn_rso_tpdmap-username,
  pbukrs LIKE zdyn_rso_tpdmap-bukrs,
  pall   TYPE alles AS CHECKBOX.
.
SELECTION-SCREEN SKIP.

PARAMETERS:
  pmsglv TYPE zdyn_ws_dmessage_level AS LISTBOX VISIBLE LENGTH 17,
  papplg TYPE lmon_aplog AS CHECKBOX.

SELECTION-SCREEN END OF BLOCK communication.

SELECTION-SCREEN BEGIN OF BLOCK action WITH FRAME TITLE text-act.

SELECTION-SCREEN BEGIN OF LINE.

PARAMETERS pauth RADIOBUTTON GROUP act USER-COMMAND actn.

SELECTION-SCREEN COMMENT 4(20) text-pwd.

PARAMETERS psap RADIOBUTTON GROUP act.

SELECTION-SCREEN COMMENT 28(20) text-mst.

PARAMETERS prso RADIOBUTTON GROUP act.

SELECTION-SCREEN COMMENT 52(20) text-rso.

SELECTION-SCREEN END OF LINE.

SELECTION-SCREEN SKIP.

SELECT-OPTIONS: sel_comp FOR ekko-bukrs NO INTERVALS MODIF ID md.
PARAMETERS:     pcreateb TYPE zdyn_drso_create_buyer AS CHECKBOX MODIF ID md.

SELECTION-SCREEN END OF BLOCK action.

SELECTION-SCREEN BEGIN OF BLOCK md WITH FRAME TITLE text-sap.
SELECT-OPTIONS: sel_lang FOR zdyn_stru_fields_with_f4helps-sylangu NO INTERVALS MODIF ID md.

SELECTION-SCREEN SKIP.

SELECT-OPTIONS: sel_curr FOR ekko-waers NO INTERVALS MODIF ID md.
PARAMETERS:     pdelcurr TYPE loesc AS CHECKBOX MODIF ID md.

SELECTION-SCREEN SKIP.

SELECT-OPTIONS: sel_pt FOR zdyn_stru_fields_with_f4helps-zterm MODIF ID md.
PARAMETERS:     pdelpt TYPE repl AS CHECKBOX MODIF ID md.

SELECTION-SCREEN SKIP.

SELECT-OPTIONS: sel_sak FOR zdyn_stru_fields_with_f4helps-saknr MODIF ID md.
PARAMETERS:     pglgrp TYPE zdyn_stru_fields_with_f4helps-ktoks MODIF ID md.
PARAMETERS:     pdelsak TYPE loesc AS CHECKBOX MODIF ID md.

SELECTION-SCREEN SKIP.

SELECT-OPTIONS: sel_kost FOR zdyn_stru_fields_with_f4helps-kostl MODIF ID md.
PARAMETERS:     pdelkost TYPE loesc AS CHECKBOX MODIF ID md.

SELECTION-SCREEN SKIP.

SELECT-OPTIONS: sel_psp FOR zdyn_stru_fields_with_f4helps-pspnr MODIF ID md.
PARAMETERS:     pchdatp TYPE fc_datum MODIF ID md.
PARAMETERS:     pdelpsp TYPE loesc AS CHECKBOX MODIF ID md.

SELECTION-SCREEN SKIP.

SELECT-OPTIONS: sel_auf FOR zdyn_stru_fields_with_f4helps-aufnr MODIF ID md.
PARAMETERS:     pchdata TYPE fc_datum MODIF ID md.
PARAMETERS:     pdelauf TYPE loesc AS CHECKBOX MODIF ID md.

SELECTION-SCREEN SKIP.

SELECT-OPTIONS: sel_lief FOR ekko-lifnr MODIF ID md.
PARAMETERS:     pchdatl  TYPE fc_datum MODIF ID md.
PARAMETERS:     pdelsflg TYPE qstatus4 MODIF ID md.
PARAMETERS:     pbanks   TYPE knbkb_49b AS CHECKBOX MODIF ID md.
PARAMETERS:     pdellief TYPE loesc AS CHECKBOX MODIF ID md.

SELECTION-SCREEN SKIP.

SELECT-OPTIONS: sel_po FOR ekko-ebeln MODIF ID md,
                selerdat FOR ekko-aedat MODIF ID md.
PARAMETERS:     pchdat TYPE fc_datum MODIF ID md.
PARAMETERS:     pdelpo TYPE mc_cm_vsd AS CHECKBOX MODIF ID md.

SELECTION-SCREEN SKIP.

SELECT-OPTIONS: sel_debi FOR kna1-kunnr MODIF ID md.
PARAMETERS:     pchdatd  TYPE fc_datum MODIF ID md.
PARAMETERS:     pdeldflg TYPE qstatus4 MODIF ID md.
PARAMETERS:     pdeldrpl TYPE repl AS CHECKBOX MODIF ID md.
PARAMETERS:     pdeldebi TYPE mc_cm_vsd AS CHECKBOX MODIF ID md.

SELECTION-SCREEN SKIP.

PARAMETERS:     pcust  LIKE zdyn_srso_f4help-extname MODIF ID md.
PARAMETERS:     pdelcu TYPE loesc AS CHECKBOX MODIF ID md.

SELECTION-SCREEN END OF BLOCK md.


****************************************************************
* Retrieve document
SELECTION-SCREEN BEGIN OF BLOCK rt WITH FRAME TITLE text-rso.

PARAMETERS: pdele TYPE bankloev AS CHECKBOX MODIF ID rso.
PARAMETERS: pmail TYPE emailflag AS CHECKBOX MODIF ID rso.
PARAMETERS: porig TYPE co_afcrea AS CHECKBOX MODIF ID rso.

SELECTION-SCREEN SKIP.

SELECTION-SCREEN BEGIN OF BLOCK rt_file WITH FRAME TITLE text-tst.

PARAMETERS:     prt_fdbk TYPE c AS CHECKBOX MODIF ID rso.
PARAMETERS:     prt_satt TYPE zdyn_ws_dskip_attach_add AS CHECKBOX MODIF ID rso.
PARAMETERS:     prt_http TYPE zdyn_ws_dskip_http_test AS CHECKBOX MODIF ID rso.
PARAMETERS:     prf_file TYPE c AS CHECKBOX MODIF ID rso USER-COMMAND rff.
PARAMETERS:     prf_fnam TYPE zdyn_ddfilename LOWER CASE MODIF ID rff.
PARAMETERS:     prt_file TYPE c AS CHECKBOX MODIF ID rso USER-COMMAND rtf.
PARAMETERS:     prt_path TYPE iwp_path_string LOWER CASE MODIF ID rtf.

SELECTION-SCREEN END OF BLOCK rt_file.

SELECTION-SCREEN END OF BLOCK rt.

****************************************************************


*-----------------------------------------------------------------
* Show/hide areas dependend on selected values
*-----------------------------------------------------------------
AT SELECTION-SCREEN OUTPUT.

  CONCATENATE icon_configuration(3) '\Q' 'General configuration'(GEN) '@'
              INTO gc_bgen.
  CONCATENATE icon_mapped_relation(3) '\Q' 'PD mapping'(PDM) '@'
              INTO gc_bpdm.
  CONCATENATE icon_viewer_optical_archive(3) '\Q' 'Archive link settings'(ARC) '@'
              INTO gc_barc.
  CONCATENATE icon_field_with_text(3) '\Q' 'Custom data definitions'(CUS) '@'
              INTO gc_bcus.
  CONCATENATE icon_change_text(3) '\Q' 'Custom data values'(CUV) '@'
              INTO gc_bcuv.
  CONCATENATE icon_information(3) '\Q' 'Version Info'(VER) '@'
              INTO gc_bver.

*AT SELECTION-SCREEN ON p_cust.
  PERFORM screen_adjust.
  PERFORM restrict_select.

AT SELECTION-SCREEN ON RADIOBUTTON GROUP act.

  PERFORM screen_adjust.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR prf_fnam.
  gs_rf_file_name = prf_fnam.
  go_file->get_file_name( EXPORTING is_file_name = gs_rf_file_name
                          IMPORTING es_file_name = gs_rf_file_name ).
  prf_fnam = gs_rf_file_name.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR prt_path.
  gs_rt_file_path = prt_path.
  go_file->get_file_path( EXPORTING is_file_path = gs_rt_file_path
                          IMPORTING es_file_path = gs_rt_file_path ).
  prt_path = gs_rt_file_path.

AT SELECTION-SCREEN.

  CLEAR: gc_tcode.
  CASE sy-ucomm.
    WHEN 'CFGGEN'.
      gc_tcode = con_ta_gen.
    WHEN 'CFGPDM'.
      gc_tcode = con_ta_pdmap.
    WHEN 'CFGARC'.
      gc_tcode = con_ta_arc.
    WHEN 'CFGCUS'.
      gc_tcode = con_ta_cus.
    WHEN 'CFGCUV'.
      gc_tcode = con_ta_cusval.
    WHEN 'CFGVER'.
      PERFORM show_version USING con_lic_component con_module.
      CLEAR sy-ucomm.
    WHEN OTHERS.
*  nothing
  ENDCASE.

  IF NOT gc_tcode IS INITIAL.
    PERFORM authority_check.
    CHECK NOT gc_tcode IS INITIAL.
    CALL TRANSACTION gc_tcode.
  ELSEIF NOT sy-ucomm IS INITIAL.
    IF puser IS INITIAL.
      CLEAR sy-ucomm.
      MESSAGE e386(so).
    ENDIF.
  ENDIF.

*INITIALIZATION.
*
*  gc_tcode = con_ta_main.
*  PERFORM authority_check.
*  CLEAR gc_tcode.

*&---------------------------------------------------------------------*
*&      Form  SCREEN_ADJUST
*&---------------------------------------------------------------------*
FORM screen_adjust .

  IF NOT psap IS INITIAL AND NOT pbukrs IS INITIAL.
    SET PARAMETER ID 'BUK' FIELD pbukrs.
  ENDIF.
  CHECK psap IS INITIAL OR prso IS INITIAL.
  LOOP AT SCREEN.
    IF screen-group1 = 'MD'.
      IF psap IS INITIAL.
        screen-active = 0.
      ELSE.
        screen-active = 1.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.
    IF screen-group1 = 'RSO'.
      IF prso IS INITIAL.
        screen-active = 0.
      ELSE.
        screen-active = 1.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.
    IF screen-group1 = 'RT'.
      IF prso IS INITIAL.
        screen-active = 0.
      ELSE.
        screen-active = 1.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.
    IF screen-group1 = 'RTF'.
      IF prso IS INITIAL OR prt_file IS INITIAL.
        screen-active = 0.
      ELSE.
        screen-active = 1.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.
    IF screen-group1 = 'RFF'.
      IF prso IS INITIAL OR prf_file IS INITIAL.
        screen-active = 0.
      ELSE.
        screen-active = 1.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.

ENDFORM.

*&---------------------------------------------------------------------*
*&      Form  AUTHORITY_CHECK
*&---------------------------------------------------------------------*
FORM authority_check .

  CALL FUNCTION 'AUTHORITY_CHECK_TCODE'
    EXPORTING
      tcode  = gc_tcode
    EXCEPTIONS
      ok     = 0
      OTHERS = 1.
  IF sy-subrc NE 0.
    MESSAGE s172(00) WITH gc_tcode.
    CLEAR gc_tcode.
    EXIT.
  ENDIF.

ENDFORM.
