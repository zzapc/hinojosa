*&---------------------------------------------------------------------*
*&  Include           ZDYN_WS_ROUTTY_SYNC_SEL
*&---------------------------------------------------------------------*

SELECTION-SCREEN BEGIN OF BLOCK cfg WITH FRAME TITLE text-img.

SELECTION-SCREEN PUSHBUTTON /10(50) gc_bgen USER-COMMAND cfggen VISIBLE LENGTH 8.
SELECTION-SCREEN PUSHBUTTON 22(50) gc_bari USER-COMMAND cfgari VISIBLE LENGTH 8.
SELECTION-SCREEN PUSHBUTTON 79(50) gc_bver USER-COMMAND cfgver VISIBLE LENGTH 3.

SELECTION-SCREEN END OF BLOCK cfg.

****************************************************************
* Communication
SELECTION-SCREEN BEGIN OF BLOCK communication WITH FRAME TITLE text-com.

PARAMETERS:
  psysid LIKE zdyn_routty_tmap-sysid DEFAULT sy-sysid NO-DISPLAY,
  puser  LIKE zdyn_routty_tmap-username, " OBLIGATORY
  pbukrs LIKE zdyn_routty_tmap-bukrs.
SELECTION-SCREEN SKIP.

PARAMETERS: pmsglv TYPE zdyn_ws_dmessage_level AS LISTBOX VISIBLE LENGTH 17.
PARAMETERS: papplg TYPE lmon_aplog AS CHECKBOX.

SELECTION-SCREEN END OF BLOCK communication.

****************************************************************
* Activity
SELECTION-SCREEN BEGIN OF BLOCK action WITH FRAME TITLE text-act.

SELECTION-SCREEN BEGIN OF LINE.

PARAMETERS pauth RADIOBUTTON GROUP act USER-COMMAND actn.

SELECTION-SCREEN COMMENT 4(20) text-pwd.

PARAMETERS parc RADIOBUTTON GROUP act.

SELECTION-SCREEN COMMENT 28(20) text-arc.

SELECTION-SCREEN END OF LINE.

SELECTION-SCREEN END OF BLOCK action.

****************************************************************
* Parameters
SELECTION-SCREEN BEGIN OF BLOCK md WITH FRAME TITLE text-arc.

SELECT-OPTIONS: sel_vbel FOR zdyn_stru_fields_with_f4helps-vbeln MODIF ID md.
PARAMETERS:     pcrdate  TYPE erdat MODIF ID md.

SELECTION-SCREEN SKIP.

SELECTION-SCREEN END OF BLOCK md.

*-----------------------------------------------------------------
* Show/hide areas dependend on selected values
*-----------------------------------------------------------------
AT SELECTION-SCREEN OUTPUT.

  CONCATENATE icon_configuration(3) '\Q' 'General configuration'(GEN) '@'
              INTO gc_bgen.
  CONCATENATE icon_mapped_relation(3) '\Q' 'Accounts Receivable output'(ARI) '@'
              INTO gc_bari.
  CONCATENATE icon_information(3) '\Q' 'Version Info'(VER) '@'
              INTO gc_bver.

  PERFORM screen_adjust.

AT SELECTION-SCREEN ON RADIOBUTTON GROUP act.

  PERFORM screen_adjust.

AT SELECTION-SCREEN.

  CLEAR gc_tcode.
  CASE sy-ucomm.
    WHEN 'CFGGEN'.
      gc_tcode = con_ta_gen.
    WHEN 'CFGARI'.
      gc_tcode = con_ta_ari.
    WHEN 'CFGVER'.
      PERFORM show_version USING con_lic_component con_module.
      CLEAR sy-ucomm.
    WHEN OTHERS.
*  nothing
  ENDCASE.

  IF NOT gc_tcode IS INITIAL.
    PERFORM authority_check.
    CHECK NOT gc_tcode IS INITIAL.
    CALL TRANSACTION gc_tcode.
  ELSEIF NOT sy-ucomm IS INITIAL.
    IF puser IS INITIAL.
      CLEAR sy-ucomm.
      MESSAGE e386(so).
    ENDIF.
  ENDIF.

INITIALIZATION.

  gc_tcode = con_ta_main.
  PERFORM authority_check.
  CLEAR gc_tcode.
