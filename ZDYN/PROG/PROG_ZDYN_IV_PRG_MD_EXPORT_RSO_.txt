*&---------------------------------------------------------------------*
*& Report  ZDYN_AP_PRG_MD_EXPORT_RSO
*&
*&---------------------------------------------------------------------*
*&
*&
*&---------------------------------------------------------------------*

REPORT zdyn_iv_prg_md_export_rso.

INCLUDE zdyn_iv_prg_md_export_rso_top.

AT SELECTION-SCREEN OUTPUT.
* On/off fields
  PERFORM disable_fields.

AT SELECTION-SCREEN ON BLOCK frame4.
* Creation of file names
  PERFORM md_create_filenames
          USING    xmlsfn
                   xmlsbfn
                   xmlbfn
                   xmlpaths
                   xmlpathc
                   date
                   date1
                   date2
                   time
                   time1
                   time2
                   savehdds
                   savess
          CHANGING gc_s_file
                   gc_sb_file
                   gc_b_file
                   gc_s_cl_path
                   gc_sb_cl_path
                   gc_b_cl_path.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR xmlpathc.
  PERFORM gui_f4.

START-OF-SELECTION.
* check permissions for transaction ZDYN_MD_EXPORT
  IF sy-tcode+0(1) = 'S'.
    CALL FUNCTION 'AUTHORITY_CHECK_TCODE'
      EXPORTING
        tcode  = 'ZDYN_MD_EXPORT'
      EXCEPTIONS
        ok     = 0
        not_ok = 1
        OTHERS = 2.
    IF sy-subrc NE 0.
      MESSAGE e172(00) WITH 'ZDYN_MD_EXPORT'.
      EXIT.
    ENDIF.
  ENDIF.

  DATA:
    lb_open_file_err LIKE boole-boole,
    lb_file_closed   LIKE boole-boole,
    lr_program       TYPE REF TO zdyn_iv_cl_prg_md_export_rso.

  zdyn_iv_cl_prg_md_export_rso=>mc_text_016 = text-016.
  zdyn_iv_cl_prg_md_export_rso=>mc_text_058 = text-058.
  CREATE OBJECT lr_program.

  WRITE: / text-010.
* Find HTML codepage
  CALL FUNCTION '/COCKPIT/FIND_ENCODING'
    IMPORTING
      applsvr_codepage      = gn_sap_codepg
      http_applsvr_codepage = gc_html_codepg.

* Check if unicode cp (then only UTF-8 is supported by
* OPEN DATASET statement).
  TRANSLATE gc_html_codepg TO UPPER CASE.                 "#EC SYNTCHAR
  SEARCH gc_html_codepg FOR 'UTF'.
  IF sy-subrc IS INITIAL.
    gc_html_codepg = gc_con_unicode_cp.
  ENDIF.

***********Supplier Activities***********
  IF xmlsfnc EQ 'X'.
    lr_program->md_add_supplier_xml_content(
      EXPORTING
        ic_html_codepage   = gc_html_codepg
        ii_uptorows        = uptorows
        gc_s_file          = gc_s_file
        ib_separate_files  = bukrschk
        gc_s_cl_path       = gc_s_cl_path
        i_filename         = xmlsfn
        ib_no_trail        = no_trail
        ib_save_local      = savehdds
        ib_disable_blocked = dis_blck
      CHANGING
        gt_supplier_tmp    = gt_supplier_tmp[]
        gt_supplier        = gt_supplier[]
        gt_xmlstrings      = gt_xmlstrings ).
  ENDIF.

**********Supplier Bank Activities*********
  IF xmlsbfnc EQ 'X'.
    lr_program->md_add_sup_bank_xml_content(
      EXPORTING
        ic_html_codepage  = gc_html_codepg
        ii_uptorows       = uptorows
        gc_sb_file        = gc_sb_file
        ib_separate_files = bukrschk
        gc_sb_cl_path     = gc_sb_cl_path
        i_filename        = xmlsbfn
        ib_no_trail       = no_trail
        ib_save_local     = savehdds
      CHANGING
        gt_sup_bank_tmp   = gt_sup_bank_tmp[]
        gt_sup_bank       = gt_sup_bank[]
        gt_xmlstrings     = gt_xmlstrings[] ).
  ENDIF.

********** Buyer Activities ********
  IF xmlbfnc EQ 'X'.

    REFRESH gt_xmlstrings.

    CLEAR:
      lb_open_file_err,
      lb_file_closed.

    PERFORM md_open_file
      USING     gc_b_file
      CHANGING  lb_open_file_err.

    IF lb_open_file_err = space.

*     Add XML Header at the file begining
      PERFORM md_add_buyer_xml_header
        TABLES  gt_xmlstrings.

*     Fetch data, convert it to XML and store in file ...
      PERFORM md_add_buyer_xml_content
         TABLES  gt_buyer_tmp
                 gt_buyer
                 gt_xmlstrings
         USING   gc_html_codepg
                 uptorows
                 gc_b_file.

*     Add XML footer to the end of file
      PERFORM add_buyer_xml_footer
        USING gc_b_file.

*     Close file
      PERFORM md_close_file
        USING     gc_b_file
                  savehdds
        CHANGING  lb_file_closed.

*     Save XML file to HDD on client side
      IF savehdds EQ con_true AND lb_file_closed EQ 'X'.
        PERFORM md_save_file_usr_side
                USING gc_b_file
                      gc_b_cl_path.
      ENDIF.
    ENDIF.
  ENDIF.

  WRITE / text-011.
