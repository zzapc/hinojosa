*&---------------------------------------------------------------------*
*&  Include           ZDYN_WS_ROUTTY_SYNC_SEL
*&---------------------------------------------------------------------*

SELECTION-SCREEN BEGIN OF BLOCK cfg WITH FRAME TITLE text-img.

SELECTION-SCREEN PUSHBUTTON /10(50) gc_bgen USER-COMMAND cfggen VISIBLE LENGTH 8.
SELECTION-SCREEN PUSHBUTTON 21(50) gc_bpdm USER-COMMAND cfgpdm VISIBLE LENGTH 8.
SELECTION-SCREEN PUSHBUTTON 32(50) gc_barc USER-COMMAND cfgarc VISIBLE LENGTH 8.
SELECTION-SCREEN PUSHBUTTON 43(50) gc_bari USER-COMMAND cfgari MODIF ID ar VISIBLE LENGTH 8.
SELECTION-SCREEN PUSHBUTTON 54(50) gc_bxmp USER-COMMAND cfgxmp MODIF ID ar VISIBLE LENGTH 8.
SELECTION-SCREEN PUSHBUTTON 65(50) gc_bxml USER-COMMAND cfgxml MODIF ID ar VISIBLE LENGTH 8.
SELECTION-SCREEN PUSHBUTTON 79(50) gc_bver USER-COMMAND cfgver VISIBLE LENGTH 3.


SELECTION-SCREEN END OF BLOCK cfg.

****************************************************************
* Communication
SELECTION-SCREEN BEGIN OF BLOCK communication WITH FRAME TITLE text-com.

PARAMETERS:
  psysid LIKE zdyn_routty_tmap-sysid    DEFAULT sy-sysid NO-DISPLAY,
  puser  LIKE zdyn_routty_tmap-username MODIF ID ap,
  pbukrs LIKE zdyn_routty_tmap-bukrs    MODIF ID ap,
  psyid  LIKE zdyn_rex_tmap-sysid       DEFAULT sy-sysid NO-DISPLAY,
  pid    LIKE zdyn_rex_tmap-id          MODIF ID ex.
SELECTION-SCREEN SKIP.

PARAMETERS: pmsglv TYPE zdyn_ws_dmessage_level AS LISTBOX VISIBLE LENGTH 17.
PARAMETERS: papplg TYPE lmon_aplog AS CHECKBOX.

SELECTION-SCREEN END OF BLOCK communication.

****************************************************************
* Activity
SELECTION-SCREEN BEGIN OF BLOCK action WITH FRAME TITLE text-act.

SELECTION-SCREEN BEGIN OF LINE.

PARAMETERS pauth RADIOBUTTON GROUP act USER-COMMAND actn.

SELECTION-SCREEN COMMENT 4(20) text-pwd.

PARAMETERS psap RADIOBUTTON GROUP act.

SELECTION-SCREEN COMMENT 28(20) text-mst.

PARAMETERS prso RADIOBUTTON GROUP act.

SELECTION-SCREEN COMMENT 52(20) text-rso.

PARAMETERS psnd RADIOBUTTON GROUP act MODIF ID sdb.

SELECTION-SCREEN COMMENT 76(20) text-snd.

SELECTION-SCREEN END OF LINE.

SELECTION-SCREEN BEGIN OF LINE.
PARAMETERS psnp RADIOBUTTON GROUP act MODIF ID snp.
SELECTION-SCREEN COMMENT 4(20) text-snp.
SELECTION-SCREEN END OF LINE.

SELECTION-SCREEN SKIP.

SELECT-OPTIONS: sel_comp FOR ekko-bukrs NO INTERVALS MODIF ID mdx.

SELECTION-SCREEN END OF BLOCK action.

****************************************************************
* Master data
SELECTION-SCREEN BEGIN OF BLOCK md WITH FRAME TITLE text-sap.

SELECTION-SCREEN BEGIN OF BLOCK general WITH FRAME.
SELECT-OPTIONS: sel_lang FOR zdyn_stru_fields_with_f4helps-sylangu NO INTERVALS MODIF ID mdx.
PARAMETERS:     punique  TYPE char1 AS CHECKBOX MODIF ID mdl.
SELECTION-SCREEN END OF BLOCK general.

SELECTION-SCREEN BEGIN OF BLOCK companies WITH FRAME.
PARAMETERS:     pccode   TYPE zdyn_dcomp_code_flag AS CHECKBOX MODIF ID mdx.
SELECTION-SCREEN END OF BLOCK companies.

SELECTION-SCREEN BEGIN OF BLOCK currency WITH FRAME.
SELECT-OPTIONS: sel_curr FOR ekko-waers NO INTERVALS MODIF ID md.
PARAMETERS:     pdelcurr TYPE repl AS CHECKBOX MODIF ID md.
SELECTION-SCREEN END OF BLOCK currency.

SELECTION-SCREEN BEGIN OF BLOCK payment_terms WITH FRAME.
SELECT-OPTIONS: sel_pt FOR zdyn_stru_fields_with_f4helps-zterm MODIF ID md.
PARAMETERS:     pdelpt TYPE repl AS CHECKBOX MODIF ID md.
SELECTION-SCREEN END OF BLOCK payment_terms.

SELECTION-SCREEN BEGIN OF BLOCK taxes WITH FRAME.
SELECT-OPTIONS: sel_tax FOR t007s-mwskz NO INTERVALS MODIF ID md.
PARAMETERS:     pdeltax TYPE repl AS CHECKBOX MODIF ID md.
SELECTION-SCREEN END OF BLOCK taxes.

SELECTION-SCREEN BEGIN OF BLOCK accounts WITH FRAME.
SELECT-OPTIONS: sel_sak FOR zdyn_stru_fields_with_f4helps-saknr MODIF ID md.
PARAMETERS:     pdelsak TYPE repl AS CHECKBOX MODIF ID md.
SELECTION-SCREEN END OF BLOCK accounts.

SELECTION-SCREEN BEGIN OF BLOCK costs WITH FRAME.
SELECT-OPTIONS: sel_kost FOR zdyn_stru_fields_with_f4helps-kostl MODIF ID md.
PARAMETERS:     pdelkost TYPE repl AS CHECKBOX MODIF ID md.
SELECTION-SCREEN END OF BLOCK costs.

SELECTION-SCREEN BEGIN OF BLOCK wbs_element WITH FRAME.
SELECT-OPTIONS: sel_psp FOR zdyn_stru_fields_with_f4helps-pspnr MODIF ID md.
PARAMETERS:     pdimp   TYPE zdyn_ddimensions MODIF ID md.
PARAMETERS:     pchdatp TYPE fc_datum MODIF ID md.
PARAMETERS:     pdelpsp TYPE repl AS CHECKBOX MODIF ID md.
SELECTION-SCREEN END OF BLOCK wbs_element.

SELECTION-SCREEN BEGIN OF BLOCK orders WITH FRAME.
SELECT-OPTIONS: sel_auf FOR zdyn_stru_fields_with_f4helps-aufnr MODIF ID md.
PARAMETERS:     pdima   TYPE zdyn_ddimensions MODIF ID md.
PARAMETERS:     pchdata TYPE fc_datum MODIF ID md.
PARAMETERS:     pdelauf TYPE repl AS CHECKBOX MODIF ID md.
SELECTION-SCREEN END OF BLOCK orders.

SELECTION-SCREEN BEGIN OF BLOCK suppliers WITH FRAME.
SELECT-OPTIONS: sel_lief FOR ekko-lifnr MODIF ID mdx.
PARAMETERS:     pchdatl  TYPE fc_datum MODIF ID mdx.
PARAMETERS:     pdellief TYPE repl AS CHECKBOX MODIF ID md.
PARAMETERS:     ptkinacs TYPE repl AS CHECKBOX MODIF ID mdx.
PARAMETERS:     pbanks   TYPE knbkb_49b AS CHECKBOX MODIF ID mdx USER-COMMAND bnk.
PARAMETERS:     pdelbank TYPE repl AS CHECKBOX DEFAULT abap_true MODIF ID md.
SELECTION-SCREEN END OF BLOCK suppliers.

SELECTION-SCREEN BEGIN OF BLOCK customers WITH FRAME.
SELECT-OPTIONS: sel_cust FOR ekko-kunnr MODIF ID mdx.
PARAMETERS:     pchdatc  TYPE fc_datum MODIF ID mdx.
PARAMETERS:     pdelcust TYPE repl AS CHECKBOX MODIF ID md.
PARAMETERS:     ptkinacc TYPE repl AS CHECKBOX MODIF ID mdx.
PARAMETERS:     pbanksc  TYPE knbkb_49b AS CHECKBOX MODIF ID mdx.
SELECTION-SCREEN END OF BLOCK customers.

SELECTION-SCREEN BEGIN OF BLOCK purchase_orders WITH FRAME.
SELECT-OPTIONS: sel_po FOR ekko-ebeln MODIF ID md,
                selerdat FOR ekko-aedat MODIF ID md.
PARAMETERS:     pchdat TYPE fc_datum MODIF ID md.
PARAMETERS:     pgm    TYPE ck_goods_receipt MODIF ID md.
PARAMETERS:     pdelpo TYPE repl AS CHECKBOX MODIF ID md.
SELECTION-SCREEN END OF BLOCK purchase_orders.

SELECTION-SCREEN BEGIN OF BLOCK md_file WITH FRAME.

PARAMETERS:     pmd_file TYPE c AS CHECKBOX MODIF ID mdl USER-COMMAND mdf.
PARAMETERS:     pmd_test TYPE c AS CHECKBOX MODIF ID mdf.
PARAMETERS:     pmd_path TYPE iwp_path_string LOWER CASE MODIF ID mdf.

SELECTION-SCREEN END OF BLOCK md_file.

SELECTION-SCREEN END OF BLOCK md.

****************************************************************
* Retrieve document
SELECTION-SCREEN BEGIN OF BLOCK rt WITH FRAME TITLE text-rso.

SELECTION-SCREEN SKIP.

SELECTION-SCREEN BEGIN OF BLOCK rt_file WITH FRAME TITLE text-tst.

PARAMETERS:     prt_fdbk TYPE c AS CHECKBOX MODIF ID rt.
PARAMETERS:     prt_satt TYPE zdyn_ws_dskip_attach_add AS CHECKBOX MODIF ID rt.
PARAMETERS:     prf_file TYPE c AS CHECKBOX MODIF ID rt USER-COMMAND rff.
PARAMETERS:     prf_fnam TYPE zdyn_ddfilename LOWER CASE MODIF ID rff.
PARAMETERS:     prt_file TYPE c AS CHECKBOX MODIF ID rt USER-COMMAND rtf.
PARAMETERS:     prt_path TYPE iwp_path_string LOWER CASE MODIF ID rtf.


SELECTION-SCREEN END OF BLOCK rt_file.

SELECTION-SCREEN END OF BLOCK rt.

****************************************************************
* Send document
SELECTION-SCREEN BEGIN OF BLOCK sd WITH FRAME TITLE text-snd.

SELECT-OPTIONS: sdl_comp FOR ekko-bukrs NO INTERVALS MODIF ID sd.
SELECT-OPTIONS: sdl_cust FOR ekko-kunnr MODIF ID sd.
SELECT-OPTIONS: sdl_vbel FOR zdyn_stru_fields_with_f4helps-vbeln MODIF ID sd.
SELECT-OPTIONS: sdl_fkar FOR zdyn_stru_fields_with_f4helps-fkart MODIF ID sd.
SELECT-OPTIONS: sdl_kapp FOR zdyn_stru_fields_with_f4helps-kappl MODIF ID sd.
SELECT-OPTIONS: sdl_ksch FOR zdyn_stru_fields_with_f4helps-kschl MODIF ID sd.
SELECT-OPTIONS: sdl_date FOR zdyn_stru_fields_with_f4helps-erdat MODIF ID sd OBLIGATORY.
PARAMETERS:     psd_pdf  TYPE c AS CHECKBOX MODIF ID sd.
PARAMETERS:     psd_att  TYPE c AS CHECKBOX MODIF ID sd.
PARAMETERS:     psd_rat  TYPE c AS CHECKBOX MODIF ID sd.
PARAMETERS:     psd_reg  TYPE c AS CHECKBOX DEFAULT abap_true MODIF ID sd.

SELECTION-SCREEN SKIP.

SELECTION-SCREEN BEGIN OF BLOCK sd_para WITH FRAME TITLE text-par.

PARAMETERS:     psd_para TYPE c AS CHECKBOX MODIF ID sd USER-COMMAND sdp.
PARAMETERS:     psd_grp  TYPE zdyn_stru_fields_with_f4helps-server_group MODIF ID sdp.
PARAMETERS:     psd_proc TYPE wlf_max_tasks DEFAULT 3 MODIF ID sdp.
PARAMETERS:     psd_pack TYPE esh_e_if_package_size DEFAULT 10 MODIF ID sdp.

SELECTION-SCREEN END OF BLOCK sd_para.

SELECTION-SCREEN SKIP.

SELECTION-SCREEN BEGIN OF BLOCK sd_file WITH FRAME TITLE text-tst.

PARAMETERS:     psd_file TYPE c AS CHECKBOX MODIF ID sd USER-COMMAND sdf.
PARAMETERS:     psd_path TYPE iwp_path_string LOWER CASE MODIF ID sdf.

SELECTION-SCREEN END OF BLOCK sd_file.

SELECTION-SCREEN END OF BLOCK sd.

****************************************************************
* Send AP document
SELECTION-SCREEN BEGIN OF BLOCK sp WITH FRAME TITLE text-snp.

SELECT-OPTIONS: spl_comp FOR bkpf-bukrs NO INTERVALS MODIF ID sp.
SELECT-OPTIONS: spl_beln FOR bkpf-belnr NO INTERVALS MODIF ID sp.
SELECT-OPTIONS: spl_gjar FOR bkpf-gjahr NO INTERVALS MODIF ID sp.
SELECT-OPTIONS: spl_xbln FOR bkpf-xblnr NO INTERVALS MODIF ID sp.
SELECT-OPTIONS: spl_blar FOR bkpf-blart NO INTERVALS MODIF ID sp.
SELECT-OPTIONS: spl_cpud FOR bkpf-cpudt NO INTERVALS MODIF ID sp.

PARAMETERS:     psp_lan  TYPE lfa1-land1 MODIF ID sp.
PARAMETERS:     psp_tax  TYPE lfa1-stceg MODIF ID sp.
PARAMETERS:     psp_rnd  TYPE bset-kbetr DEFAULT '0.01' MODIF ID sp.
PARAMETERS:     psp_sig  TYPE c AS CHECKBOX MODIF ID sp.
*PARAMETERS:     psp_reg  TYPE c AS CHECKBOX DEFAULT abap_true MODIF ID sp.

SELECTION-SCREEN SKIP.

PARAMETERS:     psp_nor  TYPE c RADIOBUTTON GROUP doc DEFAULT 'X' MODIF ID sp.
PARAMETERS:     psp_cor  TYPE c RADIOBUTTON GROUP doc MODIF ID sp.
PARAMETERS:     psp_fin  TYPE c RADIOBUTTON GROUP doc MODIF ID sp.

SELECTION-SCREEN SKIP.

SELECTION-SCREEN BEGIN OF BLOCK sp_file WITH FRAME TITLE text-tst.

PARAMETERS:     psp_file TYPE c AS CHECKBOX MODIF ID sp USER-COMMAND spf.
PARAMETERS:     psp_path TYPE iwp_path_string LOWER CASE MODIF ID spf.

SELECTION-SCREEN END OF BLOCK sp_file.

SELECTION-SCREEN END OF BLOCK sp.


****************************************************************
*-----------------------------------------------------------------
* Show/hide areas dependend on selected values
*-----------------------------------------------------------------
AT SELECTION-SCREEN OUTPUT.

  CONCATENATE icon_configuration(3) '\Q' 'General configuration'(GEN) '@'
              INTO gc_bgen.
  CONCATENATE icon_mapped_relation(3) '\Q' 'PD mapping'(PDM) '@'
              INTO gc_bpdm.
  CONCATENATE icon_viewer_optical_archive(3) '\Q' 'Archive link settings'(ARC) '@'
              INTO gc_barc.
  CONCATENATE icon_mapped_relation(3) '\Q' 'Accounts Receivable output'(ARI) '@'
              INTO gc_bari.
  CONCATENATE icon_system_copy(3) '\Q' 'SAP to XML mapping'(XMP) '@'
              INTO gc_bxmp.
  CONCATENATE icon_table_settings(3) '\Q' 'XML template'(XML) '@'
              INTO gc_bxml.
  CONCATENATE icon_information(3) '\Q' 'Version Info'(VER) '@'
              INTO gc_bver.

  PERFORM screen_adjust.
  PERFORM toolbar_adjust.
  PERFORM restrict_select.

AT SELECTION-SCREEN ON RADIOBUTTON GROUP act.

  PERFORM screen_adjust.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR prf_fnam.
  gs_rf_file_name = prf_fnam.
  go_file->get_file_name( EXPORTING is_file_name = gs_rf_file_name
                          IMPORTING es_file_name = gs_rf_file_name ).
  prf_fnam = gs_rf_file_name.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR prt_path.
  gs_rt_file_path = prt_path.
  go_file->get_file_path( EXPORTING is_file_path = gs_rt_file_path
                          IMPORTING es_file_path = gs_rt_file_path ).
  prt_path = gs_rt_file_path.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR pmd_path.
  gs_md_file_path = pmd_path.
  go_file->get_file_path( EXPORTING is_file_path = gs_md_file_path
                          IMPORTING es_file_path = gs_md_file_path ).
  pmd_path = gs_md_file_path.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR psd_path.
  gs_sd_file_path = psd_path.
  go_file->get_file_path( EXPORTING is_file_path = gs_sd_file_path
                          IMPORTING es_file_path = gs_sd_file_path ).
  psd_path = gs_sd_file_path.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR psp_path.
  gs_sp_file_path = psp_path.
  go_file->get_file_path( EXPORTING is_file_path = gs_sp_file_path
                          IMPORTING es_file_path = gs_sp_file_path ).
  psp_path = gs_sp_file_path.

AT SELECTION-SCREEN ON pbanks.
  LOOP AT SCREEN.
    IF screen-name = 'PDELBANK'.
      IF pbanks IS INITIAL.
        screen-active = 0.
      ELSE.
        screen-active = 1.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.


AT SELECTION-SCREEN.

  CLEAR gc_tcode.
  CASE sy-ucomm.
    WHEN 'CFGGEN'.
      gc_tcode = gc_ta_gen.
    WHEN 'CFGPDM'.
      gc_tcode = gc_ta_map.
    WHEN 'CFGARC'.
      gc_tcode = gc_ta_arc.
    WHEN 'CFGARI'.
      gc_tcode = gc_ta_ari.
    WHEN 'CFGXMP'.
      gc_tcode = gc_ta_xmp.
    WHEN 'CFGXML'.
      gc_tcode = gc_ta_xml.
    WHEN 'CFGVER'.
      PERFORM show_version USING con_lic_component con_module.
      CLEAR sy-ucomm.
    WHEN 'DYNS'.
      IF gb_dyn_sel_active = abap_false.
        gb_dyn_sel_active = abap_true.
      ELSE.
        gb_dyn_sel_active = abap_false.
      ENDIF.
    WHEN OTHERS.
*  nothing
  ENDCASE.

  IF NOT gc_tcode IS INITIAL.
    PERFORM authority_check.
    CHECK NOT gc_tcode IS INITIAL.
    CALL TRANSACTION gc_tcode.
  ELSEIF NOT sy-ucomm IS INITIAL.
    IF puser IS INITIAL AND pid IS INITIAL.
      CLEAR sy-ucomm.
      MESSAGE e386(so).
    ENDIF.
  ENDIF.

INITIALIZATION.

  gc_tcode = con_ta_main.
  PERFORM authority_check.
  CLEAR gc_tcode.

*&---------------------------------------------------------------------*
*&      Form  TOOLBAR_ADJUST
*&---------------------------------------------------------------------*
FORM toolbar_adjust .

  DATA lt_exclude TYPE TABLE OF sy-ucomm.

  IF psnd IS INITIAL AND psap IS INITIAL.
    APPEND 'DYNS' TO lt_exclude.
    CALL FUNCTION 'RS_SET_SELSCREEN_STATUS'
      EXPORTING
        p_status  = sy-pfkey
      TABLES
        p_exclude = lt_exclude.

    IF gb_dyn_sel_active = abap_true.
      sy-ucomm = 'DYNS'.
    ENDIF.

  ELSE.
    CALL FUNCTION 'RS_SET_SELSCREEN_STATUS'
      EXPORTING
        p_status  = sy-pfkey
      TABLES
        p_exclude = lt_exclude.
  ENDIF.

ENDFORM.

*&---------------------------------------------------------------------*
*&      Form  SCREEN_ADJUST
*&---------------------------------------------------------------------*
FORM screen_adjust .

  IF NOT psap IS INITIAL AND NOT pbukrs IS INITIAL.
    SET PARAMETER ID 'BUK' FIELD pbukrs.
  ENDIF.

  IF sy-cprog = 'ZDYN_WS_ROUTTY_EXP_SYNC'.
    gi_skip = 0.
  ELSE.
    gi_skip = 1.
  ENDIF.

  LOOP AT SCREEN.

    IF screen-group1 = 'DL'.
      IF papplg IS INITIAL.
        screen-active = 0.
      ELSE.
        screen-active = 1.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.
*   Routty AP
    IF screen-group1 = 'MD'.
      IF psap IS INITIAL OR sy-cprog = 'ZDYN_WS_ROUTTY_EXP_SYNC'.
*      IF psap IS INITIAL.
        screen-active = 0.
      ELSE.
        screen-active = 1.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.
*   Routty Cloud
    IF screen-group1 = 'MDX'.
      IF psap IS INITIAL.
        screen-active = 0.
      ELSE.
        screen-active = 1.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.
    IF screen-group1 = 'RSO'.
      IF prso IS INITIAL.
        screen-active = 0.
      ELSE.
        screen-active = 1.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.
    IF screen-group1 = 'MDL'.
      IF psap IS INITIAL OR sy-cprog <> 'ZDYN_WS_ROUTTY_EXP_SYNC'.
        screen-active = 0.
      ELSE.
        screen-active = 1.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.
    IF screen-group1 = 'MDF'.
      IF pmd_file IS INITIAL OR psap IS INITIAL OR sy-cprog <> 'ZDYN_WS_ROUTTY_EXP_SYNC'.
        screen-active = 0.
      ELSE.
        screen-active = 1.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.
    IF screen-group1 = 'SD'.
      IF psnd IS INITIAL OR sy-cprog <> 'ZDYN_WS_ROUTTY_EXP_SYNC'.
        screen-active = 0.
      ELSE.
        screen-active = 1.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.
    IF screen-group1 = 'SDB'.
      IF sy-cprog <> 'ZDYN_WS_ROUTTY_EXP_SYNC'.
        screen-active = 0.
      ELSE.
        screen-active = 1.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.
*   Send AP documents
    IF screen-group1 = 'SP'.
      IF psnp IS INITIAL OR sy-cprog <> 'ZDYN_WS_ROUTTY_EXP_SYNC'.
        screen-active = 0.
      ELSE.
        screen-active = 1.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.
    IF screen-group1 = 'SNP'.
      IF sy-cprog <> 'ZDYN_WS_ROUTTY_EXP_SYNC'.
        screen-active = 0.
      ELSE.
        screen-active = 1.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.
    IF screen-group1 = 'AP'.
      IF sy-cprog = 'ZDYN_WS_ROUTTY_SYNC'.
        screen-active = 1.
      ELSE.
        screen-active = 0.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.
    IF screen-group1 = 'AR'.
      IF sy-cprog = 'ZDYN_WS_ROUTTY_ARCH_SYNC' OR sy-cprog = 'ZDYN_WS_ROUTTY_EXP_SYNC'.
        screen-active = 1.
      ELSE.
        screen-active = 0.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.
    IF screen-group1 = 'EX'.
      IF sy-cprog = 'ZDYN_WS_ROUTTY_EXP_SYNC'.
        screen-active = 1.
      ELSE.
        screen-active = 0.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.
    IF screen-group1 = 'SDF'.
      IF psd_file IS INITIAL OR psnd IS INITIAL OR sy-cprog <> 'ZDYN_WS_ROUTTY_EXP_SYNC'.
        screen-active = 0.
      ELSE.
        screen-active = 1.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.
    IF screen-group1 = 'SDP'.
      IF psd_para IS INITIAL OR psnd IS INITIAL OR sy-cprog <> 'ZDYN_WS_ROUTTY_EXP_SYNC'.
        screen-active = 0.
      ELSE.
        screen-active = 1.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.
*   File parameters for Send AP documents
    IF screen-group1 = 'SPF'.
      IF psp_file IS INITIAL OR psnp IS INITIAL OR sy-cprog <> 'ZDYN_WS_ROUTTY_EXP_SYNC'.
        screen-active = 0.
      ELSE.
        screen-active = 1.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.
    IF screen-group1 = 'RT'.
      IF prso IS INITIAL.
        screen-active = 0.
      ELSE.
        screen-active = 1.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.
    IF screen-group1 = 'RTF'.
      IF prso IS INITIAL OR prt_file IS INITIAL.
        screen-active = 0.
      ELSE.
        screen-active = 1.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.
    IF screen-group1 = 'RFF'.
      IF prso IS INITIAL OR prf_file IS INITIAL.
        screen-active = 0.
      ELSE.
        screen-active = 1.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.
    IF screen-name = 'PDELBANK'.
      IF pbanks IS INITIAL.
        screen-active = 0.
      ELSE.
        screen-active = 1.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.

  ENDLOOP.

ENDFORM.

*&---------------------------------------------------------------------*
*&      Form  AUTHORITY_CHECK
*&---------------------------------------------------------------------*
FORM authority_check .

  CALL FUNCTION 'AUTHORITY_CHECK_TCODE'
    EXPORTING
      tcode  = gc_tcode
    EXCEPTIONS
      ok     = 0
      OTHERS = 1.
  IF sy-subrc NE 0.
    MESSAGE s172(00) WITH gc_tcode.
    CLEAR gc_tcode.
    EXIT.
  ENDIF.

ENDFORM.
