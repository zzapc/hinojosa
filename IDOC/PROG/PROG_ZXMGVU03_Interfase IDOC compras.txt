*&---------------------------------------------------------------------*
*&  Include           ZXMGVU03
*&---------------------------------------------------------------------*
*"*"Lokale Schnittstelle:
*"       IMPORTING
*"             VALUE(MESSAGE_TYPE) LIKE  EDMSG-MSGTYP
*"             VALUE(SEGMENT_NAME) LIKE  EDIDD-SEGNAM
*"             VALUE(F_MARA) LIKE  MARA STRUCTURE  MARA OPTIONAL
*"             VALUE(F_MAKT) LIKE  MAKT STRUCTURE  MAKT OPTIONAL
*"             VALUE(F_MARC) LIKE  MARC STRUCTURE  MARC OPTIONAL
*"             VALUE(F_MARD) LIKE  MARD STRUCTURE  MARD OPTIONAL
*"             VALUE(F_MFHM) LIKE  MFHM STRUCTURE  MFHM OPTIONAL
*"             VALUE(F_MPGD) LIKE  MPGD STRUCTURE  MPGD OPTIONAL
*"             VALUE(F_MPOP) LIKE  MPOP STRUCTURE  MPOP OPTIONAL
*"             VALUE(F_MPRW) LIKE  MPRW STRUCTURE  MPRW OPTIONAL
*"             VALUE(F_MVEG) LIKE  MVEG STRUCTURE  MVEG OPTIONAL
*"             VALUE(F_MVEU) LIKE  MVEU STRUCTURE  MVEU OPTIONAL
*"             VALUE(F_MKAL) LIKE  MKAL STRUCTURE  MKAL OPTIONAL
*"             VALUE(F_MARM) LIKE  MARM STRUCTURE  MARM OPTIONAL
*"             VALUE(F_MEAN) LIKE  MEAN STRUCTURE  MEAN OPTIONAL
*"             VALUE(F_MBEW) LIKE  MBEW STRUCTURE  MBEW OPTIONAL
*"             VALUE(F_MLGN) LIKE  MLGN STRUCTURE  MLGN OPTIONAL
*"             VALUE(F_MVKE) LIKE  MVKE STRUCTURE  MVKE OPTIONAL
*"             VALUE(F_MLAN) LIKE  MLAN STRUCTURE  MLAN OPTIONAL
*"             VALUE(F_MLGT) LIKE  MLGT STRUCTURE  MLGT OPTIONAL
*"       EXPORTING
*"             VALUE(IDOC_CIMTYPE) LIKE  EDIDC-CIMTYP
*"       TABLES
*"              IDOC_DATA STRUCTURE  EDIDD

** IDOC MATERIALES SAP -> AS400 Y SAP -> RTS
DATA: ls_idoc_act TYPE edidd,
      lv_ind      LIKE sy-tabix,
      lv_matnr    TYPE matnr,
      lv_object   LIKE ausp-objek,
      ls_klah     TYPE klah.

DATA lv_num TYPE i.

DATA: ls_ze1maram TYPE ze1maram.
DATA: ls_ze1marclas TYPE ze1marclas.
DATA: ls_ze1marcm TYPE ze1marcm.
DATA: ls_e1maram TYPE e1maram.
DATA: ls_e1marcm TYPE e1marcm.
DATA: ls_idoc_new TYPE edidd.

DATA: lt_class      LIKE sclass OCCURS 0,
      lt_objectdata LIKE clobjdat OCCURS 0,
      ls_objectdata TYPE clobjdat.

DATA: lt_allocvaluesnum  LIKE bapi1003_alloc_values_num OCCURS 0,
      ls_allocvaluesnum  TYPE bapi1003_alloc_values_num,
      lt_allocvalueschar LIKE bapi1003_alloc_values_char OCCURS 0,
      ls_allocvalueschar TYPE bapi1003_alloc_values_char,
      lt_allocvaluescurr LIKE bapi1003_alloc_values_curr OCCURS 0,
      ls_allocvaluescurr TYPE bapi1003_alloc_values_curr,
      lt_return          LIKE bapiret2 OCCURS 0.


FIELD-SYMBOLS:
  <fs_idoc_data>   TYPE edidd.




* Recuperamos el índice
lv_ind        = sy-tabix.
CLEAR ls_idoc_act.
ls_idoc_act  = idoc_data.

IF segment_name = 'E1MARAM'.
  ls_e1maram = ls_idoc_act-sdata.

  SELECT SINGLE mwskz INTO ls_ze1maram-zzmwskz
  FROM ent5035
  WHERE matallgnr = ls_e1maram-matnr
  AND mwskz <> ''.
  IF sy-subrc NE 0.
    ls_ze1maram-zzmwskz = 'S3'.
  ENDIF.


* INI CGIJON - 23.10.20 - INTEGRACIÓN SAP-MES
* RELLENAMOS EL CAMPO ZZWGBEZ60 (DENOMINACIÓN GRUPO DE ARTÍCULOS)
  SELECT SINGLE wgbez60 FROM t023t
    INTO ls_ze1maram-zzwgbez60
    WHERE spras = sy-langu AND
          matkl = ls_e1maram-matkl.
* FIN CGIJON - 23.10.20 - INTEGRACIÓN SAP-MES

  SELECT SINGLE zzcalidad zzgramaje zzancho zzlargo
         INTO   (ls_ze1maram-zzcalidad, ls_ze1maram-zzgramaje, ls_ze1maram-zzancho, ls_ze1maram-zzlargo)
  FROM mara
  WHERE matnr = ls_e1maram-matnr.
  IF sy-subrc = 0.
    ls_idoc_new-segnam = 'ZE1MARAM'.
    ls_idoc_new-sdata = ls_ze1maram.
    APPEND ls_idoc_new TO idoc_data.
  ENDIF.

* Rollout 32MM006 Recuperar caracteristicas del material en IDOC

  CLEAR lv_matnr.
  CALL FUNCTION 'CONVERSION_EXIT_MATN1_INPUT'
    EXPORTING
      input        = ls_e1maram-matnr
    IMPORTING
      output       = lv_matnr
    EXCEPTIONS
      length_error = 1
      OTHERS       = 2.

  lv_object = lv_matnr.

  SELECT * INTO ls_klah
  FROM klah
  WHERE klart = '001'
  AND ( vondt <= sy-datum AND bisdt >= sy-datum ).

    CALL FUNCTION 'BAPI_OBJCL_GETDETAIL'
      EXPORTING
        objectkey       = lv_object
        objecttable     = 'MARA'
        classnum        = ls_klah-class                 " 'Z_TRITURADO'
        classtype       = '001'
*       KEYDATE         = SY-DATUM
*       UNVALUATED_CHARS       = ' '
        language        = sy-langu
* IMPORTING
*       STATUS          =
*       STANDARDCLASS   =
      TABLES
        allocvaluesnum  = lt_allocvaluesnum
        allocvalueschar = lt_allocvalueschar
        allocvaluescurr = lt_allocvaluescurr
        return          = lt_return.


* caracteristicas numericas
    CLEAR ls_idoc_new.
    LOOP AT lt_allocvaluesnum INTO ls_allocvaluesnum.

      ls_ze1marclas-atnam = ls_allocvaluesnum-charact.
      lv_num = ls_allocvaluesnum-value_from.
      ls_ze1marclas-ausp1 = lv_num.
      ls_ze1marclas-atwrt = lv_num .

      ls_idoc_new-segnam = 'ZE1MARCLAS'.
      ls_idoc_new-sdata = ls_ze1marclas.
      APPEND ls_idoc_new TO idoc_data.
      CLEAR ls_idoc_new.
    ENDLOOP.

* caracteristicas caracter
    CLEAR ls_idoc_new.
    LOOP AT lt_allocvalueschar INTO ls_allocvalueschar.

      ls_ze1marclas-atnam = ls_allocvalueschar-charact.
      ls_ze1marclas-ausp1 = ls_allocvalueschar-value_char.
      ls_ze1marclas-atwrt = ls_allocvalueschar-value_neutral.

      ls_idoc_new-segnam = 'ZE1MARCLAS'.
      ls_idoc_new-sdata = ls_ze1marclas.
      APPEND ls_idoc_new TO idoc_data.
      CLEAR ls_idoc_new.
    ENDLOOP.

* caracteristicas moneda
    CLEAR ls_idoc_new.
    LOOP AT lt_allocvaluescurr INTO ls_allocvaluescurr.

      ls_ze1marclas-atnam = ls_allocvaluescurr-charact.
      lv_num = ls_allocvaluescurr-value_from.
      ls_ze1marclas-ausp1 = lv_num.
      ls_ze1marclas-atwrt = lv_num.

      ls_idoc_new-segnam = 'ZE1MARCLAS'.
      ls_idoc_new-sdata = ls_ze1marclas.
      APPEND ls_idoc_new TO idoc_data.
      CLEAR ls_idoc_new.
    ENDLOOP.

  ENDSELECT.


ENDIF.

IF segment_name = 'E1MARCM'.
  ls_e1marcm = ls_idoc_act-sdata.

* INI CGIJON - 22.12.20 - INTEGRACIÓN SAP-MES
  SELECT SINGLE werks FROM ztmes_centros
            INTO @DATA(lv_werks)
            WHERE werks = @ls_e1marcm-werks.

  IF sy-subrc = 0.

    READ TABLE idoc_data ASSIGNING <fs_idoc_data> WITH KEY segnam = 'E1MARAM'.
    IF sy-subrc = 0.
      ls_e1maram  = <fs_idoc_data>-sdata.

      SELECT SINGLE * FROM ztmes_matmas
        INTO @DATA(wa_ztmatmas)
        WHERE werks = @ls_e1marcm-werks AND
              mtart = @ls_e1maram-mtart AND
              matkl = @ls_e1maram-matkl.
      IF sy-subrc = 0.
*       verificamos si se ha cambiado la descripción del material, tipo de material, grupo de artículos o unidad de media.
*              U  Actualización
*              I  Insertar
*              E  Borrar (documentación campo individual)
*              D  Borrar
*              J  Insertar (documentación campo individual)

        SELECT * FROM cdhdr
          INTO TABLE @DATA(t_cdhr)
          WHERE objectclas = 'MATERIAL' AND
                objectid = @ls_e1maram-matnr AND
                udate = @sy-datum.

        SORT t_cdhr BY udate utime DESCENDING.
        READ TABLE t_cdhr INTO DATA(wa_cdhr) INDEX 1.

        SELECT SINGLE * FROM cdpos
          INTO @DATA(wa_cdpos)
          WHERE objectclas = @wa_cdhr-objectclas AND
                objectid = @wa_cdhr-objectid AND
                changenr = @wa_cdhr-changenr AND
                ( tabname = 'MARA' or tabname = 'DMAKT' or tabname = 'MARC' ) and
                ( fname = 'MAKTX' OR fname = 'MATKL' OR fname = 'MTART' OR fname = 'GEWEI' or fname = 'LVORM').

        IF sy-subrc = 0.
          CASE wa_cdhr-change_ind.
            WHEN 'E' OR 'D'. "Borrar
              "SIEMPRE MANDAMOS EL IDCO
              ls_ze1marcm-mes = 'X'.
            WHEN 'U'. "Actualización - SOLO MANDAMOS EL IDOC SI SE HA MODIFICADO LOS CAMPOS INDICADOS EN LAS ESPECIFICACIONES.
              ls_ze1marcm-mes = 'X'.
            WHEN 'I' OR 'J'. "  Insertar
              ls_ze1marcm-mes = 'X'.
          ENDCASE.
        ENDIF.

*        ls_ze1marcm-mes = 'X'.
      ENDIF.

    ENDIF.

  ELSE.
*    ls_ze1marcm-mes = 'X'.
    ls_ze1marcm-mes = ''.
  ENDIF.
* FIN CGIJON - 22.12.20 - INTEGRACIÓN SAP-MES

  ls_ze1marcm-werks = ls_e1marcm-werks.

  SELECT SINGLE t001k~bukrs INTO ls_ze1marcm-bukrs
  FROM t001k
  INNER JOIN t001w ON t001w~bwkey = t001k~bwkey
  WHERE t001w~werks = ls_e1marcm-werks.
  IF sy-subrc = 0.
    ls_idoc_new-segnam = 'ZE1MARCM'.
    ls_idoc_new-sdata = ls_ze1marcm.
    APPEND ls_idoc_new TO idoc_data.
  ENDIF.

ENDIF.
