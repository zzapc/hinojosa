*&---------------------------------------------------------------------*
*&  Include           ZXVSVU01
*&---------------------------------------------------------------------*
*"----------------------------------------------------------------------
*"*"Lokale Schnittstelle:
*"       IMPORTING
*"              MESSAGE_TYPE LIKE  TBDME-MESTYP
*"              IDOC_TYPE LIKE  EDIDC-IDOCTP
*"              SEGMENT_NAME LIKE  EDIDD-SEGNAM
*"       EXPORTING
*"              IDOC_CIMTYPE LIKE  EDIDC-CIMTYP
*"       TABLES
*"              IDOC_DATA STRUCTURE  EDIDD
*"----------------------------------------------------------------------
DATA ls_e1kna1m TYPE e1kna1m.
DATA ls_struc TYPE ze1sd01_1.
DATA:l_s1 TYPE string,
     l_s2 TYPE string.

"CVP 04.07.2022 09:40 crear dentro de la ampliacion ZSSDDEBMAS07 segmentos Z para pasar datos de email del cliente
IF message_type cs 'ZDEBMAS_P' AND segment_name CS 'ZE1SD01_'. "ZSDDEBMAS07
  READ TABLE idoc_data INTO DATA(ls_idoc_data) WITH KEY segnam = 'E1KNA1M'.
  IF sy-subrc = 0.
    ls_e1kna1m = ls_idoc_data-sdata.
    SELECT smtp_addr,consnumber,flgdefault,flg_nouse,home_flag
    FROM kna1 AS k
    INNER JOIN adr6 AS a ON a~addrnumber = k~adrnr
    WHERE k~kunnr = @ls_e1kna1m-kunnr
    INTO TABLE @DATA(lt_emails).
  ENDIF.

  LOOP AT idoc_data  ASSIGNING FIELD-SYMBOL(<fs_idoc_data>).
    CASE <fs_idoc_data>-segnam.
      WHEN segment_name.
        SPLIT segment_name AT '_' INTO l_s1 l_s2.
        CHECK l_s2 CO '0123456789'.
        READ TABLE lt_emails INTO DATA(ls_email) INDEX l_s2.
        IF sy-subrc = 0.
          CLEAR ls_struc.
          ls_struc-zzsmtp_addr = ls_email-smtp_addr.
          ls_struc-zzconsnumber = ls_email-consnumber.
          ls_struc-zzflgdefault = ls_email-flgdefault.
          ls_struc-zzflg_nouse = ls_email-flg_nouse.
          ls_struc-zzhome_flag = ls_email-home_flag.
          MOVE ls_struc TO <fs_idoc_data>-sdata.
        ENDIF.
    ENDCASE.
  ENDLOOP.
ENDIF.
