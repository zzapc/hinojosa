*----------------------------------------------------------------------*
*      Print of a delivery note by SAPscript SMART FORMS               *
*----------------------------------------------------------------------*
REPORT zrle_delnote.

* declaration of data
INCLUDE rle_delnote_data_declare.
* definition of forms
INCLUDE rle_delnote_forms.
INCLUDE rle_print_forms.



TYPES: BEGIN OF st_mailattach,
         attachment_type     TYPE soodk-objtp,
         attachment_subject  TYPE sood-objdes,
         attachment_size     TYPE sood-objlen,
         attachment_language TYPE sood-objla,
         att_content_text    TYPE soli_tab,
         att_content_hex     TYPE solix_tab,
         attachment_header   TYPE soli_tab,
         vsi_profile         TYPE vscan_profile,
       END OF st_mailattach.
TYPES: tt_mailattach TYPE TABLE OF st_mailattach WITH DEFAULT KEY.


*---------------------------------------------------------------------*
*       FORM ENTRY
*---------------------------------------------------------------------*
FORM entry USING return_code us_screen.

  DATA: lf_retcode TYPE sy-subrc.
  xscreen = us_screen.
  PERFORM processing USING    us_screen
                     CHANGING lf_retcode.
  IF lf_retcode NE 0.
    return_code = 1.
  ELSE.
    return_code = 0.
  ENDIF.

ENDFORM.
*---------------------------------------------------------------------*
*       FORM ENTRY
*---------------------------------------------------------------------*
FORM adobe_entry USING return_code us_screen.

  DATA lf_retcode TYPE sy-subrc.
  xscreen = us_screen.
  PERFORM adobe_processing USING us_screen CHANGING lf_retcode.
  IF lf_retcode NE 0.
    return_code = 1.
  ELSE.
    return_code = 0.
  ENDIF.

ENDFORM.
*---------------------------------------------------------------------*
*       FORM PROCESSING                                               *
*---------------------------------------------------------------------*
FORM processing USING    proc_screen
                CHANGING cf_retcode.

  DATA: ls_print_data_to_read TYPE ledlv_print_data_to_read.
  DATA: ls_dlv_delnote        TYPE ledlv_delnote.
  DATA: lf_fm_name            TYPE rs38l_fnam.
  DATA: ls_control_param      TYPE ssfctrlop.
  DATA: ls_composer_param     TYPE ssfcompop.
  DATA: ls_recipient          TYPE swotobjid.
  DATA: ls_sender             TYPE swotobjid.
  DATA: lf_formname           TYPE tdsfname.
  DATA: ls_addr_key           LIKE addr_key.

* SmartForm from customizing table TNAPR
  lf_formname = tnapr-sform.

* determine print data
  PERFORM set_print_data_to_read USING    lf_formname
                                 CHANGING ls_print_data_to_read
                                 cf_retcode.

  IF cf_retcode = 0.
* select print data
    PERFORM get_data USING    ls_print_data_to_read
                     CHANGING ls_addr_key
                              ls_dlv_delnote
                              cf_retcode.
  ENDIF.

  IF cf_retcode = 0.
    PERFORM set_print_param USING    ls_addr_key
                            CHANGING ls_control_param
                                     ls_composer_param
                                     ls_recipient
                                     ls_sender
                                     cf_retcode.
  ENDIF.

  IF cf_retcode = 0.
* determine smartform function module for delivery note
    CALL FUNCTION 'SSF_FUNCTION_MODULE_NAME'
      EXPORTING
        formname           = lf_formname
*       variant            = ' '
*       direct_call        = ' '
      IMPORTING
        fm_name            = lf_fm_name
      EXCEPTIONS
        no_form            = 1
        no_function_module = 2
        OTHERS             = 3.
    IF sy-subrc <> 0.
*   error handling
      cf_retcode = sy-subrc.
      PERFORM protocol_update.
    ENDIF.
  ENDIF.

  IF cf_retcode = 0.


*   call smartform delivery note
    CALL FUNCTION lf_fm_name
      EXPORTING
        archive_index      = toa_dara
        archive_parameters = arc_params
        control_parameters = ls_control_param
*       mail_appl_obj      =
        mail_recipient     = ls_recipient
        mail_sender        = ls_sender
        output_options     = ls_composer_param
        user_settings      = ' '
        is_dlv_delnote     = ls_dlv_delnote
        is_nast            = nast
*      importing  document_output_info =
*       job_output_info    =
*       job_output_options =
      EXCEPTIONS
        formatting_error   = 1
        internal_error     = 2
        send_error         = 3
        user_canceled      = 4
        OTHERS             = 5.
    IF sy-subrc <> 0.
*   error handling
      cf_retcode = sy-subrc.
      PERFORM protocol_update.
*     get SmartForm protocoll and store it in the NAST protocoll
      PERFORM add_smfrm_prot.                  "INS_HP_335958
    ENDIF.
  ENDIF.

* get SmartForm protocoll and store it in the NAST protocoll
* PERFORM ADD_SMFRM_PROT.                       DEL_HP_335958

ENDFORM.
*---------------------------------------------------------------------*
*       FORM ADOBE_PROCESSING                                         *
*---------------------------------------------------------------------*
FORM adobe_processing USING proc_screen CHANGING cf_retcode.

  DATA: ls_print_data_to_read TYPE ledlv_print_data_to_read,
        ls_addr_key           LIKE addr_key,
        ls_dlv_delnote        TYPE ledlv_delnote,
        ls_control_param      TYPE ssfctrlop,
        ls_composer_param     TYPE ssfcompop,
        ls_recipient          TYPE swotobjid,
        ls_sender             TYPE swotobjid,
        lf_formname           TYPE fpname,
        lf_fm_name            TYPE funcname.

  ls_print_data_to_read-hd_gen = abap_true.
  ls_print_data_to_read-hd_gen_descript = abap_true.
  ls_print_data_to_read-hd_adr = abap_true.
  ls_print_data_to_read-hd_org = abap_true.
  ls_print_data_to_read-hd_org_adr = abap_true.
  ls_print_data_to_read-it_gen = abap_true.
  ls_print_data_to_read-it_org = abap_true.
  ls_print_data_to_read-it_fin = abap_true.
  ls_print_data_to_read-it_ft = abap_true.
  ls_print_data_to_read-it_ref = abap_true.
  ls_print_data_to_read-it_confbatch = abap_true.
  ls_print_data_to_read-it_qm = abap_true.

  PERFORM get_data USING ls_print_data_to_read CHANGING ls_addr_key ls_dlv_delnote cf_retcode.
  IF cf_retcode = 0.
    PERFORM set_print_param USING ls_addr_key CHANGING ls_control_param ls_composer_param ls_recipient ls_sender cf_retcode.
  ENDIF.
  IF cf_retcode = 0.
    lf_formname = tnapr-sform.
    CALL FUNCTION 'FP_FUNCTION_MODULE_NAME'
      EXPORTING
        i_name     = lf_formname
      IMPORTING
        e_funcname = lf_fm_name.
  ENDIF.
  IF cf_retcode = 0.
    CALL FUNCTION lf_fm_name
      EXPORTING
        archive_index      = toa_dara
        archive_parameters = arc_params
        control_parameters = ls_control_param
        mail_recipient     = ls_recipient
        mail_sender        = ls_sender
        output_options     = ls_composer_param
        user_settings      = space
        is_dlv_delnote     = ls_dlv_delnote
        is_nast            = nast
      EXCEPTIONS
        formatting_error   = 1
        internal_error     = 2
        send_error         = 3
        user_canceled      = 4
        OTHERS             = 5.
    IF sy-subrc <> 0.
      cf_retcode = sy-subrc.
      PERFORM protocol_update.
      PERFORM add_smfrm_prot.
    ENDIF.
  ENDIF.
ENDFORM.






*---------------------------------------------------------------------*
*       FORM MAIL_ENTRY
*---------------------------------------------------------------------*
FORM mail_entry USING return_code us_screen.

  DATA lf_retcode TYPE sy-subrc.

  xscreen = us_screen.
  PERFORM adobe_processing_with_mail USING us_screen CHANGING lf_retcode.
  IF lf_retcode NE 0.
    return_code = 1.
  ELSE.
    return_code = 0.
  ENDIF.

ENDFORM.




*---------------------------------------------------------------------*
*       FORM adobe_processing_with_mail
*---------------------------------------------------------------------*
FORM adobe_processing_with_mail USING proc_screen CHANGING cf_retcode.

  DATA: ls_print_data_to_read TYPE ledlv_print_data_to_read,
        ls_addr_key           LIKE addr_key,
        ls_dlv_delnote        TYPE ledlv_delnote,
        ls_control_param      TYPE ssfctrlop,
        ls_composer_param     TYPE ssfcompop,
        ls_recipient          TYPE swotobjid,
        ls_sender             TYPE swotobjid,
        lf_formname           TYPE fpname,
        lf_fm_name            TYPE funcname.

  DATA: ls_pdf_file                  TYPE fpformoutput.

  ls_print_data_to_read-hd_gen = abap_true.
  ls_print_data_to_read-hd_gen_descript = abap_true.
  ls_print_data_to_read-hd_adr = abap_true.
  ls_print_data_to_read-hd_org = abap_true.
  ls_print_data_to_read-hd_org_adr = abap_true.
  ls_print_data_to_read-it_gen = abap_true.
  ls_print_data_to_read-it_org = abap_true.
  ls_print_data_to_read-it_fin = abap_true.
  ls_print_data_to_read-it_ft = abap_true.
  ls_print_data_to_read-it_ref = abap_true.
  ls_print_data_to_read-it_confbatch = abap_true.
  ls_print_data_to_read-it_qm = abap_true.

  PERFORM get_data USING ls_print_data_to_read CHANGING ls_addr_key ls_dlv_delnote cf_retcode.
  IF cf_retcode = 0.
    PERFORM set_print_param USING ls_addr_key CHANGING ls_control_param ls_composer_param ls_recipient ls_sender cf_retcode.
  ENDIF.
  IF cf_retcode = 0.
    lf_formname = tnapr-sform.
    CALL FUNCTION 'FP_FUNCTION_MODULE_NAME'
      EXPORTING
        i_name     = lf_formname
      IMPORTING
        e_funcname = lf_fm_name.
  ENDIF.
  IF cf_retcode = 0.
    ls_control_param-getotf = 'X'.

    DATA: wl_document_output_info TYPE ssfcrespd,
          wl_job_output_info      TYPE ssfcrescl,
          wl_job_output_options   TYPE ssfcresop.

    CALL FUNCTION lf_fm_name
      EXPORTING
        archive_index        = toa_dara
        archive_parameters   = arc_params
        control_parameters   = ls_control_param
        mail_recipient       = ls_recipient
        mail_sender          = ls_sender
        output_options       = ls_composer_param
        user_settings        = space
        is_dlv_delnote       = ls_dlv_delnote
        is_nast              = nast
      IMPORTING
        document_output_info = wl_document_output_info
        job_output_info      = wl_job_output_info
        job_output_options   = wl_job_output_options
      EXCEPTIONS
        formatting_error     = 1
        internal_error       = 2
        send_error           = 3
        user_canceled        = 4
        OTHERS               = 5.

    IF sy-subrc = 0.
      DATA: vl_filesize TYPE i,
            tl_pdf      TYPE TABLE OF tline WITH HEADER LINE,
            vl_pdf      TYPE xstring.
      CALL FUNCTION 'CONVERT_OTF'
        EXPORTING
          format                = 'PDF'
        IMPORTING
          bin_filesize          = vl_filesize
          bin_file              = vl_pdf
        TABLES
          otf                   = wl_job_output_info-otfdata[]
          lines                 = tl_pdf
        EXCEPTIONS
          err_max_linewidth     = 1
          err_format            = 2
          err_conv_not_possible = 3
          OTHERS                = 4.
    ENDIF.

    IF sy-subrc <> 0.
      cf_retcode = sy-subrc.
      PERFORM protocol_update.
      PERFORM add_smfrm_prot.
    ENDIF.
  ENDIF.


  IF vl_pdf IS NOT INITIAL.
    DATA: wl_vbrk      TYPE vbrk,
          wl_kna1      TYPE kna1,
          tl_smtp_addr TYPE TABLE OF adr6.

    SELECT SINGLE lfart
      INTO @DATA(vl_lfart)
      FROM likp
      WHERE vbeln = @ls_dlv_delnote-hd_org-deliv_numb.


    DATA: tl_matnr_rg TYPE RANGE OF mara-matnr,
          wl_matnr_rg LIKE LINE OF tl_matnr_rg.
    LOOP AT ls_dlv_delnote-it_gen ASSIGNING FIELD-SYMBOL(<fs_gen>).
      wl_matnr_rg = VALUE #( sign = 'I' option = 'EQ' low = <fs_gen>-material ).
      COLLECT wl_matnr_rg INTO tl_matnr_rg.
    ENDLOOP.

    IF tl_matnr_rg IS NOT INITIAL.
      SELECT email AS smtp_addr
        INTO CORRESPONDING FIELDS OF TABLE tl_smtp_addr
        FROM zssdt056
        WHERE vstel = ls_dlv_delnote-hd_org-ship_point    AND
              lfart = vl_lfart                            AND
              kunnr = ls_dlv_delnote-hd_gen-ship_to_party AND
              matnr IN tl_matnr_rg
        ORDER BY contador.
    ENDIF.

    IF tl_smtp_addr IS INITIAL.
      SELECT email AS smtp_addr
        INTO CORRESPONDING FIELDS OF TABLE tl_smtp_addr
        FROM zssdt056
        WHERE vstel = ls_dlv_delnote-hd_org-ship_point    AND
              lfart = vl_lfart                            AND
              kunnr = ls_dlv_delnote-hd_gen-ship_to_party AND
              matnr = ''
        ORDER BY contador.
    ENDIF.




    " Asunto
    DATA: vp_subject      TYPE so_obj_des.
    vp_subject = |Hinojosa: Boletín calidad albarán { ls_dlv_delnote-hd_gen-deliv_numb }|.

    " Cuerpo del mensaje. Está grabado usando SO10.
    DATA: tl_textos       TYPE STANDARD TABLE OF tline,
          tl_texto        TYPE tline,
          tl_message_body TYPE bcsy_text,
          wl_message_body LIKE LINE OF tl_message_body,
          tl_anexos	      TYPE tt_mailattach.

    "JCB 02.02.23. 75264. SD - Clase de mensaje ZL99 añadir info cuerpo mail
    DATA(objkey)      = nast-objky(10).
    DATA(objkey_pos)  = nast-objky+10(6).
    READ TABLE ls_dlv_delnote-it_gen INTO DATA(ls_gen_pos)
      WITH KEY  deliv_numb = objkey
                itm_number = objkey_pos.
    IF sy-subrc = 0 AND ls_gen_pos-batch IS NOT INITIAL.
      wl_message_body-line = |Se adjunta Boletín de calidad correspondiente al albarán { ls_dlv_delnote-hd_gen-deliv_numb } y lote { ls_gen_pos-batch }|.
    ELSE.
      wl_message_body-line = |Se adjunta Boletín de calidad correspondiente al albarán { ls_dlv_delnote-hd_gen-deliv_numb }|.
    ENDIF.
    APPEND wl_message_body TO tl_message_body.
    "Fin JCB

    " Anexos
    APPEND INITIAL LINE TO tl_anexos ASSIGNING FIELD-SYMBOL(<fs_anexos>).
    <fs_anexos>-attachment_size = xstrlen( vl_pdf ).
    <fs_anexos>-att_content_hex = cl_bcs_convert=>xstring_to_solix( vl_pdf ).
    <fs_anexos>-attachment_subject = ls_dlv_delnote-hd_gen-deliv_numb && '.pdf'.

    LOOP AT tl_smtp_addr ASSIGNING FIELD-SYMBOL(<fs_smtp_addr>).
      PERFORM f_enviar_correo
        USING   <fs_smtp_addr>-smtp_addr
                tl_message_body
                vp_subject
                tl_anexos.
    ENDLOOP.
  ENDIF.

ENDFORM.



*&---------------------------------------------------------------------*
*&      Form  f_enviar_correo
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->VP_DESTINATARIO  text
*      -->TP_MESSAGE_BODY  text
*      -->VP_SUBJECT       text
*----------------------------------------------------------------------*
FORM f_enviar_correo
  USING   vp_destinatario TYPE mailaddress
          tp_message_body TYPE bcsy_text
          vp_subject      TYPE so_obj_des
          tp_anexos	      TYPE tt_mailattach.

  DATA: rl_send_request TYPE REF TO cl_bcs VALUE IS INITIAL,
        rl_document     TYPE REF TO cl_document_bcs VALUE IS INITIAL,
        rl_sender       TYPE REF TO if_sender_bcs VALUE IS INITIAL,
        rl_recipient    TYPE REF TO if_recipient_bcs VALUE IS INITIAL.

  DATA: vl_usuario LIKE sy-uname,
        vl_result  TYPE  boolean.

  " Preparamos a la petición
  CLASS cl_bcs DEFINITION LOAD.
  TRY .
      rl_send_request = cl_bcs=>create_persistent( ).
    CATCH cx_send_req_bcs.
  ENDTRY.

  "Creamos el documento
  TRY .
      rl_document = cl_document_bcs=>create_document(
        i_type = 'RAW'
        i_text =  tp_message_body
        i_subject = vp_subject ).
    CATCH cx_document_bcs.
  ENDTRY.

  " Pasamos el documento a la request
  TRY.
      rl_send_request->set_document( rl_document ).
    CATCH cx_send_req_bcs.
  ENDTRY.



  TRY.
      rl_sender = cl_cam_address_bcs=>create_internet_address( i_address_string = 'mmartinezv@hinojosa.es' ).
    CATCH cx_address_bcs.
  ENDTRY.

  TRY.
      rl_send_request->set_sender(
        EXPORTING
          i_sender = rl_sender ).
    CATCH cx_send_req_bcs.
  ENDTRY.


  CLEAR vl_usuario.
  vl_usuario = vp_destinatario.
  SEARCH vp_destinatario FOR '@'.
  IF sy-subrc = 0.
    TRY .
        rl_recipient = cl_cam_address_bcs=>create_internet_address( vp_destinatario ).
      CATCH cx_address_bcs.
    ENDTRY.

  ELSE.
    vl_usuario = vp_destinatario.
    TRY.
        rl_recipient = cl_sapuser_bcs=>create( vl_usuario ).
      CATCH cx_address_bcs.
        RETURN.
    ENDTRY.
  ENDIF.

  TRY .
      rl_send_request->add_recipient(
          EXPORTING
            i_recipient = rl_recipient
            i_express = 'X'
            i_no_forward = ' ').

    CATCH cx_send_req_bcs.

  ENDTRY.

  "Añadimos posibles anexos
  FIELD-SYMBOLS:      <fs_anexos>        LIKE LINE OF tp_anexos.
  LOOP AT tp_anexos ASSIGNING <fs_anexos>.
    TRY .
        rl_document->add_attachment(
          EXPORTING
            i_attachment_type     = <fs_anexos>-attachment_type
            i_attachment_subject  = <fs_anexos>-attachment_subject
            i_attachment_size     = <fs_anexos>-attachment_size
            i_attachment_language = <fs_anexos>-attachment_language
            i_att_content_text    = <fs_anexos>-att_content_text
            i_att_content_hex     = <fs_anexos>-att_content_hex
            i_attachment_header   = <fs_anexos>-attachment_header
         ).

      CATCH cx_document_bcs.

    ENDTRY.
  ENDLOOP.

  TRY.
      rl_send_request->set_send_immediately( abap_true ).

      rl_send_request->send(
        EXPORTING
          i_with_error_screen = ' '
        RECEIVING
          result = vl_result ).

    CATCH cx_send_req_bcs.
      vl_result = ''.
  ENDTRY.

ENDFORM.                    "f_enviar_correo
