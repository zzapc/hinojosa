FUNCTION zsd_if_omp_hu_data_lqua.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     REFERENCE(VP_LGNUM) TYPE  LGNUM
*"     REFERENCE(VP_LQNUM) TYPE  LVS_LQNUM
*"     VALUE(WP_LQUA) TYPE  LQUA
*"  CHANGING
*"     REFERENCE(WP_DATA) TYPE  ZCCHANDLINGUNITEVENTDATA
*"----------------------------------------------------------------------
  DATA: vl_fecha TYPE sy-datum,
        vl_hora  TYPE sy-uzeit.

  vl_fecha = sy-datum.
  vl_hora = sy-uzeit.

  "
  " Configuración criterios selección.
  "
  DATA: wl_config_omp  TYPE zssd_if_omp_cfg,
        rl_range_mtart TYPE efg_tab_ranges.
  CALL FUNCTION 'ZSD_IF_OMP_TRANS_GET_CONFG'
    EXPORTING
      vp_lgnum  = vp_lgnum
    IMPORTING
      wp_config = wl_config_omp
    EXCEPTIONS
      no_config = 1
      OTHERS    = 2.
  CHECK sy-subrc = 0.


  CLEAR wp_data.

  IF wp_lqua IS INITIAL.
    SELECT SINGLE *
      FROM lqua
      INTO wp_lqua
      WHERE lgnum = vp_lgnum AND
            lqnum = vp_lqnum.
    IF sy-subrc <> 0.
      CLEAR wp_lqua.
    ENDIF.
  ENDIF.

  CHECK wp_lqua IS NOT INITIAL.

  "
  " Obtener datos del material
  "
  SELECT SINGLE cuobj
    INTO @DATA(vl_cuobj)
    FROM marc
    WHERE matnr EQ @wp_lqua-matnr AND
          werks EQ @wp_lqua-werks.

  IF sy-subrc <> 0.
    CLEAR vl_cuobj.
  ENDIF.

  SELECT SINGLE *
    INTO @DATA(wl_yhp_idx_td151)
    FROM yhp_idx_td151
    WHERE cuobj = @vl_cuobj AND
          shuty = 'UNIT'.
  IF sy-subrc <> 0.
    CLEAR wl_yhp_idx_td151.
  ENDIF.

  "
  " completamos datos para el IDOC
  "
  CLEAR wp_data-comments.
  CLEAR wp_data-endcreation.
  CLEAR wp_data-endeff.
***wp_data-ERPHANDLINGUNITID
  CALL FUNCTION 'CONVERSION_EXIT_LENUM_OUTPUT'
    EXPORTING
      input           = wp_lqua-lenum
    IMPORTING
      output          = wp_data-erphandlingunitid
    EXCEPTIONS
      t344_get_failed = 1
      OTHERS          = 2.
  IF sy-subrc <> 0.
    CLEAR wp_data-erphandlingunitid.
  ENDIF.
  wp_data-event = 'Confirmed'.
  IF wp_lqua-bestq <> space.
    wp_data-event = 'OnHold'.
  ENDIF.

  CALL FUNCTION 'ZSD_IF_OMP_COMP_DATETIME'
    EXPORTING
      vp_fecha    = vl_fecha
      vp_hora     = vl_hora
    IMPORTING
      vp_datetime = wp_data-eventdate.

*  wp_data-eventdate = |{ vl_fecha+6(2) }/{ vl_fecha+4(2) }/{ vl_fecha(4) },{ vl_hora(2) }:{ vl_hora+2(2) }:{ vl_hora+4(2) }|.
***wp_data-EVENTDATESEQNR
  wp_data-footprintlength = wl_yhp_idx_td151-length.
  CALL FUNCTION 'ZSD_IF_OMP_GET_UM'
    EXPORTING
      vp_meins = wl_yhp_idx_td151-length_uom
    IMPORTING
      vp_mseh6 = wp_data-footprintlength_uom.

  wp_data-footprintwidth = wl_yhp_idx_td151-width.
  CALL FUNCTION 'ZSD_IF_OMP_GET_UM'
    EXPORTING
      vp_meins = wl_yhp_idx_td151-width_uom
    IMPORTING
      vp_mseh6 = wp_data-footprintwidth_uom.

  wp_data-goodsid = wp_lqua-matnr.
  wp_data-grossheight = wl_yhp_idx_td151-height.
  CALL FUNCTION 'ZSD_IF_OMP_GET_UM'
    EXPORTING
      vp_meins = wl_yhp_idx_td151-height_uom
    IMPORTING
      vp_mseh6 = wp_data-grossheight_uom.

  wp_data-grossweight = wp_lqua-mgewi." Unidad es GEWEI.
  CALL FUNCTION 'ZSD_IF_OMP_GET_UM'
    EXPORTING
      vp_meins = wp_lqua-gewei
    IMPORTING
      vp_mseh6 = wp_data-grossweight_uom.

  wp_data-handlingunitquantity = wp_lqua-verme.
  CALL FUNCTION 'ZSD_IF_OMP_GET_UM'
    EXPORTING
      vp_meins = wp_lqua-meins
    IMPORTING
      vp_mseh6 = wp_data-handlingunitquantity_uom.
***wp_data-NETWEIGHT
  CLEAR wp_data-orderid.
  CLEAR wp_data-runid.
  CLEAR wp_data-startcreation.
  CLEAR wp_data-starteff.
  CLEAR wp_data-stepid.
  CLEAR wp_data-storageposition.
  wp_data-transportlocationid = wl_config_omp-transportlocationid.
  wp_data-whid = vp_lgnum.


ENDFUNCTION.
