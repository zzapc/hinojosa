FUNCTION zsdcom_comprobar_impagados_org .
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     VALUE(I_VALUT) TYPE  VALUT DEFAULT SY-DATUM
*"  EXPORTING
*"     VALUE(E_IMPAGADOS) TYPE  FLAG
*"  TABLES
*"      IT_BSEG STRUCTURE  ZSSDCOM_BSEG OPTIONAL
*"----------------------------------------------------------------------
  DATA: lti_bseg    TYPE TABLE OF zssdcom_bseg."bseg.
  DATA: ls_bseg_f TYPE zssdcom_bseg, "bseg,
        ls_bseg_l TYPE zssdcom_bseg. "bseg.
  DATA: lv_budat    TYPE budat.

* Inicializamos
  CLEAR: e_impagados.

* Impagados solo existen para posiciones Deudor...
  LOOP AT it_bseg  INTO  ls_bseg_f
                   WHERE kunnr IS NOT INITIAL
                   AND   koart = 'D'.
*   Sacamos partidas abiertas
    SELECT bukrs belnr gjahr buzei augdt augbl kunnr
             rebzg rebzj rebzz bschl umskz umsks zumsk hkont shkzg dmbtr
             wrbtr APPENDING CORRESPONDING FIELDS OF TABLE lti_bseg
        FROM  bsid
        WHERE bukrs  = ls_bseg_f-bukrs
        AND   rebzj  = ls_bseg_f-gjahr
        AND   rebzg  = ls_bseg_f-belnr
        AND   rebzz  = ls_bseg_f-buzei
        AND   augbl <> ls_bseg_f-belnr.

*   Sacamos partidas compensadas
    SELECT bukrs belnr gjahr buzei augdt augbl kunnr
             rebzg rebzj rebzz bschl umskz umsks zumsk hkont shkzg dmbtr
             wrbtr APPENDING CORRESPONDING FIELDS OF TABLE lti_bseg
      FROM  bsad
      WHERE bukrs  = ls_bseg_f-bukrs
      AND   rebzj  = ls_bseg_f-gjahr
      AND   rebzg  = ls_bseg_f-belnr
      AND   rebzz  = ls_bseg_f-buzei
      AND   augbl <> ls_bseg_f-belnr.
  ENDLOOP.

* Nos quedamos con los documentos impagados y sus asociados que hemos encontrado
  LOOP AT lti_bseg INTO ls_bseg_l.
*   Chequeamos si el documento ya está en la entrada
    READ TABLE it_bseg INTO ls_bseg_f WITH KEY belnr = ls_bseg_l-belnr
                                               gjahr = ls_bseg_l-gjahr
                                               buzei = ls_bseg_l-buzei.
    CHECK sy-subrc <> 0.

*   Recuperamos la clase de cuenta de la posición del doc. FI
    SELECT SINGLE koart INTO ls_bseg_l-koart
        FROM  bseg
        WHERE bukrs = ls_bseg_l-bukrs
        AND   belnr = ls_bseg_l-belnr
        AND   gjahr = ls_bseg_l-gjahr
        AND   buzei = ls_bseg_l-buzei.

*   Comprobamos la fecha de contabilización
    DATA lv_stblg LIKE bkpf-stblg.
    CLEAR lv_stblg.
    IF i_valut IS NOT INITIAL.
      CLEAR lv_budat.

      SELECT SINGLE budat stblg INTO (lv_budat, lv_stblg) FROM bkpf
          WHERE bukrs = ls_bseg_l-bukrs
          AND   gjahr = ls_bseg_l-gjahr
          AND   belnr = ls_bseg_l-belnr.
      IF lv_budat <= i_valut AND lv_stblg IS INITIAL.
        APPEND ls_bseg_l TO it_bseg.
        e_impagados = 'X'.
      ENDIF.
*   Añadimos la entrada
    ELSE.
      SELECT SINGLE budat stblg INTO (lv_budat, lv_stblg) FROM bkpf
    WHERE bukrs = ls_bseg_l-bukrs
    AND   gjahr = ls_bseg_l-gjahr
    AND   belnr = ls_bseg_l-belnr.
      IF  lv_stblg IS INITIAL.
        APPEND ls_bseg_l TO it_bseg.
        e_impagados = 'X'.
      ENDIF.
    ENDIF.
  ENDLOOP.

ENDFUNCTION.
