FUNCTION zsui5_sd_get_pedidos_oficina.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     VALUE(I_KUNNR) TYPE  KUNNR OPTIONAL
*"     VALUE(I_EJERCICIO) TYPE  MJAHR OPTIONAL
*"     VALUE(I_BACKOFFICE) TYPE  LIFNR OPTIONAL
*"     VALUE(I_REGIO) TYPE  REGIO OPTIONAL
*"     VALUE(I_CP) TYPE  PSTLZ OPTIONAL
*"     VALUE(I_HISTORICO) TYPE  FLAG OPTIONAL
*"     VALUE(IR_VKORG) TYPE  RSELOPTION OPTIONAL
*"  EXPORTING
*"     VALUE(E_RETURN) TYPE  BAPIRET2_T
*"     VALUE(E_DATA) TYPE  ZSUI5TT_SD_PED_OFICINA_TECNICA
*"----------------------------------------------------------------------

  DATA: ls_return TYPE bapiret2,
        lv_name   TYPE thead-tdname,
        r_kunnr   TYPE RANGE OF kunnr,
        r_regio   TYPE RANGE OF regio,
        r_cp      TYPE RANGE OF pstlz.

  DATA: r_back_ofiice TYPE RANGE OF lifnr.
  IF i_backoffice IS NOT INITIAL.
    r_back_ofiice = VALUE #( ( sign = 'I' option = 'EQ' low = i_backoffice ) ) .
  ENDIF.

  IF i_kunnr IS NOT INITIAL.
    APPEND INITIAL LINE TO r_kunnr ASSIGNING FIELD-SYMBOL(<ls_kunnr>).
    <ls_kunnr>-option = 'EQ'.
    <ls_kunnr>-sign = 'I'.
    <ls_kunnr>-low = i_kunnr.
  ENDIF.

  IF i_cp IS NOT INITIAL.
    APPEND INITIAL LINE TO r_cp ASSIGNING FIELD-SYMBOL(<ls_cp>).
    <ls_cp>-option = 'EQ'.
    <ls_cp>-sign = 'I'.
    <ls_cp>-low = i_cp.
  ENDIF.

  IF i_regio IS NOT INITIAL.
    APPEND INITIAL LINE TO r_regio ASSIGNING FIELD-SYMBOL(<ls_regio>).
    <ls_regio>-option = 'EQ'.
    <ls_regio>-sign = 'I'.
    <ls_regio>-low = i_regio.
  ENDIF.


  SELECT a~kunnr, ka~name1 AS kunnrname, a~vbeln, b~posnr, a~bstnk, b~matnr, b~arktx, b~zstatp, b~zvtext, a~vdatu
    FROM knvp AS vp
    INNER JOIN kna1 AS ka ON ka~kunnr = vp~kunnr
*    INNER JOIN kna1 AS k2 ON ka~kunnr = vp~kunn2
    INNER JOIN vbak AS a ON a~kunnr = ka~kunnr and a~vkorg = vp~vkorg
    INNER JOIN vbap AS b ON b~vbeln = a~vbeln
    INNER JOIN zssdt000_stat_p  AS c ON b~zstatp = c~zstatp AND c~app_360 = 'X'
    INTO TABLE @e_data
    WHERE vp~lifnr in @r_back_ofiice AND vp~parvw = 'A1'
    AND ka~regio IN @r_regio
    AND ka~pstlz IN @r_cp
    AND a~kunnr IN @r_kunnr
    AND b~matnr NE 'SINGLE'.

  IF sy-subrc <> 0.
    CLEAR ls_return.
    ls_return-id = 'ZSSD_UI5'.
    ls_return-type = 'E'.
    ls_return-number = '001'.
    ls_return-message_v1 = |{ i_backoffice ALPHA = OUT }|.
    APPEND ls_return TO e_return.
  ELSE.
    DATA: lv_hostname TYPE string,
          lv_port     TYPE string.
    CALL FUNCTION 'TH_GET_VIRT_HOST_DATA'
      EXPORTING
        protocol       = 0
        virt_idx       = 0
*       LOCAL          = 1
      IMPORTING
        hostname       = lv_hostname
        port           = lv_port
      EXCEPTIONS
        not_found      = 1
        internal_error = 2
        OTHERS         = 3.
    IF sy-subrc = 0.
      LOOP AT e_data ASSIGNING FIELD-SYMBOL(<ls_data>).
        <ls_data>-url_kunnr = 'http://'&& lv_hostname && ':' && lv_port && '/sap/bc/gui/sap/its/webgui?~transaction=*XD03%20RF02D-KUNNR=' && <ls_data>-kunnr .
        <ls_data>-url_vbeln = 'http://'&& lv_hostname && ':' && lv_port && '/sap/bc/gui/sap/its/webgui?~transaction=*VA23%20VBAK-VBELN=' && <ls_data>-vbeln .

      ENDLOOP.
    ENDIF.
  ENDIF.


ENDFUNCTION.
