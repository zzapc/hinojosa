FUNCTION zsdcom_grabar_facturas_prliq.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     VALUE(IS_PRLIQCAB) TYPE  ZTSDCOM_PRLIQCAB
*"     VALUE(IT_FACTS) TYPE  ZTTSDCOMLIQ002_GRID
*"     VALUE(IT_POS) TYPE  ZTTSDCOM_POS OPTIONAL
*"  EXPORTING
*"     VALUE(ET_RETURN) TYPE  BAPIRET2_T
*"----------------------------------------------------------------------

  DATA: ls_cab    TYPE ztsdcom_prliqcab,
        ls_posliq TYPE ztsdcom_prliqpos,
        lt_posliq TYPE TABLE OF ztsdcom_prliqpos,
        ls_fact   TYPE zssdcomliq002_grid,
        ls_pos    TYPE ztsdcom_pos.

  DATA: lv_stat_liqpos TYPE zstat_liq,
        lv_total       TYPE zsdcom_tot_com.
  CHECK it_facts[] IS NOT INITIAL.
* Grabamos la cabecera
  MOVE-CORRESPONDING is_prliqcab TO ls_cab.

  "EMP: 11.09.18 Sólo ponemos 01, si el status no es ni 02 ni 03.
  "ls_cab-zstat_liq = gc_liq_cr.
  IF ls_cab-zstat_liq <> gc_liq_cp AND ls_cab-zstat_liq <> gc_liq_cb.
    ls_cab-zstat_liq = gc_liq_cr.
  ENDIF.
  "FIN EMP: 11.09.18


  MODIFY ztsdcom_prliqcab FROM ls_cab.

* Recorremos todas las posiciones actualizando la tabla de posiciones de propuesta
  LOOP AT it_facts INTO ls_fact.
    CLEAR ls_posliq.
    MOVE-CORRESPONDING ls_fact     TO ls_posliq.
    MOVE-CORRESPONDING is_prliqcab TO ls_posliq.
*    MODIFY ztsdcom_prliqpos FROM ls_posliq.
    APPEND ls_posliq TO lt_posliq.
  ENDLOOP.
  MODIFY ztsdcom_prliqpos FROM TABLE lt_posliq.
  COMMIT WORK AND WAIT.

* Modificamos la tabla de posiciones de provisión para liberar
* la posición o ponerle el status de liquidación
  LOOP AT it_facts INTO ls_fact.
*   Si ls factura está marcada ---|> Marcamos el status de liquidación
    IF ls_fact-checkbox = 'X'.
      lv_total = ls_fact-ztot_com - ls_fact-zliq_par.
*     Si el importe a liquidar, es el total pendiente -> Status liquidación total
      IF ls_fact-zimp_liq = lv_total.
        lv_stat_liqpos = gc_pos_liq.
*     Sino -> Status liquidación parcial
      ELSE.
        lv_stat_liqpos = gc_pos_par.
      ENDIF.

*   Si la factura está desmarcada -> Liberamos la posición para que pueda volver a ser seleccionada
    ELSE.
*     Si tiene liquidaciones parciales -> Status liquidación parcial
      IF ls_fact-zliq_par <> 0.
        lv_stat_liqpos = gc_pos_par.
*     Sino -> Status pendiente liquidar
      ELSE.
        lv_stat_liqpos = gc_pos_pdt.
      ENDIF.
    ENDIF.

*   Modificamos la tabla
    UPDATE ztsdcom_pos SET zstat_liqpos = lv_stat_liqpos
*                           laufd        = is_prliqcab-laufd
*                           laufi        = is_prliqcab-laufi
        WHERE bukrs    = ls_fact-bukrs
        AND   belnr_vf = ls_fact-belnr
        AND   gjahr    = ls_fact-gjahr
        AND   vbeln    = ls_fact-vbeln
        AND   lifnr    = ls_fact-lifnr
        AND   kschl    = ls_fact-kschl
        AND   gsber    = ls_fact-gsber.
  ENDLOOP.

  LOOP AT it_pos INTO ls_pos WHERE laufi IS INITIAL.
    UPDATE ztsdcom_pos SET laufd = is_prliqcab-laufd laufi = is_prliqcab-laufi
    WHERE bukrs = ls_pos-bukrs
      AND belnr_vf = ls_pos-belnr_vf
      AND gjahr = ls_pos-gjahr
      AND vbeln = ls_pos-vbeln
      AND posnr = ls_pos-posnr
      AND lifnr = ls_pos-lifnr
      AND kschl = ls_pos-kschl
      AND gsber = ls_pos-gsber
      AND zcont_man = ls_pos-zcont_man.
  ENDLOOP.


* Hacemos el COMMIT
  COMMIT WORK AND WAIT.

ENDFUNCTION.
