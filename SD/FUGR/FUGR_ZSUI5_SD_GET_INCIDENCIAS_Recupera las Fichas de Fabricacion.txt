FUNCTION zsui5_sd_get_incidencias.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     VALUE(I_KUNNR) TYPE  KUNNR OPTIONAL
*"     VALUE(I_EJERCICIO) TYPE  MJAHR OPTIONAL
*"     VALUE(I_HISTORICO) TYPE  FLAG OPTIONAL
*"     VALUE(IR_VKORG) TYPE  RSELOPTION OPTIONAL
*"  EXPORTING
*"     VALUE(E_RETURN) TYPE  BAPIRET2_T
*"     VALUE(E_INCIDENCIAS) TYPE  ZSUI5TT_SD_INCIDENCIAS
*"----------------------------------------------------------------------
  DATA: ls_return     TYPE bapiret2,
        lv_fecha_ini  TYPE sy-datum,
        lv_fecha_fin  TYPE sy-datum,
        ls_incidencia TYPE zsui5s_sd_incidencias,
        it_lines      TYPE TABLE OF tline,
        lv_name       TYPE thead-tdname.

  IF i_ejercicio IS NOT INITIAL.
    lv_fecha_ini = i_ejercicio && '01' && '01'.
    lv_fecha_fin = i_ejercicio && '12' && '31'.
  ELSE.
    lv_fecha_ini = sy-datum(4) && '01' && '01'.
    lv_fecha_fin = sy-datum(4) && '12' && '31'.
  ENDIF.
  SELECT a~kunnr, a~vbeln, b~posnr, b~zznumeroincidencia, b~kdmat, d~bstkd, d~posnr AS posnr_vbkd
    FROM vbak AS a
    INNER JOIN vbap AS b ON b~vbeln = a~vbeln
*    LEFT JOIN knmt AS c ON c~vkorg = a~vkorg AND c~vtweg = a~vtweg AND c~kunnr = a~kunnr AND c~matnr = b~matnr
    LEFT JOIN vbkd AS d ON b~vbeln = d~vbeln AND ( d~posnr = b~posnr OR d~posnr = @space )
    INTO TABLE @DATA(lt_data)
    WHERE a~kunnr = @i_kunnr
    AND a~audat >= @lv_fecha_ini
    AND a~audat <= @lv_fecha_fin
    AND b~zznumeroincidencia NE @space.
  IF sy-subrc NE 0.
    CLEAR ls_return.
    ls_return-id = 'ZSSD_UI5'.
    ls_return-type = 'E'.
    ls_return-number = '001'.
    ls_return-message_v1 = |{ i_kunnr ALPHA = OUT }|.
    APPEND ls_return TO e_return.
  ELSE.
    "Obtenemos la ruta para el WebGUI de la Tx XD02
    DATA: lv_hostname TYPE string,
          lv_port     TYPE string.
    CALL FUNCTION 'TH_GET_VIRT_HOST_DATA'
      EXPORTING
        protocol       = 0
        virt_idx       = 0
*       LOCAL          = 1
      IMPORTING
        hostname       = lv_hostname
        port           = lv_port
      EXCEPTIONS
        not_found      = 1
        internal_error = 2
        OTHERS         = 3.
    IF sy-subrc = 0.
      "Añadimos las posiciones con pedido cliente en posición
      LOOP AT lt_data ASSIGNING FIELD-SYMBOL(<ls_data>) WHERE posnr_vbkd NE space.
        CLEAR ls_incidencia.
        MOVE-CORRESPONDING <ls_data> TO ls_incidencia.

        ls_incidencia-url_vbeln = 'http://'&& lv_hostname && ':' && lv_port && '/sap/bc/gui/sap/its/webgui?~transaction=*VA03%20VBAK-VBELN=' && ls_incidencia-vbeln .
        ls_incidencia-ejercicio = i_ejercicio.

        APPEND ls_incidencia TO e_incidencias.
      ENDLOOP.
       "Añadimos las posiciones con pedido cliente en cabecea y que no estén en posición
      LOOP AT lt_data ASSIGNING <ls_data> WHERE posnr_vbkd = space.
        READ TABLE e_incidencias TRANSPORTING NO FIELDS WITH KEY kunnr = <ls_data>-kunnr vbeln = <ls_data>-vbeln posnr = <ls_data>-posnr.
        IF sy-subrc NE 0.
          CLEAR ls_incidencia.
          MOVE-CORRESPONDING <ls_data> TO ls_incidencia.

          ls_incidencia-url_vbeln = 'http://'&& lv_hostname && ':' && lv_port && '/sap/bc/gui/sap/its/webgui?~transaction=*VA03%20VBAK-VBELN=' && ls_incidencia-vbeln .
          ls_incidencia-ejercicio = i_ejercicio.

          APPEND ls_incidencia TO e_incidencias.
        ENDIF.
      ENDLOOP.


    ENDIF.
  ENDIF.


ENDFUNCTION.
