FUNCTION z_ssd_cliente_exportacion.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     REFERENCE(I_KUNNR) TYPE  KUNNR
*"     REFERENCE(I_VKORG) TYPE  VKORG
*"     REFERENCE(I_VTWEG) TYPE  VTWEG OPTIONAL
*"     REFERENCE(I_SPART) TYPE  SPART OPTIONAL
*"     REFERENCE(I_FKART) TYPE  FKART OPTIONAL
*"  EXPORTING
*"     REFERENCE(E_KVGR4) TYPE  KVGR4
*"     REFERENCE(E_FAKSK) TYPE  FAKSK
*"     REFERENCE(E_FKART) TYPE  FKART
*"----------------------------------------------------------------------
* JGM 25.01.2024 GAPSD044 Facturas de exportación:
* Chequea si el Cliente es "Facturas Exportación" (KNVV-KVGR4 = 'C05'),
* devolviendo los valores necesarios para:
*
* 1-Crear la entrega con tipo de bloqueo de factura = Z3 'Factura Exportación'.
* En:
* Ampliación (nueva): ZSSD044_ENH_DELIVERY_PROC
* BAdI: ZSSD044_BADI_DELIVERY_PROC
* Clase: ZSSD044_CL_DELIVERY_PROC
* Método: CHANGE_DELIVERY_HEADER
*
* 2-La clase de la factura a crear se debe cambiar a: ZF3 "Factura inmediata".
* En:
* Ampliación (existente): ZSSD_ENH_FILL_VBRK
* Include: RV60AFZC
*
* 3-Al crear la factura, el texto de cabecera ZFC3 “Texto exportaciones”
* deberá ser obligatorio.
* En:
* Ampliación (nueva): ZSSD044_ENH_NUMBER_RANGE
* Include: RV60AFZZ
*--------------------------------------------------------------------*
  DATA: lr_vtweg TYPE RANGE OF vtweg,
        lr_spart TYPE RANGE OF spart.

  IF i_kunnr IS NOT INITIAL AND i_vkorg IS NOT INITIAL.

    IF i_vtweg IS NOT INITIAL.
      lr_vtweg = VALUE #( ( sign = 'I' option = 'EQ' low = i_vtweg ) ). "Opcional.
    ENDIF.

    IF i_spart IS NOT INITIAL.
      lr_spart = VALUE #( ( sign = 'I' option = 'EQ' low = i_spart ) ). "Opcional.
    ENDIF.

    SELECT SINGLE kvgr4 INTO @DATA(lv_kvgr4)
    FROM knvv
    WHERE kunnr EQ @i_kunnr
      AND vkorg EQ @i_vkorg
      AND vtweg IN @lr_vtweg
      AND spart IN @lr_spart.

    IF sy-subrc EQ 0 AND lv_kvgr4 EQ gc_kvgr4_c05.
      e_kvgr4 = gc_kvgr4_c05.
      e_faksk = gc_faksk_z3.
      CASE i_fkart.
        WHEN 'ZF2'.
          e_fkart = gc_fkart_zf3.
        WHEN OTHERS.
          e_fkart = i_fkart.
      ENDCASE.
    ENDIF.
  ENDIF.

ENDFUNCTION.
