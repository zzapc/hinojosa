FUNCTION zsdcom_impr_propliq_importes.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     VALUE(IS_CABLIQ) TYPE  ZSSDCOM_PRLIQCAB_KEY
*"     VALUE(I_LIFNR) TYPE  LIFNR
*"     VALUE(IT_POS) TYPE  ZTTSDCOM_PRLIQPOS_PRINT
*"  EXPORTING
*"     VALUE(ES_IMPORTES) TYPE  ZSSDCOM_PRLIQIMP_PRINT
*"----------------------------------------------------------------------

  DATA: lti_ftaxp     TYPE TABLE OF ftaxp.

  DATA: ls_pos       TYPE zssdcom_prliqpos_print,
        ls_exch_rate TYPE bapi1093_0,
        ls_ftaxp     TYPE ftaxp.

  DATA: lv_waers TYPE waers,
        lv_land1 TYPE land1.

* Inicializamos
  CLEAR es_importes.

* País del comisionista
  SELECT SINGLE land1 INTO lv_land1
      FROM  t001
      WHERE bukrs = is_cabliq-bukrs.

  READ TABLE it_pos INTO DATA(ls_pos2) INDEX 1.
* Indicador de impuestos
  SELECT SINGLE mwskz INTO es_importes-mwskz
      FROM  ztsdcom002
      WHERE bukrs = is_cabliq-bukrs
      AND   lifnr = i_lifnr
      AND   gsber = ls_pos2-gsber.

* Total de comisiones
  LOOP AT it_pos INTO ls_pos.
*    es_importes-ztot_com = es_importes-ztot_com + ls_pos-dmbtr.
    ADD ls_pos-zimp_liq TO es_importes-ztot_com."CVP 16.11.2020
    es_importes-waers    = ls_pos-waerk. "waers. "CVP 16.11.2020
  ENDLOOP.

* Importe de impuestos
  IF es_importes-mwskz IS NOT INITIAL.
*   Sacamos el porcentaje del indicador de impuestos
    CALL FUNCTION 'GET_TAX_PERCENTAGE'
      EXPORTING
        aland   = lv_land1
        datab   = is_cabliq-laufd
        mwskz   = es_importes-mwskz
        txjcd   = ''
      TABLES
        t_ftaxp = lti_ftaxp.
*   Cargamos el porcentaje y calculamos el impuesto
    READ TABLE lti_ftaxp INTO ls_ftaxp INDEX 1.
    IF sy-subrc = 0.
      DATA l_t007a TYPE t007a.
      DATA l_t005 TYPE t005.
      SELECT SINGLE * INTO l_t005 FROM t005 WHERE land1 = lv_land1.
      SELECT SINGLE * INTO l_t007a FROM t007a
        WHERE mwskz = es_importes-mwskz
          AND kalsm = l_t005-kalsm.
      IF l_t007a-egrkz <> space.
        ls_ftaxp-kbetr = 0.
      ENDIF.
      es_importes-kbetr = ls_ftaxp-kbetr / 10.
      es_importes-mwsbk = ( es_importes-ztot_com * es_importes-kbetr ) / 100.
    ENDIF.
  ENDIF.

* Sacamos el indicador de retención del comisionista
  SELECT SINGLE witht wt_withcd
      INTO (es_importes-witht, es_importes-wt_withcd)
      FROM  lfbw
      WHERE lifnr     = i_lifnr
      AND   bukrs     = is_cabliq-bukrs
      AND   wt_subjct = 'X'.

* Sacamos los datos de la retención
  IF es_importes-wt_withcd IS NOT INITIAL.
*   Sacamos el porcentaje de retención
    SELECT SINGLE qsatz INTO es_importes-qsatz
        FROM  t059z
        WHERE land1     = lv_land1
        AND   witht     = es_importes-witht
        AND   wt_withcd = es_importes-wt_withcd.
*   Calculamos el importe de retención
    es_importes-qbuihb = -1 * ( es_importes-ztot_com * es_importes-qsatz ) / 100.
  ENDIF.

ENDFUNCTION.
