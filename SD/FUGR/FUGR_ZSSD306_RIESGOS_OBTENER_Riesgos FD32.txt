FUNCTION zssd306_riesgos_obtener.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     REFERENCE(IKUNNR) TYPE  KUNNR
*"     REFERENCE(C_KKBER) TYPE  KKBER
*"  EXPORTING
*"     REFERENCE(OSKFOR) TYPE  SKFOR
*"     REFERENCE(OSSOBL) TYPE  SSOBL
*"     REFERENCE(OOBLIG) TYPE  OBLIG_F02L
*"     REFERENCE(OWAERS) TYPE  WAERS
*"     REFERENCE(OLIMIT) TYPE  KLIMK
*"     REFERENCE(ODELTA) TYPE  KLIMK
*"     REFERENCE(OERROR) TYPE  STRING
*"----------------------------------------------------------------------

  DATA v_kunnr TYPE kunnr.
  DATA l_kna1  TYPE kna1.
  DATA l_knkk  TYPE knkk.
  DATA e_kurgv TYPE kurgv.

  CLEAR: oskfor, ossobl, ooblig, owaers, odelta, olimit, oerror.

* Convertimos a formato interno
  CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
    EXPORTING
      input  = ikunnr
    IMPORTING
      output = v_kunnr.

* Chequeamos que exista el cliente
  SELECT SINGLE * INTO l_kna1 FROM kna1 WHERE kunnr = v_kunnr.
  IF sy-subrc <> 0.
    oerror = text-001.
    RETURN.
  ENDIF.

* Chequeamos que el cliente tenga credito creado
  SELECT SINGLE * INTO l_knkk FROM knkk WHERE kunnr = v_kunnr AND kkber = c_kkber.
  IF sy-subrc <> 0.
    oerror = text-002.
    RETURN.
  ENDIF.

  CALL FUNCTION 'SD_CREDIT_MASTER_DATA'
    EXPORTING
      i_kkber = c_kkber
      i_kunrg = v_kunnr
    CHANGING
      e_kurgv = e_kurgv.

  CALL FUNCTION 'CREDIT_EXPOSURE'
    EXPORTING
      kkber          = c_kkber
      kunnr          = v_kunnr
    IMPORTING
      creditlimit    = olimit
      delta_to_limit = odelta.

  oskfor = e_kurgv-skfor.
  ossobl = e_kurgv-ssobl.
  ooblig = e_kurgv-skfor + e_kurgv-ssobl + e_kurgv-sauft.
  owaers = e_kurgv-waers.

ENDFUNCTION.
