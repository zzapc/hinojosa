FUNCTION zsui5_sd_get_cliente.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     VALUE(I_KUNNR) TYPE  KUNNR OPTIONAL
*"  EXPORTING
*"     VALUE(E_RETURN) TYPE  BAPIRET2_T
*"     VALUE(E_DATA) TYPE  ZSUI5_SD_INFO_CLIENTE
*"----------------------------------------------------------------------
  DATA: ls_return TYPE bapiret2.

  SELECT SINGLE a~kunnr, a~name1, a~stcd1, a~stceg, a~telf1 AS telefono, b~street AS direccion,  m~smtp_addr AS mail, vv~klabc AS cliente_abc,
                ka~klimg AS limite, ka~waers, vp~lifnr AS comercial, lf~name1 AS nom_comercial
    FROM kna1 AS a
    INNER JOIN knvp AS c ON c~kunnr = a~kunnr AND c~parvw = 'A1'
    INNER JOIN adrc AS b ON b~addrnumber = a~adrnr
    INNER JOIN knvv AS vv ON  vv~kunnr = a~kunnr AND vv~vkorg = c~vkorg AND vv~vtweg = c~vtweg AND vv~spart = c~spart
    LEFT JOIN adr6 AS m ON m~addrnumber = a~adrnr
    LEFT JOIN knvp AS vp ON vp~kunnr = a~kunnr AND vp~parvw = 'Y0'
    LEFT JOIN lfa1 AS lf ON vp~lifnr = lf~lifnr
    LEFT JOIN knka AS ka ON ka~kunnr = a~kunnr
    INTO @DATA(ls_cliente)
*    INTO CORRESPONDING FIELDS OF @e_data
    WHERE a~kunnr = @i_kunnr.
  IF sy-subrc = 0.
    MOVE-CORRESPONDING ls_cliente TO e_data.
    IF ls_cliente-stceg IS NOT INITIAL AND e_data-stcd1 IS INITIAL.
      e_data-stcd1 = ls_cliente-stceg.
    ENDIF.

    "Obtenemos el % del Agotamiento
    DATA: lv_comprometido TYPE s066-oeikw.
    SELECT oeikw, aoeiw
      FROM s066
      INTO TABLE @DATA(lt_comprometido1)
      WHERE knkli = @i_kunnr
      AND kkber = '2001'.

    SELECT olikw, ofakw, aoliw, aofaw
    FROM  s067
    INTO TABLE @DATA(lt_comprometido2)
    WHERE knkli = @i_kunnr
    AND kkber = '2001'.

    CLEAR lv_comprometido.
    LOOP AT lt_comprometido1 INTO DATA(ls_comprometido1).
      lv_comprometido = lv_comprometido + ls_comprometido1-oeikw + ls_comprometido1-aoeiw.
    ENDLOOP.
    LOOP AT lt_comprometido2 INTO DATA(ls_comprometido2).
      lv_comprometido = lv_comprometido + ls_comprometido2-aofaw + ls_comprometido2-aoliw + ls_comprometido2-ofakw + ls_comprometido2-olikw.
    ENDLOOP.
    IF e_data-limite NE 0.
      e_data-agotamiento = lv_comprometido / ( e_data-limite / 100 ).
    ENDIF.

    "Obtenemos la ruta para el WebGUI de la Tx XD02
    DATA: lv_hostname TYPE string,
          lv_port     TYPE string.
    CALL FUNCTION 'TH_GET_VIRT_HOST_DATA'
      EXPORTING
        protocol       = 0
        virt_idx       = 0
*       LOCAL          = 1
      IMPORTING
        hostname       = lv_hostname
        port           = lv_port
      EXCEPTIONS
        not_found      = 1
        internal_error = 2
        OTHERS         = 3.
    IF sy-subrc = 0.
      e_data-masdatos = 'http://'&& lv_hostname && ':' && lv_port && '/sap/bc/gui/sap/its/webgui?~transaction=*XD03%20RF02D-KUNNR=' && e_data-kunnr .
    ENDIF.

  ELSE.
    CLEAR ls_return.
    ls_return-id = 'ZSSD_UI5'.
    ls_return-type = 'E'.
    ls_return-number = '002'.
    ls_return-message_v1 = |{ i_kunnr ALPHA = OUT }|.
    APPEND ls_return TO e_return.
  ENDIF.


ENDFUNCTION.
