FUNCTION zfmsd0006_001.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     REFERENCE(MODO_CT) TYPE  BDCMODE DEFAULT 'E'
*"  EXPORTING
*"     REFERENCE(AUFNR) TYPE  AUFNR
*"     REFERENCE(MESSAGE) TYPE  BAPI_MSG
*"  CHANGING
*"     REFERENCE(DATOS) TYPE  ZST_SD_ORDEN
*"----------------------------------------------------------------------

  CLEAR: aufnr, message.
  zst_sd_orden = datos.

  IF zst_sd_orden-werks IS INITIAL.
    GET PARAMETER ID 'WRK' FIELD zst_sd_orden-werks.
  ENDIF.

  DATA(o_par) = NEW zcl_ap_parametros( 'ZRSD0041' ).
  zst_sd_orden-auart = o_par->get_atr1( campo = 'OF_CLASE_2001' ).
  IF zst_sd_orden-auart IS INITIAL.
    message = 'Informe clase de orden en parámetro OF_CLASE_2001'.
    RETURN.
  ENDIF.
  DATA(l_dias) = o_par->get_atr1( campo = 'OF_DIAS' ctd = 'X' ).
  zst_sd_orden-gltrp = sy-datum + l_dias.
  v_modo_ct = modo_ct.

  DATA(r_vkorg_usuario) = NEW zhcl_sd_core( )->get_vkorg_aut_usuario(  ).
  IF lines( r_vkorg_usuario ) = 1.
    zst_sd_orden-vkorg = r_vkorg_usuario[ 1 ]-low.
  ENDIF.

  IF zst_sd_orden-backoffice IS INITIAL.
    SELECT valor AS vkorg, atributo1 AS backoffice FROM zparametros
      INTO TABLE @DATA(i_org)
     WHERE clave = 'CORE_BODEF'
       AND campo = 'A1'
       AND valor = @sy-uname
       AND valor2 IN @r_vkorg_usuario.
    IF lines( i_org ) = 1.
      zst_sd_orden-backoffice = i_org[ 1 ]-backoffice.
    ENDIF.
  ENDIF.

  CALL SCREEN 0100 STARTING AT 3 3 ENDING AT 80 10.

  IF v_ok = 'X'.
    datos = zst_sd_orden.
    aufnr = zst_sd_orden-aufnr.
  ELSE.
    message = zst_sd_orden-message.
  ENDIF.

ENDFUNCTION.
