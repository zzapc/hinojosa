FUNCTION zssd061_chequeo_zr01.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     REFERENCE(I_VBAK) TYPE  VBAK
*"  EXPORTING
*"     VALUE(E_ERROR) TYPE  FLAG
*"     VALUE(E_POSNR) TYPE  POSNR
*"     VALUE(E_LGORT) TYPE  LGORT_D
*"     VALUE(E_MSG) TYPE  BAPI_MSG
*"  TABLES
*"      T_VBAP STRUCTURE  VBAPVB
*"      T_KOMV STRUCTURE  KOMV
*"----------------------------------------------------------------------

  " Comprobación que aplica el desarrollo por Clase documento, tipo de posición y Área de ventas
  SELECT * INTO TABLE @DATA(lt_zssdt061)
    FROM zssdt061
    WHERE vkorg = @i_vbak-vkorg
      AND vtweg = @i_vbak-vtweg
      AND spart = @i_vbak-spart
      AND auart = @i_vbak-auart.

  " Si se dispone de una posición asociada a un Centro de montaje con Almacén relevante
  SELECT * INTO TABLE @DATA(lt_mdlg)
    FROM mdlg FOR ALL ENTRIES IN @t_vbap
    WHERE werks = @t_vbap-werks
      AND lgort = @t_vbap-lgort.

  LOOP AT t_vbap INTO DATA(s_vbap).
    READ TABLE lt_zssdt061 INTO DATA(s_zssdt061) WITH KEY pstyv = s_vbap-pstyv
                                                          lgort = s_vbap-lgort.
    IF sy-subrc EQ 0.
      READ TABLE lt_mdlg INTO DATA(s_mdlg) WITH KEY werks = s_vbap-werks
                                                    lgort = s_vbap-lgort.
      IF sy-subrc = 0.
        " Valida si tiene informada la condición ZR01
        READ TABLE t_komv INTO DATA(s_komv) WITH KEY kposn = s_vbap-posnr
                                                     kschl = 'ZR01'.
        IF sy-subrc NE 0.
          e_error = abap_true.
          e_posnr = s_vbap-posnr.
          e_lgort = s_vbap-lgort.
          CONCATENATE 'Para la posición' e_posnr
                      'para la que se ha informado del almacén' e_lgort
                      'NO se ha informado la condición ZR01'
                      INTO e_msg SEPARATED BY space.

          EXIT.
        ENDIF.
      ENDIF.
    ENDIF.
    CLEAR: s_zssdt061, s_vbap, s_mdlg, s_komv.
  ENDLOOP.

ENDFUNCTION.
