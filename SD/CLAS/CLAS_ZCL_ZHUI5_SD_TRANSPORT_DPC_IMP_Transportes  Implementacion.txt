
class ZCL_ZHUI5_SD_TRANSPORT_DPC_IMP definition
  public
  inheriting from ZCL_ZHUI5_SD_TRANSPORT_DPC
  final
  create public .

public section.

  constants C_STTRG_DESPACHO_EXPED type VTTK-STTRG value '5' ##NO_TEXT.
  constants C_STTRG_INICIO_TRANSPORTE type VTTK-STTRG value '6' ##NO_TEXT.
  constants C_STTRG_FIN_TRANSPORTE type VTTK-STTRG value '7' ##NO_TEXT.
  constants C_STATUS_TRANSPORTISTA_LIBRE type ZSSD_STATUS_TRANSPORTISTA value '' ##NO_TEXT.
  constants C_STATUS_TRANSPORTISTA_ASIG type ZSSD_STATUS_TRANSPORTISTA value '01' ##NO_TEXT.
  constants C_STATUS_TRANSPORTISTA_EN_RUTA type ZSSD_STATUS_TRANSPORTISTA value '02' ##NO_TEXT.

  methods ORDENES_PROD_GETLIST
    importing
      !TP_VBELN_RG type ZRANGE_T_VBELN
    returning
      value(TP_ORDENES_PROD) type ZSD_TRANS_T_ORDENES_PROD
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods ALBARANES_GET_PDF
    importing
      value(VP_VBELN) type ZSD_TRANS_S_ENTREGAS-VBELN
    returning
      value(VP_PDF) type XSTRING
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods ALBARANES_IMPRIMIR
    importing
      value(VP_VBELN) type ZSD_TRANS_S_ENTREGAS-VBELN
      value(VP_PREVISUALIZAR) type XFELD
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods CONSTRUCTOR
    importing
      !RP_DPC_BASE type ref to ZCL_ZHUI5_SD_TRANSPORT_DPC optional .
  class-methods CONVERTIR_TABLA_TEXTO_A_STRING
    importing
      !TP_TEXTO type TLINE_TAB
    returning
      value(VP_TEXTO) type STRING .
  methods ALBARANES_GETDETAIL
    importing
      value(VP_TKNUM) type ZSD_TRANS_S_ALBARANES-TKNUM
      value(VP_VBELN) type ZSD_TRANS_S_ALBARANES-VBELN
      value(VP_GET_DATA) type XFELD default SPACE
    returning
      value(WP_ALBARAN) type ZSD_TRANS_S_ALBARANES
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods ENTREGAS_GETDETAIL
    importing
      value(VP_TKNUM) type ZSD_TRANS_S_ENTREGAS-TKNUM
      value(VP_VBELN) type ZSD_TRANS_S_ENTREGAS-VBELN
    returning
      value(WP_ENTREGA) type ZSD_TRANS_S_ENTREGAS
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods ENTREGAS_GETLIST
    importing
      !TP_TKNUM_RG type ZSD_T_TKNUM_RG optional
    returning
      value(TP_ENTREGAS) type ZSD_TRANS_T_ENTREGAS
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods ENTREGAS_POS_GETDETAIL
    importing
      value(VP_POSNR) type ZSD_TRANS_S_ENTREGAS_POSICION-POSNR
      value(VP_TKNUM) type ZSD_TRANS_S_ENTREGAS_POSICION-TKNUM
      value(VP_VBELN) type ZSD_TRANS_S_ENTREGAS_POSICION-VBELN
    returning
      value(WP_ENTREGA_POS) type ZSD_TRANS_S_ENTREGAS_POSICION
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods ENTREGAS_POS_GETLIST
    importing
      !TP_TKNUM_RG type ZSD_T_TKNUM_RG optional
      !TP_VBELN_RG type ZSD_T_VBELN_RG optional
    returning
      value(TP_ENTREGAS_POS) type ZSD_TRANS_T_ENTREGAS_POSICION
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods FINALIZAR_TRANSPORTE
    importing
      value(VP_TKNUM) type ZSD_TRANS_S_TRANSPORTES-TKNUM
    returning
      value(WP_RETURN) type BAPIRET2
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods INICIAR_TRANSPORTE
    importing
      value(VP_TKNUM) type ZSD_TRANS_S_TRANSPORTES-TKNUM
    returning
      value(WP_RETURN) type BAPIRET2
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods REGISTRAR_DISPON_TRANSPORTISTA
    importing
      value(VP_FECHA_DISP) type ZSD_TRANS_S_TRANSPORTISTAS-FECHA_DISP
      value(VP_HORA_DISP) type ZSD_TRANS_S_TRANSPORTISTAS-HORA_DISP
      value(VP_LIFNR) type ZSD_TRANS_S_TRANSPORTISTAS-LIFNR
    returning
      value(WP_RETURN) type BAPIRET2
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods CARTAS_PORTE_IMPRIMIR
    importing
      value(VP_TKNUM) type ZSD_TRANS_S_TRANSPORTES-TKNUM
      value(VP_PREVISUALIZAR) type XFELD
    returning
      value(VP_CARTA_PORTE) type XSTRING
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods CARTAS_PORTE_GET_PDF
    importing
      value(VP_TKNUM) type ZSD_TRANS_S_TRANSPORTES-TKNUM
    returning
      value(VP_CARTA_PORTE) type XSTRING
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods HOJA_PICKING_IMPRIMIR
    importing
      value(VP_TKNUM) type ZSD_TRANS_S_TRANSPORTES-TKNUM
      value(VP_PREVISUALIZAR) type XFELD
    returning
      value(VP_CARTA_PORTE) type XSTRING
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods CARTAS_PORTE_GETDETAIL
    importing
      value(VP_TKNUM) type ZSD_TRANS_S_CARTA_PORTE-TKNUM
      value(VP_GET_DATA) type XFELD default SPACE
    returning
      value(WP_CARTA_PORTE) type ZSD_TRANS_S_CARTA_PORTE
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods TRANSPORTES_GETDETAIL
    importing
      value(VP_TKNUM) type ZSD_TRANS_S_TRANSPORTES-TKNUM
    returning
      value(WP_TRANSPORTE) type ZSD_TRANS_S_TRANSPORTES
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods TRANSPORTES_GETLIST
    importing
      !TP_TDLNR_RG type ZSD_T_TDLNR_RG optional
      value(VP_CLA_DESPACHADO) type XFELD optional
      !TP_TPLST_RG type ZSD_T_TPLST_RG optional
      !TP_ERDAT_RG type RANGE_T_DATS optional
      !TP_EXTI2_RG type ZSD_T_EXTI2_RG optional
      !TP_STTRG_RG type ZSD_T_STTRG_RG optional
      !TP_TKNUM_RG type ZSD_T_TKNUM_RG optional
      !TP_VBELN_RG type ZRANGE_T_VBELN optional
      !TP_SHTYP_RG type ZRANGE_T_SHTYP optional
      !TP_DPREG_RG type ZRANGE_T_DPREG optional
      value(VP_FEC_INI_DESPACHADO) type DATS default '00010101'
      !TP_DTABF_RG type ZRANGE_T_DTABF optional
    returning
      value(TP_TRANSPORTES) type ZSD_TRANS_T_TRANSPORTES
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods TRANSPORTISTAS_GETDETAIL
    importing
      value(VP_LIFNR) type ZSD_TRANS_S_TRANSPORTISTAS-LIFNR
    returning
      value(WP_TRANSPORTISTA) type ZSD_TRANS_S_TRANSPORTISTAS
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods TRANSPORTISTAS_GETLIST
    importing
      value(TP_LIFNR_RG) type ZRANGE_T_LIFNR optional
      value(TP_BUKRS_RG) type ZRANGE_T_BUKRS optional
    returning
      value(TP_TRANSPORTISTAS) type ZSD_TRANS_T_TRANSPORTISTAS
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .

  methods /IWBEP/IF_MGW_APPL_SRV_RUNTIME~EXECUTE_ACTION
    redefinition .
  methods /IWBEP/IF_MGW_APPL_SRV_RUNTIME~GET_STREAM
    redefinition .
protected section.

  data RG_DPC_BASE type ref to ZCL_ZHUI5_SD_TRANSPORT_DPC .

  methods ORDENES_PROD_GETLIST_EXT
    changing
      value(TP_ORDENES_PROD) type ZSD_TRANS_T_ORDENES_PROD
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods ENTREGAS_GETLIST_EXT
    changing
      value(TP_ENTREGAS) type ZSD_TRANS_T_ENTREGAS
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods ENTREGAS_POS_GETLIST_EXT
    changing
      value(TP_ENTREGAS_POS) type ZSD_TRANS_T_ENTREGAS_POSICION
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods TRANSPORTES_GETLIST_EXT
    changing
      value(TP_TRANSPORTES) type ZSD_TRANS_T_TRANSPORTES
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods TRANSPORTISTAS_GETLIST_EXT
    changing
      value(TP_TRANSPORTISTAS) type ZSD_TRANS_T_TRANSPORTISTAS
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .

  methods ALBARANES_GET_ENTITY
    redefinition .
  methods CARTASPORTE_GET_ENTITY
    redefinition .
  methods ENTREGAS_GET_ENTITY
    redefinition .
  methods ENTREGAS_GET_ENTITYSET
    redefinition .
  methods ENTREGAS_POSICIO_GET_ENTITY
    redefinition .
  methods ENTREGAS_POSICIO_GET_ENTITYSET
    redefinition .
  methods TRANSPORTES_GET_ENTITY
    redefinition .
  methods TRANSPORTES_GET_ENTITYSET
    redefinition .
  methods TRANSPORTISTAS_GET_ENTITY
    redefinition .
  methods TRANSPORTISTAS_GET_ENTITYSET
    redefinition .
private section.

  methods TRATAR_KEY_ENTITY
    importing
      !IO_TECH_REQUEST_CONTEXT type ref to /IWBEP/IF_MGW_REQ_ENTITY
    exporting
      !WP_ENTITY type DATA
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods TRATAR_KEY_ENTITYSET
    importing
      !IO_TECH_REQUEST_CONTEXT type ref to /IWBEP/IF_MGW_REQ_ENTITYSET
    exporting
      !WP_ENTITY type DATA
    raising
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
endclass. "ZCL_ZHUI5_SD_TRANSPORT_DPC_IMP definition
class ZCL_ZHUI5_SD_TRANSPORT_DPC_IMP implementation.
  METHOD /iwbep/if_mgw_appl_srv_runtime~execute_action.

    CASE io_tech_request_context->get_function_import_name( ).
      WHEN 'RegistrarDisponibilidadTransportista'.
        DATA: BEGIN OF  wl_reg_disp_transportista,
                lifnr      TYPE zsd_trans_s_transportistas-lifnr,
                fecha_disp TYPE zsd_trans_s_transportistas-fecha_disp,
                hora_disp  TYPE zsd_trans_s_transportistas-hora_disp,
              END OF wl_reg_disp_transportista.
        io_tech_request_context->get_converted_parameters( IMPORTING es_parameter_values = wl_reg_disp_transportista ).
        wl_reg_disp_transportista-lifnr = |{ wl_reg_disp_transportista-lifnr ALPHA = IN }|.

        copy_data_to_ref( EXPORTING is_data = registrar_dispon_transportista( vp_lifnr      = wl_reg_disp_transportista-lifnr
                                                                              vp_fecha_disp = wl_reg_disp_transportista-fecha_disp
                                                                              vp_hora_disp  = wl_reg_disp_transportista-hora_disp )
                          CHANGING  cr_data = er_data ).

      WHEN 'IniciarTransporte'.
        DATA: BEGIN OF  wl_iniciar_transporte,
                tknum TYPE zsd_trans_s_transportes-tknum,
              END OF wl_iniciar_transporte.
        io_tech_request_context->get_converted_parameters( IMPORTING es_parameter_values = wl_iniciar_transporte ).
        wl_iniciar_transporte-tknum = |{ wl_iniciar_transporte-tknum ALPHA = IN }|.

        copy_data_to_ref( EXPORTING is_data = iniciar_transporte( wl_iniciar_transporte-tknum )
                          CHANGING  cr_data = er_data ).

      WHEN 'FinalizarTransporte'.
        DATA: BEGIN OF  wl_finalizar_transporte,
                tknum TYPE zsd_trans_s_transportes-tknum,
              END OF wl_finalizar_transporte.
        io_tech_request_context->get_converted_parameters( IMPORTING es_parameter_values = wl_finalizar_transporte ).
        wl_finalizar_transporte-tknum = |{ wl_finalizar_transporte-tknum ALPHA = IN }|.

        copy_data_to_ref( EXPORTING is_data = finalizar_transporte( wl_finalizar_transporte-tknum )
                          CHANGING  cr_data = er_data ).

    ENDCASE.
  ENDMETHOD.
  METHOD /iwbep/if_mgw_appl_srv_runtime~get_stream.

    DATA: wl_stream TYPE ty_s_media_resource,
          wl_header TYPE ihttpnvp.
    DATA: wl_albaran     TYPE zsd_trans_s_albaranes,
          wl_carta_porte TYPE zsd_trans_s_carta_porte.


    CASE iv_entity_name.
      WHEN 'Albaranes'.
        io_tech_request_context->get_converted_keys( IMPORTING es_key_values = wl_albaran ).
        wl_albaran-vbeln = |{ wl_albaran-vbeln ALPHA = IN }|.
        wl_albaran-tknum = |{ wl_albaran-tknum ALPHA = IN }|.
        wl_albaran = albaranes_getdetail( vp_tknum       = wl_albaran-tknum
                                          vp_vbeln       = wl_albaran-vbeln
                                          vp_get_data    = 'X' ).

        wl_stream-mime_type = wl_albaran-mimetype.
        wl_stream-value     = wl_albaran-data.
        wl_header-value     = |outline; filename="{ wl_albaran-fichero }"|.

      WHEN 'CartasPorte'.
        io_tech_request_context->get_converted_keys( IMPORTING es_key_values = wl_carta_porte ).
        wl_carta_porte-tknum = |{ wl_carta_porte-tknum ALPHA = IN }|.
        wl_carta_porte = cartas_porte_getdetail( vp_tknum       = wl_carta_porte-tknum
                                                 vp_get_data    = 'X' ).

        wl_stream-mime_type = wl_carta_porte-mimetype.
        wl_stream-value     = wl_carta_porte-data.
        wl_header-value     = |outline; filename="{ wl_carta_porte-fichero }"|.
    ENDCASE.




    IF rg_dpc_base IS BOUND.
      wl_header-name = 'Content-Disposition'.
      rg_dpc_base->set_header( is_header = wl_header ).

      wl_header-name = 'Content-Type'.
      wl_header-value = wl_stream-mime_type.
      rg_dpc_base->set_header( is_header = wl_header ).

    ENDIF.
    copy_data_to_ref( EXPORTING is_data = wl_stream
                      CHANGING  cr_data = er_stream ).


  ENDMETHOD.
  method ALBARANES_GET_ENTITY.
    tratar_key_entity( EXPORTING io_tech_request_context = io_tech_request_context
                       IMPORTING wp_entity               = er_entity ).
    er_entity = albaranes_getdetail( vp_tknum     = er_entity-tknum
                                     vp_vbeln     = er_entity-vbeln
                                     vp_get_data  = 'X' ).

  endmethod.
  METHOD albaranes_get_pdf.

    SELECT DISTINCT kschl, sform, pgnam
      INTO TABLE @DATA(tl_tnapr)
      FROM tnapr
      WHERE sform = 'ZESSDF_ALBARAN4' AND
            pgnam = 'ZRVADDN05'.
    IF tl_tnapr IS NOT INITIAL.
      DATA vl_objky TYPE nast-objky.
      vl_objky = vp_vbeln.

      SELECT kappl, objky, kschl, spras, parnr, parvw, erdat, eruhr
        INTO TABLE @DATA(tl_nast)
        FROM nast
        FOR ALL ENTRIES IN @tl_tnapr
        WHERE objky = @vl_objky AND
              kschl = @tl_tnapr-kschl.
    ENDIF.

    IF tl_nast IS INITIAL.
      zcl_seis_odata_utils=>lanzar_excepcion( 'El albarán todavía no se ha generado' ).
    ENDIF.





    TRY.
        SUBMIT zrvaddn05 WITH p_vbeln = vp_vbeln AND RETURN.
        IMPORT lv_pdf_file = vp_pdf FROM MEMORY ID 'PDF_FILE'.
      CATCH cx_root INTO DATA(rl_exc).
        zcl_seis_odata_utils=>lanzar_excepcion( ).

    ENDTRY.


  ENDMETHOD.
  METHOD albaranes_getdetail.
    DATA(wl_entrega) = entregas_getdetail( vp_tknum  = vp_tknum
                                           vp_vbeln  = vp_vbeln ).

    wp_albaran-tknum    = wl_entrega-tknum.
    wp_albaran-vbeln    = wl_entrega-vbeln.
    wp_albaran-mimetype = 'application/pdf'.
    wp_albaran-fichero  = |Albaran_{ wl_entrega-vbeln }.pdf|.

    IF vp_get_data = 'X'.
      wp_albaran-data   = albaranes_get_pdf( vp_vbeln ).
      wp_albaran-base64 = CL_HTTP_UTILITY=>encode_x_base64( wp_albaran-data ).
    ENDIF.

  ENDMETHOD.
  METHOD ALBARANES_IMPRIMIR.


    SELECT DISTINCT kschl, sform, pgnam
      INTO TABLE @DATA(tl_tnapr)
      FROM tnapr
      WHERE sform = 'ZESSDF_ALBARAN4' AND
            pgnam = 'ZRVADDN05'.
    IF tl_tnapr IS NOT INITIAL.
      DATA vl_objky TYPE nast-objky.
      vl_objky = vp_vbeln.

      SELECT kappl, objky, kschl, spras, parnr, parvw, erdat, eruhr
        INTO TABLE @DATA(tl_nast)
        FROM nast
        FOR ALL ENTRIES IN @tl_tnapr
        WHERE objky = @vl_objky AND
              kschl = @tl_tnapr-kschl.
    ENDIF.

    IF tl_nast IS INITIAL.
      zcl_seis_odata_utils=>lanzar_excepcion( 'El albarán todavía no se ha generado' ).
    ENDIF.


    SUBMIT zrvaddn05 WITH p_vbeln = vp_vbeln
                     WITH p_impr  = 'X'
                     with p_previs = vp_previsualizar
                     AND RETURN.
  ENDMETHOD.
  METHOD cartas_porte_get_pdf.



*    SELECT DISTINCT kschl, sform, pgnam
*      INTO TABLE @DATA(tl_tnapr)
*      FROM tnapr
*      WHERE sform = 'ZSSDF_CARTA_PORTE_TR' AND
*            pgnam = 'ZSRV56TD00_CARTAPORTE'.
*    IF tl_tnapr IS NOT INITIAL.
*      DATA vl_objky TYPE nast-objky.
*      vl_objky = vp_tknum.
*
*      SELECT kappl, objky, kschl, spras, parnr, parvw, erdat, eruhr
*        INTO TABLE @DATA(tl_nast)
*        FROM nast
*        FOR ALL ENTRIES IN @tl_tnapr
*        WHERE objky = @vl_objky AND
*              kschl = @tl_tnapr-kschl.
*    ENDIF.
*
*    IF tl_nast IS INITIAL.
*      zcl_seis_odata_utils=>lanzar_excepcion( 'La carta de porte todavía no se ha generado' ).
*    ENDIF.



    SUBMIT zsrv56td00_cartaporte WITH p_tknum = vp_tknum AND RETURN.
    IMPORT lv_pdf_file = vp_carta_porte FROM MEMORY ID 'PDF_FILE'.



  ENDMETHOD.
  METHOD cartas_porte_getdetail.
    DATA(wl_transporte) = transportes_getdetail( vp_tknum  = vp_tknum ).

    wp_carta_porte-tknum    = wl_transporte-tknum.
    wp_carta_porte-mimetype = 'application/pdf'.
    wp_carta_porte-fichero  = |Carta_porte_{ wl_transporte-tknum }.pdf|.

    IF vp_get_data = 'X'.
      wp_carta_porte-data   = cartas_porte_get_pdf( vp_tknum ).
      wp_carta_porte-base64 = CL_HTTP_UTILITY=>encode_x_base64( wp_carta_porte-data ).
    ENDIF.


  ENDMETHOD.
  METHOD cartas_porte_imprimir.



*    SELECT DISTINCT kschl, sform, pgnam
*      INTO TABLE @DATA(tl_tnapr)
*      FROM tnapr
*      WHERE sform = 'ZSSDF_CARTA_PORTE_TR' AND
*            pgnam = 'ZSRV56TD00_CARTAPORTE'.
*    IF tl_tnapr IS NOT INITIAL.
*      DATA vl_objky TYPE nast-objky.
*      vl_objky = vp_tknum.
*
*      SELECT kappl, objky, kschl, spras, parnr, parvw, erdat, eruhr
*        INTO TABLE @DATA(tl_nast)
*        FROM nast
*        FOR ALL ENTRIES IN @tl_tnapr
*        WHERE objky = @vl_objky AND
*              kschl = @tl_tnapr-kschl.
*    ENDIF.
*
*    IF tl_nast IS INITIAL.
*      zcl_seis_odata_utils=>lanzar_excepcion( 'La carta de porte todavía no se ha generado' ).
*    ENDIF.


    SUBMIT zsrv56td00_cartaporte WITH p_tknum = vp_tknum
                                 WITH p_impr  = 'X'
                                 WITH p_previs = vp_previsualizar
                                 AND RETURN.



  ENDMETHOD.
  method CARTASPORTE_GET_ENTITY.
    tratar_key_entity( EXPORTING io_tech_request_context = io_tech_request_context
                       IMPORTING wp_entity               = er_entity ).
    er_entity = cartas_porte_getdetail( vp_tknum    = er_entity-tknum
                                        vp_get_data = 'X' ).
  endmethod.
  METHOD constructor.
    super->constructor( ).
    rg_dpc_base = rp_dpc_base.
  ENDMETHOD.
  METHOD CONVERTIR_TABLA_TEXTO_A_STRING.

    LOOP AT tp_texto ASSIGNING FIELD-SYMBOL(<fs_texto>).
      vp_texto = |{ vp_texto } { <fs_texto>-tdline }|.
    ENDLOOP.
    CONDENSE vp_texto.


*    DATA: vp_fecha     TYPE d,
*          vp_hora      TYPE t,
*          tp_texto_out LIKE tp_texto,
*          vp_user      TYPE sy-uname.
*
*
**    CLEAR: vp_user, vp_fecha, vp_hora, vp_texto, tp_texto_out.
*
*    DATA tl_texto_aux LIKE tp_texto.
*    DATA: tl_cabecera       TYPE TABLE OF string.
*    DATA: vl_token_cabecera TYPE string.
*    DATA vl_line    TYPE tdline.
*    DATA vl_format  TYPE tdformat.
*
*
*
*    LOOP AT tp_texto ASSIGNING FIELD-SYMBOL(<fs_texto>).
*      IF <fs_texto>-tdformat = '>X'.
*        <fs_texto>-tdformat = <fs_texto>-tdline(2).
*        <fs_texto>-tdline = <fs_texto>-tdline+2.
*      ENDIF.
*    ENDLOOP.
*
*    LOOP AT tp_texto ASSIGNING <fs_texto>.
**      IF <fs_texto>-tdformat = '* '.
**        " * ----------------------------------------
**        " * 30.01.2018 13:26:16 SEIS SEIS (SEIS)
**        CLEAR: vp_user, vp_fecha, vp_hora, vp_texto, tp_texto_out.
**        SPLIT <fs_texto>-tdline AT space INTO TABLE tl_cabecera.
**        CHECK lines( tl_cabecera ) > 2.
**
**        READ TABLE tl_cabecera INTO vl_token_cabecera INDEX 2.
**        IF sy-subrc = 0.
**          CALL FUNCTION 'CONVERT_DATE_TO_INTERNAL'
**            EXPORTING
**              date_external            = vl_token_cabecera
**            IMPORTING
**              date_internal            = vp_fecha
**            EXCEPTIONS
**              date_external_is_invalid = 1
**              OTHERS                   = 2.
**        ENDIF.
**
**        IF sy-subrc = 0.
**          READ TABLE tl_cabecera INTO vl_token_cabecera INDEX 3.
**          IF sy-subrc = 0.
**            CALL FUNCTION 'CONVERT_TIME_INPUT'
**              EXPORTING
**                input                     = vl_token_cabecera
**              IMPORTING
**                output                    = vp_hora
**              EXCEPTIONS
**                plausibility_check_failed = 1
**                wrong_format_in_input     = 2
**                OTHERS                    = 3.
**          ENDIF.
**        ENDIF.
**
**        IF sy-subrc <> 0.
**          CLEAR: vp_fecha, vp_hora.
**        ENDIF.
**
**      ELSE.
*        vl_line   = <fs_texto>-tdline.
*        vl_format = <fs_texto>-tdformat.
*
*        IF vp_texto IS INITIAL.
*          vp_texto = vl_line.
*        ELSE.
*          IF vl_format IS NOT INITIAL OR vl_format <> '='.
*            CONCATENATE vp_texto vl_line INTO vp_texto SEPARATED BY cl_abap_char_utilities=>cr_lf.
*          ELSE.
*            CONCATENATE vp_texto vl_line INTO vp_texto SEPARATED BY space.
*          ENDIF.
*        ENDIF.
*
*        APPEND INITIAL LINE TO tp_texto_out ASSIGNING FIELD-SYMBOL(<fs_texto_out>).
*        <fs_texto_out>-tdformat = vl_format.
*        <fs_texto_out>-tdline   = vl_line.
*
*        REPLACE ALL OCCURRENCES OF cl_abap_char_utilities=>cr_lf    IN <fs_texto_out>-tdline WITH ''.
*        REPLACE ALL OCCURRENCES OF cl_abap_char_utilities=>newline  IN <fs_texto_out>-tdline WITH ''.
*        REPLACE ALL OCCURRENCES OF '##'  IN <fs_texto_out>-tdline WITH ''.
*
**      ENDIF.
*
*
*    ENDLOOP.

  ENDMETHOD.
  METHOD entregas_get_entity.
    tratar_key_entity( EXPORTING io_tech_request_context = io_tech_request_context
                       IMPORTING wp_entity               = er_entity ).
    er_entity = entregas_getdetail( vp_tknum  = er_entity-tknum
                                    vp_vbeln  = er_entity-vbeln ).

  ENDMETHOD.
  METHOD entregas_get_entityset.
    DATA wl_entity LIKE LINE OF et_entityset.
    tratar_key_entityset( EXPORTING io_tech_request_context = io_tech_request_context
                          IMPORTING wp_entity               = wl_entity ).

    DATA(tl_tknum_rg) = VALUE zsd_t_tknum_rg( ( sign = 'I' option = 'EQ' low = wl_entity-tknum ) ).
    et_entityset =  entregas_getlist( tp_tknum_rg = tl_tknum_rg ).

    zcl_seis_odata_utils=>tratar_entityset( EXPORTING io_tech_request_context  = io_tech_request_context
                                            CHANGING et_entityset = et_entityset ).
  ENDMETHOD.
  METHOD entregas_getdetail.


    SELECT SINGLE vttp~tknum, vttp~vbeln, likp~kunnr, likp~anzpk, kna1~name1, likp~kunag, kna1~ort01 AS city1, kna1~stras,
                  likp~vstel, likp~vkorg
      INTO CORRESPONDING FIELDS OF @wp_entrega
      FROM likp INNER JOIN vttp ON vttp~vbeln = likp~vbeln
                INNER JOIN kna1 ON kna1~kunnr = likp~kunnr
      WHERE likp~vbeln = @vp_vbeln AND
            vttp~tknum = @vp_tknum.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Entrega incorrecta' ).
    ENDIF.




    DATA tl_entregas LIKE TABLE OF wp_entrega.
    APPEND wp_entrega TO tl_entregas.
    entregas_getlist_ext( CHANGING tp_entregas = tl_entregas ).
    wp_entrega = tl_entregas[ 1 ].

  ENDMETHOD.
  METHOD entregas_getlist.


    SELECT vttp~tknum, vttp~vbeln, likp~kunnr, likp~anzpk, kna1~name1, likp~kunag, kna1_kunag~name1 AS kunag_name1,
           kna1~ort01 AS city1, kna1~stras,
           likp~vstel, likp~vkorg
      INTO CORRESPONDING FIELDS OF TABLE @tp_entregas
      FROM likp INNER JOIN vttp ON vttp~vbeln = likp~vbeln
                LEFT OUTER JOIN kna1 ON kna1~kunnr = likp~kunnr
                LEFT OUTER JOIN kna1 AS kna1_kunag ON kna1_kunag~kunnr = likp~kunag
      WHERE vttp~tknum IN @tp_tknum_rg.



    entregas_getlist_ext( CHANGING tp_entregas = tp_entregas ).
  ENDMETHOD.
  METHOD entregas_getlist_ext.
    CHECK tp_entregas IS NOT INITIAL.

    SELECT *
      INTO TABLE @DATA(tl_knva)
      FROM knva
      FOR ALL ENTRIES IN @tp_entregas
      WHERE kunnr = @tp_entregas-kunnr.

    SELECT tknum, sttrg, dpreg
      INTO TABLE @DATA(tl_vttk)
      FROM vttk
      FOR ALL ENTRIES IN @tp_entregas
      WHERE vttk~tknum = @tp_entregas-tknum.


    "Lineas de entregas
    SELECT lips~vbeln, lips~posnr, lips~werks, lips~matnr, t320~lgnum, lips~lfimg, lips~vrkme
      INTO TABLE @DATA(tl_entregas_lin)
      FROM lips INNER JOIN t320 ON t320~werks = lips~werks
      FOR ALL ENTRIES IN @tp_entregas
      WHERE lips~vbeln = @tp_entregas-vbeln.



    DATA: tl_vbeln_rg TYPE RANGE OF likp-vbeln,
          wl_vbeln_rg LIKE LINE OF tl_vbeln_rg.
    LOOP AT tp_entregas ASSIGNING FIELD-SYMBOL(<fs_entregas>).
      wl_vbeln_rg = VALUE #( sign = 'I' option = 'EQ' low = <fs_entregas>-vbeln ).
      COLLECT wl_vbeln_rg INTO tl_vbeln_rg.
    ENDLOOP.

    DATA(tl_ordenes_prod) = ordenes_prod_getlist( tp_vbeln_rg = tl_vbeln_rg ).
    SORT tl_ordenes_prod BY vbeln gltrp DESCENDING gluzp DESCENDING.


    "Festivos
    SELECT *
      INTO TABLE @DATA(tl_zssdt016calendar)
      FROM zssdt016calendar
      FOR ALL ENTRIES IN @tp_entregas
      WHERE vkorg = @tp_entregas-vkorg AND
            kunwe = @tp_entregas-kunnr.


    "Num. palets teóricos
    SELECT mlgn~matnr, mlgn~lhmg1, mara~meins, mlgn~lety1, mlgn~lhme1
      INTO TABLE @DATA(tl_mlgn)
      FROM mlgn INNER JOIN mara ON mara~matnr = mlgn~matnr
      FOR ALL ENTRIES IN @tl_entregas_lin
      WHERE mlgn~matnr = @tl_entregas_lin-matnr AND
            mlgn~lgnum = @tl_entregas_lin-lgnum.


    TYPES: tt_indices TYPE TABLE OF sy-index WITH DEFAULT KEY.
    DATA: BEGIN OF wl_horarios,
            dias      TYPE text20,
            intervalo TYPE text40,
          END OF wl_horarios.
    DATA: tl_horarios LIKE TABLE OF wl_horarios.

    LOOP AT tp_entregas ASSIGNING <fs_entregas>.
      READ TABLE tl_vttk ASSIGNING FIELD-SYMBOL(<fs_vttk>) WITH KEY tknum = <fs_entregas>-tknum.
      IF sy-subrc = 0.
        IF <fs_vttk>-sttrg < c_sttrg_despacho_exped.
          "Aquí sería el cálculo de núm. palets teóricos
          LOOP AT tl_entregas_lin ASSIGNING FIELD-SYMBOL(<fs_entregas_lin>) WHERE vbeln = <fs_entregas>-vbeln.
            READ TABLE tl_mlgn ASSIGNING FIELD-SYMBOL(<fs_mlgn>) WITH KEY matnr = <fs_entregas_lin>-matnr.
            IF sy-subrc = 0.
              DATA(vl_menge) =  <fs_entregas_lin>-lfimg.
              CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
                EXPORTING
                  i_matnr              = <fs_entregas_lin>-matnr
                  i_in_me              = <fs_entregas_lin>-vrkme
                  i_out_me             = <fs_mlgn>-lhme1
                  i_menge              = vl_menge
                IMPORTING
                  e_menge              = vl_menge
                EXCEPTIONS
                  error_in_application = 1
                  error                = 2
                  OTHERS               = 3.
              IF sy-subrc = 0.
                vl_menge = vl_menge / <fs_mlgn>-lhmg1.
                vl_menge = ceil( vl_menge ).
              ELSE.
                CLEAR vl_menge.
              ENDIF.

              ADD vl_menge TO <fs_entregas>-num_palets.
            ENDIF.
          ENDLOOP.

        ELSE.
          <fs_entregas>-num_palets = <fs_entregas>-anzpk.
        ENDIF.
      ENDIF.


      READ TABLE tl_ordenes_prod ASSIGNING FIELD-SYMBOL(<fs_ordenes_prod>) WITH KEY vbeln = <fs_entregas>-vbeln.
      IF sy-subrc = 0.
        <fs_entregas>-aufnr         = <fs_ordenes_prod>-aufnr.
        <fs_entregas>-gltrp         = <fs_ordenes_prod>-gltrp.
        <fs_entregas>-gluzp         = <fs_ordenes_prod>-gluzp.
        <fs_entregas>-gstrp         = <fs_ordenes_prod>-gstrp.
        <fs_entregas>-gsuzp         = <fs_ordenes_prod>-gsuzp.
        <fs_entregas>-situacion     = <fs_ordenes_prod>-situacion.
        <fs_entregas>-situacion_txt = <fs_ordenes_prod>-situacion_txt.
      ENDIF.


      "Horario destinatario
      CLEAR tl_horarios.
      READ TABLE tl_knva ASSIGNING FIELD-SYMBOL(<fs_knva>) WITH KEY kunnr = <fs_entregas>-kunnr.
      IF sy-subrc = 0.

        "L – V: 08.00 – 13.00
        "L – J: 08.00 – 13.00 / V: 08.00 – 12.00
        "L – M – J – V: 08.00 – 13.00 / X: 08.00 – 12.00

        "lunes mañana de- mañana a- tarde de - tarde a-
        "Lunes
        CLEAR wl_horarios.
        DATA: vl_hora1_ini TYPE tims,
              vl_hora1_fin TYPE tims,
              vl_hora2_ini TYPE tims,
              vl_hora2_fin TYPE tims,
              vl_dia       TYPE c.

        DATA: vl_hora_ini_c TYPE text10,
              vl_hora_fin_c TYPE text10,
              vl_index      TYPE n.

        DO 7 TIMES.
          CLEAR: wl_horarios.
          vl_index = sy-index.
          CASE vl_index.
            WHEN 1.
              vl_hora1_ini    = <fs_knva>-moab1.
              vl_hora1_fin    = <fs_knva>-mobi1.
              vl_hora2_ini    = <fs_knva>-moab2.
              vl_hora2_fin    = <fs_knva>-mobi2.
            WHEN 2.
              vl_hora1_ini = <fs_knva>-diab1.
              vl_hora1_fin = <fs_knva>-dibi1.
              vl_hora2_ini = <fs_knva>-diab2.
              vl_hora2_fin = <fs_knva>-dibi2.
            WHEN 3.
              vl_hora1_ini = <fs_knva>-miab1.
              vl_hora1_fin = <fs_knva>-mibi1.
              vl_hora2_ini = <fs_knva>-miab2.
              vl_hora2_fin = <fs_knva>-mibi2.
            WHEN 4.
              vl_hora1_ini = <fs_knva>-doab1.
              vl_hora1_fin = <fs_knva>-dobi1.
              vl_hora2_ini = <fs_knva>-doab2.
              vl_hora2_fin = <fs_knva>-dobi2.
            WHEN 5.
              vl_hora1_ini = <fs_knva>-frab1.
              vl_hora1_fin = <fs_knva>-frbi1.
              vl_hora2_ini = <fs_knva>-frab2.
              vl_hora2_fin = <fs_knva>-frbi2.
            WHEN 6.
              vl_hora1_ini = <fs_knva>-saab1.
              vl_hora1_fin = <fs_knva>-sabi1.
              vl_hora2_ini = <fs_knva>-saab2.
              vl_hora2_fin = <fs_knva>-sabi2.
            WHEN 7.
              vl_hora1_ini = <fs_knva>-soab1.
              vl_hora1_fin = <fs_knva>-sobi1.
              vl_hora2_ini = <fs_knva>-soab2.
              vl_hora2_fin = <fs_knva>-sobi2.
          ENDCASE.
          CHECK vl_hora1_ini IS NOT INITIAL.

          WRITE vl_hora1_ini TO vl_hora_ini_c.
          WRITE vl_hora1_fin TO vl_hora_fin_c.
          wl_horarios-intervalo = |{ vl_hora_ini_c(5) } - { vl_hora_fin_c(5) }|.
          IF vl_hora2_ini IS NOT INITIAL.
            WRITE vl_hora2_ini TO vl_hora_ini_c.
            WRITE vl_hora2_fin TO vl_hora_fin_c.
            wl_horarios-intervalo = |{ wl_horarios-intervalo }; { vl_hora_ini_c(5) } - { vl_hora_fin_c(5) }|.
          ENDIF.

          READ TABLE tl_horarios ASSIGNING FIELD-SYMBOL(<fs_horarios>) WITH KEY intervalo = wl_horarios-intervalo.
          IF sy-subrc <> 0.
            wl_horarios-dias = vl_index.
            APPEND wl_horarios TO tl_horarios.
          ELSE.
            <fs_horarios>-dias =  <fs_horarios>-dias && vl_index.
          ENDIF.
        ENDDO.
      ENDIF.

      DATA: vl_dias_aux TYPE text40,
            vl_num_ant  TYPE i,
            vl_num_sig  TYPE i,
            vl_num      TYPE i.
      LOOP AT tl_horarios ASSIGNING <fs_horarios>.
        CLEAR: vl_dias_aux, vl_num_ant, vl_num_sig.

        vl_num_ant = -1.
        DATA(vl_grabar) = space.
        WHILE <fs_horarios>-dias IS NOT INITIAL.
          vl_num = <fs_horarios>-dias(1).
          IF strlen( <fs_horarios>-dias ) < 2.
            vl_grabar = 'X'.
          ELSEIF vl_num_ant <> vl_num - 1.
            vl_grabar = 'X'.
          ELSE.
            vl_num_sig = <fs_horarios>-dias+1(1).
            IF vl_num_sig <> vl_num + 1.
              vl_grabar = 'X'.
            ENDIF.
          ENDIF.

          IF vl_grabar = 'X'.
            vl_dia = vl_num.
            TRANSLATE vl_dia USING '1L2M3X4J5V6S7D'.
            IF vl_dias_aux IS INITIAL.
              vl_dias_aux = vl_dia.
            ELSE.
              vl_dias_aux = |{ vl_dias_aux } - { vl_dia }|.
            ENDIF.
          ENDIF.

          <fs_horarios>-dias = <fs_horarios>-dias+1.
          vl_num_ant         = vl_num.
        ENDWHILE.
        <fs_horarios>-dias = vl_dias_aux.

      ENDLOOP.


      DATA vl_dia_y_horario TYPE text80.
      LOOP AT tl_horarios ASSIGNING <fs_horarios>.
        vl_dia_y_horario = |{ <fs_horarios>-dias }: { <fs_horarios>-intervalo }|.
        IF <fs_entregas>-horario_descarga IS INITIAL.
          <fs_entregas>-horario_descarga = vl_dia_y_horario.
        ELSE.
          <fs_entregas>-horario_descarga = |{ <fs_entregas>-horario_descarga } { cl_abap_char_utilities=>newline } { vl_dia_y_horario }|.
        ENDIF.
      ENDLOOP.

      "Festivo
      LOOP AT tl_zssdt016calendar ASSIGNING FIELD-SYMBOL(<fs_zssdt016calendar>) WHERE vkorg = <fs_entregas>-vkorg AND
                                                                                      kunwe = <fs_entregas>-kunnr.
        IF <fs_vttk>-dpreg BETWEEN <fs_zssdt016calendar>-adatu AND <fs_zssdt016calendar>-bdatu.
          <fs_entregas>-cla_festivo = 'X'.
          EXIT.
        ENDIF.
      ENDLOOP.


    ENDLOOP.






  ENDMETHOD.
  METHOD entregas_pos_getdetail.

    SELECT SINGLE vttp~tknum, lips~vbeln, lips~posnr, lips~matnr, lips~kdmat,
                  lips~lfimg, lips~meins, lips~vgbel, lips~vgpos,
                  makt~maktx, lips~werks, lips~vrkme
      INTO CORRESPONDING FIELDS OF @wp_entrega_pos
      FROM lips INNER JOIN likp ON likp~vbeln = lips~vbeln
                INNER JOIN vttp ON vttp~vbeln = lips~vbeln
                LEFT OUTER JOIN makt ON makt~matnr = lips~matnr AND
                                        makt~spras = @sy-langu
      WHERE vttp~tknum = @vp_tknum AND
            lips~vbeln = @vp_vbeln AND
            lips~posnr = @vp_posnr.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Posición de entrega incorrecta' ).
    ENDIF.


    DATA tl_entregas_pos LIKE TABLE OF wp_entrega_pos.
    APPEND wp_entrega_pos TO tl_entregas_pos.
    entregas_pos_getlist_ext( CHANGING tp_entregas_pos = tl_entregas_pos ).
    wp_entrega_pos = tl_entregas_pos[ 1 ].

  ENDMETHOD.
  METHOD entregas_pos_getlist.
    SELECT vttp~tknum, lips~vbeln, lips~posnr, lips~matnr, lips~kdmat,
           lips~lfimg, lips~meins, lips~vgbel, lips~vgpos,
           makt~maktx, lips~werks, lips~vrkme, lips~lgort
      INTO CORRESPONDING FIELDS OF TABLE @tp_entregas_pos
      FROM lips INNER JOIN likp ON likp~vbeln = lips~vbeln
                INNER JOIN vttp ON vttp~vbeln = lips~vbeln
                LEFT OUTER JOIN makt ON makt~matnr = lips~matnr AND
                                        makt~spras = @sy-langu
      WHERE vttp~tknum IN @tp_tknum_rg AND
            lips~vbeln IN @tp_vbeln_rg.

    entregas_pos_getlist_ext( CHANGING tp_entregas_pos = tp_entregas_pos ).
  ENDMETHOD.
  METHOD entregas_pos_getlist_ext.
    CHECK tp_entregas_pos IS NOT INITIAL.


    DATA: tl_lines TYPE TABLE OF tline,
          wl_text  TYPE thead.

    DATA: tl_vbeln_rg TYPE RANGE OF likp-vbeln,
          wl_vbeln_rg LIKE LINE OF tl_vbeln_rg.
    LOOP AT tp_entregas_pos ASSIGNING FIELD-SYMBOL(<fs_entregas_pos>).
      wl_vbeln_rg = VALUE #( sign = 'I' option = 'EQ' low = <fs_entregas_pos>-vbeln ).
      COLLECT wl_vbeln_rg INTO tl_vbeln_rg.
    ENDLOOP.

    DATA(tl_ordenes_prod) = ordenes_prod_getlist( tp_vbeln_rg = tl_vbeln_rg ).

    "Lineas de entregas
    DATA(vl_werks) = tp_entregas_pos[ 1 ]-werks.
    SELECT SINGLE t320~lgnum
      INTO @DATA(vl_lgnum)
      FROM t320
      WHERE t320~werks = @vl_werks.


    "Num. palets teóricos
    SELECT mlgn~matnr, mlgn~lhmg1, mara~meins, mlgn~lety1, mlgn~lhme1
      INTO TABLE @DATA(tl_mlgn)
      FROM mlgn INNER JOIN mara ON mara~matnr = mlgn~matnr
      FOR ALL ENTRIES IN @tp_entregas_pos
      WHERE mlgn~matnr = @tp_entregas_pos-matnr AND
            mlgn~lgnum = @vl_lgnum.

    "Stock disponible 1
    SELECT matnr, werks, lgort, labst
      INTO TABLE @DATA(tl_mard)
      FROM mard
      FOR ALL ENTRIES IN @tp_entregas_pos
      WHERE matnr = @tp_entregas_pos-matnr AND
            werks = @tp_entregas_pos-werks AND
            lgort = @tp_entregas_pos-lgort.

    "Stock disponible 2
    SELECT matnr, werks, lgort, kalab
      INTO TABLE @DATA(tl_mska)
      FROM mska
      FOR ALL ENTRIES IN @tp_entregas_pos
      WHERE matnr = @tp_entregas_pos-matnr AND
            werks = @tp_entregas_pos-werks AND
            lgort = @tp_entregas_pos-lgort.



    LOOP AT tp_entregas_pos ASSIGNING <fs_entregas_pos>.
      READ TABLE tl_ordenes_prod ASSIGNING FIELD-SYMBOL(<fs_ordenes_prod>) WITH KEY vbeln = <fs_entregas_pos>-vbeln
                                                                                    posnr = <fs_entregas_pos>-posnr.
      IF sy-subrc = 0.
        <fs_entregas_pos>-aufnr = <fs_ordenes_prod>-aufnr.
        <fs_entregas_pos>-gltrp = <fs_ordenes_prod>-gltrp.
        <fs_entregas_pos>-gluzp = <fs_ordenes_prod>-gluzp.
        <fs_entregas_pos>-gltrs = <fs_ordenes_prod>-gltrs.
        <fs_entregas_pos>-gluzs = <fs_ordenes_prod>-gluzs.
        <fs_entregas_pos>-gstrp = <fs_ordenes_prod>-gstrp.
        <fs_entregas_pos>-gsuzp = <fs_ordenes_prod>-gsuzp.
        <fs_entregas_pos>-situacion     = <fs_ordenes_prod>-situacion.
        <fs_entregas_pos>-situacion_txt = <fs_ordenes_prod>-situacion_txt.
      ENDIF.


      wl_text-tdid     = 'ZC03'.
      wl_text-tdspras  = 'S'.
      wl_text-tdname   = <fs_entregas_pos>-vbeln && <fs_entregas_pos>-posnr.
      wl_text-tdobject = 'VBBP'.

      CLEAR tl_lines.
      CALL FUNCTION 'READ_TEXT'
        EXPORTING
          id       = wl_text-tdid
          language = wl_text-tdspras
          name     = wl_text-tdname
          object   = wl_text-tdobject
        TABLES
          lines    = tl_lines
        EXCEPTIONS
          OTHERS   = 1.
      <fs_entregas_pos>-texto = convertir_tabla_texto_a_string( tl_lines ).



      READ TABLE tl_mlgn ASSIGNING FIELD-SYMBOL(<fs_mlgn>) WITH KEY matnr = <fs_entregas_pos>-matnr.
      IF sy-subrc = 0.
        DATA(vl_menge) =  <fs_entregas_pos>-lfimg.
        CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
          EXPORTING
            i_matnr              = <fs_entregas_pos>-matnr
            i_in_me              = <fs_entregas_pos>-vrkme
            i_out_me             = <fs_mlgn>-lhme1
            i_menge              = vl_menge
          IMPORTING
            e_menge              = vl_menge
          EXCEPTIONS
            error_in_application = 1
            error                = 2
            OTHERS               = 3.
        IF sy-subrc = 0.
          vl_menge = vl_menge / <fs_mlgn>-lhmg1.
          vl_menge = ceil( vl_menge ).
        ELSE.
          CLEAR vl_menge.
        ENDIF.

        <fs_entregas_pos>-num_palets = vl_menge.
      ENDIF.

      READ TABLE tl_mard ASSIGNING FIELD-SYMBOL(<fs_mard>) WITH KEY werks = <fs_entregas_pos>-werks
                                                                    matnr = <fs_entregas_pos>-matnr
                                                                    lgort = <fs_entregas_pos>-lgort.
      IF sy-subrc = 0.
        ADD <fs_mard>-labst TO <fs_entregas_pos>-labst.
      ENDIF.
      LOOP AT tl_mska ASSIGNING FIELD-SYMBOL(<fs_mska>) WHERE werks = <fs_entregas_pos>-werks AND
                                                              matnr = <fs_entregas_pos>-matnr AND
                                                              lgort = <fs_entregas_pos>-lgort.
        ADD <fs_mska>-kalab TO <fs_entregas_pos>-labst.
      ENDLOOP.
    ENDLOOP.

*ANZPK







  ENDMETHOD.
  method ENTREGAS_POSICIO_GET_ENTITY.
    tratar_key_entity( EXPORTING io_tech_request_context = io_tech_request_context
                       IMPORTING wp_entity               = er_entity ).
    er_entity = entregas_pos_getdetail( vp_tknum  = er_entity-tknum
                                        vp_vbeln  = er_entity-vbeln
                                        vp_posnr  = er_entity-posnr ).
  endmethod.
  METHOD entregas_posicio_get_entityset.
    DATA wl_entity LIKE LINE OF et_entityset.
    tratar_key_entityset( EXPORTING io_tech_request_context = io_tech_request_context
                          IMPORTING wp_entity               = wl_entity ).

    DATA: tl_tknum_rg TYPE zsd_t_tknum_rg,
          tl_vbeln_rg TYPE zsd_t_vbeln_rg.
    tl_tknum_rg  = VALUE #( ( sign = 'I' option = 'EQ' low = wl_entity-tknum ) ).
    tl_vbeln_rg  = VALUE #( ( sign = 'I' option = 'EQ' low = wl_entity-vbeln ) ).

    et_entityset =  entregas_pos_getlist( tp_vbeln_rg = tl_vbeln_rg
                                          tp_tknum_rg = tl_tknum_rg ).

    zcl_seis_odata_utils=>tratar_entityset( EXPORTING io_tech_request_context  = io_tech_request_context
                                            CHANGING et_entityset = et_entityset ).
  ENDMETHOD.
  METHOD finalizar_transporte.

    DATA(wl_transporte) = transportes_getdetail( vp_tknum ).

    IF wl_transporte-sttrg <> c_sttrg_inicio_transporte.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Estado incorrecto. No se puede finalizar el trasnporte' ).
    ENDIF.

    DATA: wl_headerdata	      TYPE bapishipmentheader,
          wl_headerdataaction	TYPE bapishipmentheaderaction,
          tl_return           TYPE TABLE OF bapiret2.

    wl_headerdata-shipment_num            = vp_tknum.
    wl_headerdata-status_shpmnt_end       = 'X'.
    wl_headerdataaction-status_shpmnt_end = 'C'.

    CALL FUNCTION 'BAPI_SHIPMENT_CHANGE'
      EXPORTING
        headerdata       = wl_headerdata
        headerdataaction = wl_headerdataaction
      TABLES
        return           = tl_return.
    zcl_seis_odata_utils=>lanzar_excepcion( bapiret2_t = tl_return ).


  ENDMETHOD.
  METHOD HOJA_PICKING_IMPRIMIR.


*    SELECT DISTINCT kschl, sform, pgnam
*      INTO TABLE @DATA(tl_tnapr)
*      FROM tnapr
*      WHERE sform = 'ZESSDF_HOJA_PICKING_ZPCK' AND
*            pgnam = 'ZRV56TD00'.
*    IF tl_tnapr IS NOT INITIAL.
*      DATA vl_objky TYPE nast-objky.
*      vl_objky = vp_tknum.
*
*      SELECT kappl, objky, kschl, spras, parnr, parvw, erdat, eruhr
*        INTO TABLE @DATA(tl_nast)
*        FROM nast
*        FOR ALL ENTRIES IN @tl_tnapr
*        WHERE objky = @vl_objky AND
*              kschl = @tl_tnapr-kschl.
*    ENDIF.
*
*    IF tl_nast IS INITIAL.
*      zcl_seis_odata_utils=>lanzar_excepcion( 'La hoja de picking todavía no se ha generado' ).
*    ENDIF.



    SUBMIT ZRV56TD00 WITH p_tknum = vp_tknum
                     WITH p_impr  = 'X'
                     with p_previs = vp_previsualizar
                     AND RETURN.



  ENDMETHOD.
  METHOD iniciar_transporte.
    DATA(wl_transporte) = transportes_getdetail( vp_tknum ).

    IF wl_transporte-sttrg <> c_sttrg_despacho_exped.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Estado incorrecto. No se puede iniciar el trasnporte' ).
    ENDIF.

    DATA: wl_headerdata	      TYPE bapishipmentheader,
          wl_headerdataaction	TYPE bapishipmentheaderaction,
          tl_return           TYPE TABLE OF bapiret2.

    wl_headerdata-shipment_num              = vp_tknum.
    wl_headerdata-status_shpmnt_start       = 'X'.
    wl_headerdataaction-status_shpmnt_start = 'C'.

    CALL FUNCTION 'BAPI_SHIPMENT_CHANGE'
      EXPORTING
        headerdata       = wl_headerdata
        headerdataaction = wl_headerdataaction
      TABLES
        return           = tl_return.
    zcl_seis_odata_utils=>lanzar_excepcion( bapiret2_t = tl_return ).




  ENDMETHOD.
  METHOD ordenes_prod_getlist.
    CHECK tp_vbeln_rg IS NOT INITIAL.

    TYPES: BEGIN OF st_lips,
             vbeln TYPE lips-vbeln,
             posnr TYPE lips-posnr,
             vgbel TYPE lips-vgbel,
             vgpos TYPE lips-vgpos,
             ebeln TYPE ekpo-ebeln,
             ebelp TYPE ekpo-ebelp,
           END OF st_lips.
    DATA: tl_lips TYPE TABLE OF st_lips.


    SELECT vbeln, posnr, vgbel, vgpos
      INTO CORRESPONDING FIELDS OF TABLE @tl_lips
      FROM lips
      WHERE lips~vbeln IN @tp_vbeln_rg AND
            lips~vgbel <> @space.
    CHECK tl_lips IS NOT INITIAL.

    LOOP AT tl_lips ASSIGNING FIELD-SYMBOL(<fs_lips>).
      <fs_lips>-ebeln = <fs_lips>-vgbel.
      <fs_lips>-ebelp = <fs_lips>-vgpos+1.
    ENDLOOP.



    "Pedidos de ventas
    SELECT caufv~kdauf, caufv~kdpos, caufv~aufnr, caufv~gltrs, caufv~gluzs, caufv~gamng, caufv~gmein,
           caufv~gltrp, caufv~gluzp, caufv~gstrp, caufv~gsuzp, caufv~objnr, caufv~zzsubco_ebeln, caufv~zzsubco_ebelp
      INTO TABLE @DATA(tl_caufv)
      FROM caufv
      FOR ALL ENTRIES IN @tl_lips
      WHERE caufv~kdauf = @tl_lips-vgbel AND
            caufv~kdpos = @tl_lips-vgpos.
    LOOP AT tl_caufv ASSIGNING FIELD-SYMBOL(<fs_caufv>).
      LOOP AT tl_lips ASSIGNING <fs_lips> WHERE vgbel = <fs_caufv>-kdauf AND
                                                vgpos = <fs_caufv>-kdpos.
        APPEND INITIAL LINE TO tp_ordenes_prod ASSIGNING FIELD-SYMBOL(<fs_ordenes_prod>).
        MOVE-CORRESPONDING <fs_lips>  TO <fs_ordenes_prod>.
        MOVE-CORRESPONDING <fs_caufv> TO <fs_ordenes_prod>.
      ENDLOOP.
    ENDLOOP.


    "Pedidos subco
    SELECT caufv~kdauf, caufv~kdpos, caufv~aufnr, caufv~gltrs, caufv~gluzs, caufv~gamng, caufv~gmein,
           caufv~gltrp, caufv~gluzp, caufv~gstrp, caufv~gsuzp, caufv~objnr, caufv~zzsubco_ebeln, caufv~zzsubco_ebelp
      into TABLE @data(tl_caufv_subco)
      FROM caufv
      FOR ALL ENTRIES IN @tl_lips
      WHERE caufv~zzsubco_ebeln = @tl_lips-ebeln AND
            caufv~zzsubco_ebelp = @tl_lips-ebelp.

    LOOP AT tl_caufv_subco ASSIGNING FIELD-SYMBOL(<fs_caufv_subco>).
      LOOP AT tl_lips ASSIGNING <fs_lips> WHERE ebeln = <fs_caufv_subco>-zzsubco_ebeln AND
                                                ebelp = <fs_caufv_subco>-zzsubco_ebelp.
        APPEND INITIAL LINE TO tp_ordenes_prod ASSIGNING <fs_ordenes_prod>.
        MOVE-CORRESPONDING <fs_lips>  TO <fs_ordenes_prod>.
        MOVE-CORRESPONDING <fs_caufv_subco> TO <fs_ordenes_prod>.
      ENDLOOP.
    ENDLOOP.


    SORT tp_ordenes_prod BY vbeln posnr gltrs DESCENDING gluzs DESCENDING.
    ordenes_prod_getlist_ext( CHANGING tp_ordenes_prod = tp_ordenes_prod ).

  ENDMETHOD.
  METHOD ordenes_prod_getlist_ext.
    CHECK tp_ordenes_prod IS NOT INITIAL.


    SELECT vbeln, tknum
      INTO TABLE @DATA(tl_vttp)
      FROM vttp
      FOR ALL ENTRIES IN @tp_ordenes_prod
      WHERE vbeln = @tp_ordenes_prod-vbeln.


    SELECT aufnr, posnr, kdauf, kdpos, wemng
      INTO TABLE @DATA(tl_afpo)
      FROM afpo
      FOR ALL ENTRIES IN @tp_ordenes_prod
      WHERE aufnr = @tp_ordenes_prod-aufnr AND
            kdauf = @tp_ordenes_prod-kdauf AND
            kdpos = @tp_ordenes_prod-kdpos.

    SELECT objnr, stat
      INTO TABLE @DATA(tl_jest)
      FROM jest
      FOR ALL ENTRIES IN @tp_ordenes_prod
      WHERE objnr = @tp_ordenes_prod-objnr AND
            inact = @space.


    DATA: tl_dd07v_situacion TYPE TABLE OF dd07v.
    CALL FUNCTION 'DD_DD07V_GET'
      EXPORTING
        domain_name = 'ZSSD_DO_SITUACION'
      TABLES
        dd07v_tab   = tl_dd07v_situacion.


    LOOP AT tp_ordenes_prod ASSIGNING FIELD-SYMBOL(<fs_ordenes_prod>).
      READ TABLE tl_vttp ASSIGNING FIELD-SYMBOL(<fs_vttp>) WITH KEY vbeln = <fs_ordenes_prod>-vbeln.
      IF sy-subrc = 0.
        <fs_ordenes_prod>-tknum = <fs_vttp>-tknum.
      ENDIF.

      "Situación
      READ TABLE tl_afpo ASSIGNING FIELD-SYMBOL(<fs_afpo>) WITH KEY aufnr = <fs_ordenes_prod>-aufnr
                                                                    kdauf = <fs_ordenes_prod>-kdauf
                                                                    kdpos = <fs_ordenes_prod>-kdpos.
      IF sy-subrc = 0 AND <fs_afpo>-wemng > 0.
        <fs_ordenes_prod>-situacion = '02'.

      ELSE.
        LOOP AT tl_jest ASSIGNING FIELD-SYMBOL(<fs_jest>) WHERE objnr = <fs_ordenes_prod>-objnr.
          CASE <fs_jest>-stat.
            WHEN 'I0001'.
              <fs_ordenes_prod>-situacion = '00'.
            WHEN 'I0513'.
              <fs_ordenes_prod>-situacion = '01'.
            WHEN 'I0002'.
              <fs_ordenes_prod>-situacion = '04'.
            WHEN 'I0045' OR 'I0046'.
              <fs_ordenes_prod>-situacion = '03'.
          ENDCASE.
        ENDLOOP.
      ENDIF.


      READ TABLE tl_dd07v_situacion ASSIGNING FIELD-SYMBOL(<fs_dd07v_situacion>) WITH KEY domvalue_l = <fs_ordenes_prod>-situacion.
      IF sy-subrc = 0.
        <fs_ordenes_prod>-situacion_txt = <fs_dd07v_situacion>-ddtext.
      ENDIF.
    ENDLOOP.

  ENDMETHOD.
  METHOD registrar_dispon_transportista.

    DATA(wl_transportista) = transportistas_getdetail( vp_lifnr ).

    DATA(vl_timestamp_actual) = sy-datum && sy-uzeit.
    DATA(vl_timestamp_disp)   = vp_fecha_disp && vp_hora_disp.
    IF vl_timestamp_disp < vl_timestamp_actual.
      zcl_seis_odata_utils=>lanzar_excepcion( 'La fecha indicada es anterior a la fecha actual' ).
    ENDIF.

    IF wl_transportista-status <> c_status_transportista_libre.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Ya tiene transportes asignados. No puede indicar fecha de disponibilidad' ).
    ENDIF.
    DATA wl_zssdt071 TYPE zssdt071.
*    SELECT SINGLE *
*      INTO @DATA(wl_zssdt071)
*      FROM zssdt071
*      WHERE lifnr = @wl_transportista-lifnr.
*    IF sy-subrc = 0.
*      wl_zssdt071-aenam = sy-uname.
*      wl_zssdt071-aedat = sy-datum.
*      wl_zssdt071-aezet = sy-uzeit.
*    ELSE.
    wl_zssdt071-lifnr = wl_transportista-lifnr.
    wl_zssdt071-ernam = sy-uname.
    wl_zssdt071-erdat = sy-datum.
    wl_zssdt071-erzet = sy-uzeit.
*    ENDIF.

    wl_zssdt071-fecha_disp = vp_fecha_disp.
    wl_zssdt071-hora_disp = vp_hora_disp.

    MODIFY zssdt071 FROM wl_zssdt071.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Error al registrar disponibilidad' ).
    ENDIF.

  ENDMETHOD.
  method TRANSPORTES_GET_ENTITY.
    tratar_key_entity( EXPORTING io_tech_request_context = io_tech_request_context
                       IMPORTING wp_entity               = er_entity ).
    er_entity = transportes_getdetail( vp_tknum  = er_entity-tknum ).
  endmethod.
  METHOD transportes_get_entityset.

    DATA wl_entity LIKE LINE OF et_entityset.
    tratar_key_entityset( EXPORTING io_tech_request_context = io_tech_request_context
                          IMPORTING wp_entity               = wl_entity ).

    DATA: tl_tdlnr_rg TYPE zsd_t_tdlnr_rg.
    tl_tdlnr_rg = VALUE #( ( sign = 'I' option = 'EQ' low = wl_entity-tdlnr ) ).

    DATA(tl_filter_so) = io_tech_request_context->get_filter( )->get_filter_select_options( ).
    READ TABLE tl_filter_so ASSIGNING FIELD-SYMBOL(<fs_filter_so>) WITH KEY property = 'CLA_DESPACHADO'.
    IF sy-subrc = 0.
      et_entityset =  transportes_getlist( tp_tdlnr_rg       = tl_tdlnr_rg
                                           vp_cla_despachado = CONV #( <fs_filter_so>-select_options[ 1 ]-low ) ).

    ELSE.
      DATA tl_erdat_rg TYPE range_t_dats.
      tl_erdat_rg = VALUE #( ( sign = 'I' option = 'GE' low = sy-datum - 60 ) ).

      et_entityset =  transportes_getlist( tp_tdlnr_rg       = tl_tdlnr_rg
                                           tp_erdat_rg       = tl_erdat_rg ).
    ENDIF.



    zcl_seis_odata_utils=>tratar_entityset( EXPORTING io_tech_request_context  = io_tech_request_context
                                            CHANGING et_entityset = et_entityset ).
  ENDMETHOD.
  METHOD transportes_getdetail.

    SELECT SINGLE vttk~tknum, vttk~tdlnr, vttk~sttrg, vttk~signi AS zzmatricula, vttk~tpbez AS zzremolque, vttk~zzconductor,
                  vttk~dpreg, vttk~upreg, vttk~tplst, vttk~exti2,
                  vttk~dtdis, vttk~uzdis, vttk~dareg, vttk~uareg, vttk~dplbg, vttk~uplbg, vttk~dalbg, vttk~ualbg,
                  vttk~dplen, vttk~uplen, vttk~dalen, vttk~ualen, vttk~dpabf, vttk~upabf, vttk~dtabf, vttk~uzabf,
                  vttk~dptbg, vttk~uptbg, vttk~datbg, vttk~uatbg, vttk~dpten, vttk~upten, vttk~daten, vttk~uaten,
                  vttk~shtyp
      INTO CORRESPONDING FIELDS OF @wp_transporte
      FROM vttk
      WHERE vttk~tknum = @vp_tknum.
    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Transporte incorrecto' ).
    ENDIF.



    DATA tl_transportes LIKE TABLE OF wp_transporte.
    APPEND wp_transporte TO tl_transportes.
    transportes_getlist_ext( CHANGING tp_transportes = tl_transportes ).
    wp_transporte = tl_transportes[ 1 ].
  ENDMETHOD.
  METHOD transportes_getlist.

    DATA tl_sttrg_rg TYPE RANGE OF vttk-sttrg.

    IF vp_cla_despachado IS SUPPLIED.
      DATA(vl_sign)   = SWITCH #( vp_cla_despachado WHEN 'X' THEN 'I' ELSE 'E' ).
      DATA(vl_option) = SWITCH #( vp_cla_despachado WHEN 'X' THEN 'EQ' ELSE 'NE' ).
      vl_option = 'EQ'.
      tl_sttrg_rg = VALUE #( sign = vl_sign option = vl_option ( low = c_sttrg_fin_transporte ) ).
    ENDIF.

    DATA tl_tknum_vbeln_rg TYPE zsd_t_tknum_rg.
    IF tp_vbeln_rg IS NOT INITIAL.
      SELECT DISTINCT 'I' AS sign, 'EQ' AS option, tknum AS low
        INTO CORRESPONDING FIELDS OF TABLE @tl_tknum_vbeln_rg
        FROM vttp
        WHERE vbeln IN @tp_vbeln_rg.
    ENDIF.

    SELECT vttk~tknum, vttk~tdlnr, vttk~sttrg, vttk~signi AS zzmatricula, vttk~tpbez AS zzremolque, vttk~zzconductor,
           vttk~dpreg, vttk~upreg, vttk~tplst, vttk~exti2,
           vttk~dtdis, vttk~uzdis, vttk~dareg, vttk~uareg, vttk~dplbg, vttk~uplbg, vttk~dalbg, vttk~ualbg,
           vttk~dplen, vttk~uplen, vttk~dalen, vttk~ualen, vttk~dpabf, vttk~upabf, vttk~dtabf, vttk~uzabf,
           vttk~dptbg, vttk~uptbg, vttk~datbg, vttk~uatbg, vttk~dpten, vttk~upten, vttk~daten, vttk~uaten,
           vttk~shtyp, vttk~erdat
      INTO CORRESPONDING FIELDS OF TABLE @tp_transportes
      FROM vttk
      WHERE vttk~tknum IN @tp_tknum_rg AND
            vttk~tknum IN @tl_tknum_vbeln_rg AND
            vttk~tdlnr IN @tp_tdlnr_rg AND
            vttk~sttrg IN @tp_sttrg_rg AND
            vttk~sttrg IN @tl_sttrg_rg AND
            vttk~tplst IN @tp_tplst_rg AND
            vttk~exti2 IN @tp_exti2_rg AND
            vttk~erdat IN @tp_erdat_rg AND
            vttk~shtyp IN @tp_shtyp_rg AND
            vttk~dpreg IN @tp_dpreg_rg AND
            vttk~dtabf IN @tp_dtabf_rg AND
            ( vttk~sttrg <> @c_sttrg_fin_transporte OR vttk~dpten >= @vp_fec_ini_despachado ).


    transportes_getlist_ext( CHANGING tp_transportes = tp_transportes ).

    IF vp_cla_despachado IS SUPPLIED.
      SORT tp_transportes BY sttrg DESCENDING gltrp gluzp.
    ELSE.
      SORT tp_transportes BY gltrp gluzp.
    ENDIF.

  ENDMETHOD.
  METHOD transportes_getlist_ext.
    CHECK tp_transportes IS NOT INITIAL.

    DATA: tl_lines TYPE TABLE OF tline,
          wl_text  TYPE thead.

    DATA tl_kunnrs TYPE TABLE OF kna1-kunnr.
    DATA tl_name1s TYPE TABLE OF kna1-name1.
    DATA tl_ort01s TYPE TABLE OF kna1-ort01.
    DATA tl_bezeis TYPE TABLE OF t005u-bezei.


    DATA: tl_dd07v_situacion TYPE TABLE OF dd07v.
    CALL FUNCTION 'DD_DD07V_GET'
      EXPORTING
        domain_name = 'ZSSD_DO_SITUACION'
      TABLES
        dd07v_tab   = tl_dd07v_situacion.
    DATA: tl_dd07v_sttrg TYPE TABLE OF dd07v.
    CALL FUNCTION 'DD_DD07V_GET'
      EXPORTING
        domain_name = 'STTRG'
      TABLES
        dd07v_tab   = tl_dd07v_sttrg.





    "00	Sin planificar
    "03	Cerrada y sin fabricar
    "01	Planificado
    "04	Lanzado a máquina
    "02	Terminado
    DATA tl_situaciones TYPE TABLE OF zsd_trans_s_transportes-situacion.
    tl_situaciones = VALUE #( ( '00' ) ( '03' ) ( '01' ) ( '04' ) ( '02' ) ).


    "Destinatarios del transporte
    SELECT vttp~tknum, likp~kunnr, likp~vkorg, kna1~name1, kna1~ort01, kna1~land1, kna1~regio, t005u~bezei
      INTO TABLE @DATA(tl_destinatarios)
      FROM likp INNER JOIN vttp ON vttp~vbeln = likp~vbeln
                INNER JOIN kna1 LEFT OUTER JOIN t005u ON t005u~spras = @sy-langu  AND
                                                         t005u~land1 = kna1~land1 AND
                                                         t005u~bland = kna1~regio
                ON kna1~kunnr = likp~kunnr
      FOR ALL ENTRIES IN @tp_transportes
      WHERE vttp~tknum = @tp_transportes-tknum.

    "Festivos
    IF tl_destinatarios IS NOT INITIAL.
      SELECT *
        INTO TABLE @DATA(tl_zssdt016calendar)
        FROM zssdt016calendar
        FOR ALL ENTRIES IN @tl_destinatarios
        WHERE vkorg = @tl_destinatarios-vkorg AND
              kunwe = @tl_destinatarios-kunnr.
    ENDIF.


    "Entregas
    SELECT vttp~tknum, likp~vbeln, likp~anzpk, likp~lfdat, likp~wadat
      INTO TABLE @DATA(tl_likp)
      FROM likp INNER JOIN vttp ON vttp~vbeln = likp~vbeln
      FOR ALL ENTRIES IN @tp_transportes
      WHERE vttp~tknum = @tp_transportes-tknum.


    "Lineas de entregas
    SELECT vttp~tknum, lips~vbeln, lips~posnr, lips~werks, lips~matnr, t320~lgnum, lips~lfimg, lips~vrkme
      INTO TABLE @DATA(tl_entregas_lin)
      FROM lips INNER JOIN vttp ON vttp~vbeln = lips~vbeln
                INNER JOIN t320 ON t320~werks = lips~werks
      FOR ALL ENTRIES IN @tp_transportes
      WHERE vttp~tknum = @tp_transportes-tknum.

    "Num. palets teóricos
    SELECT mlgn~matnr, mlgn~lhmg1, mara~meins, mlgn~lhme1
      INTO TABLE @DATA(tl_mlgn)
      FROM mlgn INNER JOIN mara ON mara~matnr = mlgn~matnr
      FOR ALL ENTRIES IN @tl_entregas_lin
      WHERE mlgn~matnr = @tl_entregas_lin-matnr AND
            mlgn~lgnum = @tl_entregas_lin-lgnum.



    "Ordenes
    DATA: tl_vbeln_rg TYPE RANGE OF likp-vbeln,
          wl_vbeln_rg LIKE LINE OF tl_vbeln_rg.
    LOOP AT tl_likp ASSIGNING FIELD-SYMBOL(<fs_likp>).
      wl_vbeln_rg = VALUE #( sign = 'I' option = 'EQ' low = <fs_likp>-vbeln ).
      COLLECT wl_vbeln_rg INTO tl_vbeln_rg.
    ENDLOOP.

    DATA(tl_ordenes_prod) = ordenes_prod_getlist( tp_vbeln_rg = tl_vbeln_rg ).
    SORT tl_ordenes_prod BY tknum ASCENDING gltrs DESCENDING gluzs DESCENDING gamng DESCENDING.


    SELECT shtyp, bezei
      INTO TABLE @DATA(tl_tvtkt)
      FROM tvtkt
      FOR ALL ENTRIES IN @tp_transportes
      WHERE spras = @sy-langu AND
            shtyp = @tp_transportes-shtyp.

    SELECT lifnr, name1, zzsigni, zzremolque
      INTO TABLE @DATA(tl_lfa1)
      FROM lfa1
      FOR ALL ENTRIES IN @tp_transportes
      WHERE lifnr = @tp_transportes-tdlnr.

    LOOP AT tp_transportes ASSIGNING FIELD-SYMBOL(<fs_transportes>).
      CASE <fs_transportes>-sttrg.
        WHEN c_sttrg_fin_transporte. "c_sttrg_despacho_exped OR c_sttrg_despacho_exped OR c_sttrg_fin_transporte.
          <fs_transportes>-cla_despachado = 'X'.
      ENDCASE.

      LOOP AT tl_likp ASSIGNING <fs_likp> WHERE tknum = <fs_transportes>-tknum.
        IF <fs_transportes>-lfdat IS INITIAL OR <fs_transportes>-lfdat > <fs_likp>-lfdat.
          <fs_transportes>-lfdat = <fs_likp>-lfdat.
        ENDIF.

        IF <fs_transportes>-wadat IS INITIAL OR <fs_transportes>-wadat > <fs_likp>-wadat.
          <fs_transportes>-wadat = <fs_likp>-wadat.
        ENDIF.

        "núm. palets
        IF <fs_transportes>-sttrg < c_sttrg_despacho_exped.
          LOOP AT tl_entregas_lin ASSIGNING FIELD-SYMBOL(<fs_entregas_lin>) WHERE vbeln = <fs_likp>-vbeln.
            READ TABLE tl_mlgn ASSIGNING FIELD-SYMBOL(<fs_mlgn>) WITH KEY matnr = <fs_entregas_lin>-matnr.
            IF sy-subrc = 0.
              DATA(vl_menge) =  <fs_entregas_lin>-lfimg.
              CALL FUNCTION 'MD_CONVERT_MATERIAL_UNIT'
                EXPORTING
                  i_matnr              = <fs_entregas_lin>-matnr
                  i_in_me              = <fs_entregas_lin>-vrkme
                  i_out_me             = <fs_mlgn>-lhme1
                  i_menge              = vl_menge
                IMPORTING
                  e_menge              = vl_menge
                EXCEPTIONS
                  error_in_application = 1
                  error                = 2
                  OTHERS               = 3.
              IF sy-subrc = 0.
                vl_menge = vl_menge / <fs_mlgn>-lhmg1.
                vl_menge = ceil( vl_menge ).
              ELSE.
                CLEAR vl_menge.
              ENDIF.

              ADD vl_menge TO <fs_transportes>-num_palets.
            ENDIF.
          ENDLOOP.

        ELSE.
          ADD <fs_likp>-anzpk TO <fs_transportes>-num_palets.
        ENDIF.
      ENDLOOP.


      LOOP AT tl_ordenes_prod ASSIGNING FIELD-SYMBOL(<fs_ordenes_prod>) WHERE tknum = <fs_transportes>-tknum.
        IF <fs_transportes>-aufnr IS INITIAL.
          <fs_transportes>-aufnr          = <fs_ordenes_prod>-aufnr.
          <fs_transportes>-gltrp          = <fs_ordenes_prod>-gltrp.
          <fs_transportes>-gluzp          = <fs_ordenes_prod>-gluzp.
          <fs_transportes>-gstrp          = <fs_ordenes_prod>-gstrp.
          <fs_transportes>-gsuzp          = <fs_ordenes_prod>-gsuzp.
          <fs_transportes>-gltrs          = <fs_ordenes_prod>-gltrs.
          <fs_transportes>-gluzs          = <fs_ordenes_prod>-gluzs.

          <fs_transportes>-situacion      = <fs_ordenes_prod>-situacion.
          <fs_transportes>-situacion_txt  = <fs_ordenes_prod>-situacion_txt.

        ELSE.
          "Comprobamos por el orden de la tabla si hay que indicar la situaciòn de esta orden
          READ TABLE tl_situaciones TRANSPORTING NO FIELDS WITH KEY table_line = <fs_ordenes_prod>-situacion.
          DATA(vl_tabix_situacion_act) = sy-tabix.
          READ TABLE tl_situaciones TRANSPORTING NO FIELDS WITH KEY table_line = <fs_transportes>-situacion.
          DATA(vl_tabix_situacion_ant) = sy-tabix.
          IF vl_tabix_situacion_act < vl_tabix_situacion_ant.
            <fs_transportes>-situacion      = <fs_ordenes_prod>-situacion.
            <fs_transportes>-situacion_txt  = <fs_ordenes_prod>-situacion_txt.
          ENDIF.
        ENDIF.
      ENDLOOP.

      READ TABLE tl_dd07v_sttrg ASSIGNING FIELD-SYMBOL(<fs_dd07v_sttrg>) WITH KEY domvalue_l = <fs_transportes>-sttrg.
      IF sy-subrc = 0.
        <fs_transportes>-sttrg_txt = <fs_dd07v_sttrg>-ddtext.
      ENDIF.


      CLEAR: tl_kunnrs, tl_ort01s, tl_name1s, tl_bezeis.
      LOOP AT tl_destinatarios ASSIGNING FIELD-SYMBOL(<fs_destinatarios>) WHERE tknum = <fs_transportes>-tknum.
        COLLECT <fs_destinatarios>-kunnr INTO tl_kunnrs.
        COLLECT <fs_destinatarios>-name1 INTO tl_name1s.
        COLLECT <fs_destinatarios>-ort01 INTO tl_ort01s.
        COLLECT <fs_destinatarios>-bezei INTO tl_bezeis.

        IF <fs_transportes>-kunnr_str IS INITIAL.
          <fs_transportes>-kunnr_str = <fs_destinatarios>-name1.
        ENDIF.

        IF <fs_transportes>-city1_str IS INITIAL.
          <fs_transportes>-city1_str = <fs_destinatarios>-ort01.
        ENDIF.


        LOOP AT tl_zssdt016calendar ASSIGNING FIELD-SYMBOL(<fs_zssdt016calendar>) WHERE vkorg = <fs_destinatarios>-vkorg AND
                                                                                        kunwe = <fs_destinatarios>-kunnr.
          IF <fs_transportes>-dpreg BETWEEN <fs_zssdt016calendar>-adatu AND <fs_zssdt016calendar>-bdatu.
            <fs_transportes>-cla_festivo = 'X'.
            EXIT.
          ENDIF.
        ENDLOOP.
      ENDLOOP.

      LOOP AT tl_kunnrs ASSIGNING FIELD-SYMBOL(<fs_kunnrs>).
        IF <fs_transportes>-lista_kunnr IS INITIAL.
          <fs_transportes>-lista_kunnr = <fs_kunnrs>.
        ELSE.
          <fs_transportes>-lista_kunnr = |{ <fs_transportes>-lista_kunnr }, { <fs_kunnrs> }|.
        ENDIF.
      ENDLOOP.

      LOOP AT tl_name1s ASSIGNING FIELD-SYMBOL(<fs_name1s>).
        IF <fs_transportes>-lista_nombres_clientes IS INITIAL.
          <fs_transportes>-lista_nombres_clientes = <fs_name1s>.
        ELSE.
          <fs_transportes>-lista_nombres_clientes = |{ <fs_transportes>-lista_nombres_clientes }, { <fs_name1s> }|.
        ENDIF.
      ENDLOOP.


      LOOP AT tl_ort01s ASSIGNING FIELD-SYMBOL(<fs_ort01s>).
        IF <fs_transportes>-lista_city1 IS INITIAL.
          <fs_transportes>-lista_city1 = <fs_ort01s>.
        ELSE.
          <fs_transportes>-lista_city1 = |{ <fs_transportes>-lista_city1 }, { <fs_ort01s> }|.
        ENDIF.
      ENDLOOP.

      LOOP AT tl_bezeis ASSIGNING FIELD-SYMBOL(<fs_bezeis>).
        IF <fs_transportes>-regiones IS INITIAL.
          <fs_transportes>-regiones = <fs_bezeis>.
        ELSE.
          <fs_transportes>-regiones = |{ <fs_transportes>-regiones }, { <fs_bezeis> }|.
        ENDIF.
      ENDLOOP.


      <fs_transportes>-num_destinos =  lines( tl_kunnrs ).
      DATA(vl_lines_kunnr) = lines( tl_kunnrs ) - 1.
      DATA(vl_lines_ort01) = lines( tl_ort01s ) - 1.
      IF vl_lines_kunnr > 0.
        <fs_transportes>-kunnr_str = |{ <fs_transportes>-kunnr_str }...({ vl_lines_kunnr } más)|.
      ENDIF.

      IF vl_lines_ort01 > 0.
        <fs_transportes>-city1_str = |{ <fs_transportes>-city1_str }...({ vl_lines_ort01 } más)|.
      ENDIF.


      wl_text-tdid     = '0001'.
      wl_text-tdspras  = 'S'.
      wl_text-tdname   = <fs_transportes>-tknum.
      wl_text-tdobject = 'VTTK'.

      CLEAR tl_lines.
      CALL FUNCTION 'READ_TEXT'
        EXPORTING
          id       = wl_text-tdid
          language = wl_text-tdspras
          name     = wl_text-tdname
          object   = wl_text-tdobject
        TABLES
          lines    = tl_lines
        EXCEPTIONS
          OTHERS   = 1.

      <fs_transportes>-omptx_str1           = convertir_tabla_texto_a_string( tl_lines ).
      <fs_transportes>-texto_transportista  = convertir_tabla_texto_a_string( tl_lines ).


      wl_text-tdid     = 'ZCCA'.
      wl_text-tdspras  = 'S'.
      wl_text-tdname   = <fs_transportes>-tknum.
      wl_text-tdobject = 'VTTK'.

      CLEAR tl_lines.
      CALL FUNCTION 'READ_TEXT'
        EXPORTING
          id       = wl_text-tdid
          language = wl_text-tdspras
          name     = wl_text-tdname
          object   = wl_text-tdobject
        TABLES
          lines    = tl_lines
        EXCEPTIONS
          OTHERS   = 1.

      <fs_transportes>-texto_transporte = convertir_tabla_texto_a_string( tl_lines ).


      READ TABLE tl_tvtkt ASSIGNING FIELD-SYMBOL(<fs_tvtkt>) WITH KEY shtyp = <fs_transportes>-shtyp.
      IF sy-subrc = 0.
        <fs_transportes>-bezei = <fs_tvtkt>-bezei.
      ENDIF.

      READ TABLE tl_lfa1 ASSIGNING FIELD-SYMBOL(<fs_lfa1>) WITH KEY lifnr = <fs_transportes>-tdlnr.
      IF sy-subrc = 0.
        <fs_transportes>-name1 = <fs_lfa1>-name1.

        IF <fs_transportes>-zzmatricula IS INITIAL.
          <fs_transportes>-zzmatricula = <fs_lfa1>-zzsigni.
        ENDIF.
        IF <fs_transportes>-zzremolque IS INITIAL.
          <fs_transportes>-zzremolque = <fs_lfa1>-zzremolque.
        ENDIF.
        IF <fs_transportes>-zzconductor IS INITIAL.
          <fs_transportes>-zzconductor = <fs_lfa1>-name1.
        ENDIF.

      ENDIF.

    ENDLOOP.






  ENDMETHOD.
  METHOD transportistas_get_entity.
    tratar_key_entity( EXPORTING io_tech_request_context = io_tech_request_context
                       IMPORTING wp_entity               = er_entity ).
    er_entity = transportistas_getdetail( vp_lifnr  = er_entity-lifnr ).
  ENDMETHOD.
  method TRANSPORTISTAS_GET_ENTITYSET.

    DATA wl_entity LIKE LINE OF et_entityset.
    tratar_key_entityset( EXPORTING io_tech_request_context = io_tech_request_context
                          IMPORTING wp_entity               = wl_entity ).

    et_entityset =  transportistas_getlist(  ).

    zcl_seis_odata_utils=>tratar_entityset( EXPORTING io_tech_request_context  = io_tech_request_context
                                            CHANGING et_entityset = et_entityset ).
  endmethod.
  METHOD transportistas_getdetail.

    SELECT SINGLE lfa1~lifnr, lfa1~name1, lfa1~zzadd_info AS add04, lfa1~zzsigni AS signi, lfa1~zzremolque AS tregnb,
                  lfb1~bukrs
      INTO CORRESPONDING FIELDS OF @wp_transportista
      FROM lfa1 left OUTER JOIN lfb1 ON lfb1~lifnr = lfa1~lifnr
      WHERE lfa1~lifnr = @vp_lifnr.


    IF sy-subrc <> 0.
      zcl_seis_odata_utils=>lanzar_excepcion( 'Transportista incorrecto' ).
    ENDIF.


    DATA tl_transportistas LIKE TABLE OF wp_transportista.
    APPEND wp_transportista TO tl_transportistas.
    transportistas_getlist_ext( CHANGING tp_transportistas = tl_transportistas ).
    wp_transportista = tl_transportistas[ 1 ].

  ENDMETHOD.
  METHOD transportistas_getlist.



    SELECT lfa1~lifnr, lfa1~name1, lfa1~zzadd_info AS add04, lfa1~zzsigni AS signi, lfa1~zzremolque AS tregnb,
           lfb1~bukrs
      INTO CORRESPONDING FIELDS OF TABLE @tp_transportistas
      FROM lfa1 INNER JOIN lfb1 ON lfb1~lifnr = lfa1~lifnr
      WHERE lfa1~lifnr IN @tp_lifnr_rg AND
            lfb1~bukrs IN @tp_bukrs_rg AND
            lfa1~ktokk IN ('ZTRA', 'ZPRO').


    "Añadimos los transportistas que no datos de sociedad
    SELECT lfa1~lifnr, lfa1~name1, lfa1~zzadd_info AS add04, lfa1~zzsigni AS signi, lfa1~zzremolque AS tregnb
      APPENDING CORRESPONDING FIELDS OF TABLE @tp_transportistas
      FROM lfa1
      WHERE lfa1~lifnr IN @tp_lifnr_rg AND
            lfa1~ktokk IN ('ZTRA', 'ZPRO')  AND
            NOT EXISTS ( SELECT * FROM lfb1 WHERE lfb1~lifnr = lfa1~lifnr ).





    transportistas_getlist_ext( CHANGING tp_transportistas = tp_transportistas ).
  ENDMETHOD.
  METHOD transportistas_getlist_ext.
    CHECK tp_transportistas IS NOT INITIAL.

    DATA tl_sttrg_rg TYPE RANGE OF vttk-sttrg.
    tl_sttrg_rg = VALUE #( sign = 'E' option = 'EQ' ( low = c_sttrg_fin_transporte ) ).

    SELECT vttk~tknum, vttk~tdlnr, vttk~sttrg
      INTO TABLE @DATA(tl_vttk_vivos)
      FROM vttk
      FOR ALL ENTRIES IN @tp_transportistas
      WHERE vttk~tdlnr = @tp_transportistas-lifnr AND
            vttk~sttrg IN @tl_sttrg_rg.


    DATA vl_fec_ini_despachado TYPE d.
    vl_fec_ini_despachado = sy-datum - 3.
    tl_sttrg_rg = VALUE #( sign = 'I' option = 'EQ' ( low = c_sttrg_fin_transporte ) ).
    SELECT vttk~tknum, vttk~tdlnr, vttk~daten, vttk~uaten
      INTO TABLE @DATA(tl_vttk_despachados)
      FROM vttk
      FOR ALL ENTRIES IN @tp_transportistas
      WHERE vttk~tdlnr = @tp_transportistas-lifnr AND
            vttk~sttrg IN @tl_sttrg_rg            AND
            vttk~dpten >= @vl_fec_ini_despachado.
    SORT tl_vttk_despachados BY tdlnr ASCENDING daten DESCENDING uaten DESCENDING.




    SELECT domname, domvalue_l, ddtext
      INTO TABLE @DATA(tl_dd07t_status_trans)
      FROM dd07t
      WHERE domname     = 'ZSSD_STATUS_TRANSPORTISTA' AND
            ddlanguage  = @sy-langu.

    LOOP AT tp_transportistas ASSIGNING FIELD-SYMBOL(<fs_transportistas>).
      READ TABLE tl_vttk_vivos TRANSPORTING NO FIELDS WITH KEY tdlnr = <fs_transportistas>-lifnr
                                                               sttrg = c_sttrg_inicio_transporte.
      IF sy-subrc = 0.
        <fs_transportistas>-status = c_status_transportista_en_ruta.
      ELSE.
        READ TABLE tl_vttk_vivos TRANSPORTING NO FIELDS WITH KEY tdlnr = <fs_transportistas>-lifnr.
        IF sy-subrc = 0.
          <fs_transportistas>-status = c_status_transportista_asig.
        ENDIF.
      ENDIF.

      READ TABLE tl_dd07t_status_trans ASSIGNING FIELD-SYMBOL(<fs_dd07t_status_trans>) WITH KEY domvalue_l = <fs_transportistas>-status.
      IF sy-subrc = 0.
        <fs_transportistas>-status_txt = <fs_dd07t_status_trans>-ddtext.
      ENDIF.


      IF <fs_transportistas>-status <> c_status_transportista_en_ruta.
        SELECT lifnr, fecha_disp, hora_disp
          INTO TABLE @DATA(tl_zssdt071)
          FROM zssdt071
          UP TO 1 ROWS
          WHERE lifnr = @<fs_transportistas>-lifnr
          ORDER BY fecha_disp DESCENDING, hora_disp DESCENDING.
        IF sy-subrc = 0.
          <fs_transportistas>-fecha_disp  = tl_zssdt071[ 1 ]-fecha_disp.
          <fs_transportistas>-hora_disp   = tl_zssdt071[ 1 ]-hora_disp.
        ENDIF.
      ENDIF.

      IF <fs_transportistas>-status IS INITIAL.
        READ TABLE tl_vttk_despachados ASSIGNING FIELD-SYMBOL(<fs_vttk_despachados>)
          WITH KEY tdlnr = <fs_transportistas>-lifnr.
        IF sy-subrc = 0.
          <fs_transportistas>-tknum_ult_desp = <fs_vttk_despachados>-tknum.
          <fs_transportistas>-daten_ult_desp = <fs_vttk_despachados>-daten.
          <fs_transportistas>-uaten_ult_desp = <fs_vttk_despachados>-uaten.
        ENDIF.

      ENDIF.
    ENDLOOP.

  ENDMETHOD.
  METHOD TRATAR_KEY_ENTITY.

    DATA(vl_source_entity_type_name)  = io_tech_request_context->get_source_entity_type_name( ).
    DATA(vl_entity_type_name)         = io_tech_request_context->get_entity_type_name( ).


    CASE vl_source_entity_type_name.
*      WHEN 'Estaciones'.
*        DATA wl_estaciones TYPE zui5_wm_s_estaciones.
*        io_tech_request_context->get_converted_source_keys( IMPORTING es_key_values  = wl_estaciones ).
*        MOVE-CORRESPONDING wl_estaciones TO wp_entity.

      WHEN OTHERS.
        io_tech_request_context->get_converted_keys( IMPORTING es_key_values  = wp_entity ).
    ENDCASE.


  ENDMETHOD.
 METHOD tratar_key_entityset.
   DATA(vl_source_entity_type_name)  = io_tech_request_context->get_source_entity_type_name( ).
   DATA(vl_entity_type_name)         = io_tech_request_context->get_entity_type_name( ).

   CASE vl_source_entity_type_name.
     WHEN 'Transportistas'.
       DATA wl_transportista TYPE zsd_trans_s_transportistas.
       io_tech_request_context->get_converted_source_keys( IMPORTING es_key_values  = wl_transportista ).
       MOVE-CORRESPONDING wl_transportista TO wp_entity.

       IF vl_entity_type_name = 'Transportes'.
         ASSIGN COMPONENT 'TDLNR' OF STRUCTURE wp_entity TO FIELD-SYMBOL(<fs_tdlnr>).
         <fs_tdlnr> = wl_transportista-lifnr.
       ENDIF.

     WHEN OTHERS.
       io_tech_request_context->get_converted_source_keys( IMPORTING es_key_values  = wp_entity ).
   ENDCASE.
 ENDMETHOD.
