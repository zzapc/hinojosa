*&---------------------------------------------------------------------*
*&  Include           ZSSDD001_FORM_ALV
*&---------------------------------------------------------------------*


*&--------------------------------------------------------------------
*&      Form  load_data_into_grid
*&--------------------------------------------------------------------
FORM load_data_into_grid TABLES tabla       TYPE STANDARD TABLE
                         USING contenedor  TYPE REF TO cl_gui_custom_container
                               nombre_cont TYPE        scrfname
                               variant     LIKE        disvariant
                               estruc      LIKE        dd02l-tabname
                     CHANGING  alv         TYPE REF TO cl_gui_alv_grid
                               layout      TYPE        lvc_s_layo
                               it_fieldcat TYPE        lvc_t_fcat
                               excludin    TYPE        ui_functions.

  IF alv IS INITIAL.
    IF cl_gui_alv_grid=>offline( ) IS INITIAL.

      "CREATE OBJECT eventos.

      CREATE OBJECT contenedor
        EXPORTING
          container_name = nombre_cont
        EXCEPTIONS
          OTHERS         = 1.

      CREATE OBJECT alv
        EXPORTING
          i_parent = contenedor
        EXCEPTIONS
          OTHERS   = 1.

      CALL METHOD alv->register_edit_event
        EXPORTING
          i_event_id = cl_gui_alv_grid=>mc_evt_modified
        EXCEPTIONS
          error      = 1
          OTHERS     = 2.
      IF sy-subrc <> 0.
        MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
                   WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
      ENDIF.

      CREATE OBJECT gr_eventos.
      SET HANDLER gr_eventos->handle_hotspot_click  FOR alv.

    ELSE.

*      CREATE OBJECT alv
*        EXPORTING
*          i_parent = or_doc.

    ENDIF.

    PERFORM preparefieldcatalog USING    estruc
                                CHANGING it_fieldcat.

    PERFORM preparelayout       CHANGING layout.
    PERFORM rellenar_toolbar    CHANGING excludin.

    "SET HANDLER eventos->hotspot_click         FOR alv.

    CALL METHOD alv->set_table_for_first_display
      EXPORTING
        is_variant           = variant
        i_save               = 'A'
        is_layout            = layout
        it_toolbar_excluding = excludin
      CHANGING
        it_outtab            = tabla[]
        it_fieldcatalog      = it_fieldcat
      EXCEPTIONS
        OTHERS               = 1.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
                 WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    ENDIF.
  ELSE.
    DATA: BEGIN OF ls_stable,
            row TYPE c,
            col TYPE c,
          END OF ls_stable.

    ls_stable-row = 'X'.
    ls_stable-col = 'X'.

    CALL METHOD alv->refresh_table_display
      EXPORTING
        is_stable = ls_stable.
  ENDIF.

ENDFORM.                    "load_data_into_grid

*&--------------------------------------------------------------------
*&      Form  preparefieldcatalog
*&--------------------------------------------------------------------
FORM preparefieldcatalog USING    estruc       LIKE dd02l-tabname
                         CHANGING it_fieldcat1 TYPE lvc_t_fcat.

  DATA: l_fieldcat TYPE LINE OF lvc_t_fcat.

  REFRESH it_fieldcat1.

  CALL FUNCTION 'LVC_FIELDCATALOG_MERGE'
    EXPORTING
      i_buffer_active        = 'X'
      i_structure_name       = estruc
      i_bypassing_buffer     = 'X'
    CHANGING
      ct_fieldcat            = it_fieldcat1[]
    EXCEPTIONS
      inconsistent_interface = 1
      program_error          = 2
      OTHERS                 = 3.

  LOOP AT it_fieldcat1 INTO l_fieldcat.
    CASE l_fieldcat-fieldname.
      WHEN 'VBELN'.
        l_fieldcat-hotspot = 'X'.
    ENDCASE.
    MODIFY it_fieldcat1 FROM l_fieldcat.
  ENDLOOP.

ENDFORM.                    "preparefieldcatalog

*&--------------------------------------------------------------------
*&      Form  preparelayout
*---------------------------------------------------------------------
FORM preparelayout CHANGING cwa_layout TYPE lvc_s_layo.

  cwa_layout-zebra      = 'X'.
  cwa_layout-sel_mode   = 'D'.
  cwa_layout-cwidth_opt = 'X'.
*  cwa_layout-ctab_fname = 'CELL_COLOR'.

ENDFORM.                    "preparelayout

*&--------------------------------------------------------------------
*&      Form  rellenar_toolbar
*&--------------------------------------------------------------------
FORM rellenar_toolbar CHANGING excluirtool TYPE ui_functions.

  REFRESH: excluirtool.
  APPEND cl_gui_alv_grid=>mc_fc_loc_append_row TO excluirtool.
  APPEND cl_gui_alv_grid=>mc_fc_loc_copy TO excluirtool.
  APPEND cl_gui_alv_grid=>mc_fc_loc_copy_row TO excluirtool.
  APPEND cl_gui_alv_grid=>mc_fc_loc_cut TO excluirtool.
  APPEND cl_gui_alv_grid=>mc_fc_loc_delete_row TO excluirtool.
  APPEND cl_gui_alv_grid=>mc_fc_loc_insert_row TO excluirtool.
  APPEND cl_gui_alv_grid=>mc_fc_loc_move_row TO excluirtool.
  APPEND cl_gui_alv_grid=>mc_fc_loc_paste TO excluirtool.
  APPEND cl_gui_alv_grid=>mc_fc_loc_paste_new_row TO excluirtool.
  APPEND cl_gui_alv_grid=>mc_fc_loc_undo TO excluirtool.
  APPEND cl_gui_alv_grid=>mc_fc_refresh TO excluirtool.

ENDFORM.                    "rellenar_toolbar

FORM filas_seleccionadas USING alv TYPE REF TO cl_gui_alv_grid
                      CHANGING pt_output_sel.
  TYPES: tt_lines like gt_output.
  CLEAR: pt_output_sel.

  CALL METHOD alv->get_selected_rows
    IMPORTING
      et_index_rows = DATA(it_rows).
  IF it_rows IS NOT INITIAL.
    pt_output_sel = VALUE tt_lines( FOR wa_indx IN it_rows ( gt_output[ wa_indx-index ] ) ).
  ENDIF.
ENDFORM.                    " filas_seleccionadas
