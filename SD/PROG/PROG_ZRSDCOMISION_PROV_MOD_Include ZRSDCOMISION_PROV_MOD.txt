*&---------------------------------------------------------------------*
*&  Include           ZRSDCOMISION_PROV_MOD
*&---------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      Module  STATUS_0100  OUTPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE status_0100 OUTPUT.

* Status y título de la dynpro
  SET PF-STATUS 'MAIN'.
  SET TITLEBAR 'MAIN'.

* Creamos los objetos la primera vez
  IF g_custom_container IS INITIAL.
*   Creamos el container principal
    CREATE OBJECT g_custom_container
      EXPORTING
        container_name = gc_container.

*   Creamos el splitter dentro del container con 2 filas
    CREATE OBJECT g_splitter
      EXPORTING
        parent  = g_custom_container
        rows    = 2
        columns = 1.

*   Sacamos el contenedor de arriba (cabecera HTML)
    CALL METHOD g_splitter->get_container
      EXPORTING
        row       = 1
        column    = 1
      RECEIVING
        container = g_container_1.

*   Sacamos el contenedor de abajo (ALV GRID)
    CALL METHOD g_splitter->get_container
      EXPORTING
        row       = 2
        column    = 1
      RECEIVING
        container = g_container_2.

*   Creamos el objeto de los textos de la cabecera
    CREATE OBJECT g_dd_doc
      EXPORTING
        style = 'ALV_GRID'.

*   Creamos el objeto para el ALV
    CREATE OBJECT g_grid
      EXPORTING
        i_parent = g_container_2.

*  Ajustamos la altura del splitter
    CALL METHOD g_splitter->set_row_height
      EXPORTING
        id     = 1
        height = 20.

*   Eventos
    CREATE OBJECT g_application.
    SET HANDLER g_application->handle_print_top_of_page FOR g_grid.
    SET HANDLER g_application->handle_hotspot_click     FOR g_grid.

*   Imprimimos la cabecera HTML
    PERFORM mostrar_html_header.
*   Mostramos el ALV
    PERFORM mostrar_alv.

* Refrescamos el ALV
  ELSE.
    PERFORM refrescar_alv.
  ENDIF.

ENDMODULE.                 " STATUS_0100  OUTPUT


*&---------------------------------------------------------------------*
*&      Module  USER_COMMAND_0100  INPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE user_command_0100 INPUT.

  CASE gv_okcode.

*   Volver
    WHEN 'BACK'.
      PERFORM exit.

*   Refrescar listado
    WHEN 'REFRESH'.
      PERFORM seleccionar_datos.
      PERFORM refrescar_alv.

*   Modificar comisionista
    WHEN 'MOD_REP'.
      PERFORM modificar_comisionista.

*   Modificar % de la comisión
    WHEN 'MOD_COM'.
      PERFORM modificar_porc_comision.

*   Borrar líneas
    WHEN 'DELETE'.
      PERFORM eliminar_comisiones.

*   Insertar comisión
    WHEN 'INSERT'.
      PERFORM insertar_comision.

*   Guardar comisiones -> Contabilizar provisiones manuales
    WHEN 'SAVE'.
      PERFORM contabilizar_provisiones.

  ENDCASE.
  CLEAR gv_okcode.

ENDMODULE.                 " USER_COMMAND_0100  INPUT
