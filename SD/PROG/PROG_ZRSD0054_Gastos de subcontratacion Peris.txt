***********************************************************************
* TIPO : LISTADO
* TITULO : Gastos de subcontratación Peris
* DESCRIPCION : Gastos de subcontratación Peris
*
* AUTOR: Andrés Picazo                                FECHA: 18/02/2025
* ANALISTA: Andréa Sanchez
*
***********************************************************************
REPORT zrsd0054.

*------TABLAS/ESTRUCTURAS----------------------------------------------*
TABLES: likp, lips, mvke, mara.

INCLUDE rsdbc1xx.
*------TABLAS INTERNAS-------------------------------------------------*
*----------------------------------------------------------------------*
* CLASS lcl_alv DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_alv DEFINITION INHERITING FROM zcl_alv FINAL.
  PUBLIC SECTION.
    METHODS handle_user_command REDEFINITION.
    METHODS visualizar_objeto   REDEFINITION.
ENDCLASS.


*----------------------------------------------------------------------*
*       CLASS zcl_report DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS zcl_report DEFINITION INHERITING FROM zcl_dev FINAL.
  PUBLIC SECTION.
    TYPES: BEGIN OF t_listado,
             check     TYPE xfeld,
             lights    TYPE zico_estado_mensaje,
             vbeln     TYPE vbeln_vl,
             kunag     TYPE likp-kunag,
             name_ag   TYPE name_ag,
             kunnr     TYPE likp-kunnr,
             name_we   TYPE name_we,
             posnr     TYPE posnr,
             matnr     TYPE matnr,
             arktx     TYPE arktx,
             charg     TYPE charg_d,
             werks     TYPE werks_d,
             lfimg     TYPE lfimg,
             vrkme     TYPE vrkme,
             wadat_ist TYPE wadat_ist,
             mtpos     TYPE mvke-mtpos,
             mtart     TYPE mara-mtart,
             kbetr     TYPE konp-kbetr,
             konwa     TYPE konp-konwa,
             kpein     TYPE konp-kpein,
             kmein     TYPE konp-kmein,
             valor     TYPE konp-kbetr,
             message   TYPE bapi_msg,
           END OF t_listado,
           tt_listado TYPE STANDARD TABLE OF t_listado.

    DATA: i_listado TYPE tt_listado,
          o_alv     TYPE REF TO lcl_alv ##NEEDED.

    METHODS  main.

    METHODS: listado,
      seleccionar_datos.

ENDCLASS.

*------VARIABLES-------------------------------------------------------*
DATA o_prog TYPE REF TO zcl_report.


*------PARAMETER/SELECT-OPTIONS EN PANTALLA----------------------------*
SELECTION-SCREEN BEGIN OF BLOCK b01 WITH FRAME TITLE text-sel.
SELECT-OPTIONS: s_vstel FOR likp-vstel DEFAULT 'P001',
                s_wadat FOR likp-wadat_ist,
                s_vbeln FOR likp-vbeln,
                s_matnr FOR lips-matnr.
SELECTION-SCREEN BEGIN OF BLOCK b02 WITH FRAME TITLE text-mat.
SELECT-OPTIONS: s_mtpos FOR mvke-mtpos DEFAULT 'ZSC1',
                s_mtart FOR mara-mtart DEFAULT 'ZPSE'.
SELECTION-SCREEN END OF BLOCK b02.
SELECTION-SCREEN SKIP 1.
PARAMETERS: p_mail  AS CHECKBOX USER-COMMAND mai,
            p_email TYPE text255 MODIF ID mai.
SELECTION-SCREEN SKIP 1.
PARAMETERS p_vari LIKE disvariant-variant.
SELECTION-SCREEN END OF BLOCK b01.
__botones_plantilla.



************************************************************************
*
* LOGICA DEL PROGRAMA
*
************************************************************************

*----------------------------------------------------------------------*
* CLASS lcl_alv IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_alv IMPLEMENTATION.
  METHOD visualizar_objeto.
    " TODO: variable is assigned but only used in commented-out code (ABAP cleaner)
    DATA l_list TYPE o_prog->t_listado.

    l_list = list.
    CASE column.
*      WHEN 'BUKRS'.
*        MESSAGE l_list-bukrs TYPE 'I'.
      WHEN OTHERS. message = 'No implementado'.
    ENDCASE.
  ENDMETHOD. " handle_double_click

  METHOD handle_user_command.
    check_ucomm_sel = 'F01'.

    super->handle_user_command( e_salv_function ).

    CASE ucomm.
      WHEN 'F01'.
        " TODO: variable is assigned but never used (ABAP cleaner)
        LOOP AT o_prog->i_listado ASSIGNING FIELD-SYMBOL(<listado>) WHERE check = 'X'.
* TO DO!
        ENDLOOP.
        IF sy-subrc = 0.
          refresh( ).
        ENDIF.
      WHEN OTHERS.
    ENDCASE.
  ENDMETHOD.
ENDCLASS.


*----------------------------------------------------------------------*
*       CLASS zcl_report IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS zcl_report IMPLEMENTATION.
  METHOD main.

    seleccionar_datos( ).
    listado( ).

    IF p_mail = 'X'.
      zcl_ap_envio_mail=>mail( EXPORTING direccion = p_email
                                         subject = 'Gastos subcontratación Peris'
                                         o_alv_origen = o_alv
                                         conversion_sap = 'Y'
                                         nombre_fichero_tabla = 'gastos_subcontratacion_peris.xlsx'
                               IMPORTING message = DATA(l_msg)
                                 CHANGING i_tabla = i_listado ).
    ENDIF.

  ENDMETHOD.                    " REPORT

  METHOD seleccionar_datos.
    sgpi_texto( 'Seleccionando datos'(sda) ).
    SELECT likp~vbeln, likp~wadat_ist, likp~kunag, likp~kunnr,
           kna1_ag~name1 AS name_ag,
           kna1_we~name1 AS name_we,
           lips~posnr, lips~matnr, lips~arktx, lips~charg, lips~werks,
           lips~lfimg, lips~vrkme,
           mvke~mtpos, mara~mtart
      FROM likp JOIN lips ON lips~vbeln = likp~vbeln
                JOIN vbuk ON vbuk~vbeln = likp~vbeln
            LEFT OUTER JOIN kna1 AS kna1_ag ON kna1_ag~kunnr = likp~kunag
            LEFT OUTER JOIN kna1 AS kna1_we ON kna1_we~kunnr = likp~kunnr
            JOIN mara ON mara~matnr = lips~matnr
            JOIN mvke ON mvke~matnr = lips~matnr
                     AND mvke~vkorg = likp~vkorg
                     AND mvke~vtweg = lips~vtweg
      INTO CORRESPONDING FIELDS OF TABLE @i_listado
     WHERE vstel IN @s_vstel
       AND wadat_ist IN @s_wadat
       AND wbstk = 'C'
       AND likp~vbeln IN @s_vbeln
       AND lips~matnr IN @s_matnr
       AND ( mvke~mtpos IN @s_mtpos
          OR mara~mtart IN @s_mtart )
     ORDER BY lips~vbeln, lips~posnr.

    SORT i_listado BY vbeln matnr.


* Busco precios
    o_prog->o_sgpi->get_filas_tabla( i_listado[] ).
    LOOP AT i_listado ASSIGNING FIELD-SYMBOL(<listado>).
      sgpi_texto( texto1 = 'Procesando datos'(pda) cant_porc = 100 ).

      SELECT knumh FROM  a017
        INTO @DATA(l_knumh)
         UP TO 1 ROWS
             WHERE  kappl  = 'M'
             AND    kschl  = 'PB00'
             AND    lifnr  = '0000100944'
             AND    matnr  = @<listado>-matnr
             AND    werks  = @<listado>-werks
             AND    esokz  = '3'
             AND    datbi  >= @<listado>-wadat_ist
             AND    datab  <= @<listado>-wadat_ist
        ORDER BY datbi DESCENDING.
      ENDSELECT.
      IF sy-subrc = 0.
        SELECT kbetr konwa kpein kmein FROM konp
          INTO CORRESPONDING FIELDS OF <listado>
          UP TO 1 ROWS
          WHERE knumh = l_knumh
           AND loevm_ko = ''
         ORDER BY PRIMARY KEY.
        ENDSELECT.
        lips-lfimg = <listado>-lfimg.
        IF <listado>-kmein NE <listado>-vrkme.
          lips-lfimg = o_cache->get_mat( matnr = <listado>-matnr cantidad = <listado>-lfimg unidad_origen = <listado>-vrkme unidad_destino = <listado>-kmein ).
        ENDIF.
        IF <listado>-kpein NE 0.
          <listado>-valor = lips-lfimg * <listado>-kbetr / <listado>-kpein.
        ENDIF.
      ENDIF.

      set_status_list( EXPORTING message = <listado>-message criterio = 'V' CHANGING list = <listado> ).
    ENDLOOP.

    SORT i_listado.
  ENDMETHOD.

  METHOD listado.
    sgpi_texto( 'Generando informe'(gin) ).

    o_alv->add_button( button = 'F01' text = 'Excel'  icon = icon_xls ucomm = 'EXCEL' ).

    o_alv->set_layout( p_vari ).

    o_alv->set_top_of_page( ).

    o_alv->set_field_hotspot( campo = 'VBELN' auto = 'X' ).

    o_alv->set_field( campo = 'VBELN' op = 'KEY' ).
    o_alv->set_field_quitar( 'CHECK,LIGHTS,MESSAGE' ).
    o_alv->set_field_noout( 'MTART' ).

    o_alv->set_field_text( 'VALOR' ).
    o_alv->set_agregacion( 'VALOR' ).

    o_alv->set_orden( 'VBELN' ).
    o_alv->get_datos_layout( EXPORTING reordenar_tabla = 'X' tabla_ref = 'X' CHANGING t_tabla = i_listado ).
    o_alv->set_seleccion( CHANGING t_tabla = i_listado ).

    o_alv->show( ).
  ENDMETHOD.
ENDCLASS.

*----------------------------------------------------------------------*
* INITIALIZATION
*----------------------------------------------------------------------*
INITIALIZATION.
  o_prog = NEW #( status       = 'INICIO_DYN'
                  status_prog  = 'ZAP_STATUS'
                  no_param     = 'X'
                  guardar_logz = '' ).



  o_prog->o_alv = NEW #( status             = 'STANDARD_ALV_DYN'
                         status_prog        = 'ZAP_STATUS'
                         top_of_page_auto   = 'X'
                         top_of_page_titulo = 'X'
                         o_dev              = o_prog ).

  o_prog->initialization_i( CHANGING sscrfields = sscrfields ).
  IF o_prog->variante_forzada IS INITIAL.
    p_vari = o_prog->o_alv->get_default_layout( ).
  ENDIF.


AT SELECTION-SCREEN OUTPUT.
  o_prog->selection_screen_output( CHANGING current_scr = current_scr ).
  zcl_ap_dynpro=>screen_visible( group1 = 'MAI' variable = p_mail ).

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_vari.
  p_vari = o_prog->o_alv->get_f4_layout( ).

************************************************************************
* AT SELECTION-SCREEN.
************************************************************************
AT SELECTION-SCREEN.
  CASE sy-ucomm.
    WHEN 'ONLI'.
      o_prog->validar_seleccion_obligatoria( campos_or = 'S_VBELN,S_MATNR,S_WADAT' msgty = 'W' ).
    WHEN OTHERS.
      o_prog->at_selection( ).
  ENDCASE.


AT SELECTION-SCREEN ON EXIT-COMMAND.
  o_prog->at_selection( ).

*----------------------------------------------------------------------
* START-OF-SELECTION.
*----------------------------------------------------------------------*
START-OF-SELECTION.
  o_prog->main( ).
