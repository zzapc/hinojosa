*&---------------------------------------------------------------------*
*&  Include           YHP_SD_CREATE_POSNR_F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  GET_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form INIT_DATA .

  field-symbols <FS_VBAP> type VBAPVB.

  data: LT_VBAKKEY     type table of SALES_KEY,
        LT_VBAK        type table of VBAK,
        LT_VBAPVB      type table of VBAPVB,
        LT_VBEPVB      type table of VBEPVB,
        LT_VBKDVB      type table of VBKDVB,
        LT_VBPAVB      type table of VBPAVB,
        LT_SADRVB      type table of SADRVB,
        LT_KOMV        type table of KOMV,
        LT_FXTHEAD     type table of THEADVB,
        LT_FXTLINE     type table of BAPITEXTLI,
        LT_FXBAPITHEAD type table of BAPISDTEHD,
        LT_FXBAPITLINE type table of BAPITEXTLI,
        LT_BAPICUREFM  type table of BAPICUREFM,
        LT_BAPICUCFGM  type table of BAPICUCFGM,
        LT_BAPICUINSM  type table of BAPICUINSM,
        LT_BAPICUPRTM  type table of BAPICUPRTM,
        LT_BAPICUVALM  type table of BAPICUVALM,
        LT_BAPICUBLBM  type table of BAPICUBLBM,
        LT_BAPICUVKM   type table of BAPICUVKM,
        LT_BAPIRET2    type table of BAPIRET2,
        LT_VBAP        type table of VBAP,
        LS_VBAP        type VBAP,
        LS_ORDER_VIEW  type ORDER_VIEW.

  LT_VBAKKEY = value #( ( VBELN = P_VBELN ) ).

  LS_ORDER_VIEW = value #( HEADER     = ABAP_TRUE
                           ITEM       = ABAP_TRUE
                           SDSCHEDULE = ABAP_TRUE
                           BUSINESS   = ABAP_TRUE
                           PARTNER    = ABAP_TRUE
                           SDCOND     = ABAP_TRUE
                           CONFIGURE  = ABAP_TRUE ).

  call function 'SD_SALES_DOCUMENT_PREFETCH'
    exporting
      I_SALES_VIEW             = LS_ORDER_VIEW
      I_MEMORY_READ            = 'A'
      I_WITH_HEADER_CONDITIONS = 'X'
    tables
      I_VBAK_KEYTAB            = LT_VBAKKEY
      FXVBAK                   = LT_VBAK
      FXVBAP                   = LT_VBAPVB
      FXVBEP                   = LT_VBEPVB
      FXVBKD                   = LT_VBKDVB
      FXVBPA                   = LT_VBPAVB
      FXKOMV                   = GT_KOMV
      FXTHEAD                  = LT_FXTHEAD
      FXTLINE                  = LT_FXTLINE
      FCUREF                   = LT_BAPICUREFM
      FCUCFG                   = LT_BAPICUCFGM
      FCUINS                   = LT_BAPICUINSM
      FCUPRT                   = LT_BAPICUPRTM
      FCUVAL                   = LT_BAPICUVALM
      FCUBLB                   = LT_BAPICUBLBM
      FCUVK                    = LT_BAPICUVKM
      RETURN                   = LT_BAPIRET2.

  if LT_VBAK is initial.
    message 'Error: empty tables' type 'W'.
  else.
    GS_VBAK = LT_VBAK[ 1 ].
  endif.

  loop at LT_VBAPVB assigning <FS_VBAP>.
    if <FS_VBAP>-CUOBJ is initial.
      select  POSNR CUOBJ into corresponding fields of table LT_VBAP from VBAP where VBELN = <FS_VBAP>-VBELN.
    endif.
  endloop.
  if LT_VBAP[] is not initial.
    loop at LT_VBAPVB assigning <FS_VBAP>.
      read table LT_VBAP into LS_VBAP with key POSNR =  <FS_VBAP>-POSNR.
      if SY-SUBRC = 0.
        <FS_VBAP>-CUOBJ = LS_VBAP-CUOBJ.
      endif.
    endloop.
  endif.

  clear LT_VBAP. refresh LT_VBAP.

  GT_VBPA = LT_VBPAVB.
  GT_VBAP = LT_VBAPVB.
  GT_VBEP = LT_VBEPVB.
  GT_VBKD = LT_VBKDVB.

  call function 'MAP_INT_TO_EXT_STRUCTURE'
    tables
      FXTHEAD       = LT_FXTHEAD
      FXTLINE       = LT_FXTLINE
      FXBAPITHEAD   = LT_FXBAPITHEAD
      FXBAPITLINE   = LT_FXBAPITLINE
    exceptions
      ENTRY_MISSING = 0.

  loop at LT_FXBAPITHEAD into data(LS_FXBAPITHEAD).
    loop at LT_FXBAPITLINE into data(LS_FXBAPITLINE)
                           where APPLOBJECT = LS_FXBAPITHEAD-APPLOBJECT
                            and  TEXT_NAME  = LS_FXBAPITHEAD-TEXT_NAME
                            and  TEXT_ID    = LS_FXBAPITHEAD-TEXT_ID
                            and  LANGU      = LS_FXBAPITHEAD-LANGU
                            and  LANGU_ISO  = LS_FXBAPITHEAD-LANGU_ISO.
      append initial line to GT_TEXT  assigning field-symbol(<S_TEXT>).
      <S_TEXT> = corresponding #( base ( <S_TEXT> ) LS_FXBAPITHEAD mapping DOC_NUMBER = SD_DOC ).
      <S_TEXT> = corresponding #( base ( <S_TEXT> ) LS_FXBAPITLINE mapping TEXT_LINE  = LINE ).
    endloop.
  endloop.

  move-corresponding LT_BAPICUCFGM to GT_CFGS_REF.
  move-corresponding LT_BAPICUINSM to GT_CFGS_INST.
  move-corresponding LT_BAPICUPRTM to GT_CFGS_PART_OF.
  move-corresponding LT_BAPICUVALM to GT_CFGS_VALUE.
*    move-corresponding LT_BAPICUBLBM to ME->T_CFGS_BLOB.
*    move-corresponding LT_BAPICUVKM  to ME->T_CFGS_VK.
  move-corresponding LT_BAPICUREFM to GT_CFGS_REFINST.



endform.
*&---------------------------------------------------------------------*
*&      Form  GET_DATA_IN
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form GET_DATA_IN changing LS_BAPI_SAVE_IN type YS_BAPI_SAVE_IN.

  data: LS_VBKD type VBKD,
        LS_VBPA type VBPA,
        LS_VBAP type VBAP,
        LS_VBEP type VBEP.
  field-symbols: <S_BAPISDITM> type BAPISDITM,
                 <S_BAPIPARNR> type BAPIPARNR,
                 <S_BAPIPAREX> type BAPIPAREX,
                 <S_BAPISCHDL> type BAPISCHDL.

  LS_VBKD = value #( GT_VBKD[ POSNR = '000000' ] optional ).

  perform MAP_VBAK_TO_BAPISDHD1 using GS_VBAK
                                changing LS_BAPI_SAVE_IN-S_BAPISDHD1.
  perform MAP_VBKD_TO_BAPISDHD1 using LS_VBKD
                                changing  LS_BAPI_SAVE_IN-S_BAPISDHD1.
  loop at GT_VBPA into LS_VBPA.
    append initial line to LS_BAPI_SAVE_IN-T_BAPIPARNR assigning <S_BAPIPARNR>.
    perform MAP_VBPA_TO_BAPIPARNR using LS_VBPA
                                  changing <S_BAPIPARNR>.
  endloop.

  perform MAP_KONV_TO_BAPICOND tables GT_KOMV
                                      LS_BAPI_SAVE_IN-T_BAPICOND
                               using GS_VBAK.

  loop at GT_VBAP into LS_VBAP.
    append initial line to LS_BAPI_SAVE_IN-T_BAPISDITM  assigning <S_BAPISDITM>.
    perform MAP_VBAP_TO_BAPISDITM using LS_VBAP
                                  changing  <S_BAPISDITM> .
    clear LS_VBKD.
    LS_VBKD = value #( GT_VBKD[ POSNR = LS_VBAP-POSNR ] optional ).
    perform MAP_VBKD_TO_BAPISDITM using LS_VBKD
                                  changing <S_BAPISDITM>.

    append initial line to LS_BAPI_SAVE_IN-T_BAPIPAREX assigning <S_BAPIPAREX>.
    perform MAP_VBAP_TO_BAPIPAREX using LS_VBAP
                                  changing <S_BAPIPAREX>.
  endloop.

  loop at GT_VBEP into LS_VBEP.
    append initial line to LS_BAPI_SAVE_IN-T_BAPISCHDL assigning <S_BAPISCHDL>.
    perform MAP_VBEP_TO_BAPISCHDL using LS_VBEP
                                  changing <S_BAPISCHDL>.
  endloop.


  LS_BAPI_SAVE_IN-T_BAPISDTEXT = GT_TEXT.

  LS_BAPI_SAVE_IN-T_BAPICUCFG = GT_CFGS_REF.
  LS_BAPI_SAVE_IN-T_BAPICUINS = GT_CFGS_INST.
  LS_BAPI_SAVE_IN-T_BAPICUPRT = GT_CFGS_PART_OF.
  LS_BAPI_SAVE_IN-T_BAPICUVAL = GT_CFGS_VALUE.
*   LS_BAPI_SAVE_IN-T_BAPICUBLB = GT_CFGS_BLOB.
*   LS_BAPI_SAVE_IN-T_BAPICUVK  = GT_CFGS_VK.
  LS_BAPI_SAVE_IN-T_BAPICUREF = GT_CFGS_REFINST.

endform.
*&---------------------------------------------------------------------*
*&      Form  ENQUEUE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form ENQUEUE .

  check GS_VBAK-VBELN is not initial.

  call function 'ENQUEUE_EVVBAKE'
    exporting
      MODE_VBAK      = 'E'
      VBELN          = GS_VBAK-VBELN
      _SCOPE         = '1'
      _WAIT          = 'X'
    exceptions
      FOREIGN_LOCK   = 1
      SYSTEM_FAILURE = 2.

  if SY-SUBRC is not initial.
    /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( exporting IF_MSGTY = 'E'
                                                    IF_MSGID = 'V1'
                                                    IF_MSGNO = '042'
                                                    IF_MSGV1 = GS_VBAK-VBELN
                                                    IF_MSGV2 = SY-MSGV1 ).
  endif.

endform.
*&---------------------------------------------------------------------*
*&      Form  DEQUEUE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form DEQUEUE .

  check GS_VBAK-VBELN is not initial.

  call function 'DEQUEUE_EVVBAKE'
    exporting
      VBELN = GS_VBAK-VBELN.


endform.
*&---------------------------------------------------------------------*
*&      Form  SAVE_BAPI
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form SAVE_BAPI .

  constants: LC_TRUE  type SAP_BOOL value 'X'.

  data: LR_TABLE          type ref to CL_SALV_TABLE,
        LS_BAPIRET2       type BAPIRET2,
        LS_BAPIRET2_ERROR type BAPIRET2,
        LV_GVCIN          type /VCXI/VKCR_GVCIN,
        LV_CAD_ID         type /VCXI/CKX_ID,
        LV_GUID           type /VCXI/CKX_GUID,
        LO_PARENT         type ref to /VCXI/CL_VKSRA_SVC_ELEMENT,
        LO_SVC_INSTANCE   type ref to /VCXI/CL_VKSRA_SVC,
        LO_SPVCE_CAD      type ref to /VCXI/CL_VKSR_RUELMNT,
        LO_CAD            type ref to   /VCXI/IF_CKX_OBJECT,
        "LO_CAD_PR         type ref to YCL_HP_SVCS_PR_CADIF_PRJ,
        lo_bob_sdoc type ref to YCL_HP_SD_BOB_SDOC,
        LO_CAD_EA         type ref to ZCL_VCXI_PXSS_EA_CADIF_PRJ,
        LO_GATE           type ref to /VCXI/CL_VKSR_GATE,
        LT_SVC_ELEMENT    type /VCXI/CKX_TT_OBJECT,
        LV_fREVR          type ZVCXI_PXS_FREVR.

  call function 'CONFIGURATION_INITIALIZER'.
  call function 'SD_SALES_DOCUMENT_INIT'.

  export P_POSNR to memory id 'Y_MEM_NEWPOS'.
  set parameter id 'Y_PAR_NEWPOS' field P_POSNR.

  call function 'SD_SALESDOCUMENT_CHANGE'
    exporting
      SALESDOCUMENT       = GS_VBAK-VBELN
      ORDER_HEADER_IN     = GS_BAPI_SAVE_IN-S_BAPISDHD1
      ORDER_HEADER_INX    = GS_BAPI_SAVE_INX-S_BAPISDHD1X
      "SIMULATION          = IF_TESTRUN
      CALL_FROM_BAPI      = 'X'
      BEHAVE_WHEN_ERROR   = 'P'
      LOGIC_SWITCH        = value BAPISDLS( PRICING = 'C' )
    importing
      SALES_HEADER_OUT    = GS_BAPI_SAVE_RES-S_BAPISDHD
      SALES_HEADER_STATUS = GS_BAPI_SAVE_RES-HEADER_STATUS
    tables
      RETURN              = GS_BAPI_SAVE_RES-T_BAPIRET2
      ITEM_IN             = GS_BAPI_SAVE_IN-T_BAPISDITM
      ITEM_INX            = GS_BAPI_SAVE_INX-T_BAPISDITMX
      SCHEDULE_IN         = GS_BAPI_SAVE_IN-T_BAPISCHDL
      SCHEDULE_INX        = GS_BAPI_SAVE_INX-T_BAPISCHDLX
*     PARTNERS            = GS_BAPI_SAVE_IN-T_BAPIPARNR
*     PARTNERCHANGES      = GS_BAPI_SAVE_INX-T_BAPIPARNRC
      CONDITIONS_IN       = GS_BAPI_SAVE_IN-T_BAPICOND
      CONDITIONS_INX      = GS_BAPI_SAVE_INX-T_BAPICONDX
      SALES_TEXT          = GS_BAPI_SAVE_IN-T_BAPISDTEXT
      SALES_CFGS_REF      = GS_BAPI_SAVE_IN-T_BAPICUCFG
      SALES_CFGS_INST     = GS_BAPI_SAVE_IN-T_BAPICUINS
      SALES_CFGS_PART_OF  = GS_BAPI_SAVE_IN-T_BAPICUPRT
      SALES_CFGS_VALUE    = GS_BAPI_SAVE_IN-T_BAPICUVAL
      SALES_CFGS_BLOB     = GS_BAPI_SAVE_IN-T_BAPICUBLB
      SALES_CFGS_VK       = GS_BAPI_SAVE_IN-T_BAPICUVK
      SALES_CFGS_REFINST  = GS_BAPI_SAVE_IN-T_BAPICUREF
      EXTENSIONIN         = GS_BAPI_SAVE_IN-T_BAPIPAREX
      ITEMS_EX            = GS_BAPI_SAVE_RES-T_BAPISDIT
      SCHEDULE_EX         = GS_BAPI_SAVE_RES-T_SCHEDULE
      BUSINESS_EX         = GS_BAPI_SAVE_RES-T_BUSINESS
      INCOMPLETE_LOG      = GS_BAPI_SAVE_RES-T_INCOMPLETE_LOG
      EXTENSIONEX         = GS_BAPI_SAVE_RES-T_EXTENSION
      CONDITIONS_EX       = GS_BAPI_SAVE_RES-T_BAPICOND
      PARTNERS_EX         = GS_BAPI_SAVE_RES-T_PARTNERS
      TEXTHEADERS_EX      = GS_BAPI_SAVE_RES-T_TEXTHEADERS
      TEXTLINES_EX        = GS_BAPI_SAVE_RES-T_TEXTLINES.


*** Initialize Buffer of Integration Data Number
  call function 'ZVCXI_XCC_IDAT_INIT_NIDAT'.

  try.
      CL_SALV_TABLE=>FACTORY(
        exporting
          LIST_DISPLAY = LC_TRUE
        importing
          R_SALV_TABLE = LR_TABLE
        changing
          T_TABLE      = GS_BAPI_SAVE_RES-T_BAPIRET2 ).
    catch CX_SALV_MSG.
  endtry.
  "LR_TABLE->DISPLAY( ).

  read table GS_BAPI_SAVE_RES-T_BAPIRET2 into LS_BAPIRET2_ERROR with key TYPE = 'A' binary search.
  if LS_BAPIRET2_ERROR is initial.
    clear LS_BAPIRET2_ERROR.
    read table GS_BAPI_SAVE_RES-T_BAPIRET2 into LS_BAPIRET2_ERROR with key TYPE = 'E'.
  endif.

  free memory id 'Y_MEM_NEWPOS'.

  data LV_CLEAR type POSNR value is initial.
  set parameter  id 'Y_PAR_NEWPOS' field LV_CLEAR.


  if LS_BAPIRET2_ERROR is not initial.

    call function 'POPUP_TO_INFORM'
      exporting
        TITEL = 'Error message'
        TXT1  = LS_BAPIRET2_ERROR-MESSAGE
        TXT2  = 'Click accept to continue.'.

  else.

    commit work.

    data: LT_VBAP type table of VBAP.
    field-symbols: <S_VBAP_OLD> type VBAP.
    select  *
      from VBAP
      into corresponding fields of table LT_VBAP
      where VBELN = P_VBELN and POSNR = P_POSNR.

    if SY-SUBRC = 0.
      assign LT_VBAP[ 1 ] to <S_VBAP_OLD>.

      <S_VBAP_OLD>-ABGRU = 'Z4'.

      LV_GVCIN = /VCXI/CL_VKCR_GATEWAY=>GET_GVCIN_BY_CUOBJ( IF_CUOBJ =  <S_VBAP_OLD>-CUOBJ ).
      LO_SVC_INSTANCE = /VCXI/CL_VKSRA_SVC=>GET_INSTANCE( IF_GVCIN = LV_GVCIN ).
      LV_CAD_ID = 'CADIF_PRJ'.
      LT_SVC_ELEMENT = LO_SVC_INSTANCE->GET_SVC_ELEMENTS( IF_ID = LV_CAD_ID ).
      LO_CAD = LT_SVC_ELEMENT[ 1 ].
      LV_GUID = LO_CAD->GET_GUID( ).
      LO_SPVCE_CAD ?= /VCXI/CL_VKSR_RUELMNT=>/VCXI/IF_CKX_OBJECT~GET_INSTANCE( IF_GUID = LV_GUID ).
      create object LO_GATE exporting IR_RUELMNT = LO_SPVCE_CAD.
      create object LO_CAD_EA exporting IR_GATE = LO_GATE.
      concatenate P_VBELN P_POSNR into LV_fREVR.
      LO_CAD_EA->SET_FREVR( IF_FREVR = LV_fREVR ).

      commit work.
      wait up to 5 seconds.

      modify VBAP from table LT_VBAP.
      if SY-SUBRC = 0.
        commit work.
        wait up to 5 seconds.
      endif.

    endif.

*    call function 'BAPI_TRANSACTION_COMMIT'
*      exporting
*        WAIT = 'X'.


    create object lo_bob_sdoc.
    lo_bob_sdoc->init_from_db( if_vbeln = p_vbeln ).
    lo_bob_sdoc->save( if_testrun = abap_true ).


    perform DEQUEUE.


    set parameter id 'VBELN' field GS_VBAK-VBELN.
    leave to transaction 'VA22' and skip first screen.

  endif.

  "call function 'BAPI_TRANSACTION_ROLLBACK'.


endform.
*&---------------------------------------------------------------------*
*&      Form  MAP_VBAK_TO_BAPISDHD1
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->IS_VBAK  text
*      <--CS_BAPI_SAVE_IN_S_BAPISDHD1  text
*----------------------------------------------------------------------*
form MAP_VBAK_TO_BAPISDHD1  using    IS_VBAK type VBAK
                            changing CS_BAPISDHD1 type BAPISDHD1.

  move IS_VBAK-AUART    to CS_BAPISDHD1-DOC_TYPE.
  move IS_VBAK-VBTYP    to CS_BAPISDHD1-SD_DOC_CAT.
  move IS_VBAK-SUBMI    to CS_BAPISDHD1-COLLECT_NO.
  move IS_VBAK-VKORG    to CS_BAPISDHD1-SALES_ORG.
  move IS_VBAK-VTWEG    to CS_BAPISDHD1-DISTR_CHAN.
  move IS_VBAK-SPART    to CS_BAPISDHD1-DIVISION.
  move IS_VBAK-VKGRP    to CS_BAPISDHD1-SALES_GRP.
  move IS_VBAK-VKBUR    to CS_BAPISDHD1-SALES_OFF.
  move IS_VBAK-VDATU    to CS_BAPISDHD1-REQ_DATE_H.
  move IS_VBAK-VPRGR    to CS_BAPISDHD1-DATE_TYPE.
  move IS_VBAK-BSTDK    to CS_BAPISDHD1-PURCH_DATE.
  move IS_VBAK-BSARK    to CS_BAPISDHD1-PO_METHOD.
  move IS_VBAK-BSTZD    to CS_BAPISDHD1-PO_SUPPLEM.
  move IS_VBAK-IHREZ    to CS_BAPISDHD1-REF_1.
  move IS_VBAK-BNAME    to CS_BAPISDHD1-NAME.
  move IS_VBAK-TELF1    to CS_BAPISDHD1-TELEPHONE.
  move IS_VBAK-LIFSK    to CS_BAPISDHD1-DLV_BLOCK.
  move IS_VBAK-FAKSK    to CS_BAPISDHD1-BILL_BLOCK.
  move IS_VBAK-AUGRU    to CS_BAPISDHD1-ORD_REASON.
  move IS_VBAK-AUTLF    to CS_BAPISDHD1-COMPL_DLV.
  move IS_VBAK-ANGDT    to CS_BAPISDHD1-QT_VALID_F.
  move IS_VBAK-BNDDT    to CS_BAPISDHD1-QT_VALID_T.
  move IS_VBAK-GUEBG    to CS_BAPISDHD1-CT_VALID_F.
  move IS_VBAK-GUEEN    to CS_BAPISDHD1-CT_VALID_T.
  move IS_VBAK-KVGR1    to CS_BAPISDHD1-CUST_GRP1.
  move IS_VBAK-KVGR2    to CS_BAPISDHD1-CUST_GRP2.
  move IS_VBAK-KVGR3    to CS_BAPISDHD1-CUST_GRP3.
  move IS_VBAK-KVGR4    to CS_BAPISDHD1-CUST_GRP4.
  move IS_VBAK-KVGR5    to CS_BAPISDHD1-CUST_GRP5.
  move IS_VBAK-AUART    to CS_BAPISDHD1-SD_DOC_CAT.
  move IS_VBAK-AUDAT    to CS_BAPISDHD1-DOC_DATE.
  move IS_VBAK-GWLDT    to CS_BAPISDHD1-WAR_DATE.
  move IS_VBAK-VSBED    to CS_BAPISDHD1-SHIP_COND.
  move IS_VBAK-KTEXT    to CS_BAPISDHD1-PP_SEARCH.
  move IS_VBAK-MAHZA    to CS_BAPISDHD1-DUN_COUNT.
  move IS_VBAK-MAHDT    to CS_BAPISDHD1-DUN_DATE.

  if not IS_VBAK-PS_PSP_PNR is initial.
    call function 'CJPN_INTERN_TO_EXTERN_CONV'
      exporting
        INT_NUM   = IS_VBAK-PS_PSP_PNR
      importing
        EXT_NUM   = CS_BAPISDHD1-WBS_ELEM
      exceptions
        NOT_FOUND = 1
        others    = 2.
    if SY-SUBRC ne 0.
      clear CS_BAPISDHD1-WBS_ELEM.
      /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
    endif.
  endif.

  move IS_VBAK-ABRVW    to CS_BAPISDHD1-DLVSCHDUSE.
  move IS_VBAK-ABDIS    to CS_BAPISDHD1-PLDLVSTYP.
  move IS_VBAK-VGBEL    to CS_BAPISDHD1-REF_DOC.
  move IS_VBAK-BUKRS_VF to CS_BAPISDHD1-COMP_CDE_B.
  move IS_VBAK-TAXK1    to CS_BAPISDHD1-ALTTAX_CLS.
  move IS_VBAK-TAXK2    to CS_BAPISDHD1-TAX_CLASS2.
  move IS_VBAK-TAXK3    to CS_BAPISDHD1-TAX_CLASS3.
  move IS_VBAK-TAXK4    to CS_BAPISDHD1-TAX_CLASS4.
  move IS_VBAK-TAXK5    to CS_BAPISDHD1-TAX_CLASS5.
  move IS_VBAK-TAXK6    to CS_BAPISDHD1-TAX_CLASS6.
  move IS_VBAK-TAXK7    to CS_BAPISDHD1-TAX_CLASS7.
  move IS_VBAK-TAXK8    to CS_BAPISDHD1-TAX_CLASS8.
  move IS_VBAK-TAXK9    to CS_BAPISDHD1-TAX_CLASS9.
  move IS_VBAK-XBLNR    to CS_BAPISDHD1-REF_DOC_L.
  move IS_VBAK-ZUONR    to CS_BAPISDHD1-ASS_NUMBER.
  move IS_VBAK-VGTYP    to CS_BAPISDHD1-REFDOC_CAT.

  if IS_VBAK-WAERK is not initial.
    call function 'CURRENCY_CODE_SAP_TO_ISO'
      exporting
        SAP_CODE  = IS_VBAK-WAERK
      importing
        ISO_CODE  = CS_BAPISDHD1-CURR_ISO
      exceptions
        NOT_FOUND = 1
        others    = 2.
    if SY-SUBRC ne 0.
      clear CS_BAPISDHD1-CURR_ISO.
    endif.
  endif.

  move IS_VBAK-VBKLA    to CS_BAPISDHD1-DOC_CLASS.

  if not IS_VBAK-XBLNR is initial.
    call function 'REF_DOC_NO_CONVERSION_OUTBOUND'
      exporting
        I_REF_DOC_NO_LONG = IS_VBAK-XBLNR
      importing
        E_REF_DOC_NO_LONG = CS_BAPISDHD1-REF_DOC_L_LONG.
    if CS_BAPISDHD1-REF_DOC_L_LONG is initial.
      /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
    endif.
  endif.

  move IS_VBAK-VSNMR_V  to CS_BAPISDHD1-VERSION.

endform.
*&---------------------------------------------------------------------*
*&      Form  MAP_VBKD_TO_BAPISDHD1
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->IS_VBKD  text
*      <--CS_BAPI_SAVE_IN_S_BAPISDHD1  text
*----------------------------------------------------------------------*
form MAP_VBKD_TO_BAPISDHD1  using    IS_VBKD type VBKD
                            changing CS_BAPISDHD1 type BAPISDHD1.

  move IS_VBKD-BSTKD    to CS_BAPISDHD1-PURCH_NO_C.
  move IS_VBKD-KONDA    to CS_BAPISDHD1-PRICE_GRP.
  move IS_VBKD-KDGRP    to CS_BAPISDHD1-CUST_GROUP.
  move IS_VBKD-BZIRK    to CS_BAPISDHD1-SALES_DIST.
  move IS_VBKD-PLTYP    to CS_BAPISDHD1-PRICE_LIST.
  move IS_VBKD-INCO1    to CS_BAPISDHD1-INCOTERMS1.
  move IS_VBKD-INCO2    to CS_BAPISDHD1-INCOTERMS2.
  move IS_VBKD-ZTERM    to CS_BAPISDHD1-PMNTTRMS.
  move IS_VBKD-PRSDT    to CS_BAPISDHD1-PRICE_DATE.
  move IS_VBKD-BSTKD_E  to CS_BAPISDHD1-PURCH_NO_S.
  move IS_VBKD-BSTKD_E  to CS_BAPISDHD1-PO_DAT_S.
  move IS_VBKD-BSARK_E  to CS_BAPISDHD1-PO_METH_S.
  move IS_VBKD-IHREZ_E  to CS_BAPISDHD1-REF_1_S.
  move IS_VBKD-KZAZU    to CS_BAPISDHD1-ORDCOMB_IN.
  move IS_VBKD-PERFK    to CS_BAPISDHD1-BILL_SCHED.
  move IS_VBKD-PERRL    to CS_BAPISDHD1-INVO_SCHED.
  move IS_VBKD-MRNKZ    to CS_BAPISDHD1-MN_INVOICE.
  move IS_VBKD-KURRF    to CS_BAPISDHD1-EXRATE_FI.
  move IS_VBKD-VALTG    to CS_BAPISDHD1-ADD_VAL_DY.
  move IS_VBKD-VALDT    to CS_BAPISDHD1-FIX_VAL_DY.
  move IS_VBKD-ZLSCH    to CS_BAPISDHD1-PYMT_METH.
  move IS_VBKD-KTGRD    to CS_BAPISDHD1-ACCNT_ASGN.
  move IS_VBKD-KURSK    to CS_BAPISDHD1-EXCHG_RATE.
  move IS_VBKD-FKDAT    to CS_BAPISDHD1-BILL_DATE.
  move IS_VBKD-FBUDA    to CS_BAPISDHD1-SERV_DATE.
  move IS_VBKD-MSCHL    to CS_BAPISDHD1-DUNN_KEY.
  move IS_VBKD-MANSP    to CS_BAPISDHD1-DUNN_BLOCK.
  move IS_VBKD-ABSSC    to CS_BAPISDHD1-PMTGAR_PRO.
  move IS_VBKD-ABTNR    to CS_BAPISDHD1-DEPARTM_NO.
  move IS_VBKD-EMPST    to CS_BAPISDHD1-REC_POINT.
  move IS_VBKD-LCNUM    to CS_BAPISDHD1-DOC_NUM_FI.
  move IS_VBKD-KDKG1    to CS_BAPISDHD1-CSTCNDGRP1.
  move IS_VBKD-KDKG2    to CS_BAPISDHD1-CSTCNDGRP2.
  move IS_VBKD-KDKG3    to CS_BAPISDHD1-CSTCNDGRP3.
  move IS_VBKD-KDKG4    to CS_BAPISDHD1-CSTCNDGRP4.
  move IS_VBKD-KDKG5    to CS_BAPISDHD1-CSTCNDGRP5.
  move IS_VBKD-DELCO    to CS_BAPISDHD1-DLV_TIME.
  move IS_VBKD-VKONT    to CS_BAPISDHD1-FKK_CONACCT.
  move IS_VBKD-CAMPAIGN to CS_BAPISDHD1-CAMPAIGN.
  move IS_VBKD-VSART    to CS_BAPISDHD1-SHIP_TYPE.
  move IS_VBKD-SDABW    to CS_BAPISDHD1-S_PROC_IND.


endform.
*&---------------------------------------------------------------------*
*&      Form  MAP_VBPA_TO_BAPIPARNR
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->IS_VBPA  text
*      <--RS_BAPIPARNR  text
*----------------------------------------------------------------------*
form MAP_VBPA_TO_BAPIPARNR  using    IS_VBPA type VBPA
                            changing RS_BAPIPARNR type BAPIPARNR.

  data: LF_PARTN_NUMB type KUNNR.
  data: LS_VBADR type VBADR,
        LS_TPAR  type TPAR.

  if IS_VBPA-ADRNR is not initial.
    call function 'SD_ADDRESS_GET'
      exporting
        FIF_ADDRESS_NUMBER      = IS_VBPA-ADRNR
      importing
        FES_ADDRESS             = LS_VBADR
      exceptions
        ADDRESS_NOT_FOUND       = 1
        ADDRESS_TYPE_NOT_EXISTS = 2
        NO_PERSON_NUMBER        = 3
        others                  = 4.
    if SY-SUBRC ne 0.
      raise exception type /VCXI/CX_CKX.
    endif.
  endif.

  call function 'TPAR_SINGLE_READ'
    exporting
      I_PARVW         = IS_VBPA-PARVW
    importing
      O_TPAR          = LS_TPAR
    exceptions
      NOT_FOUND       = 1
      PARAMETER_ERROR = 2.
  if SY-SUBRC ne 0.
    raise exception type /VCXI/CX_CKX.
  endif.

  case LS_TPAR-NRART.
    when 'KU'.
      LF_PARTN_NUMB = IS_VBPA-KUNNR.
    when 'LI'.
      LF_PARTN_NUMB = IS_VBPA-LIFNR.
    when others.
      clear LF_PARTN_NUMB.
  endcase.


  move IS_VBPA-PARVW       to RS_BAPIPARNR-PARTN_ROLE.
  move LF_PARTN_NUMB         to RS_BAPIPARNR-PARTN_NUMB.
  move IS_VBPA-POSNR       to RS_BAPIPARNR-ITM_NUMBER.
  move LS_VBADR-ANRED        to RS_BAPIPARNR-TITLE.
  move LS_VBADR-NAME1        to RS_BAPIPARNR-NAME.
  move LS_VBADR-NAME2        to RS_BAPIPARNR-NAME_2.
  move LS_VBADR-NAME3        to RS_BAPIPARNR-NAME_3.
  move LS_VBADR-NAME4        to RS_BAPIPARNR-NAME_4.
  move LS_VBADR-STRAS        to RS_BAPIPARNR-STREET.
  move LS_VBADR-LAND1        to RS_BAPIPARNR-COUNTRY.
  if not LS_VBADR-LAND1 is initial.
    call function 'COUNTRY_CODE_SAP_TO_ISO'
      exporting
        SAP_CODE = LS_VBADR-LAND1
      importing
        ISO_CODE = RS_BAPIPARNR-COUNTR_ISO
      exceptions
        others   = 0.
  endif.
  move LS_VBADR-PSTLZ        to RS_BAPIPARNR-POSTL_CODE.
  move LS_VBADR-PSTL2        to RS_BAPIPARNR-POBX_PCD.
  move LS_VBADR-PFORT        to RS_BAPIPARNR-POBX_CTY.
  move LS_VBADR-ORT01        to RS_BAPIPARNR-CITY.
  move LS_VBADR-ORT02        to RS_BAPIPARNR-DISTRICT.
  move LS_VBADR-REGIO        to RS_BAPIPARNR-REGION.
  move LS_VBADR-PFACH        to RS_BAPIPARNR-PO_BOX.
  move LS_VBADR-TELF1        to RS_BAPIPARNR-TELEPHONE.
  move LS_VBADR-TELF2        to RS_BAPIPARNR-TELEPHONE2.
  move LS_VBADR-TELBX        to RS_BAPIPARNR-TELEBOX.
  move LS_VBADR-TELFX        to RS_BAPIPARNR-FAX_NUMBER.
  move LS_VBADR-TELTX        to RS_BAPIPARNR-TELETEX_NO.
  move LS_VBADR-TELX1        to RS_BAPIPARNR-TELEX_NO.
  move LS_VBADR-SPRAS        to RS_BAPIPARNR-LANGU.
  if not LS_VBADR-SPRAS is initial.
    call function 'CONVERSION_EXIT_ISOLA_OUTPUT'
      exporting
        INPUT  = LS_VBADR-SPRAS
      importing
        OUTPUT = RS_BAPIPARNR-LANGU_ISO.
  endif.
  move IS_VBPA-ABLAD       to RS_BAPIPARNR-UNLOAD_PT.
  move IS_VBPA-LZONE       to RS_BAPIPARNR-TRANSPZONE.
  move LS_VBADR-TXJCD        to RS_BAPIPARNR-TAXJURCODE.
  move LS_VBADR-ADRNR        to RS_BAPIPARNR-ADDRESS.
  move LS_VBADR-ADRNP        to RS_BAPIPARNR-PRIV_ADDR.
  move LS_VBADR-ADDRESS_TYPE to RS_BAPIPARNR-ADDR_TYPE.
endform.
*&---------------------------------------------------------------------*
*&      Form  MAP_KONV_TO_BAPICOND
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->DA_KOMV  text
*      -->CH_BAPICOND  text
*      -->DA_VBAK  text
*----------------------------------------------------------------------*
form MAP_KONV_TO_BAPICOND  tables   DA_KONV structure KONV
                                    CH_BAPICOND structure BAPICOND
                           using    DA_VBAK type VBAK.

  data: DA_WAER like  DA_KONV-WAERS.

* The coding is copied from Form MOVE_CONDITION_OUT of FuGr 2032.
  refresh CH_BAPICOND.

  loop at DA_KONV.

    if not DA_VBAK-WAERK is initial.
      DA_WAER       = DA_VBAK-WAERK.
    else.
      DA_WAER       = DA_KONV-WAERS.
    endif.

    clear CH_BAPICOND.
    move DA_KONV-KPOSN to CH_BAPICOND-ITM_NUMBER.
    move DA_KONV-STUNR to CH_BAPICOND-COND_ST_NO.
    move DA_KONV-ZAEHK to CH_BAPICOND-COND_COUNT.
    move DA_KONV-KSCHL to CH_BAPICOND-COND_TYPE.

    if not DA_KONV-KBETR is initial.
      clear CH_BAPICOND-COND_VALUE.
      if DA_KONV-KRECH ca 'AHI'. "percentage --> division by 10.
        call function 'BAPI_CURRENCY_CONV_TO_EXTERN_9'
          exporting
            CURRENCY        = '3'
            AMOUNT_INTERNAL = DA_KONV-KBETR
          importing
            AMOUNT_EXTERNAL = CH_BAPICOND-COND_VALUE.
      else.
        call function 'BAPI_CURRENCY_CONV_TO_EXTERN_9'
          exporting
            CURRENCY        = DA_KONV-WAERS
            AMOUNT_INTERNAL = DA_KONV-KBETR
          importing
            AMOUNT_EXTERNAL = CH_BAPICOND-COND_VALUE.
      endif.
    endif.

    move DA_KONV-WAERS to CH_BAPICOND-CURRENCY.
* convert currency to iso-code
    call function 'CURRENCY_CODE_SAP_TO_ISO'
      exporting
        SAP_CODE  = DA_KONV-WAERS
      importing
        ISO_CODE  = CH_BAPICOND-CURR_ISO
      exceptions
        NOT_FOUND = 01.

    move DA_KONV-KMEIN to CH_BAPICOND-COND_UNIT.
    call function 'UNIT_OF_MEASURE_SAP_TO_ISO'
      exporting
        SAP_CODE    = DA_KONV-KMEIN
      importing
        ISO_CODE    = CH_BAPICOND-CD_UNT_ISO
      exceptions
        NOT_FOUND   = 01
        NO_ISO_CODE = 02.

    move DA_KONV-KPEIN to CH_BAPICOND-COND_P_UNT.

    move DA_KONV-KAPPL to CH_BAPICOND-APPLICATIO.
    move DA_KONV-KDATU to CH_BAPICOND-CONPRICDAT.
    move DA_KONV-KRECH to CH_BAPICOND-CALCTYPCON.
* KAWRT Konditionsbasis conbaseval
    case DA_KONV-KRECH.
      when 'A' or 'H' or 'I' or 'B'.  "percentage
        CH_BAPICOND-CONBASEVAL =
        DA_KONV-KAWRT.
        if not DA_KONV-KAWRT is initial and
           not DA_WAER is initial.
          clear CH_BAPICOND-CONBASEVAL.
          call function 'BAPI_CURRENCY_CONV_TO_EXTERN_9'
            exporting
              CURRENCY        = DA_WAER
              AMOUNT_INTERNAL = DA_KONV-KAWRT
            importing
              AMOUNT_EXTERNAL = CH_BAPICOND-CONBASEVAL.
        endif.
      when others.
        CH_BAPICOND-CONBASEVAL =
        DA_KONV-KAWRT / 10.
    endcase.

    move DA_KONV-KKURS to CH_BAPICOND-CONEXCHRAT.
    move DA_KONV-KUMZA to CH_BAPICOND-NUMCONVERT.
    move DA_KONV-KUMNE to CH_BAPICOND-DENOMINATO.
    move DA_KONV-KNTYP to CH_BAPICOND-CONDTYPE.
    move DA_KONV-KSTAT to CH_BAPICOND-STAT_CON.
    move DA_KONV-KNPRS to CH_BAPICOND-SCALETYPE.
    move DA_KONV-KRUEK to CH_BAPICOND-ACCRUALS.
    move DA_KONV-KRELI to CH_BAPICOND-CONINVOLST.
    move DA_KONV-KHERK to CH_BAPICOND-CONDORIGIN.
    move DA_KONV-KGRPE to CH_BAPICOND-GROUPCOND.
    move DA_KONV-KOUPD to CH_BAPICOND-COND_UPDAT.
    move DA_KONV-KOLNR to CH_BAPICOND-ACCESS_SEQ.
    move DA_KONV-KOPOS to CH_BAPICOND-CONDCOUNT.
*         move da_konv-kdiff to ch_bapicond-roundoffdi.
* KWERT Konditionswert in Belegwährung
*         MOVE da_konv-KWERT TO ch_bapicond-CONDVALUE.
    move DA_VBAK-WAERK to CH_BAPICOND-CURRENCY_2.

    call function 'CURRENCY_CODE_SAP_TO_ISO'
      exporting
        SAP_CODE  = CH_BAPICOND-CURRENCY_2
      importing
        ISO_CODE  = CH_BAPICOND-CURR_ISO_2
      exceptions
        NOT_FOUND = 01.

    call function 'BAPI_CURRENCY_CONV_TO_EXTERN_9'
      exporting
        CURRENCY        = CH_BAPICOND-CURRENCY_2
        AMOUNT_INTERNAL = DA_KONV-KWERT
      importing
        AMOUNT_EXTERNAL = CH_BAPICOND-CONDVALUE.

* KDIFF Rundungsdifferenz Belegwährung
    call function 'BAPI_CURRENCY_CONV_TO_EXTERN_9'
      exporting
        CURRENCY        = CH_BAPICOND-CURRENCY_2
        AMOUNT_INTERNAL = DA_KONV-KDIFF
      importing
        AMOUNT_EXTERNAL = CH_BAPICOND-ROUNDOFFDI.

    move DA_KONV-KSTEU to CH_BAPICOND-CONDCNTRL.
    move DA_KONV-KINAK to CH_BAPICOND-CONDISACTI.
    move DA_KONV-KOAID to CH_BAPICOND-CONDCLASS.
    move DA_KONV-KFAKTOR  to CH_BAPICOND-FACTBASVAL.
    move DA_KONV-KZBZG to CH_BAPICOND-SCALEBASIN.

* KSTBS   Kondition-Staffelfbasis
    if not DA_KONV-KONWS is initial.
      call function 'BAPI_CURRENCY_CONV_TO_EXTERN_9'
        exporting
          CURRENCY        = DA_KONV-KONWS
          AMOUNT_INTERNAL = DA_KONV-KSTBS
        importing
          AMOUNT_EXTERNAL = CH_BAPICOND-SCALBASVAL.
    endif.

    case DA_KONV-KZBZG.
      when 'C'.
        CH_BAPICOND-SCALBASVAL
      = DA_KONV-KSTBS / 10.
    endcase.

    move DA_KONV-KONMS to CH_BAPICOND-UNITMEASUR.

    call function 'UNIT_OF_MEASURE_SAP_TO_ISO'
      exporting
        SAP_CODE    = DA_KONV-KONMS
      importing
        ISO_CODE    = CH_BAPICOND-ISO_UNIT
      exceptions
        NOT_FOUND   = 01
        NO_ISO_CODE = 02.

    move DA_KONV-KONWS to CH_BAPICOND-CURRENCKEY.

    call function 'CURRENCY_CODE_SAP_TO_ISO'
      exporting
        SAP_CODE  = DA_KONV-KONWS
      importing
        ISO_CODE  = CH_BAPICOND-CURRENISO
      exceptions
        NOT_FOUND = 01.

    move DA_KONV-KFKIV to CH_BAPICOND-CONDINCOMP.
    move DA_KONV-KVARC to CH_BAPICOND-CONDCONFIG.
    move DA_KONV-KMPRS to CH_BAPICOND-CONDCHAMAN.
    move DA_KONV-KNUMH to CH_BAPICOND-COND_NO.

    move DA_KONV-MWSK1   to CH_BAPICOND-TAX_CODE.
    move DA_KONV-VARCOND to CH_BAPICOND-VARCOND.
    move DA_KONV-ZAEKO   to CH_BAPICOND-CONDCOINHD.

    append CH_BAPICOND.

  endloop.

endform.
*&---------------------------------------------------------------------*
*&      Form  MAP_VBAP_TO_BAPISDITM
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->IS_VBAP  text
*      <--CS_BAPISDITM  text
*----------------------------------------------------------------------*
form MAP_VBAP_TO_BAPISDITM  using    IS_VBAP type VBAP
                            changing CS_BAPISDITM type BAPISDITM.
  data LS_VBAP type VBAP.

  move IS_VBAP-POSNR      to CS_BAPISDITM-ITM_NUMBER.
  move IS_VBAP-UEPOS      to CS_BAPISDITM-HG_LV_ITEM.

*    move IS_VBAP-POSEX      to CS_BAPISDITM-PO_ITM_NO.
  move IS_VBAP-POSNR      to CS_BAPISDITM-PO_ITM_NO.
  if IS_VBAP-MATNR is initial.
    read table GT_VBAP into LS_VBAP with key POSNR = P_POSNR.
    move LS_VBAP-MATNR      to CS_BAPISDITM-MATERIAL.
  else.
    move IS_VBAP-MATNR      to CS_BAPISDITM-MATERIAL.
  endif.
  move IS_VBAP-GRPOS      to CS_BAPISDITM-ALT_TO_ITM.
  move IS_VBAP-CHARG      to CS_BAPISDITM-BATCH.
  move IS_VBAP-GRKOR      to CS_BAPISDITM-DLV_GROUP.
  move IS_VBAP-KZTLF      to CS_BAPISDITM-PART_DLV.
  if IS_VBAP-POSNR <> P_POSNR.
    move IS_VBAP-ABGRU    to CS_BAPISDITM-REASON_REJ.
  endif.
  move IS_VBAP-FAKSP      to CS_BAPISDITM-BILL_BLOCK.
  move IS_VBAP-WERKS      to CS_BAPISDITM-PLANT.
  move IS_VBAP-LGORT      to CS_BAPISDITM-STORE_LOC.
  move IS_VBAP-ZMENG      to CS_BAPISDITM-TARGET_QTY.
  move IS_VBAP-ZIEME      to CS_BAPISDITM-TARGET_QU.
  move IS_VBAP-PSTYV      to CS_BAPISDITM-ITEM_CATEG.
  move IS_VBAP-ARKTX      to CS_BAPISDITM-SHORT_TEXT.
  move IS_VBAP-MVGR1      to CS_BAPISDITM-PRC_GROUP1.
  move IS_VBAP-MVGR2      to CS_BAPISDITM-PRC_GROUP2.
  move IS_VBAP-MVGR3      to CS_BAPISDITM-PRC_GROUP3.
  move IS_VBAP-MVGR4      to CS_BAPISDITM-PRC_GROUP4.
  move IS_VBAP-MVGR5      to CS_BAPISDITM-PRC_GROUP5.
  move IS_VBAP-PRODH      to CS_BAPISDITM-PROD_HIERA.
  move IS_VBAP-MATKL      to CS_BAPISDITM-MATL_GROUP.
  move IS_VBAP-VRKME      to CS_BAPISDITM-SALES_UNIT.
  move IS_VBAP-UMZIZ      to CS_BAPISDITM-TRG_QTY_NO.
  move IS_VBAP-UMZIN      to CS_BAPISDITM-TRGQTY_DEN.
  move IS_VBAP-ABLFZ      to CS_BAPISDITM-RNDDLV_QTY.
  move IS_VBAP-ABSFZ      to CS_BAPISDITM-MAXDEVAMNT.
  move IS_VBAP-KBVER      to CS_BAPISDITM-MAXDEVPER.
  move IS_VBAP-KEVER      to CS_BAPISDITM-MAXDEV_DAY.
  move IS_VBAP-VKAUS      to CS_BAPISDITM-USAGE_IND.
  move IS_VBAP-FMENG      to CS_BAPISDITM-FIXED_QUAN.
  move IS_VBAP-UEBTK      to CS_BAPISDITM-UNLMT_DLV.
  move IS_VBAP-UEBTO      to CS_BAPISDITM-OVERDLVTOL.
  move IS_VBAP-UNTTO      to CS_BAPISDITM-UNDDLV_TOL.
  move IS_VBAP-SPART      to CS_BAPISDITM-DIVISION.
  move IS_VBAP-UMVKZ      to CS_BAPISDITM-SALQTYNUM.
  move IS_VBAP-UMVKN      to CS_BAPISDITM-SALQTYDEN.
  move IS_VBAP-BRGEW      to CS_BAPISDITM-GROSS_WGHT.
  move IS_VBAP-NTGEW      to CS_BAPISDITM-NET_WEIGHT.
  move IS_VBAP-GEWEI      to CS_BAPISDITM-UNTOF_WGHT.
  move IS_VBAP-VOLUM      to CS_BAPISDITM-VOLUME.
  move IS_VBAP-VOLEH      to CS_BAPISDITM-VOLUNIT.
  move IS_VBAP-LPRIO      to CS_BAPISDITM-DLV_PRIO.
  move IS_VBAP-VSTEL      to CS_BAPISDITM-SHIP_POINT.
  move IS_VBAP-ROUTE      to CS_BAPISDITM-ROUTE.
  move IS_VBAP-ERNAM      to CS_BAPISDITM-CREATED_BY.
  move IS_VBAP-TAXM1      to CS_BAPISDITM-TAX_CLASS1.
  move IS_VBAP-TAXM2      to CS_BAPISDITM-TAX_CLASS2.
  move IS_VBAP-TAXM3      to CS_BAPISDITM-TAX_CLASS3.
  move IS_VBAP-TAXM4      to CS_BAPISDITM-TAX_CLASS4.
  move IS_VBAP-TAXM5      to CS_BAPISDITM-TAX_CLASS5.
  move IS_VBAP-TAXM6      to CS_BAPISDITM-TAX_CLASS6.
  move IS_VBAP-TAXM7      to CS_BAPISDITM-TAX_CLASS7.
  move IS_VBAP-TAXM8      to CS_BAPISDITM-TAX_CLASS8.
  move IS_VBAP-TAXM9      to CS_BAPISDITM-TAX_CLASS9.
  move IS_VBAP-KONDM      to CS_BAPISDITM-MAT_PR_GRP.
  move IS_VBAP-BWTAR      to CS_BAPISDITM-VAL_TYPE.
  move IS_VBAP-FIXMG      to CS_BAPISDITM-FIXDAT_QTY.
  move IS_VBAP-SERNR      to CS_BAPISDITM-BOMEXPL_NO.
  move IS_VBAP-ABGRS      to CS_BAPISDITM-RESANALKEY.
  move IS_VBAP-BEDAE      to CS_BAPISDITM-REQMTS_TYP.
  move IS_VBAP-NACHL      to CS_BAPISDITM-NO_GR_POST.
  move IS_VBAP-EXART      to CS_BAPISDITM-BUS_TRANST.
  move IS_VBAP-ZSCHL_K    to CS_BAPISDITM-OVERHD_KEY.
  move IS_VBAP-KALSM_K    to CS_BAPISDITM-CSTG_SHEET.
  move IS_VBAP-MFRGR      to CS_BAPISDITM-MATFRGTGRP.
  move IS_VBAP-PLAVO      to CS_BAPISDITM-PLDLVSHDIN.
  move IS_VBAP-KANNR      to CS_BAPISDITM-SEQ_NO.
  move IS_VBAP-PMATN      to CS_BAPISDITM-PR_REF_MAT.
  move IS_VBAP-AWAHR      to CS_BAPISDITM-ORDER_PROB.
  move IS_VBAP-ANTLF      to CS_BAPISDITM-MAX_PL_DLV.
  move IS_VBAP-SKOPF      to CS_BAPISDITM-ASSORT_MOD.
  move IS_VBAP-KMPMG      to CS_BAPISDITM-COMP_QUANT.
  move IS_VBAP-PRCTR      to CS_BAPISDITM-PROFIT_CTR.
  move IS_VBAP-AUFNR      to CS_BAPISDITM-ORDERID.
  move IS_VBAP-VGBEL      to CS_BAPISDITM-REF_DOC.
  move IS_VBAP-VGPOS      to CS_BAPISDITM-REF_DOC_IT.
  move IS_VBAP-VGTYP      to CS_BAPISDITM-REF_DOC_CA.
  move IS_VBAP-KDMAT      to CS_BAPISDITM-CUST_MAT35.
  move IS_VBAP-WKTNR      to CS_BAPISDITM-VAL_CONTR.
  move IS_VBAP-WKTPS      to CS_BAPISDITM-VAL_CON_I.
  move IS_VBAP-EAN11      to CS_BAPISDITM-EAN_UPC.
  move IS_VBAP-UEPVW      to CS_BAPISDITM-VW_UEPOS.
  move IS_VBAP-VKAUS      to CS_BAPISDITM-DLVSCHDUSE.
  move IS_VBAP-MATWA      to CS_BAPISDITM-MAT_ENTRD.
  move IS_VBAP-LOGSYS_EXT to CS_BAPISDITM-LOG_SYSTEM_OWN.

endform.
*&---------------------------------------------------------------------*
*&      Form  MAP_VBKD_TO_BAPISDITM
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->IS_VBKD  text
*      <--CS_BAPISDITM  text
*----------------------------------------------------------------------*
form MAP_VBKD_TO_BAPISDITM  using    IS_VBKD type VBKD
                            changing CS_BAPISDITM type BAPISDITM.

  move IS_VBKD-FKDAT      to CS_BAPISDITM-BILL_DATE.
  move IS_VBKD-BSTKD      to CS_BAPISDITM-PURCH_NO_C.
  move IS_VBKD-BSTDK      to CS_BAPISDITM-PURCH_DATE.
  move IS_VBKD-BSARK      to CS_BAPISDITM-PO_METHOD.
  move IS_VBKD-IHREZ      to CS_BAPISDITM-REF_1.
  move IS_VBKD-BSTKD_E    to CS_BAPISDITM-PURCH_NO_S.
  move IS_VBKD-BSTDK_E    to CS_BAPISDITM-PO_DAT_S.
  move IS_VBKD-BSARK_E    to CS_BAPISDITM-PO_METH_S.
  move IS_VBKD-IHREZ_E    to CS_BAPISDITM-REF_1_S.
  move IS_VBKD-POSEX_E    to CS_BAPISDITM-POITM_NO_S.
  move IS_VBKD-KONDA      to CS_BAPISDITM-PRICE_GRP.
  move IS_VBKD-KDGRP      to CS_BAPISDITM-CUST_GROUP.
  move IS_VBKD-BZIRK      to CS_BAPISDITM-SALES_DIST.
  move IS_VBKD-PLTYP      to CS_BAPISDITM-PRICE_LIST.
  move IS_VBKD-INCO1      to CS_BAPISDITM-INCOTERMS1.
  move IS_VBKD-INCO2      to CS_BAPISDITM-INCOTERMS2.
  move IS_VBKD-KZAZU      to CS_BAPISDITM-ORDCOMP_IN.
  move IS_VBKD-PERFK      to CS_BAPISDITM-BILL_SCHED.
  move IS_VBKD-PERRL      to CS_BAPISDITM-INVO_SCHED.
  move IS_VBKD-MRNKZ      to CS_BAPISDITM-MN_INVOICE.
  move IS_VBKD-KURRF      to CS_BAPISDITM-EX_RATE_FI.
  move IS_VBKD-VALTG      to CS_BAPISDITM-ADD_VAL_DY.
  move IS_VBKD-VALDT      to CS_BAPISDITM-FIX_VAL_DY.
  move IS_VBKD-ZTERM      to CS_BAPISDITM-PMNTTRMS.
  move IS_VBKD-ZLSCH      to CS_BAPISDITM-PYMT_METH.
  move IS_VBKD-KTGRD      to CS_BAPISDITM-ACCNT_ASGN.
  move IS_VBKD-KURSK      to CS_BAPISDITM-EXCHG_RATE.
  move IS_VBKD-PRSDT      to CS_BAPISDITM-PRICE_DATE.
  move IS_VBKD-FBUDA      to CS_BAPISDITM-SERV_DATE.
  move IS_VBKD-MSCHL      to CS_BAPISDITM-DUNN_KEY.
  move IS_VBKD-MANSP      to CS_BAPISDITM-DUNN_BLOCK.
  move IS_VBKD-WAKTION    to CS_BAPISDITM-PROMOTION.
  move IS_VBKD-ABSSC      to CS_BAPISDITM-PMTGAR_PRO.
  move IS_VBKD-LCNUM      to CS_BAPISDITM-DOC_NUM_FI.
  move IS_VBKD-ABTNR      to CS_BAPISDITM-DEPARTM_NO.
  move IS_VBKD-EMPST      to CS_BAPISDITM-REC_POINT.
  move IS_VBKD-KDKG1      to CS_BAPISDITM-CSTCNDGRP1.
  move IS_VBKD-KDKG2      to CS_BAPISDITM-CSTCNDGRP2.
  move IS_VBKD-KDKG3      to CS_BAPISDITM-CSTCNDGRP3.
  move IS_VBKD-KDKG4      to CS_BAPISDITM-CSTCNDGRP4.
  move IS_VBKD-KDKG5      to CS_BAPISDITM-CSTCNDGRP5.
  move IS_VBKD-DELCO      to CS_BAPISDITM-DLV_TIME.
  move IS_VBKD-FAKTF      to CS_BAPISDITM-BIL_FORM.
  move IS_VBKD-FFPRF      to CS_BAPISDITM-DLI_PROFIL.
  move IS_VBKD-RRREL      to CS_BAPISDITM-REV_TYPE.
  move IS_VBKD-ACDATV     to CS_BAPISDITM-BEGDEM_PER.
  move IS_VBKD-AKPRZ      to CS_BAPISDITM-DEPREC_PER.
  move IS_VBKD-VKONT      to CS_BAPISDITM-FKK_CONACCT.
  move IS_VBKD-WMINR      to CS_BAPISDITM-PRODCAT.
  move IS_VBKD-VSART      to CS_BAPISDITM-SHIP_TYPE.
  move IS_VBKD-SDABW      to CS_BAPISDITM-S_PROC_IND.

endform.
*&---------------------------------------------------------------------*
*&      Form  MAP_VBAP_TO_BAPIPAREX
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->IS_VBAP  text
*      <--CS_BAPIAREX  text
*----------------------------------------------------------------------*
form MAP_VBAP_TO_BAPIPAREX  using    IS_VBAP type VBAP
                            changing CS_BAPIPAREX type BAPIPAREX.
  data: LF_OFFSET type I.

  CS_BAPIPAREX-STRUCTURE = 'BAPE_VBAP'.
  describe field CS_BAPIPAREX-STRUCTURE length LF_OFFSET in character mode.

  CL_ABAP_CONTAINER_UTILITIES=>FILL_CONTAINER_C( exporting IM_VALUE     = IS_VBAP
                                                 importing EX_CONTAINER = CS_BAPIPAREX+LF_OFFSET ).
endform.
*&---------------------------------------------------------------------*
*&      Form  MAP_VBEP_TO_BAPISCHDL
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->IS_VBEP  text
*      <--CS_BAPISCHDL  text
*----------------------------------------------------------------------*
form MAP_VBEP_TO_BAPISCHDL  using    IS_VBEP type VBEP
                            changing CS_BAPISCHDL type BAPISCHDL.

  move IS_VBEP-POSNR   to CS_BAPISCHDL-ITM_NUMBER.
  move IS_VBEP-ETENR   to CS_BAPISCHDL-SCHED_LINE.
  move IS_VBEP-BDDAT   to CS_BAPISCHDL-REQ_DATE.
  move IS_VBEP-PRGRS   to CS_BAPISCHDL-DATE_TYPE.
  move IS_VBEP-EZEIT   to CS_BAPISCHDL-REQ_TIME.
  move IS_VBEP-WMENG   to CS_BAPISCHDL-REQ_QTY.
  move IS_VBEP-LIFSP   to CS_BAPISCHDL-REQ_DLV_BL.
  move IS_VBEP-ETTYP   to CS_BAPISCHDL-SCHED_TYPE.
  move IS_VBEP-TDDAT   to CS_BAPISCHDL-TP_DATE.
  move IS_VBEP-MBDAT   to CS_BAPISCHDL-MS_DATE.
  move IS_VBEP-LDDAT   to CS_BAPISCHDL-LOAD_DATE.
  move IS_VBEP-WADAT   to CS_BAPISCHDL-GI_DATE.
  move IS_VBEP-TDUHR   to CS_BAPISCHDL-TP_TIME.
  move IS_VBEP-MBUHR   to CS_BAPISCHDL-MS_TIME.
  move IS_VBEP-LDUHR   to CS_BAPISCHDL-LOAD_TIME.
  move IS_VBEP-WAUHR   to CS_BAPISCHDL-GI_TIME.
*    move IS_VBEP- to CS_BAPISCHDL-REFOBJTYPE  SWO_OBJTYP
*    move IS_VBEP- to CS_BAPISCHDL-REFOBJKEY  SWO_TYPEID
*    move IS_VBEP- to CS_BAPISCHDL-REFLOGSYS  LOGSYS
  move IS_VBEP-EDATU   to CS_BAPISCHDL-DLV_DATE.
  move IS_VBEP-EZEIT   to CS_BAPISCHDL-DLV_TIME.
  move IS_VBEP-ABART   to CS_BAPISCHDL-REL_TYPE.
  move IS_VBEP-ETART   to CS_BAPISCHDL-PLAN_SCHED_TYPE.

endform.
*&---------------------------------------------------------------------*
*&      Form  GET_DATA_INX
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form GET_DATA_INX .

  perform FILL_BAPISDHD1X.
  perform FILL_BAPICONDX.
  perform FILL_BAPISDITMX.
  perform FILL_BAPISCHDLX.
  perform FILL_BAPIPAREX.

  append lines of GS_BAPI_SAVE_INX-T_BAPIPAREX to GS_BAPI_SAVE_IN-T_BAPIPAREX.


endform.

*&---------------------------------------------------------------------*
*&      Form  FILL_BAPISDHD1X
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form FILL_BAPISDHD1X.

  if GS_VBAK-VBELN is initial.
    GS_BAPI_SAVE_INX-S_BAPISDHD1X-UPDATEFLAG = 'I'.
    /VCXI/CL_CKX_SERVICE=>FILL_X_STRUCT( exporting IS_DATA    = GS_BAPI_SAVE_IN-S_BAPISDHD1
                                         changing  CS_X_STRUC = GS_BAPI_SAVE_INX-S_BAPISDHD1X ).
  else.
    GS_BAPI_SAVE_INX-S_BAPISDHD1X-UPDATEFLAG = 'U'.
    /VCXI/CL_CKX_SERVICE=>FILL_X_STRUCT( exporting IS_DATA     = GS_BAPI_SAVE_IN-S_BAPISDHD1
                                                   IS_DATA_OLD = GS_BAPI_SAVE_IN_OLD-S_BAPISDHD1
                                         changing  CS_X_STRUC  = GS_BAPI_SAVE_INX-S_BAPISDHD1X ).
  endif.

endform.
*&---------------------------------------------------------------------*
*&      Form  FILL_BAPICONDX
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form FILL_BAPICONDX.

  types: begin of YS_KOMV_KEY,
           KPOSN type  KPOSN,
           STUNR type  STUNR,
           ZAEHK type  DZAEHK,
         end of YS_KOMV_KEY.

  data: LS_BAPICOND     type BAPICOND,
        LS_BAPICOND_OLD type BAPICOND,
        LS_KOMV_KEY     type YS_KOMV_KEY.
  data: LT_KOMV_KEY type standard table of YS_KOMV_KEY.
  field-symbols: <S_BAPICONDX> type BAPICONDX.


***--------------------------------------------------------------------------------------
*** Get all Conditions (New, Update and Deleted)
  LT_KOMV_KEY = value #( for S_BAPICOND in GS_BAPI_SAVE_IN-T_BAPICOND ( KPOSN = S_BAPICOND-ITM_NUMBER
                                                                        STUNR = S_BAPICOND-COND_ST_NO
                                                                        ZAEHK = S_BAPICOND-COND_COUNT ) ).
  loop at GS_BAPI_SAVE_IN_OLD-T_BAPICOND into LS_BAPICOND_OLD.
    if not LINE_EXISTS( LT_KOMV_KEY[ KPOSN = LS_BAPICOND_OLD-ITM_NUMBER
                                     STUNR = LS_BAPICOND_OLD-COND_ST_NO
                                     ZAEHK = LS_BAPICOND_OLD-COND_COUNT ] ).
      append value #( KPOSN = LS_BAPICOND_OLD-ITM_NUMBER
                      STUNR = LS_BAPICOND_OLD-COND_ST_NO
                      ZAEHK = LS_BAPICOND_OLD-COND_COUNT ) to LT_KOMV_KEY.
    endif.
  endloop.
  sort LT_KOMV_KEY.

***--------------------------------------------------------------------------------------
*** Conditions
  loop at LT_KOMV_KEY into LS_KOMV_KEY.
    append value #( ITM_NUMBER = LS_KOMV_KEY-KPOSN
                    COND_ST_NO = LS_KOMV_KEY-STUNR
                    COND_COUNT = LS_KOMV_KEY-ZAEHK ) to GS_BAPI_SAVE_INX-T_BAPICONDX assigning <S_BAPICONDX>.

***   Insert
    if not LINE_EXISTS( GS_BAPI_SAVE_IN_OLD-T_BAPICOND[ ITM_NUMBER = LS_KOMV_KEY-KPOSN
                                                        COND_ST_NO = LS_KOMV_KEY-STUNR
                                                        COND_COUNT = LS_KOMV_KEY-ZAEHK ] ) and
           LINE_EXISTS( GS_BAPI_SAVE_IN-T_BAPICOND[     ITM_NUMBER = LS_KOMV_KEY-KPOSN
                                                        COND_ST_NO = LS_KOMV_KEY-STUNR
                                                        COND_COUNT = LS_KOMV_KEY-ZAEHK ] ).
      <S_BAPICONDX>-UPDATEFLAG = 'I'.
      /VCXI/CL_CKX_SERVICE=>FILL_X_STRUCT( exporting IS_DATA    = GS_BAPI_SAVE_IN-T_BAPICOND[ ITM_NUMBER = LS_KOMV_KEY-KPOSN
                                                                                              COND_ST_NO = LS_KOMV_KEY-STUNR
                                                                                              COND_COUNT = LS_KOMV_KEY-ZAEHK ]
                                           changing  CS_X_STRUC = <S_BAPICONDX> ).

***   Delete
    elseif     LINE_EXISTS( GS_BAPI_SAVE_IN_OLD-T_BAPICOND[ ITM_NUMBER = LS_KOMV_KEY-KPOSN
                                                            COND_ST_NO = LS_KOMV_KEY-STUNR
                                                            COND_COUNT = LS_KOMV_KEY-ZAEHK ] ) and
           not LINE_EXISTS( GS_BAPI_SAVE_IN-T_BAPICOND[     ITM_NUMBER = LS_KOMV_KEY-KPOSN
                                                            COND_ST_NO = LS_KOMV_KEY-STUNR
                                                            COND_COUNT = LS_KOMV_KEY-ZAEHK ] ).
      <S_BAPICONDX>-UPDATEFLAG = 'D'.

***   Update
    elseif GS_BAPI_SAVE_IN-T_BAPICOND[     ITM_NUMBER = LS_KOMV_KEY-KPOSN
                                           COND_ST_NO = LS_KOMV_KEY-STUNR
                                           COND_COUNT = LS_KOMV_KEY-ZAEHK ] ne
           GS_BAPI_SAVE_IN_OLD-T_BAPICOND[ ITM_NUMBER = LS_KOMV_KEY-KPOSN
                                           COND_ST_NO = LS_KOMV_KEY-STUNR
                                           COND_COUNT = LS_KOMV_KEY-ZAEHK ].
      <S_BAPICONDX>-UPDATEFLAG = 'U'.
      /VCXI/CL_CKX_SERVICE=>FILL_X_STRUCT( exporting IS_DATA     = GS_BAPI_SAVE_IN-T_BAPICOND[     ITM_NUMBER = LS_KOMV_KEY-KPOSN
                                                                                                   COND_ST_NO = LS_KOMV_KEY-STUNR
                                                                                                   COND_COUNT = LS_KOMV_KEY-ZAEHK ]
                                                     IS_DATA_OLD = GS_BAPI_SAVE_IN_OLD-T_BAPICOND[ ITM_NUMBER = LS_KOMV_KEY-KPOSN
                                                                                                   COND_ST_NO = LS_KOMV_KEY-STUNR
                                                                                                   COND_COUNT = LS_KOMV_KEY-ZAEHK ]
                                           changing  CS_X_STRUC  = <S_BAPICONDX> ).

    endif.
  endloop.

endform.
*&---------------------------------------------------------------------*
*&      Form  FILL_BAPISDITMX
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form FILL_BAPISDITMX.

  data: LF_POSNR type POSNR,
        LF_NIDAT type ZVCXI_XCI_NIDAT.
  data: LS_BAPISDITM     type BAPISDITM,
        LS_BAPISDITM_OLD type BAPISDITM.
  data: LT_POSNR    type standard table of POSNR.
  field-symbols: <S_BAPISDITMX> type BAPISDITMX.

***--------------------------------------------------------------------------------------
*** Get all Position Numbers (New, Update and Deleted)
  LT_POSNR = value #( for S_BAPISDITM in GS_BAPI_SAVE_IN-T_BAPISDITM ( S_BAPISDITM-ITM_NUMBER ) ).
  loop at GS_BAPI_SAVE_IN_OLD-T_BAPISDITM into LS_BAPISDITM_OLD.
    if not LINE_EXISTS( LT_POSNR[ TABLE_LINE = LS_BAPISDITM_OLD-ITM_NUMBER ] ).
      append LS_BAPISDITM_OLD-ITM_NUMBER to LT_POSNR.
    endif.
  endloop.
  sort LT_POSNR.

***--------------------------------------------------------------------------------------
*** Process all Items
  loop at LT_POSNR into LF_POSNR.
    append value #( ITM_NUMBER = LF_POSNR ) to GS_BAPI_SAVE_INX-T_BAPISDITMX assigning <S_BAPISDITMX>.

***   Insert
    if not LINE_EXISTS( GS_BAPI_SAVE_IN_OLD-T_BAPISDITM[ ITM_NUMBER = LF_POSNR ] ) and
           LINE_EXISTS( GS_BAPI_SAVE_IN-T_BAPISDITM[     ITM_NUMBER = LF_POSNR ] ).
      <S_BAPISDITMX>-UPDATEFLAG = 'I'.
      /VCXI/CL_CKX_SERVICE=>FILL_X_STRUCT( exporting IS_DATA    = GS_BAPI_SAVE_IN-T_BAPISDITM[ ITM_NUMBER = LF_POSNR ]
                                           changing  CS_X_STRUC = <S_BAPISDITMX> ).

***   Delete
    elseif     LINE_EXISTS( GS_BAPI_SAVE_IN_OLD-T_BAPISDITM[ ITM_NUMBER = LF_POSNR ] ) and
           not LINE_EXISTS( GS_BAPI_SAVE_IN-T_BAPISDITM[     ITM_NUMBER = LF_POSNR ] ).
      <S_BAPISDITMX>-UPDATEFLAG = 'D'.

***   Update
    elseif LINE_EXISTS( GS_BAPI_SAVE_IN_OLD-T_BAPISDITM[ ITM_NUMBER = LF_POSNR ] ) and
           LINE_EXISTS( GS_BAPI_SAVE_IN-T_BAPISDITM[     ITM_NUMBER = LF_POSNR ] ).
      if GS_BAPI_SAVE_IN_OLD-T_BAPISDITM[ ITM_NUMBER = LF_POSNR ] ne GS_BAPI_SAVE_IN-T_BAPISDITM[ ITM_NUMBER = LF_POSNR ].
        <S_BAPISDITMX>-UPDATEFLAG = 'U'.
      endif.
      /VCXI/CL_CKX_SERVICE=>FILL_X_STRUCT( exporting IS_DATA     = GS_BAPI_SAVE_IN-T_BAPISDITM[     ITM_NUMBER = LF_POSNR ]
                                                     IS_DATA_OLD = GS_BAPI_SAVE_IN_OLD-T_BAPISDITM[ ITM_NUMBER = LF_POSNR ]
                                           changing  CS_X_STRUC  = <S_BAPISDITMX> ).
    endif.

***   Force Configuration
    call function 'ZVCXI_XCC_IDAT_GET_NIDAT'
      exporting
        IF_POSNR = LF_POSNR
      importing
        EF_NIDAT = LF_NIDAT.
    if LF_NIDAT is not initial and
       not LINE_EXISTS( GT_CFGS_UPDKZ[ POSNR = LF_POSNR ] ).
      append value #( POSNR = LF_POSNR
                      UPDKZ = 'U' ) to GT_CFGS_UPDKZ.
    endif.
    if value #( GT_CFGS_UPDKZ[ POSNR = LF_POSNR ]-UPDKZ optional ) is not initial.
      <S_BAPISDITMX>-UPDATEFLAG = switch #( <S_BAPISDITMX>-UPDATEFLAG when SPACE then 'U' else <S_BAPISDITMX>-UPDATEFLAG ).
      <S_BAPISDITMX>-PO_ITM_NO  = ABAP_TRUE. "This triggers VC Processing as it ensures VBAP-POSEX is filled  (SAP Note 549563)
    endif.

***   Mark Item as Updated in case of changed Conditions
    loop at GS_BAPI_SAVE_INX-T_BAPICONDX transporting no fields
                                         where ITM_NUMBER eq LF_POSNR
                                          and  UPDATEFLAG ne SPACE.
      <S_BAPISDITMX>-UPDATEFLAG = switch #( <S_BAPISDITMX>-UPDATEFLAG when SPACE then 'U' else <S_BAPISDITMX>-UPDATEFLAG ).
      exit.
    endloop.
  endloop.

endform.
*&---------------------------------------------------------------------*
*&      Form  FILL_BAPISCHDLX
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form FILL_BAPISCHDLX.

  types: begin of YS_VBEP_KEY,
           POSNR type POSNR,
           ETENR type ETENR,
         end of YS_VBEP_KEY.

  data: LS_BAPISCHDL     type BAPISCHDL,
        LS_BAPISCHDL_OLD type BAPISCHDL,
        LS_VBEP_KEY      type YS_VBEP_KEY.
  data: LT_VBEP_KEY type standard table of YS_VBEP_KEY.
  field-symbols: <S_BAPISCHDLX> type BAPISCHDLX.


***--------------------------------------------------------------------------------------
*** Get all Schedule Lines (New, Update and Deleted)
  LT_VBEP_KEY = value #( for S_BAPISCHDL in GS_BAPI_SAVE_IN-T_BAPISCHDL ( POSNR = S_BAPISCHDL-ITM_NUMBER
                                                                          ETENR = S_BAPISCHDL-SCHED_LINE ) ).
  loop at GS_BAPI_SAVE_IN_OLD-T_BAPISCHDL into LS_BAPISCHDL_OLD.
    if not LINE_EXISTS( LT_VBEP_KEY[ POSNR = LS_BAPISCHDL_OLD-ITM_NUMBER
                                     ETENR = LS_BAPISCHDL_OLD-SCHED_LINE ] ).
      append value #( POSNR = LS_BAPISCHDL_OLD-ITM_NUMBER
                      ETENR = LS_BAPISCHDL_OLD-SCHED_LINE ) to LT_VBEP_KEY.
    endif.
  endloop.
  sort LT_VBEP_KEY.

***--------------------------------------------------------------------------------------
*** Schedule Lines Data
  loop at LT_VBEP_KEY into LS_VBEP_KEY.
    append value #( ITM_NUMBER = LS_VBEP_KEY-POSNR
                    SCHED_LINE = LS_VBEP_KEY-ETENR ) to GS_BAPI_SAVE_INX-T_BAPISCHDLX assigning <S_BAPISCHDLX>.

***   Insert
    if not LINE_EXISTS( GS_BAPI_SAVE_IN_OLD-T_BAPISCHDL[ ITM_NUMBER = LS_VBEP_KEY-POSNR
                                                         SCHED_LINE = LS_VBEP_KEY-ETENR ] ) and
           LINE_EXISTS( GS_BAPI_SAVE_IN-T_BAPISCHDL[     ITM_NUMBER = LS_VBEP_KEY-POSNR
                                                         SCHED_LINE = LS_VBEP_KEY-ETENR ] ).
      <S_BAPISCHDLX>-UPDATEFLAG = 'I'.
      /VCXI/CL_CKX_SERVICE=>FILL_X_STRUCT( exporting IS_DATA    = GS_BAPI_SAVE_IN-T_BAPISCHDL[  ITM_NUMBER = LS_VBEP_KEY-POSNR
                                                                                                SCHED_LINE = LS_VBEP_KEY-ETENR ]
                                           changing  CS_X_STRUC = <S_BAPISCHDLX> ).

***   Delete
    elseif     LINE_EXISTS( GS_BAPI_SAVE_IN_OLD-T_BAPISCHDL[ ITM_NUMBER = LS_VBEP_KEY-POSNR
                                                             SCHED_LINE = LS_VBEP_KEY-ETENR ] ) and
           not LINE_EXISTS( GS_BAPI_SAVE_IN-T_BAPISCHDL[     ITM_NUMBER = LS_VBEP_KEY-POSNR
                                                             SCHED_LINE = LS_VBEP_KEY-ETENR ] ).
      <S_BAPISCHDLX>-UPDATEFLAG = 'D'.

***   Update
    elseif GS_BAPI_SAVE_IN-T_BAPISCHDL[     ITM_NUMBER = LS_VBEP_KEY-POSNR
                                            SCHED_LINE = LS_VBEP_KEY-ETENR ] ne
           GS_BAPI_SAVE_IN_OLD-T_BAPISCHDL[ ITM_NUMBER = LS_VBEP_KEY-POSNR
                                            SCHED_LINE = LS_VBEP_KEY-ETENR ].
      <S_BAPISCHDLX>-UPDATEFLAG = 'U'.
      /VCXI/CL_CKX_SERVICE=>FILL_X_STRUCT( exporting IS_DATA     = GS_BAPI_SAVE_IN-T_BAPISCHDL[     ITM_NUMBER = LS_VBEP_KEY-POSNR
                                                                                                    SCHED_LINE = LS_VBEP_KEY-ETENR ]
                                                     IS_DATA_OLD = GS_BAPI_SAVE_IN_OLD-T_BAPISCHDL[ ITM_NUMBER = LS_VBEP_KEY-POSNR
                                                                                                    SCHED_LINE = LS_VBEP_KEY-ETENR ]
                                           changing  CS_X_STRUC  = <S_BAPISCHDLX> ).

    endif.
  endloop.


endform.
*&---------------------------------------------------------------------*
*&      Form  FILL_BAPIPAREX
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form FILL_BAPIPAREX.

  data: LF_OFFSET type I,
        LF_OLD    type ABAP_BOOL.
  data: LS_BAPIPAREX     type BAPIPAREX,
        LS_BAPE_VBAP     type BAPE_VBAP,
        LS_BAPE_VBAP_OLD type BAPE_VBAP,
        LS_BAPE_VBAPX    type BAPE_VBAPX.
  field-symbols: <S_BAPIPAREX> type BAPIPAREX.


*** Process all Extensions
  loop at GS_BAPI_SAVE_IN-T_BAPIPAREX into LS_BAPIPAREX.
    describe field LS_BAPIPAREX-STRUCTURE length LF_OFFSET in character mode.

    case LS_BAPIPAREX-STRUCTURE.
***------------------------------------------------------------------------------------------------
***     Look for VBAP Extension
      when 'BAPE_VBAP'.
        CL_ABAP_CONTAINER_UTILITIES=>READ_CONTAINER_C( exporting IM_CONTAINER = LS_BAPIPAREX+LF_OFFSET
                                                       importing EX_VALUE     = LS_BAPE_VBAP ).
***       Find Old value
        LF_OLD = ABAP_FALSE.
        loop at GS_BAPI_SAVE_IN_OLD-T_BAPIPAREX into LS_BAPIPAREX where STRUCTURE = LS_BAPIPAREX-STRUCTURE.
          CL_ABAP_CONTAINER_UTILITIES=>READ_CONTAINER_C( exporting IM_CONTAINER = LS_BAPIPAREX+LF_OFFSET
                                                         importing EX_VALUE     = LS_BAPE_VBAP_OLD ).
          if LS_BAPE_VBAP-POSNR eq LS_BAPE_VBAP_OLD-POSNR.
            LF_OLD = ABAP_FALSE.
          else.
            clear LS_BAPE_VBAP_OLD.
          endif.
        endloop.

        if LF_OLD eq ABAP_TRUE.
***         Fill X-Structure
          /VCXI/CL_CKX_SERVICE=>FILL_X_STRUCT( exporting IS_DATA     = LS_BAPE_VBAP
                                                         IS_DATA_OLD = LS_BAPE_VBAP_OLD
                                               changing  CS_X_STRUC  = LS_BAPE_VBAPX ).
        else.
***         Fill X-Structure
          /VCXI/CL_CKX_SERVICE=>FILL_X_STRUCT( exporting IS_DATA     = LS_BAPE_VBAP
                                               changing  CS_X_STRUC  = LS_BAPE_VBAPX ).
        endif.

***       Build Extension
        append value #( STRUCTURE = 'BAPE_VBAPX' ) to GS_BAPI_SAVE_INX-T_BAPIPAREX assigning <S_BAPIPAREX>.
        describe field <S_BAPIPAREX>-STRUCTURE length LF_OFFSET in character mode.
        CL_ABAP_CONTAINER_UTILITIES=>FILL_CONTAINER_C( exporting IM_VALUE     = LS_BAPE_VBAPX
                                                       importing EX_CONTAINER = <S_BAPIPAREX>+LF_OFFSET ).

    endcase.
  endloop.

endform.
*&---------------------------------------------------------------------*
*&      Form  DEQUEUE_BATCH
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form DEQUEUE_BATCH .


  check GS_VBAK-VBELN is not initial.

  call function 'SD_DOCUMENT_NO_DEQUEUE_RESET'
    exporting
      SALESDOCUMENT          = GS_VBAK-VBELN
    exceptions
      EXC_FOREIGN_LOCK       = 1
      EXC_FOREIGN_TCODE_LOCK = 2
      others                 = 3.
  if SY-SUBRC <> 0.
    message 'Problem doing dequeue' type 'E'.
  endif.


endform.
*&---------------------------------------------------------------------*
*&      Form  ADD_CONFIGURATION
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form ADD_CONFIGURATION.

  data: LF_CUOBJ     type CUOBJ,
        LF_CONFIG_ID type CUX_CFG_ID value '000001',
        LF_INST_ID   type CU_INST_ID value '00000001'.
  data: LS_VBAP type VBAP,
        LS_CUCO type CUCO.


  LS_VBAP = value #( GT_VBAP[ POSNR = GV_NEXT_POSNR ] optional ).
  if LS_VBAP-POSNR ne GV_NEXT_POSNR.
    /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
  endif.

  check not LINE_EXISTS( GT_CFGS_REFINST[ POSEX = GV_NEXT_POSNR ] ).

*** Get next Configuration ID
  loop at GT_CFGS_REFINST into data(LS_CFGS_REFINST).
    if LF_CONFIG_ID le LS_CFGS_REFINST-CONFIG_ID.
      LF_CONFIG_ID = LS_CFGS_REFINST-CONFIG_ID + 1.
      shift LF_CONFIG_ID right deleting trailing SPACE.
      translate LF_CONFIG_ID using ' 0'.
    endif.
  endloop.
*{   INSERT         HEDK927942                                        1
*** Determine Plant if not available
  if LS_VBAP-WERKS is initial.
    select single WERKS into LS_VBAP-WERKS
      from VBAP
      where CUOBJ eq LS_VBAP-CUOBJ.
  endif.
*}   INSERT

***----------------------------------------------------------------------------
*** Get Profile Settings if already known for the item (if not continue w/o)
  .
*  select single CUOBJ
*         from MARC
*         into LF_CUOBJ
*        where MATNR eq LS_VBAP-MATNR
*         and  WERKS eq LS_VBAP-WERKS.

  select single CUOBJ
    from VBAP
    into LF_CUOBJ
    where VBELN eq P_VBELN
       and POSNR eq P_POSNR.

  if SY-SUBRC eq 0 and
     LF_CUOBJ is not initial.
    call function 'CUCB_GET_PROFILE_OF_INSTANCE'
      exporting
        INSTANCE                     = LF_CUOBJ
      importing
        PROFILE_WA                   = LS_CUCO
      exceptions
        INVALID_INSTANCE             = 1
        INSTANCE_IS_A_CLASSIFICATION = 2
        PROFILE_NOT_FOUND            = 3
        INVALID_INPUT                = 4.
    check SY-SUBRC eq 0.
  endif.


*** Reference Order Item / Instance in Configuration
  append value #( POSEX     = LS_VBAP-POSNR
                  CONFIG_ID = LF_CONFIG_ID
                  INST_ID   = LF_INST_ID
                ) to GT_CFGS_REFINST.

*** Instances
  append value #( CONFIG_ID     = LF_CONFIG_ID
                  INST_ID       = LF_INST_ID
                  OBJ_TYPE      = LS_CUCO-OBTAB
                  CLASS_TYPE    = LS_CUCO-KLART
                  OBJ_KEY       = LS_CUCO-OBJEK
                  QUANTITY      = LS_VBAP-KWMENG
                  QUANTITY_UNIT = LS_VBAP-VRKME
                  COMPLETE      = 'F'
                  CONSISTENT    = 'F'
                ) to GT_CFGS_INST.

*** Configuration Data
  append value #( POSEX         = LS_VBAP-POSNR
                  CONFIG_ID     = LF_CONFIG_ID
                  ROOT_ID       = LF_INST_ID
                  COMPLETE      = 'F'
                  CONSISTENT    = 'F'
                ) to GT_CFGS_REF.
*{   INSERT         HEDK927942                                        2
  data:  LS_CONF             type          API_VALUE.
  data:  LT_CONF             type table of API_VALUE.

*** Get Values
  if LF_CUOBJ is not initial.
    call function 'VC_I_GET_CONFIGURATION_IBASE'
      exporting
        INSTANCE            = LF_CUOBJ
      tables
        ET_CONF_WITH_AUTHOR = LT_CONF
      exceptions
        INSTANCE_NOT_FOUND  = 1.
  endif.

*** Get Configuration Value
  loop at LT_CONF into LS_CONF.
    append initial line to GT_CFGS_VALUE assigning field-symbol(<S_BAPICUVAL>).
    move LF_CONFIG_ID                  to <S_BAPICUVAL>-CONFIG_ID.
    move LF_INST_ID                    to <S_BAPICUVAL>-INST_ID.
    move LS_CONF-ATNAM                 to <S_BAPICUVAL>-CHARC.
    move LS_CONF-ATWRT                 to <S_BAPICUVAL>-VALUE.
  endloop.
*}   INSERT

*** Update Flag for Configuration
  append value #( POSNR = LS_VBAP-POSNR
                  UPDKZ = 'I'
                ) to GT_CFGS_UPDKZ.


endform.

*&---------------------------------------------------------------------*
*&      Form  GET_COPY_NEW_POSNR
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form GET_COPY_NEW_POSNR .

  data: LS_VBAP      type VBAP,
        LS_VBKD      type VBKD,
        LS_VBEP      type VBEP,
        LS_KOMV      type KOMV,
        LV_MAX_POSNR type VBAP-POSNR.

  select single max( POSNR )
  from VBAP
  into LV_MAX_POSNR
  where VBELN = P_VBELN.
  check SY-SUBRC eq 0.


  clear GV_NEXT_POSNR.
  GV_NEXT_POSNR = LV_MAX_POSNR + 10.


*** Sales Document Item Data
  LS_VBAP = GT_VBAP[ POSNR = P_POSNR ].
  LS_VBAP-POSNR = GV_NEXT_POSNR.
  append LS_VBAP to GT_VBAP.

*** Sales Document Business Data
  if LINE_EXISTS( GT_VBKD[ POSNR = P_POSNR ] ).
    LS_VBKD = GT_VBKD[ POSNR = P_POSNR ].
    LS_VBKD-POSNR = GV_NEXT_POSNR.
    append LS_VBKD to GT_VBKD.
  endif.

*** Sales Document Schedule Line Data
  loop at GT_VBEP into LS_VBEP where POSNR eq P_POSNR
                                            and  WMENG ne 0.    "BAPI can't process Order Schedule Lines w/o Requirement Quantity
    LS_VBEP-POSNR = GV_NEXT_POSNR.
    append LS_VBEP to GT_VBEP.
  endloop.

*** Pricing Communications-Condition Record
  loop at GT_KOMV into LS_KOMV where KPOSN eq P_POSNR.
    LS_KOMV-KPOSN = GV_NEXT_POSNR.
    append LS_KOMV to GT_KOMV.
  endloop.

endform.
