*&---------------------------------------------------------------------*
*& Report  ZRSD0035
*&
*&---------------------------------------------------------------------*
*&
*&
*&---------------------------------------------------------------------*
REPORT zrsd0035.

TABLES: zssdt065, likp.



""""""""""""""""""""""
" Pantalla de selección
""""""""""""""""""""""

SELECTION-SCREEN BEGIN OF SCREEN 300 AS SUBSCREEN.
SELECT-OPTIONS: s_vkorg FOR zssdt065-vkorg, "Organizacion de ventas
                s_kschl FOR zssdt065-kschl, "Clase de mensaje
                s_kunag FOR likp-kunag, "Solicitante
                s_kunnr FOR likp-kunnr. "Destinatario de mercancias
SELECTION-SCREEN END OF SCREEN 300.


SELECTION-SCREEN BEGIN OF SCREEN 200 AS SUBSCREEN.
PARAMETERS: p_var2(10) TYPE c.
SELECTION-SCREEN END OF SCREEN 200.

SELECTION-SCREEN: BEGIN OF TABBED BLOCK mytab FOR 4 LINES,
                  TAB (20) tab_dest USER-COMMAND tab_dest,
                  TAB (20) tab_sol  USER-COMMAND tab_sol
                                   DEFAULT SCREEN 300,
                  END OF BLOCK mytab.


""""""""""""""""""""""
" Eventos
""""""""""""""""""""""

INITIALIZATION.
  tab_dest = 'Email destinatarios'.
  tab_sol = 'Email solicitantes'.
  mytab-prog = sy-repid.
  mytab-dynnr = 300.
  mytab-activetab = 'TAB_DEST'.



AT SELECTION-SCREEN OUTPUT.
  LOOP AT SCREEN.
    CASE mytab-activetab.
      WHEN 'TAB_DEST'.
        IF screen-name CP '*S_KUNAG*' or screen-name CP '*S_KUNNR*'.
          screen-active = 1.
          screen-output = 1.
        ENDIF.
      WHEN 'TAB_SOL'.
        IF screen-name CP '*S_KUNAG*' or screen-name CP '*S_KUNNR*'.
          screen-active = 0.
          screen-output = 0.
        ENDIF.
    ENDCASE.
    MODIFY SCREEN.
  ENDLOOP.



START-OF-SELECTION.
  PERFORM f_start_of_selection.


*&---------------------------------------------------------------------*
*&      Form  F_START_OF_SELECTION
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM f_start_of_selection .
  DATA: tl_dba_sellist TYPE TABLE OF vimsellist.

  CALL FUNCTION 'VIEW_RANGETAB_TO_SELLIST'
    EXPORTING
      fieldname = 'VKORG'
    TABLES
      sellist   = tl_dba_sellist
      rangetab  = s_vkorg.

  CALL FUNCTION 'VIEW_RANGETAB_TO_SELLIST'
    EXPORTING
      fieldname = 'KSCHL'
    TABLES
      sellist   = tl_dba_sellist
      rangetab  = s_kschl.

  IF mytab-activetab = 'TAB_DEST'.
    CALL FUNCTION 'VIEW_RANGETAB_TO_SELLIST'
      EXPORTING
        fieldname = 'KUNAG'
      TABLES
        sellist   = tl_dba_sellist
        rangetab  = s_kunag.

    CALL FUNCTION 'VIEW_RANGETAB_TO_SELLIST'
      EXPORTING
        fieldname = 'KUNNR'
      TABLES
        sellist   = tl_dba_sellist
        rangetab  = s_kunnr.
  ENDIF.

  DATA(vl_view_name) = SWITCH dd02v-tabname( mytab-activetab WHEN 'TAB_DEST' THEN 'ZSSDV065' WHEN 'TAB_SOL' THEN 'ZSSDV057_SOL' ).


  CALL FUNCTION 'VIEW_MAINTENANCE_CALL'
    EXPORTING
      action                       = 'U'
      view_name                    = vl_view_name
    TABLES
      dba_sellist                  = tl_dba_sellist
    EXCEPTIONS
      client_reference             = 1
      foreign_lock                 = 2
      invalid_action               = 3
      no_clientindependent_auth    = 4
      no_database_function         = 5
      no_editor_function           = 6
      no_show_auth                 = 7
      no_tvdir_entry               = 8
      no_upd_auth                  = 9
      only_show_allowed            = 10
      system_failure               = 11
      unknown_field_in_dba_sellist = 12
      view_not_found               = 13
      maintenance_prohibited       = 14
      OTHERS                       = 15.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.


ENDFORM.
