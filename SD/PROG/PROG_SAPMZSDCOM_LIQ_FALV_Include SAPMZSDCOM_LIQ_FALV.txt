*&---------------------------------------------------------------------*
*&  Include           SAPMZSDCOM_LIQ_FALV
*&---------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      Form  MOSTRAR_ALV_COM
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM mostrar_alv_com .

  DATA: lti_fieldcat TYPE lvc_t_fcat.
  DATA: ls_layout    TYPE lvc_s_layo,
        ls_variant   TYPE disvariant,
        ls_fcat      TYPE lvc_s_fcat.
  DATA: lv_repid     TYPE syrepid,
        lv_ind       TYPE i.

* Variante
  lv_repid          = sy-repid.
  ls_variant-report = lv_repid.
  ls_variant-handle = 'COMI'.

* Preparamos el catálogo de campos
  PERFORM prepare_fieldcat USING    'ZSSDCOMLIQ001_GRID'
                           CHANGING lti_fieldcat.
  LOOP AT lti_fieldcat INTO ls_fcat.
    lv_ind = sy-tabix.
    CASE ls_fcat-fieldname.
      WHEN 'GSBER'.
        ls_fcat-key     = 'X'.
      WHEN 'LIFNR'.
        ls_fcat-key     = 'X'.
        ls_fcat-hotspot = 'X'.
      WHEN 'NAME1'.
        ls_fcat-key     = 'X'.
      WHEN 'BELNR_LIQ'.
        ls_fcat-hotspot = 'X'.
    ENDCASE.
    MODIFY lti_fieldcat FROM ls_fcat INDEX lv_ind.
  ENDLOOP.

* Layout
  ls_layout-cwidth_opt = 'X'.
  ls_layout-grid_title = 'Listado de comisionistas a liquidar'(t90).
  ls_layout-sel_mode = 'A'.

* Mostramos el ALV
  CALL METHOD g_grid_com->set_table_for_first_display
    EXPORTING
      i_buffer_active    = ' '
      i_bypassing_buffer = 'X'
      is_layout          = ls_layout
      is_variant         = ls_variant
      i_save             = 'A'
    CHANGING
      it_outtab          = gti_comis
      it_fieldcatalog    = lti_fieldcat.

* Volvemos a ajustar el ancho de las columnas
  PERFORM refrescar_alv_com .

ENDFORM.                    " MOSTRAR_ALV_COM


*&---------------------------------------------------------------------*
*&      Form  PREPARE_FIELDCAT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_ESTRUCTURA   text
*      <--CTI_FIELDCAT  text
*----------------------------------------------------------------------*
FORM prepare_fieldcat  USING    p_estructura
                       CHANGING cti_fieldcat TYPE lvc_t_fcat.

* Recuperamos el catálogo de campos
  CALL FUNCTION 'LVC_FIELDCATALOG_MERGE'
    EXPORTING
      i_structure_name = p_estructura
    CHANGING
      ct_fieldcat      = cti_fieldcat.

ENDFORM.                    " PREPARE_FIELDCAT


*&---------------------------------------------------------------------*
*&      Form  REFRESCAR_ALV_COM
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM refrescar_alv_com .

  DATA: ls_layout TYPE lvc_s_layo,
        ls_stable TYPE lvc_s_stbl.

* Sacamos el layout del ALV
  CALL METHOD g_grid_com->get_frontend_layout
    IMPORTING
      es_layout = ls_layout.

* Ponemos de nuevo que optimice las columnas
  ls_layout-cwidth_opt = 'X'.

* Cargamos de nuevo el layout
  CALL METHOD g_grid_com->set_frontend_layout
    EXPORTING
      is_layout = ls_layout.

* Refrescamos el ALV
  ls_stable-row = 'X'.
  ls_stable-col = 'X'.
  CALL METHOD g_grid_com->refresh_table_display
    EXPORTING
      is_stable = ls_stable.

ENDFORM.                    " REFRESCAR_ALV_COM


*&---------------------------------------------------------------------*
*&      Form  MOSTRAR_ALV_FAC
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM mostrar_alv_fac .

  DATA: lti_fieldcat TYPE lvc_t_fcat,
        lti_exc_bot  TYPE ui_functions.
  DATA: ls_layout    TYPE lvc_s_layo,
        ls_variant   TYPE disvariant,
        ls_fcat      TYPE lvc_s_fcat.
  DATA: lv_repid     TYPE syrepid,
        lv_ind       TYPE i.

* Variante
  lv_repid          = sy-repid.
  ls_variant-report = lv_repid.
  ls_variant-handle = 'FACT'.

* Preparamos el catálogo de campos
  PERFORM prepare_fieldcat USING    'ZSSDCOMLIQ002_GRID'
                           CHANGING lti_fieldcat.
  LOOP AT lti_fieldcat INTO ls_fcat.
    lv_ind = sy-tabix.
    CASE ls_fcat-fieldname.
      WHEN 'CHECKBOX'.
        ls_fcat-checkbox = 'X'.
        ls_fcat-edit     = 'X'.
      WHEN 'BUKRS' OR 'LIFNR' OR 'VBELN' OR
           'BELNR' OR 'GJAHR' OR 'KSCHL'.
        ls_fcat-key = 'X'.
      WHEN 'ZIMP_LIQ'.
        ls_fcat-edit = 'X'.
    ENDCASE.
*   Campos navegables
    IF ls_fcat-fieldname = 'VBELN' OR ls_fcat-fieldname = 'BELNR'.
      ls_fcat-hotspot = 'X'.
    ENDIF.
    MODIFY lti_fieldcat FROM ls_fcat INDEX lv_ind.
  ENDLOOP.

* Layout
  ls_layout-cwidth_opt = 'X'.
  ls_layout-grid_title = 'Listado de facturas del comisionista'(t91).

* Quitamos los botones del ALV editable
  APPEND cl_gui_alv_grid=>mc_fg_edit TO lti_exc_bot.

* Mostramos el ALV
  CALL METHOD g_grid_fac->set_table_for_first_display
    EXPORTING
      i_buffer_active      = ' '
      i_bypassing_buffer   = 'X'
      is_layout            = ls_layout
      is_variant           = ls_variant
      i_save               = 'A'
      it_toolbar_excluding = lti_exc_bot
    CHANGING
      it_outtab            = gti_facts_com
      it_fieldcatalog      = lti_fieldcat.

* El ALV NO es editable en visualización
  IF gv_modo = 'V'.
    CALL METHOD g_grid_fac->set_ready_for_input
      EXPORTING
        i_ready_for_input = 0.
  ENDIF.

* Volvemos a ajustar el ancho de las columnas
  PERFORM refrescar_alv_fac.

ENDFORM.                    " MOSTRAR_ALV_FAC


*&---------------------------------------------------------------------*
*&      Form  REFRESCAR_ALV_FAC
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM refrescar_alv_fac .

  DATA: ls_layout TYPE lvc_s_layo,
        ls_stable TYPE lvc_s_stbl.

* Sacamos el layout del ALV
  CALL METHOD g_grid_fac->get_frontend_layout
    IMPORTING
      es_layout = ls_layout.

* Ponemos de nuevo que optimice las columnas
  ls_layout-cwidth_opt = 'X'.

* Cargamos de nuevo el layout
  CALL METHOD g_grid_fac->set_frontend_layout
    EXPORTING
      is_layout = ls_layout.

* Refrescamos el ALV
  ls_stable-row = 'X'.
  ls_stable-col = 'X'.
  CALL METHOD g_grid_fac->refresh_table_display
    EXPORTING
      is_stable = ls_stable.

ENDFORM.                    " REFRESCAR_ALV_FAC
