*----------------------------------------------------------------------*
***INCLUDE ZSSDD038_SUB.
*----------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  AGREGA_CANTIDAD_TABLA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      <--P_ITAB_LIST_ENTREGA  text
*----------------------------------------------------------------------*
FORM agrega_cantidad_tabla USING pt_cant_agrega_ad TYPE tt_zssdt038
                                 pt_tdlnr          TYPE tt_tdlnr_aux
                                 pt_dd07v_tab      TYPE tt_dd07v
                                 pt_name           TYPE tt_name
                                 pt_kunn2_cliente  TYPE tt_kunnr_cliente
                           CHANGING c_itab_list_entrega TYPE tt_list_entrega.

  SELECT a~matnr, a~laeng, a~breit, a~hoehe, a~meabm, b~maktx
  INTO TABLE @DATA(lt_mara_aux)
   FROM mara AS a INNER JOIN makt AS b ON a~matnr EQ b~matnr
     FOR ALL ENTRIES IN @pt_cant_agrega_ad
   WHERE a~matnr EQ @pt_cant_agrega_ad-matnr
     AND spras EQ @sy-langu.

  SELECT DISTINCT a~matnr, a~vkorg, a~vtweg, a~mvgr1, b~bezei
    INTO TABLE @DATA(lt_mvke_aux)
    FROM mvke AS a INNER JOIN tvm1t AS b ON a~mvgr1 EQ b~mvgr1
         FOR ALL ENTRIES IN @pt_cant_agrega_ad
     WHERE a~matnr = @pt_cant_agrega_ad-matnr
       AND a~vkorg = @pt_cant_agrega_ad-vkorg
       AND a~vtweg = '10'
       AND b~spras = @sy-langu.

  LOOP AT pt_cant_agrega_ad INTO DATA(ls_cant).

    READ TABLE c_itab_list_entrega INTO DATA(ls_list_entrega) WITH KEY vstel = ls_cant-vstel
                                                                       kunwe = ls_cant-kunwe
                                                                       matnr = ls_cant-matnr.
    IF sy-subrc NE 0.
      MOVE-CORRESPONDING ls_cant TO ls_list_entrega.
      ls_list_entrega-vrkme = ls_cant-meins.

      ls_list_entrega-tdlnr     = ls_cant-tdlnr.
      READ TABLE pt_tdlnr INTO DATA(s_tdlnr) WITH KEY lifnr = ls_cant-tdlnr.
      IF sy-subrc = 0.
        ls_list_entrega-tdlnr_name1 = s_tdlnr-name1.
      ENDIF.

      ls_list_entrega-vkorg     = ls_cant-vkorg.
      ls_list_entrega-vstel     = ls_cant-vstel.
      ls_list_entrega-kunwe     = ls_cant-kunwe.
      READ TABLE pt_name INTO DATA(s_name) WITH KEY kunnr = ls_cant-kunwe.
      IF sy-subrc EQ 0.
        ls_list_entrega-name1     = s_name-name1.
      ENDIF.

      CLEAR s_name.
      READ TABLE  pt_kunn2_cliente INTO DATA(ls_cliente) WITH KEY vkorg = ls_cant-vkorg
                                                                  kunn2 = ls_cant-kunwe.
      IF sy-subrc EQ 0.
        ls_list_entrega-kun_c = ls_cliente-kunnr.
        READ TABLE pt_name INTO s_name WITH KEY kunnr = ls_cliente-kunnr.
        IF sy-subrc EQ 0.
          ls_list_entrega-name_c = s_name-name1.
        ENDIF.
      ENDIF.

      ls_list_entrega-wadat_ist = ls_cant-budat.
      ls_list_entrega-matnr     = ls_cant-matnr.
      READ TABLE lt_mara_aux INTO DATA(s_mara) WITH KEY matnr =  ls_cant-matnr.
      IF sy-subrc EQ 0.
        ls_list_entrega-laeng     = s_mara-laeng.
        ls_list_entrega-breit     = s_mara-breit.
        ls_list_entrega-hoehe     = s_mara-hoehe.
        ls_list_entrega-maktx     = s_mara-maktx.
      ENDIF.
      READ TABLE lt_mvke_aux INTO DATA(s_mvke) WITH KEY matnr = ls_cant-matnr
                                                    vkorg = ls_cant-vkorg.
      IF sy-subrc EQ 0.
        ls_list_entrega-mvgr1     = s_mvke-mvgr1.
        ls_list_entrega-bezei     = s_mvke-bezei.
      ENDIF.

      " Entregado Manual
      IF  ls_cant-cant_ent IS NOT INITIAL.
        ls_list_entrega-tipo_reg = c_02.
        READ TABLE pt_dd07v_tab INTO DATA(s_dd07v) WITH KEY domvalue_l = ls_list_entrega-tipo_reg.
        IF sy-subrc EQ 0.
          ls_list_entrega-ddtext = s_dd07v-ddtext.
        ENDIF.
        ls_list_entrega-lfimg = ls_cant-cant_ent.
        COLLECT ls_list_entrega INTO c_itab_list_entrega.
      ENDIF.

      " Ajuste Manual
      IF ls_cant-cant_rec IS NOT INITIAL.
        ls_list_entrega-tipo_reg = c_03.
        READ TABLE pt_dd07v_tab INTO s_dd07v WITH KEY domvalue_l = ls_list_entrega-tipo_reg.
        IF sy-subrc EQ 0.
          ls_list_entrega-ddtext = s_dd07v-ddtext.
        ENDIF.
        ls_list_entrega-lfimg = ls_cant-cant_rec.
        COLLECT ls_list_entrega INTO c_itab_list_entrega.
      ENDIF.
    ENDIF.
    CLEAR: ls_list_entrega, ls_cant, s_dd07v, s_mara, s_mvke.
  ENDLOOP.

ENDFORM.
