*&---------------------------------------------------------------------*
*& Report  ZRSD0027
*&
*&---------------------------------------------------------------------*
*& Lista distribución envío de facturas
*& Javier Calás. 27.12.22
*&---------------------------------------------------------------------*
REPORT zrsd0027.


""""""""""""""""""""""
" Tipos de datos
""""""""""""""""""""""
TABLES: zssdt057.

""""""""""""""""""""""
" Pantalla de selección
""""""""""""""""""""""



SELECTION-SCREEN BEGIN OF SCREEN 300 AS SUBSCREEN.
SELECT-OPTIONS: p_vkorg FOR zssdt057-vkorg,
                p_kschl FOR zssdt057-kschl,
                p_kunag FOR zssdt057-kunag.
SELECTION-SCREEN END OF SCREEN 300.


SELECTION-SCREEN BEGIN OF SCREEN 200 AS SUBSCREEN.
PARAMETERS: p_var2(10) TYPE c.
SELECTION-SCREEN END OF SCREEN 200.

SELECTION-SCREEN: BEGIN OF TABBED BLOCK mytab FOR 4 LINES,
                  TAB (20) tab_dest USER-COMMAND tab_dest,
                  TAB (20) tab_sol  USER-COMMAND tab_sol
                                   DEFAULT SCREEN 300,
                  END OF BLOCK mytab.




""""""""""""""""""""""
" Eventos
""""""""""""""""""""""

INITIALIZATION.
  tab_dest = 'Email destinatarios'.
  tab_sol = 'Email solicitantes'.
  mytab-prog = sy-repid.
  mytab-dynnr = 300.
  mytab-activetab = 'TAB_DEST'.



AT SELECTION-SCREEN OUTPUT.
  LOOP AT SCREEN.
    CASE mytab-activetab.
      WHEN 'TAB_DEST'.
        IF screen-name CP '*P_KUNAG*'.
          screen-active = 1.
          screen-output = 1.
        ENDIF.
      WHEN 'TAB_SOL'.
        IF screen-name CP '*P_KUNAG*'.
          screen-active = 0.
          screen-output = 0.
        ENDIF.
    ENDCASE.
    MODIFY SCREEN.
  ENDLOOP.



AT SELECTION-SCREEN.
  CASE sy-ucomm.
    WHEN 'TAB_DEST'.
      mytab-activetab = 'TAB_DEST'.
    WHEN 'TAB_SOL'.
      mytab-activetab = 'TAB_SOL'.
  ENDCASE.


AT SELECTION-SCREEN ON p_vkorg.
  IF sy-ucomm <> 'TAB_DEST' AND sy-ucomm <> 'TAB_SOL'.
    IF p_vkorg IS INITIAL.
      MESSAGE 'Debe indicar org. ventas' TYPE 'E'.
    ENDIF.
  ENDIF.



START-OF-SELECTION.
  PERFORM f_start_of_selection.




*&---------------------------------------------------------------------*
*&      Form  F_START_OF_SELECTION
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM f_start_of_selection .

  DATA: tl_dba_sellist TYPE TABLE OF vimsellist.
  CALL FUNCTION 'VIEW_RANGETAB_TO_SELLIST'
    EXPORTING
      fieldname          = 'VKORG'
      append_conjunction = 'AND'
    TABLES
      sellist            = tl_dba_sellist
      rangetab           = p_vkorg[].
  CALL FUNCTION 'VIEW_RANGETAB_TO_SELLIST'
    EXPORTING
      fieldname          = 'KSCHL'
      append_conjunction = 'AND'
    TABLES
      sellist            = tl_dba_sellist
      rangetab           = p_kschl[].

  IF mytab-activetab = 'TAB_DEST'.
    CALL FUNCTION 'VIEW_RANGETAB_TO_SELLIST'
      EXPORTING
        fieldname          = 'KUNAG'
        append_conjunction = 'AND'
      TABLES
        sellist            = tl_dba_sellist
        rangetab           = p_kunag[].
  ENDIF.


  DATA(vl_view_name) = SWITCH dd02v-tabname( mytab-activetab WHEN 'TAB_DEST' THEN 'ZSSDV057' WHEN 'TAB_SOL' THEN 'ZSSDV057_SOL' ).


  CALL FUNCTION 'VIEW_MAINTENANCE_CALL'
    EXPORTING
      action                       = 'U'
      view_name                    = vl_view_name
    TABLES
      dba_sellist                  = tl_dba_sellist
    EXCEPTIONS
      client_reference             = 1
      foreign_lock                 = 2
      invalid_action               = 3
      no_clientindependent_auth    = 4
      no_database_function         = 5
      no_editor_function           = 6
      no_show_auth                 = 7
      no_tvdir_entry               = 8
      no_upd_auth                  = 9
      only_show_allowed            = 10
      system_failure               = 11
      unknown_field_in_dba_sellist = 12
      view_not_found               = 13
      maintenance_prohibited       = 14
      OTHERS                       = 15.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.
