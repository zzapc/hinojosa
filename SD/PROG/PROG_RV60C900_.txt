FORM DATEN_KOPIEREN_900.
*{   INSERT         HEDK905991                                        2
* rutina para tener en cuenta las diferentes opciones de agrupación
* de facturas que puedan tener los clientes.
    DATA: BEGIN OF ZUK,
          VKORG LIKE VBAK-VKORG,
          VTWEG LIKE VBAK-VTWEG,
          SPART LIKE VBAK-SPART,
          AGRUPADOR(10),
        END OF ZUK.
* SCT 20.04.18 Ticket 48184 Nota OSS 36832 para que el campo REGIO no se pase
* a la cabecera factura y nos determine partición de facturas como estaba
* haciendo por estandar
  VBRK-REGIO = SPACE.
* Fin SCT 20.04.18

* SCT 20.04.18 Ticket 48184 blanqueo Incoterm2 en Cipasi
* para que no parta facturas por este campo si cliente ES
  IF VBRK-VKORG = '4000' AND VBRK-LAND1 = 'ES'.
    VBRK-INCO2 = SPACE.
  ENDIF.
*Fin SCT 20.04.18



  CLEAR ZUK.

  ZUK-VKORG = VBAK-VKORG.
  ZUK-VTWEG = VBAK-VTWEG.
  ZUK-SPART = VBAK-SPART.

  CASE VBAK-KVGR3.
     when 'C01'.  "Factura por cliente (Solicitante)
        IF LIKP-KUNAG is initial.
           ZUK-AGRUPADOR = VBAK-KUNNR.
        ELSE.
           ZUK-AGRUPADOR = LIKP-KUNAG.
        ENDIF.
     when 'C02'.  "Factura por destinatario de mercancia
        IF LIKP-KUNNR is initial.
           ZUK-AGRUPADOR = VBPA-KUNNR.
        ELSE.
           ZUK-AGRUPADOR = LIKP-KUNNR.
        ENDIF.
     when 'C03'.  "Factura por pedido
           ZUK-AGRUPADOR = VBAK-VBELN.
     when 'C04'.  "Factura por entrega
           ZUK-AGRUPADOR = LIKP-VBELN.
     when 'C05'.  "Factura NºPedido Cliente.
           ZUK-AGRUPADOR = VBKD-BSTKD.
  endcase.

  VBRK-ZUKRI = ZUK.

*}   INSERT
ENDFORM.
