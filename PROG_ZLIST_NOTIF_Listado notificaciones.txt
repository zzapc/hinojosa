***********************************************************************
* APLICACION : PP
* TIPO : LISTADO
* TITULO : Listado notificaciones
* *
* DESCRIPCION : Listado notificaciones
*
*
* AUTOR: Andrés Picazo                                FECHA: 07/12/2012
*
* MODIFICACIONES
*
***********************************************************************
REPORT zlist_notif.

*------INCLUDES--------------------------------------------------------*

*------TABLAS/ESTRUCTURAS----------------------------------------------*
TABLES: afru, sscrfields.

*------TABLAS INTERNAS-------------------------------------------------*
TYPES: BEGIN OF t_listado,
         aufnr TYPE afru-aufnr,
         vornr TYPE afru-vornr,
         ktsch TYPE afvc-ktsch,
         rueck TYPE afru-rueck,
         rmzhl TYPE afru-rmzhl,
         ersda TYPE afru-ersda,
         isdd  TYPE afru-isdd,
         isdz  TYPE afru-isdz,
         iedd  TYPE afru-iedd,
         iedz  TYPE afru-iedz,
         stokz TYPE afru-stokz,
         stzhl TYPE afru-stzhl,
         gmnga TYPE afru-gmnga,
         gmein TYPE afru-gmein,
         lmnga TYPE afru-lmnga,
         xmnga TYPE afru-xmnga,
         meinh TYPE afru-meinh,
END OF t_listado.
DATA: i_listado TYPE TABLE OF t_listado,
      l_listado TYPE t_listado.

FIELD-SYMBOLS <listado> TYPE t_listado.

*------VARIABLES-------------------------------------------------------*
DATA o_prog TYPE REF TO zcl_ap_dev.

*----------------------------------------------------------------------*
* CLASS lcl_alv DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_alv DEFINITION INHERITING FROM zcl_ap_alv. "_check.
  PUBLIC SECTION.
    METHODS: handle_double_click REDEFINITION.
    METHODS: handle_user_command REDEFINITION.
    METHODS: top_of_page REDEFINITION.
ENDCLASS. "lcl_alv DEFINITION

DATA: o_alv TYPE REF TO lcl_alv.

*------PARAMETER/SELECT-OPTIONS EN PANTALLA----------------------------*
SELECTION-SCREEN BEGIN OF BLOCK 001 WITH FRAME.
SELECT-OPTIONS: s_aufnr FOR afru-aufnr,
                s_vornr FOR afru-vornr,
                s_ersda FOR afru-ersda.
SELECTION-SCREEN: SKIP 1.
PARAMETERS: p_ok AS CHECKBOX DEFAULT 'X',
            p_ko AS CHECKBOX DEFAULT 'X'.
SELECTION-SCREEN: SKIP 1.
PARAMETERS: p_vari LIKE disvariant-variant.
SELECTION-SCREEN END OF BLOCK 001.
SELECTION-SCREEN: FUNCTION KEY 1.
SELECTION-SCREEN: FUNCTION KEY 2.
SELECTION-SCREEN: FUNCTION KEY 3.


************************************************************************
*
* LOGICA DEL PROGRAMA
*
************************************************************************

*----------------------------------------------------------------------*
* CLASS lcl_alv IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_alv IMPLEMENTATION.
  METHOD handle_double_click.
    READ TABLE i_listado INDEX row INTO l_listado.
    IF sy-subrc = 0.
      zcl_ap_orden_pp=>ver_notificacion( rueck = l_listado-rueck
                                         rmzhl = l_listado-rmzhl ).
    ENDIF.
  ENDMETHOD. "handle_double_click
  METHOD handle_user_command.
    DATA: l_row TYPE i.

    CASE e_salv_function.
      WHEN 'EXCEL'.
        exportar_excel( ).

      WHEN OTHERS.
    ENDCASE.
  ENDMETHOD. "handle_USER_COMMAND
  METHOD top_of_page.
    CLEAR o_content.

    o_top_page->set_titulo( sy-title ).

    IF NOT s_aufnr[] IS INITIAL.
      APPEND 'Filtros para ejecución:' TO o_top_page->i_filtros.
      o_top_page->add_rango( texto = 'Orden' rango = s_aufnr[] ).
      o_top_page->crea_info_seleccion( ).
    ENDIF.

    o_content = o_top_page->get_grid( ).

  ENDMETHOD.                    "top_of_page
ENDCLASS. "lcl_alv IMPLEMENTATION

*----------------------------------------------------------------------*
* INITIALIZATION
*----------------------------------------------------------------------*
INITIALIZATION.

  CREATE OBJECT o_prog
    EXPORTING
      status = ''.

  o_prog->initialization( CHANGING sscrfields = sscrfields ).

  CREATE OBJECT o_alv
    EXPORTING
      status = 'STANDARD'.
  p_vari = o_alv->get_default_layout( ).

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_vari.

  p_vari = o_alv->get_f4_layout( ).

************************************************************************
* AT SELECTION-SCREEN.
************************************************************************
AT SELECTION-SCREEN.

  o_prog->at_selection(  ).

*----------------------------------------------------------------------
* START-OF-SELECTION.
*----------------------------------------------------------------------*
START-OF-SELECTION.

  PERFORM leer_datos.
  PERFORM listado.

************************************************************************
*
* FORMS ADICIONALES
*
************************************************************************
*&---------------------------------------------------------------------*
*& Form listado
*&---------------------------------------------------------------------*
FORM listado .

  o_prog->sgpi_texto( 'Generando informe' ).
  o_alv->set_layout( p_vari ).
  o_alv->set_top_of_page( ).
  o_alv->set_orden( campo = 'AUFNR,VORNR' subtot = 'X' ).
  o_alv->set_orden( 'ISDD,ISDZ' ).
  o_alv->set_agregacion( 'GMNGA,LMNGA,XMNGA' ).

  o_alv->show( ).

ENDFORM. " listado
*&---------------------------------------------------------------------*
*&      Form  LEER_DATOS
*&---------------------------------------------------------------------*
FORM leer_datos .

  SELECT * FROM afru
    INTO CORRESPONDING FIELDS OF TABLE i_listado
   WHERE aufnr IN s_aufnr
     AND vornr IN s_vornr
     AND ersda IN s_ersda.

  IF p_ok IS INITIAL.
    DELETE i_listado WHERE lmnga NE 0
                       AND xmnga = 0.
    DELETE i_listado WHERE xmnga = 0
                       AND lmnga = 0.
  ENDIF.

  IF p_ko IS INITIAL.
    DELETE i_listado WHERE xmnga NE 0
                       AND lmnga = 0.
    DELETE i_listado WHERE xmnga = 0
                       AND lmnga = 0.
  ENDIF.

  LOOP AT i_listado ASSIGNING <listado> WHERE NOT stzhl IS INITIAL.
    <listado>-gmnga = - <listado>-gmnga.
    <listado>-xmnga = - <listado>-xmnga.
    <listado>-lmnga = - <listado>-lmnga.
  ENDLOOP.

  LOOP AT i_listado ASSIGNING <listado>.
    SELECT SINGLE ktsch
    INTO <listado>-ktsch
    FROM afvc JOIN afko ON afvc~aufpl = afko~aufpl
   WHERE afko~aufnr = <listado>-aufnr
     AND vornr      = <listado>-vornr.
  ENDLOOP.

ENDFORM.                    " LEER_DATOS
