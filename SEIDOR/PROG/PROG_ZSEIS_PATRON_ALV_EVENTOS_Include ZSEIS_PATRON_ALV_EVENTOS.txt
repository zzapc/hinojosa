*&---------------------------------------------------------------------*
*&  Include           ZSEIS_PATRON_ALV_EVENTOS
*&---------------------------------------------------------------------*

**************************************+
* EVENTOS
**************************************+

**************************************+
*   INITIALIZATION
**************************************+
INITIALIZATION.
  vg_stable-row = 'X'.
  vg_stable-col = 'X'.

  wg_authority_check-nombre = 'Z_GL_APLIC'.
  wg_authority_check-campo  = 'ZGLAC_APPS'.
  wg_authority_check-valor  = sy-cprog.

  SELECT *
    INTO TABLE tg_d347t
    FROM d347t
    WHERE progname  = sy-cprog AND
          sprsl     = sy-langu.


  vg_ventana_inicial = '0100'.
  PERFORM f_initialization.
  vg_ventana_inicial_ini = vg_ventana_inicial.
  PERFORM f_validar_authority_check.



**************************************+
*   START-OF-SELECTION
**************************************+
START-OF-SELECTION.
  FIELD-SYMBOLS: <fs_datos_cabecera> TYPE any.

  PERFORM f_start_of_selection.



FORM f_start_of_selection.
  "Cálculos antes de calcular los datos del ALV
  PERFORM f_pre_llenar_datos IN PROGRAM (sy-cprog) IF FOUND.

  "Cálculo de los datos del ALV
  PERFORM f_llenar_datos TABLES it_alv USING vg_ventana_inicial.
  READ TABLE tg_config_edit ASSIGNING FIELD-SYMBOL(<fs_config_edit>) WITH KEY dynpro = vg_ventana_inicial.
  IF sy-subrc = 0 AND <fs_config_edit>-es_cabecera = 'X'.
    ASSIGN (<fs_config_edit>-nombre_workarea) TO <fs_datos_cabecera>.
    IF <fs_config_edit>-borrado_logico = 'X'.
      ASSIGN COMPONENT <fs_config_edit>-campo_borrado_logico OF STRUCTURE <fs_datos_cabecera> TO FIELD-SYMBOL(<fs_borrado>).
      IF sy-subrc = 0 AND <fs_borrado> = 'X'.
        vg_cabecera_borrada = 'X'.
      ENDIF.
    ENDIF.
  ENDIF.




  LOOP AT tg_config_subscreen ASSIGNING FIELD-SYMBOL(<fs_config_subscreen>) WHERE dynpro = vg_ventana_inicial.
    PERFORM f_llenar_datos TABLES <fs_config_subscreen>-tabla USING <fs_config_subscreen>-subscreen.
  ENDLOOP.

  PERFORM f_ejecutar_objeto_bloqueo USING vg_ventana_inicial 'ENQUEUE' CHANGING it_alv.
  PERFORM f_set_estilos_alv         USING vg_ventana_inicial CHANGING it_alv.

  IF vg_ventana_inicial = 0.
    vg_ventana_inicial = vg_ventana_inicial_ini.
  ELSE.
    "Llamada a la dynpro a mostrar
    CALL SCREEN vg_ventana_inicial.
  ENDIF.
ENDFORM.


FORM f_set_estilos_alv USING vp_dynnr
                       CHANGING tp_alv TYPE tt_lin_alv_patron.
  "Estilos de las celdas
  DATA: wl_estilo TYPE lvc_s_styl.
  FIELD-SYMBOLS: <it_estilos> TYPE ANY TABLE.


  LOOP AT tp_alv ASSIGNING FIELD-SYMBOL(<fs_alv>).
    ASSIGN COMPONENT 'IT_ESTILOS' OF STRUCTURE <fs_alv> TO <it_estilos>.
    CHECK sy-subrc = 0.


    "Miramos si la fila está borrada lógicamente
    READ TABLE tg_config_edit ASSIGNING FIELD-SYMBOL(<fs_config_edit>) WITH KEY dynpro = vp_dynnr.
    IF sy-subrc = 0 AND <fs_config_edit>-borrado_logico = 'X' AND <fs_config_edit>-campo_borrado_logico IS NOT INITIAL.
      ASSIGN COMPONENT <fs_config_edit>-campo_borrado_logico OF STRUCTURE <fs_alv> TO FIELD-SYMBOL(<fs_borrado>).
    ENDIF.


    IF <fs_borrado> IS ASSIGNED AND <fs_borrado> = 'X' OR <fs_alv>-zzbloqueada = 'X'.
      DATA rl_descr_ref TYPE REF TO cl_abap_structdescr.
      cl_abap_structdescr=>describe_by_name(
        EXPORTING
          p_name         = 'ST_LIN_ALV'
        RECEIVING
          p_descr_ref    = DATA(rl_descr_ref_aux)
        EXCEPTIONS
          type_not_found = 1
          OTHERS         = 2  ).
      IF sy-subrc = 0.
        rl_descr_ref ?= rl_descr_ref_aux.
        DATA(tl_components) = rl_descr_ref->get_components( ) .
        LOOP AT tl_components ASSIGNING FIELD-SYMBOL(<fs_component>).
          wl_estilo-fieldname = <fs_component>-name.
          wl_estilo-style = c_estilo_disabled.
          INSERT wl_estilo INTO TABLE <it_estilos>.
        ENDLOOP.
      ENDIF.
    ENDIF.


    "Colocamos el formato protegido para las celdas de la clave
    IF <it_estilos> IS ASSIGNED AND <it_estilos> IS INITIAL.
      LOOP AT it_alv_pk ASSIGNING FIELD-SYMBOL(<fs_alv_pk>) WHERE dynpro = vp_dynnr.
        wl_estilo-fieldname = <fs_alv_pk>-campo_pk.
        wl_estilo-style = c_estilo_disabled.
        INSERT wl_estilo INTO TABLE <it_estilos>.
      ENDLOOP.
    ENDIF.
  ENDLOOP.


ENDFORM.
