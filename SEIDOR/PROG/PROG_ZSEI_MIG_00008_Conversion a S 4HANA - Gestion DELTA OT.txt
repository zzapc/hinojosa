*---------------------------------------------------------------------*
* Report ZSEI_MIG_00008
*---------------------------------------------------------------------*

REPORT zsei_mig_00008.

SELECTION-SCREEN BEGIN OF BLOCK va0 WITH FRAME TITLE TEXT-007.
  PARAMETERS: log_dest LIKE rfcdes-rfcdest  DEFAULT ' '." OBLIGATORY.
  PARAMETERS: log_ord  LIKE rfcdes-rfcdest  DEFAULT ' '.
SELECTION-SCREEN END OF BLOCK va0.

SELECTION-SCREEN BEGIN OF BLOCK va1 WITH FRAME TITLE TEXT-001.
  PARAMETERS: p_req    TYPE char20.
  PARAMETERS: p_file   LIKE file_table-filename.
SELECTION-SCREEN END OF BLOCK va1.

SELECTION-SCREEN BEGIN OF BLOCK var WITH FRAME TITLE TEXT-002.
  PARAMETERS: p_vari TYPE disvariant-variant.
SELECTION-SCREEN END OF BLOCK var.

INCLUDE zsei_mig_00008_top.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_file.
  PERFORM file_open.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_vari.
  PERFORM f4_for_variant.

AT SELECTION-SCREEN.
  PERFORM pai_of_selection_screen.

INITIALIZATION.
  PERFORM variant_init.

START-OF-SELECTION.

  IF p_req IS NOT INITIAL AND p_file IS INITIAL.
    PERFORM fill_excel.
  ENDIF.

  IF p_file IS NOT INITIAL AND p_req IS INITIAL.
    PERFORM upload_excel.
  ENDIF.

  PERFORM loop_excel.
  PERFORM create_and_display_alv.

*---------------------------------------------------------------------*
* Form CREATE_AND_DISPLAY_ALV
*---------------------------------------------------------------------*
FORM create_and_display_alv.

  PERFORM alv_layout.
  PERFORM alv_field_catalog.
  PERFORM alv_display.

ENDFORM.

*---------------------------------------------------------------------*
*      Form  ALV_FIELD_CATALOG
*---------------------------------------------------------------------*
FORM alv_field_catalog.

  CLEAR: gt_fieldcat.

  CLEAR: gs_fieldcat.
  gs_fieldcat-fieldname = 'OBJNAME'.
  gs_fieldcat-ref_table = 'VRSD'.
  gs_fieldcat-ref_field = 'OBJNAME'.
  gs_fieldcat-outputlen = '40'.
  APPEND gs_fieldcat TO gt_fieldcat.

  CLEAR: gs_fieldcat.
  gs_fieldcat-fieldname = 'OBJTYP1'.
  gs_fieldcat-ref_table = 'VRSD'.
  gs_fieldcat-ref_field = 'OBJTYPE'.
  APPEND gs_fieldcat TO gt_fieldcat.

  CLEAR: gs_fieldcat.
  gs_fieldcat-fieldname = 'OBJTYPE'.
  gs_fieldcat-ref_table = 'VRSD'.
  gs_fieldcat-ref_field = 'OBJTYPE'.
  APPEND gs_fieldcat TO gt_fieldcat.

  CLEAR: gs_fieldcat.
  gs_fieldcat-fieldname = 'ORDEN'.
  gs_fieldcat-ref_table = 'VRSD_OLD'.
  gs_fieldcat-ref_field = 'KORRNUM'.
  gs_fieldcat-scrtext_s = TEXT-003.
  gs_fieldcat-coltext   = TEXT-003.
  gs_fieldcat-seltext   = TEXT-003.
  APPEND gs_fieldcat TO gt_fieldcat.

  CLEAR: gs_fieldcat.
  gs_fieldcat-fieldname = 'DESC'.
  gs_fieldcat-scrtext_s = TEXT-005.
  gs_fieldcat-coltext   = TEXT-005.
  gs_fieldcat-seltext   = TEXT-005.
  gs_fieldcat-outputlen = '40'.
  APPEND gs_fieldcat TO gt_fieldcat.

  CLEAR: gs_fieldcat.
  gs_fieldcat-fieldname = 'ICON'.
  gs_fieldcat-scrtext_s = TEXT-004.
  gs_fieldcat-coltext   = TEXT-004.
  gs_fieldcat-seltext   = TEXT-004.
  APPEND gs_fieldcat TO gt_fieldcat.

*  CLEAR: gs_fieldcat.
*  gs_fieldcat-fieldname = 'VERSNO1'.
*  gs_fieldcat-ref_table = 'VRSD'.
*  gs_fieldcat-ref_field = 'VERSNO'.
*  gs_fieldcat-lzero     = 'X'.
*  APPEND gs_fieldcat TO gt_fieldcat.

*  CLEAR: gs_fieldcat.
*  gs_fieldcat-fieldname = 'DELTA'.
*  gs_fieldcat-scrtext_s = TEXT-000.
*  gs_fieldcat-coltext   = TEXT-000.
*  gs_fieldcat-seltext   = TEXT-000.
*  APPEND gs_fieldcat TO gt_fieldcat.

ENDFORM.

*---------------------------------------------------------------------*
*      Form  ALV_DISPLAY
*---------------------------------------------------------------------*
FORM alv_display.

  g_repid = sy-cprog.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY_LVC'
    EXPORTING
      i_callback_program       = g_repid
      i_callback_pf_status_set = 'STATUS_SET'
      i_callback_user_command  = 'USER_COMMAND'
      is_layout_lvc            = gs_layout
      i_default                = 'X'
      i_save                   = 'A'
      it_fieldcat_lvc          = gt_fieldcat
      is_variant               = g_variant
    TABLES
      t_outtab                 = gt_alv[]
    EXCEPTIONS
      program_error            = 1
      OTHERS                   = 2.

ENDFORM.

*---------------------------------------------------------------------*
*      Form  STATUS_SET
*---------------------------------------------------------------------*
FORM status_set USING rt_extab TYPE slis_t_extab.

  SET PF-STATUS 'ZSTATUS'.

ENDFORM.

*---------------------------------------------------------------------*
*      Form  user_command
*---------------------------------------------------------------------*
FORM user_command USING l_ucomm     TYPE sy-ucomm
                        l_selfield  TYPE slis_selfield.

  DATA:
    lv_trkorr      TYPE trkorr.

  CASE l_ucomm.
    WHEN 'REFRESH'.
      PERFORM loop_excel.

    WHEN '&IC1'.
      READ TABLE gt_alv INTO gs_alv INDEX l_selfield-tabindex.
      IF sy-subrc EQ 0.
        CASE l_selfield-fieldname.
          WHEN 'ORDEN' OR 'DESC'.

            lv_trkorr = gs_alv-orden.

            CALL FUNCTION 'TR_DISPLAY_REQUEST'
              EXPORTING
                i_trkorr = lv_trkorr.

          WHEN OTHERS.

            CASE gs_alv-objtype.
              WHEN 'TABD'.
                SUBMIT radvvtb2 AND RETURN
                  WITH objname  = gs_alv-objname
                  WITH objnam2  = gs_alv-objname
                  WITH versno1  = '00000'
                  WITH versno2  = '00000'
                  WITH infoln1a = ''
                  WITH infoln1b = ''
                  WITH infoln2a = ''
                  WITH infoln2b = ''
                  WITH log_dest = log_dest
                  WITH rem_syst = 'R3D'.
              WHEN 'REPS'.
                SUBMIT rsvrsrs3 AND RETURN
                  WITH objname  = gs_alv-objname
                  WITH objnam2  = gs_alv-objname
                  WITH versno1  = '00000'
                  WITH versno2  = '00000'
                  WITH objtyp1  = gs_alv-objtype
                  WITH objtyp2  = gs_alv-objtype
                  WITH infoln1a = ''
                  WITH infoln1b = ''
                  WITH infoln2a = ''
                  WITH infoln2b = ''
                  WITH log_dest = log_dest.
              WHEN 'DTED'.
                SUBMIT radvvde2 AND RETURN
                  WITH objname = gs_alv-objname
                  WITH objnam2 = gs_alv-objname
                  WITH versno1 = '00000'
                  WITH versno2 = '00000'
                  WITH infoln1a = ''
                  WITH infoln1b = ''
                  WITH infoln2a = ''
                  WITH infoln2b = ''
                  WITH log_dest = log_dest.
            ENDCASE.

        ENDCASE.
      ENDIF.
  ENDCASE.

  l_selfield-refresh    = 'X'.
  l_selfield-col_stable = 'X'.
  l_selfield-row_stable = 'X'.

ENDFORM.

*---------------------------------------------------------------------*
*      Form  ALV_LAYOUT
*---------------------------------------------------------------------*
FORM alv_layout.

  gs_layout-zebra      = 'X'.
  gs_layout-sel_mode   = 'A'.
  gs_layout-col_opt    = 'X'.

ENDFORM.

*---------------------------------------------------------------------*
*       FORM get_versions                                             *
*---------------------------------------------------------------------*
FORM get_versions.

  DATA:
    lv_no_release_transformation TYPE svrs_bool.

  no_delta = ' '.

  IF logdest_lft = space OR logdest_lft = 'NONE'.
    lv_no_release_transformation = 'X'.
  ELSE.
    lv_no_release_transformation = ' '.
  ENDIF.

  CALL FUNCTION 'SVRS_GET_REPS_FROM_OBJECT'
    EXPORTING
      destination                  = logdest_lft
      object_name                  = objname_lft
      object_type                  = objtype_lft
      versno                       = version_lft
      iv_no_release_transformation = lv_no_release_transformation
    TABLES
      repos_tab                    = abaptext_sec
      trdir_tab                    = trdir_sec
      vsmodilog                    = gt_smodilog_lft
    EXCEPTIONS
      no_version                   = 01.
  IF sy-subrc <> 0.
    no_delta = 'A'. "No existe en origen
    EXIT.
  ENDIF.

  IF logdest_rgt = space OR logdest_rgt = 'NONE'.
    lv_no_release_transformation = 'X'.
  ELSE.
    lv_no_release_transformation = ' '.
  ENDIF.

  CALL FUNCTION 'SVRS_GET_REPS_FROM_OBJECT'
    EXPORTING
      destination                  = logdest_rgt
      object_name                  = objname_rgt
      object_type                  = objtype_rgt
      versno                       = version_rgt
      iv_no_release_transformation = lv_no_release_transformation
    TABLES
      repos_tab                    = abaptext_pri
      trdir_tab                    = trdir_pri
      vsmodilog                    = gt_smodilog_rgt
    EXCEPTIONS
      no_version                   = 01.

  IF sy-subrc <> 0.
    no_delta = 'B'. "No existe en destino
    EXIT.
  ENDIF.

ENDFORM.                    "get_versions

*---------------------------------------------------------------------*
*       FORM GET_DELTA                                                *
*---------------------------------------------------------------------*
FORM get_delta.

  no_delta = ' '.

  CALL FUNCTION 'SVRS_COMPUTE_DELTA_REPS'
    EXPORTING
      compare_mode            = comp_mode
      ignore_case_differences = gv_ignore_case_differences
    TABLES
      texttab_old             = abaptext_sec
      texttab_new             = abaptext_pri
      trdirtab_old            = trdir_sec
      trdirtab_new            = trdir_pri
      trdir_delta             = trdir_delta
      text_delta              = abaptext_delta.

  READ TABLE abaptext_delta INDEX 1 TRANSPORTING NO FIELDS.
  IF sy-subrc <> 0.
    CLEAR: no_delta.
  ELSE.
    no_delta  = 'X'.
  ENDIF.

ENDFORM.                    "get_delta

*---------------------------------------------------------------------*
*       FORM check_system_params
*---------------------------------------------------------------------*
FORM check_system_params.

  CALL FUNCTION 'TR_SYS_PARAMS'
    IMPORTING
      systemname    = systemname
      systemtype    = systemtype
    EXCEPTIONS
      no_systemname = 01
      no_systemtype = 02.

ENDFORM.                    "check_system_params

*---------------------------------------------------------------------*
* Form inicializate
*---------------------------------------------------------------------*
FORM inicializate.

  gv_ignore_case_differences = 'X'.
  one_column                 = 'X'.
  disp_mode                  = '2'.
  with_linenumbers           = 'X'.
  comp_mode                  = '1'.
  delta_only                 = ' '.
  shorten_same               = 'X'.
  ls_left_col                = '81'.
  ls_right_col               = '83'.
  split_edi                  = ' '.
  with_korrnum               = ' '.
  keep_pos                   = ' '.
  remote_comp                = ' '.
  left_flag                  = 'X'.

  PERFORM check_system_params.

  double_remote_comp = ' '.
  remote_comp        = 'X'.
  local_system       = systemname.
  sort_vers          = 'X'.

  CLEAR: no_delta, infoln1a, infoln1b, infoln2a, infoln2b, version_rgt, version_lft, objname_rgt, objname_lft, objtype_rgt, objtype_lft.
  CLEAR: korrnum_rgt, korrnum_lft, korrtxt_rgt, korrtxt_lft, korrinf_rgt, korrinf_lft, remsyst_lft, remsyst_rgt, logdest_lft, logdest_rgt, namlen.
  CLEAR: object_lft, object_rgt, gt_source_lft, gt_source_rgt, gv_transport_key, gt_mod_tab_lft, gt_mod_tab_rgt.
  CLEAR: objname, objnam2, objtyp1, objtyp2, versno1, versno2, objtype, no_version.

  REFRESH: abaptext_pri, abaptext_sec, abaptext_delta, abaptext_delta2, trdir_pri, trdir_old, trdir_sec,gt_smodilog_rgt.
  REFRESH: trdir_delta, trdir_delta2, textpool_pri, textpool_sec, textpool_delta, textpool_delta2, korrnum_tab, gt_smodilog_lft.

  UNASSIGN: <refname_lft>, <refname_rgt>.

ENDFORM.

*---------------------------------------------------------------------*
* Form inicializate_by_obj
*---------------------------------------------------------------------*
FORM inicializate_by_obj.

* Las variables más importantes del programa
  objname = objnam2 = ls_excel-objname.

  CASE ls_excel-objtyp1.
    WHEN 'DTEL' OR 'DTED'. "Tipo de datos
      objtype = objtyp1 = objtyp2 = 'DTED'.
      no_version = 'X'.
      no_delta   = 'D'. "Hay que llamar XXXXXX
    WHEN 'TABL' OR 'TABD'. "Tabla
      objtype = objtyp1 = objtyp2 = 'TABD'.
      no_version = 'X'.
      no_delta   = 'T'. "Hay que llamar RADVVTB2
    WHEN 'TRAN'."Transacción
      objtype = objtyp1 = objtyp2 = ls_excel-objtyp1.
      no_version = 'X'.
      no_delta   = 'E'.
    WHEN 'DEVC'."Paquete
      objtype = objtyp1 = objtyp2 = ls_excel-objtyp1.
      no_version = 'X'.
      no_delta   = 'E'.
    WHEN 'PROG' OR 'REPS'. "Programa
      objtype = objtyp1 = objtyp2 = 'REPS'.
    WHEN OTHERS.
      objtype = objtyp1 = objtyp2 = ls_excel-objtyp1.
      no_version = 'X'.
      no_delta   = 'E'.
  ENDCASE.

* Con esto seleccionas siempre la versión activa
  versno1 = '00000'.
  versno2 = '00000'.

  IF objtyp1 = 'XSLT' OR objtyp2 = 'XSLT'.
    comp_mode                  = 1.
    gv_ignore_case_differences = ' '.
    CLEAR split_edi.
  ENDIF.

  IF objtyp1 = 'METH'.
    namlen = '91'.
  ELSE.
    namlen = '40'.
  ENDIF.

  version_lft  = versno1.
  version_rgt  = versno2.
  objtype_lft  = objtyp1.
  objtype_rgt  = objtyp2.
  objname_lft  = objname.
  objname_rgt  = objnam2.
  korrnum_lft  = infoln1b-korrnum.
  korrnum_rgt  = infoln2b-korrnum.
  korrinf_lft  = infoln1b.
  korrinf_rgt  = infoln2b.
  logdest_lft  = log_dst1.
  logdest_rgt  = log_dest.
  remsyst_lft  = local_system.
  remsyst_rgt  = 'R3D'.

  ASSIGN objname_lft(namlen) TO <refname_lft>.
  ASSIGN objname_rgt(namlen) TO <refname_rgt>.

ENDFORM.

*---------------------------------------------------------------------*
* Form upload_excel
*---------------------------------------------------------------------*
FORM upload_excel.

  DATA:
    lt_raw     TYPE truxs_t_text_data,
    i_filename TYPE rlgrap-filename.

  i_filename = p_file.

  CALL FUNCTION 'TEXT_CONVERT_XLS_TO_SAP'
    EXPORTING
      i_line_header        = 'X'
      i_tab_raw_data       = lt_raw
      i_filename           = i_filename
    TABLES
      i_tab_converted_data = lt_excel[]
    EXCEPTIONS
      conversion_failed    = 1
      OTHERS               = 2.

ENDFORM.

*---------------------------------------------------------------------*
* Form file_open
*---------------------------------------------------------------------*
FORM file_open.

  cl_gui_frontend_services=>file_open_dialog(
    EXPORTING
      window_title            = 'Seleccione el fichero'
    CHANGING
      file_table              = lt_filetable
      rc                      = lv_rc
    EXCEPTIONS
      file_open_dialog_failed = 1
      cntl_error              = 2
      error_no_gui            = 3
      not_supported_by_gui    = 4
      OTHERS                  = 5 ).
  IF sy-subrc EQ 0.
    IF lv_rc EQ 1.
      READ TABLE lt_filetable INTO ls_filetable INDEX 1.
      IF sy-subrc EQ 0.
        p_file = ls_filetable-filename.
      ENDIF.
    ENDIF.
  ENDIF.

ENDFORM.

*---------------------------------------------------------------------*
*      Form  F4_FOR_VARIANT
*---------------------------------------------------------------------*
FORM f4_for_variant.

  g_variant-report = sy-repid.

  CALL FUNCTION 'LVC_VARIANT_F4'
    EXPORTING
      is_variant    = g_variant
      i_save        = g_save
    IMPORTING
      e_exit        = g_exit
      es_variant    = gx_variant
    EXCEPTIONS
      not_found     = 1
      program_error = 2
      OTHERS        = 3.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE 'S' NUMBER sy-msgno WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ELSE.
    IF g_exit = space.
      p_vari = gx_variant-variant.
    ENDIF.
  ENDIF.

ENDFORM.

*---------------------------------------------------------------------*
*      Form  PAI_OF_SELECTION_SCREEN
*---------------------------------------------------------------------*
FORM pai_of_selection_screen.

  IF NOT p_vari IS INITIAL.

    gx_variant-report  = sy-repid.
    gx_variant-variant = p_vari.

    CALL FUNCTION 'LVC_VARIANT_EXISTENCE_CHECK'
      EXPORTING
        i_save     = g_save
      CHANGING
        cs_variant = gx_variant.

    g_variant = gx_variant.

  ELSE.

    PERFORM variant_init.

  ENDIF.

ENDFORM.

*---------------------------------------------------------------------*
*      Form  VARIANT_INIT
*---------------------------------------------------------------------*
FORM variant_init.

  CLEAR gx_variant.

  g_repid           = sy-repid.
  g_save            = 'A'.
  gx_variant-report = g_repid.

  CALL FUNCTION 'LVC_VARIANT_DEFAULT_GET'
    EXPORTING
      i_save        = g_save
    CHANGING
      cs_variant    = gx_variant
    EXCEPTIONS
      wrong_input   = 1
      not_found     = 2
      program_error = 3
      OTHERS        = 4.
  IF sy-subrc = 0.
    p_vari = gx_variant-variant.
  ENDIF.

ENDFORM.

*---------------------------------------------------------------------*
* Form loop_excel
*---------------------------------------------------------------------*
FORM loop_excel.

  REFRESH: gt_alv.
  CLEAR:   gs_alv.

  LOOP AT lt_excel INTO ls_excel.

    PERFORM inicializate.
    PERFORM inicializate_by_obj.

    IF no_version IS INITIAL.

      PERFORM get_versions.
      IF no_delta IS INITIAL.
        PERFORM get_delta.
      ENDIF.

    ELSE.

*     Si es una tabla mirar función específica
      IF no_delta EQ 'T'.

        CALL FUNCTION 'ZMIG_DELTA_TABD'
          EXPORTING
            i_objname  = ls_excel-objname
            i_objnam2  = ls_excel-objname
            i_versno1  = '00000'
            i_versno2  = '00000'
            i_log_dest = log_dest
            i_rem_syst = 'R3D'
          IMPORTING
            e_delta    = no_delta.

      ENDIF.

*     Si es una tabla mirar función específica
      IF no_delta EQ 'D'.

        CALL FUNCTION 'ZMIG_DELTA_DTED'
          EXPORTING
            i_objname  = ls_excel-objname
            i_objnam2  = ls_excel-objname
            i_versno1  = '00000'
            i_versno2  = '00000'
            i_log_dest = log_dest
            i_rem_syst = 'R3D'
          IMPORTING
            e_delta    = no_delta.

      ENDIF.

    ENDIF.

    PERFORM get_last_version_info.

    CLEAR: gs_alv.
    gs_alv-objname = ls_excel-objname.
    gs_alv-objtyp1 = ls_excel-objtyp1.
    gs_alv-objtype = objtype.
    gs_alv-orden   = lv_orden.
    gs_alv-versno1 = ls_excel-versno1.
    gs_alv-delta   = no_delta.
    gs_alv-desc    = lv_description.

    CASE gs_alv-delta.
      WHEN 'E'. "No se compararlo
        gs_alv-icon = '@02@'.
      WHEN 'A'. "No exite en origen
        gs_alv-icon = '@B9@'.
      WHEN 'B'. "no existe en destino
        gs_alv-icon = '@B8@'.
      WHEN 'X'. "Origen<>Destino
        gs_alv-icon = '@5C@'.
      WHEN 'T' OR 'D'. "Objeto del diccionario
        gs_alv-icon = '@VB@'.
      WHEN OTHERS.
        gs_alv-icon = '@5B@'.
    ENDCASE.

    APPEND gs_alv TO gt_alv.

  ENDLOOP.

ENDFORM.

*---------------------------------------------------------------------*
* Form get_last_version_info
*---------------------------------------------------------------------*
FORM get_last_version_info.

  DATA:
    lv_objname      TYPE vrsd_old-objname,
    lv_objtype      TYPE vrsd_old-objtype,
    lversno_list    TYPE STANDARD TABLE OF vrsn,
    version_list    TYPE STANDARD TABLE OF vrsd_old,
    ls_version_list LIKE LINE OF version_list,
    lv_request      TYPE char20,
    lt_objects      TYPE STANDARD TABLE OF cts_obj.

  lv_objname = ls_excel-objname.
  lv_objtype = objtype.

  REFRESH: lversno_list.
  CLEAR:   lversno_list, lv_orden, lv_description.

  CALL FUNCTION 'SVRS_GET_VERSION_DIRECTORY'
    EXPORTING
      objname      = lv_objname
      objtype      = lv_objtype
    TABLES
      lversno_list = lversno_list
      version_list = version_list
    EXCEPTIONS
      no_entry     = 1
      OTHERS       = 2.
  IF sy-subrc EQ 0.

    READ TABLE version_list INTO ls_version_list WITH KEY versno = '00000'.
    IF sy-subrc EQ 0.
      lv_orden = ls_version_list-korrnum.
    ENDIF.

  ENDIF.

  IF ls_version_list-korrnum IS NOT INITIAL.

    lv_request = ls_version_list-korrnum.

    CALL FUNCTION 'CTS_API_READ_CHANGE_REQUEST'
      EXPORTING
        request     = lv_request
      IMPORTING
        description = lv_description
      TABLES
        objects     = lt_objects.

  ENDIF.

ENDFORM.

*---------------------------------------------------------------------*
* Form fill_excel
*---------------------------------------------------------------------*
FORM fill_excel.

  DATA:
    lv_request TYPE char20,
    lt_objects TYPE STANDARD TABLE OF cts_obj,
    ls_objects LIKE LINE OF lt_objects.

  lv_request = p_req.

  IF log_ord IS INITIAL.
    CALL FUNCTION 'CTS_API_READ_CHANGE_REQUEST'
      EXPORTING
        request = lv_request
      TABLES
        objects = lt_objects.
  ELSE.
    CALL FUNCTION 'CTS_API_READ_CHANGE_REQUEST' DESTINATION log_ord
      EXPORTING
        request = lv_request
      TABLES
        objects = lt_objects.
  ENDIF.

  LOOP AT lt_objects INTO ls_objects.
    CLEAR: ls_excel.
    ls_excel-objname = ls_objects-name.
    ls_excel-objtyp1 = ls_objects-object.
    APPEND ls_excel TO lt_excel.
  ENDLOOP.

ENDFORM.
