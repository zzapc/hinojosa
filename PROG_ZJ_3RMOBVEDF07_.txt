*----------------------------------------------------------------------*
*   INCLUDE J_3RMOBVEDF07                                              *
*&---------------------------------------------------------------------*
form get_stock_on_bwtar
    using p_wa_mard_mara type type_mard_mara
          p_wa_mbew      type type_xbew
          pt_charg      type tt_mcha_mchb
          pt_mska       type tt_mska
          pt_mslb       type tt_mslb
          pt_mspr       type tt_mspr
          pt_msku       type tt_msku
          pt_ekpo       type tt_ekpo
          pt_ekkn       type tt_ekkn
          p_charg       type charg_d
          pt_mcha       type tt_mcha                           "1156945
    changing p_xmensh    type menge_d
             p_nothing   type type_1c.

  clear p_nothing.

  if p_wa_mard_mara-lgort = gc_str_phantom.
    if p_wa_mard_mara-sobkz is initial.
      perform get_stock_on_bwtar_way
          using p_wa_mard_mara
                pt_ekpo
*                  pt_ekkn
                p_wa_mbew
*                  p_wa_mbew
                pt_charg
                p_charg                                        "1114541
          changing p_xmensh
                   p_nothing.
    elseif p_wa_mard_mara-sobkz = gc_spec_e.
      perform get_stock_on_bwtar_way_e
          using p_wa_mard_mara
                pt_ekpo
                pt_ekkn
                p_wa_mbew
                p_charg                                        "1114541
          changing p_xmensh.
*                     p_nothing.
    elseif p_wa_mard_mara-sobkz = gc_spec_q.
      perform get_stock_on_bwtar_way_q
          using p_wa_mard_mara
                pt_ekpo
                pt_ekkn
                p_wa_mbew
                p_charg                                        "1673200
          changing p_xmensh.
*                     p_nothing.
    elseif p_wa_mard_mara-sobkz = gc_spec_w or p_wa_mard_mara-sobkz = gc_spec_v.
      perform get_stock_on_bwtar_way_wv
          using p_wa_mard_mara
                p_wa_mbew
                pt_msku
                p_charg
                pt_mcha                                        "1156945
          changing p_xmensh
                   p_nothing.
* if without batch, get quantity material from msku
    elseif p_wa_mard_mara-sobkz = gc_spec_o.
      perform get_stock_on_bwtar_way_o
          using p_wa_mard_mara
                p_wa_mbew
                pt_mslb
                p_charg
                pt_mcha                                        "1156945
          changing p_xmensh
                   p_nothing.
    else.
* special stock
      p_xmensh = p_wa_mard_mara-labst.
    endif.
  else.
    if p_wa_mard_mara-sobkz is initial.
      perform get_stock_on_bwtar_all
          using p_wa_mard_mara
                p_wa_mbew
                pt_charg
                p_charg
          changing p_xmensh
                   p_nothing.
    elseif p_wa_mard_mara-sobkz = gc_spec_e.
* if split valuation, get quantity material from mska
      perform get_stock_on_bwtar_e
          using p_wa_mard_mara
                p_wa_mbew
                pt_mska
                p_charg
                pt_mcha                                        "1156945
          changing p_xmensh
                   p_nothing.
    elseif p_wa_mard_mara-sobkz = gc_spec_q.
* if split valuation, get quantity material from mspr
      perform get_stock_on_bwtar_q
          using p_wa_mard_mara
                p_wa_mbew
                pt_mspr
                p_charg
                pt_mcha                                        "1156945
          changing p_xmensh
                   p_nothing.
    endif.
  endif.

endform.                    " get_stock_on_bwtar
*&---------------------------------------------------------------------*
*&      Form  get_stock_on_bwtar_all
*&---------------------------------------------------------------------*
form get_stock_on_bwtar_all
    using p_mard_mara   type type_mard_mara
          p_mbew        type type_xbew
          pt_charg    type tt_mcha_mchb
          p_charg       type charg_d
    changing p_xmensh  type menge_d
             p_nothing type type_1c.

  data: lv_index like sy-tabix.                                "1665666

  field-symbols: <fs_charg> type line of tt_mcha_mchb.         "1665666

  If pa_batch Is Initial.                                      "1114541
*   exclude batch processing                                   "1114541
    if p_mard_mara-xchar_del is initial.                       "1114541
      p_xmensh                                                 "1114541
        = p_mard_mara-labst                                    "1114541
*        + p_mard_mara-umlme                                   "1251769
        + p_mard_mara-insme                                    "1114541
        + p_mard_mara-einme                                    "1114541
        + p_mard_mara-speme.                                   "1114541
    else.                                                      "1114541
*     if split valuation, get quantity material from MCHB      "1114541
      read table pt_charg with key                             "1665666 start
        matnr = p_mard_mara-matnr
        bwtar = p_mbew-bwtar
        werks = p_mard_mara-werks
        lgort = p_mard_mara-lgort
      transporting no fields.
      if sy-subrc = 0.
        lv_index = sy-tabix.
        loop at pt_charg assigning <fs_charg> from lv_index.
          if <fs_charg>-matnr ne p_mard_mara-matnr or
             <fs_charg>-bwtar ne p_mbew-bwtar      or
             <fs_charg>-werks ne p_mard_mara-werks or
             <fs_charg>-lgort ne p_mard_mara-lgort.
            exit.
          endif.
          p_xmensh = p_xmensh
                  + <fs_charg>-clabs
                  + <fs_charg>-cinsm
                  + <fs_charg>-cspem
                  + <fs_charg>-ceinm.
        endloop.
      else.                                                    "1665666 end
        p_nothing = gc_true.                                   "1114541
      endif.                                                   "1114541
    endif.                                                     "1114541
  Else.                                                        "1114541
    if p_mard_mara-xchar is initial.
      p_xmensh
* valuation unrestricted-use stock
        = p_mard_mara-labst
* stock in transfer from storage to storage
*        + p_mard_mara-umlme                                   "1251769
* stock in quality inspection
        + p_mard_mara-insme
        + p_mard_mara-einme
* blocked stock
        + p_mard_mara-speme.
*        + p_mard_mara-retme.
    else.
*     if split valuation, get quantity material from MCHB
      read table pt_charg with table key                       "1665666 start
        matnr = p_mard_mara-matnr
        bwtar = p_mbew-bwtar
        werks = p_mard_mara-werks
        lgort = p_mard_mara-lgort
        charg = p_charg
      transporting no fields.
      if sy-subrc = 0.
        lv_index = sy-tabix.
        loop at pt_charg assigning <fs_charg> from lv_index.
          if <fs_charg>-matnr ne p_mard_mara-matnr or
             <fs_charg>-bwtar ne p_mbew-bwtar      or
             <fs_charg>-werks ne p_mard_mara-werks or
             <fs_charg>-lgort ne p_mard_mara-lgort or
             <fs_charg>-charg ne p_charg.
            exit.
          endif.
          p_xmensh = p_xmensh
                  + <fs_charg>-clabs
*                    + <fs_charg>-cumlm
                  + <fs_charg>-cinsm
                  + <fs_charg>-cspem
                  + <fs_charg>-ceinm.
        endloop.
      else.                                                    "1665666 end
*       not exist
        p_nothing = gc_true.
      endif.
    endif.
  Endif.                                                       "1114541

endform.                    " get_stock_on_bwtar_all
*&---------------------------------------------------------------------*
*&      Form  get_stock_on_bwtar_e
*&---------------------------------------------------------------------*
*----------------------------------------------------------------------*
form get_stock_on_bwtar_e
    using p_mard_mara   type type_mard_mara
          p_mbew        type type_xbew
          pt_mska       type tt_mska
*          pt_charg    type tt_mcha_mchb
          p_charg       type charg_d
          pt_mcha       type tt_mcha                           "1156945
    changing p_xmensh  type menge_d
             p_nothing type type_1c.
  data:
      lw_mska type type_mska,
      lw_mcha type type_mcha.

  If pa_batch Is Initial.                                      "1114541
*   exclude batch processing                                   "1114541
    loop at pt_mska into lw_mska                               "1114541
        where matnr  = p_mard_mara-matnr                       "1114541
          and werks  = p_mard_mara-werks                       "1114541
          and lgort  = p_mard_mara-lgort                       "1114541
          and sobkz  = p_mard_mara-sobkz                       "1114541
          and vbeln  = p_mard_mara-vbeln                       "1114541
          and posnr  = p_mard_mara-posnr.                      "1114541
      If p_mard_mara-xchar_del Is Not Initial                  "1114541
              And p_mbew-bwtar Is Not Initial.                 "1114541
*       For split valuation check valuation type               "1114541
        Clear lw_mcha.                                         "1156945
*       Exclude batch with old valuation type from calculation "1247791
        Loop At pt_mcha Into lw_mcha                           "1247791
            Where matnr = lw_mska-matnr                        "1156945
              And werks = lw_mska-werks                        "1156945
              And charg = lw_mska-charg.                       "1156945
          Check lw_mcha-changed ne 'O'.                        "1247791
          Exit.                                                "1247791
        Endloop.                                               "1247791
        Check p_mbew-bwtar = lw_mcha-bwtar.                    "1156945
      Endif.                                                   "1114541
      p_xmensh = p_xmensh + lw_mska-kalab + lw_mska-kains + lw_mska-kaspe.
    endloop.                                                   "1114541
  Else.                                                        "1114541
    loop at pt_mska into lw_mska
        where matnr  = p_mard_mara-matnr
          and werks  = p_mard_mara-werks
          and lgort  = p_mard_mara-lgort
          and charg  = p_charg                                 "1114541
          and sobkz  = p_mard_mara-sobkz
          and vbeln  = p_mard_mara-vbeln
          and posnr  = p_mard_mara-posnr.
      p_xmensh = p_xmensh + lw_mska-kalab + lw_mska-kains + lw_mska-kaspe.
    endloop.
  Endif.                                                       "1114541
* not exist
  if not sy-subrc is initial.
    p_nothing = gc_true.
  endif.
endform.                    " get_stock_on_bwtar_e
*&---------------------------------------------------------------------*
*&      Form  get_stock_on_bwtar_q
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_WA_MARD_MARA  text
*      -->P_P_WA_MBEW  text
*      -->P_PPT_MSPR  text
*      -->P_PPT_MCHA  text
*      <--P_P_XMENSH  text
*      <--P_P_NOTHING  text
*----------------------------------------------------------------------*
form get_stock_on_bwtar_q
    using p_mard_mara   type type_mard_mara
          p_qbew        type type_xbew
          pt_mspr       type tt_mspr
          p_charg       type charg_d
          pt_mcha       type tt_mcha                           "1156945
    changing p_xmensh  type menge_d
             p_nothing type type_1c.

  data:
      lw_mspr type type_mspr,
      lw_mcha type type_mcha.

  If pa_batch Is Initial.                                      "1114541
*   exclude batch processing                                   "1114541
    loop at pt_mspr into lw_mspr                               "1114541
        where matnr  = p_mard_mara-matnr                       "1114541
          and werks  = p_mard_mara-werks                       "1114541
          and lgort  = p_mard_mara-lgort                       "1114541
          and sobkz  = p_mard_mara-sobkz                       "1114541
          and pspnr  = p_mard_mara-pspnr.                      "1114541
      If p_mard_mara-xchar_del Is Not Initial                  "1114541
              And p_qbew-bwtar Is Not Initial.                 "1114541
*       For split valuation check valuation type               "1114541
        Clear lw_mcha.                                         "1156945
*       Exclude batch with old valuation type from calculation "1247791
        Loop At pt_mcha Into lw_mcha                           "1247791
            Where matnr = lw_mspr-matnr                        "1156945
              And werks = lw_mspr-werks                        "1156945
              And charg = lw_mspr-charg.                       "1156945
          Check lw_mcha-changed ne 'O'.                        "1247791
          Exit.                                                "1247791
        Endloop.                                               "1247791
        Check p_qbew-bwtar = lw_mcha-bwtar.                    "1156945
      Endif.                                                   "1114541

      p_xmensh = p_xmensh + lw_mspr-prlab + lw_mspr-prins + lw_mspr-prspe
                          + lw_mspr-prein. "2242168
    endloop.                                                   "1114541
  Else.                                                        "1114541
    loop at pt_mspr into lw_mspr
        where matnr  = p_mard_mara-matnr
          and werks  = p_mard_mara-werks
          and lgort  = p_mard_mara-lgort
      and charg  = p_charg
      and sobkz  = p_mard_mara-sobkz
      and pspnr  = p_mard_mara-pspnr.

      p_xmensh = p_xmensh + lw_mspr-prlab + lw_mspr-prins + lw_mspr-prspe
                          + lw_mspr-prein. "2242168
    endloop.
  Endif.                                                       "1114541
* not exist
  if not sy-subrc is initial.
    p_nothing = gc_true.
  endif.
endform.                    " get_stock_on_bwtar_q
*&---------------------------------------------------------------------*
*&      Form  get_stock_on_bwtar_way
*&---------------------------------------------------------------------*
form get_stock_on_bwtar_way
    using p_mard_mara   type type_mard_mara
          pt_ekpo      type tt_ekpo
*          pt_ekkn      TYPE tt_ekkn
          pwa_mbew       type type_xbew
          pt_charg     type tt_mcha_mchb
          p_charg      type charg_d                            "1114541
    changing p_xmensh   like mseg-menge
             p_nothing type type_1c.

  data:
      lv_sysubrc  type sysubrc,                                "1354071
      lw_ekpo  type type_ekpo,
      lv_index like sy-tabix.                                  "1665666

  field-symbols: <fs_charg> type line of tt_mcha_mchb.         "1665666

  if p_mard_mara-xchar = gc_true or
     p_mard_mara-xchar_del = gc_true.                          "1251769
    if pwa_mbew-bwtty is initial.
*     material with non-valuated batches
      read table pt_charg with key                             "1665666 start
        matnr = p_mard_mara-matnr
        bwtar = pwa_mbew-bwtar
        werks = p_mard_mara-werks
      transporting no fields.
      if sy-subrc = 0.
        lv_index = sy-tabix.
        loop at pt_charg assigning <fs_charg> from lv_index.
          if <fs_charg>-matnr ne p_mard_mara-matnr or
             <fs_charg>-bwtar ne pwa_mbew-bwtar    or
             <fs_charg>-werks ne p_mard_mara-werks.
            exit.
          endif.
          If pa_batch Is Not Initial.
            Check p_charg = <fs_charg>-charg.
          Endif.
          p_xmensh = p_xmensh + <fs_charg>-cumlm.
        endloop.
      endif.                                                   "1665666 end
      If p_charg is initial.                                   "1251769
        p_xmensh = p_xmensh + p_mard_mara-umlmc + p_mard_mara-trame.
      Endif.                                                   "1251769
    else.
      loop at pt_ekpo into lw_ekpo
          where matnr = pwa_mbew-matnr
            and bwkey = pwa_mbew-bwkey
            and bwtar = pwa_mbew-bwtar
            and sobkz = pwa_mbew-sobkz.                        "1582729

*       PO transfer stock should be displayed in the section   "1354071
*       with empty batch only as we don't know yet to what
*       batch it will be posted
        check p_charg is initial.                              "1354071
        if lw_ekpo-bewtp = gc_bewtp_u or lw_ekpo-bewtp = gc_bewtp_e.

          data: lv_converted_menge type mseg-menge. "Convert Unit of measure in PO to Unit of measure in MM documents
          clear lv_converted_menge.

          IF lw_ekpo-umren <> 0.
            lv_converted_menge = lw_ekpo-menge * ( lw_ekpo-umrez / lw_ekpo-umren ).
          ENDIF.

          if lw_ekpo-shkzg = gc_shkzg_s.
            p_xmensh = p_xmensh - lv_converted_menge.
          else.
            p_xmensh = p_xmensh + lv_converted_menge.
          endif.
        endif.
      endloop.
      lv_sysubrc = sy-subrc.                                   "1354071

*     material with valuated batches
      read table pt_charg with key                             "1665666 start
        matnr = pwa_mbew-matnr
        bwtar = pwa_mbew-bwtar
        werks = p_mard_mara-werks
      transporting no fields.
      if sy-subrc = 0.
        lv_index = sy-tabix.
        loop at pt_charg assigning <fs_charg> from lv_index.
          if <fs_charg>-matnr ne pwa_mbew-matnr    or
             <fs_charg>-bwtar ne pwa_mbew-bwtar    or
             <fs_charg>-werks ne p_mard_mara-werks.
            exit.
          endif.
          If pa_batch Is Not Initial.
            Check p_charg = <fs_charg>-charg.
          Endif.
          p_xmensh = p_xmensh + <fs_charg>-cumlm.
        endloop.
      endif.                                                   "1665666 end

      if not sy-subrc is initial and
         not lv_sysubrc is initial.                            "1354071
        p_nothing = gc_true.
      endif.
    endif.
  else.
    p_xmensh
*    valuation unrestricted-use stock
        = p_mard_mara-trame
*    stock in transfer from storage to storage
        + p_mard_mara-umlme
        + p_mard_mara-umlmc
*    stock in quality inspection
*        + p_mard_mara-insme
        + p_mard_mara-einme
*    blocked stock
        + p_mard_mara-speme
        + p_mard_mara-retme
        + p_mard_mara-bwesb.                                " ERP2005
  endif.
endform.                    " get_stock_on_bwtar_way
*&---------------------------------------------------------------------*
*&      Form  get_stock_on_bwtar_way_o
*&---------------------------------------------------------------------*
form get_stock_on_bwtar_way_o
    using p_mard_mara   type type_mard_mara
          p_mbew        type type_xbew
          pt_mslb       type tt_mslb
          p_charg       type charg_d
          pt_mcha       type tt_mcha                           "1156945
    changing p_xmensh  type menge_d
             p_nothing type type_1c.

  data:
      lw_mcha type type_mcha,                                  "1156945
      lw_mslb type type_mslb.

  If pa_batch Is Initial.                                      "1114541
*   exclude batch processing                                   "1114541
    loop at pt_mslb into lw_mslb                               "1114541
    where matnr  = p_mard_mara-matnr                           "1114541
      and werks  = p_mard_mara-werks                           "1114541
      and sobkz  = p_mard_mara-sobkz                           "1114541
      and lifnr  = p_mard_mara-sortl.                          "1114541

      If p_mard_mara-xchar_del Is Not Initial                  "1114541
              And p_mbew-bwtar Is Not Initial.                 "1114541
*       For split valuation check valuation type               "1114541
        Clear lw_mcha.                                         "1156945
        Read Table pt_mcha With Key                            "1156945
            matnr = lw_mslb-matnr                              "1156945
            werks = lw_mslb-werks                              "1156945
            charg = lw_mslb-charg Into lw_mcha.                "1156945
        Check p_mbew-bwtar = lw_mcha-bwtar.                    "1156945
      Endif.                                                   "1114541
      p_xmensh = p_xmensh + lw_mslb-lblab + lw_mslb-lbins.     "1114541
    endloop.                                                   "1114541
  Else.                                                        "1114541
    loop at pt_mslb into lw_mslb
    where matnr  = p_mard_mara-matnr
      and werks  = p_mard_mara-werks
      and charg  = p_charg
      and sobkz  = p_mard_mara-sobkz
      and lifnr  = p_mard_mara-sortl.

      p_xmensh = p_xmensh + lw_mslb-lblab + lw_mslb-lbins.
    endloop.
  Endif.                                                       "1114541

  if not sy-subrc is initial.
    p_nothing = gc_true.
  endif.

endform.                    " get_stock_on_bwtar_way_o
*&---------------------------------------------------------------------*
*&      Form  get_stock_on_bwtar_way_wv
*&---------------------------------------------------------------------*
form get_stock_on_bwtar_way_wv
    using p_mard_mara   type type_mard_mara
          p_mbew        type type_xbew
          pt_msku       type tt_msku
          p_charg       type charg_d
          pt_mcha       type tt_mcha                           "1156945
    changing p_xmensh  type menge_d
             p_nothing type type_1c.

  data:
      lw_mcha type type_mcha,                                  "1156945
      lw_msku type type_msku.

  If pa_batch Is Initial.                                      "1114541
*   exclude batch processing                                   "1114541
    loop at pt_msku into lw_msku                               "1114541
    where matnr  = p_mard_mara-matnr                           "1114541
      and werks  = p_mard_mara-werks                           "1114541
      and sobkz  = p_mard_mara-sobkz                           "1114541
      and kunnr  = p_mard_mara-sortl.                          "1114541

      If p_mard_mara-xchar_del Is Not Initial                  "1114541
              And p_mbew-bwtar Is Not Initial.                 "1114541
*       For split valuation check valuation type               "1114541
        Clear lw_mcha.                                         "1156945
        Read Table pt_mcha With Key                            "1156945
            matnr = lw_msku-matnr                              "1156945
            werks = lw_msku-werks                              "1156945
            charg = lw_msku-charg Into lw_mcha.                "1156945
        Check p_mbew-bwtar = lw_mcha-bwtar.                    "1156945
      Endif.                                                   "1114541
      p_xmensh = p_xmensh + lw_msku-kulab + lw_msku-kuins.     "1114541
    endloop.                                                   "1114541
  Else.                                                        "1114541
    loop at pt_msku into lw_msku
    where matnr  = p_mard_mara-matnr
      and werks  = p_mard_mara-werks
      and charg  = p_charg
      and sobkz  = p_mard_mara-sobkz
      and kunnr  = p_mard_mara-sortl.

      p_xmensh = p_xmensh + lw_msku-kulab + lw_msku-kuins.
    endloop.
  Endif.                                                       "1114541
* not exist
  if not sy-subrc is initial.
    p_nothing = gc_true.
    return.
  endif.

  if p_mard_mara-xchar is initial and pa_batch = gc_true.      "1114541
    p_xmensh = p_xmensh + p_mard_mara-umlmc.
  endif.
endform.                    " get_stock_on_bwtar_way_wv
*&---------------------------------------------------------------------*
*&      Form  t030_lesen
*&---------------------------------------------------------------------*
form t030_lesen
    using p_xbklas    type bklas
          p_xktopl    type ktopl
          p_xbwmod    type bwmod
    changing p_xsaknr type saknr.
  data:
        l_konts type saknr.

  call function 'MR_ACCOUNT_ASSIGNMENT'                        "1328916
    exporting
      bewertungsklasse       = p_xbklas     " valuation class
      bewertung_modif        = p_xbwmod     " valuation grouping code
      kontenplan             = p_xktopl     " chart of accounts
*     konto_modif            = '   '        " account modification for BSX
      soll_haben_kennzeichen = gc_shkzg_s   " debit/credit indicator
      vorgangsschluessel     = gc_ktosl     " transaction key BSX
    importing
      konto                  = l_konts      " GL account number
    exceptions
      others                 = 1.
  if sy-subrc <> 0.
*   initial GL account number in case of missing customizing
    p_xsaknr = '0'.
  else.
    p_xsaknr = l_konts.
  endif.

endform.                    " t030_lesen
*&---------------------------------------------------------------------*
*&      Form  get_stock_on_bwtar_way_e
*&---------------------------------------------------------------------*
form get_stock_on_bwtar_way_e
    using p_mard_mara type type_mard_mara
          pt_ekpo     type tt_ekpo
          pt_ekkn     type tt_ekkn
          pwa_mbew    type type_xbew
          p_charg     type charg_d                             "1114541
    changing p_xmensh type menge_d.

  data: lw_ekpo type type_ekpo,
        lw_ekkn type type_ekkn.

    loop at pt_ekpo into lw_ekpo
      where matnr = pwa_mbew-matnr
        and bwkey = pwa_mbew-bwkey
        and bwtar = pwa_mbew-bwtar.

*     PO transfer stock should be displayed in the section     "1673200
*     with empty batch only as we don't know yet to what
*     batch it will be posted
      check p_charg is initial.

      loop at pt_ekkn into lw_ekkn
        where ebeln = lw_ekpo-ebeln
          and ebelp = lw_ekpo-ebelp.
      check lw_ekkn-vbeln = p_mard_mara-vbeln
        and lw_ekkn-vbelp = p_mard_mara-posnr.

      if lw_ekpo-bewtp = gc_bewtp_u or lw_ekpo-bewtp = gc_bewtp_e.
        if lw_ekpo-shkzg = gc_shkzg_s.
          p_xmensh = p_xmensh - lw_ekpo-menge.
        else.
          p_xmensh = p_xmensh + lw_ekpo-menge.
        endif.
      endif.
      endloop.
    endloop.

  p_xmensh = p_xmensh + p_mard_mara-umlmc.

endform.                    " get_stock_on_bwtar_way_e
*&---------------------------------------------------------------------*
*&      Form  get_stock_on_bwtar_way_q
*&---------------------------------------------------------------------*
form get_stock_on_bwtar_way_q
    using p_mard_mara  type type_mard_mara
          pt_ekpo     type tt_ekpo
          pt_ekkn     type tt_ekkn
          pwa_mbew      type type_xbew
          p_charg     type charg_d                             "1673200
    changing p_xmensh  type menge_d.

  data: lw_ekpo type type_ekpo,
        lw_ekkn type type_ekkn.

  loop at pt_ekpo into lw_ekpo
      where matnr = pwa_mbew-matnr
        and bwkey = pwa_mbew-bwkey
        and bwtar = pwa_mbew-bwtar.

*   PO transfer stock should be displayed in the section       "1673200
*   with empty batch only as we don't know yet to what
*   batch it will be posted
    check p_charg is initial.

    loop at pt_ekkn into lw_ekkn
        where ebeln = lw_ekpo-ebeln
          and ebelp = lw_ekpo-ebelp.
      check lw_ekkn-ps_psp_pnr = p_mard_mara-pspnr.

      if lw_ekpo-bewtp = gc_bewtp_u or lw_ekpo-bewtp = gc_bewtp_e.
        if lw_ekpo-shkzg = gc_shkzg_s.
          p_xmensh = p_xmensh - lw_ekpo-menge.
        else.
          p_xmensh = p_xmensh + lw_ekpo-menge.
        endif.
      endif.
    endloop.
  endloop.
  p_xmensh = p_xmensh + p_mard_mara-umlmc.

endform.                    " get_stock_on_bwtar_way_q
