
class ZCL_WM_ZWM_LTBK_ADIT_DAO definition
  public
  final
  create public .

public section.

  class-methods CREATE_ENTRY
    importing
      !IS_DATA type LTBK
      !IV_COMMIT type BOOLEAN default ABAP_FALSE .
  class-methods STORE_DATA_FOR_ENTRY
    importing
      !IS_DATA type ZWM_LTBK_ADIT_INC .
  class-methods CLEAR_DATA_FOR_ENTRY .
  class-methods GET_QTY_4_BENUM
    importing
      !IV_BENUM type LTBK-BENUM
      !IV_LGNUM type ZWM_LTBK_ADIT-LGNUM
      !IV_LGPLA type LGPLA
    returning
      value(RS_DATA) type ZWM_LTBK_ADIT .
  class-methods GET_ENTRY
    importing
      !IV_LGNUM type ZWM_LTBK_ADIT-LGNUM
      !IV_TBNUM type ZWM_LTBK_ADIT-TBNUM
    returning
      value(RS_DATA) type ZWM_LTBK_ADIT .
  class-methods GET_INTERNAL_ENTRY
    importing
      !IV_NLPLA type ZWM_LTBK_ADIT_INC-NLPLA
      !IV_BENUM type ZWM_LTBK_ADIT_INC-BENUM
    returning
      value(RS_DATA) type ZWM_LTBK_ADIT_INC .
  PROTECTED SECTION.
    CLASS-DATA: gs_inc TYPE zwm_ltbk_adit_inc.
    CLASS-DATA: gt_inc TYPE TABLE OF zwm_ltbk_adit_inc WITH DEFAULT KEY.
  PRIVATE SECTION.
endclass. "ZCL_WM_ZWM_LTBK_ADIT_DAO definition
class ZCL_WM_ZWM_LTBK_ADIT_DAO implementation.
  METHOD clear_data_for_entry.
    CLEAR gs_inc.
  ENDMETHOD.
  METHOD create_entry.

    clear_data_for_entry( ).

    CHECK is_data IS NOT INITIAL.
    TRY.
        gs_inc = gt_inc[ benum = is_data-benum nlpla = is_data-nlpla ].
        DATA(ls_inc) = gs_inc.
        DATA(ls_data) = CORRESPONDING zwm_ltbk_adit( is_data ).
        MOVE-CORRESPONDING: ls_inc TO ls_data.

        MODIFY zwm_ltbk_adit
        FROM ls_data.

        CHECK iv_commit = abap_true.

        COMMIT WORK.

      CATCH cx_root.
        RETURN.
    ENDTRY.

  ENDMETHOD.
  METHOD get_entry.
    SELECT SINGLE * FROM zwm_ltbk_adit
    INTO CORRESPONDING FIELDS OF rs_data
    WHERE lgnum = iv_lgnum AND tbnum = iv_tbnum.

  ENDMETHOD.
  METHOD GET_INTERNAL_ENTRY.
    READ TABLE gt_inc into rs_data with key nlpla = iv_nlpla
                                            benum = iv_benum.

  ENDMETHOD.
  METHOD get_qty_4_benum.
    SELECT SINGLE * FROM zwm_ltbk_adit
    INTO CORRESPONDING FIELDS OF rs_data
    WHERE lgnum = iv_lgnum
    AND benum = iv_benum
    AND nlpla = iv_lgpla
    AND menge > 0.
  ENDMETHOD.
  METHOD store_data_for_entry.
    DELETE gt_inc WHERE benum = is_data-benum AND nlpla = is_data-nlpla. " there can be only one
    APPEND is_data TO gt_inc ASSIGNING FIELD-SYMBOL(<fs>).
    IF <fs>-meins IS INITIAL. <fs>-meins = 'M'. ENDIF.
  ENDMETHOD.
