FUNCTION z_scm042_iniciar_carga_wm.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     VALUE(I_TKNUM) TYPE  TKNUM
*"  EXPORTING
*"     VALUE(ET_RETURN) TYPE  BAPIRET2_TT
*"----------------------------------------------------------------------
* JGM 02.2024 GAPCM042 Aplicativo Gestión de Desvíos:
* Iniciar carga, igual que el botón "Inicio carga" de la aplicación
* UI5 de la pistola para WM.
* Se "reinician" los estados iniciales del transporte.
*--------------------------------------------------------------------*
  DATA: lv_error TYPE flag.
  DATA: lt_log TYPE bapiret2_tt.
  DATA: ls_shipment_data TYPE zswm_shipment_data.
  DATA: lr_type_error TYPE RANGE OF bapi_mtype.


  IF i_tknum IS NOT INITIAL.
    lr_type_error = VALUE #( sign = 'I' option = 'EQ' ( low = 'E' ) ( low = 'A' ) ( low = 'X' ) ).

    "Para poder reutilizar las funciones WM de la aplicación UI5 sobre el mismo transporte,
    "hay que limpiar el campo ZSWM_UI5_TKNUM-INICIO para este tknum antes de iniciar -lo
    "chequean estas funciones WM-, y volver a marcarlo al terminar.

    SELECT SINGLE *
      INTO @DATA(ls_zui5)
      FROM zswm_ui5_tknum
      WHERE tknum EQ @i_tknum
        AND inicio EQ 'X'.

    IF sy-subrc EQ 0.
      UPDATE zswm_ui5_tknum SET inicio = space
      WHERE tknum EQ i_tknum.

      IF sy-subrc EQ 0.
        COMMIT WORK AND WAIT.

        CALL FUNCTION 'ZSWM_PUT_SHIPMENT_DATA'
          EXPORTING
*           TCODE            =
*           UNAME            = SY-UNAME
            tknum            = i_tknum
*           OPERARIO         =
            rt_shipment_data = ls_shipment_data
            inicio_carga     = 'X'
          IMPORTING
            error            = lv_error
            tt_log           = lt_log.

        IF lv_error IS INITIAL.
          "Actualizar de nuevo ZSWM_UI5_TKNUM-INICIO, pues la función WM asume que es la
          "primera vez que se inicia éste transporte e intentará un insert en ZSWM_UI5_TKNUM,
          "que no actualizará Inicio en el registro.

          UPDATE zswm_ui5_tknum SET inicio = 'X'
          WHERE tknum EQ i_tknum.

          IF sy-subrc EQ 0.
            COMMIT WORK AND WAIT.
          ENDIF.

          "Carga iniciada transporte"
          PERFORM fill_return USING 'S' 'ZSCM042' '012' i_tknum space space space CHANGING et_return.

          "En caso de tener que rellenar "Cuestionario de Riesgos Laborales", revisar fm:
          "ZSWM_PUT_UI5_RLAB
        ELSE.
          "Mensajes de error FM WM.
          DELETE lt_log WHERE type NOT IN lr_type_error.
          APPEND LINES OF lt_log TO et_return.
        ENDIF.

      ENDIF.  "update inicio.
    ENDIF.  "select zswm_ui5_tknum.

  ENDIF.  "tknum no initial.

ENDFUNCTION.
