FUNCTION z_scm042_cerrar_transp_wm.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     VALUE(I_TKNUM) TYPE  TKNUM
*"     VALUE(I_CIERRA) TYPE  FLAG
*"     VALUE(I_DAR_SALIDA) TYPE  FLAG
*"  EXPORTING
*"     VALUE(ET_RETURN) TYPE  BAPIRET2_TT
*"----------------------------------------------------------------------
* JGM 02.2024 GAPCM042 Aplicativo Gestión de Desvíos:
* Replica las acciones-botones de la pistola WM sobre un transporte:
* -Cerrar Transporte (se crean las posiciones de pallets).
* -Dar Salida (se contabilizan las SM de entregas).
*--------------------------------------------------------------------*
  CONSTANTS:
    lc_tcode_rf_ui5 TYPE tcode VALUE 'ZWM_RF_UI5_EXP_001'.

  DATA: lv_error TYPE flag.
  DATA: lt_log TYPE bapiret2_t.
  DATA: lr_type_error TYPE RANGE OF bapi_mtype.


  IF i_tknum IS NOT INITIAL.
    lr_type_error = VALUE #( sign = 'I' option = 'EQ' ( low = 'E' ) ( low = 'A' ) ).

    CASE abap_true.
      WHEN i_cierra.
        "Inicio Cierre:".
        PERFORM fill_return USING 'S' 'ZSCM042' '028' i_tknum space space space CHANGING et_return.
      WHEN i_dar_salida.
        "Inicio Salida:".
        PERFORM fill_return USING 'S' 'ZSCM042' '029' i_tknum space space space CHANGING et_return.
      WHEN OTHERS.
    ENDCASE.

    CALL FUNCTION 'ZSWM_CLOSE_DELIVRY_OUT_UI5'
      EXPORTING
        tcode      = lc_tcode_rf_ui5
*       UNAME      = SY-UNAME
*       OPERARIO   =
        tknum      = i_tknum
*       VBELN      =
        cierra     = i_cierra
        dar_salida = i_dar_salida
      IMPORTING
        error      = lv_error
        tt_log     = lt_log.

    IF lv_error IS INITIAL.
      CASE abap_true.
        WHEN i_cierra.
          "Cierre transporte".
          PERFORM fill_return USING 'S' 'ZSCM042' '014' i_tknum space space space CHANGING et_return.
        WHEN i_dar_salida.
          "Salida transporte".
          PERFORM fill_return USING 'S' 'ZSCM042' '015' i_tknum space space space CHANGING et_return.
        WHEN OTHERS.
      ENDCASE.
    ELSE.
      "Mensajes de error FM WM.
      DELETE lt_log WHERE type NOT IN lr_type_error.
      APPEND LINES OF lt_log TO et_return.
    ENDIF.

  ENDIF.

ENDFUNCTION.
