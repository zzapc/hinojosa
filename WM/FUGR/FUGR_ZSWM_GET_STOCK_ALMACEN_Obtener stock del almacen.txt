FUNCTION zswm_get_stock_almacen.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     VALUE(VP_WERKS) TYPE  MARD-WERKS
*"     VALUE(VP_LGORT) TYPE  MARD-LGORT OPTIONAL
*"     VALUE(TP_MATNR_RG) TYPE  RANGE_T_MATNR OPTIONAL
*"  EXPORTING
*"     VALUE(TP_STOCK) TYPE  ZSWM_STOCK_ALMACEN_T
*"----------------------------------------------------------------------

  DATA: data_u            TYPE zswm_data_ui5_eeeoo_str.
  CALL FUNCTION 'ZSWM_GET_DATA_USER_UI5'
    EXPORTING
      statu      = 'X'
      no_gen_log = abap_true
    IMPORTING
      data_user  = data_u.

  IF vp_lgort IS INITIAL AND tp_matnr_rg IS NOT INITIAL.
    DATA(wl_matnr_rg) = tp_matnr_rg[ 1 ].
    SELECT SINGLE mtart
      INTO @DATA(vl_mtart)
      FROM mara
      WHERE matnr = @wl_matnr_rg-low.

    SELECT SINGLE atributo1
      INTO vp_lgort
      FROM zparametros
      WHERE clave     = 'UI5_ALM_PT'    AND
            campo     = 'ALMACEN_APRO'  AND
            valor     = data_u-lgnum   AND
            valor2    = vl_mtart.
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.
  ENDIF.



  SELECT mchb~werks, mchb~lgort, mchb~matnr, mchb~charg, mchb~clabs AS labst,
       makt~maktx, mara~meins, t001l~lgobe, mara~mtart
  INTO CORRESPONDING FIELDS OF TABLE @tp_stock
  FROM mchb INNER JOIN mara ON mara~matnr = mchb~matnr
            INNER JOIN makt ON makt~matnr = mchb~matnr AND
                               makt~spras = @sy-langu
            INNER JOIN t001l ON t001l~werks = mchb~werks AND
                                t001l~lgort = mchb~lgort
  WHERE mchb~werks = @vp_werks      AND
        mchb~lgort = @vp_lgort      AND
        mchb~matnr IN @tp_matnr_rg  AND
        mchb~clabs > 0.
  IF sy-subrc <> 0.
    SELECT mard~werks, mard~lgort, mard~matnr, mard~labst,
           makt~maktx, mara~meins, t001l~lgobe, mara~mtart
      INTO CORRESPONDING FIELDS OF TABLE @tp_stock
      FROM mard INNER JOIN mara ON mara~matnr = mard~matnr
                INNER JOIN makt ON makt~matnr = mard~matnr AND
                                   makt~spras = @sy-langu
            INNER JOIN t001l ON t001l~werks = mard~werks AND
                                t001l~lgort = mard~lgort
      WHERE mard~werks = @vp_werks      AND
            mard~lgort = @vp_lgort      AND
            mard~matnr IN @tp_matnr_rg  AND
            mard~labst > 0.

  ENDIF.





ENDFUNCTION.
