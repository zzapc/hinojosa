*&---------------------------------------------------------------------*
*& Report  ZRWM0005
*&
*&---------------------------------------------------------------------*
*& Autor: Carlos Vivó Jiménez
*& Proyecto: Roll-out WM CB - GAP WM02
*& Fecha: 22/03/2019
*&
*& Descripción: Informe de los registros de la tabla ZWM_RF_LOGIN
*&---------------------------------------------------------------------*
REPORT zrwm0005.

INCLUDE zrwm0005_top.

SELECTION-SCREEN BEGIN OF BLOCK zrpp0005 WITH FRAME TITLE text-001.
PARAMETERS: p_lgnum TYPE lgnum MEMORY ID lgn MATCHCODE OBJECT h_t300 OBLIGATORY.
SELECTION-SCREEN SKIP.
SELECT-OPTIONS: s_date FOR zwm_rf_login-fecha,
                s_time FOR zwm_rf_login-hora.
SELECTION-SCREEN SKIP.
SELECT-OPTIONS: s_user FOR zwm_rf_login-usuario,
                s_oper FOR zwm_rf_login-operario,
                s_name FOR zwm_rf_oper-nombre.
SELECTION-SCREEN END OF BLOCK zrpp0005.


START-OF-SELECTION.

  IF s_name[] IS NOT INITIAL.
    DATA: name LIKE zwm_rf_oper-nombre.

    LOOP AT s_name ASSIGNING FIELD-SYMBOL(<name>).
      TRANSLATE: <name>-low TO UPPER CASE, <name>-high TO UPPER CASE.
    ENDLOOP.

    SELECT * FROM zwm_rf_oper
      INTO TABLE @DATA(t_oper)
      WHERE lgnum EQ @p_lgnum.

    LOOP AT t_oper ASSIGNING FIELD-SYMBOL(<oper>).
      name = <oper>-nombre.
      TRANSLATE name TO UPPER CASE.
      IF name IN s_name.
        CLEAR s_name2.
        s_name2-sign = 'I'.
        s_name2-option = 'EQ'.
        s_name2-low = <oper>-nombre.
        APPEND s_name2.
      ENDIF.
    ENDLOOP.
  ENDIF.


  SELECT * FROM zwm_rf_login AS a INNER JOIN zwm_rf_oper AS b
    ON a~operario EQ b~operario
    INTO CORRESPONDING FIELDS OF TABLE gt_datos
    WHERE a~lgnum EQ p_lgnum
      AND a~usuario IN s_user
      AND a~fecha IN s_date
      AND a~hora IN s_time
      AND a~operario IN s_oper
      AND b~nombre IN s_name2.

  IF sy-subrc NE 0.
    MESSAGE 'No hay datos para la selección realizada' TYPE 'I'.
  ENDIF.

END-OF-SELECTION.

* Instanciar ALV desde tabla interna
  cl_salv_table=>factory( IMPORTING r_salv_table = gr_table CHANGING t_table = gt_datos ).

* Disposición ALV
  layout_settings = gr_table->get_layout( ).
  layout_key-report = sy-repid.
  layout_settings->set_key( layout_key ).
  layout_settings->set_save_restriction( if_salv_c_layout=>restrict_none ).

* Optimizar columnas
  columns = gr_table->get_columns( ).
  columns->set_optimize( ).

* Modificar/ocultar columnas
  TRY.
      column = columns->get_column( 'MANDT' ).
      column->set_visible( if_salv_c_bool_sap=>false ).
    CATCH cx_salv_not_found INTO not_found.
  ENDTRY.

* Funciones ALV activar
  functions = gr_table->get_functions( ).
  functions->set_all( ).

* Opciones generales ALV
  display_settings = gr_table->get_display_settings( ).
  display_settings->set_striped_pattern( if_salv_c_bool_sap=>true ).
  display_settings->set_list_header( 'Registros de operarios' ).

  gr_table->display( ).
