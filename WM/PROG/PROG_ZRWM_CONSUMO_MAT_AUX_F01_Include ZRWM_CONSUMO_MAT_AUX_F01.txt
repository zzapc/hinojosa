*&---------------------------------------------------------------------*
*&  Include           ZRWM_CONSUMO_MAT_AUX_F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  F_CREA_OT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_GV_MBLNR  text
*      -->P_GV_MJAHR  text
*----------------------------------------------------------------------*
FORM f_crea_ot  USING  pe_mblnr TYPE mblnr
                       pe_mjahr TYPE mjahr
                CHANGING pc_tbnum TYPE tbnum
                         pc_tanum TYPE tanum.
*MZ_WM_RF_F01 -> fin_descarga_puerto
  DATA: ls_ltbk  TYPE ltbk,
        lt_ltbp  TYPE STANDARD TABLE OF ltbp,
        lt_trite TYPE l03b_trite_t,
        ls_trite TYPE l03b_trite.

  CLEAR: pc_tbnum, pc_tanum.

  IF pe_mblnr IS NOT INITIAL AND pe_mjahr IS NOT INITIAL.

    SELECT SINGLE *
      INTO ls_ltbk
      FROM ltbk
      WHERE lgnum = p_lgnum AND
            mblnr = pe_mblnr AND
            mjahr = pe_mjahr.

    IF sy-subrc EQ 0.

      pc_tbnum = ls_ltbk-tbnum.

      SELECT * INTO TABLE lt_ltbp
                 FROM ltbp
                 WHERE lgnum = p_lgnum AND
                       tbnum = pc_tbnum.

      LOOP AT lt_ltbp INTO DATA(ls_ltbp).
        ls_trite-tbpos = ls_ltbp-tbpos.
        ls_trite-altme = ls_ltbp-meins.
        ls_trite-anfme = ls_ltbp-menge.
        ls_trite-charg = ls_ltbp-charg.
        ls_trite-nltyp = ls_ltbk-nltyp.
        ls_trite-nlpla = ls_ltbk-nlpla.

        ls_trite-vlenr = ls_ltbp-charg.
        ls_trite-vlpla = gs_lqua-lgpla.
        ls_trite-vltyp = gs_lqua-lgtyp.
        ls_trite-letyp = gs_lqua-letyp.
        APPEND ls_trite TO lt_trite.
      ENDLOOP.

      CALL FUNCTION 'L_TO_CREATE_TR'
        EXPORTING
          i_lgnum                        = p_lgnum
          i_tbnum                        = pc_tbnum
*         i_bname                        = gst_zwm005-bname
          i_squit                        = abap_true
          i_tbeli                        = abap_true
          it_trite                       = lt_trite
        IMPORTING
          e_tanum                        = pc_tanum
        EXCEPTIONS
          foreign_lock                   = 1
          qm_relevant                    = 2
          tr_completed                   = 3
          xfeld_wrong                    = 4
          ldest_wrong                    = 5
          drukz_wrong                    = 6
          tr_wrong                       = 7
          squit_forbidden                = 8
          no_to_created                  = 9
          update_without_commit          = 10
          no_authority                   = 11
          preallocated_stock             = 12
          partial_transfer_req_forbidden = 13
          input_error                    = 14
          OTHERS                         = 15.

      IF pc_tanum IS NOT INITIAL.

        CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
          EXPORTING
            wait = 'X'.


      ENDIF.

    ENDIF.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  F_CREA_291
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_LENUM  text
*      <--P_GV_MBLNR  text
*      <--P_GV_MJAHR  text
*----------------------------------------------------------------------*
FORM f_crea_291  USING    pe_lenum TYPE lenum
                 CHANGING pc_mblnr TYPE mblnr
                          pc_mjahr TYPE mjahr.
  CONSTANTS: lc_03         TYPE gm_code VALUE '03'. " MB1A - Goods Issue

  DATA: ls_header TYPE bapi2017_gm_head_01,
        ls_code   TYPE bapi2017_gm_code,
        ls_ewm    TYPE /spe/bapi2017_gm_ref_ewm,
        lt_item   TYPE STANDARD TABLE OF bapi2017_gm_item_create,
        ls_item   TYPE bapi2017_gm_item_create,
        lt_ret    TYPE bapiret2_t.

  CLEAR: pc_mblnr, pc_mjahr.

  IF pe_lenum IS NOT INITIAL.

    SELECT SINGLE * INTO gs_lqua
      FROM lqua
       WHERE lgnum = p_lgnum AND
             lenum = p_lenum.

    IF sy-subrc EQ 0.
      ls_header-pstng_date = sy-datum.
      ls_header-doc_date   = sy-datum.
      ls_header-ref_doc_no = pe_lenum.
      ls_header-pr_uname   = sy-uname.
      ls_header-header_txt = 'Cons mat. aux. desde cuanto' .

* codigo de tipo de movimiento de mercancias
      ls_code-gm_code = lc_03.

* datos de la UA que genera el movimiento
      ls_item-plant    = gs_lqua-werks.
      ls_item-stge_loc = gs_lqua-lgort.
      ls_item-material = gs_lqua-matnr.
      ls_item-batch    = gs_lqua-charg.
      ls_item-move_type = '291'.
      ls_item-entry_qnt = gs_lqua-gesme.
      ls_item-entry_uom = gs_lqua-gewei.

      APPEND ls_item TO lt_item.

      CALL FUNCTION 'BAPI_GOODSMVT_CREATE'
        EXPORTING
          goodsmvt_header  = ls_header
          goodsmvt_code    = ls_code
*         goodsmvt_ref_ewm = ls_ewm
        IMPORTING
          materialdocument = pc_mblnr
          matdocumentyear  = pc_mjahr
        TABLES
          goodsmvt_item    = lt_item
          return           = lt_ret.

      IF pc_mblnr IS NOT INITIAL AND pc_mjahr IS NOT INITIAL.
        CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'.
      ENDIF.
    ENDIF.

  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  F_SHOW_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM f_show_data .
* mostrar ALV con datos de GT_data
  DATA:  ls_data             TYPE tr_resumen,
         lv_functions        TYPE REF TO cl_salv_functions_list,
         lv_title            TYPE lvc_title,
         lv_display_settings TYPE REF TO cl_salv_display_settings,
         lv_selections       TYPE REF TO cl_salv_selections,
         lv_events           TYPE REF TO cl_salv_events_table,
         lv_layout           TYPE REF TO cl_salv_layout,
         lv_lay              TYPE slis_vari,
         lv_key              TYPE salv_s_layout_key.

  REFRESH gt_data.

  CLEAR ls_data.
  ls_data-lgnum = p_lgnum.
  ls_data-lenum = p_lenum.
  ls_data-mblnr = gv_mblnr.
  ls_data-mjahr = gv_mjahr.
  ls_data-tbnum = gv_tbnum.
  ls_data-tanum = gv_tanum.
  APPEND ls_data TO gt_data.

  IF gt_data[] IS NOT INITIAL.

    TRY.
        CALL METHOD cl_salv_table=>factory
          IMPORTING
            r_salv_table = go_table
          CHANGING
            t_table      = gt_data.           "internal table


        lv_display_settings = go_table->get_display_settings( ).
        lv_title = 'Consumo de material auxiliar'.
        lv_display_settings->set_list_header( lv_title ).
        lv_display_settings->set_fit_column_to_table_size( abap_true ).
        lv_display_settings->set_striped_pattern( abap_true ).

* grabamos el layout
        lv_layout = go_table->get_layout( ).
        lv_key-report = sy-repid.
        lv_layout->set_key( lv_key ).
        lv_layout->set_save_restriction( if_salv_c_layout=>restrict_none ).
        IF p_vari IS NOT INITIAL.
          MOVE p_vari TO lv_lay.
          lv_layout->set_initial_layout( lv_lay ).
        ENDIF.

        lv_functions = go_table->get_functions( ).
        lv_functions->set_all( abap_true ).
        lv_functions->set_export_xml( abap_true ).
        lv_functions->set_export_spreadsheet( abap_true ).
        lv_functions->set_view_lotus( abap_false ).
        lv_functions->set_graphics( abap_false ).
        lv_functions->set_export_localfile( abap_true ).
        lv_functions->set_view_excel( abap_true ).

* Enable cell selection mode
        lv_selections = go_table->get_selections( ).
        lv_selections->set_selection_mode( if_salv_c_selection_mode=>row_column ).

        lv_events = go_table->get_event( ).
        IF go_handler IS NOT BOUND.
          CREATE OBJECT go_handler.
        ENDIF.
        SET HANDLER go_handler->on_link_click FOR lv_events.

        PERFORM f_modifica_cabeceras.
* Display table
        go_table->display( ).
      CATCH cx_salv_msg.
    ENDTRY.
  ELSE.
    MESSAGE text-e01 TYPE 'S' DISPLAY LIKE 'E'.
  ENDIF.
ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  F_DAME_VARIANT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      <--P_P_VARI  text
*----------------------------------------------------------------------*
FORM f_dame_variant  CHANGING pc_vari TYPE slis_vari.
  DATA: ls_variant TYPE disvariant,
        lv_exit    TYPE char1.

  ls_variant-report = sy-repid.

  CALL FUNCTION 'LVC_VARIANT_F4'
    EXPORTING
      is_variant = ls_variant
      i_save     = 'A'
    IMPORTING
      e_exit     = lv_exit
      es_variant = ls_variant
    EXCEPTIONS
      not_found  = 1
      OTHERS     = 2.
  IF sy-subrc NE 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ELSE.
    CHECK lv_exit EQ space.
    pc_vari = ls_variant-variant.
  ENDIF.
ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  F_MODIFICA_CABECERAS
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM f_modifica_cabeceras .
  DATA: lo_cols       TYPE REF TO cl_salv_columns,
        lo_column_tab TYPE REF TO cl_salv_column_table,
        lt_dd03l      TYPE enh_t_dd03l,
        lv_text       TYPE string,
        lo_column     TYPE REF TO cl_salv_column.

  lo_cols = go_table->get_columns( ).
  lo_cols->set_optimize( abap_true ).
* MBLNR
  lo_column = lo_cols->get_column( 'MBLNR' ).
  lv_text = 'Doc material'.
  PERFORM f_modif_columna USING lv_text
                          CHANGING lo_column.
  TRY.
      lo_column_tab ?= lo_cols->get_column( 'MBLNR' ).

    CATCH cx_salv_not_found.                            "#EC NO_HANDLER

  ENDTRY.
  lo_column_tab->set_cell_type( if_salv_c_cell_type=>hotspot ).

* LENUM
  lo_column = lo_cols->get_column( 'LENUM' ).
  lv_text = 'Unidad almacén'.
  PERFORM f_modif_columna USING lv_text
                          CHANGING lo_column.
  TRY.
      lo_column_tab ?= lo_cols->get_column( 'LENUM' ).

    CATCH cx_salv_not_found.                            "#EC NO_HANDLER

  ENDTRY.
  lo_column_tab->set_cell_type( if_salv_c_cell_type=>hotspot ).

* TANUM
  lo_column = lo_cols->get_column( 'TANUM' ).
  lv_text = 'Número de OT'.
  PERFORM f_modif_columna USING lv_text
                          CHANGING lo_column.
  TRY.
      lo_column_tab ?= lo_cols->get_column( 'TANUM' ).

    CATCH cx_salv_not_found.                            "#EC NO_HANDLER

  ENDTRY.
  lo_column_tab->set_cell_type( if_salv_c_cell_type=>hotspot ).

* TBNUM
  lo_column = lo_cols->get_column( 'TBNUM' ).
  lv_text = 'Número de Necesidad'.
  PERFORM f_modif_columna USING lv_text
                          CHANGING lo_column.
  TRY.
      lo_column_tab ?= lo_cols->get_column( 'TBNUM' ).

    CATCH cx_salv_not_found.                            "#EC NO_HANDLER

  ENDTRY.
  lo_column_tab->set_cell_type( if_salv_c_cell_type=>hotspot ).


ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  F_MODIF_COLUMNA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_LV_TEXT  text
*      <--P_LO_COLUMN  text
*----------------------------------------------------------------------*
FORM f_modif_columna USING    pe_text TYPE string
                      CHANGING pc_column TYPE REF TO cl_salv_column.
  DATA: lv_short TYPE scrtext_s,
        lv_med   TYPE scrtext_m,
        lv_long  TYPE scrtext_l,
        lv_len   TYPE lvc_outlen,
        lv_tool  TYPE lvc_tip.


  lv_long  = pe_text.
  lv_med   = pe_text.
  lv_short = pe_text.
  pc_column->set_long_text( lv_long ). "40
  pc_column->set_fixed_header_text( abap_true ).
  pc_column->set_medium_text( lv_med ). "20
  pc_column->set_short_text( lv_short ). "10
  pc_column->set_tooltip( lv_tool ).
  lv_len = strlen( pe_text ).
  pc_column->set_output_length( lv_len ).
  pc_column->set_zero( abap_true ).
ENDFORM.
