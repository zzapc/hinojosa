FUNCTION Z_WHS_POPUP_EDICION_LINEA.
*"--------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     REFERENCE(LINEA) TYPE  ZCONTENIDO_FICHERO
*"     REFERENCE(TITULO) TYPE  ANY
*"     REFERENCE(EDITAR) TYPE  ABAP_BOOL DEFAULT ''
*"     REFERENCE(IDMSG) TYPE  ZIDMSG OPTIONAL
*"     REFERENCE(IDESTRUCTURA) TYPE  ZESTRUCTURA_CAB OPTIONAL
*"     REFERENCE(SOLO_UTILES) TYPE  ABAP_BOOL DEFAULT 'X'
*"  EXPORTING
*"     REFERENCE(CANCELADO) TYPE  ABAP_BOOL
*"  CHANGING
*"     REFERENCE(VALORES) TYPE  ZWHS_VALORES_T
*"--------------------------------------------------------------------
DATA: l_salida  TYPE string,
        l_mensaje TYPE bapi_msg.

  FIELD-SYMBOLS: <valor> TYPE zwhs_valores.

  v_solo_utiles = solo_utiles.

  IF NOT idmsg IS INITIAL.
    v_idmsg = idmsg.
  ENDIF.

  IF NOT idestructura IS INITIAL.
    v_idestructura = idestructura.
  ENDIF.

  i_valores = valores.
  LOOP AT i_valores ASSIGNING <valor>.
    <valor>-recalcular = '@0M@'.


    zwhs=>valor_easy2sap( EXPORTING idmsg        = v_idmsg
                                       idestructura = v_idestructura
                                       idcampo      = <valor>-idcampo
                                       entrada      = <valor>-valor
                             IMPORTING salida       = l_salida
                                       mensaje      = l_mensaje ).
    IF l_mensaje IS INITIAL.
      <valor>-valor_sap = l_salida.
    ENDIF.
  ENDLOOP.

  i_valores_todos = i_valores.
  IF solo_utiles = 'X'.
    DELETE i_valores WHERE activo = ''.
  ENDIF.

  v_titulo  = titulo.
  v_editar  = editar.
  v_linea   = linea.

  CLEAR v_inicio.

  CALL SCREEN 0100 STARTING AT 3 1 ENDING AT 128 18.

  IF i_valores IS INITIAL.
    cancelado = 'X'.
  ELSE.

    IF solo_utiles = 'X'.
      LOOP AT i_valores_todos ASSIGNING <valor> WHERE activo = ''.
        APPEND <valor> TO i_valores.
      ENDLOOP.
      SORT i_valores_todos.
    ENDIF.

    valores = i_valores.
    CLEAR cancelado.
  ENDIF.

  o_alv->free( ).
  CLEAR: o_alv, o_event.





ENDFUNCTION.
