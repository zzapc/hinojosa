*&---------------------------------------------------------------------*
*&  Include           ZRINV0002_CLASS
*&---------------------------------------------------------------------*

****************************************************************
* LOCAL CLASSES: Definition
****************************************************************
*===============================================================
CLASS lcl_application DEFINITION.
* PUBLICO: Los métodos de los eventos
  PUBLIC SECTION.

*   Evento para navegar
    METHODS handle_hotspot
                  FOR EVENT hotspot_click OF cl_gui_alv_grid
      IMPORTING e_row_id e_column_id es_row_no.


*   Evento para que añada o elimine botones al menu
    METHODS handle_toolbar
                  FOR EVENT toolbar OF cl_gui_alv_grid
      IMPORTING e_object e_interactive.

*   Evento para detectar accion botonera
    METHODS handle_user_command
                  FOR EVENT user_command OF cl_gui_alv_grid
      IMPORTING e_ucomm.

*   Evento para editar ALV
    METHODS handle_data_changed
                  FOR EVENT data_changed OF cl_gui_alv_grid
      IMPORTING er_data_changed.

ENDCLASS.                    "lcl_event_receiver DEFINITION



*&---------------------------------------------------------------------*
*&       Class (Implementation)  lcl_application
*&---------------------------------------------------------------------*
*        Text
*----------------------------------------------------------------------*
CLASS lcl_application IMPLEMENTATION.
*&---------------------------------------------------------------------*
*         METHOD handle_toolbar
*----------------------------------------------------------------------*
  METHOD handle_toolbar.

    CASE sy-dynnr.
      WHEN '0100'.
*   crear inventario
        CLEAR gs_toolbar.
        MOVE  'CREAR_INV'                TO gs_toolbar-function.
        MOVE  icon_intensify             TO gs_toolbar-icon.
        MOVE  text-002                   TO gs_toolbar-quickinfo.
        MOVE  text-002                   TO gs_toolbar-text.
        MOVE space                       TO gs_toolbar-disabled.
        APPEND gs_toolbar                TO e_object->mt_toolbar.

      WHEN '0200'.
*   guardar
        CLEAR gs_toolbar.
        MOVE  'SAVE_ALV'                      TO gs_toolbar-function.
        MOVE  icon_system_save                TO gs_toolbar-icon.
        MOVE  'Guardar cambios de posiciones' TO gs_toolbar-quickinfo.
        MOVE  'Guardar cambios'               TO gs_toolbar-text.
        MOVE space                            TO gs_toolbar-disabled.
        APPEND gs_toolbar                     TO e_object->mt_toolbar.

    ENDCASE.

*   Borrado de Botones Estandar:
    DELETE e_object->mt_toolbar WHERE ( function EQ '&LOCAL&APPEND'
    OR function EQ '&LOCAL&INSERT_ROW'
    OR function EQ '&LOCAL&DELETE_ROW'
    OR function EQ '&LOCAL&COPY_ROW'
    OR function EQ '&INFO'
    OR function EQ 'CAMBIAR_STATUS'
    OR function EQ 'EXPORT'
    OR function EQ '&REFRESH'
    OR function EQ 'IMPORT'
    OR function EQ 'VALIDAR'
    OR function EQ 'FINAL'
    OR function EQ '&LOCAL&CUT'
    OR function EQ '&LOCAL&COPY'
    OR function EQ '&LOCAL&PASTE'
    OR function EQ '&LOCAL&UNDO'
    OR function EQ '&LOCAL&APPEND'
    OR function EQ '&LOCAL&INSERT_ROW'
    OR function EQ '&LOCAL&DELETE_ROW'
    OR function EQ '&LOCAL&COPY_ROW' ).

    IF zsinv0002_grid-status CA 'CBI'.
      DELETE e_object->mt_toolbar WHERE function EQ 'SAVE_ALV'.
    ENDIF.

  ENDMETHOD.                    "handle_toolbar


*&---------------------------------------------------------------------*
*         METHOD handle_user_command
*----------------------------------------------------------------------*
  METHOD handle_user_command.
    CASE e_ucomm. "GV_OKCODE.
      WHEN 'CREAR_INV'.
        CLEAR: e_ucomm. " gv_werks, gv_lgort.
        CALL SCREEN 0300 STARTING AT 10 10.

      WHEN 'SAVE_ALV'.
        CALL METHOD g_grid_200->check_changed_data.

        PERFORM actualizar_modificaciones.

        IF zsinv0002_grid-status EQ 'F'.
          CALL FUNCTION 'ZMM_INV_IMPACTO_ECONOMICO'
            EXPORTING
              docum = zsinv0002_grid-docum.

          SELECT SINGLE * FROM ztinv2_ci
            INTO CORRESPONDING FIELDS OF zsinv0002_grid
            WHERE docum EQ zsinv0002_grid-docum.
        ENDIF.

        PERFORM listado_posiciones USING zsinv0002_grid-docum
                                         zsinv0002_grid-status.

    ENDCASE.
  ENDMETHOD.

*&---------------------------------------------------------------------*
*         METHOD handle_hotspot
*----------------------------------------------------------------------*
  METHOD handle_hotspot.
    DATA: lt_casuistica TYPE STANDARD TABLE OF dd07v,
          wa_casuistica TYPE                   dd07v,
          sw_bloqueado  TYPE         flag.

    CLEAR gv_imp_calc.
    CLEAR zsinv0002_grid.

*   Leemos de la tabla la línea pulsada
    IF sy-dynnr EQ '0100'.
      READ TABLE gti_mostrar INDEX es_row_no-row_id INTO g_mostrar.
      MOVE-CORRESPONDING g_mostrar TO zsinv0002_grid.
    ELSEIF sy-dynnr EQ '0500'.
      READ TABLE gti_mostrar5 INDEX es_row_no-row_id INTO g_mostrar5.
      MOVE-CORRESPONDING g_mostrar5 TO zsinv0002_grid.
    ENDIF.
    IF zsinv0002_grid IS NOT INITIAL.
      CASE e_column_id.
        WHEN 'DOCUM'.
          PERFORM bloquear_inventario USING zsinv0002_grid-docum 'B' CHANGING sw_bloqueado.
          IF sw_bloqueado EQ c_si.
            DATA: lt_ztinv2_pi TYPE ty_ztinv2_pi.

            PERFORM listado_posiciones USING zsinv0002_grid-docum
                                             zsinv0002_grid-status.

            CALL SCREEN '0200'.
          ENDIF.
        WHEN 'MULTI' OR 'ICON_MULTI'.
          IF zsinv0002_grid-multi IS NOT INITIAL.
            SELECT * FROM ztinv2_ci_multi
              INTO CORRESPONDING FIELDS OF TABLE gti_mostrar5
              WHERE docum EQ zsinv0002_grid-docum.

            LOOP AT gti_mostrar5 ASSIGNING <mostrar5>.
              MOVE-CORRESPONDING zsinv0002_grid TO <mostrar5>.
            ENDLOOP.

            CALL SCREEN '0500'.
          ENDIF.
        WHEN 'ICON_LOG'.
          IF zsinv0002_grid-log IS NOT INITIAL.
            g_extnumber = zsinv0002_grid-docum.

            CALL FUNCTION 'APPL_LOG_DISPLAY'
              EXPORTING
                object                    = 'ZMM'
                subobject                 = 'ZINV2'
                external_number           = g_extnumber
                date_from                 = zsinv0002_grid-erdat " para que salgan siempre todos
                suppress_selection_dialog = 'X'.
          ENDIF.
      ENDCASE.
    ENDIF.

  ENDMETHOD.                    "handle_hotspot_click
*&---------------------------------------------------------------------*
*         METHOD handle data changed
*----------------------------------------------------------------------*
  METHOD handle_data_changed.

    PERFORM posicion_modificada USING er_data_changed.

  ENDMETHOD.                    "  "handle_data_changed
ENDCLASS.                    "lcl_application IMPLEMENTATION
