FUNCTION z_e80_envio_entregas_prepa.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     REFERENCE(I_VBELN) TYPE  VBELN_VL OPTIONAL
*"     REFERENCE(I_WERKS) TYPE  WERKS_D OPTIONAL
*"     REFERENCE(I_IDMSG) TYPE  ZE80_ID_MENSAJE OPTIONAL
*"----------------------------------------------------------------------
  DATA: lo_smile                      TYPE REF TO zco_si_smile80_shp_ins_out_sap,
        ls_output                     TYPE zmt_smile80_shp_ins_out_sap_to,
        ls_zdt_smile80_shp_ins_out_t4 TYPE zdt_smile80_shp_ins_out_sap_t4,
        ls_document                   TYPE zdt_smile80_shp_ins_out_sap_t2,
        ls_body                       TYPE zdt_smile80_shp_ins_out_sap_t5,
        ls_header                     TYPE zdt_smile80_shp_ins_out_sap_t1,
        lt_delivery_item              TYPE zdt_smile80_shp_ins_out_sa_tab,
        ls_delivery_item              LIKE LINE OF lt_delivery_item,
        ls_likp                       TYPE likp,
        lt_lips                       TYPE TABLE OF lips,
        lt_lips2                      TYPE TABLE OF lips,
        ls_lips                       LIKE LINE OF lt_lips,
        ls_lips2                      LIKE LINE OF lt_lips,
        lv_rango                      TYPE nrnr,
        lv_mensaje_id                 TYPE string,
        tsl                           TYPE timestampl,
        lv_vbeln                      TYPE vbeln_vl,
        ls_zte80_messages             TYPE zte80_messages,
        lt_datos                      TYPE zte80_messages.

  IF i_werks = '3000'.

    CREATE OBJECT lo_smile.

    SELECT SINGLE * FROM likp INTO ls_likp WHERE vbeln = i_vbeln.

    SELECT * FROM lips INTO TABLE lt_lips WHERE vbeln = i_vbeln.

    CASE i_werks.
      WHEN '3000'. lv_rango = '01'.
    ENDCASE.

    CALL FUNCTION 'NUMBER_GET_NEXT'
      EXPORTING
        nr_range_nr   = lv_rango
        object        = 'ZPP_E80MSG'
        quantity      = '1'
        ignore_buffer = 'X'
      IMPORTING
        number        = lv_mensaje_id.

    GET TIME STAMP FIELD tsl.

    "Consolidar materiales por kilos
    LOOP AT lt_lips INTO ls_lips.

      READ TABLE lt_lips2 ASSIGNING FIELD-SYMBOL(<fs_lips2>) WITH KEY matnr = ls_lips-matnr.

      IF sy-subrc <> 0.
        APPEND ls_lips TO lt_lips2.

      ELSE.
        <fs_lips2>-lfimg = <fs_lips2>-lfimg + ls_lips-lfimg.

      ENDIF.

    ENDLOOP.

    LOOP AT lt_lips2 INTO ls_lips2.

      ls_delivery_item-sku = ls_lips2-matnr.
      ls_delivery_item-quantity = ls_lips2-lfimg.

      ls_delivery_item-sku = |{ ls_delivery_item-sku ALPHA = OUT }|.

      APPEND ls_delivery_item TO lt_delivery_item.

    ENDLOOP.

    lv_vbeln = |{ i_vbeln ALPHA = OUT }|.
    ls_likp-kunnr = |{ ls_likp-kunnr ALPHA = OUT }|.

    "Rellenar cabecera del mensaje
    ls_header-sender       = 'HOST'.
    ls_header-receiver     = 'E80'.
    ls_header-send_date    = tsl.
    ls_header-message_type = 'SHP_INS'.
    ls_header-message_id   = lv_mensaje_id.

    "Rellenar cuerpo del mensaje
    ls_body-shipment_number = lv_vbeln.
    ls_body-due_date_time   = ls_likp-lfdat.
    ls_body-priority        = '5'.
    ls_body-customer        = ls_likp-kunnr.
    ls_body-quality_type    = ls_likp-zzsegcal.

    ls_body-delivery_item                   = lt_delivery_item.
    ls_document-header                      = ls_header.
    ls_document-body                        = ls_body.
    ls_zdt_smile80_shp_ins_out_t4-document  = ls_document.
    ls_output-mt_smile80_shp_ins_out_sap_to = ls_zdt_smile80_shp_ins_out_t4.

    lo_smile->si_smile80_shp_ins_out_sap_to(
    EXPORTING output = ls_output ).

    ls_zte80_messages-id_mensaje = lv_mensaje_id.
    ls_zte80_messages-estado = '0'.
    ls_zte80_messages-centro = i_werks.
    ls_zte80_messages-fecha_env = sy-datum.
    ls_zte80_messages-hora_env = sy-uzeit.
    ls_zte80_messages-tipo_mensaje = 'SHP_INS'.
    ls_zte80_messages-numero_de_dcmnt = lv_vbeln.
    MODIFY zte80_messages FROM ls_zte80_messages.

    UPDATE zte80_messages SET estado = '2' fecha_rep = sy-datum hora_rep = sy-uzeit WHERE id_mensaje = i_idmsg.

    CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
      EXPORTING
        wait = abap_true.

  ELSE.

  ENDIF.

ENDFUNCTION.
