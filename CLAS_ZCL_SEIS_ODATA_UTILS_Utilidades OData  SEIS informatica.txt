
CLASS zcl_seis_odata_utils DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.

    TYPES:
      "Tipo inventado para que puedan compilar los ejemplos
      BEGIN OF ty_s_mgw_request_context,
        werks TYPE werks_d,
      END OF ty_s_mgw_request_context .
    TYPES:
      BEGIN OF ty_s_mgw_response_entity_cntxt,
        werks TYPE werks_d,
      END OF ty_s_mgw_response_entity_cntxt .
    TYPES:
      BEGIN OF ty_t_inline_info,
        werks TYPE werks_d,
      END OF ty_t_inline_info .

    CLASS-METHODS anyadir_headers_control_cache
      CHANGING
        !tp_headers TYPE tihttpnvp .
    CLASS-METHODS lanzar_excepcion
      IMPORTING
        VALUE(message)    TYPE simple OPTIONAL
        VALUE(id)         TYPE symsgid DEFAULT sy-msgid
        VALUE(number)     TYPE symsgno DEFAULT sy-msgno
        VALUE(message_v1) TYPE simple DEFAULT sy-msgv1
        VALUE(message_v2) TYPE simple DEFAULT sy-msgv2
        VALUE(message_v3) TYPE simple DEFAULT sy-msgv3
        VALUE(message_v4) TYPE simple DEFAULT sy-msgv4
        VALUE(bapiret2)   TYPE bapiret2 OPTIONAL
        VALUE(bapiret2_t) TYPE bapiret2_t OPTIONAL
          PREFERRED PARAMETER message
      RAISING
        /iwbep/cx_mgw_busi_exception .
    CLASS-METHODS grabar_anexo_content_server
      IMPORTING
        VALUE(key)         TYPE swo_typeid
        VALUE(type)        TYPE swo_objtyp
        VALUE(description) TYPE string
        VALUE(extension)   TYPE string DEFAULT 'JPG'
        !anexo             TYPE xstring
      RETURNING
        VALUE(wp_obj_id)   TYPE soodk
      RAISING
        /iwbep/cx_mgw_busi_exception .
    CLASS-METHODS convertir_order_by_a_string
      IMPORTING
        VALUE(it_order)  TYPE /iwbep/t_mgw_sorting_order
      RETURNING
        VALUE(vp_return) TYPE string .
    CLASS-METHODS tratar_key_converted
      IMPORTING
        !io_tech_request_context TYPE REF TO /iwbep/if_mgw_req_entityset OPTIONAL
      CHANGING
        !es_entity               TYPE data .
    CLASS-METHODS tratar_entityset
      IMPORTING
        !io_tech_request_context  TYPE REF TO /iwbep/if_mgw_req_entityset OPTIONAL
        !it_filter_select_options TYPE /iwbep/t_mgw_select_option OPTIONAL
        !it_order                 TYPE /iwbep/t_mgw_sorting_order OPTIONAL
        !it_campos_astext         TYPE stringtab OPTIONAL
        !is_paging                TYPE /iwbep/s_mgw_paging OPTIONAL
      CHANGING
        !et_entityset             TYPE STANDARD TABLE .
    CLASS-METHODS tratar_paging
      IMPORTING
        !is_paging    TYPE /iwbep/s_mgw_paging
      CHANGING
        !et_entityset TYPE STANDARD TABLE .
    CLASS-METHODS tratar_ordenacion
      IMPORTING
        !it_order         TYPE /iwbep/t_mgw_sorting_order
        !it_campos_astext TYPE stringtab OPTIONAL
      CHANGING
        !et_entityset     TYPE STANDARD TABLE .
    CLASS-METHODS tratar_key
      IMPORTING
        !tp_key                       TYPE /iwbep/t_mgw_name_value_pair
        !vp_convertir_formato_interno TYPE xfeld DEFAULT 'X'
      EXPORTING
        !wp_return                    TYPE any .
    CLASS-METHODS convertir_filtros_formato_int
      CHANGING
        !wp_entity                TYPE any
        !it_filter_select_options TYPE /iwbep/t_mgw_select_option .
    CLASS-METHODS tratar_filtros
      IMPORTING
        !it_filter_select_options TYPE /iwbep/t_mgw_select_option
      CHANGING
        !et_entityset             TYPE STANDARD TABLE .
    CLASS-METHODS conversion_num_interno
      IMPORTING
        VALUE(input) TYPE any
      EXPORTING
        !output      TYPE any .
    CLASS-METHODS convert_filter_string_a_rango
      IMPORTING
        !iv_filter_string               TYPE string
      RETURNING
        VALUE(it_filter_select_options) TYPE /iwbep/t_mgw_select_option .
  PROTECTED SECTION.

    METHODS ejemplo_update_entity
      IMPORTING
        !iv_entity_name          TYPE string
        !iv_entity_set_name      TYPE string
        !iv_source_name          TYPE string
        !it_key_tab              TYPE /iwbep/t_mgw_name_value_pair
        !io_tech_request_context TYPE REF TO /iwbep/if_mgw_req_entity_u OPTIONAL
        !it_navigation_path      TYPE /iwbep/t_mgw_navigation_path
        !io_data_provider        TYPE REF TO /iwbep/if_mgw_entry_provider OPTIONAL
      EXPORTING
        !er_entity               TYPE data
      RAISING
        /iwbep/cx_mgw_busi_exception
        /iwbep/cx_mgw_tech_exception .
    METHODS ejemplo_create_entity
      IMPORTING
        !iv_entity_name          TYPE string
        !iv_entity_set_name      TYPE string
        !iv_source_name          TYPE string
        !it_key_tab              TYPE /iwbep/t_mgw_name_value_pair
        !io_tech_request_context TYPE REF TO /iwbep/if_mgw_req_entity_c OPTIONAL
        !it_navigation_path      TYPE /iwbep/t_mgw_navigation_path
        !io_data_provider        TYPE REF TO /iwbep/if_mgw_entry_provider OPTIONAL
      EXPORTING
        !er_entity               TYPE data
      RAISING
        /iwbep/cx_mgw_busi_exception
        /iwbep/cx_mgw_tech_exception .
    METHODS ejemplo_delete_entity
      IMPORTING
        !iv_entity_name          TYPE string
        !iv_entity_set_name      TYPE string
        !iv_source_name          TYPE string
        !it_key_tab              TYPE /iwbep/t_mgw_name_value_pair
        !io_tech_request_context TYPE REF TO /iwbep/if_mgw_req_entity_d OPTIONAL
        !it_navigation_path      TYPE /iwbep/t_mgw_navigation_path
      RAISING
        /iwbep/cx_mgw_busi_exception
        /iwbep/cx_mgw_tech_exception .
    METHODS ejemplo_get_entity
      IMPORTING
        !iv_entity_name          TYPE string
        !iv_entity_set_name      TYPE string
        !iv_source_name          TYPE string
        !it_key_tab              TYPE /iwbep/t_mgw_name_value_pair
        !io_request_object       TYPE REF TO /iwbep/if_mgw_req_entity OPTIONAL
        !io_tech_request_context TYPE REF TO /iwbep/if_mgw_req_entity OPTIONAL
        !it_navigation_path      TYPE /iwbep/t_mgw_navigation_path
      EXPORTING
        !er_entity               TYPE data
        !es_response_context     TYPE /iwbep/if_mgw_appl_srv_runtime=>ty_s_mgw_response_entity_cntxt
      RAISING
        /iwbep/cx_mgw_busi_exception
        /iwbep/cx_mgw_tech_exception .
    METHODS ejemplo_get_entityset
      IMPORTING
        !iv_entity_name           TYPE string
        !iv_entity_set_name       TYPE string
        !iv_source_name           TYPE string
        !it_filter_select_options TYPE /iwbep/t_mgw_select_option
        !is_paging                TYPE /iwbep/s_mgw_paging
        !it_key_tab               TYPE /iwbep/t_mgw_name_value_pair
        !it_navigation_path       TYPE /iwbep/t_mgw_navigation_path
        !it_order                 TYPE /iwbep/t_mgw_sorting_order
        !iv_filter_string         TYPE string
        !iv_search_string         TYPE string
        !io_tech_request_context  TYPE REF TO /iwbep/if_mgw_req_entityset OPTIONAL
      EXPORTING
        !et_entityset             TYPE data
        !es_response_context      TYPE /iwbep/if_mgw_appl_srv_runtime=>ty_s_mgw_response_context
      RAISING
        /iwbep/cx_mgw_busi_exception
        /iwbep/cx_mgw_tech_exception .
    METHODS ejemplo_get_stream
      IMPORTING
        !iv_entity_name          TYPE string OPTIONAL
        !iv_entity_set_name      TYPE string OPTIONAL
        !iv_source_name          TYPE string OPTIONAL
        !it_key_tab              TYPE /iwbep/t_mgw_name_value_pair OPTIONAL
        !it_navigation_path      TYPE /iwbep/t_mgw_navigation_path OPTIONAL
        !io_tech_request_context TYPE REF TO /iwbep/if_mgw_req_entity OPTIONAL
      EXPORTING
        !er_stream               TYPE REF TO data
        !es_response_context     TYPE /iwbep/if_mgw_appl_srv_runtime=>ty_s_mgw_response_entity_cntxt
      RAISING
        /iwbep/cx_mgw_busi_exception
        /iwbep/cx_mgw_tech_exception .
    METHODS ejemplo_execute_action
      IMPORTING
        !iv_action_name          TYPE string OPTIONAL
        !it_parameter            TYPE /iwbep/t_mgw_name_value_pair OPTIONAL
        !io_tech_request_context TYPE REF TO /iwbep/if_mgw_req_func_import OPTIONAL
      EXPORTING
        !er_data                 TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_busi_exception
        /iwbep/cx_mgw_tech_exception .
    METHODS tratar_key_entityset
      IMPORTING
        !io_tech_request_context TYPE REF TO /iwbep/if_mgw_req_entityset OPTIONAL
      EXPORTING
        !wp_entity               TYPE data
      RAISING
        /iwbep/cx_mgw_busi_exception .
    METHODS tratar_key_entity
      IMPORTING
        !io_tech_request_context TYPE REF TO /iwbep/if_mgw_req_entity OPTIONAL
      EXPORTING
        !wp_entity               TYPE data
      RAISING
        /iwbep/cx_mgw_busi_exception .
    METHODS ejemplo_create_stream
      IMPORTING
        !iv_entity_name          TYPE string OPTIONAL
        !iv_entity_set_name      TYPE string OPTIONAL
        !iv_source_name          TYPE string OPTIONAL
        !is_media_resource       TYPE /iwbep/if_mgw_core_srv_runtime=>ty_s_media_resource
        !it_key_tab              TYPE /iwbep/t_mgw_name_value_pair OPTIONAL
        !it_navigation_path      TYPE /iwbep/t_mgw_navigation_path OPTIONAL
        !iv_slug                 TYPE string
        !io_tech_request_context TYPE REF TO /iwbep/if_mgw_req_entity_c OPTIONAL
      EXPORTING
        !er_entity               TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_busi_exception
        /iwbep/cx_mgw_tech_exception .
  PRIVATE SECTION.

    METHODS read_entity
      IMPORTING
        !iv_entity_name      TYPE string
        !iv_source_name      TYPE string
        !is_request_details  TYPE ty_s_mgw_request_context
      CHANGING
        !ct_headers          TYPE tihttpnvp
        !cr_entity           TYPE REF TO data
        !cs_response_context TYPE ty_s_mgw_response_entity_cntxt OPTIONAL
        !ct_expand_skiptoken TYPE string_table OPTIONAL
        !ct_inline_info      TYPE ty_t_inline_info OPTIONAL
      RAISING
        /iwbep/cx_mgw_busi_exception
        /iwbep/cx_mgw_tech_exception .
    METHODS read_entityset
      IMPORTING
        !iv_entity_name           TYPE string
        !iv_source_name           TYPE string
        !is_paging                TYPE /iwbep/s_mgw_paging
        !it_order                 TYPE /iwbep/t_mgw_sorting_order
        !it_filter_select_options TYPE /iwbep/t_mgw_select_option
        !is_request_details       TYPE ty_s_mgw_request_context
      CHANGING
        !ct_headers               TYPE tihttpnvp
        !cr_entityset             TYPE REF TO data
        !cr_deleted_entityset     TYPE REF TO data OPTIONAL
        !cs_response_context      TYPE /iwbep/if_mgw_appl_types=>ty_s_mgw_response_context OPTIONAL
        !ct_inline_info           TYPE ty_t_inline_info OPTIONAL
      RAISING
        /iwbep/cx_mgw_busi_exception
        /iwbep/cx_mgw_tech_exception .
    METHODS changeset_begin
      IMPORTING
        !it_operation_info TYPE /iwbep/t_mgw_operation_info
      CHANGING
        !cv_defer_mode     TYPE xsdboolean OPTIONAL
      RAISING
        /iwbep/cx_mgw_busi_exception
        /iwbep/cx_mgw_tech_exception .
    METHODS changeset_end
      RAISING
        /iwbep/cx_mgw_busi_exception
        /iwbep/cx_mgw_tech_exception .
endclass. "ZCL_SEIS_ODATA_UTILS definition
class ZCL_SEIS_ODATA_UTILS implementation.
  METHOD anyadir_headers_control_cache.
    APPEND INITIAL LINE TO tp_headers ASSIGNING FIELD-SYMBOL(<fs_header>).
    <fs_header>-name  = 'Cache-Control'.
    <fs_header>-value = 'no-cache, no-store'.
    APPEND INITIAL LINE TO tp_headers ASSIGNING <fs_header>.
    <fs_header>-name  = 'Pragma'.
    <fs_header>-value = 'no-cache'.
  ENDMETHOD.
  METHOD changeset_begin.
    ""Metodo /iwbep/if_mgw_appl_srv_runtime~changeset_begin.
    ""Se indica en clase ZCL_XXXXXXXX_DPC_EXT

    "Método dejado en blanco deliberadamente para permitir llamadas batch
  ENDMETHOD.
  METHOD changeset_end.
    ""Metodo /iwbep/if_mgw_appl_srv_runtime~changeset_end.
    ""Se indica en clase ZCL_XXXXXXXX_DPC_EXT

    "Método dejado en blanco deliberadamente para permitir llamadas batch
  ENDMETHOD.
  METHOD conversion_num_interno.


    DATA vl_dcpfm TYPE usr01-dcpfm.
    DATA vl_output TYPE c LENGTH 80.

    vl_output = input.

    SELECT SINGLE dcpfm FROM usr01 INTO vl_dcpfm
      WHERE bname = sy-uname.
    IF vl_dcpfm = 'X'.
      "Formato X,XXX,XXX.YY
      REPLACE ALL OCCURRENCES OF ',' IN vl_output WITH ''.
    ELSE.
      "Formato X.XXX.XXX,YY
      REPLACE ALL OCCURRENCES OF '.' IN vl_output WITH ''.
      REPLACE ALL OCCURRENCES OF ',' IN vl_output WITH '.'.
    ENDIF.

    output = vl_output.



  ENDMETHOD.
  METHOD convert_filter_string_a_rango.



    DATA:
      lt_filter_select_options TYPE /iwbep/t_mgw_select_option,
      lt_filter_string         TYPE TABLE OF string,
      lt_key_value             TYPE /iwbep/t_mgw_name_value_pair,
      ls_filter_string         TYPE string,
      lv_input                 TYPE string,
      lv_name                  TYPE string,
      lv_value                 TYPE string.

    CONSTANTS:
      co_substringof TYPE string VALUE 'substringof('  ##NO_TEXT,
      co_startswith  TYPE string VALUE 'startswith('   ##NO_TEXT,
      co_endswith    TYPE string VALUE 'endswith('     ##NO_TEXT.

    FIELD-SYMBOLS:
      <fs_range_tab>     LIKE LINE OF lt_filter_select_options,
      <fs_select_option> TYPE /iwbep/s_cod_select_option,
      <fs_key_value>     LIKE LINE OF lt_key_value.

    lv_input = iv_filter_string.

*--- get rid of ) & ' and make AND's uppercase
    REPLACE ALL OCCURRENCES OF ')' IN lv_input WITH ''.
    REPLACE ALL OCCURRENCES OF `'` IN lv_input WITH ''.
    REPLACE ALL OCCURRENCES OF 'and' IN lv_input WITH 'AND'.
    REPLACE ALL OCCURRENCES OF 'eq' IN lv_input WITH 'EQ'.
    SPLIT lv_input AT 'AND' INTO TABLE lt_filter_string.

*--- build a table of key value pairs based on filter string
    LOOP AT lt_filter_string INTO ls_filter_string.
      APPEND INITIAL LINE TO lt_key_value ASSIGNING <fs_key_value>.

      IF ls_filter_string CS co_substringof.
        ls_filter_string = substring_after( val = ls_filter_string sub = co_substringof ).
        CONDENSE ls_filter_string.
        SPLIT ls_filter_string AT ',' INTO lv_value lv_name.
        <fs_key_value>-value = |*{ lv_value }*|.
      ELSEIF ls_filter_string CS co_startswith.
        ls_filter_string = substring_after( val = ls_filter_string sub = co_startswith ).
        CONDENSE ls_filter_string.
        SPLIT ls_filter_string AT ',' INTO lv_name lv_value.
        <fs_key_value>-value = |{ lv_value }*|.
      ELSEIF ls_filter_string CS co_endswith.
        ls_filter_string = substring_after( val = ls_filter_string sub = co_endswith ).
        CONDENSE ls_filter_string.
        SPLIT ls_filter_string AT ',' INTO lv_name lv_value.
        <fs_key_value>-value = |*{ lv_value }|.
      ELSE.
        CONDENSE ls_filter_string.
        SPLIT ls_filter_string AT ' EQ ' INTO lv_name lv_value.
        <fs_key_value>-value = |{ lv_value }|.
      ENDIF.
      <fs_key_value>-name = to_upper( lv_name ).
    ENDLOOP.

*--- add key value pairs to filter select options
    LOOP AT lt_key_value ASSIGNING <fs_key_value>.
      APPEND INITIAL LINE TO lt_filter_select_options ASSIGNING <fs_range_tab>.
      <fs_range_tab>-property = <fs_key_value>-name.
      APPEND INITIAL LINE TO <fs_range_tab>-select_options ASSIGNING <fs_select_option>.
      <fs_select_option>-sign = 'I'.
      IF <fs_key_value>-value CS '*'.
        <fs_select_option>-option = 'CP'.
      ELSE.
        <fs_select_option>-option = 'EQ'.
      ENDIF.
      <fs_select_option>-low = <fs_key_value>-value.
    ENDLOOP.
*--- call method that create where string for filter select options
*   me->set_where_filter( lt_filter_select_options ).

    it_filter_select_options = lt_filter_select_options.


  ENDMETHOD.
  METHOD convertir_filtros_formato_int.

    LOOP AT it_filter_select_options ASSIGNING FIELD-SYMBOL(<fs_filter_select_options>).
      ASSIGN COMPONENT <fs_filter_select_options>-property OF STRUCTURE wp_entity TO FIELD-SYMBOL(<fs>).
      CHECK sy-subrc = 0.

      LOOP AT <fs_filter_select_options>-select_options ASSIGNING FIELD-SYMBOL(<fs_select_option>).
        CHECK <fs_select_option>-option <> 'CP' AND <fs_select_option>-option <> 'NP'.

        <fs> = <fs_select_option>-low.
        DATA(rl_type_descr) = CAST cl_abap_elemdescr( cl_abap_typedescr=>describe_by_data( <fs> ) ).
        DATA(vl_edit_mask) = rl_type_descr->edit_mask.
        REPLACE '==' INTO vl_edit_mask WITH ''.
        CONDENSE vl_edit_mask NO-GAPS.
        CHECK vl_edit_mask IS NOT INITIAL.

        DATA(vl_funcion_conversion_exit) = 'CONVERSION_EXIT_' && vl_edit_mask && '_INPUT'.

        CALL FUNCTION vl_funcion_conversion_exit
          EXPORTING
            input  = <fs_select_option>-low
          IMPORTING
            output = <fs>.
        <fs_select_option>-low = <fs>.

        IF <fs_select_option>-high IS NOT INITIAL.
          CALL FUNCTION vl_funcion_conversion_exit
            EXPORTING
              input  = <fs_select_option>-high
            IMPORTING
              output = <fs>.
          <fs_select_option>-high = <fs>.
        ENDIF.

      ENDLOOP.
    ENDLOOP.



  ENDMETHOD.
  METHOD convertir_order_by_a_string.
    LOOP AT it_order ASSIGNING FIELD-SYMBOL(<fs_order>).
      IF vp_return IS NOT INITIAL.
        vp_return = vp_return && ','.
      ENDIF.
      DATA(vl_tipo_order) = SWITCH #( <fs_order>-order WHEN 'asc' THEN 'ASCENDING' ELSE 'DESCENDING' ).
      CONCATENATE vp_return <fs_order>-property vl_tipo_order INTO vp_return SEPARATED BY space.
    ENDLOOP.
    CONDENSE vp_return.
  ENDMETHOD.
  METHOD ejemplo_create_entity.
    ""Se indica en clase ZCL_XXXXXXXX_DPC_IMP

*  io_data_provider->read_entry_data( IMPORTING es_data = er_entity ).
*  albaranes_cab_create( CHANGING wp_albaranes_cab = er_entity ).

  ENDMETHOD.
  METHOD ejemplo_create_stream.
    ""Metodo /IWBEP/IF_MGW_APPL_SRV_RUNTIME~CREATE_STREAM
    ""Se indica en clase ZCL_XXXXXXXX_DPC_IMP

*    CASE iv_entity_name.
*      WHEN 'AlbaranesCabAnexos'.
*        DATA: wl_albaranes_cab_anexo TYPE zmm_ui5_s_albaranes_cab_anexos.
*        zcl_seis_odata_utils=>tratar_key(
*          EXPORTING
*            tp_key    = it_key_tab
*          IMPORTING
*            wp_return = wl_albaranes_cab_anexo ).
*
*        wl_albaranes_cab_anexo-anexo        = is_media_resource-value.
*        wl_albaranes_cab_anexo-mimetype     = is_media_resource-mime_type.
*        wl_albaranes_cab_anexo-descripcion  = iv_slug.
*
*        albaranes_cab_anexos_create( CHANGING wp_anexo = wl_albaranes_cab_anexo ).
*
*    ENDCASE.
  ENDMETHOD.
  METHOD ejemplo_delete_entity.
    ""Se indica en clase ZCL_XXXXXXXX_DPC_IMP


*    DATA: wl_entity TYPE zmm_ui5_s_albaranes_cab.
*    io_tech_request_context->get_converted_keys( IMPORTING es_key_values  = wl_entity ).
*
*    albaranes_cab_delete( wl_entity ).

  ENDMETHOD.
  METHOD ejemplo_execute_action.
    ""Metodo /IWBEP/IF_MGW_APPL_SRV_RUNTIME~EXECUTE_ACTION
    ""Se indica en clase ZCL_XXXXXXXX_DPC_IMP

*    CASE iv_action_name.
*      WHEN 'obtenerDatosConexion'.
*        copy_data_to_ref( EXPORTING is_data = obtener_datos_conexion( )
*                          CHANGING  cr_data = er_data ).
*      WHEN 'parsearLecturaCB'.
*        DATA: BEGIN OF wl_params_parsear_lectura,
*                lectura TYPE string,
*              END OF wl_params_parsear_lectura.
*        zcl_seis_odata_utils=>tratar_key( EXPORTING tp_key = it_parameter IMPORTING wp_return = wl_params_parsear_lectura ).
*
*        copy_data_to_ref( EXPORTING is_data = parsear_lectura( wl_params_parsear_lectura-lectura )
*                          CHANGING  cr_data = er_data ).
*      WHEN 'imprimirMatricula'.
*        DATA: BEGIN OF wl_params_imprimir_etiqueta,
*                matricula TYPE exidv,
*                centro    TYPE werks_d,
*                almacen   TYPE lgort_d,
*                impresora TYPE zmm_ui5_s_puestos_imp-pto_impresion,
*                cantidad  TYPE p DECIMALS 3,
*                numcopias TYPE i,
*              END OF wl_params_imprimir_etiqueta.
*        zcl_seis_odata_utils=>tratar_key( EXPORTING tp_key = it_parameter IMPORTING wp_return = wl_params_imprimir_etiqueta ).
*
*        copy_data_to_ref(
*          EXPORTING is_data = imprimir_etiqueta(
*            vp_matricula  = wl_params_imprimir_etiqueta-matricula
*            vp_werks      = wl_params_imprimir_etiqueta-centro
*            vp_lgort      = wl_params_imprimir_etiqueta-almacen
*            vp_impresora  = wl_params_imprimir_etiqueta-impresora
*            vp_cantidad   = wl_params_imprimir_etiqueta-cantidad
*            vp_numcopias  = wl_params_imprimir_etiqueta-numcopias )
*          CHANGING  cr_data = er_data ).
*
*
*      WHEN OTHERS.
*        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
*
*    ENDCASE.


  ENDMETHOD.
  METHOD ejemplo_get_entity.
    ""Se indica en clase ZCL_XXXXXXXX_DPC_IMP

*    tratar_key_entity( EXPORTING io_tech_request_context = io_tech_request_context
*                       IMPORTING wp_entity               = er_entity ).
*    er_entity = albaranes_cab_getdetail(
*      vp_werks = er_entity-werks
*      vp_contador = er_entity-contador
*      vp_posicion_alb = er_entity-posicion_alb ).
  ENDMETHOD.
  METHOD ejemplo_get_entityset.
*    ""Se indica en clase ZCL_XXXXXXXX_DPC_IMP
*
*    DATA wl_entity LIKE LINE OF et_entityset.
*    tratar_key_entityset( EXPORTING io_tech_request_context = io_tech_request_context
*                          IMPORTING wp_entity               = wl_entity ).
*
*    DATA(tl_filter_select_options)  = io_tech_request_context->get_filter( )->get_filter_select_options( ).
*    READ TABLE tl_filter_select_options  ASSIGNING FIELD-SYMBOL(<fs_filter_so_fecha>) WITH KEY property = 'DATS'.
*
*
*
*    IF <fs_filter_so_fecha> IS ASSIGNED.
*      et_entityset =  fichajes_getlist( EXPORTING vp_pernr    = wl_entity-pernr
*                                                  tp_fecha_rg = <fs_filter_so_fecha>-select_options ).
*    ELSE.
*      et_entityset =  fichajes_getlist( wl_entity-pernr ).
*    ENDIF.
*
*    zcl_seis_odata_utils=>tratar_entityset( EXPORTING io_tech_request_context  = io_tech_request_context
*                                            CHANGING et_entityset = et_entityset ).
*
  ENDMETHOD.
  METHOD ejemplo_get_stream.
    "Metodo /IWBEP/IF_MGW_APPL_SRV_RUNTIME~GET_STREAM
    ""Se indica en clase ZCL_XXXXXXXX_DPC_IMP

*    DATA: vl_xstring TYPE xstring,
*          wl_stream  TYPE ty_s_media_resource.
*    DATA wl_header TYPE ihttpnvp.
*
*    CASE iv_entity_name.
*      WHEN 'AlbaranesCabAnexos'.
*        DATA: wl_albaranes_cab_anexo TYPE zmm_ui5_s_albaranes_cab_anexos.
*        io_tech_request_context->get_converted_keys( IMPORTING es_key_values = wl_albaranes_cab_anexo ).
*
*        wl_albaranes_cab_anexo = albaranes_cab_anexos_getdetail(
*            vp_werks        = wl_albaranes_cab_anexo-werks
*            vp_contador     = wl_albaranes_cab_anexo-contador
*            vp_posicion_alb = wl_albaranes_cab_anexo-posicion_alb
*            vp_num_anexo    = wl_albaranes_cab_anexo-num_anexo ).
*
*        wl_header-value = 'outline; filename="&1";'.
*        REPLACE FIRST OCCURRENCE OF '&1' IN wl_header-value WITH wl_albaranes_cab_anexo-descripcion.
*
**        wl_stream-mime_type = wl_albaranes_cab_anexo-mimetype.
*        wl_stream-value     = wl_albaranes_cab_anexo-anexo.
*    ENDCASE.
*
*    wl_header-name = 'Content-Disposition'.
*    rg_dpc_base->set_header( is_header = wl_header ).
*    copy_data_to_ref( EXPORTING is_data = wl_stream
*                      CHANGING  cr_data = er_stream ).
*
  ENDMETHOD.
  METHOD ejemplo_update_entity.
*    io_data_provider->read_entry_data( IMPORTING es_data = er_entity ).
*    albaranes_cab_update( CHANGING wp_albaranes_cab = er_entity ).
  ENDMETHOD.
  METHOD grabar_anexo_content_server.

    DATA: ls_fol_id   TYPE soodk,
          ls_obj_data TYPE sood1,
          ls_folmem_k TYPE sofmk,
          ls_note     TYPE borident,
          ls_object   TYPE borident,
          lv_ep_note  TYPE borident-objkey,
          lv_offset   TYPE i.

    DATA: it_objhead TYPE STANDARD TABLE OF soli,
          it_content TYPE STANDARD TABLE OF soli,
          wa_content TYPE soli,
          vl_length  TYPE i.

    ls_object-objkey  = key.
    ls_object-objtype = type.


    CALL FUNCTION 'SCMS_XSTRING_TO_BINARY'
      EXPORTING
        buffer        = anexo
      IMPORTING
        output_length = vl_length
      TABLES
        binary_tab    = it_content.
    ls_obj_data-objlen = vl_length.


    CALL FUNCTION 'SO_CONVERT_CONTENTS_BIN'
      EXPORTING
        it_contents_bin = it_content[]
      IMPORTING
        et_contents_bin = it_content[].

    CALL FUNCTION 'SO_FOLDER_ROOT_ID_GET'
      EXPORTING
        region    = 'B'
      IMPORTING
        folder_id = ls_fol_id
      EXCEPTIONS
        OTHERS    = 1.

    ls_obj_data-objsns    = 'O'.
    ls_obj_data-objla     = sy-langu.
    ls_obj_data-objdes    = description.
    ls_obj_data-file_ext  = extension.
*    ls_obj_data-objlen    = lines( it_content ) * 255.

    CALL FUNCTION 'SO_OBJECT_INSERT'
      EXPORTING
        folder_id             = ls_fol_id
        object_type           = 'EXT'
        object_hd_change      = ls_obj_data
      IMPORTING
        object_id             = wp_obj_id
      TABLES
        objhead               = it_objhead
        objcont               = it_content
      EXCEPTIONS
        active_user_not_exist = 35
        folder_not_exist      = 6
        object_type_not_exist = 17
        owner_not_exist       = 22
        parameter_error       = 23
        OTHERS                = 1000.

    IF sy-subrc = 0.
      ls_folmem_k-foltp = ls_fol_id-objtp.
      ls_folmem_k-folyr = ls_fol_id-objyr.
      ls_folmem_k-folno = ls_fol_id-objno.
      ls_folmem_k-doctp = wp_obj_id-objtp.
      ls_folmem_k-docyr = wp_obj_id-objyr.
      ls_folmem_k-docno = wp_obj_id-objno.
      lv_ep_note = ls_folmem_k.
      ls_note-objtype = 'MESSAGE'.
      ls_note-objkey = lv_ep_note.
      CALL FUNCTION 'BINARY_RELATION_CREATE_COMMIT'
        EXPORTING
          obj_rolea    = ls_object
          obj_roleb    = ls_note
          relationtype = 'ATTA'
        EXCEPTIONS
          OTHERS       = 1.
    ENDIF.

    IF sy-subrc <> 0.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_busi_exception
        EXPORTING
          textid  = /iwbep/cx_mgw_busi_exception=>business_error
          message = 'Error al grabar anexo'.
    ENDIF.


  ENDMETHOD.
  METHOD lanzar_excepcion.


    DATA vl_bapi_msg TYPE bapi_msg.
    DATA wl_textid TYPE  scx_t100key.

    IF message IS SUPPLIED .
      IF message IS NOT INITIAL.
        vl_bapi_msg = message.

        RAISE EXCEPTION TYPE /iwbep/cx_mgw_busi_exception
          EXPORTING
            textid  = /iwbep/cx_mgw_busi_exception=>business_error
            message = vl_bapi_msg.
      ENDIF.
    ELSEIF bapiret2 IS SUPPLIED.
      IF bapiret2 IS NOT INITIAL.
        wl_textid-msgid = bapiret2-id.
        wl_textid-msgno = bapiret2-number.
        wl_textid-attr1 = bapiret2-message_v1.
        wl_textid-attr2 = bapiret2-message_v2.
        wl_textid-attr3 = bapiret2-message_v3.
        wl_textid-attr4 = bapiret2-message_v4.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_busi_exception
          EXPORTING
            textid = wl_textid.
      ENDIF.
    ELSEIF bapiret2_t IS SUPPLIED.
      LOOP AT bapiret2_t INTO bapiret2 WHERE type CA 'EA'.
        wl_textid-msgid = bapiret2-id.
        wl_textid-msgno = bapiret2-number.
        wl_textid-attr1 = bapiret2-message_v1.
        wl_textid-attr2 = bapiret2-message_v2.
        wl_textid-attr3 = bapiret2-message_v3.
        wl_textid-attr4 = bapiret2-message_v4.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_busi_exception
          EXPORTING
            textid = wl_textid.
      ENDLOOP.
    ELSEIF id IS SUPPLIED.
      wl_textid-msgid = id.
      wl_textid-msgno = number.
      wl_textid-attr1 = message_v1.
      wl_textid-attr2 = message_v2.
      wl_textid-attr3 = message_v3.
      wl_textid-attr4 = message_v4.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_busi_exception
        EXPORTING
          textid = wl_textid.

    ELSEIF sy-msgid IS NOT INITIAL.
      wl_textid-msgid = sy-msgid.
      wl_textid-msgno = sy-msgno.
      wl_textid-attr1 = sy-msgv1.
      wl_textid-attr2 = sy-msgv2.
      wl_textid-attr3 = sy-msgv3.
      wl_textid-attr4 = sy-msgv4.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_busi_exception
        EXPORTING
          textid = wl_textid.
    ENDIF.
  ENDMETHOD.
  METHOD read_entity.
    ""Metodo /iwbep/if_mgw_core_srv_runtime~read_entity
    ""Se indica en clase ZCL_XXXXXXXX_DPC_EXT



*    CALL METHOD super->/iwbep/if_mgw_core_srv_runtime~read_entity
*      EXPORTING
*        iv_entity_name      = iv_entity_name
*        iv_source_name      = iv_source_name
*        is_request_details  = is_request_details
*      CHANGING
*        ct_headers          = ct_headers
*        cr_entity           = cr_entity
*        cs_response_context = cs_response_context
*        ct_expand_skiptoken = ct_expand_skiptoken
*        ct_inline_info      = ct_inline_info.
*
*
*    zcl_seis_odata_utils=>anyadir_headers_control_cache( CHANGING tp_headers = ct_headers ).


  ENDMETHOD.
  METHOD read_entityset.
    ""Metodo /iwbep/if_mgw_core_srv_runtime~read_entityset
    ""Se indica en clase ZCL_XXXXXXXX_DPC_EXT

*    CALL METHOD super->/iwbep/if_mgw_core_srv_runtime~read_entityset
*      EXPORTING
*        iv_entity_name           = iv_entity_name
*        iv_source_name           = iv_source_name
*        is_paging                = is_paging
*        it_order                 = it_order
*        it_filter_select_options = it_filter_select_options
*        is_request_details       = is_request_details
*      CHANGING
*        ct_headers               = ct_headers
*        cr_entityset             = cr_entityset
*        cr_deleted_entityset     = cr_deleted_entityset
*        cs_response_context      = cs_response_context
*        ct_inline_info           = ct_inline_info.
*
*    zcl_seis_odata_utils=>anyadir_headers_control_cache( CHANGING tp_headers = ct_headers ).


  ENDMETHOD.
  METHOD tratar_entityset.
    DATA: tl_filter_select_options TYPE /iwbep/t_mgw_select_option,
          tl_order                 TYPE /iwbep/t_mgw_sorting_order,
          wl_paging	               TYPE /iwbep/s_mgw_paging.

    IF it_filter_select_options IS SUPPLIED.
      tl_filter_select_options = it_filter_select_options.
    ELSEIF io_tech_request_context IS SUPPLIED.
      DATA(rl_filter) = io_tech_request_context->get_filter( ).
      tl_filter_select_options = rl_filter->get_filter_select_options( ).

      DATA: rl_entity TYPE REF TO data.
      CREATE DATA rl_entity LIKE LINE OF et_entityset.
      ASSIGN rl_entity->* TO FIELD-SYMBOL(<fs_entity>).
      convertir_filtros_formato_int(
        CHANGING  wp_entity                = <fs_entity>
                  it_filter_select_options = tl_filter_select_options ).
    ENDIF.

    IF it_order IS SUPPLIED.
      tl_order = it_order.
    ELSEIF io_tech_request_context IS SUPPLIED.
      DATA(tl_orderby) = io_tech_request_context->get_orderby( ).
      LOOP AT tl_orderby ASSIGNING FIELD-SYMBOL(<fs_orderby>).
        APPEND INITIAL LINE TO tl_order ASSIGNING FIELD-SYMBOL(<fs_order>).
        MOVE-CORRESPONDING <fs_orderby> TO <fs_order>.
      ENDLOOP.
    ENDIF.

    IF is_paging IS SUPPLIED.
      wl_paging = is_paging.
    ELSE.
      wl_paging-top   = io_tech_request_context->get_top( ).
      wl_paging-skip  = io_tech_request_context->get_skip( ).
    ENDIF.

    tratar_filtros(
      EXPORTING
        it_filter_select_options = tl_filter_select_options
      CHANGING
        et_entityset             = et_entityset ).

    tratar_ordenacion(
      EXPORTING
        it_order          = tl_order
        it_campos_astext  = it_campos_astext
      CHANGING
        et_entityset = et_entityset ).

    tratar_paging(
      EXPORTING
        is_paging    = wl_paging
      CHANGING
        et_entityset = et_entityset ).

  ENDMETHOD.
  METHOD tratar_filtros.


    DATA vl_insertar  TYPE c.
    DATA vl_null      TYPE c.
    FIELD-SYMBOLS: <fs_entityset>             TYPE any,
                   <fs>                       TYPE any,
                   <fs_filter_select_options> LIKE LINE OF it_filter_select_options,
                   <fs_select_option>         LIKE LINE OF <fs_filter_select_options>-select_options.

    LOOP AT et_entityset ASSIGNING <fs_entityset>.
      vl_insertar = 'X'.
      LOOP AT it_filter_select_options ASSIGNING <fs_filter_select_options>.
        ASSIGN COMPONENT <fs_filter_select_options>-property OF STRUCTURE <fs_entityset> TO <fs>.
        CHECK sy-subrc = 0.

        CLEAR vl_null.
        LOOP AT <fs_filter_select_options>-select_options ASSIGNING <fs_select_option>.
          IF <fs_select_option>-low = 'null'.
            vl_null = 'X'.
            IF <fs_select_option>-sign ='I' AND <fs_select_option>-option = 'EQ' AND <fs> IS NOT INITIAL.
              CLEAR vl_insertar.
              EXIT.
            ELSEIF <fs_select_option>-sign ='I' AND <fs_select_option>-option = 'NE' AND <fs> IS INITIAL.
              CLEAR vl_insertar.
              EXIT.
            ENDIF.
          ENDIF.
        ENDLOOP.

        IF vl_insertar IS INITIAL.
          EXIT.
        ENDIF.

        IF vl_null IS INITIAL.
          DATA: tl_select_option_aux TYPE RANGE OF  char255.
          FIELD-SYMBOLS: <fs_select_option_aux> LIKE LINE OF tl_select_option_aux.

          CLEAR tl_select_option_aux.
          LOOP AT <fs_filter_select_options>-select_options ASSIGNING <fs_select_option>.
            APPEND INITIAL LINE TO tl_select_option_aux ASSIGNING <fs_select_option_aux>.
            MOVE-CORRESPONDING <fs_select_option> TO <fs_select_option_aux>.
          ENDLOOP.

          IF <fs> NOT IN tl_select_option_aux.
            CLEAR vl_insertar.
            EXIT.
          ENDIF.
        ENDIF.
      ENDLOOP.

      IF vl_insertar IS INITIAL.
        DELETE et_entityset.
      ENDIF.
    ENDLOOP.

  ENDMETHOD.
  METHOD tratar_key.
    FIELD-SYMBOLS: <fs_key_tab> LIKE LINE OF tp_key,
                   <fs>         TYPE any.

    LOOP AT tp_key ASSIGNING <fs_key_tab>.
      ASSIGN COMPONENT <fs_key_tab>-name OF STRUCTURE wp_return TO <fs>.
      CHECK sy-subrc = 0.
      <fs> = <fs_key_tab>-value.

      IF vp_convertir_formato_interno = 'X'.
        DATA(rl_type_descr) = CAST cl_abap_elemdescr( cl_abap_typedescr=>describe_by_data( <fs> ) ).
        DATA(vl_edit_mask) = rl_type_descr->edit_mask.
        REPLACE '==' INTO vl_edit_mask WITH ''.
        CONDENSE vl_edit_mask NO-GAPS.

        IF vl_edit_mask IS NOT INITIAL.
          DATA(vl_funcion_conversion_exit) = 'CONVERSION_EXIT_' && vl_edit_mask && '_INPUT'.
          CALL FUNCTION vl_funcion_conversion_exit
            EXPORTING
              input  = <fs>
            IMPORTING
              output = <fs>.
        ENDIF.
      ENDIF.
    ENDLOOP.

  ENDMETHOD.
  METHOD tratar_key_converted.
    io_tech_request_context->get_converted_source_keys( IMPORTING es_key_values	= es_entity ).
  ENDMETHOD.
  METHOD tratar_key_entity.
    ""Se implementa en clase ZCL_XXXXXXXX_DPC_IMP


*    DATA(vl_source_entity_type_name)  = io_tech_request_context->get_source_entity_type_name( ).
*    DATA(vl_entity_type_name)         = io_tech_request_context->get_entity_type_name( ).
*
*
*    CASE vl_source_entity_type_name.
*      WHEN 'AlbaranesCab'.
*        DATA wl_albaranes_cab TYPE zmm_ui5_s_albaranes_cab.
*        io_tech_request_context->get_converted_source_keys( IMPORTING es_key_values  = wl_albaranes_cab ).
*        wl_albaranes_cab = albaranes_cab_getdetail(
*                              vp_werks         = wl_albaranes_cab-werks
*                              vp_contador      = wl_albaranes_cab-contador
*                              vp_posicion_alb  = wl_albaranes_cab-posicion_alb ).
*        MOVE-CORRESPONDING wl_albaranes_cab TO wp_entity.
*
*      WHEN 'AlbaranesLin'.
*        DATA wl_albaranes_lin TYPE zmm_ui5_s_albaranes_lin.
*        io_tech_request_context->get_converted_source_keys( IMPORTING es_key_values  = wl_albaranes_lin ).
*        wl_albaranes_lin = albaranes_lin_getdetail(
*                              vp_werks            = wl_albaranes_lin-werks
*                              vp_contador         = wl_albaranes_lin-contador
*                              vp_posicion_alb     = wl_albaranes_lin-posicion_alb
*                              vp_posicion_alb_lin = wl_albaranes_lin-posicion_alb_lin ).
*        MOVE-CORRESPONDING wl_albaranes_lin TO wp_entity.
*
*      WHEN OTHERS.
*        io_tech_request_context->get_converted_keys( IMPORTING es_key_values  = wp_entity ).
*
*    ENDCASE.

  ENDMETHOD.
  METHOD tratar_key_entityset.
    ""Se implementa en clase ZCL_XXXXXXXX_DPC_IMP


*    DATA(vl_source_entity_type_name)  = io_tech_request_context->get_source_entity_type_name( ).
*    DATA(vl_entity_type_name)         = io_tech_request_context->get_entity_type_name( ).
*
*    CASE vl_source_entity_type_name.
*      WHEN 'TiposFichaje'.
*        DATA wl_tipos_fichaje TYPE zhr_s_ss_tipos_fichaje.
*        io_tech_request_context->get_converted_source_keys( IMPORTING es_key_values  = wl_tipos_fichaje ).
*        wl_tipos_fichaje = tipos_fichaje_getdetail( wl_tipos_fichaje-id ).
*        MOVE-CORRESPONDING wl_tipos_fichaje TO wp_entity.
*
*      WHEN OTHERS.
*        io_tech_request_context->get_converted_source_keys( IMPORTING es_key_values  = wp_entity ).
*    ENDCASE.
*


  ENDMETHOD.
  METHOD tratar_ordenacion.

    DATA: tl_sort   TYPE abap_sortorder_tab.
    FIELD-SYMBOLS: <fs_order> LIKE LINE OF it_order,
                   <fs_sort>  LIKE LINE OF tl_sort.


    LOOP AT it_order ASSIGNING <fs_order>.
      APPEND INITIAL LINE TO tl_sort ASSIGNING <fs_sort>.
      <fs_sort>-name    = <fs_order>-property.

      IF <fs_order>-order = 'desc'.
        <fs_sort>-descending = 'X'.
      ENDIF.

      READ TABLE it_campos_astext TRANSPORTING NO FIELDS
        WITH KEY table_line = <fs_sort>-name.
      IF sy-subrc = 0.
        <fs_sort>-astext  = 'X'.
      ENDIF.
    ENDLOOP.


    IF tl_sort IS NOT INITIAL.
      SORT et_entityset BY (tl_sort).
    ENDIF.

  ENDMETHOD.
  METHOD tratar_paging.

    IF is_paging-skip > 0.
      DELETE et_entityset FROM 1 TO is_paging-skip.
    ENDIF.
    IF is_paging-top > 0.
      DATA vl_top TYPE i.
      vl_top  = is_paging-top + 1.
      IF vl_top <= lines( et_entityset ).
        DELETE et_entityset FROM vl_top TO lines( et_entityset ).
      ENDIF.
    ENDIF.


  ENDMETHOD.
