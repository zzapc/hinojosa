FUNCTION zswm_get_tipos_almacen_aprov.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     VALUE(VP_LGNUM) TYPE  LGNUM OPTIONAL
*"     VALUE(VP_LGTYP) TYPE  LGTYP OPTIONAL
*"     VALUE(VP_MTART) TYPE  MTART OPTIONAL
*"     VALUE(VP_CLA_DESAPROVISIONAR) TYPE  XFELD DEFAULT SPACE
*"  EXPORTING
*"     VALUE(TP_TIPOS_ALMACEN_APROV) TYPE  ZSWM_T_TIPOS_ALMACEN_APROV
*"----------------------------------------------------------------------


  DATA: data_u TYPE zswm_data_ui5_eeeoo_str.
  CALL FUNCTION 'ZSWM_GET_DATA_USER_UI5'
    EXPORTING
      statu      = 'X'
      no_gen_log = abap_true
    IMPORTING
      data_user  = data_u.

  IF vp_lgnum IS INITIAL.
    vp_lgnum = data_u-lgnum.
  ENDIF.

  DATA tl_mtart_rg TYPE RANGE OF mara-mtart.
  IF vp_mtart IS NOT INITIAL.
    tl_mtart_rg = VALUE #( ( sign = 'I' option = 'EQ' low = vp_mtart ) ).
  ENDIF.

  SELECT valor     AS lgnum,
         valor2    AS mtart,
         atributo1 AS lgort,
         atributo2 AS lgtyp_buscar,
         atributo3 AS lgtyps_desapro,
         atributo4 AS lgtyp_repro
     INTO TABLE @DATA(tl_params_almacen_apro)
     FROM zparametros
     WHERE clave =  'UI5_ALM_PT'   AND
           campo =  'ALMACEN_APRO' AND
           valor =   @vp_lgnum    AND
           valor2 IN @tl_mtart_rg.

  DATA: wl_tipos_almacen_aprov LIKE LINE OF tp_tipos_almacen_aprov.
  FIELD-SYMBOLS: <fs_tipos_almacen_aprov> LIKE LINE OF tp_tipos_almacen_aprov.
  LOOP AT tl_params_almacen_apro ASSIGNING FIELD-SYMBOL(<fs_params_almacen_apro>).
    wl_tipos_almacen_aprov-lgnum = <fs_params_almacen_apro>-lgnum.
    wl_tipos_almacen_aprov-mtart = <fs_params_almacen_apro>-mtart.

    "Buscar
    wl_tipos_almacen_aprov-lgtyp = <fs_params_almacen_apro>-lgtyp_buscar.
    IF wl_tipos_almacen_aprov-lgtyp IS NOT INITIAL.
      READ TABLE tp_tipos_almacen_aprov ASSIGNING <fs_tipos_almacen_aprov> WITH KEY lgnum = wl_tipos_almacen_aprov-lgnum
                                                                                    lgtyp = wl_tipos_almacen_aprov-lgtyp
                                                                                    mtart = wl_tipos_almacen_aprov-mtart.
      IF sy-subrc <> 0.
        APPEND INITIAL LINE TO tp_tipos_almacen_aprov ASSIGNING <fs_tipos_almacen_aprov>.
        <fs_tipos_almacen_aprov>-lgnum = wl_tipos_almacen_aprov-lgnum.
        <fs_tipos_almacen_aprov>-lgtyp = wl_tipos_almacen_aprov-lgtyp.
        <fs_tipos_almacen_aprov>-mtart = wl_tipos_almacen_aprov-mtart.
      ENDIF.
      <fs_tipos_almacen_aprov>-cla_buscar = 'X'.
    ENDIF.



    "Reprocesar
    wl_tipos_almacen_aprov-lgtyp = <fs_params_almacen_apro>-lgtyp_repro.
    IF wl_tipos_almacen_aprov-lgtyp IS NOT INITIAL.
      READ TABLE tp_tipos_almacen_aprov ASSIGNING <fs_tipos_almacen_aprov> WITH KEY lgnum = wl_tipos_almacen_aprov-lgnum
                                                                                    lgtyp = wl_tipos_almacen_aprov-lgtyp
                                                                                    mtart = wl_tipos_almacen_aprov-mtart.
      IF sy-subrc <> 0.
        APPEND INITIAL LINE TO tp_tipos_almacen_aprov ASSIGNING <fs_tipos_almacen_aprov>.
        <fs_tipos_almacen_aprov>-lgnum = wl_tipos_almacen_aprov-lgnum.
        <fs_tipos_almacen_aprov>-lgtyp = wl_tipos_almacen_aprov-lgtyp.
        <fs_tipos_almacen_aprov>-mtart = wl_tipos_almacen_aprov-mtart.
      ENDIF.
      <fs_tipos_almacen_aprov>-cla_reprocesar = 'X'.
    ENDIF.


    "Desaprovisionar
    DATA: tl_lgtyp_desapro TYPE TABLE OF char10.
    SPLIT <fs_params_almacen_apro>-lgtyps_desapro AT ',' INTO TABLE tl_lgtyp_desapro.
    LOOP AT tl_lgtyp_desapro ASSIGNING FIELD-SYMBOL(<fs_lgtyp_desapro>).
      CONDENSE <fs_lgtyp_desapro> NO-GAPS.
      wl_tipos_almacen_aprov-lgtyp = <fs_lgtyp_desapro>.

      READ TABLE tp_tipos_almacen_aprov ASSIGNING <fs_tipos_almacen_aprov> WITH KEY lgnum = wl_tipos_almacen_aprov-lgnum
                                                                                    lgtyp = wl_tipos_almacen_aprov-lgtyp
                                                                                    mtart = wl_tipos_almacen_aprov-mtart.
      IF sy-subrc <> 0.
        APPEND INITIAL LINE TO tp_tipos_almacen_aprov ASSIGNING <fs_tipos_almacen_aprov>.
        <fs_tipos_almacen_aprov>-lgnum = wl_tipos_almacen_aprov-lgnum.
        <fs_tipos_almacen_aprov>-lgtyp = wl_tipos_almacen_aprov-lgtyp.
        <fs_tipos_almacen_aprov>-mtart = wl_tipos_almacen_aprov-mtart.
      ENDIF.
      <fs_tipos_almacen_aprov>-cla_desaprovisionar = 'X'.
    ENDLOOP.
  ENDLOOP.


  DATA tl_tipos_almacen TYPE  zswm_t_tipos_almacen.
  CALL FUNCTION 'ZSWM_GET_TIPOS_ALMACEN'
    EXPORTING
      vp_lgnum         = vp_lgnum
      vp_lgtyp         = vp_lgtyp
    IMPORTING
      tp_tipos_almacen = tl_tipos_almacen.

  LOOP AT tp_tipos_almacen_aprov ASSIGNING <fs_tipos_almacen_aprov>.
    READ TABLE tl_tipos_almacen ASSIGNING FIELD-SYMBOL(<fs_tipos_almacen>) WITH KEY lgnum = <fs_tipos_almacen_aprov>-lgnum
                                                                                    lgtyp = <fs_tipos_almacen_aprov>-lgtyp.
    IF sy-subrc <> 0.
      DELETE tp_tipos_almacen_aprov. CONTINUE.
    ENDIF.
    MOVE-CORRESPONDING <fs_tipos_almacen> TO <fs_tipos_almacen_aprov>.


    READ TABLE tl_params_almacen_apro ASSIGNING <fs_params_almacen_apro> WITH KEY lgnum = <fs_tipos_almacen_aprov>-lgnum
                                                                                  mtart = <fs_tipos_almacen_aprov>-mtart.
    IF sy-subrc = 0.
      <fs_tipos_almacen_aprov>-lgort = <fs_params_almacen_apro>-lgort.
    ENDIF.
  ENDLOOP.


  IF vp_cla_desaprovisionar = 'X'.
    DELETE tp_tipos_almacen_aprov WHERE cla_desaprovisionar = space.
  ENDIF.

ENDFUNCTION.
