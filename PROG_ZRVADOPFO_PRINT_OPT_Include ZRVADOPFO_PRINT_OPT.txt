*&---------------------------------------------------------------------*
*&  Include           ZRVADOPFO_PRINT_OPT
*&---------------------------------------------------------------------*

  DATA:
    ls_comm_values             TYPE szadr_comm_values,
    lv_comm_type               TYPE ad_comm,
    lv_otf_memory_switch       TYPE c.


* reset return code
  retcode = 0.

  CASE nast-nacha.
    WHEN gc_nacha-external_send.
*     Send
      IF NOT nast-tcode IS INITIAL.
        CALL FUNCTION 'ADDR_GET_NEXT_COMM_TYPE'
          EXPORTING
            strategy           = nast-tcode
            address_number     = addr_key-addrnumber
            person_number      = addr_key-persnumber
          IMPORTING
            comm_type          = lv_comm_type
            comm_values        = ls_comm_values
          EXCEPTIONS
            address_not_exist  = 1
            person_not_exist   = 2
            no_comm_type_found = 3
            internal_error     = 4
            parameter_error    = 5
            OTHERS             = 6.
        IF sy-subrc <> 0.
          retcode = sy-subrc.
          PERFORM protocol_update.
          RETURN.
        ENDIF.
*       Proccess communication type
        CASE lv_comm_type.
          WHEN 'INT'.  "e-mail
            fp_outputparams-getpdf = gc_true.
            gv_device              = gc_device-email.
            gv_email     = ls_comm_values-adsmtp-smtp_addr.
          WHEN 'FAX'.
            fp_outputparams-getpdf = gc_true.
            gv_device              = gc_device-fax.
            nast-telfx          = ls_comm_values-adfax-fax_number.
            nast-tland          = ls_comm_values-adfax-country.
          WHEN 'LET'.   "Printer
            gv_device              = gc_device-printer.
        ENDCASE.
      ELSE.
        gv_device                  = gc_device-printer.
      ENDIF.
    WHEN gc_nacha-printer.
*     Print
      gv_device                    = gc_device-printer.
    WHEN gc_nacha-fax.
*     Fax
      fp_outputparams-getpdf       = gc_true.
      gv_device                    = gc_device-fax.
  ENDCASE.

* The original document should be printed only once
**  IF NOT us_screen               IS INITIAL  AND
**         gs_interface-head_detail-repeat EQ gc_false.
  IF NOT us_screen               IS INITIAL.
    fp_outputparams-noprint   = gc_false. "gc_true.
    fp_outputparams-nopributt = gc_true.
    fp_outputparams-noarchive = gc_true.
  ENDIF.

* Set up screen display
  IF us_screen     = 'X'.
    fp_outputparams-preview = gc_true.
    fp_outputparams-getpdf  = gc_false.
  ELSEIF us_screen = 'W'. "Web dynpro
    fp_outputparams-getpdf  = gc_true.
    gv_device               = gc_device-web_dynpro.
  ENDIF.

* Set basic data
* DTM - Ventana diálogo SAP para impresiones ZCARGAS
  IF nast-kschl EQ 'ZPAK' OR nast-kschl EQ 'ZCTF'.
    fp_outputparams-nodialog  = abap_false.
  ELSE.
    fp_outputparams-nodialog  = gc_true.
  ENDIF.
* DTM
  fp_outputparams-dest      = nast-ldest.
  fp_outputparams-copies    = nast-anzal.
  fp_outputparams-dataset   = nast-dsnam.
  fp_outputparams-suffix1   = nast-dsuf1.
  fp_outputparams-suffix2   = nast-dsuf2.
  fp_outputparams-cover     = nast-tdocover.
  fp_outputparams-covtitle  = nast-tdcovtitle.
  fp_outputparams-authority = nast-tdautority.
  fp_outputparams-receiver  = nast-tdreceiver.
  fp_outputparams-division  = nast-tddivision.
  fp_outputparams-arcmode   = nast-tdarmod.
  fp_outputparams-reqimm    = nast-dimme.
  fp_outputparams-reqdel    = nast-delet.
  fp_outputparams-senddate  = nast-vsdat.
  fp_outputparams-sendtime  = nast-vsura.

* Print Preview for CRM Web UI, printing button always active
  IF cl_ops_switch_check=>sd_sfws_sc3( ) EQ gc_true.
    CALL FUNCTION 'GET_OTF_MEMORY_SWITCH'
      IMPORTING
        ev_otf_memory_switch = lv_otf_memory_switch.
    IF lv_otf_memory_switch EQ abap_true.
      fp_outputparams-noprint   = gc_false.
    ENDIF.
  ENDIF.

* Set language and default language
  fp_docparams-langu     = nast-spras.
*  cs_docparams-replangu1 = <gs_vbdka>-spras_vko.
*  cs_docparams-replangu2 = gc_english.
  fp_docparams-country   = us_country.
