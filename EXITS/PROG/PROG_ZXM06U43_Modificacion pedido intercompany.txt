*&---------------------------------------------------------------------*
*&  Include           ZXM06U43
*&---------------------------------------------------------------------*

*--> INI FCARDONA 18.10.2021 67171 - Informe INTRASTAT SAP.

***Este include se ejecuta para chequear datos CUSTOM. Lo activamos para que nos permita chequear datos INTRASTAT, ya que la BADI no permite importar datos de memoria.

CONSTANTS: lc_intrastat_data(14)  TYPE c VALUE 'INTRASTAT_DATA',
           lc_intrastat_iva(18)   TYPE c VALUE 'ZMM_INTRASTAT_TIVA'.

TYPES: BEGIN OF ty_intrastat,
  expos TYPE eipo-expos,
  segal TYPE eipo-segal,
  exprf TYPE eipo-exprf,
  exart TYPE eipo-exart,
END OF ty_intrastat.

DATA: ls_intrastat TYPE ty_intrastat,
      lt_intrastat TYPE TABLE OF ty_intrastat.

DATA: lr_mwskz TYPE RANGE OF mwskz,
      ls_mwskz LIKE LINE OF lr_mwskz.

"Instanciamos la clase intrastat manager con el proveedor del pedido
DATA(lo_intrastat_manager) = NEW zcl_intrastat_manager( i_lifnr = i_ekko-lifnr ).

"Chequeamos si se ha instanciado
IF ( lo_intrastat_manager IS BOUND ).

  "Obtenemos país e indicador CEE
  lo_intrastat_manager->get_country(
*    EXPORTING
*      i_lifnr =     " Número de cuenta del proveedor o acreedor
    IMPORTING
      e_land1 = DATA(lv_land)    " Clave de país
      e_xegld = DATA(lv_xegld)   " Indicador: ¿ Miembro de la CEE ?
  ).

  "Si es CEE y no es ES
  IF ( lv_xegld = abap_true AND lv_land <> 'ES' ).

    "Traemos datos de pestaña INTRASTAT
    IMPORT lt_intrastat FROM MEMORY ID lc_intrastat_data.

    "Recorremos las posiciones del pedido para chequear el tipo de IVA aplicado
    LOOP AT tekpo.

      "Comprobamos si pedido es de servicio
      lo_intrastat_manager->check_if_iva_is_service(
        EXPORTING
          i_mwskz   = tekpo-mwskz       " Indicador IVA
        IMPORTING
          e_service = DATA(lv_service)  " 'X' True, ' ' False
      ).

      "Si es de servicio...
      IF ( lv_service = abap_true ).

        "Leemos la tabla importada de memoria para la posición concreta
        READ TABLE lt_intrastat INTO ls_intrastat WITH KEY expos = tekpo-ebelp.
        IF ( sy-subrc = 0 ).
          "Comprobamos si han rellenado indicador exclusión.
          IF ( ls_intrastat-segal IS INITIAL ).
            MESSAGE e188(zmm) WITH ls_intrastat-expos.
          ENDIF.
        ENDIF.

      ELSE. "Si no es de servicio

        "Leemos la tabla importada de memoria para la posición concreta
        READ TABLE lt_intrastat INTO ls_intrastat WITH KEY expos = tekpo-ebelp.
        IF ( sy-subrc = 0 ).
          "Comprobamos si han rellenado indicador exclusión.
          IF ( ls_intrastat-segal IS NOT INITIAL ).
            MESSAGE e188(zmm) WITH ls_intrastat-expos.
          ENDIF.

        ENDIF.

      ENDIF.

    ENDLOOP.

  ENDIF.

ENDIF.

*<-- FIN FCARDONA 18.10.2021 67171 - Informe INTRASTAT SAP.
