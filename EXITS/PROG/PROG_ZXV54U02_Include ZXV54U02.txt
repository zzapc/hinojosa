*&---------------------------------------------------------------------*
*&  Include           ZXV54U02
*&---------------------------------------------------------------------*

*--------------------------------------------------------------------*
*"*"Lokale Schnittstelle:
*"       IMPORTING
*"             VALUE(I_VFSI) TYPE  V54A0_VFSI
*"             VALUE(I_AKTYP) LIKE  T180-AKTYP
*"             VALUE(I_SCD_ITEM) TYPE  V54A0_SCD_ITEM
*"             VALUE(I_REFOBJ) TYPE  V54A0_REFOBJ
*"       CHANGING
*"             REFERENCE(C_KOMK) TYPE  V54A0_KOMK
*--------------------------------------------------------------------*

" ASC 04.03.24 Cambiamos la ruta para que se determine correctamente la ZF00

CHECK i_scd_item-vfkp-kalsm = 'ZSDFC4' AND
      i_scd_item-vfkp-tplst = '2000'.

TYPES: BEGIN OF ty_zf15,
         vbeln TYPE vbeln,
         tdlnr TYPE tdlnr,
         vstel TYPE vstel,
         vsbed TYPE vsbed,
         regio TYPE regio,
         pstlz TYPE pstlz,
         route TYPE route,
         kunnr TYPE kunnr.
TYPES: END OF ty_zf15.

TYPES: BEGIN OF ty_tarifa_entrega,
         vbeln TYPE vbeln_vl,
         kbetr TYPE kbetr_kond.
TYPES: END OF ty_tarifa_entrega.

DATA: lv_tknum          TYPE tknum,
      lt_tarifa_entrega TYPE TABLE OF ty_tarifa_entrega,
      ls_tarifa_entrega TYPE ty_tarifa_entrega,
      tt_zf15           TYPE TABLE OF ty_zf15,
      l_kbetr_tarifa    TYPE kbetr,
      l_vttk            TYPE vttk.

IF i_scd_item-vfkp-rebel IS NOT INITIAL.

  lv_tknum = i_scd_item-vfkp-rebel.

  SELECT d~vbeln a~tdlnr d~vstel d~vsbed e~region AS regio e~post_code1 AS pstlz d~route d~kunnr
     INTO TABLE tt_zf15 FROM vttk AS a
     INNER JOIN vtts AS b ON a~tknum = b~tknum
     INNER JOIN vttp AS c ON b~tknum = c~tknum AND b~tsrfo = c~tprfo
     INNER JOIN likp AS d ON c~vbeln = d~vbeln
     INNER JOIN adrc AS e ON b~adrnz = e~addrnumber
     WHERE a~tknum = lv_tknum.

  SELECT SINGLE * INTO l_vttk FROM vttk WHERE tknum = lv_tknum.

  CLEAR: ls_tarifa_entrega, lt_tarifa_entrega[].

  LOOP AT tt_zf15 INTO DATA(l_zf15_aux).

    CLEAR: l_kbetr_tarifa.

*   Precio por tarifa
    SELECT SINGLE b~kbetr
      INTO l_kbetr_tarifa
      FROM a959 AS a
      INNER JOIN konp AS b ON a~knumh = b~knumh
                          AND a~kschl = b~kschl
     WHERE a~kschl = 'ZF00'
       AND a~tdlnr = l_zf15_aux-tdlnr
       AND a~vstel = l_zf15_aux-vstel
       AND a~vsbed = l_zf15_aux-vsbed
       AND a~route = l_zf15_aux-route
       AND b~loevm_ko = space
       AND a~datab <= sy-datum
       AND a~datbi >= sy-datum
       AND a~kbstat = space.
    IF sy-subrc <> 0.
      SELECT SINGLE b~kbetr
         INTO l_kbetr_tarifa
         FROM a958 AS a
         INNER JOIN konp AS b ON a~knumh = b~knumh
                             AND a~kschl = b~kschl
       WHERE a~kschl  = 'ZF00'
         AND a~tdlnr  = l_zf15_aux-tdlnr
         AND a~vstel  = l_zf15_aux-vstel
         AND a~add04  = l_vttk-add04
         AND a~pstlzz = l_zf15_aux-pstlz
         AND b~loevm_ko = space
         AND a~datab <= sy-datum
         AND a~datbi >= sy-datum
         AND a~kbstat = space.
    ENDIF.

    CLEAR ls_tarifa_entrega.
    ls_tarifa_entrega-vbeln = l_zf15_aux-vbeln.
    ls_tarifa_entrega-kbetr = l_kbetr_tarifa.
    APPEND ls_tarifa_entrega TO lt_tarifa_entrega.

  ENDLOOP.

  "Obtenemos la de mayor tarifa
  SORT lt_tarifa_entrega BY kbetr DESCENDING.
  READ TABLE lt_tarifa_entrega INTO ls_tarifa_entrega INDEX 1.

  SELECT SINGLE route INTO c_komk-route FROM likp WHERE vbeln = ls_tarifa_entrega-vbeln.

*    C_KOMK-ADD04 = l_vttk-add04.

ENDIF.
