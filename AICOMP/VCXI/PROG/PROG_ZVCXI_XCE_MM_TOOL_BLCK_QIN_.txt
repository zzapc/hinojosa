*&---------------------------------------------------------------------*
*& Report ZVCXI_XCE_MM_TOOL_BLCK_QIN
*&---------------------------------------------------------------------*
*& Release Tools to Production or Return Tools to Supplier after
*& Quality Inspection.
*& In case of release, the Batch Classification is updated with release
*& Date. Print Tool Sets are separated into single Ink Tools with the
*& release.
*&---------------------------------------------------------------------*
report ZVCXI_XCE_MM_TOOL_BLCK_QIN.

***------------------------------------------------------------------------------------------------
*** Selection Screen
***------------------------------------------------------------------------------------------------

*** General Parameters
selection-screen begin of block SEL with frame .
  parameters: PA_MATNR type MATNR   obligatory,
              PA_WERKS type WERKS_D obligatory,
              PA_CHARG type CHARG_D matchcode object MCH1.
selection-screen end of block SEL.

*** Radio Buttons to select whether release or return is processed
parameters: PA_RELEA radiobutton group RB1 user-command FRB1 default 'X',
            PA_RETUR radiobutton group RB1.
selection-screen skip.

*** Parameters for Release
selection-screen begin of block REL with frame title text-002.
  parameters: PA_STINF type ATWRT modif id G1,
              PA_TLTIM type ZVCXI_XCS_TLTIM modif id G1.
selection-screen end of block REL.

*** Parameters for Return
selection-screen begin of block RET with frame title text-001.
  parameters: PA_GRBEW type MB_GRBEW modif id G2.
selection-screen end of block RET.

***------------------------------------------------------------------------------------------------
*** Modify Parameters displayed on Selection Screen based on selected Radio Button
***------------------------------------------------------------------------------------------------
at selection-screen output.

  loop at screen.
    case ABAP_TRUE.
      when PA_RELEA.
        case SCREEN-GROUP1.
          when 'G1'.
            SCREEN-ACTIVE = '1'.
          when 'G2'.
            SCREEN-ACTIVE = '0'.
        endcase.

      when PA_RETUR.
        case SCREEN-GROUP1.
          when 'G1'.
            SCREEN-ACTIVE = '0'.
          when 'G2'.
            SCREEN-ACTIVE   = '1'.
            SCREEN-REQUIRED = '2'.
        endcase.
    endcase.
    modify screen.
  endloop.


***------------------------------------------------------------------------------------------------
*** Search Help for "Reason for Movement"
***------------------------------------------------------------------------------------------------
at selection-screen on value-request for PA_GRBEW.

  data: LF_SUBRC type I.
  data: LS_SHLP type SHLP_DESCR.
  data: LT_RETURN type table of DDSHRETVAL.

*** Get Search Help
  call function 'F4IF_GET_SHLP_DESCR'
    exporting
      SHLPNAME = 'H_T157D'
    importing
      SHLP     = LS_SHLP.

*** Set Movement Type
  LS_SHLP-INTERFACE[ SHLPFIELD = 'BWART' ]-VALUE = '122'.              "Return delivery to vendor

*** Request Reason for Movement
  LS_SHLP-INTERFACE[ SHLPFIELD = 'GRUND' ]-VALFIELD = 'PA_GRBEW'.
  LS_SHLP-INTERFACE[ SHLPFIELD = 'GRUND' ]-F4FIELD  = ABAP_TRUE.

*** Call Search Help
  call function 'F4IF_START_VALUE_REQUEST'
    exporting
      SHLP          = LS_SHLP
    importing
      RC            = LF_SUBRC
    tables
      RETURN_VALUES = LT_RETURN.
  if LF_SUBRC eq 0.
    PA_GRBEW = value #( LT_RETURN[ FIELDNAME = 'GRUND' ]-FIELDVAL optional ).
  endif.


***------------------------------------------------------------------------------------------------
*** Process based on Selection
***------------------------------------------------------------------------------------------------
start-of-selection.

  try.
      data(LR_QIN) = new ZCL_VCXI_XCE_MM_TOOL_BLCK_QIN( ).
      data(LR_MSG) = new /VCXI/CL_CKXM_MESSAGE( ).

      case ABAP_TRUE.
***     Release Tool
        when PA_RELEA.
          LR_QIN->RELEASE( exporting IS_QINRELEASE = value #( MATNR   = PA_MATNR
                                                              WERKS   = PA_WERKS
                                                              CHARG   = PA_CHARG
                                                              STINF   = PA_STINF
                                                              STINF_X = cond #( when PA_STINF is not initial then ABAP_TRUE )
                                                              TLTIM   = PA_TLTIM
                                                              TLTIM_X = cond #( when PA_TLTIM is not initial then ABAP_TRUE ) )
                           importing ET_BAPIRET2   = data(LT_BAPIRET2) ).

***     Return Tool
        when PA_RETUR.
***       Check if obligatory Parameters are set
          if PA_GRBEW is initial.
            /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( IF_MSGID = '00'
                                                  IF_MSGNO = '278'
                                                  IF_MSGTY = 'E'
                                                  IF_MSGV1 = 'Reason for Movement' ).
          endif.

          LR_QIN->RETURN( exporting IS_QINRETURN = value #( MATNR = PA_MATNR
                                                            WERKS = PA_WERKS
                                                            CHARG = PA_CHARG
                                                            GRBEW = PA_GRBEW )
                          importing ET_BAPIRET2  = LT_BAPIRET2 ).
      endcase.

      loop at LT_BAPIRET2 into data(LS_BAPIRET2).
        LR_MSG->ADD_MESSAGE_BY_BAPIRET2( IS_BAPIRET2 = LS_BAPIRET2 ).
      endloop.

      LR_MSG->DISPLAY_MSG( ).

    catch /VCXI/CX_CKX into data(LR_CX_CKX).
      LR_CX_CKX->SHOW_MESSAGE( IF_TYPE = 'S' ).
  endtry.
