*&---------------------------------------------------------------------*
*& Report ZVCXI_XCE_MM_TOOL_GR
*&---------------------------------------------------------------------*
*& This report creates a Material Document for the receival of tools
*& based on a Purchasing Document.
*&---------------------------------------------------------------------*
report ZVCXI_XCE_MM_TOOL_GR.

tables: EKPO.

data: GT_GMITEM   type table of BAPI2017_GM_ITEM_CREATE.

selection-screen begin of block SEL with frame.
  parameters:     PA_EBELN type EBELN memory id BES.
  select-options: SO_EBELP for  EKPO-EBELP.
selection-screen end of block SEL.

***------------------------------------------------------------------------------------------------
*** Process based on Selection
***------------------------------------------------------------------------------------------------
start-of-selection.
  try.
      data(GR_MSG) = new /VCXI/CL_CKXM_MESSAGE( ).

***   Get Purchasing Document Item
      data(LR_BOB_PURORD) = new ZCL_VCXI_XCI_BOB_PURORD( ).
      LR_BOB_PURORD->INIT_FROM_DB( exporting IF_PO_NUMBER = PA_EBELN
                                   changing  CR_MSG       = GR_MSG ).

***   Collect Items
      loop at LR_BOB_PURORD->T_ITEM into data(LS_ITEM)
                                   where PO_ITEM in SO_EBELP.
        append LR_BOB_PURORD->PREPARE_GMITEM_GR( IF_EBELP  = LS_ITEM-PO_ITEM
                                                 IF_INSMK  = '3'     "Blocked
                                               ) to GT_GMITEM.
      endloop.
      check GT_GMITEM is not initial.

***   Create Goods Movements
      ZCL_VCXI_XCI_BOB_GOODSMVT=>CREATE( exporting IF_BKTXT   = 'Tool Received'(001)
                                                   IF_GMCODE  = ZCL_VCXI_XCI_BOB_GOODSMVT=>C_GMCODE_GR_PURORD   "Goods receipt for purchase order
                                                   IT_GMITEM  = GT_GMITEM
                                         changing  CR_MSG     = GR_MSG ).
***   Show Result
      GR_MSG->DISPLAY_MSG( ).

    catch /VCXI/CX_CKX into data(GR_CX_CKX).
      GR_CX_CKX->SHOW_MESSAGE( ).
  endtry.
