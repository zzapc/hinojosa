*&---------------------------------------------------------------------*
*& Report ZVCXI_PXE_CAD_BOARD
*&---------------------------------------------------------------------*
*& This report can be used to send Board Grades as XML form SAP to CAD.
*&---------------------------------------------------------------------*
report ZVCXI_PXE_CAD_BOARD.

tables: ZVCXI_PCS_TD100,
        ZVCXI_PCS_TD110.

types: begin of YS_BOARD,
         BOARD type ZVCXI_PCS_BOARD,
         MBOAR type ZVCXI_PCSS_MBOAR,
         WERKS type WERKS_D,
       end of YS_BOARD,
       YT_BOARD type table of YS_BOARD.

*** Board Selection
selection-screen begin of block B1 with frame title text-B01.
  select-options: SO_MBOAR for ZVCXI_PCS_TD100-MBOAR,
                  SO_WERKS for ZVCXI_PCS_TD110-WERKS obligatory,
                  SO_FLUTE for ZVCXI_PCS_TD100-FLUTE,
                  SO_BOARD for ZVCXI_PCS_TD100-BOARD.
selection-screen end of block B1.

*** Processing Control
parameters: PA_DISP type ABAP_BOOL as checkbox,
            PA_WSRV type ABAP_BOOL as checkbox.


***#####################################################################
*** S T A R T - O F - S E L E C T I O N
***#####################################################################
start-of-selection.

  try.
***   Select all relevant Boards
      select TD100~BOARD, TD100~MBOAR, TD110~WERKS
             into table @data(GT_BOARD)
             from ZVCXI_PCS_TD100        as TD100
             inner join  ZVCXI_PCS_TD110 as TD110
                 on   TD110~GBOGV eq TD100~GBOGV
            where TD100~MBOAR in @SO_MBOAR
             and  TD100~BOARD in @SO_BOARD
             and  TD100~FLUTE in @SO_FLUTE
             and  TD110~WERKS in @SO_WERKS.
      check SY-SUBRC eq 0.

***   Create and send XML
      perform CREATE_AND_SEND using GT_BOARD[].

    catch /VCXI/CX_CKX into data(GR_CX_CKX).
      GR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

*&---------------------------------------------------------------------*
*& Form CREATE_AND_SEND
*&---------------------------------------------------------------------*
*& Create and send XML
*&---------------------------------------------------------------------*
form CREATE_AND_SEND using IT_BOARD type YT_BOARD.

  data: LF_TEXT type STRING.
  data: LR_IXML_DOCUMENT type ref to IF_IXML_DOCUMENT.


  try.
***   Document for Grades Update
      data(LR_DOCUMENT) = new ZCL_VCXI_XCA_DPM_DOCUMENT( IF_DOMID = 'ZVCXI_PXE_CAD_O_BOARD' ).

***   Create new instance and Load Values
      data(LR_DP_BOARD) = new ZCL_VCXI_PXE_CAD_DP_BOARD( ).
      loop at IT_BOARD assigning field-symbol(<S_BOARD>).
        LR_DP_BOARD->LOAD( IF_BOARD = <S_BOARD>-BOARD
                           IF_MBOAR = <S_BOARD>-MBOAR
                           IF_WERKS = <S_BOARD>-WERKS ).
      endloop.

***   Convert to XML
      data(LF_XML) = LR_DOCUMENT->CONV_DP_TO_XML( exporting IR_DATA          = ref #( LR_DP_BOARD )
                                                  importing ER_IXML_DOCUMENT = LR_IXML_DOCUMENT ).

***   Disply XML
      if PA_DISP eq ABAP_TRUE.
        call function '/VCXI/CKX_XML_POPUP_START'
          exporting
            IR_IXML_DOCUMENT = LR_IXML_DOCUMENT.
      endif.

***   Send XML to Web Service
      if PA_WSRV eq ABAP_TRUE.
        "TBD.
      endif.

    catch /VCXI/CX_CKX into data(GR_CX_CKX).
      GR_CX_CKX->SHOW_MESSAGE( ).
  endtry.


endform.
