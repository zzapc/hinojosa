*&---------------------------------------------------------------------*
*& Report ZVCXI_PXE_CAD_INB
*&---------------------------------------------------------------------*
*& Manually receive XML as file upload from CAD
*&---------------------------------------------------------------------*
report ZVCXI_PXE_CAD_INB.

data: GF_XXML type XSTRING.

***---------------------------------------------------------------------
*** S T A R T - O F - S E L E C T I O N
***---------------------------------------------------------------------
start-of-selection.

  try.
***   Get XML via File Upload
      perform GET_XXML changing GF_XXML.

***   Process Inbound directly or as Job
      data(GR_RECORD) = new ZCL_VCXI_PXE_CAD_INB( )->RECEIVE( IF_XXML = GF_XXML ).

    catch /VCXI/CX_CKX into data(GR_CX_CKX).
      GR_CX_CKX->SHOW_MESSAGE( ).
  endtry.


*&---------------------------------------------------------------------*
*& Form GET_XXML
*&---------------------------------------------------------------------*
*& Get XML via File Upload
*&---------------------------------------------------------------------*
form GET_XXML  changing CF_XXML type XSTRING
               raising  /VCXI/CX_CKX.

  data: LF_RC   type I,
        LF_XXML type XSTRING.
  data: LT_FILE     type FILETABLE,
        LT_BIN_DATA type W3MIMETABTYPE.

*** Get File Path
  CL_GUI_FRONTEND_SERVICES=>FILE_OPEN_DIALOG( exporting  FILE_FILTER             = '*.xml'
                                              changing   FILE_TABLE              = LT_FILE
                                                         RC                      = LF_RC
                                              exceptions FILE_OPEN_DIALOG_FAILED = 1
                                                         CNTL_ERROR              = 2
                                                         ERROR_NO_GUI            = 3
                                                         NOT_SUPPORTED_BY_GUI    = 4 ).
  if SY-SUBRC ne 0.
    /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
  endif.

*** Upload File
  CL_GUI_FRONTEND_SERVICES=>GUI_UPLOAD(
   exporting
      FILENAME                = conv #( LT_FILE[ 1 ]-FILENAME )
      FILETYPE                = 'BIN'
    changing
      DATA_TAB                =  LT_BIN_DATA     " Transfer table for file contents
    exceptions
      FILE_OPEN_ERROR         = 1                " File does not exist and cannot be opened
      FILE_READ_ERROR         = 2                " Error when reading file
      NO_BATCH                = 3                " Cannot execute front-end function in background
      GUI_REFUSE_FILETRANSFER = 4                " Incorrect front end or error on front end
      INVALID_TYPE            = 5                " Incorrect parameter FILETYPE
      NO_AUTHORITY            = 6                " No upload authorization
      UNKNOWN_ERROR           = 7                " Unknown error
      BAD_DATA_FORMAT         = 8                " Cannot Interpret Data in File
      HEADER_NOT_ALLOWED      = 9                " Invalid header
      SEPARATOR_NOT_ALLOWED   = 10               " Invalid separator
      HEADER_TOO_LONG         = 11               " Header information currently restricted to 1023 bytes
      UNKNOWN_DP_ERROR        = 12               " Error when calling data provider
      ACCESS_DENIED           = 13               " Access to file denied.
      DP_OUT_OF_MEMORY        = 14               " Not enough memory in data provider
      DISK_FULL               = 15               " Storage medium is full.
      DP_TIMEOUT              = 16               " Data provider timeout
      NOT_SUPPORTED_BY_GUI    = 17               " GUI does not support this
      ERROR_NO_GUI            = 18               " GUI not available
      others                  = 19 ).
  if SY-SUBRC ne 0.
    message id SY-MSGID type SY-MSGTY number SY-MSGNO with SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  endif.

  CF_XXML = CL_BCS_CONVERT=>SOLIX_TO_XSTRING( IT_SOLIX = LT_BIN_DATA ).

endform.
