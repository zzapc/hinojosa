*&---------------------------------------------------------------------*
*& Report ZVCXI_PXE_CAD_CUSTOMER
*&---------------------------------------------------------------------*
*& This report can be used to send Customers as XML form SAP to CAD.
*&---------------------------------------------------------------------*
report ZVCXI_PXE_CAD_CUSTOMER.

tables: KNVV.

types: YT_KUNNR type table of KUNNR.

data: GT_KUNNR type YT_KUNNR.


*** Customer Selection
selection-screen begin of block B1 with frame title text-B01.
  select-options: SO_VKORG for KNVV-VKORG obligatory,
                  SO_VTWEG for KNVV-VTWEG,
                  SO_SPART for KNVV-SPART,
                  SO_KUNNR for KNVV-KUNNR.
selection-screen end of block B1.

*** Processing Control
parameters: PA_DISP type ABAP_BOOL as checkbox,
            PA_WSRV type ABAP_BOOL as checkbox.


***#####################################################################
*** S T A R T - O F - S E L E C T I O N
***#####################################################################
start-of-selection.

  try.
***   Select all relevant Customer
      select KUNNR
             into table @GT_KUNNR
             from KNVV
            where VKORG in @SO_VKORG
             and  VTWEG in @SO_VTWEG
             and  SPART in @SO_SPART
             and  KUNNR in @SO_KUNNR.
      check SY-SUBRC eq 0.

***   Create and send XML
      perform CREATE_AND_SEND using GT_KUNNR[].

    catch /VCXI/CX_CKX into data(GR_CX_CKX).
      GR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

*&---------------------------------------------------------------------*
*& Form CREATE_AND_SEND
*&---------------------------------------------------------------------*
*& Create and send XML
*&---------------------------------------------------------------------*
form CREATE_AND_SEND using IT_KUNNR type YT_KUNNR.

  data: LF_TEXT type STRING.
  data: LR_IXML_DOCUMENT type ref to IF_IXML_DOCUMENT.


  try.
***   Document for Grades Update
      data(LR_DOCUMENT) = new ZCL_VCXI_XCA_DPM_DOCUMENT( IF_DOMID = 'ZVCXI_PXE_CAD_O_CUSTOMER' ).

***   Create new instance and Load Values
      data(LR_DP_CUSTOMER) = new ZCL_VCXI_PXE_CAD_DP_CUSTOMER( ).
      loop at IT_KUNNR assigning field-symbol(<F_KUNNR>).
        LR_DP_CUSTOMER->LOAD( IF_KUNNR = <F_KUNNR> ).
      endloop.

***   Convert to XML
      data(LF_XML) = LR_DOCUMENT->CONV_DP_TO_XML( exporting IR_DATA          = ref #( LR_DP_CUSTOMER )
                                                  importing ER_IXML_DOCUMENT = LR_IXML_DOCUMENT ).

***   Disply XML
      if PA_DISP eq ABAP_TRUE.
        call function '/VCXI/CKX_XML_POPUP_START'
          exporting
            IR_IXML_DOCUMENT = LR_IXML_DOCUMENT.
      endif.

***   Send XML to Web Service
      if PA_WSRV eq ABAP_TRUE.
        "TBD.
      endif.

    catch /VCXI/CX_CKX into data(GR_CX_CKX).
      GR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

endform.
