
class ZCL_VCXI_PFSP_GRID_MFMI_REEL definition
  public
  inheriting from ZCL_VCXI_XCS_GRID_PRVDR
  create public .

public section.

  type-pools ABAP .
  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_CONTAINER_NAME type SCRFNAME
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL default ABAP_TRUE
    raising
      /VCXI/CX_CKX .
protected section.

  type-pools ABAP .
  data F_PURCHASED type ABAP_BOOL .

  methods CHANGE_STYLE_MFMI
    importing
      !IF_PURCH type ZVCXI_XCS_PURCH
      !IF_DUMMY type ABAP_BOOL
    changing
      !CT_STYL type LVC_T_STYL
    raising
      /VCXI/CX_CKX .
  methods CHECK_PSCALE_MGR_ACTIV
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    returning
      value(RF_ACTIV) type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
  methods GET_MFMI_REEL_VIS
    importing
      !IR_PR_MFMI_REEL type ref to ZCL_VCXI_PFSP_PR_MFMI_REEL
    returning
      value(RS_MFMI_REEL_VIS) type ZVCXI_PFSP_S_MFMI_REEL_VIS
    raising
      /VCXI/CX_CKX .
  methods SET_MFMI_REEL_VIS
    importing
      !IR_PR_MFMI_REEL type ref to ZCL_VCXI_PFSP_PR_MFMI_REEL
      !IS_MFMI_REEL_VIS type ZVCXI_PFSP_S_MFMI_REEL_VIS
    raising
      /VCXI/CX_CKX .

  methods GET_FIELDCAT
    redefinition .
  methods GET_GRID_DATA
    redefinition .
  methods HANDLE_DATA_CHANGED_FINISHED
    redefinition .
private section.
endclass. "ZCL_VCXI_PFSP_GRID_MFMI_REEL definition
class ZCL_VCXI_PFSP_GRID_MFMI_REEL implementation.
method CHANGE_STYLE_MFMI.

  free CT_STYL.

*** --------------------------------------------------------------------------------------------
*** Set Style Details
*** Set Reel Matnr = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MATNR'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set ID of MF x-connection = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'IMFXC'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Base Weight = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGT'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Base Weight Unit = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGP'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Material Flow in Factor = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MFICF'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Material Flow in Setup Percentage = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MFISP'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Text = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'TEXT'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set PC Type = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PCTYP'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set PC Length = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PCLEN'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set PC Width = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PCWID'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set PC Height = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PCHEI'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).
*** Set PC Ups = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PCUPS'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Number of Outs = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'NOOUT'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set PC Direction = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PCDIR'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Vendor Price and Price Unit = Editable if PURCH = ABAP_TRUE
  if IF_PURCH eq ABAP_TRUE.

    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR'
                                IF_STYLE     = ME->F_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).

    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                                IF_STYLE     = ME->F_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).

    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                                IF_STYLE     = ME->F_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).

  else.

    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR'
                                IF_STYLE     = ME->F_STYLE_DISABLED
                      changing  CT_STYL      = CT_STYL ).

    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                                IF_STYLE     = ME->F_STYLE_DISABLED
                      changing  CT_STYL      = CT_STYL ).

    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                                IF_STYLE     = ME->F_STYLE_DISABLED
                      changing  CT_STYL      = CT_STYL ).

  endif.

*** Set Purchase Flag not Editable if it is a Dummy
  if IF_DUMMY eq ABAP_TRUE.
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PURCH'
                                IF_STYLE     = ME->F_STYLE_DISABLED
                      changing  CT_STYL      = CT_STYL ).
  else.
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PURCH'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).
  endif.

endmethod.
method CHECK_PSCALE_MGR_ACTIV.

  data:          LT_GATE              type        /VCXI/VKSR_TT_GATE.
  data:          LR_GATE              type ref to /VCXI/CL_VKSR_GATE,
                 LR_PSCALE_MGR        type ref to ZIF_VCXI_XCS_PSCALE_MGR.

***------------------------------------------------------------------
*** If we are connect to a Scale Manager -> Request Price depending on Consumption
  LT_GATE = IR_GATE->GET_PRVDR( IF_IPRVT = ZIF_VCXI_XCS_PSCALE_MGR=>C_IPRVT_XCS_PSCALE_MGR
                                IF_INUSE = ABAP_TRUE ).

*** Assign Reference of Price Scale Manager
  read table LT_GATE into LR_GATE index 1.
  if SY-SUBRC eq 0.
    try.
        move LR_GATE->R_CLSPR ?to LR_PSCALE_MGR.
      catch CX_SY_MOVE_CAST_ERROR.
        exit.
    endtry.
  endif.

  check LR_PSCALE_MGR is bound.
  move ABAP_TRUE to RF_ACTIV.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_GATE           = IR_GATE
                      IR_UIGATE         = IR_UIGATE
                      IF_TABNAME        = 'ZVCXI_PFSP_S_MFMI_REEL_GRID'
                      IF_CONTAINER_NAME = IF_CONTAINER_NAME
                      IF_APPL_EVENTS    = IF_APPL_EVENTS ).

*** Define Provider Type for Button
  ME->F_IPRVT = ZIF_VCXI_XCSP_MFI=>C_IPRVT_XCS_MFI.

endmethod.
method GET_FIELDCAT.

  data:          LF_LOCKED type        /VCXI/CKX_LOCKED,
                 LF_INUSE  type        /VCXI/VKSR_INUSE.
  field-symbols: <S_FCAT>  type        LVC_S_FCAT.

***----------------------------------------------------------------------------
*** Get Standard Field Catalog
  RT_FCAT = SUPER->GET_FIELDCAT( ).

*** Get Lock and In-Use Status
  LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
  LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***----------------------------------------------------------------------------
*** Adjust Field Catalog
  loop at RT_FCAT assigning <S_FCAT>.
    case <S_FCAT>-FIELDNAME.
      when 'POSID' or 'GATKY' or 'R_GATE' or 'T_STYL'.
        move ABAP_TRUE    to <S_FCAT>-TECH.

*** Set Edit for all fields which should Editable at all
*** -> Don't forget to set also a Style for this Field
      when 'MATNR' or 'IMFXC' or 'BAWGT' or 'BAWGP' or 'MFICF'
        or 'MFISP' or 'TEXT' or 'PCWID' or 'PCLEN' or 'PCHEI'
        or 'PCUPS'.
        if LF_LOCKED eq ABAP_TRUE and
           LF_INUSE  eq ABAP_TRUE.
          move ABAP_TRUE    to <S_FCAT>-EDIT.
        endif.

*** Set Edit for Price fields if it is purchased
*** And Hide it if not needed
*** -> Don't forget to set also a Style for this Field
      when 'LIFNR' or 'PRICE' or 'PRICP'.
        if ME->F_PURCHASED eq ABAP_TRUE.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE    to <S_FCAT>-EDIT.
          endif.
        else.
          move ABAP_TRUE    to <S_FCAT>-NO_OUT.
        endif.

      when 'PURCH'.
        if LF_LOCKED eq ABAP_TRUE and
           LF_INUSE  eq ABAP_TRUE.
          move ABAP_TRUE to <S_FCAT>-EDIT.
        endif.

      when 'LIFNR_DESCR' or 'PRICE_CURR' or 'PRICP_UOM'.
        if ME->F_PURCHASED eq ABAP_FALSE.
          move ABAP_TRUE    to <S_FCAT>-NO_OUT.
        endif.

    endcase.
  endloop.

endmethod.
method GET_GRID_DATA.

  data: LF_PURCH type ABAP_BOOL.
  data: LS_MFMI_REEL_VIS  type ZVCXI_PFSP_S_MFMI_REEL_VIS,
        LS_MFMI_REEL_GRID type ZVCXI_PFSP_S_MFMI_REEL_GRID.
  data: LT_GATE type /VCXI/VKSR_TT_GATE.
  data: LR_GATE         type ref to   /VCXI/CL_VKSR_GATE,
        LR_PR_MFMI_REEL type ref to   ZCL_VCXI_PFSP_PR_MFMI_REEL.
  field-symbols: <T_DATA> type standard table,
                 <S_DATA> type          ANY.

*** Dereferencing of Data Reference
  assign IR_DATA->* to <T_DATA>.

  clear: ME->F_PURCHASED.

*** Get Gates of Reels
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_PFSP_PR_MFMI_REEL=>C_IPRVT_PF_MFMI_REEL
                                   IF_INUSE  = ABAP_TRUE
                                   IF_SORTED = ABAP_TRUE ).

  loop at LT_GATE into LR_GATE.
    clear: LS_MFMI_REEL_GRID.

    try.
        move LR_GATE->R_CLSPR ?to LR_PR_MFMI_REEL.
        check LR_PR_MFMI_REEL is bound.

*** --------------------------------------------------------------------------------------------
***     Get Data from PR Class
        move LR_GATE->GET_POSID( ) to LS_MFMI_REEL_GRID-POSID.
        move LR_GATE->F_GATKY      to LS_MFMI_REEL_GRID-GATKY.
        move LR_GATE               to LS_MFMI_REEL_GRID-R_GATE.

        LS_MFMI_REEL_VIS = ME->GET_MFMI_REEL_VIS( IR_PR_MFMI_REEL = LR_PR_MFMI_REEL ).
        move-corresponding LS_MFMI_REEL_VIS to LS_MFMI_REEL_GRID.

***     Get Flags for Fieldcat
        case LS_MFMI_REEL_VIS-PURCH.
          when ZCL_VCXI_XCSS_PR_MS=>C_PURCH_DIRECT_PRICE.
            ME->F_PURCHASED = LS_MFMI_REEL_VIS-PURCH.
          when ZCL_VCXI_XCSS_PR_MS=>C_PURCH_DIRECT.
            if ME->F_PURCHASED ne ZCL_VCXI_XCSS_PR_MS=>C_PURCH_DIRECT_PRICE.
              ME->F_PURCHASED = LS_MFMI_REEL_VIS-PURCH.
            endif.
        endcase.

        if LS_MFMI_REEL_VIS-PURCH ne ZIF_VCXI_XCSP_MFM=>C_PURCH_STOCK and
           ME->CHECK_PSCALE_MGR_ACTIV( IR_GATE = LR_GATE ) eq ABAP_FALSE.
          move ABAP_TRUE to LF_PURCH.
        endif.

*** --------------------------------------------------------------------------------------------
***     Set Style Details
        ME->CHANGE_STYLE_MFMI( exporting IF_PURCH = LF_PURCH
                                         IF_DUMMY = LR_PR_MFMI_REEL->IS_DUMMY( )
                               changing  CT_STYL  = LS_MFMI_REEL_GRID-T_STYL ).

***     Call DVC Service - Modify Style
        ZCL_VCXI_XCDR_SERVICE=>MODIFY_STYLE( exporting IR_GATE = LR_GATE
                                             changing  CT_STYL = LS_MFMI_REEL_GRID-T_STYL ).

*** --------------------------------------------------------------------------------------------
***     Add Entry
        append initial line to <T_DATA> assigning <S_DATA>.
        move-corresponding LS_MFMI_REEL_GRID to <S_DATA>.

      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.
  endloop.

endmethod.
method GET_MFMI_REEL_VIS.

  data:  LS_CONSU type ZVCXI_XCSP_S_CONSU,
         LS_MFISP type ZVCXI_XCSP_S_MFISP,
         LS_PRICE type ZVCXI_XCS_S_PRICE,
         LS_PRICP type ZVCXI_XCS_S_PRICP,
         LS_BAWGT type ZVCXI_P0S_S_BAWGT,
         LS_BAWGP type ZVCXI_P0S_S_BAWGP,
         LS_PCDIM type ZVCXI_P0SP_S_PCDIM.

*** Material
  move IR_PR_MFMI_REEL->GET_MATNR( )                          to RS_MFMI_REEL_VIS-MATNR.
  move IR_PR_MFMI_REEL->GET_MATNR_DESCR( )                    to RS_MFMI_REEL_VIS-MATNR_DESCR.

*** ID of MF x-connection
  move IR_PR_MFMI_REEL->GET_IMFXC( )                          to RS_MFMI_REEL_VIS-IMFXC.

*** Consumption
  move IR_PR_MFMI_REEL->GET_CONSU( )                          to LS_CONSU.
  move-corresponding LS_CONSU                                 to RS_MFMI_REEL_VIS.

*** MF Ratio
  move IR_PR_MFMI_REEL->GET_MFRAT( )                          to RS_MFMI_REEL_VIS-MFRAT.

*** Setup Percentage for Material Flow In
  move IR_PR_MFMI_REEL->ZIF_VCXI_XCSP_MFI~GET_MFISP( )        to LS_MFISP.
  move-corresponding LS_MFISP                                 to RS_MFMI_REEL_VIS.

*** Consumption Factor for Material Flow In
  move IR_PR_MFMI_REEL->ZIF_VCXI_XCSP_MFI~GET_MFICF( )        to RS_MFMI_REEL_VIS-MFICF.

*** Text
  move IR_PR_MFMI_REEL->ZIF_VCXI_XCSP_MFM~GET_TEXT( )         to RS_MFMI_REEL_VIS-TEXT.

*** Vendor
  move IR_PR_MFMI_REEL->ZIF_VCXI_XCSP_MFM~GET_LIFNR( )        to RS_MFMI_REEL_VIS-LIFNR.
  move IR_PR_MFMI_REEL->ZIF_VCXI_XCSP_MFM~GET_LIFNR_DESCR( )  to RS_MFMI_REEL_VIS-LIFNR_DESCR.

*** Price
  move IR_PR_MFMI_REEL->ZIF_VCXI_XCSP_MFM~GET_PRICE( )        to LS_PRICE.
  move-corresponding LS_PRICE                                 to RS_MFMI_REEL_VIS.

  move IR_PR_MFMI_REEL->ZIF_VCXI_XCSP_MFM~GET_PRICP( )        to LS_PRICP.
  move-corresponding LS_PRICP                                 to RS_MFMI_REEL_VIS.

*** Purchased Flag
  move IR_PR_MFMI_REEL->ZIF_VCXI_XCSP_MFM~GET_PURCH( )        to RS_MFMI_REEL_VIS-PURCH.

*** Base Weight
  move IR_PR_MFMI_REEL->GET_BAWGT( )                          to LS_BAWGT.
  move-corresponding LS_BAWGT                                 to RS_MFMI_REEL_VIS.

  move IR_PR_MFMI_REEL->GET_BAWGP( )                          to LS_BAWGP.
  move-corresponding LS_BAWGP                                 to RS_MFMI_REEL_VIS.

*** PC Dimensions
  move IR_PR_MFMI_REEL->GET_PCDIM( )                          to LS_PCDIM.
  move-corresponding LS_PCDIM                                 to RS_MFMI_REEL_VIS.

endmethod.
method HANDLE_DATA_CHANGED_FINISHED.

  data:          LS_MFMI_REEL_VIS     type          ZVCXI_PFSP_S_MFMI_REEL_VIS,
                 LS_MFMI_REEL_GRID    type          ZVCXI_PFSP_S_MFMI_REEL_GRID.
  data:          LR_PR_MFMI_REEL      type ref to   ZCL_VCXI_PFSP_PR_MFMI_REEL,
                 LR_CX_CKX            type ref to   /VCXI/CX_CKX.
  field-symbols: <T_DATA>             type standard table,
                 <S_DATA>             type          any.

  check E_MODIFIED eq ABAP_TRUE.

  try.

      SUPER->HANDLE_DATA_CHANGED_FINISHED( E_MODIFIED    = E_MODIFIED
                                           ET_GOOD_CELLS = ET_GOOD_CELLS ).

***   Dereferencing of Data Reference
      assign ME->R_DATA->* to <T_DATA>.

      loop at <T_DATA> assigning <S_DATA>.
        move-corresponding <S_DATA> to LS_MFMI_REEL_GRID.
        check LS_MFMI_REEL_GRID-R_GATE is bound.

        move LS_MFMI_REEL_GRID-R_GATE->R_CLSPR ?to LR_PR_MFMI_REEL.

***     Set Values to PR Class
        move-corresponding LS_MFMI_REEL_GRID to LS_MFMI_REEL_VIS.
        ME->SET_MFMI_REEL_VIS( IR_PR_MFMI_REEL  = LR_PR_MFMI_REEL
                               IS_MFMI_REEL_VIS = LS_MFMI_REEL_VIS ).
      endloop.


      ME->R_GATE->RAISE_EVENTS( ).

    catch /VCXI/CX_CKX into LR_CX_CKX.
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

*** Request Refresh for ALV
  move ABAP_TRUE to ME->F_REFRESH_REQ.

endmethod.
method SET_MFMI_REEL_VIS.

  data:  LS_MFISP          type ZVCXI_XCSP_S_MFISP,
         LS_PRICE          type ZVCXI_XCS_S_PRICE,
         LS_PRICP          type ZVCXI_XCS_S_PRICP,
         LS_BAWGT          type ZVCXI_P0S_S_BAWGT,
         LS_BAWGP          type ZVCXI_P0S_S_BAWGP,
         LS_PCDIM          type ZVCXI_P0SP_S_PCDIM,
         LS_MFMI_REEL_VIS  type ZVCXI_PFSP_S_MFMI_REEL_VIS.

*** Get Current data to do Update only if Value changed.
  LS_MFMI_REEL_VIS = ME->GET_MFMI_REEL_VIS( IR_PR_MFMI_REEL = IR_PR_MFMI_REEL ).

*** Material
  if LS_MFMI_REEL_VIS-MATNR ne IS_MFMI_REEL_VIS-MATNR.
    IR_PR_MFMI_REEL->SET_MATNR( IF_MATNR = IS_MFMI_REEL_VIS-MATNR
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** ID for MF x-connection
  if LS_MFMI_REEL_VIS-IMFXC ne IS_MFMI_REEL_VIS-IMFXC.
    IR_PR_MFMI_REEL->SET_IMFXC( IF_IMFXC = IS_MFMI_REEL_VIS-IMFXC
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Setup Percentage for Material Flow In
  if LS_MFMI_REEL_VIS-MFISP ne IS_MFMI_REEL_VIS-MFISP.
    move-corresponding IS_MFMI_REEL_VIS to LS_MFISP.
    IR_PR_MFMI_REEL->SET_MFISP( IS_MFISP = LS_MFISP
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Consumption Factor for Material Flow In
  if LS_MFMI_REEL_VIS-MFICF ne IS_MFMI_REEL_VIS-MFICF.
    IR_PR_MFMI_REEL->SET_MFICF( IF_MFICF = IS_MFMI_REEL_VIS-MFICF
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Text
  if LS_MFMI_REEL_VIS-TEXT ne IS_MFMI_REEL_VIS-TEXT.
    IR_PR_MFMI_REEL->SET_TEXT( IF_TEXT = IS_MFMI_REEL_VIS-TEXT
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Purchase Flag
  if LS_MFMI_REEL_VIS-PURCH ne IS_MFMI_REEL_VIS-PURCH.
    IR_PR_MFMI_REEL->SET_PURCH( IF_PURCH = IS_MFMI_REEL_VIS-PURCH
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Vendor
  if LS_MFMI_REEL_VIS-LIFNR ne IS_MFMI_REEL_VIS-LIFNR.
    IR_PR_MFMI_REEL->SET_LIFNR( IF_LIFNR = IS_MFMI_REEL_VIS-LIFNR
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Price
  if LS_MFMI_REEL_VIS-PRICE ne IS_MFMI_REEL_VIS-PRICE.
    move-corresponding IS_MFMI_REEL_VIS to LS_PRICE.
    IR_PR_MFMI_REEL->SET_PRICE( IS_PRICE = LS_PRICE
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

  if LS_MFMI_REEL_VIS-PRICP ne IS_MFMI_REEL_VIS-PRICP.
    move-corresponding IS_MFMI_REEL_VIS to LS_PRICP.
    IR_PR_MFMI_REEL->SET_PRICP( IS_PRICP = LS_PRICP
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Base Weight
  if LS_MFMI_REEL_VIS-BAWGT ne IS_MFMI_REEL_VIS-BAWGT.
    move-corresponding IS_MFMI_REEL_VIS to LS_BAWGT.
    IR_PR_MFMI_REEL->SET_BAWGT( IS_BAWGT = LS_BAWGT
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

  if LS_MFMI_REEL_VIS-BAWGP ne IS_MFMI_REEL_VIS-BAWGP.
    move-corresponding IS_MFMI_REEL_VIS to LS_BAWGP.
    IR_PR_MFMI_REEL->SET_BAWGP( IS_BAWGP = LS_BAWGP
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** PC Dimensions
  move-corresponding IS_MFMI_REEL_VIS to LS_PCDIM.
  IR_PR_MFMI_REEL->SET_PCDIM( IS_PCDIM = LS_PCDIM
                              IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).

endmethod.
