
class ZCL_VCXI_FFSP_PR_ADDON_FOLD definition
  public
  inheriting from ZCL_VCXI_XCSP_PR_ADDON
  create public .

public section.

  constants C_PARID_TFOLD type /VCXI/VKS_PARID value 'ZVCXI_FF_TFOLD' ##NO_TEXT.
  constants C_IAATY_TFOLD type /VCXI/VSMC_IAATY value 'FF_AA_TFOLD' ##NO_TEXT.

  methods GET_NOLAY
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
      !EF_NOLAY type ZVCXI_F0S_NOLAY
    raising
      /VCXI/CX_CKX .
  methods GET_TFOLD
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
      !EF_TFOLD type ZVCXI_FFS_TFOLD
      !EF_DESCR type /VCXI/CKX_DESCR
    raising
      /VCXI/CX_CKX .
  methods SET_TFOLD
    importing
      !IF_TFOLD type ZVCXI_FFS_TFOLD
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
protected section.

  constants C_IEVNT_O_TFOLD_CHANGED type /VCXI/VKSC_IEVNT value 'TFOLD_CHANGED' ##NO_TEXT.

  methods GET_RTE_SAVAL
    redefinition .
private section.
endclass. "ZCL_VCXI_FFSP_PR_ADDON_FOLD definition
class ZCL_VCXI_FFSP_PR_ADDON_FOLD implementation.
  method GET_NOLAY.

    data: LF_TFOLD type ZVCXI_FFS_TFOLD.

    clear EF_NOLAY.

*** Get Folding Type
    ME->GET_TFOLD( importing EF_TFOLD = LF_TFOLD ).

    check LF_TFOLD is not initial.

*** Get Number of Base Layers from Customizing
    select single NOLAY
             from ZVCXI_FFS_TC140
             into EF_NOLAY
            where TFOLD eq LF_TFOLD.                                 "#EC CI_SUBRC

  endmethod.
  method GET_RTE_SAVAL.


    data:          LF_GAATY      type        /VCXI/VSMC_GAATY,
                   LF_TFOLD      type        ZVCXI_FFS_TFOLD.
    data:          LT_GATE       type        /VCXI/VKSR_TT_GATE.
    data:          LR_CX_CKX     type ref to /VCXI/CX_CKX.
    field-symbols: <S_RTE_SAVAL> type        /VCXI/VSMR_S_RTE_SAVAL,
                   <S_AVVAL>     type        /VCXI/VSMR_S_RTE_RANGE_AVVAL.

*** Get Super
    RT_RTE_SAVAL = SUPER->GET_RTE_SAVAL( ).

    check ME->R_RTE_CADDON          is bound and
          ME->R_RTE_CADDON->R_ATYPE is bound.

***--------------------------------------------------------------------------------------
*** Aspect - Folding Type
    try.
        ME->GET_TFOLD( importing EF_TFOLD = LF_TFOLD ).

        LF_GAATY = ME->R_RTE_CADDON->R_ATYPE->GET_GAATY_BY_IAATY( IF_IAATY = C_IAATY_TFOLD
                                                                  IF_MAATY = /VCXI/CL_VSMC_ATYPE=>C_MAATY_VALUE ).
        append initial line to RT_RTE_SAVAL assigning <S_RTE_SAVAL>.
        move LF_GAATY to <S_RTE_SAVAL>-GAATY.

        append initial line to <S_RTE_SAVAL>-I_AVVAL assigning <S_AVVAL>.
        move 'I'              to <S_AVVAL>-SIGN.
        move 'EQ'             to <S_AVVAL>-OPTION.
        move LF_TFOLD         to <S_AVVAL>-LOW.

      catch /VCXI/CX_CKX into LR_CX_CKX.
        return.
    endtry.

  endmethod.
  method GET_TFOLD.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TFOLD ).
    move LS_PARAM-ATWRT to EF_TFOLD.
    move LS_PARAM-SETBY to EF_SETBY.

    check EF_TFOLD is not initial.

    if EF_DESCR is requested.
      select single DESCR
             from ZVCXI_FFS_TC140T
             into EF_DESCR
            where LANGU eq SY-LANGU
             and  TFOLD eq EF_TFOLD.
    endif.

  endmethod.
  method SET_TFOLD.

    data: LF_CHANGED type ABAP_BOOL.
    data: LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TFOLD ).

    move IF_TFOLD to LS_PARAM-ATWRT.
    move IF_SETBY to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_TFOLD_CHANGED ).
    endif.

  endmethod.
