
class ZCL_VCXI_XCA_MWB_REGMDL definition
  public
  inheriting from /VCXI/CL_CKR_REGELMNT
  final
  create public .

public section.

  constants C_CLSNA_CLQRY type SEOCLSNAME value 'ZCL_VCXI_XCA_MWB_CLQRY' ##NO_TEXT.
  constants C_CLSNA_CLMAT type SEOCLSNAME value 'ZCL_VCXI_XCA_MWB_CLMAT' ##NO_TEXT.
  constants C_CLSNA_CLDUI type SEOCLSNAME value 'ZCL_VCXI_XCA_MWB_CLDUI' ##NO_TEXT.
  constants C_ELMTY_REGMDL type /VCXI/CKR_ELMTY value 'ZVCXI_XCA_MWB' ##NO_TEXT.
  data S_RPSEL type ZVCXI_XCA_MWB_S_RPSEL read-only .
  data S_CLQRY type ZVCXI_XCA_MWB_S_CLQRY read-only .
  data S_CLMAT type ZVCXI_XCA_MWB_S_CLMAT read-only .
  data S_CLDUI type ZVCXI_XCA_MWB_S_CLDUI read-only .

  methods CONSTRUCTOR
    importing
      !IF_GREGE type /VCXI/CKR_GREGE
      !IR_ELMTY type ref to /VCXI/CL_CKR_ELMTY
    raising
      /VCXI/CX_CKX .
  methods SET_CLMAT
    importing
      !IF_CLMAT type ZVCXI_XCA_MWB_CLMAT
    raising
      /VCXI/CX_CKX .
  methods SET_CLQRY
    importing
      !IF_CLQRY type ZVCXI_XCA_MWB_CLQRY
    raising
      /VCXI/CX_CKX .
  methods SET_CLDUI
    importing
      !IF_CLDUI type ZVCXI_XCA_MWB_CLDUI
    raising
      /VCXI/CX_CKX .
  methods SET_RPSEL
    importing
      !IF_RPSEL type ZVCXI_XCA_MWB_RPSEL
    raising
      /VCXI/CX_CKX .

  methods CHECK
    redefinition .
protected section.

  methods CHECK_CLQRY
    raising
      /VCXI/CX_CKX .
  methods CHECK_CLMAT
    raising
      /VCXI/CX_CKX .
  methods CHECK_RPSEL
    raising
      /VCXI/CX_CKX .
  methods CHECK_CLDUI
    raising
      /VCXI/CX_CKX .

  methods HANDLE_COPY
    redefinition .
  methods HANDLE_SAVE
    redefinition .
private section.

  methods LOAD_FROM_DB
    raising
      /VCXI/CX_CKX .
endclass. "ZCL_VCXI_XCA_MWB_REGMDL definition
class ZCL_VCXI_XCA_MWB_REGMDL implementation.
  method CHECK.

*** Perform Super Check
    SUPER->CHECK( ).

*** Check Report Sel.Screen
    ME->CHECK_RPSEL( ).

*** Check Class for Query
    ME->CHECK_CLQRY( ).

*** Check Class for DUI
    ME->CHECK_CLDUI( ).

*** Check Class for Material
    ME->CHECK_CLMAT( ).

  endmethod.
  method CHECK_CLDUI.

***------------------------------------------------------------------------------------------------
*** DUI Class is required.
    if ME->S_CLDUI-CLDUI is initial.
***   DUI Class is required for Material Workbench Model.
      /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( IF_MSGTY = 'E'
                                            IF_MSGID = 'ZVCXI_XCA_MWB'
                                            IF_MSGNO = '004' ).

***------------------------------------------------------------------------------------------------
    else.
***   Check compatibility
      /VCXI/CL_CKXB_SERVICE=>CHECK_COMPATIBILITY_BY_NAME( IF_CLASSNAME_SUPER = C_CLSNA_CLDUI
                                                          IF_CLASSNAME_SUB   = ME->S_CLDUI-CLDUI
                                                          IF_RAISE_EXCEPTION = ABAP_TRUE ).
    endif.

  endmethod.
  method CHECK_CLMAT.

***------------------------------------------------------------------------------------------------
*** Material Class is required.
    if ME->S_CLMAT-CLMAT is initial.
***   Material Class is required for Material Workbench Model.
      /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( IF_MSGTY = 'E'
                                            IF_MSGID = 'ZVCXI_XCA_MWB'
                                            IF_MSGNO = '005' ).

***------------------------------------------------------------------------------------------------
    else.
***   Check compatibility
      /VCXI/CL_CKXB_SERVICE=>CHECK_COMPATIBILITY_BY_NAME( IF_CLASSNAME_SUPER = C_CLSNA_CLMAT
                                                          IF_CLASSNAME_SUB   = ME->S_CLMAT-CLMAT
                                                          IF_RAISE_EXCEPTION = ABAP_TRUE ).
    endif.

  endmethod.
  method CHECK_CLQRY.

***------------------------------------------------------------------------------------------------
*** Query Class is required.
    if ME->S_CLQRY-CLQRY is initial.
***   Query Class is required for Material Workbench Model.
      /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( IF_MSGTY = 'E'
                                            IF_MSGID = 'ZVCXI_XCA_MWB'
                                            IF_MSGNO = '003' ).

***------------------------------------------------------------------------------------------------
    else.
***   Check compatibility
      /VCXI/CL_CKXB_SERVICE=>CHECK_COMPATIBILITY_BY_NAME( IF_CLASSNAME_SUPER = C_CLSNA_CLQRY
                                                          IF_CLASSNAME_SUB   = ME->S_CLQRY-CLQRY
                                                          IF_RAISE_EXCEPTION = ABAP_TRUE ).
    endif.

  endmethod.
  method CHECK_RPSEL.

    data: LS_TRDIR type TRDIR.

***------------------------------------------------------------------------------------------------
*** Report Sel.Screen is required.
    if ME->S_RPSEL-RPSEL is initial.
***   Report Sel.Screen is required for Material Workbench Model.
      /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( IF_MSGTY = 'E'
                                            IF_MSGID = 'ZVCXI_XCA_MWB'
                                            IF_MSGNO = '001' ).

***------------------------------------------------------------------------------------------------
    else.
***   Check Report Name
      call function 'READ_TRDIR'
        exporting
          I_PROGNAME = ME->S_RPSEL-RPSEL
        importing
          E_TRDIR    = LS_TRDIR
        exceptions
          NOT_EXISTS = 1
          others     = 2.
      if SY-SUBRC <> 0.
***     Forward message
        /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( IF_MSGTY = 'E'
                                              IF_MSGID = SY-MSGID
                                              IF_MSGNO = SY-MSGNO
                                              IF_MSGV1 = SY-MSGV1
                                              IF_MSGV2 = SY-MSGV2
                                              IF_MSGV3 = SY-MSGV3
                                              IF_MSGV4 = SY-MSGV4 ).
      else.
        check LS_TRDIR-SUBC ne '1'.
***     Program & is not executable.
        /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( IF_MSGTY = 'E'
                                              IF_MSGID = 'ZVCXI_XCA_MWB'
                                              IF_MSGNO = '002'
                                              IF_MSGV1 = ME->S_RPSEL-RPSEL ).
      endif.
    endif.

  endmethod.
  method CONSTRUCTOR.

***------------------------------------------------------------------------------------------------
*** Super Constructor
    SUPER->CONSTRUCTOR( IF_GREGE = IF_GREGE
                        IR_ELMTY = IR_ELMTY ).

***------------------------------------------------------------------------------------------------
*** Load DB
    ME->LOAD_FROM_DB( ).

  endmethod.
  method HANDLE_COPY.

    data:  LR_REGMDL type ref to ZCL_VCXI_XCA_MWB_REGMDL.


***----------------------------------------------------------------------------
*** Call Super Copy
    RR_REGELMNT = SUPER->HANDLE_COPY( ).

***----------------------------------------------------------------------------
    try.
        move RR_REGELMNT ?to LR_REGMDL.
        check LR_REGMDL is bound.

***----------------------------------------------------------------------------
***     Copy Report Sel.Screen
        LR_REGMDL->SET_RPSEL( IF_RPSEL = ME->S_RPSEL-RPSEL ).

***     Copy Query, Detail UI and Material Class
        LR_REGMDL->SET_CLQRY( IF_CLQRY = ME->S_CLQRY-CLQRY ).
        LR_REGMDL->SET_CLDUI( IF_CLDUI = ME->S_CLDUI-CLDUI ).
        LR_REGMDL->SET_CLMAT( IF_CLMAT = ME->S_CLMAT-CLMAT ).

      catch CX_SY_MOVE_CAST_ERROR.
        exit.
    endtry.

  endmethod.
  method HANDLE_SAVE.

*** Save only if changed
    check ME->F_CHANGED eq ABAP_TRUE.

***----------------------------------------------------------------------------
*** Call Super Save
    SUPER->HANDLE_SAVE( ).

***----------------------------------------------------------------------------
    if ME->F_DELETED eq ABAP_FALSE.

***   Save to DB
      call function 'ZVCXI_XCA_MWB_DB_SAVE_REGMDL'
        in update task
        exporting
          IF_GREGE = ME->F_GREGE
          IS_CLQRY = ME->S_CLQRY
          IS_CLDUI = ME->S_CLDUI
          IS_CLMAT = ME->S_CLMAT
          IS_RPSEL = ME->S_RPSEL.

***----------------------------------------------------------------------------
    else.
***   Delete from DB
      call function 'ZVCXI_XCA_MWB_DB_DEL_REGMDL'
        in update task
        exporting
          IF_GREGE = ME->F_GREGE.
    endif.

  endmethod.
  method LOAD_FROM_DB.

    data: LS_TC100 type ZVCXI_XCA_TC100.


***------------------------------------------------------------------------------------------------
*** Load Workbench Model
    select single *
           into LS_TC100
           from ZVCXI_XCA_TC100
          where GREGE eq ME->F_GREGE.
    check SY-SUBRC eq 0.

*** Load Class and Description
    ME->S_CLQRY-CLQRY       = LS_TC100-CLQRY.
    ME->S_CLQRY-CLQRY_DESCR = /VCXI/CL_CKXB_SERVICE=>GET_CLS_DESCR( IF_CLSNAME = ME->S_CLQRY-CLQRY ).

    ME->S_CLDUI-CLDUI       = LS_TC100-CLDUI.
    ME->S_CLDUI-CLDUI_DESCR = /VCXI/CL_CKXB_SERVICE=>GET_CLS_DESCR( IF_CLSNAME = ME->S_CLDUI-CLDUI ).

    ME->S_CLMAT-CLMAT       = LS_TC100-CLMAT.
    ME->S_CLMAT-CLMAT_DESCR = /VCXI/CL_CKXB_SERVICE=>GET_CLS_DESCR( IF_CLSNAME = ME->S_CLMAT-CLMAT ).

    ME->S_RPSEL-RPSEL       = LS_TC100-RPSEL.
    ME->S_RPSEL-RPSEL_DESCR = /VCXI/CL_CKXB_SERVICE=>GET_REP_DESCR( ME->S_RPSEL-RPSEL ).

  endmethod.
  method SET_CLDUI.

***---------------------------------------------------------------------------------------------------------------
*** Check of the Lock-Status of the Object
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

***---------------------------------------------------------------------------------------------------------------
    check IF_CLDUI ne ME->S_CLDUI-CLDUI.

*** Set new Class
    ME->S_CLDUI-CLDUI       = IF_CLDUI.
    ME->S_CLDUI-CLDUI_DESCR = /VCXI/CL_CKXB_SERVICE=>GET_CLS_DESCR( IF_CLSNAME = ME->S_CLDUI-CLDUI ).

***---------------------------------------------------------------------------------------------------------------
*** Raise Changed Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method SET_CLMAT.

***---------------------------------------------------------------------------------------------------------------
*** Check of the Lock-Status of the Object
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

***---------------------------------------------------------------------------------------------------------------
    check IF_CLMAT ne ME->S_CLMAT-CLMAT.

*** Set new Class
    ME->S_CLMAT-CLMAT       = IF_CLMAT.
    ME->S_CLMAT-CLMAT_DESCR = /VCXI/CL_CKXB_SERVICE=>GET_CLS_DESCR( IF_CLSNAME = ME->S_CLMAT-CLMAT ).

***---------------------------------------------------------------------------------------------------------------
*** Raise Changed Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method SET_CLQRY.

***---------------------------------------------------------------------------------------------------------------
*** Check of the Lock-Status of the Object
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

***---------------------------------------------------------------------------------------------------------------
    check IF_CLQRY ne ME->S_CLQRY-CLQRY.

*** Set new Class
    ME->S_CLQRY-CLQRY       = IF_CLQRY.
    ME->S_CLQRY-CLQRY_DESCR = /VCXI/CL_CKXB_SERVICE=>GET_CLS_DESCR( IF_CLSNAME = ME->S_CLQRY-CLQRY ).

***---------------------------------------------------------------------------------------------------------------
*** Raise Changed Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method SET_RPSEL .

***---------------------------------------------------------------------------------------------------------------
*** Check of the Lock-Status of the Object
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

***---------------------------------------------------------------------------------------------------------------
    check IF_RPSEL ne ME->S_RPSEL-RPSEL.

*** Set new Report Sel.Screen
    ME->S_RPSEL-RPSEL       = IF_RPSEL.
    ME->S_RPSEL-RPSEL_DESCR = /VCXI/CL_CKXB_SERVICE=>GET_REP_DESCR( ME->S_RPSEL-RPSEL ).

***---------------------------------------------------------------------------------------------------------------
*** Raise Changed Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
