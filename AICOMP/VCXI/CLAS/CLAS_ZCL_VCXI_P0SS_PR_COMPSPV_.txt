
class ZCL_VCXI_P0SS_PR_COMPSPV definition
  public
  inheriting from /VCXI/CL_VKSR_CLSPR
  create public .

public section.

  methods COPY_COMP_BY_SVC
    importing
      !IR_SVC type ref to /VCXI/CL_VKSRA_SVC
    raising
      /VCXI/CX_CKX .
protected section.

  constants C_PARID_COMPR type /VCXI/VKS_PARID value 'ZVCXI_P0_COMPR' ##NO_TEXT.
  constants C_IEVNT_O_COMPR_CHANGED type /VCXI/VKSC_IEVNT value 'COMPR_CHANGED' ##NO_TEXT.
private section.
endclass. "ZCL_VCXI_P0SS_PR_COMPSPV definition
class ZCL_VCXI_P0SS_PR_COMPSPV implementation.
  method COPY_COMP_BY_SVC.

    data: LS_PARAM type /VCXI/VKSR_S_PARAM.
    data: LT_SVCEL type /VCXI/CKX_TT_OBJECT,
          LT_SPVRE type /VCXI/VKSR_TT_SPVRE.
    data: LR_SPVRE        type ref to /VCXI/CL_VKSR_SPVRE,
          LR_SPVCE        type ref to /VCXI/CL_VKSC_SPVCE,
          LR_SPVRE_PARENT type ref to /VCXI/CL_VKSR_SPVRE,
          LR_OBJECT       type ref to /VCXI/IF_CKX_OBJECT.


***----------------------------------------------------------------------------
*** Get Component SE and Component FG Elements and sort them
    LT_SVCEL = IR_SVC->GET_SVC_ELEMENTS( IF_ATTID  = 'MQ_COMP'
                                         IF_ATTVA  = 'X'
                                         IF_INUSE  = ABAP_TRUE
                                         IF_SORTED = ABAP_TRUE ).

*** Get current Supervised Runtime Elements
    LT_SPVRE = ME->R_GATE->GET_SPVRE( ).

***----------------------------------------------------------------------------
*** Delete old Components and keep parent
    delete LT_SPVRE where TABLE_LINE->R_PARENT is not initial.
    check LT_SPVRE is not initial.
    LR_SPVRE_PARENT ?= LT_SPVRE[ 1 ].

*** Find Customizing Element for Component
    loop at ME->R_GATE->GET_SPVCE( ) into LR_OBJECT.
      if LR_OBJECT->GET_ID( ) eq 'COMP_MQ'.
        LR_SPVCE ?= LR_OBJECT.
        exit.
      endif.
    endloop.

*** Add new Components
    loop at LT_SVCEL into LR_OBJECT.
      create object LR_SPVRE
        exporting
          IR_SPVCE  = LR_SPVCE
          IR_PARENT = LR_SPVRE_PARENT.
      LR_SPVRE->SET_INUSE( IF_INUSE = ABAP_TRUE ).
      append LR_SPVRE to LT_SPVRE.

***   Copy Component Ratio
      LS_PARAM = cast /VCXI/CL_VKSRA_SVC_ELEMENT( LR_OBJECT )->GET_PARAM( IF_PARID = ZCL_VCXI_P0SS_PR_COMP_MQ=>C_PARID_COMPR ).
      LR_SPVRE->SET_PARAM( IS_PARAM = LS_PARAM ).
    endloop.

    ME->R_GATE->SET_SPVRE( IT_SPVRE = LT_SPVRE ).

  endmethod.
endclass. "ZCL_VCXI_P0SS_PR_COMPSPV implementation
