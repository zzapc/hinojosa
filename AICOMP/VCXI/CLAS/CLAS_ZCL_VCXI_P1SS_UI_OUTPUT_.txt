
class ZCL_VCXI_P1SS_UI_OUTPUT definition
  public
  inheriting from ZCL_VCXI_XCS_UI_TAB
  create public .

public section.

  data S_OUTPUT_VIS type ZVCXI_P1SS_S_OUTPUT_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABPT type ZVCXI_XCS_TABPT default '0100S_OUT_TAB-TAB'
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  constants C_UC_CPROP type SYUCOMM value 'OUTPUT_CPROP' ##NO_TEXT.
  constants C_UC_SCSTR type SYUCOMM value 'OUTPUT_SCSTR' ##NO_TEXT.
  data R_PR_OUTPUT type ref to ZCL_VCXI_P1SS_PR_OUTPUT .
  data R_TOOLBAR type ref to ZCL_VCXI_XCS_TOOLBAR_PRVDR .

  methods HANDLE_UC_CPROP
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_SCSTR
    raising
      /VCXI/CX_CKX .
  methods INIT_VIS
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
private section.
endclass. "ZCL_VCXI_P1SS_UI_OUTPUT definition
class ZCL_VCXI_P1SS_UI_OUTPUT implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_P1SS_UI_OUTPUT_BIND'
      exporting
        IR_UI_OUTPUT  = ME
      importing
        ES_DYNP       = ME->S_DYNP
        EF_ACTIVE_TAB = ME->F_ACTIVE_TAB.


*** Create Toolbar for Provider
    create object ME->R_TOOLBAR
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_TOOLBAR'.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE
                        IF_TABPT  = IF_TABPT ).

    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_OUTPUT.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

*** Free Toolbar
    if ME->R_TOOLBAR is bound.
      ME->R_TOOLBAR->DO_FREE( ).
      clear ME->R_TOOLBAR.
    endif.

  endmethod.
  method DO_MODIFY_SCREEN.

    try.
        SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                 IF_DYNNR = IF_DYNNR ).

        loop at screen.

***       Reopen Info Buttons
          if SCREEN-GROUP1 eq 'INF'.
            move 1 to SCREEN-INPUT.
          endif.

          modify screen.
        endloop.

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_PBO.

    data: LT_PRVDR_SELECTED type /VCXI/VKSR_TT_GATE.

    SUPER->DO_PBO( ).

*** Initialize Visualization Structure
    ME->INIT_VIS( ).

***--------------------------------------------------------------------------------------
*** Initialize Toolbar and provide active tab as Selected Provider (for inserting/deletion)
    append ME->R_OLD_GATE to LT_PRVDR_SELECTED.
    ME->R_TOOLBAR->REFRESH_TOOLBAR( IT_PRVDR_SELECTED = LT_PRVDR_SELECTED ).

  endmethod.
  method DO_USER_COMMAND.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    case IF_UCOMM.
      when C_UC_CPROP.
        ME->HANDLE_UC_CPROP( ).
      when C_UC_SCSTR.
        ME->HANDLE_UC_SCSTR( ).

    endcase.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_OUTPUT.

  endmethod.
  method HANDLE_UC_CPROP.

    data: LS_SURWT type ZVCXI_PXS_S_SURWT.
    data: LR_BL_CRBPROP type ref to ZCL_VCXI_P1SS_BL_CRBPROP,
          LR_BL_CRFPROP type ref to ZCL_VCXI_P1SS_BL_CRFPROP.


*** Get Corrugator Property (Outbound)
    LR_BL_CRBPROP = ME->R_PR_OUTPUT->ZIF_VCXI_P1SS_COMM_PREV~GET_CRBPX( ).
    LR_BL_CRFPROP = ME->R_PR_OUTPUT->ZIF_VCXI_P1SS_COMM_PREV~GET_CRFPX( ).

*** Get Surface Weight
    LS_SURWT = ME->R_PR_OUTPUT->ZIF_VCXI_P1SS_COMM_PREV~GET_SURWT( ).

*** Start Popup
    ZCL_VCXI_P1SS_POPUP_CRPROP=>START( IR_BL_CRBPROP = LR_BL_CRBPROP
                                       IR_BL_CRFPROP = LR_BL_CRFPROP
                                       IF_WERKS      = ME->R_PR_OUTPUT->GET_WERKS( )
                                       IS_SURWT      = LS_SURWT ).

  endmethod.
  method HANDLE_UC_SCSTR.

    data: LR_BL_CRBPROP type ref to ZCL_VCXI_P1SS_BL_CRBPROP.

*** Get Score String (Outbound)
    LR_BL_CRBPROP = ME->R_PR_OUTPUT->ZIF_VCXI_P1SS_COMM_PREV~GET_CRBPX( ).
    check LR_BL_CRBPROP is bound.

*** Start Popup
    ZCL_VCXI_PXSS_POPUP_SCSTR=>START( IT_SCORE_COM = LR_BL_CRBPROP->T_SCORE
                                      IT_TAPE_COM  = LR_BL_CRBPROP->T_TAPE_COM ).

  endmethod.
  method INIT_VIS.

    data: LR_BL_CRFPROP type ref to ZCL_VCXI_P1SS_BL_CRFPROP.

    free: ME->S_OUTPUT_VIS.
    check ME->R_PR_OUTPUT is bound.

***--------------------------------------------------------------------------------------
*** Get Sheet Dimension (Inbound)
    ME->R_PR_OUTPUT->ZIF_VCXI_P1SS_COMM_PREV~GET_SHTDIM( importing ES_SHTLE = ME->S_OUTPUT_VIS-S_SHTLE
                                                                   ES_SHTWI = ME->S_OUTPUT_VIS-S_SHTWI ).

*** Number Ups (Inbound)
    ME->S_OUTPUT_VIS-NOUPS = ME->R_PR_OUTPUT->ZIF_VCXI_P1SS_COMM_PREV~GET_NOUPS( ).

*** Get Flute & Caliper (Inbound)
    LR_BL_CRFPROP = ME->R_PR_OUTPUT->ZIF_VCXI_P1SS_COMM_PREV~GET_CRFPX( ).
    if LR_BL_CRFPROP is bound.
      move LR_BL_CRFPROP->F_FLUTE to ME->S_OUTPUT_VIS-FLUTE.
      move LR_BL_CRFPROP->S_CALIP to ME->S_OUTPUT_VIS-S_CALIP.
    endif.

  endmethod.
