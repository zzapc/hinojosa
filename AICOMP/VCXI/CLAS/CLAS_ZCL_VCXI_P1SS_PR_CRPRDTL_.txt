
class ZCL_VCXI_P1SS_PR_CRPRDTL definition
  public
  inheriting from /VCXI/CL_VKSR_CLSPR
  create public .

public section.

  interfaces ZIF_VCXI_P4SS_PRDTL .
  interfaces ZIF_VCXI_PXSS_PRINT_COM .
  interfaces ZIF_VCXI_PXSS_FDI .

  constants C_IEVNT_O_ADDWT_CHANGED type /VCXI/VKSC_IEVNT value 'ADDWT_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_BOARD_CHANGED type /VCXI/VKSC_IEVNT value 'BOARD_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_BOXPA_CHANGED type /VCXI/VKSC_IEVNT value 'BOXPA_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_CALIP_CHANGED type /VCXI/VKSC_IEVNT value 'CALIP_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_COUAR_CHANGED type /VCXI/VKSC_IEVNT value 'COUAR_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_FDUST_CHANGED type /VCXI/VKSC_IEVNT value 'FDUST_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_FEFCO_CHANGED type /VCXI/VKSC_IEVNT value 'FEFCO_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_FGAP_CHANGED type /VCXI/VKSC_IEVNT value 'FGAP_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_FLUTE_CHANGED type /VCXI/VKSC_IEVNT value 'FLUTE_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_FLUTO_CHANGED type /VCXI/VKSC_IEVNT value 'FLUTO_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_FTUCK_CHANGED type /VCXI/VKSC_IEVNT value 'FTUCK_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_JEXT_CHANGED type /VCXI/VKSC_IEVNT value 'JEXT_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_JOIPO_CHANGED type /VCXI/VKSC_IEVNT value 'JOIPO_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_JOIWI_CHANGED type /VCXI/VKSC_IEVNT value 'JOIWI_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_OVLAP_CHANGED type /VCXI/VKSC_IEVNT value 'OVLAP_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_PROH2_CHANGED type /VCXI/VKSC_IEVNT value 'PROH2_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_PROHE_CHANGED type /VCXI/VKSC_IEVNT value 'PROHE_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_PROLE_CHANGED type /VCXI/VKSC_IEVNT value 'PROLE_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_PROWI_CHANGED type /VCXI/VKSC_IEVNT value 'PROWI_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_SHTLE_CHANGED type /VCXI/VKSC_IEVNT value 'SHTLE_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_SHTPL_CHANGED type /VCXI/VKSC_IEVNT value 'SHTPL_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_SHTPW_CHANGED type /VCXI/VKSC_IEVNT value 'SHTPW_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_SHTWI_CHANGED type /VCXI/VKSC_IEVNT value 'SHTWI_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_SURWT_CHANGED type /VCXI/VKSC_IEVNT value 'SURWT_CHANGED' ##NO_TEXT.
  constants C_PARID_ADDWT type /VCXI/VKS_PARID value 'ZVCXI_PX_ADDWT' ##NO_TEXT.
  constants C_PARID_BOXPA type /VCXI/VKS_PARID value 'ZVCXI_P0_BOXPA' ##NO_TEXT.
  constants C_PARID_CALIP type /VCXI/VKS_PARID value 'ZVCXI_P0_CALIP' ##NO_TEXT.
  constants C_PARID_COUAR type /VCXI/VKS_PARID value 'ZVCXI_PX_COUAR' ##NO_TEXT.
  constants C_PARID_FDUST type /VCXI/VKS_PARID value 'ZVCXI_PC_FDUST' ##NO_TEXT.
  constants C_PARID_FEFCO type /VCXI/VKS_PARID value 'ZVCXI_P0_PRSTY' ##NO_TEXT.
  constants C_PARID_FGAPB type /VCXI/VKS_PARID value 'ZVCXI_P0_FGAPB' ##NO_TEXT.
  constants C_PARID_FGAPT type /VCXI/VKS_PARID value 'ZVCXI_P0_FGAPT' ##NO_TEXT.
  constants C_PARID_FLUTE type /VCXI/VKS_PARID value 'ZVCXI_PC_FLUTE' ##NO_TEXT.
  constants C_PARID_FLUTO type /VCXI/VKS_PARID value 'ZVCXI_PC_FLUTO' ##NO_TEXT.
  constants C_PARID_FTUCK type /VCXI/VKS_PARID value 'ZVCXI_PC_FTUCK' ##NO_TEXT.
  constants C_PARID_JEXTB type /VCXI/VKS_PARID value 'ZVCXI_P0_JEXTB' ##NO_TEXT.
  constants C_PARID_JEXTT type /VCXI/VKS_PARID value 'ZVCXI_P0_JEXTT' ##NO_TEXT.
  constants C_PARID_JOILE type /VCXI/VKS_PARID value 'ZVCXI_P0_JOILE' ##NO_TEXT.
  constants C_PARID_JOIPO type /VCXI/VKS_PARID value 'ZVCXI_P0_JOIPO' ##NO_TEXT.
  constants C_PARID_JOITY type /VCXI/VKS_PARID value 'ZVCXI_P0_JOITY' ##NO_TEXT.
  constants C_PARID_JOIWI type /VCXI/VKS_PARID value 'ZVCXI_P0_JOIWI' ##NO_TEXT.
  constants C_PARID_OVLAP type /VCXI/VKS_PARID value 'ZVCXI_P0_OVLAP' ##NO_TEXT.
  constants C_PARID_PRINT type /VCXI/VKS_PARID value 'ZVCXI_PX_PRINT' ##NO_TEXT.
  constants C_PARID_PROH2 type /VCXI/VKS_PARID value 'ZVCXI_PC_PROH2' ##NO_TEXT.
  constants C_PARID_PROHE type /VCXI/VKS_PARID value 'ZVCXI_P0_PROHE' ##NO_TEXT.
  constants C_PARID_PROLE type /VCXI/VKS_PARID value 'ZVCXI_P0_PROLE' ##NO_TEXT.
  constants C_PARID_PROWI type /VCXI/VKS_PARID value 'ZVCXI_P0_PROWI' ##NO_TEXT.
  constants C_PARID_SHTPL type /VCXI/VKS_PARID value 'ZVCXI_P0_SHTPL' ##NO_TEXT.
  constants C_PARID_SHTPW type /VCXI/VKS_PARID value 'ZVCXI_P0_SHTPW' ##NO_TEXT.
  constants C_PARID_SURWT type /VCXI/VKS_PARID value 'ZVCXI_PX_SURWT' ##NO_TEXT.
  constants C_TPRST_FEFCO type ZVCXI_P0SS_TPRST value 'FEFCO' ##NO_TEXT.

  methods GET_ADDWT
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_ADDWT) type ZVCXI_PXS_S_ADDWT
    raising
      /VCXI/CX_CKX .
  methods CHECK_CUST_DESIGN
    returning
      value(RF_FCUSD) type ZVCXI_P0SS_FCUSD
    raising
      /VCXI/CX_CKX .
  methods GET_COUAR
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_COUAR) type ZVCXI_PXS_S_COUAR
    raising
      /VCXI/CX_CKX .
  methods GET_BOXPA
    returning
      value(RF_BOXPA) type ZVCXI_P0SS_BOXPA
    raising
      /VCXI/CX_CKX .
  methods GET_JOITY
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
      !EF_DESCR type /VCXI/CKX_DESCR
    returning
      value(RF_JOITY) type ZVCXI_P0S_JOITY
    raising
      /VCXI/CX_CKX .
  methods GET_CALIP
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_CALIP) type ZVCXI_P0SS_S_CALIP
    raising
      /VCXI/CX_CKX .
  methods GET_BOARD
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RF_BOARD) type ZVCXI_PCS_BOARD
    raising
      /VCXI/CX_CKX .
  methods GET_FDUST
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_FDUST) type ZVCXI_PCSS_S_FDUST
    raising
      /VCXI/CX_CKX .
  methods GET_FEFCO
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
      !EF_DESCR type /VCXI/CKX_DESCR
      !ER_CUST_PRSTY type ref to ZCL_VCXI_PXSS_CUST_PRSTY
    returning
      value(RF_FEFCO) type ZVCXI_PCS_FEFCO
    raising
      /VCXI/CX_CKX .
  methods GET_FLUTO
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RF_FLUTO) type ZVCXI_PCSS_FLUTO
    raising
      /VCXI/CX_CKX .
  methods SET_ADDWT
    importing
      !IS_ADDWT type ZVCXI_PXS_S_ADDWT
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods LOAD_BOARD
    importing
      !IF_BOARD type ZVCXI_PCS_BOARD
      !IF_MBOAR type ZVCXI_PCSS_MBOAR
      !IF_WERKS type WERKS_D
    raising
      /VCXI/CX_CKX .
  methods GET_FTUCK
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_FTUCK) type ZVCXI_PCSS_S_FTUCK
    raising
      /VCXI/CX_CKX .
  methods SET_COUAR
    importing
      !IS_COUAR type ZVCXI_PXS_S_COUAR
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_JOILE
    importing
      !IS_JOILE type ZVCXI_P0S_S_JOILE optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
      !IF_FOVRL type ZVCXI_P0SS_FOVRL optional
    raising
      /VCXI/CX_CKX .
  methods GET_JOILE
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_JOILE) type ZVCXI_P0S_S_JOILE
    raising
      /VCXI/CX_CKX .
  methods SET_CALIP
    importing
      !IS_CALIP type ZVCXI_P0SS_S_CALIP
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods GET_FGAPB
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_FGAPB) type ZVCXI_P0SS_S_FGAPB
    raising
      /VCXI/CX_CKX .
  methods SET_JOITY
    importing
      !IF_JOITY type ZVCXI_P0S_JOITY optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods GET_FGAPT
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_FGAPT) type ZVCXI_P0SS_S_FGAPT
    raising
      /VCXI/CX_CKX .
  methods GET_JOIPO
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RF_JOIPO) type ZVCXI_P0S_JOIPO
    raising
      /VCXI/CX_CKX .
  methods SET_FLUTE
    importing
      !IF_FLUTE type ZVCXI_PCS_FLUTE
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods GET_JEXTB
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_JEXTB) type ZVCXI_P0SS_S_JEXTB
    raising
      /VCXI/CX_CKX .
  methods GET_JEXTT
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_JEXTT) type ZVCXI_P0SS_S_JEXTT
    raising
      /VCXI/CX_CKX .
  methods GET_FLUTE
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
      !EF_DESCR type /VCXI/CKX_DESCR
    returning
      value(RF_FLUTE) type ZVCXI_PCS_FLUTE
    raising
      /VCXI/CX_CKX .
  methods GET_JOIWI
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_JOIWI) type ZVCXI_P0S_S_JOIWI
    raising
      /VCXI/CX_CKX .
  methods GET_PROH2
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_PROH2) type ZVCXI_PCSS_S_PROH2
    raising
      /VCXI/CX_CKX .
  methods GET_PROHE
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_PROHE) type ZVCXI_P0S_S_PROHE
    raising
      /VCXI/CX_CKX .
  methods GET_PROLE
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_PROLE) type ZVCXI_P0S_S_PROLE
    raising
      /VCXI/CX_CKX .
  methods GET_PROWI
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_PROWI) type ZVCXI_P0S_S_PROWI
    raising
      /VCXI/CX_CKX .
  methods GET_PRSTY_ELEMENTS
    returning
      value(RT_ELEMENT) type ZVCXI_P0SS_TT_ELEMENT
    raising
      /VCXI/CX_CKX .
  methods GET_SHTLE
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_SHTLE) type ZVCXI_P0SS_S_SHTLE
    raising
      /VCXI/CX_CKX .
  methods GET_PRODIM_DESCR
    returning
      value(RF_DESCR) type /VCXI/CKX_DESCR
    raising
      /VCXI/CX_CKX .
  methods GET_SHTPL
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_SHTPL) type ZVCXI_P0SS_S_SHTPL
    raising
      /VCXI/CX_CKX .
  methods GET_OVLAP
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_OVLAP) type ZVCXI_P0S_S_OVLAP
    raising
      /VCXI/CX_CKX .
  methods GET_SHTPW
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_SHTPW) type ZVCXI_P0SS_S_SHTPW
    raising
      /VCXI/CX_CKX .
  methods SET_BOARD
    importing
      !IF_BOARD type ZVCXI_PCS_BOARD
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods GET_SHTWI
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_SHTWI) type ZVCXI_P0SS_S_SHTWI
    raising
      /VCXI/CX_CKX .
  methods GET_SURWT
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_SURWT) type ZVCXI_PXS_S_SURWT
    raising
      /VCXI/CX_CKX .
  methods SET_BOXPA
    importing
      !IF_BOXPA type ZVCXI_P0SS_BOXPA optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_FDUST
    importing
      !IS_FDUST type ZVCXI_PCSS_S_FDUST optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_FEFCO
    importing
      !IF_FEFCO type ZVCXI_PCS_FEFCO
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_FLUTO
    importing
      !IF_FLUTO type ZVCXI_PCSS_FLUTO
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_FTUCK
    importing
      !IS_FTUCK type ZVCXI_PCSS_S_FTUCK optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_JOIPO
    importing
      !IF_JOIPO type ZVCXI_P0S_JOIPO
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_JOIWI
    importing
      !IS_JOIWI type ZVCXI_P0S_S_JOIWI optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_JEXTB
    importing
      !IS_JEXTB type ZVCXI_P0SS_S_JEXTB optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_JEXTT
    importing
      !IS_JEXTT type ZVCXI_P0SS_S_JEXTT optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_PROH2
    importing
      !IS_PROH2 type ZVCXI_PCSS_S_PROH2 optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_PROHE
    importing
      !IS_PROHE type ZVCXI_P0S_S_PROHE optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_PROLE
    importing
      !IS_PROLE type ZVCXI_P0S_S_PROLE
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_PROWI
    importing
      !IS_PROWI type ZVCXI_P0S_S_PROWI
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_SHTLE
    importing
      !IS_SHTLE type ZVCXI_P0SS_S_SHTLE
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_SHTPL
    importing
      !IS_SHTPL type ZVCXI_P0SS_S_SHTPL
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_SHTPW
    importing
      !IS_SHTPW type ZVCXI_P0SS_S_SHTPW
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_SHTWI
    importing
      !IS_SHTWI type ZVCXI_P0SS_S_SHTWI
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_SURWT
    importing
      !IS_SURWT type ZVCXI_PXS_S_SURWT
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_FGAPB
    importing
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
      !IS_FGAPB type ZVCXI_P0SS_S_FGAPB optional
    raising
      /VCXI/CX_CKX .
  methods SET_FGAPT
    importing
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
      !IS_FGAPT type ZVCXI_P0SS_S_FGAPT optional
    raising
      /VCXI/CX_CKX .
  methods SET_OVLAP
    importing
      !IS_OVLAP type ZVCXI_P0S_S_OVLAP optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .

  methods GET_STATUS
    redefinition .
  methods GET_STATUS_MSG
    redefinition .
  methods HANDLE_EVENT
    redefinition .
protected section.

  constants C_IEVNT_I_INIT_PRODIM type /VCXI/VKSC_IEVNT value 'INIT_PRODIM' ##NO_TEXT.
  constants C_IEVNT_I_RECALC_AREA type /VCXI/VKSC_IEVNT value 'RECALC_AREA' ##NO_TEXT.
  constants C_IEVNT_I_RECALC_HNDDIM type /VCXI/VKSC_IEVNT value 'RECALC_HNDDIM' ##NO_TEXT.
  constants C_IEVNT_I_RECALC_JOIDIM type /VCXI/VKSC_IEVNT value 'RECALC_JOIDIM' ##NO_TEXT.
  constants C_IEVNT_I_RECALC_SHTDIM type /VCXI/VKSC_IEVNT value 'RECALC_SHTDIM' ##NO_TEXT.
  constants C_IEVNT_I_RECALC_WEIGHT type /VCXI/VKSC_IEVNT value 'RECALC_WEIGHT' ##NO_TEXT.
  constants C_IEVNT_I_SET_DEFAULT type /VCXI/VKSC_IEVNT value 'SET_DEFAULT' ##NO_TEXT.
  constants C_IEVNT_O_JOILE_CHANGED type /VCXI/VKSC_IEVNT value 'JOILE_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_JOITY_CHANGED type /VCXI/VKSC_IEVNT value 'JOITY_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_PRINT_CHANGED type /VCXI/VKSC_IEVNT value 'PRINT_CHANGED' ##NO_TEXT.
  constants C_PARID_SHTLE type /VCXI/VKS_PARID value 'ZVCXI_P0_SHTLE' ##NO_TEXT.
  constants C_PARID_SHTWI type /VCXI/VKS_PARID value 'ZVCXI_P0_SHTWI' ##NO_TEXT.

  methods CHECK_FEFCO
    importing
      !IR_MESSAGE type ref to /VCXI/CL_CKXM_MESSAGE optional
    returning
      value(RF_ELMST) type /VCXI/VKSR_ELMST
    raising
      /VCXI/CX_CKX .
  methods CHECK_JOIPO
    importing
      !IR_MESSAGE type ref to /VCXI/CL_CKXM_MESSAGE optional
    returning
      value(RF_ELMST) type /VCXI/VKSR_ELMST
    raising
      /VCXI/CX_CKX .
  methods HANDLE_SET_DEFAULT
    raising
      /VCXI/CX_CKX .
  methods CHECK_CALIP
    importing
      !IR_MESSAGE type ref to /VCXI/CL_CKXM_MESSAGE optional
    returning
      value(RF_ELMST) type /VCXI/VKSR_ELMST
    raising
      /VCXI/CX_CKX .
  methods CHECK_FLUTE
    importing
      !IR_MESSAGE type ref to /VCXI/CL_CKXM_MESSAGE optional
    returning
      value(RF_ELMST) type /VCXI/VKSR_ELMST
    raising
      /VCXI/CX_CKX .
  methods CHECK_SURWT
    importing
      !IR_MESSAGE type ref to /VCXI/CL_CKXM_MESSAGE optional
    returning
      value(RF_ELMST) type /VCXI/VKSR_ELMST
    raising
      /VCXI/CX_CKX .
  methods CHECK_PRODIM
    importing
      !IR_MESSAGE type ref to /VCXI/CL_CKXM_MESSAGE optional
    returning
      value(RF_ELMST) type /VCXI/VKSR_ELMST
    raising
      /VCXI/CX_CKX .
  methods GET_FOLDIM
    returning
      value(RS_FOLDIM) type ZVCXI_PXS_S_FOLDIM
    raising
      /VCXI/CX_CKX .
  methods GET_PRVDR_SCORE_MGR
    exporting
      !ER_GATE_SCORE_MGR type ref to /VCXI/CL_VKSR_GATE
    returning
      value(RR_PR_SCORE_MGR) type ref to ZCL_VCXI_PXSS_PR_SCORE_MGR
    raising
      /VCXI/CX_CKX .
  methods HANDLE_INIT_PRODIM
    raising
      /VCXI/CX_CKX .
  methods HANDLE_RECALC_JOIDIM
    raising
      /VCXI/CX_CKX .
  methods HANDLE_RECALC_SHTDIM
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_P1SS_PR_CRPRDTL definition
class ZCL_VCXI_P1SS_PR_CRPRDTL implementation.
  method CHECK_CALIP.

*** Check Caliper
    if ME->GET_CALIP( )-CALIP is not initial.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G.
      if IR_MESSAGE is bound.
***     Caliper is maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P1SS'
                                 IF_MSGTY = 'S'
                                 IF_MSGNO = '125' ).
      endif.

    else.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
      if IR_MESSAGE is bound.
***     Caliper is not maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P1SS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '126' ).
      endif.
    endif.

  endmethod.
  method CHECK_CUST_DESIGN.

    data: LR_CUST_PRSTY  type ref to ZCL_VCXI_PXSS_CUST_PRSTY.


    ME->GET_FEFCO( importing ER_CUST_PRSTY = LR_CUST_PRSTY ).
    check LR_CUST_PRSTY is bound.
    RF_FCUSD = LR_CUST_PRSTY->GET_FCUSD( ).

  endmethod.
  method CHECK_FEFCO.

    data: LF_FEFCO type ZVCXI_PCS_FEFCO.


    LF_FEFCO = ME->GET_FEFCO( ).
    if LF_FEFCO is initial.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
      if IR_MESSAGE is bound.
***     FEFCO is not maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P1SS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '103' ).
      endif.

    else.
***   Check Existance
      case ZCL_VCXI_PCSS_CUST_FEFCO=>CHECK_PRSTY_EXIST( IF_PRSTY = LF_FEFCO ).
        when ABAP_TRUE.
          RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G.
          if IR_MESSAGE is bound.
***         FEFCO is maintained and valid.
            IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P1SS'
                                     IF_MSGTY = 'S'
                                     IF_MSGNO = '101' ).
          endif.
        when others.
          RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
          if IR_MESSAGE is bound.
***         FEFCO is maintained but not valid.
            IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P1SS'
                                     IF_MSGTY = 'E'
                                     IF_MSGNO = '102' ).
          endif.
      endcase.
    endif.

  endmethod.
  method CHECK_FLUTE.

*** Check Flute
    if ME->GET_FLUTE( ) is not initial.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G.
      if IR_MESSAGE is bound.
***     Flute is maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P1SS'
                                 IF_MSGTY = 'S'
                                 IF_MSGNO = '123' ).
      endif.

    else.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
      if IR_MESSAGE is bound.
***     Flute is not maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P1SS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '124' ).
      endif.
    endif.

  endmethod.
  method CHECK_JOIPO.

    data: LR_CUST_PRSTY  type ref to ZCL_VCXI_PXSS_CUST_PRSTY.


    ME->GET_FEFCO( importing ER_CUST_PRSTY = LR_CUST_PRSTY ).
    check LR_CUST_PRSTY is bound.
    RF_ELMST = switch #( LR_CUST_PRSTY->CHECK_JOIPO( IF_JOIPO   = ME->GET_JOIPO( )
                                                     IR_MESSAGE = IR_MESSAGE ) when ABAP_TRUE then /VCXI/CL_VKSR_SERVICE=>C_ELMST_G
                                                                                              else /VCXI/CL_VKSR_SERVICE=>C_ELMST_R ).

  endmethod.
  method CHECK_PRODIM.

    data: LF_COMPLETE type ABAP_BOOL.
    data: LR_CUST_PRSTY type ref to ZCL_VCXI_PXSS_CUST_PRSTY.


*** Get Product Style Customizing and Elements
    ME->GET_FEFCO( importing ER_CUST_PRSTY = LR_CUST_PRSTY ).
    check LR_CUST_PRSTY is bound.

***--------------------------------------------------------------------------------------
*** Check Element Values
    LF_COMPLETE = LR_CUST_PRSTY->CHECK_ELEMV( IT_ELEMENT = ME->GET_PRSTY_ELEMENTS( )
                                              IF_BOXPA   = ME->GET_BOXPA( )
                                              IR_MESSAGE = IR_MESSAGE ).

    RF_ELMST = switch #( LF_COMPLETE when ABAP_TRUE then /VCXI/CL_VKSR_SERVICE=>C_ELMST_G
                                                    else /VCXI/CL_VKSR_SERVICE=>C_ELMST_R ).

  endmethod.
  method CHECK_SURWT.

*** Check Surface Weight
    if ME->GET_SURWT( )-SURWT is not initial.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G.
      if IR_MESSAGE is bound.
***     Surface Weight is maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P1SS'
                                 IF_MSGTY = 'S'
                                 IF_MSGNO = '127' ).
      endif.

    else.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
      if IR_MESSAGE is bound.
***     Surface Weight is not maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P1SS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '128' ).
      endif.
    endif.

  endmethod.
  method GET_ADDWT.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_ADDWT ).
    move LS_PARAM-ATFLV to RS_ADDWT-ADDWT.
    move LS_PARAM-UNIT  to RS_ADDWT-ADDWT_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_BOARD.

    data:  LS_LEOKY  type  /VCXI/VKSR_S_LEOKY.

    LS_LEOKY = ME->R_GATE->GET_LEOKY( ).

    RF_BOARD = LS_LEOKY-LEOKY.
    EF_SETBY = LS_LEOKY-SETBY.

  endmethod.
  method GET_BOXPA.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_BOXPA ).
    move LS_PARAM-ATWRT to RF_BOXPA.

  endmethod.
  method GET_CALIP.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_CALIP ).
    move LS_PARAM-ATFLV to RS_CALIP-CALIP.
    move LS_PARAM-UNIT  to RS_CALIP-CALIP_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_COUAR.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_COUAR ).
    move LS_PARAM-ATFLV to RS_COUAR-COUAR.
    move LS_PARAM-UNIT  to RS_COUAR-COUAR_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_FDUST.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_FDUST ).
    move LS_PARAM-ATFLV to RS_FDUST-FDUST.
    move LS_PARAM-UNIT  to RS_FDUST-FDUST_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_FEFCO.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

***----------------------------------------------------------------------------
*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_FEFCO ).
    move LS_PARAM-ATWRT to RF_FEFCO.
    move LS_PARAM-SETBY to EF_SETBY.

    check RF_FEFCO is not initial.

***----------------------------------------------------------------------------
    if EF_DESCR is requested.
***   Identify Description
      EF_DESCR = ZCL_VCXI_P0SS_CUST_PRSTY=>GET_PRSTY_DESCR( IF_TPRST = ZCL_VCXI_P1SS_PR_CFG=>C_TPRST_FEFCO
                                                            IF_PRSTY = RF_FEFCO ).
    endif.

    if ER_CUST_PRSTY is requested.
***   Customizing Instance for Product Style
      try.
          ER_CUST_PRSTY = ZCL_VCXI_PXSS_CUST_PRSTY=>GET_INSTANCE( IF_TPRST = ZCL_VCXI_P1SS_PR_CFG=>C_TPRST_FEFCO
                                                                  IF_PRSTY = RF_FEFCO ).
        catch /VCXI/CX_CKX.
          clear ER_CUST_PRSTY.
      endtry.
    endif.

  endmethod.
  method GET_FGAPB.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_FGAPB ).
    RS_FGAPB-FGAPB     = LS_PARAM-ATFLV.
    RS_FGAPB-FGAPB_UOM = LS_PARAM-UNIT.
    EF_SETBY           = LS_PARAM-SETBY.

  endmethod.
  method GET_FGAPT.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_FGAPT ).
    RS_FGAPT-FGAPT     = LS_PARAM-ATFLV.
    RS_FGAPT-FGAPT_UOM = LS_PARAM-UNIT.
    EF_SETBY           = LS_PARAM-SETBY.

  endmethod.
  method GET_FLUTE.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


***--------------------------------------------------------------------------------------
*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_FLUTE ).
    RF_FLUTE = LS_PARAM-ATWRT.
    EF_SETBY = LS_PARAM-SETBY.

    check RF_FLUTE is not initial.

***--------------------------------------------------------------------------------------
    if EF_DESCR is requested.
***   Identify Description
      EF_DESCR = ZCL_VCXI_PCSS_CUST_BOARD=>GET_FLUTE_DESCR( IF_FLUTE = RF_FLUTE ).
    endif.

  endmethod.
  method GET_FLUTO.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_FLUTO ).
    move LS_PARAM-ATWRT to RF_FLUTO.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_FOLDIM.

    data: LR_CUST_PRSTY  type ref to ZCL_VCXI_PXSS_CUST_PRSTY.


*** Get FEFCO Customizing
    ME->GET_FEFCO( importing ER_CUST_PRSTY = LR_CUST_PRSTY ).
    check LR_CUST_PRSTY is bound.

*** Get Folding Dimensions
    RS_FOLDIM = LR_CUST_PRSTY->GET_FOLDIM( IF_ORIEN   = ME->GET_FLUTO( )
                                           IT_ELEMENT = ME->GET_PRSTY_ELEMENTS( ) ).

  endmethod.
  method GET_FTUCK.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_FTUCK ).
    move LS_PARAM-ATFLV to RS_FTUCK-FTUCK.
    move LS_PARAM-UNIT  to RS_FTUCK-FTUCK_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_JEXTB.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_JEXTB ).
    move LS_PARAM-ATFLV to RS_JEXTB-JEXTB.
    move LS_PARAM-UNIT  to RS_JEXTB-JEXTB_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_JEXTT.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_JEXTT ).
    move LS_PARAM-ATFLV to RS_JEXTT-JEXTT.
    move LS_PARAM-UNIT  to RS_JEXTT-JEXTT_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_JOILE.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_JOILE ).
    move LS_PARAM-ATFLV to RS_JOILE-JOILE.
    move LS_PARAM-UNIT  to RS_JOILE-JOILE_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_JOIPO.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_JOIPO ).
    move LS_PARAM-ATWRT to RF_JOIPO.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_JOITY.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

***----------------------------------------------------------------------------
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_JOITY ).
    move LS_PARAM-ATWRT to RF_JOITY.
    move LS_PARAM-SETBY to EF_SETBY.

***----------------------------------------------------------------------------
    if EF_DESCR is requested.
***   Identify Description
      EF_DESCR = ZCL_VCXI_P0SS_CUST_PRSTY=>GET_JOITY_DESCR( IF_JOITY = RF_JOITY ).
    endif.

  endmethod.
  method GET_JOIWI.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_JOIWI ).
    move LS_PARAM-ATFLV to RS_JOIWI-JOIWI.
    move LS_PARAM-UNIT  to RS_JOIWI-JOIWI_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_OVLAP.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_OVLAP ).
    move LS_PARAM-ATFLV to RS_OVLAP-OVLAP.
    move LS_PARAM-UNIT  to RS_OVLAP-OVLAP_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_PRODIM_DESCR.

    data: LF_BOXPA type ZVCXI_P0SS_BOXPA.
    data: LR_CUST_PRSTY  type ref to ZCL_VCXI_PXSS_CUST_PRSTY.


*** Get FEFCO Customizing and Part of Box
    ME->GET_FEFCO( importing ER_CUST_PRSTY = LR_CUST_PRSTY ).
    LF_BOXPA = ME->GET_BOXPA( ).

    if LR_CUST_PRSTY is bound.
***   Length
      if LR_CUST_PRSTY->IS_IELEM_USED( IF_IELEM = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_LENGTH
                                       IF_BOXPA = LF_BOXPA ).
        RF_DESCR = RF_DESCR && switch /VCXI/CKX_DESCR( RF_DESCR when '' then 'L' else '/L' ).
      endif.

***   Width
      if LR_CUST_PRSTY->IS_IELEM_USED( IF_IELEM = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_WIDTH
                                       IF_BOXPA = LF_BOXPA ).
        RF_DESCR = RF_DESCR && switch /VCXI/CKX_DESCR( RF_DESCR when '' then 'W' else '/W' ).
      endif.

***   Height
      if LR_CUST_PRSTY->IS_IELEM_USED( IF_IELEM = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_HEIGHT
                                       IF_BOXPA = LF_BOXPA ).
        RF_DESCR = RF_DESCR && switch /VCXI/CKX_DESCR( RF_DESCR when '' then 'H' else '/H' ).
      endif.

***   Second Height
      if LR_CUST_PRSTY->IS_IELEM_USED( IF_IELEM = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_HEIGHT2
                                       IF_BOXPA = LF_BOXPA ).
        RF_DESCR = RF_DESCR && switch /VCXI/CKX_DESCR( RF_DESCR when '' then 'h' else '/h' ).
      endif.
    else.
***   Fallback
      RF_DESCR = 'L/W/H'.
    endif.

*** Full Description String
    RF_DESCR = 'Product Dim. ('(T01) && RF_DESCR && ')'.

  endmethod.
  method GET_PROH2.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PROH2 ).
    move LS_PARAM-ATFLV to RS_PROH2-PROH2.
    move LS_PARAM-UNIT  to RS_PROH2-PROH2_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_PROHE.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PROHE ).
    move LS_PARAM-ATFLV to RS_PROHE-PROHE.
    move LS_PARAM-UNIT  to RS_PROHE-PROHE_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_PROLE.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PROLE ).
    move LS_PARAM-ATFLV to RS_PROLE-PROLE.
    move LS_PARAM-UNIT  to RS_PROLE-PROLE_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_PROWI.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PROWI ).
    move LS_PARAM-ATFLV to RS_PROWI-PROWI.
    move LS_PARAM-UNIT  to RS_PROWI-PROWI_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_PRSTY_ELEMENTS.

    data: LS_PROLE type ZVCXI_P0S_S_PROLE,
          LS_PROWI type ZVCXI_P0S_S_PROWI,
          LS_PROHE type ZVCXI_P0S_S_PROHE,
          LS_PROH2 type ZVCXI_PCSS_S_PROH2,
          LS_JOIWI type ZVCXI_P0S_S_JOIWI,
          LS_OVLAP type ZVCXI_P0S_S_OVLAP,
          LS_FDUST type ZVCXI_PCSS_S_FDUST,
          LS_FTUCK type ZVCXI_PCSS_S_FTUCK,
          LS_SHTPL type ZVCXI_P0SS_S_SHTPL,
          LS_SHTPW type ZVCXI_P0SS_S_SHTPW,
          LS_CALIP type ZVCXI_P0SS_S_CALIP,
          LS_FGAPT type ZVCXI_P0SS_S_FGAPT,
          LS_FGAPB type ZVCXI_P0SS_S_FGAPB.


***--------------------------------------------------------------------------------------
*** Product Length
    LS_PROLE = ME->GET_PROLE( ).
    if LS_PROLE-PROLE is not initial.
      append value #( IELEM     = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_LENGTH
                      ELEMV     = LS_PROLE-PROLE
                      ELEMV_UOM = LS_PROLE-PROLE_UOM
                    ) to RT_ELEMENT.
    endif.

*** Product Width
    LS_PROWI = ME->GET_PROWI( ).
    if LS_PROWI-PROWI is not initial.
      append value #( IELEM     = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_WIDTH
                      ELEMV     = LS_PROWI-PROWI
                      ELEMV_UOM = LS_PROWI-PROWI_UOM
                    ) to RT_ELEMENT.
    endif.

*** Product Height
    LS_PROHE = ME->GET_PROHE( ).
    if LS_PROHE-PROHE is not initial.
      append value #( IELEM     = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_HEIGHT
                      ELEMV     = LS_PROHE-PROHE
                      ELEMV_UOM = LS_PROHE-PROHE_UOM
                    ) to RT_ELEMENT.
    endif.

*** Product Height2
    LS_PROH2 = ME->GET_PROH2( ).
    if LS_PROH2-PROH2 is not initial.
      append value #( IELEM     = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_HEIGHT2
                      ELEMV     = LS_PROHE-PROHE
                      ELEMV_UOM = LS_PROHE-PROHE_UOM
                    ) to RT_ELEMENT.
    endif.

*** Get Joint Width
    LS_JOIWI = ME->GET_JOIWI( ).
    if LS_JOIWI-JOIWI is not initial.
      append value #( IELEM     = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_JOIWI
                      ELEMV     = LS_JOIWI-JOIWI
                      ELEMV_UOM = LS_JOIWI-JOIWI_UOM
                    ) to RT_ELEMENT.
    endif.

*** Overlap
    LS_OVLAP = ME->GET_OVLAP( ).
    if LS_OVLAP-OVLAP is not initial.
      append value #( IELEM     = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_OVLAP
                      ELEMV     = LS_OVLAP-OVLAP
                      ELEMV_UOM = LS_OVLAP-OVLAP_UOM
                    ) to RT_ELEMENT.
    endif.

*** Dust Flap
    LS_FDUST = ME->GET_FDUST( ).
    if LS_FDUST-FDUST is not initial.
      append value #( IELEM     = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_FDUST
                      ELEMV     = LS_FDUST-FDUST
                      ELEMV_UOM = LS_FDUST-FDUST_UOM
                    ) to RT_ELEMENT.
    endif.

*** Tuck in Flap
    LS_FTUCK = ME->GET_FTUCK( ).
    if LS_FTUCK-FTUCK is not initial.
      append value #( IELEM     = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_FTUCK
                      ELEMV     = LS_FTUCK-FTUCK
                      ELEMV_UOM = LS_FTUCK-FTUCK_UOM
                    ) to RT_ELEMENT.
    endif.

***--------------------------------------------------------------------------------------
*** Get Product Sheet Dimensions (Outbound)
    LS_SHTPL = ME->GET_SHTPL( ).
    if LS_SHTPL-SHTPL is not initial.
      append value #( IELEM     = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_SHTPL
                      ELEMV     = LS_SHTPL-SHTPL
                      ELEMV_UOM = LS_SHTPL-SHTPL_UOM
                    ) to RT_ELEMENT.
    endif.

    LS_SHTPW = ME->GET_SHTPW( ).
    if LS_SHTPW-SHTPW is not initial.
      append value #( IELEM     = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_SHTPW
                      ELEMV     = LS_SHTPW-SHTPW
                      ELEMV_UOM = LS_SHTPW-SHTPW_UOM
                    ) to RT_ELEMENT.
    endif.

***--------------------------------------------------------------------------------------
*** Get Caliper
    LS_CALIP = ME->GET_CALIP( ).
    if LS_CALIP-CALIP is not initial.
      append value #( IELEM     = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_CALIP
                      ELEMV     = LS_CALIP-CALIP
                      ELEMV_UOM = LS_CALIP-CALIP_UOM
                    ) to RT_ELEMENT.
    endif.

***------------------------------------------------------------------------------------------------
*** Gap in Flap Top
    LS_FGAPT = ME->GET_FGAPT( ).
    if LS_FGAPT-FGAPT is not initial.
      append value #( IELEM     = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_GAP_FLAP_T
                      ELEMV     = LS_FGAPT-FGAPT / 2      "Use half of the Gap as Reduction for Scores
                      ELEMV_UOM = LS_FGAPT-FGAPT_UOM
                    ) to RT_ELEMENT.
    endif.

*** Gap in Flap Bottom
    LS_FGAPB = ME->GET_FGAPB( ).
    if LS_FGAPB-FGAPB is not initial.
      append value #( IELEM     = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_GAP_FLAP_B
                      ELEMV     = LS_FGAPB-FGAPB / 2      "Use half of the Gap as Reduction for Scores
                      ELEMV_UOM = LS_FGAPB-FGAPB_UOM
                    ) to RT_ELEMENT.
    endif.

  endmethod.
  method GET_PRVDR_SCORE_MGR.

    data: LT_GATE         type        /VCXI/VKSR_TT_GATE.
    data: LR_GATE         type ref to /VCXI/CL_VKSR_GATE.

***--------------------------------------------------------------------------------------
*** Get Score Manager provider
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_PXSS_PR_SCORE_MGR=>C_IPRVT_PX_SCORE_MGR
                                     IF_INUSE = ABAP_TRUE ).

***--------------------------------------------------------------------------------------
*** Get Score Manager Reference
    loop at LT_GATE into LR_GATE.
      try.
          move LR_GATE->R_CLSPR ?to RR_PR_SCORE_MGR.
          check RR_PR_SCORE_MGR is bound.
          move LR_GATE to ER_GATE_SCORE_MGR.
          exit.

        catch CX_SY_MOVE_CAST_ERROR.
          clear: RR_PR_SCORE_MGR, ER_GATE_SCORE_MGR.
      endtry.
    endloop.

  endmethod.
  method GET_SHTLE.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SHTLE ).
    move LS_PARAM-ATFLV to RS_SHTLE-SHTLE.
    move LS_PARAM-UNIT  to RS_SHTLE-SHTLE_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_SHTPL.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SHTPL ).
    move LS_PARAM-ATFLV to RS_SHTPL-SHTPL.
    move LS_PARAM-UNIT  to RS_SHTPL-SHTPL_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_SHTPW.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SHTPW ).
    move LS_PARAM-ATFLV to RS_SHTPW-SHTPW.
    move LS_PARAM-UNIT  to RS_SHTPW-SHTPW_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_SHTWI.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SHTWI ).
    move LS_PARAM-ATFLV to RS_SHTWI-SHTWI.
    move LS_PARAM-UNIT  to RS_SHTWI-SHTWI_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_STATUS.

    RF_ELMST = SUPER->GET_STATUS( ).

***--------------------------------------------------------------------------------------
*** Check FEFCO
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = ME->CHECK_FEFCO( ) ).

*** Check Product Dimensions
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = ME->CHECK_PRODIM( ) ).

*** Check Flute
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = ME->CHECK_FLUTE( ) ).

*** Check Caliper
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = ME->CHECK_CALIP( ) ).

*** Check Surface Weight
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = ME->CHECK_SURWT( ) ).

*** Check Joint Position
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = ME->CHECK_JOIPO( ) ).

  endmethod.
  method GET_STATUS_MSG.

    SUPER->GET_STATUS_MSG( IR_MESSAGE = IR_MESSAGE ).

***--------------------------------------------------------------------------------------
*** Check FEFCO
    ME->CHECK_FEFCO( IR_MESSAGE = IR_MESSAGE ).

*** Check Product Dimensions
    ME->CHECK_PRODIM( IR_MESSAGE = IR_MESSAGE ).

*** Check Flute
    ME->CHECK_FLUTE( IR_MESSAGE = IR_MESSAGE ).

*** Check Caliper
    ME->CHECK_CALIP( IR_MESSAGE = IR_MESSAGE ).

*** Check Surface Weight
    ME->CHECK_SURWT( IR_MESSAGE = IR_MESSAGE ).

*** Check Joint Position
    ME->CHECK_JOIPO( IR_MESSAGE = IR_MESSAGE ).

  endmethod.
  method GET_SURWT.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

***--------------------------------------------------------------------------------------
*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SURWT ).
    move LS_PARAM-ATFLV to RS_SURWT-SURWT.
    move LS_PARAM-UNIT  to RS_SURWT-SURWT_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method HANDLE_EVENT.

    SUPER->HANDLE_EVENT( IF_IEVNT         = IF_IEVNT
                         IT_GATE_PROVIDER = IT_GATE_PROVIDER ).

    case IF_IEVNT.
      when C_IEVNT_I_SET_DEFAULT.
        ME->HANDLE_SET_DEFAULT( ).

      when C_IEVNT_I_INIT_PRODIM.
        ME->HANDLE_INIT_PRODIM( ).
      when C_IEVNT_I_RECALC_SHTDIM.
        ME->HANDLE_RECALC_SHTDIM( ).
      when C_IEVNT_I_RECALC_JOIDIM.
        ME->HANDLE_RECALC_JOIDIM( ).

***   Event Forwarding
      when C_IEVNT_I_RECALC_HNDDIM.
        ME->R_GATE->SET_EVENT( IF_IEVNT = ZIF_VCXI_P4SS_PRDTL~C_IEVNT_O_HNDDIM_CHANGED ).
      when C_IEVNT_I_RECALC_AREA.
        ME->R_GATE->SET_EVENT( IF_IEVNT = ZIF_VCXI_P4SS_PRDTL~C_IEVNT_O_AREA_CHANGED ).
      when C_IEVNT_I_RECALC_WEIGHT.
        ME->R_GATE->SET_EVENT( IF_IEVNT = ZIF_VCXI_P4SS_PRDTL~C_IEVNT_O_WEIGHT_CHANGED ).

    endcase.

  endmethod.
  method HANDLE_INIT_PRODIM.

    data: LF_BOXPA type ZVCXI_P0SS_BOXPA,
          LF_SETBY type /VCXI/VKSR_SETBY.
    data: LS_ELEMV type ZVCXI_P0SS_S_ELEMV.
    data: LT_ELEMENT type ZVCXI_P0SS_TT_ELEMENT.
    data: LR_CUST_PRSTY type ref to ZCL_VCXI_PXSS_CUST_PRSTY.


*** Get Product Style Customizing and Elements
    ME->GET_FEFCO( importing ER_CUST_PRSTY = LR_CUST_PRSTY ).
    check LR_CUST_PRSTY is bound.
    LT_ELEMENT = ME->GET_PRSTY_ELEMENTS( ).

***--------------------------------------------------------------------------------------
*** Part of Box
    if LR_CUST_PRSTY->GET_BOXPA( ) is initial.
      ME->SET_BOXPA( IF_SETBY = SPACE ).
    endif.
    LF_BOXPA = ME->GET_BOXPA( ).

***--------------------------------------------------------------------------------------
*** Height
    if not LR_CUST_PRSTY->IS_IELEM_USED( IF_IELEM = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_HEIGHT
                                         IF_BOXPA = LF_BOXPA ).
      ME->SET_PROHE( IF_SETBY = SPACE ).
    endif.

*** Second Height
    if not LR_CUST_PRSTY->IS_IELEM_USED( IF_IELEM = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_HEIGHT2
                                         IF_BOXPA = LF_BOXPA ).
      ME->SET_PROH2( IF_SETBY = SPACE ).
    endif.

*** Overlap
    if LR_CUST_PRSTY->IS_IELEM_USED( IF_IELEM = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_OVLAP
                                     IF_BOXPA = LF_BOXPA ).
      ME->GET_OVLAP( importing EF_SETBY = LF_SETBY ).
      if LF_SETBY is initial and   "only if initial
         LR_CUST_PRSTY is bound.
***     Set Default Value for Element
        LS_ELEMV = LR_CUST_PRSTY->GET_ELEMV( IF_IELEM   = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_OVLAP
                                             IT_ELEMENT = LT_ELEMENT ).
        ME->SET_OVLAP( IS_OVLAP = value #( OVLAP     = LS_ELEMV-ELEMV
                                           OVLAP_UOM = LS_ELEMV-ELEMV_UOM ) ).
      endif.
    else.
      ME->SET_OVLAP( IF_SETBY = SPACE ).
    endif.

*** Tuck In Flap
    if LR_CUST_PRSTY->IS_IELEM_USED( IF_IELEM = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_FTUCK
                                     IF_BOXPA = LF_BOXPA ).
      ME->GET_FTUCK( importing EF_SETBY = LF_SETBY ).
      if LF_SETBY is initial and   "only if initial
         LR_CUST_PRSTY is bound.
***     Set Default Value for Element
        LS_ELEMV = LR_CUST_PRSTY->GET_ELEMV( IF_IELEM   = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_FTUCK
                                             IT_ELEMENT = LT_ELEMENT ).
        ME->SET_FTUCK( IS_FTUCK = value #( FTUCK     = LS_ELEMV-ELEMV
                                           FTUCK_UOM = LS_ELEMV-ELEMV_UOM ) ).
      endif.
    else.
      ME->SET_FTUCK( IF_SETBY = SPACE ).
    endif.

*** Dust Flap
    if LR_CUST_PRSTY->IS_IELEM_USED( IF_IELEM = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_FDUST
                                     IF_BOXPA = LF_BOXPA ).
      ME->GET_FDUST( importing EF_SETBY = LF_SETBY ).
      if LF_SETBY is initial and   "only if initial
         LR_CUST_PRSTY is bound.
***     Set Default Value for Element
        LS_ELEMV = LR_CUST_PRSTY->GET_ELEMV( IF_IELEM   = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_FDUST
                                             IT_ELEMENT = LT_ELEMENT ).
        ME->SET_FDUST( IS_FDUST = value #( FDUST     = LS_ELEMV-ELEMV
                                           FDUST_UOM = LS_ELEMV-ELEMV_UOM ) ).
      endif.
    else.
      ME->SET_FDUST( IF_SETBY = SPACE ).
    endif.

*** Gap in Flap Top
    if LR_CUST_PRSTY->IS_IELEM_USED( IF_IELEM = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_GAP_FLAP_T
                                     IF_BOXPA = LF_BOXPA ).
      ME->GET_FGAPT( importing EF_SETBY = LF_SETBY ).
      if LF_SETBY is initial and   "only if initial
         LR_CUST_PRSTY is bound.
***     Set Default Value for Element
        LS_ELEMV = LR_CUST_PRSTY->GET_ELEMV( IF_IELEM   = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_GAP_FLAP_T
                                             IT_ELEMENT = LT_ELEMENT ).
        ME->SET_FGAPT( IS_FGAPT = value #( FGAPT     = LS_ELEMV-ELEMV
                                           FGAPT_UOM = LS_ELEMV-ELEMV_UOM ) ).
      endif.
    else.
      ME->SET_FGAPT( IF_SETBY = SPACE ).
    endif.

*** Gap in Flap Bottom
    if LR_CUST_PRSTY->IS_IELEM_USED( IF_IELEM = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_GAP_FLAP_B
                                     IF_BOXPA = LF_BOXPA ).
      ME->GET_FGAPB( importing EF_SETBY = LF_SETBY ).
      if LF_SETBY is initial and   "only if initial
         LR_CUST_PRSTY is bound.
***     Set Default Value for Element
        LS_ELEMV = LR_CUST_PRSTY->GET_ELEMV( IF_IELEM   = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_GAP_FLAP_B
                                             IT_ELEMENT = LT_ELEMENT ).
        ME->SET_FGAPB( IS_FGAPB = value #( FGAPB     = LS_ELEMV-ELEMV
                                           FGAPB_UOM = LS_ELEMV-ELEMV_UOM ) ).
      endif.
    else.
      ME->SET_FGAPB( IF_SETBY = SPACE ).
    endif.

  endmethod.
  method HANDLE_RECALC_JOIDIM.

    data: LF_JOIPO type ZVCXI_P0S_JOIPO,
          LF_SETBY type /VCXI/VKSR_SETBY.
    data: LS_ELEMV type ZVCXI_P0SS_S_ELEMV,
          LS_JOILE type ZVCXI_P0S_S_JOILE.
    data: LT_ELEMENT type ZVCXI_P0SS_TT_ELEMENT.
    data: LR_CUST_PRSTY type ref to ZCL_VCXI_PXSS_CUST_PRSTY.


*** Get Product Style Customizing and Elements
    ME->GET_FEFCO( importing ER_CUST_PRSTY = LR_CUST_PRSTY ).
    check LR_CUST_PRSTY is bound.
    LT_ELEMENT = ME->GET_PRSTY_ELEMENTS( ).

***--------------------------------------------------------------------------------------
*** Joint Position
    LF_JOIPO = ME->GET_JOIPO( importing EF_SETBY = LF_SETBY ).
    if LF_SETBY is initial and   "only if initial
       LR_CUST_PRSTY is bound.
      LR_CUST_PRSTY->GET_JOIPO( importing EF_DEFAULT = LF_JOIPO ).
      if LF_JOIPO is not initial.
        ME->SET_JOIPO( IF_JOIPO = LF_JOIPO ).
      endif.
    endif.

***--------------------------------------------------------------------------------------
*** Joint Type
    if LF_JOIPO ne ZCL_VCXI_PXSS_PR_SCORE_MGR=>C_JOIPO_NONE.

    else.
***   Initialize Joint Type
      ME->SET_JOITY( IF_SETBY = SPACE ).
    endif.

***--------------------------------------------------------------------------------------
*** Joint Width
    if LF_JOIPO ne ZCL_VCXI_PXSS_PR_SCORE_MGR=>C_JOIPO_NONE.
      ME->GET_JOIWI( importing EF_SETBY = LF_SETBY ).
      if LF_SETBY is initial and   "only if initial
         LR_CUST_PRSTY is bound.
***     Set Default Value for Element
        LS_ELEMV = LR_CUST_PRSTY->GET_ELEMV( IF_IELEM   = ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_JOIWI
                                             IT_ELEMENT = LT_ELEMENT ).
        ME->SET_JOIWI( IS_JOIWI = value #( JOIWI     = LS_ELEMV-ELEMV
                                           JOIWI_UOM = LS_ELEMV-ELEMV_UOM ) ).
      endif.
    else.
***   Initialize Joint Width
      ME->SET_JOIWI( IF_SETBY = SPACE ).
    endif.

***--------------------------------------------------------------------------------------
*** Joint Length
    if LF_JOIPO ne ZCL_VCXI_PXSS_PR_SCORE_MGR=>C_JOIPO_NONE.
      ME->GET_JOILE( importing EF_SETBY = LF_SETBY ).
      if LF_SETBY ne /VCXI/CL_VKSR_GATE=>C_SETBY_USER.
***     Use Product Height as Joint Length
        LS_JOILE = corresponding #( ME->GET_PROHE( ) mapping JOILE     = PROHE
                                                             JOILE_UOM = PROHE_UOM ).
***     Add Joint Extension Top and Bottom
        try.
            data(LS_JEXTT) = ME->GET_JEXTT( ).
            if LS_JEXTT-JEXTT is not initial.
              ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_JOILE-JOILE
                                                                  IF_CALCV_UOM_1 = LS_JOILE-JOILE_UOM
                                                                  IF_CALCV_2     = LS_JEXTT-JEXTT
                                                                  IF_CALCV_UOM_2 = LS_JEXTT-JEXTT_UOM
                                                                  IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_ADD
                                                                  IF_UOM         = LS_JOILE-JOILE_UOM
                                                        importing EF_CALCV       = LS_JOILE-JOILE
                                                                  EF_CALCV_UOM   = LS_JOILE-JOILE_UOM ).
            endif.
            data(LS_JEXTB) = ME->GET_JEXTB( ).
            if LS_JEXTB is not initial.
              ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_JOILE-JOILE
                                                                  IF_CALCV_UOM_1 = LS_JOILE-JOILE_UOM
                                                                  IF_CALCV_2     = LS_JEXTB-JEXTB
                                                                  IF_CALCV_UOM_2 = LS_JEXTB-JEXTB_UOM
                                                                  IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_ADD
                                                                  IF_UOM         = LS_JOILE-JOILE_UOM
                                                        importing EF_CALCV       = LS_JOILE-JOILE
                                                                  EF_CALCV_UOM   = LS_JOILE-JOILE_UOM ).
            endif.
          catch /VCXI/CX_CKX into data(LR_CX_CKX).
            ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( LR_CX_CKX ).
            clear LS_JOILE-JOILE.
        endtry.

***     Set new Default
        ME->SET_JOILE( IS_JOILE = LS_JOILE ).
      endif.
    else.
***   Initialize Joint Length and Extension
      ME->SET_JOILE( IF_SETBY = SPACE ).
      ME->SET_JEXTT( IF_SETBY = SPACE ).
      ME->SET_JEXTB( IF_SETBY = SPACE ).
    endif.

  endmethod.
  method HANDLE_RECALC_SHTDIM.

    data: LS_SHTLE type ZVCXI_P0SS_S_SHTLE,
          LS_SHTWI type ZVCXI_P0SS_S_SHTWI.
    data: LR_PR_SCORE_MGR type ref to ZCL_VCXI_PXSS_PR_SCORE_MGR.

*** Ger Score Manager Provider
    LR_PR_SCORE_MGR = ME->GET_PRVDR_SCORE_MGR( ).
    check LR_PR_SCORE_MGR is bound.

*** Get Sheet Dimensions based on Scores
    LR_PR_SCORE_MGR->GET_SHT_DIM( importing ES_SHTLE = LS_SHTLE
                                            ES_SHTWI = LS_SHTWI ).

*** Set Sheet Dimensions
    ME->SET_SHTLE( exporting IS_SHTLE = LS_SHTLE ).
    ME->SET_SHTWI( exporting IS_SHTWI = LS_SHTWI ).


*** Set Product Sheet Dimension for Non-Custom Designs
    if ME->CHECK_CUST_DESIGN( ) ne ABAP_TRUE.
      ME->SET_SHTPL( IS_SHTPL = value #( SHTPL     = LS_SHTLE-SHTLE
                                         SHTPL_UOM = LS_SHTLE-SHTLE_UOM ) ).
      ME->SET_SHTPW( IS_SHTPW = value #( SHTPW     = LS_SHTWI-SHTWI
                                         SHTPW_UOM = LS_SHTWI-SHTWI_UOM ) ).
    endif.

  endmethod.
method HANDLE_SET_DEFAULT.

  ZCL_VCXI_XCDR_SERVICE=>SET_DEFAULT( IR_GATE = ME->R_GATE ).

endmethod.
  method LOAD_BOARD.

    data: LF_DCONF type /VCXI/VKCR_DCONF.
    data: LS_CALIP       type ZVCXI_P0SS_S_CALIP,
          LS_CALCV_SURWT type ZVCXI_XCS_S_CALCV,
          LS_SURWT       type ZVCXI_PXS_S_SURWT,
          LS_SURWT_TMP   type ZVCXI_PXS_S_SURWT,
          LS_BGWHT       type ZVCXI_PCSS_S_BGWHT.
    data: LS_BPAP type ZVCXI_PCSS_S_BPAP,
          LS_BSTA type ZVCXI_PCSS_S_BSTA,
          LS_BWAX type ZVCXI_PCSS_S_BWAX.
    data: LR_CUST_BOARD  type ref to ZCL_VCXI_PCSS_CUST_BOARD,
          LR_CUST_PAPER  type ref to ZCL_VCXI_PCSS_CUST_PAPER,
          LR_CUST_STARCH type ref to ZCL_VCXI_P1SS_CUST_STARCH,
          LR_CUST_WAX    type ref to ZCL_VCXI_P1SS_CUST_WAX.


*** Get Configuration Date
    LF_DCONF = ME->R_GATE->GET_APPDATA( )->F_DCONF.

***--------------------------------------------------------------------------------------
*** Get Board Customizing
    LR_CUST_BOARD = ZCL_VCXI_PCSS_CUST_BOARD=>GET_INSTANCE( IF_BOARD = IF_BOARD
                                                            IF_MBOAR = IF_MBOAR
                                                            IF_WERKS = IF_WERKS
                                                            IF_DATE  = LF_DCONF ).
    check LR_CUST_BOARD is bound.

***--------------------------------------------------------------------------------------
*** Set BoardID
    ME->SET_BOARD( IF_BOARD = LR_CUST_BOARD->F_BOARD ).

*** Set Flute
    ME->SET_FLUTE( IF_FLUTE = LR_CUST_BOARD->F_FLUTE ).

*** Get Parameter Units
    LS_CALIP = value #( CALIP_UOM = ME->GET_CALIP( )-CALIP_UOM ).
    LS_SURWT = value #( SURWT_UOM = ME->GET_SURWT( )-SURWT_UOM ).

***--------------------------------------------------------------------------------------
*** Loop at Papers based on Default Work Center
    loop at LR_CUST_BOARD->GET_BPAP( IF_ARBPL = LR_CUST_BOARD->F_ARBPL ) into LS_BPAP.
***   Get Paper Customizing
      LR_CUST_PAPER = ZCL_VCXI_PCSS_CUST_PAPER=>GET_INSTANCE_PAPER( IF_MATNR = LS_BPAP-BPMAT
                                                                    IF_WERKS = IF_WERKS
                                                                    IF_DATE  = LF_DCONF ).
***   Calculate the Caliper
      ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LR_CUST_PAPER->S_CALIP-CALIP
                                                          IF_CALCV_UOM_1 = LR_CUST_PAPER->S_CALIP-CALIP_UOM
                                                          IF_CALCV_2     = LS_CALIP-CALIP
                                                          IF_CALCV_UOM_2 = LS_CALIP-CALIP_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_ADD
                                                          IF_UOM         = LS_CALIP-CALIP_UOM
                                                importing EF_CALCV       = LS_CALIP-CALIP
                                                          EF_CALCV_UOM   = LS_CALIP-CALIP_UOM ).

***   Calculate Surface Weight
      ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LR_CUST_PAPER->S_BAWGT-BAWGT
                                                          IF_CALCV_UOM_1 = LR_CUST_PAPER->S_BAWGT-BAWGT_UOM
                                                          IF_CALCV_2     = LR_CUST_PAPER->S_BAWGP-BAWGP
                                                          IF_CALCV_UOM_2 = LR_CUST_PAPER->S_BAWGP-BAWGP_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_DIVIDE
                                                importing EF_CALCV       = LS_CALCV_SURWT-CALCV
                                                          EF_CALCV_UOM   = LS_CALCV_SURWT-CALCV_UOM ).

      LS_CALCV_SURWT-CALCV = LS_CALCV_SURWT-CALCV * LS_BPAP-BPFAC.

      ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_CALCV_SURWT-CALCV
                                                          IF_CALCV_UOM_1 = LS_CALCV_SURWT-CALCV_UOM
                                                          IF_CALCV_2     = LS_SURWT-SURWT
                                                          IF_CALCV_UOM_2 = LS_SURWT-SURWT_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_ADD
                                                          IF_UOM         = LS_SURWT-SURWT_UOM
                                                importing EF_CALCV       = LS_SURWT-SURWT
                                                          EF_CALCV_UOM   = LS_SURWT-SURWT_UOM ).
    endloop.

***--------------------------------------------------------------------------------------
*** Add Starch to Surface Weight
    LS_BSTA = LR_CUST_BOARD->GET_BSTA( IF_ARBPL = LR_CUST_BOARD->F_ARBPL ).
    if LS_BSTA-STMAT is not initial.
      LR_CUST_STARCH = ZCL_VCXI_P1SS_CUST_STARCH=>GET_INSTANCE_STARCH( IF_MATNR = LS_BSTA-STMAT
                                                                       IF_WERKS = IF_WERKS
                                                                       IF_DATE  = LF_DCONF ).
***   Get Surface Weight
      LS_SURWT_TMP = LR_CUST_STARCH->GET_SURWT( IF_UOM   = LS_SURWT-SURWT_UOM
                                                IF_STARF = LS_BSTA-STARF ).
      ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_SURWT_TMP-SURWT
                                                          IF_CALCV_UOM_1 = LS_SURWT_TMP-SURWT_UOM
                                                          IF_CALCV_2     = LS_SURWT-SURWT
                                                          IF_CALCV_UOM_2 = LS_SURWT-SURWT_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_ADD
                                                          IF_UOM         = LS_SURWT-SURWT_UOM
                                                importing EF_CALCV       = LS_SURWT-SURWT
                                                          EF_CALCV_UOM   = LS_SURWT-SURWT_UOM ).
    endif.


***--------------------------------------------------------------------------------------
*** Add Wax to Surface Weight
    loop at LR_CUST_BOARD->GET_BWAX( IF_ARBPL = LR_CUST_BOARD->F_ARBPL ) into LS_BWAX.
      LR_CUST_WAX = ZCL_VCXI_P1SS_CUST_WAX=>GET_INSTANCE_WAX( IF_MATNR = LS_BWAX-WAMAT
                                                              IF_WERKS = IF_WERKS
                                                              IF_DATE  = LF_DCONF ).
***   Get Surface Weight
      LS_SURWT_TMP = LR_CUST_WAX->GET_SURWT( IF_UOM   = LS_SURWT-SURWT_UOM
                                             IF_WAXFA = LS_BWAX-WAXFA ).
      ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_SURWT_TMP-SURWT
                                                          IF_CALCV_UOM_1 = LS_SURWT_TMP-SURWT_UOM
                                                          IF_CALCV_2     = LS_SURWT-SURWT
                                                          IF_CALCV_UOM_2 = LS_SURWT-SURWT_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_ADD
                                                          IF_UOM         = LS_SURWT-SURWT_UOM
                                                importing EF_CALCV       = LS_SURWT-SURWT
                                                          EF_CALCV_UOM   = LS_SURWT-SURWT_UOM ).

    endloop.

***--------------------------------------------------------------------------------------
*** Add BoardGrade Wall Height to Caliper
    LS_BGWHT = LR_CUST_BOARD->GET_BGWHT( IF_ARBPL = LR_CUST_BOARD->F_ARBPL ).
    if LS_BGWHT-BGWHT_UOM is not initial.
      ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_BGWHT-BGWHT
                                                          IF_CALCV_UOM_1 = LS_BGWHT-BGWHT_UOM
                                                          IF_CALCV_2     = LS_CALIP-CALIP
                                                          IF_CALCV_UOM_2 = LS_CALIP-CALIP_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_ADD
                                                          IF_UOM         = LS_CALIP-CALIP_UOM
                                                importing EF_CALCV       = LS_CALIP-CALIP
                                                          EF_CALCV_UOM   = LS_CALIP-CALIP_UOM ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Caliper
    ME->SET_CALIP( IS_CALIP = LS_CALIP ).

*** Set Surface Weight
    ME->SET_SURWT( IS_SURWT = LS_SURWT ).

  endmethod.
  method SET_ADDWT.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_ADDWT ).

    LS_PARAM-ATFLV = IS_ADDWT-ADDWT.
    LS_PARAM-UNIT  = IS_ADDWT-ADDWT_UOM.
    LS_PARAM-SETBY = IF_SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_ADDWT_CHANGED ).
    endif.

  endmethod.
  method SET_BOARD.

    data:  LF_CHANGED  type ABAP_BOOL.
    data:  LS_LEOKY    type /VCXI/VKSR_S_LEOKY.

    move IF_BOARD to LS_LEOKY-LEOKY.
    move IF_SETBY to LS_LEOKY-SETBY.

    LF_CHANGED = ME->R_GATE->SET_LEOKY( IS_LEOKY = LS_LEOKY ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_BOARD_CHANGED ).
    endif.

  endmethod.
  method SET_BOXPA.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_BOXPA ).

    if IF_BOXPA is supplied.
      LS_PARAM-ATWRT = IF_BOXPA.
    endif.

    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATWRT.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_BOXPA_CHANGED ).
    endif.

  endmethod.
  method SET_CALIP.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_CALIP ).

    LS_PARAM-ATFLV = IS_CALIP-CALIP.
    LS_PARAM-UNIT  = IS_CALIP-CALIP_UOM.
    LS_PARAM-SETBY = IF_SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_CALIP_CHANGED ).
    endif.

  endmethod.
  method SET_COUAR.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_COUAR ).

    move IS_COUAR-COUAR     to LS_PARAM-ATFLV.
    move IS_COUAR-COUAR_UOM to LS_PARAM-UNIT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_COUAR_CHANGED ).
    endif.

  endmethod.
  method SET_FDUST.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_FDUST ).

    if IS_FDUST is supplied.
      LS_PARAM-ATFLV = IS_FDUST-FDUST.
      LS_PARAM-UNIT  = IS_FDUST-FDUST_UOM.
    endif.

    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_FDUST_CHANGED ).
    endif.

  endmethod.
  method SET_FEFCO.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_FEFCO ).

    move IF_FEFCO to LS_PARAM-ATWRT.
    move IF_SETBY to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_FEFCO_CHANGED ).
    endif.

  endmethod.
  method SET_FGAPB.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_FGAPB ).

    if IS_FGAPB is supplied.
      LS_PARAM-ATFLV = IS_FGAPB-FGAPB.
      LS_PARAM-UNIT  = IS_FGAPB-FGAPB_UOM.
    endif.

    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_FGAP_CHANGED ).
    endif.

  endmethod.
  method SET_FGAPT.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_FGAPT ).

    if IS_FGAPT is supplied.
      LS_PARAM-ATFLV = IS_FGAPT-FGAPT.
      LS_PARAM-UNIT  = IS_FGAPT-FGAPT_UOM.
    endif.

    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_FGAP_CHANGED ).
    endif.

  endmethod.
  method SET_FLUTE.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_FLUTE ).

    LS_PARAM-ATWRT = IF_FLUTE.
    LS_PARAM-SETBY = IF_SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_FLUTE_CHANGED ).
    endif.

  endmethod.
  method SET_FLUTO.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_FLUTO ).

    move IF_FLUTO     to LS_PARAM-ATWRT.
    move IF_SETBY     to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_FLUTO_CHANGED ).
    endif.

  endmethod.
  method SET_FTUCK.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_FTUCK ).

    if IS_FTUCK is supplied.
      LS_PARAM-ATFLV = IS_FTUCK-FTUCK.
      LS_PARAM-UNIT  = IS_FTUCK-FTUCK_UOM.
    endif.

    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_FTUCK_CHANGED ).
    endif.

  endmethod.
  method SET_JEXTB.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_JEXTB ).

    if IS_JEXTB is supplied.
      LS_PARAM-ATFLV = IS_JEXTB-JEXTB.
      LS_PARAM-UNIT  = IS_JEXTB-JEXTB_UOM.
    endif.

    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_JEXT_CHANGED ).
    endif.

  endmethod.
  method SET_JEXTT.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_JEXTT ).

    if IS_JEXTT is supplied.
      LS_PARAM-ATFLV = IS_JEXTT-JEXTT.
      LS_PARAM-UNIT  = IS_JEXTT-JEXTT_UOM.
    endif.

    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_JEXT_CHANGED ).
    endif.

  endmethod.
  method SET_JOILE.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_JOILE ).

    if IS_JOILE is supplied.
      LS_PARAM-ATFLV = IS_JOILE-JOILE.
      LS_PARAM-UNIT  = IS_JOILE-JOILE_UOM.
    endif.

*** Identify correct SETBY
    if IF_FOVRL is supplied.
      LS_PARAM-SETBY = switch #( IF_FOVRL when ABAP_TRUE then /VCXI/CL_VKSR_GATE=>C_SETBY_USER
                                                         else SPACE ).
    else.
      LS_PARAM-SETBY = IF_SETBY.
    endif.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_JOILE_CHANGED ).
    endif.

  endmethod.
  method SET_JOIPO.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_JOIPO ).

    move IF_JOIPO     to LS_PARAM-ATWRT.
    move IF_SETBY     to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_JOIPO_CHANGED ).
    endif.

  endmethod.
  method SET_JOITY.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_JOITY ).

    move IF_JOITY     to LS_PARAM-ATWRT.
    move IF_SETBY     to LS_PARAM-SETBY.

    if IF_JOITY is supplied.
      LS_PARAM-ATWRT = IF_JOITY.
    endif.

    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_JOITY_CHANGED ).
    endif.

  endmethod.
  method SET_JOIWI.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_JOIWI ).

    if IS_JOIWI is supplied.
      LS_PARAM-ATFLV = IS_JOIWI-JOIWI.
      LS_PARAM-UNIT  = IS_JOIWI-JOIWI_UOM.
    endif.

    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_JOIWI_CHANGED ).
    endif.

  endmethod.
  method SET_OVLAP.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_OVLAP ).

    if IS_OVLAP is supplied.
      LS_PARAM-ATFLV = IS_OVLAP-OVLAP.
      LS_PARAM-UNIT  = IS_OVLAP-OVLAP_UOM.
    endif.

    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_OVLAP_CHANGED ).
    endif.

  endmethod.
  method SET_PROH2.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PROH2 ).

    if IS_PROH2 is supplied.
      LS_PARAM-ATFLV = IS_PROH2-PROH2.
      LS_PARAM-UNIT  = IS_PROH2-PROH2_UOM.
    endif.

    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_PROH2_CHANGED ).
    endif.

  endmethod.
  method SET_PROHE.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PROHE ).

    if IS_PROHE is supplied.
      LS_PARAM-ATFLV = IS_PROHE-PROHE.
      LS_PARAM-UNIT  = IS_PROHE-PROHE_UOM.
    endif.

    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_PROHE_CHANGED ).
    endif.

  endmethod.
  method SET_PROLE.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PROLE ).

    move IS_PROLE-PROLE     to LS_PARAM-ATFLV.
    move IS_PROLE-PROLE_UOM to LS_PARAM-UNIT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_PROLE_CHANGED ).
    endif.

  endmethod.
  method SET_PROWI.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PROWI ).

    move IS_PROWI-PROWI     to LS_PARAM-ATFLV.
    move IS_PROWI-PROWI_UOM to LS_PARAM-UNIT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_PROWI_CHANGED ).
    endif.

  endmethod.
  method SET_SHTLE.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SHTLE ).

    move IS_SHTLE-SHTLE     to LS_PARAM-ATFLV.
    move IS_SHTLE-SHTLE_UOM to LS_PARAM-UNIT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_SHTLE_CHANGED ).
    endif.

  endmethod.
  method SET_SHTPL.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SHTPL ).

    move IS_SHTPL-SHTPL     to LS_PARAM-ATFLV.
    move IS_SHTPL-SHTPL_UOM to LS_PARAM-UNIT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_SHTPL_CHANGED ).
    endif.

  endmethod.
  method SET_SHTPW.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SHTPW ).

    move IS_SHTPW-SHTPW     to LS_PARAM-ATFLV.
    move IS_SHTPW-SHTPW_UOM to LS_PARAM-UNIT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_SHTPW_CHANGED ).
    endif.

  endmethod.
  method SET_SHTWI.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SHTWI ).

    move IS_SHTWI-SHTWI     to LS_PARAM-ATFLV.
    move IS_SHTWI-SHTWI_UOM to LS_PARAM-UNIT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_SHTWI_CHANGED ).
    endif.

  endmethod.
  method SET_SURWT.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SURWT ).

    move IS_SURWT-SURWT     to LS_PARAM-ATFLV.
    move IS_SURWT-SURWT_UOM to LS_PARAM-UNIT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_SURWT_CHANGED ).
    endif.

  endmethod.
  method ZIF_VCXI_P4SS_PRDTL~GET_ADDWT.

*** Get Additional Weight
    RS_ADDWT = ME->GET_ADDWT( ).

*** Adjust Unit if requested
    if IF_UOM is supplied.
      ZCL_VCXI_XCS_SERVICE_UNIT=>CONVERT_UNIT( exporting IF_VALUE_IN  = RS_ADDWT-ADDWT
                                                         IF_UOM_IN    = RS_ADDWT-ADDWT_UOM
                                                         IF_UOM_OUT   = IF_UOM
                                               importing EF_VALUE_OUT = RS_ADDWT-ADDWT
                                                         EF_UOM_OUT   = RS_ADDWT-ADDWT_UOM ).
    endif.

  endmethod.
  method ZIF_VCXI_P4SS_PRDTL~GET_COUAR.

    RS_COUAR = ME->GET_COUAR( ).

    if IF_UOM is supplied.
      ZCL_VCXI_XCS_SERVICE_UNIT=>CONVERT_UNIT( exporting IF_VALUE_IN  = RS_COUAR-COUAR
                                                         IF_UOM_IN    = RS_COUAR-COUAR_UOM
                                                         IF_UOM_OUT   = IF_UOM
                                               importing EF_VALUE_OUT = RS_COUAR-COUAR
                                                         EF_UOM_OUT   = RS_COUAR-COUAR_UOM ).
    endif.

  endmethod.
  method ZIF_VCXI_P4SS_PRDTL~GET_COUWT.

    data: LS_COUAR type ZVCXI_PXS_S_COUAR,
          LS_SURWT type ZVCXI_PXS_S_SURWT.


***------------------------------------------------------------------------------------------------
*** Get Surface Weight
    LS_SURWT = ME->GET_SURWT( ).

*** Get Cut Out Area
    LS_COUAR = ME->ZIF_VCXI_P4SS_PRDTL~GET_COUAR( ).

***------------------------------------------------------------------------------------------------
*** Calculate Cut Out Waste Weight
    if IF_UOM is supplied.
      ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_COUAR-COUAR
                                                          IF_CALCV_UOM_1 = LS_COUAR-COUAR_UOM
                                                          IF_CALCV_2     = LS_SURWT-SURWT
                                                          IF_CALCV_UOM_2 = LS_SURWT-SURWT_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_MULTIPLY
                                                          IF_UOM         = IF_UOM
                                                importing EF_CALCV       = RS_COUWT-COUWT
                                                          EF_CALCV_UOM   = RS_COUWT-COUWT_UOM ).
    else.
      ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_COUAR-COUAR
                                                          IF_CALCV_UOM_1 = LS_COUAR-COUAR_UOM
                                                          IF_CALCV_2     = LS_SURWT-SURWT
                                                          IF_CALCV_UOM_2 = LS_SURWT-SURWT_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_MULTIPLY
                                                importing EF_CALCV       = RS_COUWT-COUWT
                                                          EF_CALCV_UOM   = RS_COUWT-COUWT_UOM ).
    endif.

  endmethod.
  method ZIF_VCXI_P4SS_PRDTL~GET_HNDDIM.

    data: LS_FOLDIM type ZVCXI_PXS_S_FOLDIM.

***   For Closed Products use Folding Dimension
*    if LR_BL_CRFPROP->GET_TFOLD( ) eq ZCL_VCXI_P0SS_CUST_PRSTY=>C_TFOLD_CLOSED.
    LS_FOLDIM = ME->GET_FOLDIM( ).
    RS_HNDDIM = value #( HNDLE     = LS_FOLDIM-FOLLE
                         HNDLE_UOM = LS_FOLDIM-FOLLE_UOM
                         HNDWI     = LS_FOLDIM-FOLWI
                         HNDWI_UOM = LS_FOLDIM-FOLWI_UOM
                         HNDHE     = LS_FOLDIM-FOLHE
                         HNDHE_UOM = LS_FOLDIM-FOLHE_UOM ).
*    else.
****     Otherwise use Sheet Dimension
*      LR_BL_CRBPROP->GET_SHTDIM( importing ES_SHTLE = LS_SHTLE
*                                           ES_SHTWI = LS_SHTWI ).
*      LS_HNDDIM = value #( HNDLE     = LS_SHTLE-SHTLE
*                           HNDLE_UOM = LS_SHTLE-SHTLE_UOM
*                           HNDWI     = LS_SHTWI-SHTWI
*                           HNDWI_UOM = LS_SHTWI-SHTWI_UOM
*                           HNDHE     = LR_BL_CRFPROP->S_CALIP-CALIP
*                           HNDHE_UOM = LR_BL_CRFPROP->S_CALIP-CALIP_UOM ).
*    endif.
*  endif.

*** Ensure no negative Values have been calculated
    RS_HNDDIM-HNDLE = cond #( when RS_HNDDIM-HNDLE lt 0 then 0 else RS_HNDDIM-HNDLE ).
    RS_HNDDIM-HNDWI = cond #( when RS_HNDDIM-HNDWI lt 0 then 0 else RS_HNDDIM-HNDWI ).
    RS_HNDDIM-HNDHE = cond #( when RS_HNDDIM-HNDHE lt 0 then 0 else RS_HNDDIM-HNDHE ).

  endmethod.
  method ZIF_VCXI_P4SS_PRDTL~GET_SBLAR.

    data: LS_SHTWI type ZVCXI_P0SS_S_SHTWI,
          LS_SHTLE type ZVCXI_P0SS_S_SHTLE.


***------------------------------------------------------------------------------------------------
*** Get Sheet Dimension
    LS_SHTWI = ME->GET_SHTWI( ).
    LS_SHTLE = ME->GET_SHTLE( ).

***------------------------------------------------------------------------------------------------
*** Calculate Area
    if IF_UOM is supplied.
      ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_SHTLE-SHTLE
                                                          IF_CALCV_UOM_1 = LS_SHTLE-SHTLE_UOM
                                                          IF_CALCV_2     = LS_SHTWI-SHTWI
                                                          IF_CALCV_UOM_2 = LS_SHTWI-SHTWI_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_MULTIPLY
                                                          IF_UOM         = IF_UOM
                                                importing EF_CALCV       = RS_SBLAR-SBLAR
                                                          EF_CALCV_UOM   = RS_SBLAR-SBLAR_UOM ).
    else.
      ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_SHTLE-SHTLE
                                                          IF_CALCV_UOM_1 = LS_SHTLE-SHTLE_UOM
                                                          IF_CALCV_2     = LS_SHTWI-SHTWI
                                                          IF_CALCV_UOM_2 = LS_SHTWI-SHTWI_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_MULTIPLY
                                                importing EF_CALCV       = RS_SBLAR-SBLAR
                                                          EF_CALCV_UOM   = RS_SBLAR-SBLAR_UOM ).
    endif.

  endmethod.
  method ZIF_VCXI_P4SS_PRDTL~GET_SBLWT.

    data: LS_SBLAR type ZVCXI_PXS_S_SBLAR,
          LS_SURWT type ZVCXI_PXS_S_SURWT.


***------------------------------------------------------------------------------------------------
*** Get Surface Weight
    LS_SURWT = ME->GET_SURWT( ).

*** Get Sold Blank Area
    LS_SBLAR = ME->ZIF_VCXI_P4SS_PRDTL~GET_SBLAR( ).

***------------------------------------------------------------------------------------------------
*** Calculate Sold Blank Weight
    if IF_UOM is supplied.
      ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_SBLAR-SBLAR
                                                          IF_CALCV_UOM_1 = LS_SBLAR-SBLAR_UOM
                                                          IF_CALCV_2     = LS_SURWT-SURWT
                                                          IF_CALCV_UOM_2 = LS_SURWT-SURWT_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_MULTIPLY
                                                          IF_UOM         = IF_UOM
                                                importing EF_CALCV       = RS_SBLWT-SBLWT
                                                          EF_CALCV_UOM   = RS_SBLWT-SBLWT_UOM ).
    else.
      ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_SBLAR-SBLAR
                                                          IF_CALCV_UOM_1 = LS_SBLAR-SBLAR_UOM
                                                          IF_CALCV_2     = LS_SURWT-SURWT
                                                          IF_CALCV_UOM_2 = LS_SURWT-SURWT_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_MULTIPLY
                                                importing EF_CALCV       = RS_SBLWT-SBLWT
                                                          EF_CALCV_UOM   = RS_SBLWT-SBLWT_UOM ).
    endif.

  endmethod.
  method ZIF_VCXI_PXSS_FDI~GET_DATA.

    data: LT_SCORE_COM type ZVCXI_PXSS_TT_SCORE_COM,
          LT_TAPE_COM  type ZVCXI_PXSS_TT_TAPE_COM.
    data: LR_DATA_SUPER   type ref to DATA,
          LR_PR_SCORE_MGR type ref to ZCL_VCXI_PXSS_PR_SCORE_MGR.
    field-symbols: <S_FDI_CRPRDTL>   type ZVCXI_P1SS_S_FDI_CRPRDTL.


***--------------------------------------------------------------------------------------
*** Create Data Reference
    create data RR_DATA type ZVCXI_P1SS_S_FDI_CRPRDTL.
    assign RR_DATA->* to <S_FDI_CRPRDTL>.


***--------------------------------------------------------------------------------------
*** Get FEFCO
    <S_FDI_CRPRDTL>-FEFCO = ME->GET_FEFCO( ).

*** Get Part of Box
    <S_FDI_CRPRDTL>-BOXPA = ME->GET_BOXPA( ).

*** Get Product Dimension
    <S_FDI_CRPRDTL>-PRODIM_DESCR = ME->GET_PRODIM_DESCR( ).
    <S_FDI_CRPRDTL>-S_PROWI      = ME->GET_PROWI( ).
    <S_FDI_CRPRDTL>-S_PROLE      = ME->GET_PROLE( ).
    <S_FDI_CRPRDTL>-S_PROHE      = ME->GET_PROHE( ).
    <S_FDI_CRPRDTL>-S_PROH2      = ME->GET_PROH2( ).

*** Get Product Sheet Dimension
    <S_FDI_CRPRDTL>-S_SHTPW = ME->GET_SHTPW( ).
    <S_FDI_CRPRDTL>-S_SHTPL = ME->GET_SHTPL( ).

*** Get Overlap/Tuck in/Dust Flap
    <S_FDI_CRPRDTL>-S_OVLAP = ME->GET_OVLAP( ).
    <S_FDI_CRPRDTL>-S_FTUCK = ME->GET_FTUCK( ).
    <S_FDI_CRPRDTL>-S_FDUST = ME->GET_FDUST( ).

***------------------------------------------------------------------------------------------------
*** Get Board ID
    <S_FDI_CRPRDTL>-BOARD = ME->GET_BOARD( ).

*** Get Flute Orientation
    <S_FDI_CRPRDTL>-FLUTO = ME->GET_FLUTO( ).

*** Get Flute, Caliper, Surface Weight, Additional Weight
    <S_FDI_CRPRDTL>-FLUTE   = ME->GET_FLUTE( importing EF_DESCR = <S_FDI_CRPRDTL>-FLUTE_DESCR ).
    <S_FDI_CRPRDTL>-S_CALIP = ME->GET_CALIP( ).
    <S_FDI_CRPRDTL>-S_SURWT = ME->GET_SURWT( ).
    <S_FDI_CRPRDTL>-S_ADDWT = ME->GET_ADDWT( ).

***------------------------------------------------------------------------------------------------
*** Joint Information
    <S_FDI_CRPRDTL>-JOIPO   = ME->GET_JOIPO( ).
    <S_FDI_CRPRDTL>-JOITY   = ME->GET_JOITY( ).
    <S_FDI_CRPRDTL>-S_JOIWI = ME->GET_JOIWI( ).
    <S_FDI_CRPRDTL>-S_JOILE = ME->GET_JOILE( ).

***------------------------------------------------------------------------------------------------
*** Cut Out Area
    <S_FDI_CRPRDTL>-S_COUAR = ME->GET_COUAR( ).

***------------------------------------------------------------------------------------------------
*** Scores & Tapes from Score Manager Provider
    LR_PR_SCORE_MGR = ME->GET_PRVDR_SCORE_MGR( ).
    if LR_PR_SCORE_MGR is bound.
      LT_SCORE_COM = LR_PR_SCORE_MGR->GET_SCORE_COM( ).
      LT_TAPE_COM  = LR_PR_SCORE_MGR->GET_TAPE_COM( ).

***   Score String Length
      ZCL_VCXI_P0SSC_PRSTY=>GET_CSCOR( exporting IF_DIMEN = ZCL_VCXI_PXSS_BL_SCORE=>C_DIMEN_LENGTH
                                       importing EF_DESCR = <S_FDI_CRPRDTL>-SSTRL_DESCR ).
      <S_FDI_CRPRDTL>-SSTRL = ZCL_VCXI_PXSS_BL_SCORE=>GET_SCSTR_COM( IT_SCORE_COM = LT_SCORE_COM
                                                                 IF_DIMEN     = ZCL_VCXI_PXSS_BL_SCORE=>C_DIMEN_LENGTH ).

***   Score String Cross
      ZCL_VCXI_P0SSC_PRSTY=>GET_CSCOR( exporting IF_DIMEN = ZCL_VCXI_PXSS_BL_SCORE=>C_DIMEN_WIDTH
                                       importing EF_DESCR = <S_FDI_CRPRDTL>-SSTRC_DESCR ).
      <S_FDI_CRPRDTL>-SSTRC = ZCL_VCXI_PXSS_BL_SCORE=>GET_SCSTR_COM( IT_SCORE_COM = LT_SCORE_COM
                                                                 IT_TAPE_COM  = LT_TAPE_COM
                                                                 IF_DIMEN     = ZCL_VCXI_PXSS_BL_SCORE=>C_DIMEN_WIDTH ).
    endif.

  endmethod.
  method ZIF_VCXI_PXSS_FDI~SET_DATA.

    data: LS_FDI_CRPRDTL_OLD type ZVCXI_P1SS_S_FDI_CRPRDTL,
          LS_FDI_CRPRDTL_NEW type ZVCXI_P1SS_S_FDI_CRPRDTL.
    data: LR_DATA_OLD  type ref to DATA.
    field-symbols: <S_DATA> type ANY.


***--------------------------------------------------------------------------------------
*** Dereference Data
    LR_DATA_OLD = cond #( when IR_DATA_OLD is supplied then IR_DATA_OLD
                                                       else ME->ZIF_VCXI_PXSS_FDI~GET_DATA( ) ).
    if LR_DATA_OLD is bound.
      assign LR_DATA_OLD->* to <S_DATA>.
      LS_FDI_CRPRDTL_OLD = corresponding #( <S_DATA> ).
    endif.

    if IR_DATA is bound.
      assign IR_DATA->* to <S_DATA>.
      LS_FDI_CRPRDTL_NEW = corresponding #( <S_DATA> ).
    endif.


***--------------------------------------------------------------------------------------
*** Set FEFCO
    if LS_FDI_CRPRDTL_OLD-FEFCO ne LS_FDI_CRPRDTL_NEW-FEFCO.
      ME->SET_FEFCO( IF_FEFCO = LS_FDI_CRPRDTL_NEW-FEFCO
                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Part of Box
    if LS_FDI_CRPRDTL_OLD-BOXPA ne LS_FDI_CRPRDTL_NEW-BOXPA.
      ME->SET_BOXPA( IF_BOXPA = LS_FDI_CRPRDTL_NEW-BOXPA
                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Product Length
    if LS_FDI_CRPRDTL_OLD-S_PROLE ne LS_FDI_CRPRDTL_NEW-S_PROLE.
      ME->SET_PROLE( IS_PROLE = LS_FDI_CRPRDTL_NEW-S_PROLE
                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Product Width
    if LS_FDI_CRPRDTL_OLD-S_PROWI ne LS_FDI_CRPRDTL_NEW-S_PROWI.
      ME->SET_PROWI( IS_PROWI = LS_FDI_CRPRDTL_NEW-S_PROWI
                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Product Height
    if LS_FDI_CRPRDTL_OLD-S_PROHE ne LS_FDI_CRPRDTL_NEW-S_PROHE.
      ME->SET_PROHE( IS_PROHE = LS_FDI_CRPRDTL_NEW-S_PROHE
                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Product Height2
    if LS_FDI_CRPRDTL_OLD-S_PROH2 ne LS_FDI_CRPRDTL_NEW-S_PROH2.
      ME->SET_PROH2( IS_PROH2 = LS_FDI_CRPRDTL_NEW-S_PROH2
                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Product Sheet Length
    if LS_FDI_CRPRDTL_OLD-S_SHTPL ne LS_FDI_CRPRDTL_NEW-S_SHTPL.
      ME->SET_SHTPL( IS_SHTPL = LS_FDI_CRPRDTL_NEW-S_SHTPL
                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Product Sheet Width
    if LS_FDI_CRPRDTL_OLD-S_SHTPW ne LS_FDI_CRPRDTL_NEW-S_SHTPW.
      ME->SET_SHTPW( IS_SHTPW = LS_FDI_CRPRDTL_NEW-S_SHTPW
                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Overlap
    if LS_FDI_CRPRDTL_OLD-S_OVLAP ne LS_FDI_CRPRDTL_NEW-S_OVLAP.
      ME->SET_OVLAP( IS_OVLAP = LS_FDI_CRPRDTL_NEW-S_OVLAP
                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Tuck in Flap
    if LS_FDI_CRPRDTL_OLD-S_FTUCK ne LS_FDI_CRPRDTL_NEW-S_FTUCK.
      ME->SET_FTUCK( IS_FTUCK = LS_FDI_CRPRDTL_NEW-S_FTUCK
                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Dust Flap
    if LS_FDI_CRPRDTL_OLD-S_FDUST ne LS_FDI_CRPRDTL_NEW-S_FDUST.
      ME->SET_FDUST( IS_FDUST = LS_FDI_CRPRDTL_NEW-S_FDUST
                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Board
    if LS_FDI_CRPRDTL_OLD-BOARD ne LS_FDI_CRPRDTL_NEW-BOARD.
      ME->SET_BOARD( IF_BOARD = LS_FDI_CRPRDTL_NEW-BOARD
                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Flute Orientation
    if LS_FDI_CRPRDTL_OLD-FLUTO ne LS_FDI_CRPRDTL_NEW-FLUTO.
      ME->SET_FLUTO( IF_FLUTO = LS_FDI_CRPRDTL_NEW-FLUTO
                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Flute
    if LS_FDI_CRPRDTL_OLD-FLUTE ne LS_FDI_CRPRDTL_NEW-FLUTE.
      ME->SET_FLUTE( IF_FLUTE = LS_FDI_CRPRDTL_NEW-FLUTE
                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Caliper
    if LS_FDI_CRPRDTL_OLD-S_CALIP ne LS_FDI_CRPRDTL_NEW-S_CALIP.
      ME->SET_CALIP( IS_CALIP = LS_FDI_CRPRDTL_NEW-S_CALIP
                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Surface Weight
    if LS_FDI_CRPRDTL_OLD-S_SURWT ne LS_FDI_CRPRDTL_NEW-S_SURWT.
      ME->SET_SURWT( IS_SURWT = LS_FDI_CRPRDTL_NEW-S_SURWT
                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Additional Weight
    if LS_FDI_CRPRDTL_OLD-S_ADDWT ne LS_FDI_CRPRDTL_NEW-S_ADDWT.
      ME->SET_ADDWT( IS_ADDWT = LS_FDI_CRPRDTL_NEW-S_ADDWT
                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Joint Position
    if LS_FDI_CRPRDTL_OLD-JOIPO ne LS_FDI_CRPRDTL_NEW-JOIPO.
      ME->SET_JOIPO( IF_JOIPO = LS_FDI_CRPRDTL_NEW-JOIPO
                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Joint Type
    if LS_FDI_CRPRDTL_OLD-JOITY ne LS_FDI_CRPRDTL_NEW-JOITY.
      ME->SET_JOITY( IF_JOITY = LS_FDI_CRPRDTL_NEW-JOITY
                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Joint Width
    if LS_FDI_CRPRDTL_OLD-S_JOIWI ne LS_FDI_CRPRDTL_NEW-S_JOIWI.
      ME->SET_JOIWI( IS_JOIWI = LS_FDI_CRPRDTL_NEW-S_JOIWI
                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Joint Length
    if LS_FDI_CRPRDTL_OLD-S_JOILE ne LS_FDI_CRPRDTL_NEW-S_JOILE.
      ME->SET_JOILE( IS_JOILE = LS_FDI_CRPRDTL_NEW-S_JOILE
                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Cut Out Area
    if LS_FDI_CRPRDTL_OLD-S_COUAR ne LS_FDI_CRPRDTL_NEW-S_COUAR.
      ME->SET_COUAR( IS_COUAR = LS_FDI_CRPRDTL_NEW-S_COUAR
                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method ZIF_VCXI_PXSS_PRINT_COM~GET_PRINT_COM.

    data: LS_PARAM  type /VCXI/VKSR_S_PARAM.

*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PRINT ).

    if LS_PARAM-STRNG is not initial.
      call transformation ID
           source xml LS_PARAM-STRNG
           result T_PRINT = RT_PRINT_COM.
    endif.

  endmethod.
  method ZIF_VCXI_PXSS_PRINT_COM~IS_EDIT_ALLOWED.

    RF_EDITABLE = ABAP_TRUE.

  endmethod.
  method ZIF_VCXI_PXSS_PRINT_COM~SET_PRINT_COM.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PRINT ).

    if IT_PRINT_COM is supplied.
      call transformation ID
           source T_PRINT = IT_PRINT_COM
           result xml LS_PARAM-STRNG.
    endif.
    if IF_SETBY is initial.
      clear LS_PARAM-STRNG.
    endif.
    LS_PARAM-SETBY = IF_SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_PRINT_CHANGED ).
    endif.

  endmethod.
