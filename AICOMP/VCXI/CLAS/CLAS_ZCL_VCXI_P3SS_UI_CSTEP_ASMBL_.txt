
class ZCL_VCXI_P3SS_UI_CSTEP_ASMBL definition
  public
  inheriting from ZCL_VCXI_P3SS_UI_CSTEP
  create public .

public section.

  data S_CSTEP_ASMBL_VIS type ZVCXI_P3SS_S_CSTEP_ASMBL_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
protected section.

  data R_GRID_JOINT type ref to ZCL_VCXI_PXSS_GRID_JOINT .
  data R_GRID_XPART type ref to ZCL_VCXI_PXSS_GRID_XPART .
  data R_PR_CSTEP_ASMBL type ref to ZCL_VCXI_P3SS_PR_CSTEP_ASMBL .
  data R_TOOLBAR type ref to ZCL_VCXI_XCS_TOOLBAR_PRVDR .

  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_P3SS_UI_CSTEP_ASMBL definition
class ZCL_VCXI_P3SS_UI_CSTEP_ASMBL implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

***--------------------------------------------------------------------------------------
*** Bind UI to Dynpro
    call function 'ZVCXI_P3SS_UI_CSTEP_ASMBL_BIND'
      exporting
        IR_UI_CSTEP_ASMBL = ME
      importing
        ES_DYNP           = ME->S_DYNP.

***--------------------------------------------------------------------------------------
*** Create Grid for Joints
    create object ME->R_GRID_JOINT
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_JOINT'.

*** Create Grid for Parts
    create object ME->R_GRID_XPART
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_XPART'.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_CSTEP_ASMBL.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

    if ME->R_GRID_JOINT is bound.
      ME->R_GRID_JOINT->DO_FREE( ).
      clear ME->R_GRID_JOINT.
    endif.
    if ME->R_GRID_XPART is bound.
      ME->R_GRID_XPART->DO_FREE( ).
      clear ME->R_GRID_XPART.
    endif.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Super PBO
    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize GRID
    if ME->R_GRID_JOINT is bound.
      ME->R_GRID_JOINT->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_JOINT->REFRESH_GRID( ).
    endif.
    if ME->R_GRID_XPART is bound.
      ME->R_GRID_XPART->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_XPART->REFRESH_GRID( ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_CSTEP_ASMBL_VIS type ZVCXI_P3SS_S_CSTEP_ASMBL_VIS.


*** Call SUPER
    SUPER->DO_UPDATE_VIS( IS_CSTEP_VIS = IS_CSTEP_VIS ).

    check ME->R_PR_CSTEP_ASMBL is bound.

*** "Cast" Vis Structure
    move-corresponding IS_CSTEP_VIS to LS_CSTEP_ASMBL_VIS.

***--------------------------------------------------------------------------------------
*** Update Handling Dimension
    if ME->S_CSTEP_ASMBL_VIS-S_HNDDIM ne LS_CSTEP_ASMBL_VIS-S_HNDDIM.
      ME->R_PR_CSTEP_ASMBL->SET_HNDDIM( IS_HNDDIM = LS_CSTEP_ASMBL_VIS-S_HNDDIM
                                        IF_SETBY  = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method INIT_VIS.

    SUPER->INIT_VIS( ).

    free: ME->S_CSTEP_ASMBL_VIS.
    ME->S_CSTEP_ASMBL_VIS-S_CSTEP_VIS = ME->S_CSTEP_VIS.

***--------------------------------------------------------------------------------------
*** Handling Dimension
    ME->S_CSTEP_ASMBL_VIS-S_HNDDIM = ME->R_PR_CSTEP_ASMBL->GET_HNDDIM( ).

  endmethod.
