
class ZCL_VCXI_P2SS_PR_FCSHT definition
  public
  inheriting from ZCL_VCXI_PXSS_PR_XSHT
  create public .

public section.

  interfaces ZIF_VCXI_P2SS_COMM_PREV .

  methods GET_FCBPX
    exporting
      !EF_FCBPX type ZVCXI_P2S_FCBPX
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RR_BL_FCBPROP) type ref to ZCL_VCXI_P2SS_BL_FCBPROP
    raising
      /VCXI/CX_CKX .
  methods GET_FCFPX
    exporting
      !EF_FCFPX type ZVCXI_P2S_FCFPX
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RR_BL_FCFPROP) type ref to ZCL_VCXI_P2SS_BL_FCFPROP
    raising
      /VCXI/CX_CKX .

  methods GET_CALIP
    redefinition .
  methods GET_HNDDIM
    redefinition .
  methods GET_PRODH
    redefinition .
  methods GET_SURWT
    redefinition .
  methods ZIF_VCXI_PXSS_COMM_PREV~DO_FORWARD
    redefinition .
  methods GET_ADDWT
    redefinition .
protected section.

  constants C_IEVNT_O_SURWT_CHANGED type /VCXI/VKSC_IEVNT value 'SURWT_CHANGED' ##NO_TEXT.
  constants C_PARID_FCFPX type /VCXI/VKS_PARID value 'ZVCXI_P2_FCFPX' ##NO_TEXT.
  constants C_PARID_SURWT type /VCXI/VKS_PARID value 'ZVCXI_PX_SURWT' ##NO_TEXT.
  constants C_IEVNT_O_FCFPX_CHANGED type /VCXI/VKSC_IEVNT value 'FCFPX_CHANGED' ##NO_TEXT.

  methods SET_FCFPX
    importing
      !IF_FCFPX type ZVCXI_P2S_FCFPX
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_SURWT
    importing
      !IS_SURWT type ZVCXI_PXS_S_SURWT
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_P2SS_PR_FCSHT definition
class ZCL_VCXI_P2SS_PR_FCSHT implementation.
  method GET_ADDWT.

    data: LR_BL_FCFPROP type ref to ZCL_VCXI_P2SS_BL_FCFPROP.


*** Get Folding Carton Forward Property XML (Outbound)
    LR_BL_FCFPROP = ME->ZIF_VCXI_P2SS_COMM_PREV~GET_FCFPX( ).
    check LR_BL_FCFPROP is bound.

*** Get Additional Weight
    RS_ADDWT = LR_BL_FCFPROP->S_ADDWT.

  endmethod.
  method GET_CALIP.

    data: LR_BL_FCFPROP type ref to ZCL_VCXI_P2SS_BL_FCFPROP.


*** Get Folding Carton Forward Property XML (Outbound)
    LR_BL_FCFPROP = ME->ZIF_VCXI_P2SS_COMM_PREV~GET_FCFPX( ).
    check LR_BL_FCFPROP is bound.

*** Get Caliper
    RS_CALIP = LR_BL_FCFPROP->S_CALIP.

  endmethod.
  method GET_FCBPX.

    data: LT_SCORE  type ZVCXI_PXSS_TT_SCORE_COM.


***--------------------------------------------------------------------------------------
*** Create fresh instance of Folding Carton Backward Property
    RR_BL_FCBPROP = ZCL_VCXI_P2SS_BL_FCBPROP=>FACTORY( ).

    if RR_BL_FCBPROP is bound.
***   Set Sheet Scores
      LT_SCORE = ZCL_VCXI_PXSS_BL_SCORE=>GET_SCORE_SHTN( IS_SHTNW = ME->GET_SHTWI( )
                                                         IS_SHTNL = ME->GET_SHTLE( ) ).
      RR_BL_FCBPROP->SET_SCORE( IT_SCORE = LT_SCORE ).

***   Sheet ECMA
      RR_BL_FCBPROP->SET_ECMA_SHEET( ).

***   Cost Split Distribution
      RR_BL_FCBPROP->SET_CSPLIT( IT_CSPLIT = ME->GET_CSPLIT( ) ).
    endif.

***--------------------------------------------------------------------------------------
*** Return Folding Carton Backward Property XML (Outbound)
    if EF_FCBPX is requested.
      move RR_BL_FCBPROP->GET_FCBPX( ) to EF_FCBPX.
    endif.

  endmethod.
  method GET_FCFPX.

***--------------------------------------------------------------------------------------
*** Get Folding Carton Forward Property (Inbound)
    RR_BL_FCFPROP = ME->ZIF_VCXI_P2SS_COMM_PREV~GET_FCFPX( importing EF_SETBY = EF_SETBY ).

***--------------------------------------------------------------------------------------
*** Get new Folding Carton Forward Property XML
    if EF_FCFPX is requested.
      EF_FCFPX = RR_BL_FCFPROP->GET_FCFPX( ).
    endif.

  endmethod.
  method GET_HNDDIM.

    data: LR_BL_FCFPROP type ref to ZCL_VCXI_P2SS_BL_FCFPROP.


*** Get Folding Carton Forward Property XML (Outbound)
    LR_BL_FCFPROP = ME->GET_FCFPX( ).
    check LR_BL_FCFPROP is bound.

*** Get Handling Dimension
    if ME->GET_FDIMT( ) ne ABAP_TRUE.
      RS_HNDDIM = LR_BL_FCFPROP->S_HNDDIM.
    else.
      RS_HNDDIM = value #( S_HNDLE = LR_BL_FCFPROP->S_HNDDIM-S_HNDWI
                           S_HNDWI = LR_BL_FCFPROP->S_HNDDIM-S_HNDLE
                           S_HNDHE = LR_BL_FCFPROP->S_HNDDIM-S_HNDHE ).
    endif.

  endmethod.
  method GET_PRODH.

    data: LR_BL_FCFPROP type ref to ZCL_VCXI_P2SS_BL_FCFPROP.


*** Get Folding Carton Forward Property XML (Outbound)
    LR_BL_FCFPROP = ME->ZIF_VCXI_P2SS_COMM_PREV~GET_FCFPX( ).
    check LR_BL_FCFPROP is bound.

*** Get Product Hierarchy
    RF_PRODH = LR_BL_FCFPROP->F_PRODH.

    check RF_PRODH is not initial.

    if EF_DESCR is requested.
      EF_DESCR = ZCL_VCXI_XCS_SERVICE=>GET_PRODH_DESCR( IF_PRODH = RF_PRODH ).
    endif.

  endmethod.
  method GET_SURWT.

***--------------------------------------------------------------------------------------
*** Get Surface Weight (Inbound)
    RS_SURWT = ME->ZIF_VCXI_P2SS_COMM_PREV~GET_SURWT( ).

  endmethod.
  method SET_FCFPX.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_FCFPX ).

    move IF_FCFPX   to LS_PARAM-STRNG.
    move IF_SETBY   to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_FCFPX_CHANGED ).
    endif.

  endmethod.
  method SET_SURWT.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SURWT ).

    move IS_SURWT-SURWT     to LS_PARAM-ATFLV.
    move IS_SURWT-SURWT_UOM to LS_PARAM-UNIT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_SURWT_CHANGED ).
    endif.

  endmethod.
  method ZIF_VCXI_P2SS_COMM_PREV~GET_FCBPX.

***--------------------------------------------------------------------------------------
*** Get Folding Carton Backward Property (Outbound)
    RR_BL_FCBPROP = ME->GET_FCBPX( ).

***--------------------------------------------------------------------------------------
*** Get Folding Carton Backward Property Instance
    if EF_FCBPX is requested.
      move RR_BL_FCBPROP->GET_FCBPX( ) to EF_FCBPX.
    endif.

  endmethod.
  method ZIF_VCXI_P2SS_COMM_PREV~GET_FCFPX.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

***--------------------------------------------------------------------------------------
*** Get Parameter
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_FCFPX ).
    move LS_PARAM-STRNG to EF_FCFPX.
    move LS_PARAM-SETBY to EF_SETBY.

***--------------------------------------------------------------------------------------
*** Get Folding Carton Forward Property Instance
    if RR_BL_FCFPROP is requested.
      RR_BL_FCFPROP = ZCL_VCXI_P2SS_BL_FCFPROP=>FACTORY( IF_FCFPX = EF_FCFPX ).
    endif.

  endmethod.
  method ZIF_VCXI_P2SS_COMM_PREV~GET_NOUPS.

*** Get Number of Ups (Outbound)
    RF_NOUPS = ME->GET_NOUPS( importing EF_SETBY = EF_SETBY ).

  endmethod.
  method ZIF_VCXI_P2SS_COMM_PREV~GET_SHTDIM.

    ME->ZIF_VCXI_P2SS_COMM_PREV~GET_FCBPX( )->GET_SHTDIM( importing ES_SHTLE = ES_SHTLE
                                                                    ES_SHTWI = ES_SHTWI ).

  endmethod.
  method ZIF_VCXI_P2SS_COMM_PREV~GET_SURWT.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SURWT ).
    move LS_PARAM-ATFLV to RS_SURWT-SURWT.
    move LS_PARAM-UNIT  to RS_SURWT-SURWT_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method ZIF_VCXI_PXSS_COMM_PREV~DO_FORWARD.

    data: LS_SURWT type ZVCXI_PXS_S_SURWT.
    data: LR_GATE            type ref to /VCXI/CL_VKSR_GATE,
          LR_COMM_NEXT       type ref to ZIF_VCXI_P2SS_COMM_NEXT,
          LR_BL_FCFPROP      type ref to ZCL_VCXI_P2SS_BL_FCFPROP,
          LR_BL_FCFPROP_NEXT type ref to ZCL_VCXI_P2SS_BL_FCFPROP.

***------------------------------------------------------------------------------------------------
*** Process all provided Gates
    loop at IT_GATE into LR_GATE.
      try.
          move LR_GATE->R_CLSPR ?to LR_COMM_NEXT.
        catch CX_SY_MOVE_CAST_ERROR.
          clear LR_COMM_NEXT.
      endtry.
      check LR_COMM_NEXT is bound.

***   Get Surface Weight
      LS_SURWT = LR_COMM_NEXT->GET_SURWT( ).

***   Combine Folding Carton Forward Property
      LR_BL_FCFPROP_NEXT = LR_COMM_NEXT->GET_FCFPX( ).
      if LR_BL_FCFPROP is not bound.
        move LR_BL_FCFPROP_NEXT to LR_BL_FCFPROP.
      else.
        LR_BL_FCFPROP->MERGE( IR_BL_FCFPROP = LR_BL_FCFPROP_NEXT ).
      endif.
    endloop.

*** Forward Surface Weight
    ME->SET_SURWT( IS_SURWT = LS_SURWT ).

*** Forward Folding Carton Forward Property XML
    if LR_BL_FCFPROP is bound.
      ME->SET_FCFPX( IF_FCFPX = LR_BL_FCFPROP->GET_FCFPX( ) ).
    else.
      ME->SET_FCFPX( IF_FCFPX = SPACE ).
    endif.

  endmethod.
