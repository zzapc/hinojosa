
class ZCL_VCXI_FFSS_POPUP_PSCALE definition
  public
  create protected .

public section.

  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods DO_USER_COMMAND
    importing
      !IF_UCOMM type SYUCOMM
    raising
      /VCXI/CX_CKX .
  class-methods START
    importing
      !IF_LOCKED type /VCXI/CKX_LOCKED optional
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
    raising
      /VCXI/CX_CKX .
protected section.

  constants C_UC_CANCEL type SYUCOMM value 'CANCEL' ##NO_TEXT.
  constants C_UC_DELETE type SYUCOMM value 'DELETE' ##NO_TEXT.
  constants C_UC_OKAY type SYUCOMM value 'OKAY' ##NO_TEXT.
  data F_ISVCE type ZVCXI_XCS_ISVCE .
  data F_LOCKED type /VCXI/CKX_LOCKED .
  data F_REFRESH_REQ type ABAP_BOOL .
  data R_CONTAINER type ref to CL_GUI_CUSTOM_CONTAINER .
  data R_GATE type ref to /VCXI/CL_VKSR_GATE .
  data R_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE .
  data R_GRID_PSCALE type ref to ZCL_VCXI_XCS_GRID_PSCALE .

  methods CONSTRUCTOR
    importing
      !IF_LOCKED type /VCXI/CKX_LOCKED
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
    raising
      /VCXI/CX_CKX .
  methods DO_FREE
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_CANCEL .
  methods HANDLE_UC_OKAY
    raising
      /VCXI/CX_CKX .
  methods SET_PF_STATUS
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_FFSS_POPUP_PSCALE definition
class ZCL_VCXI_FFSS_POPUP_PSCALE implementation.
method CONSTRUCTOR.

  move IR_GATE        to ME->R_GATE.
  move IR_UIGATE      to ME->R_UIGATE.
  move IF_LOCKED      to ME->F_LOCKED.

*** Create Grid for Price Scale
  create object ME->R_GRID_PSCALE
    exporting
      IR_GATE           = ME->R_GATE
      IR_UIGATE         = ME->R_UIGATE
      IF_CONTAINER_NAME = '0100CC_GRID'.

endmethod.
method DO_FREE.

*** Free Grid
  if ME->R_GRID_PSCALE is bound.
    ME->R_GRID_PSCALE->DO_FREE( ).
    clear ME->R_GRID_PSCALE.
  endif.

endmethod.
method DO_PBO.

*** Set PF-Status
  ME->SET_PF_STATUS( ).

*** Refresh Grid
  if ME->R_GRID_PSCALE is bound.
    if ME->R_GRID_PSCALE->F_REFRESH_REQ eq ABAP_FALSE.
      move ABAP_TRUE to ME->R_GRID_PSCALE->F_REFRESH_REQ.
    endif.
    ME->R_GRID_PSCALE->REFRESH_GRID( ).
  endif.

endmethod.
method DO_USER_COMMAND.

  case IF_UCOMM.

    when C_UC_OKAY.
      ME->HANDLE_UC_OKAY( ).

    when C_UC_CANCEL.
      ME->HANDLE_UC_CANCEL( ).

  endcase.

endmethod.
method HANDLE_UC_CANCEL.

***--------------------------------------------------------------------------------------
*** Leave Popup
  leave to screen 0.

endmethod.
method HANDLE_UC_OKAY.

*** Raise Event
  ME->R_GATE->RAISE_EVENTS( ).

*** Leave Popup
  leave to screen 0.

endmethod.
method SET_PF_STATUS.

  data: LT_EXCLUDE type table of SYUCOMM.

  if ME->F_LOCKED ne ABAP_TRUE.
    append C_UC_OKAY to LT_EXCLUDE.
  endif.

  append C_UC_DELETE to LT_EXCLUDE.

  set pf-status '0100' of program 'SAPLZVCXI_FFSS_POPUP_PSCALE'
      excluding LT_EXCLUDE.

endmethod.
method START.

  data:  LR_POPUP_PSCALE type ref to ZCL_VCXI_FFSS_POPUP_PSCALE.

*** Create Instance
    create object LR_POPUP_PSCALE
     exporting
       IR_GATE   = IR_GATE
       IR_UIGATE = IR_UIGATE
       IF_LOCKED = IF_LOCKED.

*** Call Popup
    call function 'ZVCXI_FFSS_POPUP_PSCALE_START'
      exporting
       IR_POPUP_PSCALE = LR_POPUP_PSCALE.

*** Free the Popup
    LR_POPUP_PSCALE->DO_FREE( ).

endmethod.
