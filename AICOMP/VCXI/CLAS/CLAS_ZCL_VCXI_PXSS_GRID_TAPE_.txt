
class ZCL_VCXI_PXSS_GRID_TAPE definition
  public
  inheriting from ZCL_VCXI_XCS_GRID_PRVDR
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABNAME type TABNAME default 'ZVCXI_PXSS_S_TAPE_GRID'
      !IF_CONTAINER_NAME type SCRFNAME
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL default ABAP_TRUE
    raising
      /VCXI/CX_CKX .
protected section.

  methods CHANGE_STYLE_TAPE
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IS_DATA type ANY
    changing
      !CT_STYL type LVC_T_STYL
    raising
      /VCXI/CX_CKX .
  methods GET_TAPE_VIS
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    changing
      !CS_DATA type ANY
    raising
      /VCXI/CX_CKX .
  methods SET_TAPE_VIS
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IS_DATA type ANY
    raising
      /VCXI/CX_CKX .

  methods GET_DISABLED_ADD
    redefinition .
  methods GET_FIELDCAT
    redefinition .
  methods GET_GRID_DATA
    redefinition .
  methods HANDLE_DATA_CHANGED_FINISHED
    redefinition .
  methods INIT_STYLE
    redefinition .
  methods GET_DISABLED_DEL
    redefinition .
private section.
endclass. "ZCL_VCXI_PXSS_GRID_TAPE definition
class ZCL_VCXI_PXSS_GRID_TAPE implementation.
  method CHANGE_STYLE_TAPE.

    data: LS_TAPE_VIS type ZVCXI_PXSS_S_TAPE_VIS.
    data: LR_PR_TAPE type ref to ZCL_VCXI_PXSS_PR_TAPE.


    free CT_STYL.

***--------------------------------------------------------------------------------------
*** Get PR Class
    try.
        move IR_GATE->R_CLSPR ?to LR_PR_TAPE.
      catch CX_SY_MOVE_CAST_ERROR.
    endtry.
    check LR_PR_TAPE is bound.

*** "Cast" generic structure to Vis Structure
    move-corresponding IS_DATA to LS_TAPE_VIS.


***------------------------------------------------------------------
*** Set Tape Type = Enable
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'TTAPE'
                                IF_STYLE     = ME->F_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).

*** Set Tape Position = Enable
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'TAPOS'
                                IF_STYLE     = ME->F_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).


  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_GATE           = IR_GATE
                        IR_UIGATE         = IR_UIGATE
                        IF_TABNAME        = IF_TABNAME
                        IF_CONTAINER_NAME = IF_CONTAINER_NAME
                        IF_APPL_EVENTS    = IF_APPL_EVENTS ).

*** Define Provider Type for Button
    ME->F_IPRVT = ZCL_VCXI_PXSS_PR_TAPE=>C_IPRVT_PX_TAPE.

  endmethod.
  method GET_DISABLED_ADD.

    data: LF_CADLS type ZVCXI_PXS_CADLS.


*** Get CAD Lock Status
    LF_CADLS = cast ZCL_VCXI_PXSS_PR_SCORE_MGR( ME->R_GATE->R_CLSPR )->GET_CADLS( ).

    if ( LF_CADLS eq ZCL_VCXI_PXSS_PR_CADIF_PRJ=>C_CADLS_REVISION or
         LF_CADLS eq ZCL_VCXI_PXSS_PR_CADIF_PRJ=>C_CADLS_LOCKED ).
***   Disable Tapes in case of CAD
      RF_DISABLED = ABAP_TRUE.
    else.
      RF_DISABLED = SUPER->GET_DISABLED_ADD( ).
    endif.

  endmethod.
  method GET_DISABLED_DEL.

    data: LF_CADLS type ZVCXI_PXS_CADLS.


*** Get CAD Lock Status
    LF_CADLS = cast ZCL_VCXI_PXSS_PR_SCORE_MGR( ME->R_GATE->R_CLSPR )->GET_CADLS( ).

    if ( LF_CADLS eq ZCL_VCXI_PXSS_PR_CADIF_PRJ=>C_CADLS_REVISION or
         LF_CADLS eq ZCL_VCXI_PXSS_PR_CADIF_PRJ=>C_CADLS_LOCKED ).
***   Disable Tapes in case of CAD
      RF_DISABLED = ABAP_TRUE.
    else.
      RF_DISABLED = SUPER->GET_DISABLED_DEL( ).
    endif.

  endmethod.
  method GET_FIELDCAT.

    data: LF_LOCKED type  /VCXI/CKX_LOCKED,
          LF_INUSE  type  /VCXI/VKSR_INUSE.
    field-symbols:  <S_FCAT>  type  LVC_S_FCAT.

***--------------------------------------------------------------------------------------
*** Get Standard Field Catalog
    RT_FCAT = SUPER->GET_FIELDCAT( ).

*** Get Lock and In-Use Status
    LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
    LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***--------------------------------------------------------------------------------------
*** Adjust Field Catalog
    loop at RT_FCAT assigning <S_FCAT>.
      case <S_FCAT>-FIELDNAME.
        when 'POSID' or 'GATKY' or 'R_GATE' or 'T_STYL'.
          move ABAP_TRUE    to <S_FCAT>-TECH.

        when 'TTAPE'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE    to <S_FCAT>-EDIT.
          endif.

        when 'TAPOS'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE    to <S_FCAT>-EDIT.
          endif.

      endcase.
    endloop.

  endmethod.
  method GET_GRID_DATA.

    data: LS_LAYOUT type LVC_S_LAYO.
    data: LT_GATE type /VCXI/VKSR_TT_GATE.
    data: LR_GATE type ref to /VCXI/CL_VKSR_GATE.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type          ANY,
                   <T_STYL> type          LVC_T_STYL.

***--------------------------------------------------------------------------------------
*** Dereferencing of Data Reference
    assign IR_DATA->* to <T_DATA>.

*** Get Layout
    LS_LAYOUT = ME->GET_LAYOUT( ).

*** Get Gates of Provider
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ME->F_IPRVT
                                     IF_INUSE  = ABAP_TRUE
                                     IF_SORTED = ABAP_TRUE ).

***--------------------------------------------------------------------------------------
    loop at LT_GATE into LR_GATE.
***   Add Line with Gate Key Data
      append initial line to <T_DATA> assigning <S_DATA>.
      move-corresponding value ZVCXI_PXSS_S_TAPE_GRID( POSID  = LR_GATE->GET_POSID( )
                                                       GATKY  = LR_GATE->F_GATKY
                                                       R_GATE = LR_GATE
                                                     ) to <S_DATA>.

***   Get Visualization Data
      ME->GET_TAPE_VIS( exporting IR_GATE = LR_GATE
                        changing  CS_DATA = <S_DATA> ).

***   Set Style Details
      if LS_LAYOUT-STYLEFNAME is not initial.
        assign component LS_LAYOUT-STYLEFNAME of structure <S_DATA> to <T_STYL>.
        if SY-SUBRC eq 0.
          ME->CHANGE_STYLE_TAPE( exporting IR_GATE = LR_GATE
                                           IS_DATA = <S_DATA>
                                  changing CT_STYL = <T_STYL> ).
***       Call DVC Servcie - Modify Style
          ZCL_VCXI_XCDR_SERVICE=>MODIFY_STYLE( exporting IR_GATE = LR_GATE
                                               changing  CT_STYL = <T_STYL> ).
        endif.
      endif.
    endloop.

  endmethod.
  method GET_TAPE_VIS.

    data: LS_TAPE_VIS type ZVCXI_PXSS_S_TAPE_VIS.
    data: LR_PR_TAPE  type ref to ZCL_VCXI_PXSS_PR_TAPE.


***--------------------------------------------------------------------------------------
*** Get PR Class
    try.
        move IR_GATE->R_CLSPR ?to LR_PR_TAPE.
      catch CX_SY_MOVE_CAST_ERROR.
    endtry.
    check LR_PR_TAPE is bound.

*** "Cast" generic structure to Vis Structure
    LS_TAPE_VIS = corresponding #( CS_DATA ).

***--------------------------------------------------------------------------------------
*** Tape Type
    LS_TAPE_VIS-TTAPE = LR_PR_TAPE->GET_TTAPE( importing EF_DESCR = LS_TAPE_VIS-TTAPE_DESCR ).

*** Tape Position
    LS_TAPE_VIS-S_TAPOS = LR_PR_TAPE->GET_TAPOS( ).

***--------------------------------------------------------------------------------------
*** "Cast" Vis Structure back to generic structure
    CS_DATA = corresponding #( base ( CS_DATA ) LS_TAPE_VIS ).

  endmethod.
  method HANDLE_DATA_CHANGED_FINISHED.

    data: LS_TAPE_GRID type ZVCXI_PXSS_S_TAPE_GRID.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type          ANY.

    check E_MODIFIED eq ABAP_TRUE.

    try.
        SUPER->HANDLE_DATA_CHANGED_FINISHED( E_MODIFIED    = E_MODIFIED
                                             ET_GOOD_CELLS = ET_GOOD_CELLS ).

***     Dereferencing of Data Reference
        assign ME->R_DATA->* to <T_DATA>.

        loop at <T_DATA> assigning <S_DATA>.
          move-corresponding <S_DATA> to LS_TAPE_GRID.

          check LS_TAPE_GRID-R_GATE is bound and
                LS_TAPE_GRID-R_GATE->F_FREED is initial.

***       Set Values to PR Class
          ME->SET_TAPE_VIS( IR_GATE = LS_TAPE_GRID-R_GATE
                            IS_DATA = <S_DATA> ).

        endloop.

        ME->R_GATE->RAISE_EVENTS( ).

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

*** Request Refresh for ALV
    move ABAP_TRUE to ME->F_REFRESH_REQ.

  endmethod.
  method INIT_STYLE.

    data: LF_CADLS type ZVCXI_PXS_CADLS.


*** Get CAD Lock Status
    LF_CADLS = cast ZCL_VCXI_PXSS_PR_SCORE_MGR( ME->R_GATE->R_CLSPR )->GET_CADLS( ).

    if ( LF_CADLS eq ZCL_VCXI_PXSS_PR_CADIF_PRJ=>C_CADLS_REVISION or
         LF_CADLS eq ZCL_VCXI_PXSS_PR_CADIF_PRJ=>C_CADLS_LOCKED ).
***   Disable Tapes in case of CAD
      ME->F_STYLE_ENABLED  = CL_GUI_ALV_GRID=>MC_STYLE_DISABLED.
      ME->F_STYLE_DISABLED = CL_GUI_ALV_GRID=>MC_STYLE_DISABLED.
    else.
      SUPER->INIT_STYLE( ).
    endif.

  endmethod.
  method SET_TAPE_VIS.

    data: LS_TAPE_VIS_OLD type        ZVCXI_PXSS_S_TAPE_VIS,
          LS_TAPE_VIS_NEW type        ZVCXI_PXSS_S_TAPE_VIS.
    data: LR_PR_TAPE  type ref to ZCL_VCXI_PXSS_PR_TAPE.

***--------------------------------------------------------------------------------------
*** Get PR Class
    check IR_GATE->R_CLSPR is instance of ZCL_VCXI_PXSS_PR_TAPE.
    LR_PR_TAPE ?= IR_GATE->R_CLSPR.
    check LR_PR_TAPE is bound.

*** Get old Values
    ME->GET_TAPE_VIS( exporting IR_GATE = IR_GATE
                        changing  CS_DATA = LS_TAPE_VIS_OLD ).
*** Get new Values
    LS_TAPE_VIS_NEW = corresponding #( IS_DATA ).


***--------------------------------------------------------------------------------------
*** Tape Type
    if LS_TAPE_VIS_NEW-TTAPE ne LS_TAPE_VIS_OLD-TTAPE.
      LR_PR_TAPE->SET_TTAPE( IF_TTAPE = LS_TAPE_VIS_NEW-TTAPE
                             IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Tape Position
    if LS_TAPE_VIS_NEW-S_TAPOS ne LS_TAPE_VIS_OLD-S_TAPOS.
      LR_PR_TAPE->SET_TAPOS( IS_TAPOS = LS_TAPE_VIS_NEW-S_TAPOS
                             IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
