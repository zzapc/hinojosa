
class ZCL_VCXI_XCM_MBQ_STP_S505 definition
  public
  inheriting from ZCL_VCXI_XCM_MBQ_STP_S000
  create public .

public section.

  methods EXECUTE
    redefinition .
protected section.
private section.
endclass. "ZCL_VCXI_XCM_MBQ_STP_S505 definition
class ZCL_VCXI_XCM_MBQ_STP_S505 implementation.
  method EXECUTE.

    data: LS_MATLI type ZVCXI_XCM_MBQ_S_MATLI,
          LS_KNMT  type KNMT.
    data: LR_BOB_CMIR type ref to ZCL_VCXI_XCI_BOB_CMIR,
          LR_ACTDAT   type ref to DATA.
    field-symbols: <S_ACTDAT_CMIR> type ZVCXI_XCM_MBQ_S_ACTDAT_CMIR.


    try.
***     Get Material List Entry of active Material List Entry
        LS_MATLI = ME->R_DCO_INPUT->T_MATLI[ MLCNT = ME->R_DCO_INPUT->F_MLCNT ].

***     Check Activity - Create Customer-Materials Info Record
        if not LINE_EXISTS( LS_MATLI-T_ACTCON[ ACTID = ZCL_VCXI_XCM_MBQ_CUST=>C_ACTID_CMIR ] ).
***       Set Status to CANCELED
          ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_CANCELED ).
          return.
        endif.

***     Load Activity Control Data
        LR_ACTDAT = LS_MATLI-T_ACTCON[ ACTID = ZCL_VCXI_XCM_MBQ_CUST=>C_ACTID_CMIR ]-R_ACTDAT.
        assign LR_ACTDAT->* to <S_ACTDAT_CMIR>.


***--------------------------------------------------------------------------------------
***     Create Business Object for Customer-Materials Info Record
        LR_BOB_CMIR = new #( IF_KUNNR = <S_ACTDAT_CMIR>-KUNNR
                             IF_VKORG = <S_ACTDAT_CMIR>-VKORG
                             IF_SPART = <S_ACTDAT_CMIR>-SPART
                             IF_VTWEG = <S_ACTDAT_CMIR>-VTWEG ).

***--------------------------------------------------------------------------------------
***     Check duplicate entries of Customer Material Number
        LS_KNMT = LR_BOB_CMIR->GET_KNMT( IF_KDMAT = <S_ACTDAT_CMIR>-KDMAT ).
        if LS_KNMT-MATNR is not initial and
           LS_KNMT-MATNR ne LS_MATLI-MATNR.
***       Customer material &1 has already been used in info &2
          ME->R_RPSEG->R_MSG->ADD_MESSAGE( IF_MSGID = 'VS'
                                           IF_MSGTY = 'E'
                                           IF_MSGNO = '003'
                                           IF_MSGV1 = <S_ACTDAT_CMIR>-KDMAT
                                           IF_MSGV2 = LS_KNMT-MATNR ).

***       Set Status to Cancled
          ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_CANCELED ).
          return.
        endif.

***     Avoid Update of CMIR
        LS_KNMT = LR_BOB_CMIR->GET_KNMT( IF_MATNR = LS_MATLI-MATNR ).
        if LS_KNMT-MATNR is not initial.
***       Information for Material &1 already exists as &2.
          ME->R_RPSEG->R_MSG->ADD_MESSAGE( IF_MSGID = 'ZVCXI_XCM_MBQ'
                                           IF_MSGTY = 'E'
                                           IF_MSGNO = '501'
                                           IF_MSGV1 = LS_KNMT-MATNR
                                           IF_MSGV2 = LS_KNMT-KDMAT ).

***       Set Status to Cancled
          ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_CANCELED ).
          return.
        endif.

***--------------------------------------------------------------------------------------
***     Update Customer Info Record
        LR_BOB_CMIR->UPDATE( IF_MATNR = LS_MATLI-MATNR
                             IF_KDMAT = <S_ACTDAT_CMIR>-KDMAT
                             IF_KDPTX = <S_ACTDAT_CMIR>-KDPTX
                             IF_UNTTO = <S_ACTDAT_CMIR>-UNTTO
                             IF_UEBTO = <S_ACTDAT_CMIR>-UEBTO
                             IF_UEBTK = <S_ACTDAT_CMIR>-UEBTK ).

***     Customer-material info was saved
        ME->R_RPSEG->R_MSG->ADD_MESSAGE( IF_MSGID = 'VS'
                                         IF_MSGTY = 'S'
                                         IF_MSGNO = '011' ).

***     Set Status to Complete
        ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_COMPLETED ).

***--------------------------------------------------------------------------------------
      catch /VCXI/CX_CKX into data(LR_CX_CKX).
***     Set Status to Error
        ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_ERROR ).

***     Add Message to Message Log
        ME->R_RPSEG->R_MSG->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).

    endtry.

*** Perform Save and Commit
    ME->R_RPSEG->SAVE_AND_COMMIT( ).

  endmethod.
endclass. "ZCL_VCXI_XCM_MBQ_STP_S505 definition
endclass. "ZCL_VCXI_XCM_MBQ_STP_S505 implementation
