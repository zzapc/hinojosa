
class ZCL_VCXI_PCSS_UI_SHEET definition
  public
  inheriting from ZCL_VCXI_P0SS_UI_SHEET
  create public .

public section.

  data S_PC_SHEET_VIS type ZVCXI_PCSS_S_SHEET_VIS read-only .

  methods DO_UPDATE_PC_SHEET_VIS
    importing
      !IS_PC_SHEET_VIS type ZVCXI_PCSS_S_SHEET_VIS
    raising
      /VCXI/CX_CKX .
  type-pools ABAP .
  methods SHLP_BOARW
    importing
      !IF_DISPLAY type ABAP_BOOL
    changing
      !CS_BOARW type ZVCXI_PCSS_S_BOARW
    raising
      /VCXI/CX_CKX .
  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
protected section.

  constants C_FOVRS type SYUCOMM value 'UC0100_FOVRS'. "#EC NOTEXT
  data R_PC_PR_SHEET type ref to ZCL_VCXI_PCSS_PR_SHEET .

  methods INIT_ACTIV_AREA
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_PCSS_UI_SHEET definition
class ZCL_VCXI_PCSS_UI_SHEET implementation.
method ACTIVATE.

  SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
  call function 'ZVCXI_PCSS_UI_SHEET_BIND'
    exporting
      IR_UI_SHEET = ME
    importing
      ES_DYNP     = ME->S_DYNP.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                      IR_UIGATE = IR_UIGATE
                      IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
  try.
      move IR_GATE->R_CLSPR ?to ME->R_PC_PR_SHEET.
    catch CX_SY_MOVE_CAST_ERROR.
***   UI ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '801'.
  endtry.

endmethod.
method DO_UPDATE_PC_SHEET_VIS.

  data: LS_BOARW     type ZVCXI_PCSS_S_BOARW,
        LS_FDUST     type ZVCXI_PCSS_S_FDUST,
        LS_FTUCK     type ZVCXI_PCSS_S_FTUCK,
        LS_SHEET_VIS type ZVCXI_P0SS_S_SHEET_VIS.

***--------------------------------------------------------------------------------------
*** Update General Data
  move-corresponding IS_PC_SHEET_VIS to LS_SHEET_VIS.
  ME->DO_UPDATE_SHEET_VIS( IS_SHEET_VIS = LS_SHEET_VIS ).

***--------------------------------------------------------------------------------------
*** Update Self Deckeling Information - Board Grade Width if needed
  if ME->R_PC_PR_SHEET->GET_FOVRS( ) eq ABAP_TRUE.
    if ME->S_PC_SHEET_VIS-BOARW ne IS_PC_SHEET_VIS-BOARW.
      move-corresponding IS_PC_SHEET_VIS to LS_BOARW.
      ME->R_PC_PR_SHEET->SET_BOARW( IS_BOARW = LS_BOARW
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.
  endif.

*** Set Override Self-Deckeling
  if ME->S_PC_SHEET_VIS-FOVRS ne IS_PC_SHEET_VIS-FOVRS.
    ME->R_PC_PR_SHEET->SET_FOVRS( IF_FOVRS = IS_PC_SHEET_VIS-FOVRS ).
  endif.

***--------------------------------------------------------------------------------------
*** Update Flute Orientation
  if ME->S_PC_SHEET_VIS-FLUTO ne IS_PC_SHEET_VIS-FLUTO.
    ME->R_PC_PR_SHEET->SET_FLUTO( IF_FLUTO = IS_PC_SHEET_VIS-FLUTO
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

***--------------------------------------------------------------------------------------
*** Update Dust Flap if needed
  if ME->R_PC_PR_SHEET->R_CUST_PRSTY is bound and
     ME->R_PC_PR_SHEET->R_CUST_PRSTY->CHECK_SCORE_ELEMENT( IF_IELEM = 'DUST_FLAP' ) eq ABAP_TRUE.
    if ME->S_PC_SHEET_VIS-FDUST ne IS_PC_SHEET_VIS-FDUST.
      move-corresponding IS_PC_SHEET_VIS to LS_FDUST.
      ME->R_PC_PR_SHEET->SET_FDUST( IS_FDUST = LS_FDUST
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.
  endif.

***--------------------------------------------------------------------------------------
*** Update Tuck in Flap if needed
  if ME->R_PC_PR_SHEET->R_CUST_PRSTY is bound and
     ME->R_PC_PR_SHEET->R_CUST_PRSTY->CHECK_SCORE_ELEMENT( IF_IELEM = 'TUCKIN_FLAP' ) eq ABAP_TRUE.
    if ME->S_PC_SHEET_VIS-FTUCK ne IS_PC_SHEET_VIS-FTUCK.
      move-corresponding IS_PC_SHEET_VIS to LS_FTUCK.
      ME->R_PC_PR_SHEET->SET_FTUCK( IS_FTUCK = LS_FTUCK
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.
  endif.

endmethod.
method INIT_ACTIV_AREA.

  SUPER->INIT_ACTIV_AREA( ).

  if ME->S_PC_SHEET_VIS-FDUST is not initial or
     ME->S_PC_SHEET_VIS-FTUCK is not initial.
    move ABAP_TRUE  to ME->R_PR_SHEET->F_JOFL_ACTIV.
  endif.

endmethod.
method INIT_VIS.

  data: LS_BOARW type        ZVCXI_PCSS_S_BOARW,
        LS_SIDRU type        ZVCXI_P0SS_S_SIDRU,
        LS_FDUST type        ZVCXI_PCSS_S_FDUST,
        LS_FTUCK type        ZVCXI_PCSS_S_FTUCK.

  SUPER->INIT_VIS( ).

  free: ME->S_PC_SHEET_VIS.
  move-corresponding ME->S_SHEET_VIS to ME->S_PC_SHEET_VIS.

***--------------------------------------------------------------------------------------
*** Get Board width and Side Run
  ME->R_PC_PR_SHEET->GET_BOARW( importing ES_BOARW = LS_BOARW ).
  ME->R_PC_PR_SHEET->GET_SIDRU( importing ES_SIDRU = LS_SIDRU ).
  move-corresponding LS_BOARW               to ME->S_PC_SHEET_VIS.
  move-corresponding LS_SIDRU               to ME->S_PC_SHEET_VIS.
  move ME->R_PC_PR_SHEET->GET_FOVRS( )      to ME->S_PC_SHEET_VIS-FOVRS.

***--------------------------------------------------------------------------------------
*** Get Flute Orientation
  move ME->R_PC_PR_SHEET->GET_FLUTO( )       to ME->S_PC_SHEET_VIS-FLUTO.
  move ME->R_PC_PR_SHEET->GET_FLUTO_DESCR( ) to ME->S_PC_SHEET_VIS-FLUTO_DESCR.

***--------------------------------------------------------------------------------------
*** Get Dust Flap and Tuck in Flap
  ME->R_PC_PR_SHEET->GET_FDUST( importing ES_FDUST = LS_FDUST ).
  ME->R_PC_PR_SHEET->GET_FTUCK( importing ES_FTUCK = LS_FTUCK ).
  move-corresponding LS_FDUST               to ME->S_PC_SHEET_VIS.
  move-corresponding LS_FTUCK               to ME->S_PC_SHEET_VIS.

endmethod.
method SHLP_BOARW.

  data:  LS_RETURN type DDSHRETVAL.
  data:  LT_COWID  type ZVCXI_PCS_TT_COWID,
         LT_RETURN type table of DDSHRETVAL.

*** Get all allowed Corrugation Width
  LT_COWID = ME->R_PC_PR_SHEET->GET_COWID_ALL( ).

*** Call Search Help
  call function 'F4IF_INT_TABLE_VALUE_REQUEST'
    exporting
      RETFIELD        = 'COWID'
      VALUE_ORG       = 'S'
      DISPLAY         = IF_DISPLAY
    tables
      VALUE_TAB       = LT_COWID
      RETURN_TAB      = LT_RETURN
    exceptions
      PARAMETER_ERROR = 1
      NO_VALUES_FOUND = 2
      others          = 3.

  if SY-SUBRC eq 0 and
     IF_DISPLAY ne ABAP_TRUE.
    read table LT_RETURN index 1 into LS_RETURN.
    if SY-SUBRC eq 0.
      try.
          call function 'RSDS_CONVERT_NUMBER'
            exporting
              INPUT  = LS_RETURN-FIELDVAL
            importing
              OUTPUT = CS_BOARW-BOARW.
        catch CX_ROOT.
      endtry.
    endif.
  endif.

endmethod.
