
class ZCL_VCXI_P5SS_UI_CSTEP definition
  public
  inheriting from /VCXI/CL_VKSRU_CLSUI
  create public .

public section.

  data S_CSTEP_VIS type ZVCXI_P5SS_S_CSTEP_VIS read-only .
  data F_CURSOR type SCRFNAME .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_VIS
    importing
      !IS_CSTEP_VIS type ANY
    raising
      /VCXI/CX_CKX .
  methods SET_CURSOR
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  constants C_UC_RLPROP_IN type SYUCOMM value 'RLPROP_IN' ##NO_TEXT.
  constants C_UC_RLPROP_OUT type SYUCOMM value 'RLPROP_OUT' ##NO_TEXT.
  data R_PR_CSTEP type ref to ZCL_VCXI_P5SS_PR_CSTEP .
  data R_TEXTEDIT_LTEXT type ref to ZCL_VCXI_PXSS_TEXTEDIT_LTEXT .

  methods HANDLE_UC_RLPROP
    importing
      !IF_UCOMM type SYUCOMM
    raising
      /VCXI/CX_CKX .
  methods INIT_VIS
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
private section.
endclass. "ZCL_VCXI_P5SS_UI_CSTEP definition
class ZCL_VCXI_P5SS_UI_CSTEP implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Create Text Editor for Long Text
    create object ME->R_TEXTEDIT_LTEXT
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_TEXTEDIT_LTEXT'.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_CSTEP.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

***--------------------------------------------------------------------------------------
*** Update the Long Text before leaving the screen
    if ME->R_TEXTEDIT_LTEXT is bound.
      ME->R_TEXTEDIT_LTEXT->UPDATE_TEXT( ).
      ME->R_TEXTEDIT_LTEXT->DO_FREE( ).
      clear ME->R_TEXTEDIT_LTEXT.
    endif.

  endmethod.
  method DO_MODIFY_SCREEN.

    data: LF_LOCKED type        /VCXI/CKX_LOCKED,
          LF_INUSE  type        /VCXI/VKSR_INUSE.
    data: LR_CX_CKX type ref to /VCXI/CX_CKX.

    try.
        SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                 IF_DYNNR = IF_DYNNR ).

        LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
        LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***     Modify Screen using DynamicVC
        ZCL_VCXI_XCDR_SERVICE=>MODIFY_SCREEN( IR_GATE = ME->R_GATE ).

        loop at screen.
***       Lock everything if not in change mode
          if LF_LOCKED ne ABAP_TRUE or
             LF_INUSE  ne ABAP_TRUE.
            move 0 to SCREEN-INPUT.
          endif.

***       Reopen Info Buttons
          if SCREEN-GROUP1 eq 'INF'.
            move 1 to SCREEN-INPUT.
          endif.
          modify screen.

***       Set Cursor
          if SCREEN-NAME eq ME->F_CURSOR.
            set cursor field ME->F_CURSOR.
          endif.
        endloop.

      catch /VCXI/CX_CKX into LR_CX_CKX.
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Initialize VIS
    ME->INIT_VIS( ).

***--------------------------------------------------------------------------------------
*** Get TextEdit Conversion Text
    if ME->R_TEXTEDIT_LTEXT is bound.
      move ABAP_TRUE to ME->R_TEXTEDIT_LTEXT->F_REFRESH_REQ.
      ME->R_TEXTEDIT_LTEXT->REFRESH_TEXTEDIT( ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_CSTEP_VIS type ZVCXI_P5SS_S_CSTEP_VIS.


    check ME->R_PR_CSTEP is bound.

*** "Cast" Vis Structure
    move-corresponding IS_CSTEP_VIS to LS_CSTEP_VIS.

***--------------------------------------------------------------------------------------
*** Update TextEdit Long Text
    if ME->R_TEXTEDIT_LTEXT is bound.
      ME->R_TEXTEDIT_LTEXT->UPDATE_TEXT( ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

***--------------------------------------------------------------------------------------
*** Update TextEdit Long Text
    if ME->R_TEXTEDIT_LTEXT is bound.
      ME->R_TEXTEDIT_LTEXT->UPDATE_TEXT( ).
    endif.

***--------------------------------------------------------------------------------------
*** Call Super
    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    case IF_UCOMM.
***   Roll Property Popup
      when C_UC_RLPROP_IN or C_UC_RLPROP_OUT.
        ME->HANDLE_UC_RLPROP( exporting IF_UCOMM = IF_UCOMM  ).

    endcase.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_CSTEP.

  endmethod.
  method HANDLE_UC_RLPROP.

    data: LS_WEBWI type ZVCXI_PXS_S_WEBWI,
          LS_SURWT type ZVCXI_PXS_S_SURWT.
    data: LR_BL_RLBPROP type ref to ZCL_VCXI_P5SS_BL_RLBPROP,
          LR_BL_RLFPROP type ref to ZCL_VCXI_P5SS_BL_RLFPROP.


    case IF_UCOMM.
      when C_UC_RLPROP_IN.
***     Get Roll Property (Inbound)
        LR_BL_RLBPROP = ME->R_PR_CSTEP->ZIF_VCXI_P5SS_COMM_PREV~GET_RLBPX( ).
        LR_BL_RLFPROP = ME->R_PR_CSTEP->ZIF_VCXI_P5SS_COMM_PREV~GET_RLFPX( ).
        LS_WEBWI      = ME->R_PR_CSTEP->ZIF_VCXI_P5SS_COMM_PREV~GET_WEBWI( ).
        LS_SURWT      = ME->R_PR_CSTEP->ZIF_VCXI_P5SS_COMM_PREV~GET_SURWT( ).

      when C_UC_RLPROP_OUT.
***     Get Roll Property (Outbound)
        LR_BL_RLBPROP = ME->R_PR_CSTEP->ZIF_VCXI_P5SS_COMM_NEXT~GET_RLBPX( ).
        LR_BL_RLFPROP = ME->R_PR_CSTEP->ZIF_VCXI_P5SS_COMM_NEXT~GET_RLFPX( ).
        LS_WEBWI      = ME->R_PR_CSTEP->ZIF_VCXI_P5SS_COMM_NEXT~GET_WEBWI( ).
        LS_SURWT      = ME->R_PR_CSTEP->ZIF_VCXI_P5SS_COMM_NEXT~GET_SURWT( ).

    endcase.

*** Start Popup
    ZCL_VCXI_P5SS_POPUP_RLPROP=>START( IR_BL_RLBPROP = LR_BL_RLBPROP
                                       IR_BL_RLFPROP = LR_BL_RLFPROP
                                       IF_WERKS      = ME->R_PR_CSTEP->GET_WERKS( )
                                       IS_WEBWI      = LS_WEBWI
                                       IS_SURWT      = LS_SURWT ).

  endmethod.
  method INIT_VIS.

    data: LR_BL_RLFPROP type ref to ZCL_VCXI_P5SS_BL_RLFPROP.


    check ME->R_PR_CSTEP is bound.
    free: ME->S_CSTEP_VIS.


***--------------------------------------------------------------------------------------
*** Get Web Width (Inbound)
    ME->S_CSTEP_VIS-S_IOSEC_IN-S_WEBWI = ME->R_PR_CSTEP->ZIF_VCXI_P5SS_COMM_PREV~GET_WEBWI( ).

*** Get Surface Weight (Inbound)
    ME->S_CSTEP_VIS-S_IOSEC_IN-S_SURWT = ME->R_PR_CSTEP->ZIF_VCXI_P5SS_COMM_PREV~GET_SURWT( ).

*** Number Ups (Inbound)
    ME->S_CSTEP_VIS-S_IOSEC_IN-NOUPS   = ME->R_PR_CSTEP->ZIF_VCXI_P5SS_COMM_PREV~GET_NOUPS( ).

*** Get Substrate and Caliper (Inbound)
    LR_BL_RLFPROP = ME->R_PR_CSTEP->ZIF_VCXI_P5SS_COMM_PREV~GET_RLFPX( ).
    if LR_BL_RLFPROP is bound.
      ME->S_CSTEP_VIS-S_IOSEC_IN-SBSTR   = LR_BL_RLFPROP->GET_SBSTR( ).
      ME->S_CSTEP_VIS-S_IOSEC_IN-S_CALIP = LR_BL_RLFPROP->S_CALIP.
    endif.


***--------------------------------------------------------------------------------------
*** Get Web Width (Outbound)
    ME->S_CSTEP_VIS-S_IOSEC_OUT-S_WEBWI = ME->R_PR_CSTEP->ZIF_VCXI_P5SS_COMM_NEXT~GET_WEBWI( ).

*** Get Surface Weight (Outbound)
    ME->S_CSTEP_VIS-S_IOSEC_OUT-S_SURWT = ME->R_PR_CSTEP->ZIF_VCXI_P5SS_COMM_NEXT~GET_SURWT( ).

*** Number Ups (Outbound)
    ME->S_CSTEP_VIS-S_IOSEC_OUT-NOUPS = ME->R_PR_CSTEP->ZIF_VCXI_P5SS_COMM_NEXT~GET_NOUPS( ).

*** Get Substrate and Caliper (Outbound)
    LR_BL_RLFPROP = ME->R_PR_CSTEP->ZIF_VCXI_P5SS_COMM_NEXT~GET_RLFPX( ).
    if LR_BL_RLFPROP is bound.
      ME->S_CSTEP_VIS-S_IOSEC_OUT-SBSTR   = LR_BL_RLFPROP->GET_SBSTR( ).
      ME->S_CSTEP_VIS-S_IOSEC_OUT-S_CALIP = LR_BL_RLFPROP->S_CALIP.
    endif.

  endmethod.
  method SET_CURSOR.

    data:  LF_CURSOR type SCRFNAME.

    get cursor field LF_CURSOR.
    if LF_CURSOR is not initial.
      move LF_CURSOR to ME->F_CURSOR.
    endif.

  endmethod.
