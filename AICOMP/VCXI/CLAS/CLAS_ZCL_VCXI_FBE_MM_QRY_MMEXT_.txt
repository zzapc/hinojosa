
class ZCL_VCXI_FBE_MM_QRY_MMEXT definition
  public
  inheriting from ZCL_VCXI_XCE_MM_QRY_MMEXT
  create public .

public section.

  data:
    I_IBAGT type range of ZVCXI_FBS_IBAGT .
  data:
    I_BGWID type range of ZVCXI_FBS_BGWID .
  data:
    I_BGHEI type range of ZVCXI_FBS_BGHEI .
  data:
    I_OLWID type range of ZVCXI_FBS_OLWID .
  data:
    I_GULEF type range of ZVCXI_F0S_GULEF .
  data:
    I_GURIG type range of ZVCXI_F0S_GURIG .
  data:
    I_GUTOP type range of ZVCXI_FBS_GUTOP .
  data:
    I_GUBOT type range of ZVCXI_FBS_GUBOT .
  data:
    I_FOTOP type range of ZVCXI_FBS_FOTOP .
  data:
    I_FOBOT type range of ZVCXI_FBS_FOBOT .
  data I_TWICK type range of ZVCXI_FBS_TWICK .
  data I_WILIP type range of ZVCXI_FBS_WILIP .

  methods CONSTRUCTOR
    raising
      /VCXI/CX_CKX .

  methods EXECUTE
    redefinition .
protected section.
private section.
endclass. "ZCL_VCXI_FBE_MM_QRY_MMEXT definition
class ZCL_VCXI_FBE_MM_QRY_MMEXT implementation.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( ).

*** Define relevant Context
    ME->I_CNTXT = value #( ( SIGN = 'I' OPTION = 'EQ' LOW = ZCL_VCXI_FBE_MM_MMEXT=>C_CNTXT_FB ) ).

  endmethod.
  method EXECUTE.

    data: LF_MATNR   type MATNR.
    data: LS_ORGDATA type ZVCXI_XCA_MWB_S_ORGDATA.
    data: LT_MATNR   type table of MATNR,
          LT_ORGDATA type ZVCXI_XCA_MWB_TT_ORGDATA.
    data: LI_CUOBJ type range of CUOBJ.
    data: LR_CLMAT type ref to ZCL_VCXI_XCA_MWB_CLMAT.


*** Convert Varinat Flag to Range based on Configuration Number
    if ME->I_XLTYP is not initial.
      if ABAP_TRUE in ME->I_XLTYP.
        append value #( SIGN = 'I' OPTION = 'NE' LOW = 0 ) to LI_CUOBJ.
      endif.
      if ABAP_FALSE in ME->I_XLTYP.
        append value #( SIGN = 'I' OPTION = 'EQ' LOW = 0 ) to LI_CUOBJ.
      endif.
    endif.

***------------------------------------------------------------------
*** Select Data from Database
    select ZVCXI_XCE_TD100~MATNR
           from ZVCXI_XCE_TD100
          inner join ZVCXI_FBE_TD100
             on ZVCXI_FBE_TD100~MATNR eq ZVCXI_XCE_TD100~MATNR and
                ZVCXI_FBE_TD100~WERKS eq ZVCXI_XCE_TD100~WERKS
          inner join MARA
             on MARA~MATNR eq ZVCXI_XCE_TD100~MATNR
          inner join MARC
             on MARC~MATNR eq ZVCXI_XCE_TD100~MATNR and
                MARC~WERKS eq ZVCXI_XCE_TD100~WERKS
          inner join MAKT
             on MAKT~MATNR eq MARA~MATNR and
                MAKT~SPRAS eq SY-LANGU
           into table LT_MATNR
           up to ME->F_MAXSEL rows
          where ZVCXI_XCE_TD100~MATNR in ME->I_MATNR
            and ZVCXI_XCE_TD100~WERKS in ME->I_WERKS
            and ZVCXI_XCE_TD100~CNTXT in ME->I_CNTXT
            and ZVCXI_FBE_TD100~IBAGT in ME->I_IBAGT       "Bag Type
            and ZVCXI_FBE_TD100~BGWID in ME->I_BGWID       "Bag Width
            and ZVCXI_FBE_TD100~BGHEI in ME->I_BGHEI       "Bag Height
            and ZVCXI_FBE_TD100~OLWID in ME->I_OLWID       "Overlap on Width
            and ZVCXI_FBE_TD100~GULEF in ME->I_GULEF       "Left Gusset
            and ZVCXI_FBE_TD100~GURIG in ME->I_GURIG       "Right Gusset
            and ZVCXI_FBE_TD100~GUTOP in ME->I_GUTOP       "Top Gusset
            and ZVCXI_FBE_TD100~GUBOT in ME->I_GUBOT       "Bottom Gusset
            and ZVCXI_FBE_TD100~FOTOP in ME->I_FOTOP       "Top Fold
            and ZVCXI_FBE_TD100~FOBOT in ME->I_FOBOT       "Bottom Fold
            and ZVCXI_FBE_TD100~TWICK in ME->I_TWICK       "Wicketing Type
            and ZVCXI_FBE_TD100~WILIP in ME->I_WILIP       "Wicket Lip
            and MARA~MSTAE            in ME->I_MSTAE
            and MAKT~MAKTX            in ME->I_MAKTX
            and MARC~MMSTA            in ME->I_MMSTA
            and MARC~CUOBJ            in LI_CUOBJ
            and MARC~LVORM            ne 'X'
           group by ZVCXI_XCE_TD100~MATNR
           order by ZVCXI_XCE_TD100~MATNR.

***------------------------------------------------------------------
*** Create Material Object List based on Selection Result
    loop at LT_MATNR into LF_MATNR.
***   Get all Organisational Data for a Material
      LT_ORGDATA = ME->GET_ORGDATA( IF_MATNR = LF_MATNR ).

***   Create Material instance for each Org Data
      loop at LT_ORGDATA into LS_ORGDATA.
        append ZCL_VCXI_XCA_MWB_CLMAT=>FACTORY( IR_REGMDL  = ME->R_REGMDL
                                                IF_MATNR   = LF_MATNR
                                                IS_ORGDATA = LS_ORGDATA ) to RT_RESULT.
      endloop.
    endloop.

  endmethod.
