
class ZCL_VCXI_PCSP_UI_ACTVY_CORR definition
  public
  inheriting from /VCXI/CL_VKSRU_CLSUI
  create public .

public section.

  data S_ACTVY_CORR_VIS type ZVCXI_PCSP_S_ACTVY_CORR_VIS .
  data F_CURSOR type SCRFNAME .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_ACTVY_CORR_VIS
    importing
      !IS_ACTVY_CORR_VIS type ZVCXI_PCSP_S_ACTVY_CORR_VIS
    raising
      /VCXI/CX_CKX .
  methods SET_CURSOR
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
protected section.

  data R_PR_ACTVY_CORR type ref to ZCL_VCXI_PCSP_PR_ACTVY_CORR .

  methods DO_UPDATE_MC_STARCH_VIS
    importing
      !IS_ACTVY_CORR_VIS type ZVCXI_PCSP_S_ACTVY_CORR_VIS
    raising
      /VCXI/CX_CKX .
  methods INIT_VIS_ACTVY_CORR
    raising
      /VCXI/CX_CKX .
  methods INIT_VIS_MC_STARCH
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
private section.
endclass. "ZCL_VCXI_PCSP_UI_ACTVY_CORR definition
class ZCL_VCXI_PCSP_UI_ACTVY_CORR implementation.
method ACTIVATE.

  SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
  call function 'ZVCXI_PCSP_UI_ACTVY_CORR_BIND'
    exporting
      IR_UI_ACTVY_CORR = ME
    importing
      ES_DYNP          = ME->S_DYNP.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                      IR_UIGATE = IR_UIGATE
                      IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
  try.
      move IR_GATE->R_CLSPR ?to ME->R_PR_ACTVY_CORR.
    catch CX_SY_MOVE_CAST_ERROR.
***   UI ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '801'.
  endtry.

endmethod.
method DO_MODIFY_SCREEN.

  data:  LF_LOCKED type        /VCXI/CKX_LOCKED,
         LF_INUSE  type        /VCXI/VKSR_INUSE.
  data:  LR_CX_CKX type ref to /VCXI/CX_CKX.

  SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                           IF_DYNNR = IF_DYNNR ).

  try.

      if IF_REPID eq ME->S_DYNP-REPID and
         IF_DYNNR eq ME->S_DYNP-DYNNR.

        LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
        LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***     Modify Scree using DynamicVC
        ZCL_VCXI_XCDR_SERVICE=>MODIFY_SCREEN( IR_GATE = ME->R_GATE ).

        loop at screen.
          if LF_LOCKED ne ABAP_TRUE or
             LF_INUSE  ne ABAP_TRUE.
            move 0 to SCREEN-INPUT.
          endif.
          modify screen.
        endloop.
      endif.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

endmethod.
method DO_PBO.

  ME->INIT_VIS_ACTVY_CORR( ).
  ME->INIT_VIS_MC_STARCH( ).

  set cursor field ME->F_CURSOR.
  clear ME->F_CURSOR.

endmethod.
method DO_UPDATE_ACTVY_CORR_VIS.


*** Set PC Length, Width and Height
  if ME->S_ACTVY_CORR_VIS-S_PCLEN ne IS_ACTVY_CORR_VIS-S_PCLEN.
    ME->R_PR_ACTVY_CORR->SET_PCLEN( IS_PCLEN = IS_ACTVY_CORR_VIS-S_PCLEN
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

  if ME->S_ACTVY_CORR_VIS-S_PCWID ne IS_ACTVY_CORR_VIS-S_PCWID.
    ME->R_PR_ACTVY_CORR->SET_PCWID( IS_PCWID = IS_ACTVY_CORR_VIS-S_PCWID
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

  if ME->S_ACTVY_CORR_VIS-S_PCHEI ne IS_ACTVY_CORR_VIS-S_PCHEI.
    ME->R_PR_ACTVY_CORR->SET_PCHEI( IS_PCHEI = IS_ACTVY_CORR_VIS-S_PCHEI
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Set Number Ups
  if ME->S_ACTVY_CORR_VIS-PCUPS ne IS_ACTVY_CORR_VIS-PCUPS.
    ME->R_PR_ACTVY_CORR->SET_PCUPS( IF_PCUPS = IS_ACTVY_CORR_VIS-PCUPS
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Set PC Direction
  if ME->S_ACTVY_CORR_VIS-PCDIR ne IS_ACTVY_CORR_VIS-PCDIR.
    ME->R_PR_ACTVY_CORR->SET_PCDIR( IF_PCDIR = IS_ACTVY_CORR_VIS-PCDIR
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Set PC Type
  if ME->S_ACTVY_CORR_VIS-PCTYP ne IS_ACTVY_CORR_VIS-PCTYP.
    ME->R_PR_ACTVY_CORR->SET_PCTYP( IF_PCTYP = IS_ACTVY_CORR_VIS-PCTYP
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Set Board ID
  if ME->S_ACTVY_CORR_VIS-BOARD ne IS_ACTVY_CORR_VIS-BOARD.
    ME->R_PR_ACTVY_CORR->SET_BOARD( IF_BOARD = IS_ACTVY_CORR_VIS-BOARD
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Set Flute Type
  if ME->S_ACTVY_CORR_VIS-FLUTE ne IS_ACTVY_CORR_VIS-FLUTE.
    ME->R_PR_ACTVY_CORR->SET_FLUTE( IF_FLUTE = IS_ACTVY_CORR_VIS-FLUTE
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Set Corrugation Width
  if ME->S_ACTVY_CORR_VIS-S_COWID ne IS_ACTVY_CORR_VIS-S_COWID.
    ME->R_PR_ACTVY_CORR->SET_COWID( IS_COWID = IS_ACTVY_CORR_VIS-S_COWID
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Set Corrugation Length
  if ME->S_ACTVY_CORR_VIS-S_COLEN ne IS_ACTVY_CORR_VIS-S_COLEN.
    ME->R_PR_ACTVY_CORR->SET_COLEN( IS_COLEN = IS_ACTVY_CORR_VIS-S_COLEN
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Update Material Consumption Starch
  ME->DO_UPDATE_MC_STARCH_VIS( IS_ACTVY_CORR_VIS = IS_ACTVY_CORR_VIS ).

endmethod.
method DO_UPDATE_MC_STARCH_VIS.

  data:          LS_AVGCO             type          ZVCXI_XCS_S_AVGCO,
                 LS_AVGCP             type          ZVCXI_XCS_S_AVGCP.
  data:          LT_GATE              type          /VCXI/VKSR_TT_GATE.
  data:          LR_GATE              type ref to   /VCXI/CL_VKSR_GATE,
                 LR_PR_MC_STARCH      type ref to   ZCL_VCXI_PCSP_PR_MC_STARCH.

*** Set Starch Consumption Data
*** Get Gates of Material Consumption
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCSP_PR_MC=>C_IPRVT_XCS_MC
                                   IF_INUSE = ABAP_TRUE ).

  read table LT_GATE into LR_GATE index 1.

  try.
      move LR_GATE->R_CLSPR ?to LR_PR_MC_STARCH.
      check LR_PR_MC_STARCH is bound.

      move-corresponding IS_ACTVY_CORR_VIS to:
                         LS_AVGCO, LS_AVGCP.

***   Set Material Number
      if ME->S_ACTVY_CORR_VIS-MATNR ne IS_ACTVY_CORR_VIS-MATNR.
        LR_PR_MC_STARCH->SET_MATNR( IF_MATNR = IS_ACTVY_CORR_VIS-MATNR
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
      endif.

***   Set Average Consumption and Consumption per
      if ME->S_ACTVY_CORR_VIS-AVGCO ne IS_ACTVY_CORR_VIS-AVGCO.
        LR_PR_MC_STARCH->SET_AVGCO( IS_AVGCO = LS_AVGCO
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
      endif.

      if ME->S_ACTVY_CORR_VIS-AVGCP ne IS_ACTVY_CORR_VIS-AVGCP.
        LR_PR_MC_STARCH->SET_AVGCP( IS_AVGCP = LS_AVGCP
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
      endif.

***   Set Starch Factor
      if ME->S_ACTVY_CORR_VIS-STARF ne IS_ACTVY_CORR_VIS-STARF.
        LR_PR_MC_STARCH->SET_STARF( IF_STARF = IS_ACTVY_CORR_VIS-STARF
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
      endif.

    catch CX_SY_MOVE_CAST_ERROR.
      clear: LR_PR_MC_STARCH.
  endtry.

endmethod.
method HANDLE_GATE_FREED.

  SUPER->HANDLE_GATE_FREED( ).

  free: ME->R_PR_ACTVY_CORR.

endmethod.
method INIT_VIS_ACTVY_CORR.

  free: ME->S_ACTVY_CORR_VIS.

*** Get PC Dimensions
  ME->S_ACTVY_CORR_VIS-S_PCLEN = ME->R_PR_ACTVY_CORR->GET_PCLEN( ).
  ME->S_ACTVY_CORR_VIS-S_PCWID = ME->R_PR_ACTVY_CORR->GET_PCWID( ).
  ME->S_ACTVY_CORR_VIS-S_PCHEI = ME->R_PR_ACTVY_CORR->GET_PCHEI( ).

*** Get Number Ups
  move ME->R_PR_ACTVY_CORR->GET_PCUPS( ) to ME->S_ACTVY_CORR_VIS-PCUPS.

*** Get PC Direction
  move ME->R_PR_ACTVY_CORR->GET_PCDIR( ) to ME->S_ACTVY_CORR_VIS-PCDIR.

*** Get PC Type and Description
  move ME->R_PR_ACTVY_CORR->GET_PCTYP( )       to ME->S_ACTVY_CORR_VIS-PCTYP.
  move ME->R_PR_ACTVY_CORR->GET_PCTYP_DESCR( ) to ME->S_ACTVY_CORR_VIS-PCTYP_DESCR.

*** Get Board ID
  move ME->R_PR_ACTVY_CORR->GET_BOARD( ) to ME->S_ACTVY_CORR_VIS-BOARD.

*** Get Flute Type
  move ME->R_PR_ACTVY_CORR->GET_FLUTE( ) to ME->S_ACTVY_CORR_VIS-FLUTE.

*** Get Corrugation Width/Length
  ME->S_ACTVY_CORR_VIS-S_COWID = ME->R_PR_ACTVY_CORR->GET_COWID( ).
  ME->S_ACTVY_CORR_VIS-S_COLEN = ME->R_PR_ACTVY_CORR->GET_COLEN( ).

endmethod.
method INIT_VIS_MC_STARCH.

  data:          LS_AVGCO             type          ZVCXI_XCS_S_AVGCO,
                 LS_AVGCP             type          ZVCXI_XCS_S_AVGCP,
                 LS_CONSU             type          ZVCXI_XCSP_S_CONSU.
  data:          LT_GATE              type          /VCXI/VKSR_TT_GATE.
  data:          LR_GATE              type ref to   /VCXI/CL_VKSR_GATE,
                 LR_PR_MC_STARCH      type ref to   ZCL_VCXI_PCSP_PR_MC_STARCH.

***--------------------------------------------------------------------------------------
*** Get Gates of Material Consumption
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCSP_PR_MC=>C_IPRVT_XCS_MC
                                   IF_INUSE = ABAP_TRUE ).

  read table LT_GATE into LR_GATE index 1.
  check SY-SUBRC eq 0.

  try.
      move LR_GATE->R_CLSPR ?to LR_PR_MC_STARCH.
      check LR_PR_MC_STARCH is bound.

***   Get Material Number
      move LR_PR_MC_STARCH->GET_MATNR( )       to ME->S_ACTVY_CORR_VIS-MATNR.
      move LR_PR_MC_STARCH->GET_MATNR_DESCR( ) to ME->S_ACTVY_CORR_VIS-MATNR_DESCR.

***   Get Average Consumption and Consumption per
      move LR_PR_MC_STARCH->GET_AVGCO( ) to LS_AVGCO.
      move-corresponding LS_AVGCO        to ME->S_ACTVY_CORR_VIS.

      move LR_PR_MC_STARCH->GET_AVGCP( ) to LS_AVGCP.
      move-corresponding LS_AVGCP        to ME->S_ACTVY_CORR_VIS.

***   Get Starch Factor
      move LR_PR_MC_STARCH->GET_STARF( )       to ME->S_ACTVY_CORR_VIS-STARF.

***   Get Starch Consumption
      move LR_PR_MC_STARCH->GET_CONSU( ) to LS_CONSU.
      move-corresponding LS_CONSU        to ME->S_ACTVY_CORR_VIS.

    catch CX_SY_MOVE_CAST_ERROR.
      clear: LR_PR_MC_STARCH.
  endtry.

endmethod.
method SET_CURSOR.

  data:  LF_CURSOR type SCRFNAME.

  get cursor field LF_CURSOR.
  if LF_CURSOR is not initial.
    move LF_CURSOR to ME->F_CURSOR.
  endif.

endmethod.
