
class ZCL_VCXI_XCA_MWB_UI_REGMDL definition
  public
  inheriting from /VCXI/CL_CKRU_UI_REGELMNT
  final
  create public .

public section.

  data S_REGMDL_VIS type ZVCXI_XCA_MWB_S_REGMDL_VIS read-only .

  methods UPDATE_REGMDL
    importing
      !IS_REGMDL_VIS type ZVCXI_XCA_MWB_S_REGMDL_VIS
      !IF_DO_CHECK type ABAP_BOOL default ABAP_TRUE
    raising
      /VCXI/CX_CKX .
  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/CL_CKRU_UIFW
      !IR_OBJECT type ref to /VCXI/IF_CKX_OBJECT
    raising
      /VCXI/CX_CKX .

  methods DO_PBO
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  data R_REGMDL type ref to ZCL_VCXI_XCA_MWB_REGMDL .

  methods HANDLE_UC_CHOOSE
    redefinition .
private section.
endclass. "ZCL_VCXI_XCA_MWB_UI_REGMDL definition
class ZCL_VCXI_XCA_MWB_UI_REGMDL implementation.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_OBJECT = IR_OBJECT ).

    try.
        move ME->R_OBJECT ?to ME->R_REGMDL.
      catch CX_SY_MOVE_CAST_ERROR.
        raise exception type /VCXI/CX_CKX.
    endtry.

    call function 'ZVCXI_XCA_MWB_UI_REGMDL_BIND'
      exporting
        IR_UI_REGMDL = ME
      importing
        ES_DYNP      = ME->S_DYNP.

  endmethod.
  method DO_PBO.

    free ME->S_REGMDL_VIS.
    check ME->R_REGMDL is bound.

***------------------------------------------------------------------------------------------------
    SUPER->DO_PBO( ).
    move ME->S_REGELMNT_VIS to ME->S_REGMDL_VIS-S_REGEL.

***------------------------------------------------------------------------------------------------
    move ME->R_REGMDL->S_CLQRY to ME->S_REGMDL_VIS-S_CLQRY.
    move ME->R_REGMDL->S_CLDUI to ME->S_REGMDL_VIS-S_CLDUI.
    move ME->R_REGMDL->S_CLMAT to ME->S_REGMDL_VIS-S_CLMAT.
    move ME->R_REGMDL->S_RPSEL to ME->S_REGMDL_VIS-S_RPSEL.

***------------------------------------------------------------------------------------------------
*** Modify Screen
    loop at screen.
      if ME->R_REGELMNT->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ) ne ABAP_TRUE.
        move 0 to SCREEN-INPUT.
        move 0 to SCREEN-REQUIRED.
      endif.

***   Make Buttons ready to use
      if SCREEN-GROUP1 eq 'INF'.
        move 1 to SCREEN-INPUT.
      endif.

      modify screen.
    endloop.

  endmethod.
  method DO_USER_COMMAND.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    case IF_UCOMM.
      when 'CLQRY_INFO'.
        /VCXI/CL_CKXB_SERVICE=>DISPLAY_DOCU_FOR_CLASS( IF_CLSNAME = ME->R_REGMDL->S_CLQRY-CLQRY ).
      when 'CLDUI_INFO'.
        /VCXI/CL_CKXB_SERVICE=>DISPLAY_DOCU_FOR_CLASS( IF_CLSNAME = ME->R_REGMDL->S_CLDUI-CLDUI ).
      when 'CLMAT_INFO'.
        /VCXI/CL_CKXB_SERVICE=>DISPLAY_DOCU_FOR_CLASS( IF_CLSNAME = ME->R_REGMDL->S_CLMAT-CLMAT ).
    endcase.

  endmethod.
  method HANDLE_UC_CHOOSE.

    SUPER->HANDLE_UC_CHOOSE( ).

    case ME->R_UIFW->F_CURSOR.
      when 'ZVCXI_XCA_MWB_S_REGMDL_VIS-IPSET'.
        ME->HANDLE_UC_CHOOSE_PRPSET( ).

      when 'ZVCXI_XCA_MWB_S_REGMDL_VIS-CLQRY'.
        ME->HANDLE_UC_CHOOSE_CLASS( IF_CLSNA = ME->R_REGMDL->S_CLQRY-CLQRY ).

      when 'ZVCXI_XCA_MWB_S_REGMDL_VIS-CLDUI'.
        ME->HANDLE_UC_CHOOSE_CLASS( IF_CLSNA = ME->R_REGMDL->S_CLDUI-CLDUI ).

      when 'ZVCXI_XCA_MWB_S_REGMDL_VIS-CLMAT'.
        ME->HANDLE_UC_CHOOSE_CLASS( IF_CLSNA = ME->R_REGMDL->S_CLMAT-CLMAT ).

      when 'ZVCXI_XCA_MWB_S_REGMDL_VIS-RPSEL'.
        ME->HANDLE_UC_CHOOSE_REPORT( IF_REPNA = ME->R_REGMDL->S_RPSEL-RPSEL ).

    endcase.

  endmethod.
  method UPDATE_REGMDL.

    data:  LS_REGELMNT_VIS type /VCXI/CKRU_S_REGELMNT_VIS.

***----------------------------------------------------------------------------
*** process 'super' update
    move-corresponding IS_REGMDL_VIS to LS_REGELMNT_VIS.
    ME->UPDATE_REGELMNT( IS_REGELMNT_VIS = LS_REGELMNT_VIS
                         IF_DO_CHECK     = ABAP_FALSE ).

***----------------------------------------------------------------------------
*** Set Sel.Screen Report
    ME->R_REGMDL->SET_RPSEL( IF_RPSEL = IS_REGMDL_VIS-RPSEL ).

*** Set Query Class
    ME->R_REGMDL->SET_CLQRY( IF_CLQRY = IS_REGMDL_VIS-CLQRY ).

*** Set Detail UI Class
    ME->R_REGMDL->SET_CLDUI( IF_CLDUI = IS_REGMDL_VIS-CLDUI ).

*** Set Material Class
    ME->R_REGMDL->SET_CLMAT( IF_CLMAT = IS_REGMDL_VIS-CLMAT ).

***----------------------------------------------------------------------------
*** Perform Check
    if IF_DO_CHECK eq ABAP_TRUE.
      ME->R_REGMDL->CHECK( ).
    endif.

  endmethod.
