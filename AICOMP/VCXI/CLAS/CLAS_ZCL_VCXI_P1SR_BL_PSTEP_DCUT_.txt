
class ZCL_VCXI_P1SR_BL_PSTEP_DCUT definition
  public
  inheriting from ZCL_VCXI_P1SR_BL_PSTEP
  create public .

public section.

  interfaces ZIF_VCXI_PXSR_PCDIR .
  interfaces ZIF_VCXI_XCSR_BL_OUTSO .
  interfaces ZIF_VCXI_PXSR_ADJ_PCDIM .

  constants C_IPSTP_DCUT type /VCXI/VSMC_IPSTP value 'P1_PS_DCUT' ##NO_TEXT.

  methods CONSTRUCTOR
    importing
      !IF_IPSTP type /VCXI/VSMC_IPSTP default C_IPSTP_DCUT
      !IF_DATE type DATS
      !IR_APPDATA type ref to /VCXI/CL_VKSR_APPDATA
    raising
      /VCXI/CX_CKX .

  methods ADD_ASPECTS_TO_RTE_PSTEP
    redefinition .
  methods ADD_LIMITS_TO_RTE_PSTEP
    redefinition .
  methods COMPARE
    redefinition .
  methods COPY
    redefinition .
  methods GET_ARBPL_INFO
    redefinition .
  methods GET_SPVRE_ACTVY
    redefinition .
  methods GET_SPVRE_MF
    redefinition .
  methods INIT_BY_SPEC
    redefinition .
protected section.

  data F_FLUTE type ZVCXI_PCS_FLUTE .
  data F_TDCUT type ZVCXI_P0SS_TDCUT .
  data S_MS_TOOL type ZVCXI_P0SR_S_MS_TOOL .
  data S_PCDIM_IN type ZVCXI_P0SP_S_PCDIM .
  data S_TRLET type ZVCXI_PXS_S_TRLET .
  data S_TRWIT type ZVCXI_PXS_S_TRWIT .
  data T_MFCSP type /VCXI/CKX_TT_STRING .
  data T_MFMO_P type ZVCXI_P0SR_TT_MFMO_P .
  data T_MS_EMBMAT type ZVCXI_PXSR_TT_MS_EMBMAT .
  data T_PCDIM_OUT type ZVCXI_P0SP_TT_PCDIM .

  methods GET_MS_EMBMAT_AVGCO
    importing
      !IS_MS_EMBMAT type ZVCXI_PXSR_S_MS_EMBMAT
    exporting
      !ES_AVGCO type ZVCXI_XCS_S_AVGCO
      !ES_AVGCP type ZVCXI_XCS_S_AVGCP
    raising
      /VCXI/CX_CKX .
  methods ADD_SPVRE_MC_EMBMAT
    importing
      !IR_BL_RTESPV type ref to ZCL_VCXI_XCSR_BL_RTESPV
      !IR_SPVRE_PARENT type ref to /VCXI/CL_VKSR_SPVRE
    changing
      !CT_SPVRE type /VCXI/VKSR_TT_SPVRE
    raising
      /VCXI/CX_CKX .
  methods ADD_SPVRE_MC_TOOL
    importing
      !IR_BL_RTESPV type ref to ZCL_VCXI_XCSR_BL_RTESPV
      !IR_SPVRE_PARENT type ref to /VCXI/CL_VKSR_SPVRE
    changing
      !CT_SPVRE type /VCXI/VKSR_TT_SPVRE
    raising
      /VCXI/CX_CKX .
  methods FILL_SPVRE_ADDON_DCUT
    importing
      !IR_BL_RTESPV type ref to ZCL_VCXI_XCSR_BL_RTESPV
      !IR_SPVRE_ADDON type ref to /VCXI/CL_VKSR_SPVRE
    changing
      !CT_SPVRE type /VCXI/VKSR_TT_SPVRE
    raising
      /VCXI/CX_CKX .
  methods INIT_BY_CFG
    importing
      !IT_PRVDR type /VCXI/VKSR_TT_GATE
    raising
      /VCXI/CX_CKX .
  methods INIT_BY_EMBOS
    importing
      !IT_PRVDR type /VCXI/VKSR_TT_GATE
    raising
      /VCXI/CX_CKX .
  methods INIT_BY_COMM_PREV
    importing
      !IT_PRVDR type /VCXI/VKSR_TT_GATE
    raising
      /VCXI/CX_CKX .
  methods INIT_BY_DCUT
    importing
      !IT_PRVDR type /VCXI/VKSR_TT_GATE
    raising
      /VCXI/CX_CKX .

  methods ADD_SPVRE_MFSO_P
    redefinition .
  methods FILL_SPVRE_ADDON
    redefinition .
  methods GET_PCDIM_OUT
    redefinition .
  methods GET_XML_DREF
    redefinition .
private section.
endclass. "ZCL_VCXI_P1SR_BL_PSTEP_DCUT definition
class ZCL_VCXI_P1SR_BL_PSTEP_DCUT implementation.
  method ADD_ASPECTS_TO_RTE_PSTEP.

***----------------------------------------------------------------------------
*** Get Super
    SUPER->ADD_ASPECTS_TO_RTE_PSTEP( IR_RTE_PSTEP = IR_RTE_PSTEP ).


***----------------------------------------------------------------------------
*** Aspect - Number of Outs
    try.
        IR_RTE_PSTEP->SET_RTE_SAREC( IF_IAATY  = ZCL_VCXI_PXSP_PR_ADDON_DCUT=>C_IAATY_NOOUT
                                     II_ARECN  = value #( ( SIGN = 'I'  OPTION = 'LE'
                                                            LOW = reduce #( init LF_NOOUT = 0 for LS_PCDIM_OUT in ME->T_PCDIM_OUT next LF_NOOUT = LF_NOOUT + LS_PCDIM_OUT-NOOUT ) ) )
                                     IF_MAXSEL = 1
                                     IF_ASORT  = /VCXI/CL_VSMR_RTE_CADDON=>C_ASORT_DESCENDING ).
      catch /VCXI/CX_CKX.
    endtry.

*** Aspect - Embossing Type
    try.
        if ME->T_MS_EMBMAT is not initial.
          IR_RTE_PSTEP->SET_RTE_SAVAL( IF_IAATY  = ZCL_VCXI_PXSP_PR_ADDON_DCUT=>C_IAATY_TEMBO
                                       II_AVVAL  = value #( for LS_MS_EMBMAT in ME->T_MS_EMBMAT ( SIGN = 'I' OPTION = 'EQ' LOW = LS_MS_EMBMAT-TEMBO ) ) ).
        endif.
      catch /VCXI/CX_CKX.
    endtry.

  endmethod.
  method ADD_LIMITS_TO_RTE_PSTEP.

    data: LF_LDUOM type /VCXI/VSMC_LDUOM,
          LF_LDVAL type /VCXI/VSMR_LDVAL.
    data: LI_LVVAL type /VCXI/VSMR_TT_RTE_RANGE_LVVAL.
    data: LT_LTYPE type /VCXI/CKX_TT_OBJECT.
    data: LR_LTYPE  type ref to /VCXI/CL_VSMC_LTYPE,
          LR_OBJECT type ref to /VCXI/IF_CKX_OBJECT.
    field-symbols: <S_LVVAL> type /VCXI/VSMR_S_RTE_RANGE_LVVAL.


    SUPER->ADD_LIMITS_TO_RTE_PSTEP( IR_RTE_PSTEP = IR_RTE_PSTEP ).

***----------------------------------------------------------------------------
*** Get all linked Limits
    LT_LTYPE = ME->R_PSTEP->/VCXI/IF_VSMC_LINK_LTYPE~GET_LINKED_LTYPE( ).
    loop at LT_LTYPE into LR_OBJECT.
      move LR_OBJECT ?to LR_LTYPE.

      clear: LF_LDVAL,
             LF_LDUOM,
             LI_LVVAL.

***----------------------------------------------------------------------------
      case LR_LTYPE->/VCXI/IF_CKX_OBJECT~GET_ID( ).
***     Sheet Length (Inbound)
        when 'P0_LT_DI_SHTLEN'.
          move ME->S_PCDIM_IN-PCLEN      to LF_LDVAL.
          move ME->S_PCDIM_IN-PCLEN_UOM  to LF_LDUOM.

***     Sheet Width (Inbound)
        when 'P0_LT_DI_SHTWID'.
          move ME->S_PCDIM_IN-PCWID      to LF_LDVAL.
          move ME->S_PCDIM_IN-PCWID_UOM  to LF_LDUOM.

***     Die Cut Type
        when 'PX_LT_VP_TDCUT'.
          if ME->F_TDCUT ne 'ALL'.
            append initial line to LI_LVVAL assigning <S_LVVAL>.
            move 'I'                       to <S_LVVAL>-SIGN.
            move 'EQ'                      to <S_LVVAL>-OPTION.
            move ME->F_TDCUT               to <S_LVVAL>-LOW.
          else.
            append initial line to LI_LVVAL assigning <S_LVVAL>.
            move 'I'                       to <S_LVVAL>-SIGN.
            move 'CP'                      to <S_LVVAL>-OPTION.
            move '*'                       to <S_LVVAL>-LOW.
          endif.

***     LE/TR Trim
        when 'P0_LT_DP_LETRTRIM'.
          if ZIF_VCXI_PXSR_PCDIR~F_PCDIR eq 'A'.
            LF_LDVAL = ME->S_TRLET-TRLET.
            LF_LDUOM = ME->S_TRLET-TRLET_UOM.
          else.
            LF_LDVAL = ME->S_TRWIT-TRWIT.
            LF_LDUOM = ME->S_TRWIT-TRWIT_UOM.
          endif.

***     Side Trim
        when 'P0_LT_DP_SIDETRIM'.
          if ZIF_VCXI_PXSR_PCDIR~F_PCDIR eq 'A'.
            LF_LDVAL = ME->S_TRWIT-TRWIT.
            LF_LDUOM = ME->S_TRWIT-TRWIT_UOM.
          else.
            LF_LDVAL = ME->S_TRLET-TRLET.
            LF_LDUOM = ME->S_TRLET-TRLET_UOM.
          endif.

***     Number of Outs
        when 'PX_LT_DP_NOUTT'.
          LF_LDVAL = reduce #( init LF_NOOUT = 0 for LS_PCDIM_OUT in ME->T_PCDIM_OUT next LF_NOOUT = LF_NOOUT + LS_PCDIM_OUT-NOOUT ).
          LF_LDUOM = 'ST'.

***     Flute Type
        when 'PC_LT_VP_FLUTE'.
          append initial line to LI_LVVAL assigning <S_LVVAL>.
          move 'I'                       to <S_LVVAL>-SIGN.
          move 'EQ'                      to <S_LVVAL>-OPTION.
          move ME->F_FLUTE               to <S_LVVAL>-LOW.

***     Embossing Type
        when 'PX_LT_VP_TEMBO'.
          if ME->T_MS_EMBMAT is not initial.
            LI_LVVAL = value #( for LS_MS_EMBMAT in ME->T_MS_EMBMAT ( SIGN = 'I' OPTION = 'EQ' LOW = LS_MS_EMBMAT-TEMBO ) ).
          else.
***         Not applicable Limit
            IR_RTE_PSTEP->SET_LIMIT_NA( IR_LTYPE = LR_LTYPE ).
            continue.
          endif.

        when others.
          continue.
      endcase.


***----------------------------------------------------------------------------
***   Set
      case LR_LTYPE->F_MLTYP.
        when /VCXI/CL_VSMC_LTYPE=>C_MLTYP_D.
          IR_RTE_PSTEP->SET_RTE_LDIM( IR_LTYPE = LR_LTYPE
                                      IF_LDVAL = LF_LDVAL
                                      IF_LDUOM = LF_LDUOM ).
        when /VCXI/CL_VSMC_LTYPE=>C_MLTYP_V.
          IR_RTE_PSTEP->SET_RTE_LVAL( IR_LTYPE = LR_LTYPE
                                      II_LVVAL = LI_LVVAL ).
      endcase.
    endloop.

  endmethod.
  method ADD_SPVRE_MC_EMBMAT.

    data: LS_MS_EMBMAT type ZVCXI_PXSR_S_MS_EMBMAT,
          LS_AVGCO     type ZVCXI_XCS_S_AVGCO,
          LS_AVGCP     type ZVCXI_XCS_S_AVGCP.
    data: LR_SPVRE type ref to /VCXI/CL_VKSR_SPVRE.


    loop at ME->T_MS_EMBMAT into LS_MS_EMBMAT.

***------------------------------------------------------------------------------------------------
***   Create Supervisor Runtime Element
      LR_SPVRE = new #( IR_SPVCE  = IR_BL_RTESPV->GET_SPVCE( IF_ID           = switch #( LS_MS_EMBMAT-FEMBM when ABAP_TRUE then 'MC_EMBMAT'
                                                                                                                           else 'EMBOS' )
                                                             IR_SPVCE_PARENT = IR_SPVRE_PARENT->R_SPVCE )
                        IR_PARENT = IR_SPVRE_PARENT ).
      append LR_SPVRE to CT_SPVRE.

***   Set In Use
      LR_SPVRE->SET_INUSE( IF_INUSE = ABAP_TRUE ).

      case LS_MS_EMBMAT-FEMBM.
        when ABAP_TRUE.
***------------------------------------------------------------------------------------------------
***       Set Material
          LR_SPVRE->SET_LEOKY( IS_LEOKY = value #( LEOKY = LS_MS_EMBMAT-MATNR
                                                   SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).

***       Set Embossing Type
          LR_SPVRE->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_PXSP_PR_MC_EMBMAT=>C_PARID_TEMBO
                                                   PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR
                                                   ATWRT = LS_MS_EMBMAT-TEMBO
                                                   SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).

***       Set Average Consumption and Average Consumption Per
          ME->GET_MS_EMBMAT_AVGCO( exporting IS_MS_EMBMAT = LS_MS_EMBMAT
                                   importing ES_AVGCO     = LS_AVGCO
                                             ES_AVGCP     = LS_AVGCP ).
          LR_SPVRE->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_PXSP_PR_MC_EMBMAT=>C_PARID_AVGCO_A
                                                   PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC
                                                   ATFLV = LS_AVGCO-AVGCO
                                                   UNIT  = LS_AVGCO-AVGCO_UOM
                                                   SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).
          LR_SPVRE->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_PXSP_PR_MC_EMBMAT=>C_PARID_AVGCP_P
                                                   PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC
                                                   ATFLV = LS_AVGCP-AVGCP
                                                   UNIT  = LS_AVGCP-AVGCP_UOM
                                                   SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).

***       Set Procurement Option
          LR_SPVRE->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_PXSP_PR_MC_EMBMAT=>C_PARID_PURCH
                                                   PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR
                                                   ATWRT = LS_MS_EMBMAT-PURCH
                                                   SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).

          if LS_MS_EMBMAT-PURCH ne ZCL_VCXI_XCSS_PR_MS=>C_PURCH_STOCK.
***         Set Price and Price Unit
            LR_SPVRE->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_PXSP_PR_MC_EMBMAT=>C_PARID_PRICE
                                                     PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC
                                                     ATFLV = LS_MS_EMBMAT-PRICE
                                                     CURKY = LS_MS_EMBMAT-PRICE_CURR
                                                     SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).
            LR_SPVRE->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_PXSP_PR_MC_EMBMAT=>C_PARID_PRICP_A
                                                     PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC
                                                     ATFLV = LS_MS_EMBMAT-PRICP
                                                     UNIT  = LS_MS_EMBMAT-PRICP_UOM
                                                     SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).

***         Set Vendor
            LR_SPVRE->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_PXSP_PR_MC_EMBMAT=>C_PARID_LIFNR
                                                     PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR
                                                     ATWRT = LS_MS_EMBMAT-LIFNR
                                                     SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).
          endif.

***------------------------------------------------------------------------------------------------
        when others.
***       Set Embossing Type
          LR_SPVRE->SET_LEOKY( IS_LEOKY = value #( LEOKY = LS_MS_EMBMAT-TEMBO
                                                   SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).

      endcase.
    endloop.

  endmethod.
  method ADD_SPVRE_MC_TOOL.

    data: LR_SPVRE_MC_TOOL type ref to /VCXI/CL_VKSR_SPVRE.


    check ME->S_MS_TOOL-MATNR is not initial.

***------------------------------------------------------------------------------------------------
*** Create Supervisor Runtime Element
    LR_SPVRE_MC_TOOL = new #( IR_SPVCE  = IR_BL_RTESPV->GET_SPVCE( IF_ID           = 'MC_TOOL'
                                                                   IR_SPVCE_PARENT = IR_SPVRE_PARENT->R_SPVCE )
                              IR_PARENT = IR_SPVRE_PARENT ).
    append LR_SPVRE_MC_TOOL to CT_SPVRE.

*** Set In Use
    LR_SPVRE_MC_TOOL->SET_INUSE( IF_INUSE = ABAP_TRUE ).

***------------------------------------------------------------------------------------------------
*** Set Material Number
    LR_SPVRE_MC_TOOL->SET_LEOKY( IS_LEOKY = value #( LEOKY = ME->S_MS_TOOL-MATNR
                                                     SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).

***------------------------------------------------------------------------------------------------
*** Set Average Consumption
    LR_SPVRE_MC_TOOL->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_XCSP_PR_MC_TOOL=>C_PARID_AVGCO_P
                                                     PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC
                                                     ATFLV = 1
                                                     UNIT  = 'ST'
                                                     SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).

*** Set Average Consumption Per
    LR_SPVRE_MC_TOOL->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_XCSP_PR_MC_TOOL=>C_PARID_AVGCP_P
                                                     PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC
                                                     ATFLV = ME->S_MS_TOOL-LTITO
                                                     UNIT  = ME->S_MS_TOOL-LTITO_UOM
                                                     SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).

  endmethod.
  method ADD_SPVRE_MFSO_P.

    data: LF_INDEX type I.
    data: LS_PCDIM_OUT type ZVCXI_P0SP_S_PCDIM.
    data: LR_SPVCE        type ref to /VCXI/CL_VKSC_SPVCE,
          LR_SPVRE_MFSO_P type ref to /VCXI/CL_VKSR_SPVRE.

*** Remove existing
    loop at CT_SPVRE into LR_SPVRE_MFSO_P.
      if LR_SPVRE_MFSO_P->GET_ID( ) eq 'MFSO_P'.
        delete CT_SPVRE.
      endif.
    endloop.

***------------------------------------------------------------------------------------------------
*** Get Customizing Element
    LR_SPVCE = IR_BL_RTESPV->GET_SPVCE( IF_ID           = 'MFSO_P'
                                        IR_SPVCE_PARENT = IR_SPVRE_MFMGR->R_SPVCE ).

    loop at ME->T_PCDIM_OUT into LS_PCDIM_OUT.
      LF_INDEX = SY-TABIX.

***   Suppress other Outs, if they are created as Material Flow Material Out
      if ME->T_MFMO_P is not initial.
        check LF_INDEX le 1.
      endif.

***   Create Supervisor Runtime Element
      create object LR_SPVRE_MFSO_P
        exporting
          IR_SPVCE  = LR_SPVCE
          IR_PARENT = IR_SPVRE_MFMGR.
      append LR_SPVRE_MFSO_P to CT_SPVRE.

***   Set In Use
      LR_SPVRE_MFSO_P->SET_INUSE( IF_INUSE = ABAP_TRUE ).

***------------------------------------------------------------------------------------------------
***   Get MF X-Connect
      if LINE_EXISTS( ME->T_IMFXC_OUT[ LF_INDEX ] ).
***     Set MF X-Connect
        LR_SPVRE_MFSO_P->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_XCSP_PR_MF=>C_PARID_IMFXC
                                                        PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR
                                                        ATWRT = ME->T_IMFXC_OUT[ LF_INDEX ]
                                                        SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).
      endif.

***   Cost Split IDs relevant for Material Flow
      if LINE_EXISTS( ME->T_MFCSP[ LF_INDEX ] ).
        LR_SPVRE_MFSO_P->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_XCSP_PR_MFSO=>C_PARID_MFCSP
                                                        PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_STRG
                                                        STRNG = ME->T_MFCSP[ LF_INDEX ]
                                                        SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).
      endif.
    endloop.

  endmethod.
  method COMPARE.

    data:  LR_BL_PSTEP_DCUT type ref to ZCL_VCXI_P1SR_BL_PSTEP_DCUT.

*** Super Compare
    RF_SIMILAR = SUPER->COMPARE( IR_BL_PSTEP = IR_BL_PSTEP ).
    check RF_SIMILAR eq ABAP_TRUE.

    move IR_BL_PSTEP ?to LR_BL_PSTEP_DCUT.


***------------------------------------------------------------------------------------------------
*** PC Dimensions
    if ME->S_PCDIM_IN  eq LR_BL_PSTEP_DCUT->S_PCDIM_IN and
       ME->T_PCDIM_OUT eq LR_BL_PSTEP_DCUT->T_PCDIM_OUT.
      move ABAP_TRUE  to RF_SIMILAR.
    else.
      move ABAP_FALSE to RF_SIMILAR.
      exit.
    endif.

*** Die Cut Type
    if ME->F_TDCUT eq LR_BL_PSTEP_DCUT->F_TDCUT.
      move ABAP_TRUE  to RF_SIMILAR.
    else.
      move ABAP_FALSE to RF_SIMILAR.
      exit.
    endif.

*** Trim
    if ME->S_TRLET   eq LR_BL_PSTEP_DCUT->S_TRLET and
       ME->S_TRWIT   eq LR_BL_PSTEP_DCUT->S_TRWIT.
      move ABAP_TRUE  to RF_SIMILAR.
    else.
      move ABAP_FALSE to RF_SIMILAR.
      exit.
    endif.

*** Tool
    if ME->S_MS_TOOL eq LR_BL_PSTEP_DCUT->S_MS_TOOL.
      move ABAP_TRUE  to RF_SIMILAR.
    else.
      move ABAP_FALSE to RF_SIMILAR.
      exit.
    endif.

*** Flute
    if ME->F_FLUTE eq LR_BL_PSTEP_DCUT->F_FLUTE.
      move ABAP_TRUE  to RF_SIMILAR.
    else.
      move ABAP_FALSE to RF_SIMILAR.
      exit.
    endif.

*** Material Flow Material Out - PC
    if ME->T_MFMO_P eq LR_BL_PSTEP_DCUT->T_MFMO_P.
      move ABAP_TRUE  to RF_SIMILAR.
    else.
      move ABAP_FALSE to RF_SIMILAR.
      exit.
    endif.

*** Material Flow relevant Cost Split IDs
    if ME->T_MFCSP eq LR_BL_PSTEP_DCUT->T_MFCSP.
      move ABAP_TRUE  to RF_SIMILAR.
    else.
      move ABAP_FALSE to RF_SIMILAR.
      return.
    endif.

*** Embossing Material
    if ME->T_MS_EMBMAT eq LR_BL_PSTEP_DCUT->T_MS_EMBMAT.
      move ABAP_TRUE  to RF_SIMILAR.
    else.
      move ABAP_FALSE to RF_SIMILAR.
      return.
    endif.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IF_IPSTP   = IF_IPSTP
                        IF_DATE    = IF_DATE
                        IR_APPDATA = IR_APPDATA ).

*** Initialize Outsourcing Info
    move ZCL_VCXI_XCSP_PR_OUTSO=>C_PARID_PRICP_P to ME->ZIF_VCXI_XCSR_BL_OUTSO~F_PARID_PRICP.
    ME->INIT_OUTSO( ).

  endmethod.
  method COPY.

    data:  LR_BL_PSTEP_DCUT type ref to ZCL_VCXI_P1SR_BL_PSTEP_DCUT.

*** Super Copy
    RR_BL_PSTEP = SUPER->COPY( ).
    move RR_BL_PSTEP ?to LR_BL_PSTEP_DCUT.

***------------------------------------------------------------------------------------------------
*** PC Dimensions
    move ME->S_PCDIM_IN    to LR_BL_PSTEP_DCUT->S_PCDIM_IN.
    move ME->T_PCDIM_OUT   to LR_BL_PSTEP_DCUT->T_PCDIM_OUT.

*** Die Cut Type
    move ME->F_TDCUT       to LR_BL_PSTEP_DCUT->F_TDCUT.

*** Trim
    move ME->S_TRLET       to LR_BL_PSTEP_DCUT->S_TRLET.
    move ME->S_TRWIT       to LR_BL_PSTEP_DCUT->S_TRWIT.

*** Tool
    move ME->S_MS_TOOL     to LR_BL_PSTEP_DCUT->S_MS_TOOL.

*** Flute
    move ME->F_FLUTE       to LR_BL_PSTEP_DCUT->F_FLUTE.

*** Material Flow Material Out - PC
    move ME->T_MFMO_P      to LR_BL_PSTEP_DCUT->T_MFMO_P.

*** Material Flow relevant Cost Split IDs
    move ME->T_MFCSP       to LR_BL_PSTEP_DCUT->T_MFCSP.

*** Embossing Material
    move ME->T_MS_EMBMAT   to LR_BL_PSTEP_DCUT->T_MS_EMBMAT.

  endmethod.
  method FILL_SPVRE_ADDON.

    SUPER->FILL_SPVRE_ADDON( exporting IR_BL_RTESPV   = IR_BL_RTESPV
                                       IR_SPVRE_ADDON = IR_SPVRE_ADDON
                                       IR_ARBPL       = IR_ARBPL
                             changing  CT_SPVRE       = CT_SPVRE ).

    case IR_SPVRE_ADDON->GET_ID( ).
      when 'P1_AT_DCUT'.
        ME->FILL_SPVRE_ADDON_DCUT( exporting IR_BL_RTESPV   = IR_BL_RTESPV
                                             IR_SPVRE_ADDON = IR_SPVRE_ADDON
                                   changing  CT_SPVRE       = CT_SPVRE ).
    endcase.

  endmethod.
  method FILL_SPVRE_ADDON_DCUT.

*** Add PC Out
    ME->ADD_SPVRE_PCOUT( exporting IR_BL_RTESPV    = IR_BL_RTESPV
                                   IR_SPVRE_PARENT = IR_SPVRE_ADDON
                                   IT_PCDIM        = ME->T_PCDIM_OUT
                         changing  CT_SPVRE        = CT_SPVRE ).

*** Add Material Consumption for Embossing Material
    ME->ADD_SPVRE_MC_EMBMAT( exporting IR_BL_RTESPV    = IR_BL_RTESPV
                                       IR_SPVRE_PARENT = IR_SPVRE_ADDON
                             changing  CT_SPVRE        = CT_SPVRE ).

*** Add Material Consumption for Tool
    ME->ADD_SPVRE_MC_TOOL( exporting IR_BL_RTESPV    = IR_BL_RTESPV
                                     IR_SPVRE_PARENT = IR_SPVRE_ADDON
                           changing  CT_SPVRE        = CT_SPVRE ).

  endmethod.
  method GET_ARBPL_INFO.

***------------------------------------------------------------------
*** Don't overrule existing
    check CF_ID_GRP   is initial and
          CF_ID_ARBPL is initial.

***------------------------------------------------------------------
*** Standard
    move 'CONV'     to CF_ID_ARBPL.

**** If Manual
*    if ME->ZIF_VCXI_XCSR_BL_MANU~F_MANWC eq ABAP_TRUE.
*      move 'MANU'     to CF_ID_ARBPL.
*    endif.

*** If Outsourcing
    if ME->ZIF_VCXI_XCSR_BL_OUTSO~F_OUTSO eq ABAP_TRUE.
      move 'OUTSO'    to CF_ID_ARBPL.
    endif.

  endmethod.
  method GET_MS_EMBMAT_AVGCO.

    clear: ES_AVGCO, ES_AVGCP.

*** Use 1000 PC as base for more accuracy of M2
    ES_AVGCP-AVGCP     = 1000.
    ES_AVGCP-AVGCP_UOM = 'ST'.

*** Embossing Area
    ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = IS_MS_EMBMAT-EMBLE * ES_AVGCP-AVGCP
                                                        IF_CALCV_UOM_1 = IS_MS_EMBMAT-EMBLE_UOM
                                                        IF_CALCV_2     = IS_MS_EMBMAT-EMBWI
                                                        IF_CALCV_UOM_2 = IS_MS_EMBMAT-EMBWI_UOM
                                                        IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_MULTIPLY
                                              importing EF_CALCV       = ES_AVGCO-AVGCO
                                                        EF_CALCV_UOM   = ES_AVGCO-AVGCO_UOM ).

  endmethod.
  method GET_PCDIM_OUT.

    data: LF_PCTYP type ZVCXI_P0SP_PCTYP,
          LF_NOUPS type ZVCXI_PXS_NOUPS,
          LF_NOUTT type ZVCXI_PXS_NOUTT.
    data: LS_HNDDIM type ZVCXI_PXS_S_HNDDIM.
    data: LT_ICSPL  type ZVCXI_XCSP_TT_ICSPL.
    data: LR_GATE          type ref to /VCXI/CL_VKSR_GATE,
          LR_PR_CSTEP_DCUT type ref to ZCL_VCXI_P1SS_PR_CSTEP_DCUT,
          LR_PR_CFG        type ref to ZCL_VCXI_P1SS_PR_CFG,
          LR_PR_CRCONO     type ref to ZCL_VCXI_P1SS_PR_CRCONO,
          LR_BL_CRFPROP    type ref to ZCL_VCXI_P1SS_BL_CRFPROP,
          LR_BL_CRBPROP    type ref to ZCL_VCXI_P1SS_BL_CRBPROP.

***--------------------------------------------------------------------------------------
*** Get PC Type and Direction
    ZCL_VCXI_P0SR_CUST_PSTEP=>GET_PSTEP_INFO( exporting IF_IPSTP = ME->F_IPSTP
                                              importing EF_PCTYP = LF_PCTYP ).

***--------------------------------------------------------------------------------------
*** Process Outbound Provider
    loop at IT_PRVDR into LR_GATE.
      case type of LR_GATE->R_CLSPR.
***     Conversion Step Die Cutting
        when type ZCL_VCXI_P1SS_PR_CSTEP_DCUT into LR_PR_CSTEP_DCUT.
          LR_BL_CRFPROP = LR_PR_CSTEP_DCUT->ZIF_VCXI_P1SS_COMM_NEXT~GET_CRFPX( ).
          LR_BL_CRBPROP = LR_PR_CSTEP_DCUT->ZIF_VCXI_P1SS_COMM_NEXT~GET_CRBPX( ).
          LF_NOUPS      = LR_PR_CSTEP_DCUT->ZIF_VCXI_P1SS_COMM_NEXT~GET_NOUPS( ).
          LF_NOUTT      = LR_PR_CSTEP_DCUT->GET_NOUTT( ).
***     Finished Good
        when type ZCL_VCXI_P1SS_PR_CFG into LR_PR_CFG.
          LR_BL_CRFPROP = LR_PR_CFG->GET_CRFPX( ).
          LR_BL_CRBPROP = LR_PR_CFG->GET_CRBPX( ).
          LF_NOUPS      = LR_PR_CFG->GET_NOUPS( ).
          LF_NOUTT      = LR_PR_CFG->GET_NOUTT( ).
***     Semi-Finished Good
        when type ZCL_VCXI_P1SS_PR_CRCONO into LR_PR_CRCONO.
          LR_BL_CRFPROP = LR_PR_CRCONO->ZIF_VCXI_P1SS_COMM_NEXT~GET_CRFPX( ).
          LR_BL_CRBPROP = LR_PR_CRCONO->ZIF_VCXI_P1SS_COMM_NEXT~GET_CRBPX( ).
          LF_NOUPS      = LR_PR_CRCONO->ZIF_VCXI_P1SS_COMM_NEXT~GET_NOUPS( ).
          LF_NOUTT      = LR_PR_CRCONO->GET_NOUTT( ).
        when others.
          continue.
      endcase.

***   Get Handling Dimensions based on Corrugator Forward Property
      if LR_BL_CRFPROP is bound.
        LS_HNDDIM = LR_BL_CRFPROP->S_HNDDIM.
      else.
        clear LS_HNDDIM.
      endif.

***   Add PC Dimension
      append value #( PCTYP     = LF_PCTYP
                      PCLEN     = LS_HNDDIM-HNDLE
                      PCLEN_UOM = LS_HNDDIM-HNDLE_UOM
                      PCWID     = LS_HNDDIM-HNDWI
                      PCWID_UOM = LS_HNDDIM-HNDWI_UOM
                      PCHEI     = LS_HNDDIM-HNDHE
                      PCHEI_UOM = LS_HNDDIM-HNDHE_UOM

                      PCUPS     = LF_NOUPS
                      NOOUT     = LF_NOUTT
                      PCDIR     = 'A'
                    ) to RT_PCDIM.

***   Collect relevant Cost Split IDs
      if LR_BL_CRBPROP is bound.
        clear LT_ICSPL.
        LT_ICSPL = value #( for LS_CSPLIT in LR_BL_CRBPROP->T_CSPLIT where ( DCSPL ne 0 ) ( LS_CSPLIT-ICSPL ) ).
        append ZCL_VCXI_XCSP_PR_MFSO=>CONV_TO_MFCSP( IT_ICSPL = LT_ICSPL ) to ME->T_MFCSP.
      endif.
    endloop.

  endmethod.
  method GET_SPVRE_ACTVY.

    data: LR_SPVCE       type ref to /VCXI/CL_VKSC_SPVCE,
          LR_SPVRE_ACTVY type ref to /VCXI/CL_VKSR_SPVRE.

***------------------------------------------------------------------------------------------------
*** Get Customizing Element for Activity Unitizing
    LR_SPVCE = IR_BL_RTESPV->GET_SPVCE( IF_ID           = 'ACTVY_DCUT'
                                        IR_SPVCE_PARENT = IR_SPVRE_ARBPL->R_SPVCE ).

*** Create Supervisor Runtime Element
    create object LR_SPVRE_ACTVY
      exporting
        IR_SPVCE  = LR_SPVCE
        IR_PARENT = IR_SPVRE_ARBPL.
    append LR_SPVRE_ACTVY to RT_SPVRE.

*** Set In Use
    LR_SPVRE_ACTVY->SET_INUSE( IF_INUSE = ABAP_TRUE ).

***------------------------------------------------------------------------------------------------
*** Add PC Out
    ME->ADD_SPVRE_PCOUT( exporting IR_BL_RTESPV    = IR_BL_RTESPV
                                   IR_SPVRE_PARENT = LR_SPVRE_ACTVY
                                   IT_PCDIM        = ME->T_PCDIM_OUT
                         changing  CT_SPVRE        = RT_SPVRE ).

*** Add Material Consumption for Embossing Material
    ME->ADD_SPVRE_MC_EMBMAT( exporting IR_BL_RTESPV    = IR_BL_RTESPV
                                       IR_SPVRE_PARENT = LR_SPVRE_ACTVY
                             changing  CT_SPVRE        = RT_SPVRE ).

*** Add Material Consumption for Tool
    ME->ADD_SPVRE_MC_TOOL( exporting IR_BL_RTESPV    = IR_BL_RTESPV
                                     IR_SPVRE_PARENT = LR_SPVRE_ACTVY
                           changing  CT_SPVRE        = RT_SPVRE ).

  endmethod.
  method GET_SPVRE_MF.

*** Force new MFO
    move ABAP_FALSE to CF_MFO.

*** Call Super
    SUPER->GET_SPVRE_MF( exporting IR_BL_RTESPV   = IR_BL_RTESPV
                                   IR_SPVRE_MFMGR = IR_SPVRE_MFMGR
                         changing  CT_SPVRE       = CT_SPVRE
                                   CF_MFI         = CF_MFI
                                   CF_MFO         = CF_MFO ).

***------------------------------------------------------------------------------------------------
*** Add Material Flow Material Out - PC
    ME->ADD_SPVRE_MFMO_P( exporting IR_BL_RTESPV   = IR_BL_RTESPV
                                    IR_SPVRE_MFMGR = IR_SPVRE_MFMGR
                                    IT_MFMO_P      = ME->T_MFMO_P
                          changing  CT_SPVRE       = CT_SPVRE ).

  endmethod.
  method GET_XML_DREF.

*** Super References
    RT_DATA = SUPER->GET_XML_DREF( ).

*** Add additional References
    append value #( NAME = 'S_PCDIM_IN'   VALUE = ref #( ME->S_PCDIM_IN )  ) to RT_DATA.
    append value #( NAME = 'T_PCDIM_OUT'  VALUE = ref #( ME->T_PCDIM_OUT ) ) to RT_DATA.

    append value #( NAME = 'F_TDCUT'      VALUE = ref #( ME->F_TDCUT )     ) to RT_DATA.
    append value #( NAME = 'S_MS_TOOL'    VALUE = ref #( ME->S_MS_TOOL )   ) to RT_DATA.
    append value #( NAME = 'T_MS_EMBMAT'  VALUE = ref #( ME->T_MS_EMBMAT ) ) to RT_DATA.

    append value #( NAME = 'S_TRWIT'      VALUE = ref #( ME->S_TRWIT )     ) to RT_DATA.
    append value #( NAME = 'S_TRLET'      VALUE = ref #( ME->S_TRLET )     ) to RT_DATA.

    append value #( NAME = 'F_FLUTE'      VALUE = ref #( ME->F_FLUTE )     ) to RT_DATA.

    append value #( NAME = 'T_MFMO_P'     VALUE = ref #( ME->T_MFMO_P )    ) to RT_DATA.

    append value #( NAME = 'T_MFCSP'      VALUE = ref #( ME->T_MFCSP )     ) to RT_DATA.

  endmethod.
  method INIT_BY_CFG.

    data: LR_GATE type ref to /VCXI/CL_VKSR_GATE.


*** Get Material Numbers of Finished Good
    loop at IT_PRVDR into LR_GATE where TABLE_LINE->R_CLSPR is instance of ZCL_VCXI_P1SS_PR_CFG.
      append value #( POSID = LR_GATE->GET_POSID( )
                      MATNR = cast ZCL_VCXI_P1SS_PR_CFG( LR_GATE->R_CLSPR )->ZIF_VCXI_PXSS_FG~GET_MATNR( )
                    ) to ME->T_MFMO_P.
    endloop.

*** Remove those w/o Material Number as they are SFGs (before MBQ)
    delete ME->T_MFMO_P where MATNR is initial.

  endmethod.
  method INIT_BY_COMM_PREV.

    data: LR_GATE       type ref to /VCXI/CL_VKSR_GATE,
          LR_COMM_PREV  type ref to ZIF_VCXI_P1SS_COMM_PREV,
          LR_BL_CRFPROP type ref to ZCL_VCXI_P1SS_BL_CRFPROP.


***--------------------------------------------------------------------------------------
*** Get Interface Reference for Communication with Previous
    loop at IT_PRVDR into LR_GATE
                    where TABLE_LINE->R_CLSPR is instance of ZIF_VCXI_P1SS_COMM_PREV.
      LR_COMM_PREV ?= LR_GATE->R_CLSPR.
      exit.
    endloop.
    check LR_COMM_PREV  is bound.


***--------------------------------------------------------------------------------------
*** Get Corrugated Forward Property XML (Inbound)
    LR_BL_CRFPROP = LR_COMM_PREV->GET_CRFPX( ).
    if LR_BL_CRFPROP is bound.
***   Get Flute
      ME->F_FLUTE = LR_BL_CRFPROP->F_FLUTE.
    endif.

  endmethod.
  method INIT_BY_DCUT.

    data: LF_SHTDI type ZVCXI_P0SS_SHTDI.
    data: LR_GATE type ref to /VCXI/CL_VKSR_GATE,
          LR_DCUT type ref to ZIF_VCXI_PXSS_DCUT.


***--------------------------------------------------------------------------------------
*** Get Interface Reference for Die Cutting
    loop at IT_PRVDR into LR_GATE
                    where TABLE_LINE->R_CLSPR is instance of ZIF_VCXI_PXSS_DCUT.
      LR_DCUT ?= LR_GATE->R_CLSPR.
      exit.
    endloop.
    check LR_DCUT  is bound.


***--------------------------------------------------------------------------------------
*** Die Cut Type
    ME->F_TDCUT = LR_DCUT->GET_TDCUT( ).

*** Get Trim on Length (Total) and Trim on Width (Total)
    ME->S_TRLET = LR_DCUT->GET_TRLET( ).
    ME->S_TRWIT = LR_DCUT->GET_TRWIT( ).

*** Sheet Direction
    move LR_DCUT->GET_SHTDI( ) to LF_SHTDI.
    if LF_SHTDI is not initial.
      move LF_SHTDI                               to ME->ZIF_VCXI_PXSR_PCDIR~F_PCDIR.
      move ZIF_VCXI_PXSR_PCDIR=>C_PCDOP_MANDATORY to ME->ZIF_VCXI_PXSR_PCDIR~F_PCDOP.
    endif.

***--------------------------------------------------------------------------------------
*** Die Cut Tool Number
    ME->S_MS_TOOL-MATNR = LR_DCUT->GET_DCTTN( ).

  endmethod.
  method INIT_BY_EMBOS.

    data: LR_GATE         type ref to /VCXI/CL_VKSR_GATE,
          LR_PR_EMBOS     type ref to ZCL_VCXI_PXSS_PR_EMBOS,
          LR_PR_MS_EMBMAT type ref to ZCL_VCXI_PXSS_PR_MS_EMBMAT.
    field-symbols: <S_MS_EMBMAT> type ZVCXI_PXSR_S_MS_EMBMAT.


*** Get Embossment
    loop at IT_PRVDR into LR_GATE where TABLE_LINE->R_CLSPR is instance of ZCL_VCXI_PXSS_PR_EMBOS.
      LR_PR_EMBOS = cast ZCL_VCXI_PXSS_PR_EMBOS( LR_GATE->R_CLSPR ).

      append initial line to ME->T_MS_EMBMAT assigning <S_MS_EMBMAT>.
      <S_MS_EMBMAT>-TEMBO   = LR_PR_EMBOS->GET_TEMBO( importing EF_FEMBM = <S_MS_EMBMAT>-FEMBM ).
      <S_MS_EMBMAT>-S_EMBLE = LR_PR_EMBOS->GET_EMBLE( ).
      <S_MS_EMBMAT>-S_EMBWI = LR_PR_EMBOS->GET_EMBWI( ).

***   Embossing Material
      if <S_MS_EMBMAT>-FEMBM eq ABAP_TRUE.
        LR_PR_MS_EMBMAT = LR_PR_EMBOS->GET_PRVDR_MS_EMBMAT( importing ER_GATE_MS_EMBMAT = LR_GATE ).
        if LR_PR_MS_EMBMAT is bound and
           LINE_EXISTS( IT_PRVDR[ TABLE_LINE = LR_GATE ] ).
          <S_MS_EMBMAT>-MATNR   = LR_PR_MS_EMBMAT->GET_MATNR( ).
          <S_MS_EMBMAT>-PURCH   = LR_PR_MS_EMBMAT->GET_PURCH( ).
          <S_MS_EMBMAT>-LIFNR   = LR_PR_MS_EMBMAT->GET_LIFNR( ).
          <S_MS_EMBMAT>-S_PRICE = LR_PR_MS_EMBMAT->GET_PRICE( ).
          <S_MS_EMBMAT>-S_PRICP = LR_PR_MS_EMBMAT->GET_PRICP( ).
        endif.
      endif.
    endloop.

  endmethod.
  method INIT_BY_SPEC.

    data: LT_PCDIM type ZVCXI_P0SP_TT_PCDIM.


***--------------------------------------------------------------------------------------
*** Get Inbound PC Dimension
    LT_PCDIM = ME->GET_PCDIM_IN( IT_PRVDR = IT_PRVDR ).
    ME->S_PCDIM_IN = value #( LT_PCDIM[ 1 ] optional ).

*** Get Outbound PC Dimension
    ME->T_PCDIM_OUT = ME->GET_PCDIM_OUT( IT_PRVDR = IT_PRVDR ).

***--------------------------------------------------------------------------------------
*** Initialize by Die Cutting
    ME->INIT_BY_DCUT( IT_PRVDR = IT_PRVDR ).

***--------------------------------------------------------------------------------------
*** Call SUPER
    SUPER->INIT_BY_SPEC( IR_GATE  = IR_GATE
                         IT_PRVDR = IT_PRVDR ).

*** Initialize by Interface for Communication with Previous
    ME->INIT_BY_COMM_PREV( IT_PRVDR = IT_PRVDR ).

*** Initialize by Finished Goods
    ME->INIT_BY_CFG( IT_PRVDR = IT_PRVDR ).

*** Initialize by Embossment
    ME->INIT_BY_EMBOS( IT_PRVDR = IT_PRVDR ).

  endmethod.
  method ZIF_VCXI_PXSR_ADJ_PCDIM~CALC_PCDIM_IN.
  endmethod.
  method ZIF_VCXI_PXSR_ADJ_PCDIM~CALC_PCDIM_OUT.

    CT_PCDIM = ME->T_PCDIM_OUT.

    loop at CT_PCDIM assigning field-symbol(<S_PCDIM>).
***   Suppress Ups of Material Flow Material Out -> Don't consider in conversion MQTYO to MQTYI
      if SY-TABIX gt 1 and
         ME->T_MFMO_P is not initial.
        clear <S_PCDIM>-PCUPS.
      endif.
    endloop.

  endmethod.
  method ZIF_VCXI_PXSR_PCDIR~GET_PCDIM.

    RT_PCDIM = value #( ( ME->S_PCDIM_IN ) ).

  endmethod.
  method ZIF_VCXI_PXSR_PCDIR~SET_PCDIR.

    field-symbols: <S_PCDIM_OUT>  type ZVCXI_P0SP_S_PCDIM.

    move IF_PCDIR to ME->ZIF_VCXI_PXSR_PCDIR~F_PCDIR.

*** Turn PC Dimension IN
    ME->S_PCDIM_IN  = ME->TURN_PCDIM( IS_PCDIM = ME->S_PCDIM_IN
                                      IF_PCDIR = IF_PCDIR ).

*** Turn PC Dimension OUT
    loop at ME->T_PCDIM_OUT assigning <S_PCDIM_OUT>.
      <S_PCDIM_OUT> = ME->TURN_PCDIM( IS_PCDIM = <S_PCDIM_OUT>
                                      IF_PCDIR = IF_PCDIR ).
    endloop.

  endmethod.
