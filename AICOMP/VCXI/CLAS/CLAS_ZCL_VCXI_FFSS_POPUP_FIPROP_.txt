
class ZCL_VCXI_FFSS_POPUP_FIPROP definition
  public
  create protected .

public section.

  constants C_UC_CANCEL type SYUCOMM value 'CANCEL' ##NO_TEXT.
  data S_POPUP_FIPROP_VIS type ZVCXI_FFSS_S_POPUP_FIPROP_VIS .

  methods DO_USER_COMMAND
    importing
      !IF_UCOMM type SYUCOMM
    raising
      /VCXI/CX_CKX .
  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  class-methods START
    importing
      !IR_BL_FIBPROP type ref to ZCL_VCXI_FFSS_BL_FIBPROP
      !IR_BL_FIFPROP type ref to ZCL_VCXI_FFSS_BL_FIFPROP
      !IS_FIWID type ZVCXI_F0S_S_FIWID
      !IS_FITIN type ZVCXI_FFS_S_FITIN
    raising
      /VCXI/CX_CKX .
protected section.

  data T_FIRNG type ZVCXI_FFSS_TT_FIRNG .
  data R_SALV_FIRNG type ref to CL_SALV_TABLE .
  data R_CONTAINER_FIRNG type ref to CL_GUI_CUSTOM_CONTAINER .

  methods INIT_SALV_FIRNG
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_CANCEL
    raising
      /VCXI/CX_CKX .
  methods SET_PF_STATUS
    raising
      /VCXI/CX_CKX .
  methods CONSTRUCTOR
    importing
      !IR_BL_FIFPROP type ref to ZCL_VCXI_FFSS_BL_FIFPROP
      !IR_BL_FIBPROP type ref to ZCL_VCXI_FFSS_BL_FIBPROP
      !IS_FIWID type ZVCXI_F0S_S_FIWID
      !IS_FITIN type ZVCXI_FFS_S_FITIN
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_FFSS_POPUP_FIPROP definition
class ZCL_VCXI_FFSS_POPUP_FIPROP implementation.
  method CONSTRUCTOR.

*** Film Style
    move IR_BL_FIFPROP->F_FISTY to ME->S_POPUP_FIPROP_VIS-FISTY.
    ZCL_VCXI_FFSS_BL_FIFPROP=>GET_FISTY_DESCR( exporting IF_FISTY       = ME->S_POPUP_FIPROP_VIS-FISTY
                                               importing EF_FISTY_DESCR = ME->S_POPUP_FIPROP_VIS-FISTY_DESCR ).

*** Film Dimensions
    move IS_FIWID to ME->S_POPUP_FIPROP_VIS-S_FIWID.
    move IS_FITIN to ME->S_POPUP_FIPROP_VIS-S_FITIN.

*** Gusset Dimensions
    move IR_BL_FIFPROP->S_GULEF to ME->S_POPUP_FIPROP_VIS-S_GULEF.
    move IR_BL_FIFPROP->S_GURIG to ME->S_POPUP_FIPROP_VIS-S_GURIG.

*** Folding Dimensions
    move IR_BL_FIFPROP->F_TFOLD   to ME->S_POPUP_FIPROP_VIS-TFOLD.
    move IR_BL_FIFPROP->F_NOLAY_F to ME->S_POPUP_FIPROP_VIS-NOLAY.

*** Winding Dimensions
    ME->S_POPUP_FIPROP_VIS-S_WIWID = IR_BL_FIFPROP->GET_WIWID( IS_FIWID = ME->S_POPUP_FIPROP_VIS-S_FIWID ).
    ME->S_POPUP_FIPROP_VIS-S_WITHI = IR_BL_FIFPROP->GET_WITHI( IS_FIWID = ME->S_POPUP_FIPROP_VIS-S_FIWID
                                                               IS_FITIN = ME->S_POPUP_FIPROP_VIS-S_FITIN ).

*** Buffer Film Range Table
    move IR_BL_FIFPROP->T_FIRNG to ME->T_FIRNG.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Set PF-Status
    ME->SET_PF_STATUS( ).

    ME->INIT_SALV_FIRNG( ).

  endmethod.
  method DO_USER_COMMAND.

    case IF_UCOMM.
      when C_UC_CANCEL.
        ME->HANDLE_UC_CANCEL( ).

    endcase.

  endmethod.
  method HANDLE_UC_CANCEL.

*** Leave Popup
    leave to screen 0.

  endmethod.
  method INIT_SALV_FIRNG.

    data: LR_CX_SALV_MSG type ref to CX_SALV_MSG.

    check ME->R_CONTAINER_FIRNG is not bound.

*** Initialize Container
    create object ME->R_CONTAINER_FIRNG
      exporting
        CONTAINER_NAME              = '0100CC_FIRNG'
        LIFETIME                    = CL_GUI_CUSTOM_CONTAINER=>LIFETIME_DYNPRO
      exceptions
        CNTL_ERROR                  = 1
        CNTL_SYSTEM_ERROR           = 2
        CREATE_ERROR                = 3
        LIFETIME_ERROR              = 4
        LIFETIME_DYNPRO_DYNPRO_LINK = 5
        others                      = 6.
    if SY-SUBRC ne 0.
      /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
    endif.

*** Initialize SALV
    try.
        CL_SALV_TABLE=>FACTORY( exporting R_CONTAINER  = ME->R_CONTAINER_FIRNG
                                importing R_SALV_TABLE = ME->R_SALV_FIRNG
                                changing  T_TABLE      = ME->T_FIRNG ).

        ME->R_SALV_FIRNG->DISPLAY( ).
      catch CX_SALV_MSG into LR_CX_SALV_MSG.
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_SALV_MSG ).
    endtry.

  endmethod.
  method SET_PF_STATUS.

    set pf-status '0100' of program 'SAPLZVCXI_FFSS_POPUP_FIPROP'.

  endmethod.
  method START.

    data: LR_POPUP_FIPROP type ref to ZCL_VCXI_FFSS_POPUP_FIPROP.

ENHANCEMENT-SECTION START SPOTS ZVCXI_FFSS_ES_POPUP_FIPROP .
*** Create Instance
    create object LR_POPUP_FIPROP
      exporting
        IR_BL_FIBPROP = IR_BL_FIBPROP
        IR_BL_FIFPROP = IR_BL_FIFPROP
        IS_FIWID      = IS_FIWID
        IS_FITIN      = IS_FITIN.

*** Call Popup
    call function 'ZVCXI_FFSS_POPUP_FIPROP_START'
      exporting
        IR_POPUP_FIPROP = LR_POPUP_FIPROP.

END-ENHANCEMENT-SECTION.
  endmethod.
