
class ZCL_VCXI_XCSUC_UI_DFLT_OV definition
  public
  create public .

public section.

  data F_CURSOR type SCRFNAME .

  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods DO_USER_COMMAND
    changing
      !CF_UCOMM type SYUCOMM .
  methods SET_CURSOR .
  class-methods START_UI
    raising
      /VCXI/CX_CKX .
  methods CONSTRUCTOR
    raising
      /VCXI/CX_CKX .
protected section.

  data R_GRID_DFLT type ref to ZCL_VCXI_XCSUC_GRID_DFLT .
  constants C_UC_BACK type SYUCOMM value 'BACK'. "#EC NOTEXT
  constants C_UC_CANCEL type SYUCOMM value 'CANCEL'. "#EC NOTEXT
  constants C_UC_EXIT type SYUCOMM value 'EXIT'. "#EC NOTEXT
  data F_REFRESH_REQ type ABAP_BOOL .
  constants C_UC_CUST_BASE type SYUCOMM value 'CUST_BASE'. "#EC NOTEXT
  constants C_UC_CUST_SHMAP type SYUCOMM value 'CUST_SHMAP'. "#EC NOTEXT
  constants C_UC_CUST_CPACK type SYUCOMM value 'CUST_CPACK'. "#EC NOTEXT
  constants C_UC_SIM_DFLT type SYUCOMM value 'SIM_DFLT'. "#EC NOTEXT

  methods DO_FREE
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_BACK
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_CANCEL
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_EXIT
    raising
      /VCXI/CX_CKX .
  methods SET_PFSTATUS
    raising
      /VCXI/CX_CKX .
  methods SET_TITLE
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_CALL_PROG
    importing
      !IF_PROGRAMM type PROGRAMM
    exceptions
      /VCXI/CX_CKX .
  methods HANDLE_UC_CUST_BASE
    raising
      /VCXI/CX_CKX .
private section.

  data S_DYNP type /VCXI/CKX_S_DYNP_KEY .
endclass. "ZCL_VCXI_XCSUC_UI_DFLT_OV definition
class ZCL_VCXI_XCSUC_UI_DFLT_OV implementation.
method CONSTRUCTOR.

*** Bind UI Defaulting Overview
  call function 'ZVCXI_XCSUC_UI_DFLT_OV_BIND'
    exporting
      IR_UI_DFLT_OV = ME
    importing
      RS_DYNP       = ME->S_DYNP.

***----------------------------------------------------------------------------
  if ME->R_GRID_DFLT is not bound.
*** Create Grid for Defaulting Elements
    create object ME->R_GRID_DFLT.
  endif.

  move ABAP_TRUE to ME->F_REFRESH_REQ.

endmethod.
method DO_FREE.

***------------------------------------------------------------------
*** Free Default Element Overview Grid
  if ME->R_GRID_DFLT is bound.
    ME->R_GRID_DFLT->DO_FREE( ).
    clear ME->R_GRID_DFLT.
  endif.

endmethod.
method DO_PBO.

  data: LR_CX_CKX type ref to /VCXI/CX_CKX.

  try.

      ME->SET_PFSTATUS( ).
      ME->SET_TITLE( ).

***   Refresh Grid
      if ME->R_GRID_DFLT is bound.
        if ME->F_REFRESH_REQ eq ABAP_TRUE.
          move ABAP_TRUE to ME->R_GRID_DFLT->F_REFRESH_ALL_REQ.
          move ABAP_TRUE to ME->R_GRID_DFLT->F_REFRESH_REQ.
        endif.
        ME->R_GRID_DFLT->REFRESH_GRID( ).
      endif.

      move ABAP_FALSE to ME->F_REFRESH_REQ.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      LR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

endmethod.
method DO_USER_COMMAND .

  data:  LR_CX_CKX type ref to /VCXI/CX_CKX.

  try.

      case CF_UCOMM.
        when ZCL_VCXI_XCSUC_UI_DFLT_OV=>C_UC_BACK.
          clear CF_UCOMM.
          ME->HANDLE_UC_BACK( ).
        when ZCL_VCXI_XCSUC_UI_DFLT_OV=>C_UC_EXIT.
          clear CF_UCOMM.
          ME->HANDLE_UC_EXIT( ).
        when ZCL_VCXI_XCSUC_UI_DFLT_OV=>C_UC_CANCEL.
          clear CF_UCOMM.
        when ZCL_VCXI_XCSUC_UI_DFLT_OV=>C_UC_CUST_BASE.
          clear CF_UCOMM.
          ME->HANDLE_UC_CUST_BASE( ).
        when ZCL_VCXI_XCSUC_UI_DFLT_OV=>C_UC_CUST_SHMAP.
          clear CF_UCOMM.
          ME->HANDLE_UC_CALL_PROG( IF_PROGRAMM = 'ZVCXI_XCSUC_SHMAP_START' ).
        when ZCL_VCXI_XCSUC_UI_DFLT_OV=>C_UC_CUST_CPACK.
          clear CF_UCOMM.
          ME->HANDLE_UC_CALL_PROG( IF_PROGRAMM = 'ZVCXI_XCSUC_CPACK_START' ).
        when ZCL_VCXI_XCSUC_UI_DFLT_OV=>C_UC_SIM_DFLT.
          clear CF_UCOMM.
          call transaction 'ZVCXI_XCSU_SIM'.
      endcase.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      LR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

endmethod.
method HANDLE_UC_BACK .

  ME->DO_FREE( ).
  leave to screen 0.

endmethod.
method HANDLE_UC_CALL_PROG.

  submit (IF_PROGRAMM) and return.

endmethod.
method HANDLE_UC_CANCEL .

  ME->DO_FREE( ).
  leave to screen 0.

endmethod.
method HANDLE_UC_CUST_BASE.

  call function 'VIEWCLUSTER_MAINTENANCE_CALL'
    exporting
      VIEWCLUSTER_NAME             = 'ZVCXI_XCSUC_BASE'
      MAINTENANCE_ACTION           = 'S'
      SHOW_SELECTION_POPUP         = SPACE
      NO_WARNING_FOR_CLIENTINDEP   = SPACE
    exceptions
      CLIENT_REFERENCE             = 1
      FOREIGN_LOCK                 = 2
      VIEWCLUSTER_NOT_FOUND        = 3
      VIEWCLUSTER_IS_INCONSISTENT  = 4
      MISSING_GENERATED_FUNCTION   = 5
      NO_UPD_AUTH                  = 6
      NO_SHOW_AUTH                 = 7
      OBJECT_NOT_FOUND             = 8
      NO_TVDIR_ENTRY               = 9
      NO_CLIENTINDEP_AUTH          = 10
      INVALID_ACTION               = 11
      SAVING_CORRECTION_FAILED     = 12
      SYSTEM_FAILURE               = 13
      UNKNOWN_FIELD_IN_DBA_SELLIST = 14
      MISSING_CORR_NUMBER          = 15
      others                       = 16.

  if SY-SUBRC ne 0.
    raise exception type /VCXI/CX_CKX
     exporting
        F_MSGTY = 'I'
        F_MSGID = SY-MSGID
        F_MSGNO = SY-MSGNO
        F_MSGV1 = SY-MSGV1
        F_MSGV2 = SY-MSGV2
        F_MSGV3 = SY-MSGV3
        F_MSGV4 = SY-MSGV4.

  endif.

endmethod.
method HANDLE_UC_EXIT .

  ME->DO_FREE( ).
  leave to screen 0.

endmethod.
method SET_CURSOR.

  data:  LF_CURSOR type SCRFNAME.

  get cursor field LF_CURSOR.
  if LF_CURSOR is not initial.
    move LF_CURSOR to ME->F_CURSOR.
  endif.

endmethod.
method SET_PFSTATUS.

***----------------------------------------------------------------------------
*** Set PF-Status
  set pf-status '0100' of program ME->S_DYNP-REPID.

endmethod.
method SET_TITLE.

  set titlebar '0100' of program ME->S_DYNP-REPID.

endmethod.
method START_UI.

  data:  LR_UI_DFLT_OV type ref to ZCL_VCXI_XCSUC_UI_DFLT_OV.

  BREAK ADD_AUTHCHECK_KFISCHER.
**** Check Authorization
*  ZCL_VCXI_XCSUC_DFLT=>CHECK_AUTH( IF_ACTVT = ZCL_VCXI_XCSUC_DFLT=>C_ACTVT_DISPLAY ).

*** Create Instance of UI
  create object LR_UI_DFLT_OV.

*** Call Screen
  call function 'ZVCXI_XCSUC_UI_DFLT_OV_START'.

endmethod.
