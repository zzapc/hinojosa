
class ZCL_VCXI_P1SS_UI_CSTEP_WPATCH definition
  public
  inheriting from ZCL_VCXI_P1SS_UI_CSTEP
  create public .

public section.

  data S_CSTEP_WPATCH_VIS type ZVCXI_P1SS_S_CSTEP_WPATCH_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABPT type ZVCXI_XCS_TABPT default '0100S_CSD_TAB-TAB'
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
  methods DEACTIVATE
    redefinition .
protected section.

  data R_GRID_MS_WPATCH type ref to ZCL_VCXI_PXSS_GRID_MS_WPATCH .
  data R_PR_CSTEP_WPATCH type ref to ZCL_VCXI_P1SS_PR_CSTEP_WPATCH .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_P1SS_UI_CSTEP_WPATCH definition
class ZCL_VCXI_P1SS_UI_CSTEP_WPATCH implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

***--------------------------------------------------------------------------------------
*** Bind UI to Dynpro
    call function 'ZVCXI_P1SS_UI_CSTEP_WPTCH_BIND'
      exporting
        IR_UI_CSTEP_WPATCH = ME
      importing
        ES_DYNP            = ME->S_DYNP.

***--------------------------------------------------------------------------------------
*** Create Grid for Window Patch Materials
    create object ME->R_GRID_MS_WPATCH
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_MS_WPATCH'.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_CSTEP_WPATCH.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

    if ME->R_GRID_MS_WPATCH is bound.
      ME->R_GRID_MS_WPATCH->DO_FREE( ).
      clear ME->R_GRID_MS_WPATCH.
    endif.

  endmethod.
  method DO_MODIFY_SCREEN.

    try.
        SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                 IF_DYNNR = IF_DYNNR ).

***----------------------------------------------------------------------------
***     Modify Screen
        loop at screen.
***       Handling Dimension changeable if Override Flag is set
          if SCREEN-GROUP1 eq 'OHD'.
            if ME->S_CSTEP_WPATCH_VIS-FOVRR_HNDDIM eq ABAP_FALSE.
              move 0 to SCREEN-INPUT.
            endif.
          endif.

          modify screen.
        endloop.

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Super PBO
    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize Grid
    if ME->R_GRID_MS_WPATCH is bound.
      ME->R_GRID_MS_WPATCH->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_MS_WPATCH->REFRESH_GRID( ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_CSTEP_WPATCH_VIS type ZVCXI_P1SS_S_CSTEP_WPATCH_VIS.


*** Call SUPER
    SUPER->DO_UPDATE_VIS( IS_CSTEP_VIS = IS_CSTEP_VIS ).

    check ME->R_PR_CSTEP_WPATCH is bound.

*** "Cast" Vis Structure
    move-corresponding IS_CSTEP_VIS to LS_CSTEP_WPATCH_VIS.

***--------------------------------------------------------------------------------------
*** Set Applicaton Side
    if ME->S_CSTEP_WPATCH_VIS-APPSI ne LS_CSTEP_WPATCH_VIS-APPSI.
      ME->R_PR_CSTEP_WPATCH->SET_APPSI( IF_APPSI = LS_CSTEP_WPATCH_VIS-APPSI
                                        IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Sheet Direction
    if ME->S_CSTEP_WPATCH_VIS-SHTDI ne LS_CSTEP_WPATCH_VIS-SHTDI.
      ME->R_PR_CSTEP_WPATCH->SET_SHTDI( IF_SHTDI = LS_CSTEP_WPATCH_VIS-SHTDI
                                        IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Update Handling Dimension if it is "Overriden"
    if ME->S_CSTEP_WPATCH_VIS-FOVRR_HNDDIM eq ABAP_TRUE and
       ME->S_CSTEP_WPATCH_VIS-S_HNDDIM ne LS_CSTEP_WPATCH_VIS-S_HNDDIM.
      ME->R_PR_CSTEP_WPATCH->SET_HNDDIM( IS_HNDDIM = LS_CSTEP_WPATCH_VIS-S_HNDDIM
                                         IF_SETBY  = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Flag Override Handling Dimension
    if ME->S_CSTEP_WPATCH_VIS-FOVRR_HNDDIM ne LS_CSTEP_WPATCH_VIS-FOVRR_HNDDIM.
      ME->R_PR_CSTEP_WPATCH->SET_HNDDIM( IF_FOVRR = LS_CSTEP_WPATCH_VIS-FOVRR_HNDDIM ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

***--------------------------------------------------------------------------------------
*** Process Changes in Grid
    if ME->R_GRID_MS_WPATCH is bound.
      ME->R_GRID_MS_WPATCH->CHECK_CHANGED_DATA( ).
    endif.

***--------------------------------------------------------------------------------------
*** Call SUPER
    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_CSTEP_WPATCH.

  endmethod.
  method INIT_VIS.

    SUPER->INIT_VIS( ).

    free: ME->S_CSTEP_WPATCH_VIS.
    ME->S_CSTEP_WPATCH_VIS-S_CSTEP_VIS = ME->S_CSTEP_VIS.

***--------------------------------------------------------------------------------------
*** Application Side
    ME->S_CSTEP_WPATCH_VIS-APPSI    = ME->R_PR_CSTEP_WPATCH->GET_APPSI( ).

*** Sheet Direction
    ME->S_CSTEP_WPATCH_VIS-SHTDI    = ME->R_PR_CSTEP_WPATCH->GET_SHTDI( ).

*** Handling Dimension
    ME->S_CSTEP_WPATCH_VIS-S_HNDDIM = ME->R_PR_CSTEP_WPATCH->GET_HNDDIM( importing EF_FOVRR = ME->S_CSTEP_WPATCH_VIS-FOVRR_HNDDIM ).

  endmethod.
