
class ZCL_VCXI_P1SR_BL_PSTEP_ADSTP definition
  public
  inheriting from ZCL_VCXI_P1SR_BL_PSTEP
  create public .

public section.

  interfaces ZIF_VCXI_PXSR_PCDIR .
  interfaces ZIF_VCXI_XCSR_BL_OUTSO .
  interfaces ZIF_VCXI_XCSR_BL_MANU .
  interfaces ZIF_VCXI_PXSR_ADJ_PCDIM .

  constants C_IPSTP_ADSTP type /VCXI/VSMC_IPSTP value 'PX_PS_ADSTP' ##NO_TEXT.

  methods CONSTRUCTOR
    importing
      !IF_IPSTP type /VCXI/VSMC_IPSTP default C_IPSTP_ADSTP
      !IF_DATE type DATS
      !IR_APPDATA type ref to /VCXI/CL_VKSR_APPDATA
    raising
      /VCXI/CX_CKX .

  methods /VCXI/IF_VSMR_BL_PSTEP~GET_ATYPE
    redefinition .
  methods COMPARE
    redefinition .
  methods COPY
    redefinition .
  methods GET_ARBPL_INFO
    redefinition .
  methods GET_SPVRE_ACTVY
    redefinition .
  methods INIT_BY_SPEC
    redefinition .
  methods ADD_LIMITS_TO_RTE_PSTEP
    redefinition .
protected section.

  constants C_ATTID_ISVCE type /VCXI/VKS_ATTID value 'ISVCE_MC' ##NO_TEXT.
  data S_PCDIM_IN type ZVCXI_P0SP_S_PCDIM .
  data S_PCDIM_OUT type ZVCXI_P0SP_S_PCDIM .
  data T_MS_ADMAT type ZVCXI_PXSR_TT_MS_ADMAT .

  methods ADD_SPVRE_MC_ADMAT
    importing
      !IR_BL_RTESPV type ref to ZCL_VCXI_XCSR_BL_RTESPV
      !IR_SPVRE_PARENT type ref to /VCXI/CL_VKSR_SPVRE
    changing
      !CT_SPVRE type /VCXI/VKSR_TT_SPVRE
    raising
      /VCXI/CX_CKX .
  methods FILL_SPVRE_ADDON_ADSTP
    importing
      !IR_BL_RTESPV type ref to ZCL_VCXI_XCSR_BL_RTESPV
      !IR_SPVRE_ADDON type ref to /VCXI/CL_VKSR_SPVRE
    changing
      !CT_SPVRE type /VCXI/VKSR_TT_SPVRE
    raising
      /VCXI/CX_CKX .
  methods INIT_BY_CSTEP_ADSTP
    importing
      !IT_PRVDR type /VCXI/VKSR_TT_GATE
    raising
      /VCXI/CX_CKX .
  methods INIT_BY_MS_ADMAT
    importing
      !IT_PRVDR type /VCXI/VKSR_TT_GATE
    raising
      /VCXI/CX_CKX .

  methods FILL_SPVRE_ADDON
    redefinition .
  methods GET_XML_DREF
    redefinition .
private section.
endclass. "ZCL_VCXI_P1SR_BL_PSTEP_ADSTP definition
class ZCL_VCXI_P1SR_BL_PSTEP_ADSTP implementation.
  method /VCXI/IF_VSMR_BL_PSTEP~GET_ATYPE.

    append /VCXI/CL_VSMC_ATYPE=>GET_INSTANCE_BY_IATYP( IF_IATYP = ZCL_VCXI_PXSP_PR_ADDON_ADSTP=>C_ISVCE ) to RT_ATYPE.

  endmethod.
  method ADD_LIMITS_TO_RTE_PSTEP.

    data: LF_LDUOM type /VCXI/VSMC_LDUOM,
          LF_LDVAL type /VCXI/VSMR_LDVAL.
    data: LI_LVVAL type /VCXI/VSMR_TT_RTE_RANGE_LVVAL.
    data: LT_LTYPE type /VCXI/CKX_TT_OBJECT.
    data: LR_LTYPE  type ref to /VCXI/CL_VSMC_LTYPE,
          LR_OBJECT type ref to /VCXI/IF_CKX_OBJECT.


    SUPER->ADD_LIMITS_TO_RTE_PSTEP( IR_RTE_PSTEP = IR_RTE_PSTEP ).

***----------------------------------------------------------------------------
*** Get all linked Limits
    LT_LTYPE = ME->R_PSTEP->/VCXI/IF_VSMC_LINK_LTYPE~GET_LINKED_LTYPE( ).
    loop at LT_LTYPE into LR_OBJECT.
      move LR_OBJECT ?to LR_LTYPE.

      clear: LF_LDVAL,
             LF_LDUOM,
             LI_LVVAL.

***----------------------------------------------------------------------------
      case LR_LTYPE->/VCXI/IF_CKX_OBJECT~GET_ID( ).
***     Sheet Length (Inbound)
        when 'P0_LT_DI_SHTLEN'.
          move ME->S_PCDIM_IN-PCLEN      to LF_LDVAL.
          move ME->S_PCDIM_IN-PCLEN_UOM  to LF_LDUOM.

***     Sheet Width (Inbound)
        when 'P0_LT_DI_SHTWID'.
          move ME->S_PCDIM_IN-PCWID      to LF_LDVAL.
          move ME->S_PCDIM_IN-PCWID_UOM  to LF_LDUOM.

        when others.
          continue.
      endcase.


***----------------------------------------------------------------------------
***   Set
      case LR_LTYPE->F_MLTYP.
        when /VCXI/CL_VSMC_LTYPE=>C_MLTYP_D.
          IR_RTE_PSTEP->SET_RTE_LDIM( IR_LTYPE = LR_LTYPE
                                      IF_LDVAL = LF_LDVAL
                                      IF_LDUOM = LF_LDUOM ).
        when /VCXI/CL_VSMC_LTYPE=>C_MLTYP_V.
          IR_RTE_PSTEP->SET_RTE_LVAL( IR_LTYPE = LR_LTYPE
                                      II_LVVAL = LI_LVVAL ).
      endcase.
    endloop.

  endmethod.
  method ADD_SPVRE_MC_ADMAT.

    data: LS_MS_ADMAT type ZVCXI_PXSR_S_MS_ADMAT,
          LS_AVGCO    type ZVCXI_XCS_S_AVGCO,
          LS_AVGCP    type ZVCXI_XCS_S_AVGCP.
    data: LR_SPVRE type ref to /VCXI/CL_VKSR_SPVRE.


    loop at ME->T_MS_ADMAT into LS_MS_ADMAT.

***------------------------------------------------------------------------------------------------
***   Create Supervisor Runtime Element
      LR_SPVRE = new #( IR_SPVCE  = IR_BL_RTESPV->GET_SPVCE( IF_ID           = LS_MS_ADMAT-ISVCE
                                                             IR_SPVCE_PARENT = IR_SPVRE_PARENT->R_SPVCE )
                        IR_PARENT = IR_SPVRE_PARENT ).
      append LR_SPVRE to CT_SPVRE.

***   Set In Use
      LR_SPVRE->SET_INUSE( IF_INUSE = ABAP_TRUE ).

***------------------------------------------------------------------------------------------------
***   Material Number
      LR_SPVRE->SET_LEOKY( IS_LEOKY = value #( LEOKY = LS_MS_ADMAT-MATNR
                                               SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).

***   Text
      LR_SPVRE->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_PXSP_PR_MC_ADMAT=>C_PARID_TEXT
                                               PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR
                                               ATWRT = LS_MS_ADMAT-TEXT
                                               SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).

***   Average Consumption
      LR_SPVRE->SET_PARAM( IS_PARAM = value #( PARID = switch #( LS_MS_ADMAT-ISVCE when 'MC_ADMAT_A' then ZCL_VCXI_PXSP_PR_MC_ADMAT=>C_PARID_AVGCO_A
                                                                                   when 'MC_ADMAT_L' then ZCL_VCXI_PXSP_PR_MC_ADMAT=>C_PARID_AVGCO_L
                                                                                   when 'MC_ADMAT_P' then ZCL_VCXI_PXSP_PR_MC_ADMAT=>C_PARID_AVGCO_P
                                                                                   when 'MC_ADMAT_W' then ZCL_VCXI_PXSP_PR_MC_ADMAT=>C_PARID_AVGCO_W )
                                               PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC
                                               ATFLV = LS_MS_ADMAT-AVGCO
                                               UNIT  = LS_MS_ADMAT-AVGCO_UOM
                                               SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).

***   Average Consumption Per
      LR_SPVRE->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_PXSP_PR_MC_ADMAT=>C_PARID_AVGCP_P
                                               PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC
                                               ATFLV = LS_MS_ADMAT-AVGCP
                                               UNIT  = LS_MS_ADMAT-AVGCP_UOM
                                               SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).

***------------------------------------------------------------------------------------------------
***   Purchase Flag
      LR_SPVRE->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_PXSP_PR_MC_ADMAT=>C_PARID_PURCH
                                               PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR
                                               ATWRT = LS_MS_ADMAT-PURCH
                                               SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).
***   Purchase Information
      check LS_MS_ADMAT-PURCH ne ZCL_VCXI_XCSS_PR_MS=>C_PURCH_STOCK.

***   Price
      LR_SPVRE->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_PXSP_PR_MC_ADMAT=>C_PARID_PRICE
                                               PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC
                                               ATFLV = LS_MS_ADMAT-PRICE
                                               CURKY = LS_MS_ADMAT-PRICE_CURR
                                               SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).

***   Price Unit
      LR_SPVRE->SET_PARAM( IS_PARAM = value #( PARID = switch #( LS_MS_ADMAT-ISVCE when 'MC_ADMAT_A' then ZCL_VCXI_PXSP_PR_MC_ADMAT=>C_PARID_PRICP_A
                                                                                   when 'MC_ADMAT_L' then ZCL_VCXI_PXSP_PR_MC_ADMAT=>C_PARID_PRICP_L
                                                                                   when 'MC_ADMAT_P' then ZCL_VCXI_PXSP_PR_MC_ADMAT=>C_PARID_PRICP_P
                                                                                   when 'MC_ADMAT_W' then ZCL_VCXI_PXSP_PR_MC_ADMAT=>C_PARID_PRICP_W )
                                               PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC
                                               ATFLV = LS_MS_ADMAT-PRICP
                                               UNIT  = LS_MS_ADMAT-PRICP_UOM
                                               SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).

***   Vendor
      LR_SPVRE->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_PXSP_PR_MC_ADMAT=>C_PARID_LIFNR
                                               PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR
                                               ATWRT = LS_MS_ADMAT-LIFNR
                                               SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).


***------------------------------------------------------------------------------------------------
****   Add Price Scale
*      if LS_MS_ADMAT-T_PSCALE is not initial.
*        IR_BL_RTESPV->ADD_PSCALE_MGR( exporting IR_SPVRE_PARENT = LR_SPVRE
*                                                IT_PSCALE       = LS_MS_ADMAT-T_PSCALE
*                                      changing  CT_SPVRE        = CT_SPVRE ).
*      endif.
    endloop.

  endmethod.
  method COMPARE.

    data:  LR_BL_PSTEP_ADSTP type ref to ZCL_VCXI_P1SR_BL_PSTEP_ADSTP.


*** Super Compare
    RF_SIMILAR = SUPER->COMPARE( IR_BL_PSTEP = IR_BL_PSTEP ).
    check RF_SIMILAR eq ABAP_TRUE.

    move IR_BL_PSTEP ?to LR_BL_PSTEP_ADSTP.


***------------------------------------------------------------------------------------------------
*** PC Dimensions
    if ME->S_PCDIM_IN  eq LR_BL_PSTEP_ADSTP->S_PCDIM_IN and
       ME->S_PCDIM_OUT eq LR_BL_PSTEP_ADSTP->S_PCDIM_OUT.
      move ABAP_TRUE  to RF_SIMILAR.
    else.
      move ABAP_FALSE to RF_SIMILAR.
      exit.
    endif.

*** Additional Material
    if ME->T_MS_ADMAT eq LR_BL_PSTEP_ADSTP->T_MS_ADMAT.
      move ABAP_TRUE  to RF_SIMILAR.
    else.
      move ABAP_FALSE to RF_SIMILAR.
      return.
    endif.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IF_IPSTP   = IF_IPSTP
                        IF_DATE    = IF_DATE
                        IR_APPDATA = IR_APPDATA ).

*** Initialize Manual Work Center Info
    move ZCL_VCXI_XCSP_PR_MANU=>C_PARID_MSRSR_P to ME->ZIF_VCXI_XCSR_BL_MANU~F_PARID_MSRSR.
    ME->INIT_MANU( ).

*** Initialize Outsourcing Info
    move ZCL_VCXI_XCSP_PR_OUTSO=>C_PARID_PRICP_P to ME->ZIF_VCXI_XCSR_BL_OUTSO~F_PARID_PRICP.
    ME->INIT_OUTSO( ).

  endmethod.
  method COPY.

    data:  LR_BL_PSTEP_ADSTP type ref to ZCL_VCXI_P1SR_BL_PSTEP_ADSTP.


*** Super Copy
    RR_BL_PSTEP = SUPER->COPY( ).
    move RR_BL_PSTEP ?to LR_BL_PSTEP_ADSTP.

***------------------------------------------------------------------------------------------------
*** PC Dimensions
    move ME->S_PCDIM_IN    to LR_BL_PSTEP_ADSTP->S_PCDIM_IN.
    move ME->S_PCDIM_OUT   to LR_BL_PSTEP_ADSTP->S_PCDIM_OUT.

*** Additional Materials
    move ME->T_MS_ADMAT    to LR_BL_PSTEP_ADSTP->T_MS_ADMAT.

  endmethod.
  method FILL_SPVRE_ADDON.

    SUPER->FILL_SPVRE_ADDON( exporting IR_BL_RTESPV   = IR_BL_RTESPV
                                       IR_SPVRE_ADDON = IR_SPVRE_ADDON
                                       IR_ARBPL       = IR_ARBPL
                             changing  CT_SPVRE       = CT_SPVRE ).

    case IR_SPVRE_ADDON->GET_ID( ).
      when ZCL_VCXI_PXSP_PR_ADDON_ADSTP=>C_ISVCE.
        ME->FILL_SPVRE_ADDON_ADSTP( exporting IR_BL_RTESPV   = IR_BL_RTESPV
                                              IR_SPVRE_ADDON = IR_SPVRE_ADDON
                                    changing  CT_SPVRE       = CT_SPVRE ).
    endcase.

  endmethod.
  method FILL_SPVRE_ADDON_ADSTP.

***------------------------------------------------------------------------------------------------
*** Set Length
    IR_SPVRE_ADDON->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_PXSP_PR_ADDON_ADSTP=>C_PARID_PCLEN
                                                   PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC
                                                   ATFLV = ME->S_PCDIM_OUT-PCLEN
                                                   UNIT  = ME->S_PCDIM_OUT-PCLEN_UOM
                                                   SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).
*** Set Width
    IR_SPVRE_ADDON->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_PXSP_PR_ADDON_ADSTP=>C_PARID_PCWID
                                                   PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC
                                                   ATFLV = ME->S_PCDIM_OUT-PCWID
                                                   UNIT  = ME->S_PCDIM_OUT-PCWID_UOM
                                                   SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).
*** Set Height
    IR_SPVRE_ADDON->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_PXSP_PR_ADDON_ADSTP=>C_PARID_PCHEI
                                                   PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC
                                                   ATFLV = ME->S_PCDIM_OUT-PCHEI
                                                   UNIT  = ME->S_PCDIM_OUT-PCHEI_UOM
                                                   SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).

***------------------------------------------------------------------------------------------------
*** Add Material Consumption for Additional Materials
    ME->ADD_SPVRE_MC_ADMAT( exporting IR_BL_RTESPV    = IR_BL_RTESPV
                                      IR_SPVRE_PARENT = IR_SPVRE_ADDON
                            changing  CT_SPVRE        = CT_SPVRE ).

  endmethod.
  method GET_ARBPL_INFO.

***------------------------------------------------------------------
*** Don't overrule existing
    check CF_ID_GRP   is initial and
          CF_ID_ARBPL is initial.

***------------------------------------------------------------------
*** Standard
    move 'CONV'     to CF_ID_ARBPL.

*** If Manual
    if ME->ZIF_VCXI_XCSR_BL_MANU~F_MANWC eq ABAP_TRUE.
      move 'MANU'     to CF_ID_ARBPL.
    endif.

*** If Outsourcing
    if ME->ZIF_VCXI_XCSR_BL_OUTSO~F_OUTSO eq ABAP_TRUE.
      move 'OUTSO'    to CF_ID_ARBPL.
    endif.

  endmethod.
  method GET_SPVRE_ACTVY.

    data: LR_SPVCE       type ref to /VCXI/CL_VKSC_SPVCE,
          LR_SPVRE_ACTVY type ref to /VCXI/CL_VKSR_SPVRE.

***------------------------------------------------------------------------------------------------
*** Get Customizing Element for Activity
    LR_SPVCE = IR_BL_RTESPV->GET_SPVCE( IF_ID           = 'ACTVY_ADSTP'
                                        IR_SPVCE_PARENT = IR_SPVRE_ARBPL->R_SPVCE ).

*** Create Supervisor Runtime Element
    create object LR_SPVRE_ACTVY
      exporting
        IR_SPVCE  = LR_SPVCE
        IR_PARENT = IR_SPVRE_ARBPL.
    append LR_SPVRE_ACTVY to RT_SPVRE.

*** Set In Use
    LR_SPVRE_ACTVY->SET_INUSE( IF_INUSE = ABAP_TRUE ).

***------------------------------------------------------------------------------------------------
*** Set Length
    LR_SPVRE_ACTVY->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_PXSP_PR_ACTVY_ADSTP=>C_PARID_PCLEN
                                                   PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC
                                                   ATFLV = ME->S_PCDIM_OUT-PCLEN
                                                   UNIT  = ME->S_PCDIM_OUT-PCLEN_UOM
                                                   SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).
*** Set Width
    LR_SPVRE_ACTVY->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_PXSP_PR_ACTVY_ADSTP=>C_PARID_PCWID
                                                   PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC
                                                   ATFLV = ME->S_PCDIM_OUT-PCWID
                                                   UNIT  = ME->S_PCDIM_OUT-PCWID_UOM
                                                   SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).
*** Set Height
    LR_SPVRE_ACTVY->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_PXSP_PR_ACTVY_ADSTP=>C_PARID_PCHEI
                                                   PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC
                                                   ATFLV = ME->S_PCDIM_OUT-PCHEI
                                                   UNIT  = ME->S_PCDIM_OUT-PCHEI_UOM
                                                   SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).

***------------------------------------------------------------------------------------------------
*** Add Material Consumption for Additional Material
    ME->ADD_SPVRE_MC_ADMAT( exporting IR_BL_RTESPV    = IR_BL_RTESPV
                                      IR_SPVRE_PARENT = LR_SPVRE_ACTVY
                            changing  CT_SPVRE        = RT_SPVRE ).

  endmethod.
  method GET_XML_DREF.

*** Super References
    RT_DATA = SUPER->GET_XML_DREF( ).

*** Add additional References
    append value #( NAME = 'S_PCDIM_IN'   VALUE = ref #( ME->S_PCDIM_IN )  ) to RT_DATA.
    append value #( NAME = 'S_PCDIM_OUT'  VALUE = ref #( ME->S_PCDIM_OUT ) ) to RT_DATA.

    append value #( NAME = 'T_MS_ADMAT'   VALUE = ref #( ME->T_MS_ADMAT )  ) to RT_DATA.

  endmethod.
  method INIT_BY_CSTEP_ADSTP.

    data: LF_SHTDI type ZVCXI_P0SS_SHTDI.
    data: LR_GATE           type ref to /VCXI/CL_VKSR_GATE,
          LR_PR_CSTEP_ADSTP type ref to ZCL_VCXI_P1SS_PR_CSTEP_ADSTP.


***--------------------------------------------------------------------------------------
*** Get Reference for CStep Applying Additional Materials
    loop at IT_PRVDR into LR_GATE
                    where TABLE_LINE->R_CLSPR is instance of ZCL_VCXI_P1SS_PR_CSTEP_ADSTP.
      LR_PR_CSTEP_ADSTP ?= LR_GATE->R_CLSPR.
      exit.
    endloop.
    check LR_PR_CSTEP_ADSTP is bound.


***--------------------------------------------------------------------------------------
*** Sheet Direction
    move LR_PR_CSTEP_ADSTP->GET_SHTDI( ) to LF_SHTDI.
    if LF_SHTDI is not initial.
      move LF_SHTDI                               to ME->ZIF_VCXI_PXSR_PCDIR~F_PCDIR.
      move ZIF_VCXI_PXSR_PCDIR=>C_PCDOP_MANDATORY to ME->ZIF_VCXI_PXSR_PCDIR~F_PCDOP.
    endif.

***--------------------------------------------------------------------------------------
*** Set Data based on Step Type
    case LR_PR_CSTEP_ADSTP->GET_TASTP( ).
      when ZCL_VCXI_P1SS_PR_CSTEP_ADSTP=>C_TASTP_STANDARD.

      when ZCL_VCXI_P1SS_PR_CSTEP_ADSTP=>C_TASTP_MANUAL.
        ME->ZIF_VCXI_XCSR_BL_MANU~F_MANWC = ABAP_TRUE.
        ME->ZIF_VCXI_XCSR_BL_MANU~F_TEXT  = LR_PR_CSTEP_ADSTP->GET_TEXT( ).
        ME->ZIF_VCXI_XCSR_BL_MANU~S_MSRS  = LR_PR_CSTEP_ADSTP->GET_MSRS( ).
        ME->ZIF_VCXI_XCSR_BL_MANU~S_SCRAP = LR_PR_CSTEP_ADSTP->GET_SCRAP( ).

      when ZCL_VCXI_P1SS_PR_CSTEP_ADSTP=>C_TASTP_OUTSOURCE.
        ME->ZIF_VCXI_XCSR_BL_OUTSO~F_OUTSO  = ABAP_TRUE.
        ME->ZIF_VCXI_XCSR_BL_OUTSO~F_TEXT   = LR_PR_CSTEP_ADSTP->GET_TEXT( ).
        ME->ZIF_VCXI_XCSR_BL_OUTSO~F_LIFNR  = LR_PR_CSTEP_ADSTP->GET_LIFNR( ).
        ME->ZIF_VCXI_XCSR_BL_OUTSO~S_SCRAP  = LR_PR_CSTEP_ADSTP->GET_SCRAP( ).
        ME->ZIF_VCXI_XCSR_BL_OUTSO~T_PSCALE = ME->GET_PSCALE( IR_GATE_PARENT = LR_GATE ).

    endcase.

  endmethod.
  method INIT_BY_MS_ADMAT.

    data: LR_GATE        type ref to /VCXI/CL_VKSR_GATE,
          LR_PR_MS_ADMAT type ref to ZCL_VCXI_PXSS_PR_MS_ADMAT.


    loop at IT_PRVDR into LR_GATE
                    where TABLE_LINE->R_CLSPR is instance of ZCL_VCXI_PXSS_PR_MS_ADMAT.
      LR_PR_MS_ADMAT ?= LR_GATE->R_CLSPR.

      append value #( ISVCE   = LR_GATE->GET_ATTRI( IF_ATTID = C_ATTID_ISVCE )-ATTVA
                      MATNR   = LR_PR_MS_ADMAT->GET_MATNR( )
                      TEXT    = LR_PR_MS_ADMAT->GET_TEXT( )
                      S_AVGCO = LR_PR_MS_ADMAT->GET_AVGCO( )
                      S_AVGCP = LR_PR_MS_ADMAT->GET_AVGCP( )
                      PURCH   = LR_PR_MS_ADMAT->GET_PURCH( )
                      LIFNR   = LR_PR_MS_ADMAT->GET_LIFNR( )
                      S_PRICE = LR_PR_MS_ADMAT->GET_PRICE( )
                      S_PRICP = LR_PR_MS_ADMAT->GET_PRICP( )
                    ) to ME->T_MS_ADMAT.
    endloop.

  endmethod.
  method INIT_BY_SPEC.

    data: LT_PCDIM type ZVCXI_P0SP_TT_PCDIM.


***--------------------------------------------------------------------------------------
*** Get Inbound PC Dimension
    LT_PCDIM = ME->GET_PCDIM_IN( IT_PRVDR = IT_PRVDR ).
    ME->S_PCDIM_IN = value #( LT_PCDIM[ 1 ] optional ).

*** Get Outbound PC Dimension
    LT_PCDIM = ME->GET_PCDIM_OUT( IT_PRVDR = IT_PRVDR ).
    ME->S_PCDIM_OUT = value #( LT_PCDIM[ 1 ] optional ).

*** Initialize by CStep Additional Step
    ME->INIT_BY_CSTEP_ADSTP( IT_PRVDR = IT_PRVDR ).

***--------------------------------------------------------------------------------------
*** Call SUPER
    SUPER->INIT_BY_SPEC( IR_GATE  = IR_GATE
                         IT_PRVDR = IT_PRVDR ).

***--------------------------------------------------------------------------------------
*** Initialize by Additional Material
    ME->INIT_BY_MS_ADMAT( IT_PRVDR = IT_PRVDR ).

  endmethod.
  method ZIF_VCXI_PXSR_ADJ_PCDIM~CALC_PCDIM_IN.
  endmethod.
  method ZIF_VCXI_PXSR_ADJ_PCDIM~CALC_PCDIM_OUT.

    data: LS_PCDIM_OUT type  ZVCXI_P0SP_S_PCDIM.

*** Keep Number of Outs of provided Dimensions
    LS_PCDIM_OUT = ME->S_PCDIM_OUT.
    LS_PCDIM_OUT-NOOUT = reduce #( init LF_NOOUT = 0 for LS_PCDIM in CT_PCDIM next LF_NOOUT = LF_NOOUT + LS_PCDIM-NOOUT ).

    CT_PCDIM = value #( ( LS_PCDIM_OUT ) ).

  endmethod.
  method ZIF_VCXI_PXSR_PCDIR~GET_PCDIM.

    RT_PCDIM = value #( ( ME->S_PCDIM_IN ) ).

  endmethod.
  method ZIF_VCXI_PXSR_PCDIR~SET_PCDIR.

    move IF_PCDIR to ME->ZIF_VCXI_PXSR_PCDIR~F_PCDIR.

*** Turn PC Dimension IN
    ME->S_PCDIM_IN  = ME->TURN_PCDIM( IS_PCDIM = ME->S_PCDIM_IN
                                      IF_PCDIR = IF_PCDIR ).

*** Turn PC Dimension OUT
    ME->S_PCDIM_OUT = ME->TURN_PCDIM( IS_PCDIM = ME->S_PCDIM_OUT
                                      IF_PCDIR = IF_PCDIR ).

  endmethod.
