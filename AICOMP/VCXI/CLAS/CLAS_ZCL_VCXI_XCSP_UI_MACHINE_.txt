
class ZCL_VCXI_XCSP_UI_MACHINE definition
  public
  inheriting from /VCXI/CL_VKSRU_CLSUI
  create public .

public section.

  interfaces /VCXI/IF_VKSU_UIFW .

  data F_ACTIVE_TAB type STRING .
  data S_MACHINE_TAB type ZVCXI_XCSP_S_TAB_DESCR .
  data S_MACHINE_VIS type ZVCXI_XCSP_S_MACHINE_VIS .
  data F_ACTIVE_TAB_OLD type STRING .
  data R_DEFAULT_GATE type ref to /VCXI/CL_VKSR_GATE .
  data F_CURSOR type SCRFNAME .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_MACHINE_VIS
    importing
      !IS_MACHINE_VIS type ZVCXI_XCSP_S_MACHINE_VIS
    raising
      /VCXI/CX_CKX .
  methods GET_REGISTERED_UI_INFO
    returning
      value(RS_DYNP) type /VCXI/CKX_S_DYNP_KEY
    raising
      /VCXI/CX_CKX .
  methods SET_CURSOR
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  constants C_TAB type STRING value 'TAB' ##NO_TEXT.
  constants C_TABNAME type STRING value '0100S_TAB-TAB' ##NO_TEXT.
  data F_TAB_LIST_REFRESH_REQ type ABAP_BOOL .
  data R_PR_MACHINE type ref to ZCL_VCXI_XCSP_PR_MACHINE .
  data R_REGISTERED_UI type ref to /VCXI/CL_VKSU_CLSUI .
  data S_DYNP_BLANK type /VCXI/CKX_S_DYNP_KEY .
  data T_TAB_LIST type ZVCXI_XCSP_TT_TAB_LIST .
  data F_ALREADY_OPEN type ABAP_BOOL .
  data R_OLD_GATE type ref to /VCXI/CL_VKSR_GATE .

  methods DISPLAY_ACTIVE_TAB
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_FEATURE
    importing
      !IF_UCOMM type SYUCOMM
    raising
      /VCXI/CX_CKX .
  methods INIT_TAB_LIST
    raising
      /VCXI/CX_CKX .
  methods INIT_VIS
    raising
      /VCXI/CX_CKX .
  methods OPEN_SUBSCREEN
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    returning
      value(RR_CLSUI) type ref to /VCXI/CL_VKSRU_CLSUI
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
private section.

  methods INIT_BLANK_UI
    raising
      /VCXI/CX_CKX .
endclass. "ZCL_VCXI_XCSP_UI_MACHINE definition
class ZCL_VCXI_XCSP_UI_MACHINE implementation.
method /VCXI/IF_VKSU_UIFW~DO_MODIFY_SCREEN.

  if ME->R_REGISTERED_UI is bound.
    ME->R_REGISTERED_UI->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                           IF_DYNNR = IF_DYNNR ).
  endif.


endmethod.
method /VCXI/IF_VKSU_UIFW~DO_USER_COMMAND.

  data:  LR_CX_CKX type ref to /VCXI/CX_CKX.

  try.
      if ME->R_REGISTERED_UI is bound.
        ME->R_REGISTERED_UI->DO_USER_COMMAND( IF_UCOMM = CF_UCOMM ).
      endif.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      LR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

endmethod.
method /VCXI/IF_VKSU_UIFW~REFRESH_SCREEN.

  ME->R_UIFW->REFRESH_SCREEN( ).

endmethod.
method /VCXI/IF_VKSU_UIFW~REGISTER_UI.

  do.
    if ME->R_REGISTERED_UI is not bound.
      exit.
    endif.
    ME->R_REGISTERED_UI->DEACTIVATE( ).
  enddo.

  move IR_UI to ME->R_REGISTERED_UI.


endmethod.
method /VCXI/IF_VKSU_UIFW~UNREGISTER_UI.

  if IR_UI eq ME->R_REGISTERED_UI.
    clear ME->R_REGISTERED_UI.
  endif.

endmethod.
method ACTIVATE.

  SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
  call function 'ZVCXI_XCSP_UI_MACHINE_BIND'
    exporting
      IR_UI_MACHINE = ME
    importing
      ES_DYNP       = ME->S_DYNP
      EF_ACTIVE_TAB = ME->F_ACTIVE_TAB.

  if ME->R_REGISTERED_UI is bound.
    ME->R_REGISTERED_UI->ACTIVATE( ).
  endif.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                      IR_UIGATE = IR_UIGATE
                      IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
  try.
      move IR_GATE->R_CLSPR ?to ME->R_PR_MACHINE.
    catch CX_SY_MOVE_CAST_ERROR.
***   UI ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '801'.
  endtry.


*** Request Init List for Tabs
  move ABAP_TRUE to ME->F_TAB_LIST_REFRESH_REQ.

*** Initialize Blank UI
  ME->INIT_BLANK_UI( ).

endmethod.
method DEACTIVATE.

  SUPER->DEACTIVATE( ).

  if ME->R_REGISTERED_UI is bound.
    ME->R_REGISTERED_UI->DEACTIVATE( ).
  endif.

  clear: ME->F_ALREADY_OPEN.

endmethod.
method DISPLAY_ACTIVE_TAB.

  data:  LS_TAB_LIST    type        ZVCXI_XCSP_S_TAB_LIST.
  data:  LR_GATE        type ref to /VCXI/CL_VKSR_GATE.

  check ME->F_ACTIVE_TAB cs C_TABNAME.

  check ME->F_ACTIVE_TAB   ne ME->F_ACTIVE_TAB_OLD
     or ME->F_ALREADY_OPEN eq ABAP_FALSE.

  read table ME->T_TAB_LIST into LS_TAB_LIST with key TABNAME = ME->F_ACTIVE_TAB.
  if SY-SUBRC eq 0.
    move LS_TAB_LIST-R_GATE to LR_GATE.
  endif.

  R_OLD_GATE = LR_GATE.

  ME->OPEN_SUBSCREEN( IR_GATE = LR_GATE ).

endmethod.
method DO_MODIFY_SCREEN.

  data: LF_LOCKED type /VCXI/CKX_LOCKED,
        LF_INUSE  type /VCXI/VKSR_INUSE.
  data: LS_MDS type /VCXI/VSMC_S_MDS.

  try.
      SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                               IF_DYNNR = IF_DYNNR ).

      LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
      LF_INUSE  = ME->R_GATE->IS_INUSE( ).
      LS_MDS    = ME->R_PR_MACHINE->GET_MDS( ).

***   Modify Scree using DynamicVC
      ZCL_VCXI_XCDR_SERVICE=>MODIFY_SCREEN( IR_GATE = ME->R_GATE ).

      loop at screen.
        if LF_LOCKED ne ABAP_TRUE or
           LF_INUSE  ne ABAP_TRUE.
          move 0 to SCREEN-INPUT.
        endif.

***     Machine Data Source
        if ( SCREEN-GROUP1 eq 'ST' and                                   "Setup Time
             LS_MDS-MDSST eq /VCXI/CL_VSMC_MGRPV_SPEED=>C_MDS_NA )  or
           ( SCREEN-GROUP1 eq 'RS' and                                   "Run Speed
             LS_MDS-MDSRS eq /VCXI/CL_VSMC_MGRPV_SPEED=>C_MDS_NA )  or
           ( SCREEN-GROUP1 eq 'RSF' and                                  "Run Slowdown Factor
             LS_MDS-MDSRS ne /VCXI/CL_VSMC_MGRPV_SPEED=>C_MDS_MRC ) or
           ( SCREEN-GROUP1 eq 'TT' and                                   "Teardown Time
             LS_MDS-MDSTT eq /VCXI/CL_VSMC_MGRPV_SPEED=>C_MDS_NA )  or
           ( SCREEN-GROUP1 eq 'SW' and                                   "Setup Waste
             LS_MDS-MDSWA ne /VCXI/CL_VSMC_MGRPV_SPEED=>C_MDS_MRC ) or
           ( SCREEN-GROUP1 eq 'RW' and                                   "Run Waste
             LS_MDS-MDSWA eq /VCXI/CL_VSMC_MGRPV_SPEED=>C_MDS_NA )  or
           ( SCREEN-GROUP1 eq 'RWF' and                                  "Run Waste Factor
             LS_MDS-MDSWA ne /VCXI/CL_VSMC_MGRPV_SPEED=>C_MDS_MRC ).
          SCREEN-INVISIBLE = 1.
          SCREEN-INPUT     = 0.
        endif.

***     IQ.catalyst Feature Button
        if ( SCREEN-NAME eq '0100BTN_FEATURE_ST' and                    "Setup Time
             LS_MDS-MDSST ne /VCXI/CL_VSMC_MGRPV_SPEED=>C_MDS_IQC )  or
           ( SCREEN-NAME eq '0100BTN_FEATURE_RS' and                    "Run Speed
             LS_MDS-MDSRS ne /VCXI/CL_VSMC_MGRPV_SPEED=>C_MDS_IQC )  or
           ( SCREEN-NAME eq '0100BTN_FEATURE_TT' and                    "Teardown Time
             LS_MDS-MDSTT ne /VCXI/CL_VSMC_MGRPV_SPEED=>C_MDS_IQC )  or
           ( SCREEN-NAME eq '0100BTN_FEATURE_WA' and                    "Waste
             LS_MDS-MDSWA ne /VCXI/CL_VSMC_MGRPV_SPEED=>C_MDS_IQC ).
          SCREEN-INVISIBLE = 1.
          SCREEN-INPUT     = 0.
        endif.

        modify screen.
      endloop.

      loop at screen.
***     Set the Tabs which aren't in use as invisible.
        if SCREEN-NAME cs C_TABNAME.
          read table ME->T_TAB_LIST with key TABNAME = SCREEN-NAME transporting no fields.
          if SY-SUBRC ne 0.
            move 1 to SCREEN-INVISIBLE.
          endif.
        endif.
        modify screen.
      endloop.

      loop at screen.
***     Set tabs back to input
        if SCREEN-NAME cs C_TABNAME.
          move 1 to SCREEN-INPUT.
        endif.
        modify screen.
      endloop.

    catch /VCXI/CX_CKX into data(LR_CX_CKX).
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

endmethod.
method DO_PBO.

*** Check if the PR Class is bound
  check ME->R_PR_MACHINE is bound.

  if ME->F_ACTIVE_TAB ne ME->F_ACTIVE_TAB_OLD.
    move ME->F_ACTIVE_TAB to ME->F_ACTIVE_TAB_OLD.
    clear: ME->F_ALREADY_OPEN.
  endif.

  ME->INIT_VIS( ).
  ME->INIT_TAB_LIST( ).
  ME->DISPLAY_ACTIVE_TAB( ).

  set cursor field ME->F_CURSOR.
  clear ME->F_CURSOR.

endmethod.
method DO_UPDATE_MACHINE_VIS.


  if ME->S_MACHINE_VIS-WERKS ne IS_MACHINE_VIS-WERKS or
      ME->S_MACHINE_VIS-ARBPL ne IS_MACHINE_VIS-ARBPL.
    ME->R_PR_MACHINE->SET_WERKS_ARBPL( IF_WERKS = IS_MACHINE_VIS-WERKS
                                       IF_ARBPL = IS_MACHINE_VIS-ARBPL
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

endmethod.
  method DO_USER_COMMAND.

    data:  LF_UCOMM      type SYUCOMM.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    if IF_UCOMM cs C_TABNAME.
      move IF_UCOMM to ME->F_ACTIVE_TAB.
    endif.

    move IF_UCOMM to LF_UCOMM.
    ME->/VCXI/IF_VKSU_UIFW~DO_USER_COMMAND( changing CF_UCOMM = LF_UCOMM ).

    case IF_UCOMM.
***   IQ.catalyst Feature
      when 'FEATURE_ST' or
           'FEATURE_RS' or
           'FEATURE_TT' or
           'FEATURE_WA'.
        ME->HANDLE_UC_FEATURE( IF_UCOMM = IF_UCOMM ).
    endcase.

  endmethod.
method GET_REGISTERED_UI_INFO.

  try.
      if ME->R_REGISTERED_UI is bound.
        move ME->R_REGISTERED_UI->S_DYNP to RS_DYNP.
      else.
        move ME->S_DYNP_BLANK-REPID    to RS_DYNP-REPID.
        move ME->S_DYNP_BLANK-DYNNR    to RS_DYNP-DYNNR.
      endif.

    catch /VCXI/CX_CKX.
      move ME->S_DYNP_BLANK-REPID    to RS_DYNP-REPID.
      move ME->S_DYNP_BLANK-DYNNR    to RS_DYNP-DYNNR.
  endtry.

endmethod.
method HANDLE_GATE_FREED.

  SUPER->HANDLE_GATE_FREED( ).

  free: ME->R_PR_MACHINE.

endmethod.
  method HANDLE_UC_FEATURE.

    data: LT_FEATURE     type /VCXI/CKI_TT_FEATURE,
          LT_FEATURE_INP type /VCXI/CKI_TT_FEATURE.
    data: LR_SALV type ref to CL_SALV_TABLE.


    check ME->R_PR_MACHINE is bound and
          ME->R_PR_MACHINE->R_RTE_CMACH is bound.

*** Get All defined Features
    LT_FEATURE = ME->R_PR_MACHINE->GET_FEATURE( ).

***------------------------------------------------------------------------------------------------
*** Filter provided Features to match requested
    case IF_UCOMM.
      when 'FEATURE_ST'.   "Setup Time
        ME->R_PR_MACHINE->R_RTE_CMACH->FILTER_FEATURE( exporting IT_FEATURE    = LT_FEATURE
                                                       importing ET_FEATURE_ST = LT_FEATURE_INP ).
      when 'FEATURE_RS'.   "Run Speed
        ME->R_PR_MACHINE->R_RTE_CMACH->FILTER_FEATURE( exporting IT_FEATURE    = LT_FEATURE
                                                       importing ET_FEATURE_RS = LT_FEATURE_INP ).
      when 'FEATURE_TT'.   "Teardown Time
        ME->R_PR_MACHINE->R_RTE_CMACH->FILTER_FEATURE( exporting IT_FEATURE    = LT_FEATURE
                                                       importing ET_FEATURE_TT = LT_FEATURE_INP ).
      when 'FEATURE_WA'.   "Waste
        ME->R_PR_MACHINE->R_RTE_CMACH->FILTER_FEATURE( exporting IT_FEATURE    = LT_FEATURE
                                                       importing ET_FEATURE_WA = LT_FEATURE_INP ).
    endcase.


***------------------------------------------------------------------------------------------------
    try.
***     Create Simple ALV
        CL_SALV_TABLE=>FACTORY( importing R_SALV_TABLE = LR_SALV
                                changing T_TABLE       = LT_FEATURE_INP ).

***     Display as Popup
        LR_SALV->SET_SCREEN_POPUP( START_COLUMN = 10
                                   END_COLUMN   = 130
                                   START_LINE   = 10
                                   END_LINE     = 30 ).
        LR_SALV->DISPLAY( ).

      catch CX_SALV_ERROR into data(LR_CX_SALV_ERROR).
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( LR_CX_SALV_ERROR ).
    endtry.

  endmethod.
method INIT_BLANK_UI.

  data:  LR_CLSUI type ref to /VCXI/CL_VKSU_CLSUI.

  create object LR_CLSUI
    exporting
      IR_UIFW = ME.

  LR_CLSUI->ACTIVATE( ).
  move LR_CLSUI->S_DYNP to ME->S_DYNP_BLANK.

endmethod.
method INIT_TAB_LIST.

  data:           LF_TAB             type         NUMC3,
                  LF_FNAME           type         STRING.
  data:           LR_GATE            type ref to  /VCXI/CL_VKSR_GATE.
  data:           LT_GATE            type         /VCXI/VKSR_TT_GATE,
                  LT_GATE_CHILDS     type         /VCXI/VKSR_TT_GATE,
                  LT_PRVDR_DEF       type         /VCXI/VKSR_TT_PRVDR_DEF.
  data:           LS_PRVDR_DEF       type         /VCXI/VKSR_S_PRVDR_DEF.
  field-symbols:  <S_TAB_LIST>       type         ZVCXI_XCSP_S_TAB_LIST,
                  <F_DESCR>          type         /VCXI/CKX_DESCR.

  free: ME->T_TAB_LIST,
        ME->S_MACHINE_TAB,
        LF_TAB.

***------------------------------------------------------------------
*** Get all Children
  LT_PRVDR_DEF = ME->R_GATE->GET_PRVDR_DEF( IF_TPRVR = 'CHILD' ).

  loop at LT_PRVDR_DEF into LS_PRVDR_DEF.

    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = LS_PRVDR_DEF-IPRVT
                                     IF_INUSE = ABAP_TRUE ).

    append lines of LT_GATE to LT_GATE_CHILDS.

  endloop.

*** Sort Childs
  ME->R_GATE->SORT_GATE( changing CT_GATE = LT_GATE_CHILDS ).

  loop at LT_GATE_CHILDS into LR_GATE.

    check LR_GATE->GET_CLSUI( ) is not initial.

    append initial line to ME->T_TAB_LIST assigning <S_TAB_LIST>.
    add 1 to LF_TAB.
    concatenate C_TABNAME LF_TAB into <S_TAB_LIST>-TABNAME.
    move LR_GATE                   to <S_TAB_LIST>-R_GATE.

*** Set Activ Tab if it was set by default Gate
    if ME->R_DEFAULT_GATE is bound.
      if LR_GATE eq ME->R_DEFAULT_GATE or
         ME->R_DEFAULT_GATE->IS_IN_LINE( IR_GATE = LR_GATE ) eq ABAP_TRUE.
        move <S_TAB_LIST>-TABNAME to ME->F_ACTIVE_TAB.
        move <S_TAB_LIST>-TABNAME to ME->F_ACTIVE_TAB_OLD.
        if LR_GATE ne ME->R_OLD_GATE.
          move ABAP_FALSE to ME->F_ALREADY_OPEN.
        endif.
      endif.
*** Default gate is not bound but Gate of Activ Tab do not match
    else.
      if ME->F_ACTIVE_TAB eq <S_TAB_LIST>-TABNAME and
         ME->R_OLD_GATE   ne LR_GATE.
        move ABAP_FALSE to ME->F_ALREADY_OPEN.
      endif.
    endif.

    concatenate C_TAB LF_TAB into LF_FNAME.
    assign component LF_FNAME of structure ME->S_MACHINE_TAB to <F_DESCR>.
    if SY-SUBRC eq 0.
      if <S_TAB_LIST>-R_GATE->R_CLSPR is bound.
        move <S_TAB_LIST>-R_GATE->R_CLSPR->GET_DESCR( ) to <F_DESCR>.
      else.
        move <S_TAB_LIST>-R_GATE->GET_DESCR( ) to <F_DESCR>.
      endif.
    endif.
  endloop.

*** Clear Default gate
  clear: ME->R_DEFAULT_GATE.

*** Check Active Tab exists
  if ME->F_ACTIVE_TAB cs C_TABNAME.
    read table ME->T_TAB_LIST with key TABNAME = ME->F_ACTIVE_TAB
                              transporting no fields.
    if SY-SUBRC ne 0.
      read table ME->T_TAB_LIST assigning <S_TAB_LIST> index 1.
      if SY-SUBRC eq 0.
        move <S_TAB_LIST>-TABNAME to ME->F_ACTIVE_TAB.
      else.
        clear ME->F_ACTIVE_TAB.
      endif.
    endif.
  endif.

endmethod.
  method INIT_VIS.

    free: ME->S_MACHINE_VIS.

*** Get Workcenter and Plant
    ME->R_PR_MACHINE->GET_WERKS_ARBPL( importing EF_WERKS = ME->S_MACHINE_VIS-WERKS
                                                 EF_ARBPL = ME->S_MACHINE_VIS-ARBPL ).
    ME->S_MACHINE_VIS-ARBPL_DESCR = ME->R_PR_MACHINE->GET_ARBPL_DESCR( ).

*** Get Quantities (Base/Machine & In/Out)
    ME->S_MACHINE_VIS-S_MQTYI = ME->R_PR_MACHINE->GET_MQTYI( ).
    ME->S_MACHINE_VIS-S_MQTYO = ME->R_PR_MACHINE->GET_MQTYO( ).

*** Get Machine Speed
    ME->S_MACHINE_VIS-S_MSRS = ME->R_PR_MACHINE->GET_MSRS( ).

*** Get Machine Setup Values
    ME->S_MACHINE_VIS-S_MSSTI = ME->R_PR_MACHINE->GET_MSSTI( ).
    ME->S_MACHINE_VIS-S_MSSWA = ME->R_PR_MACHINE->GET_MSSWA( ).

*** Get Machine Run Values
    ME->S_MACHINE_VIS-S_MSRSF = ME->R_PR_MACHINE->GET_MSRSF( ).
    ME->S_MACHINE_VIS-S_MSRTI = ME->R_PR_MACHINE->GET_MSRTI( ).
    ME->S_MACHINE_VIS-S_MSRWF = ME->R_PR_MACHINE->GET_MSRWF( ).
    ME->S_MACHINE_VIS-S_MSRWA = ME->R_PR_MACHINE->GET_MSRWA( ).

*** Get Machine Teardown Value
    ME->S_MACHINE_VIS-S_MSTTI = ME->R_PR_MACHINE->GET_MSTTI( ).

*** Get Number of Helpers
    move ME->R_PR_MACHINE->GET_HELPN( ) to ME->S_MACHINE_VIS-HELPN.

  endmethod.
method OPEN_SUBSCREEN.

  data:     LF_CLSUI  type        /VCXI/VKSC_CLSUI.

  if IR_GATE is bound.
    LF_CLSUI = IR_GATE->GET_CLSUI( ).
  endif.

  if LF_CLSUI is not initial.
*** Create Subscreen
    create object RR_CLSUI
      type
        (LF_CLSUI)
      exporting
        IR_UIFW    = ME
        IR_UIGATE  = ME->R_UIGATE
        IR_GATE    = IR_GATE.
    RR_CLSUI->ACTIVATE( ).
    ME->F_ALREADY_OPEN = ABAP_TRUE.
  else.
*** Clear Subscreen
    if ME->R_REGISTERED_UI is bound.
      ME->R_REGISTERED_UI->DEACTIVATE( ).
    endif.
  endif.

endmethod.
method SET_CURSOR.

  data:  LF_CURSOR type SCRFNAME.

  get cursor field LF_CURSOR.
  if LF_CURSOR is not initial.
    move LF_CURSOR to ME->F_CURSOR.
  endif.

endmethod.
