
class ZCL_VCXI_FBSP_PR_ACTVY_WICKET definition
  public
  inheriting from /VCXI/CL_VKSR_CLSPR
  create public .

public section.

  constants C_PARID_TWICK type /VCXI/VKS_PARID value 'ZVCXI_FB_TWICK' ##NO_TEXT.

  methods GET_TWICK
    exporting
      !EF_TWICK type ZVCXI_FBS_TWICK
      !EF_SETBY type /VCXI/VKSR_SETBY
    raising
      /VCXI/CX_CKX .
  methods SET_TWICK
    importing
      !IF_TWICK type ZVCXI_FBS_TWICK
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .

  methods HANDLE_EVENT
    redefinition .
protected section.

  constants C_IEVNT_I_RECALC_MC_CONSU type /VCXI/VKSC_IEVNT value 'RECALC_MC_CONSU' ##NO_TEXT.
  constants C_IEVNT_O_TWICK_CHANGED type /VCXI/VKSC_IEVNT value 'TWICK_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_RECALC_CONSU_REQ type /VCXI/VKSC_IEVNT value 'RECALC_CONSU_REQ' ##NO_TEXT.

  methods HANDLE_RECALC_MC_CONSU
    importing
      !IT_GATE type /VCXI/VKSR_TT_GATE
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_FBSP_PR_ACTVY_WICKET definition
class ZCL_VCXI_FBSP_PR_ACTVY_WICKET implementation.
  method GET_TWICK.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TWICK ).
    move LS_PARAM-ATWRT to EF_TWICK.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method HANDLE_EVENT.

    SUPER->HANDLE_EVENT( IF_IEVNT         = IF_IEVNT
                         IT_GATE_PROVIDER = IT_GATE_PROVIDER ).

    case IF_IEVNT.
      when C_IEVNT_I_RECALC_MC_CONSU.
        ME->HANDLE_RECALC_MC_CONSU( IT_GATE = IT_GATE_PROVIDER ).

    endcase.

  endmethod.
  method HANDLE_RECALC_MC_CONSU.

    data: LF_UPDATE_ALL  type        ABAP_BOOL.
    data: LS_MQTYI       type        ZVCXI_XCSP_S_MQTYI.
    data: LT_GATE        type        /VCXI/VKSR_TT_GATE.
    data: LR_GATE        type ref to /VCXI/CL_VKSR_GATE,
          LR_GATE_PARENT type ref to /VCXI/CL_VKSR_GATE,
          LR_OPERATION   type ref to ZIF_VCXI_XCSP_OPERATION,
          LR_PR_MC       type ref to ZCL_VCXI_XCSP_PR_MC,
          LR_PR_MC_WIMAT type ref to ZCL_VCXI_FBSP_PR_MC_WIMAT.

***--------------------------------------------------------------------------------------
*** Should all MC Wicketing Materials's updated or just the requester
    read table IT_GATE with key TABLE_LINE = ME->R_GATE transporting no fields.
    if SY-SUBRC = 0.
      LF_UPDATE_ALL = ABAP_TRUE.
    else.
      LR_GATE_PARENT = ME->R_GATE->GET_PARENT( ).
      read table IT_GATE with key TABLE_LINE = LR_GATE_PARENT transporting no fields.
      if SY-SUBRC = 0.
        LF_UPDATE_ALL = ABAP_TRUE.
      endif.
    endif.

***------------------------------------------------------------------
*** Get Machine Quantity
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZIF_VCXI_XCSP_OPERATION=>C_IPRVT_XCS_OPERATION
                                     IF_INUSE = ABAP_TRUE ).

    read table LT_GATE into LR_GATE index 1.
    if SY-SUBRC = 0.

      clear: LR_OPERATION.
      try.
          move LR_GATE->R_CLSPR ?to LR_OPERATION.
        catch CX_SY_MOVE_CAST_ERROR.
          clear: LR_OPERATION.
      endtry.
      if LR_OPERATION is bound.
        move LR_OPERATION->GET_MQTYI( ) to LS_MQTYI.
      endif.

    endif.

***--------------------------------------------------------------------------------------
*** Get all Material Consumptions
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCSP_PR_MC=>C_IPRVT_XCS_MC
                                     IF_INUSE = ABAP_TRUE ).

***--------------------------------------------------------------------------------------
*** Start Recalc Consumption of all Material Consumptions or just the requester
    loop at LT_GATE into LR_GATE.
      if LF_UPDATE_ALL eq ABAP_FALSE.
        read table IT_GATE with key TABLE_LINE = LR_GATE transporting no fields.
        if SY-SUBRC ne 0.
          continue.
        endif.
      endif.

***   Try Calculation for Wicketing Material
      try.
          move LR_GATE->R_CLSPR ?to LR_PR_MC_WIMAT.
        catch CX_SY_MOVE_CAST_ERROR.
          clear LR_PR_MC_WIMAT.
      endtry.
      if LR_PR_MC_WIMAT is bound.
        LR_PR_MC_WIMAT->CALC_CONSU( IS_MQTYI = LS_MQTYI ).
      else.
***     Calculate standard Consumption
        try.
            move LR_GATE->R_CLSPR ?to LR_PR_MC.
          catch CX_SY_MOVE_CAST_ERROR.
            clear LR_PR_MC.
        endtry.
        if LR_PR_MC is bound.
          LR_PR_MC->CALC_CONSU( IS_MQTYI = LS_MQTYI ).
        endif.
      endif.
    endloop.

  endmethod.
  method SET_TWICK.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TWICK ).

    move IF_TWICK           to LS_PARAM-ATWRT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_TWICK_CHANGED ).
    endif.

  endmethod.
