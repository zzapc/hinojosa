
class ZCL_VCXI_FBSP_EA_MANU_PTP definition
  public
  inheriting from ZCL_VCXI_XCSP_EA_MANU
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
protected section.

  data R_PR_MANU_PTP type ref to ZCL_VCXI_FBSP_PR_MANU_PTP .

  methods GET_QUANTITY
    redefinition .
private section.
endclass. "ZCL_VCXI_FBSP_EA_MANU_PTP definition
class ZCL_VCXI_FBSP_EA_MANU_PTP implementation.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_GATE = IR_GATE ).

*** Assign the PR Class to the Element API Class
    try.
        move ME->R_GATE->R_CLSPR ?to ME->R_PR_MANU_PTP.
      catch CX_SY_MOVE_CAST_ERROR.
***     Element API ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '804'.
    endtry.

  endmethod.
  method GET_QUANTITY.

    data: LF_UMREZ type UMREZ,
          LF_UMREN type UMREN.
    data: LS_MQTYI type ZVCXI_XCSP_S_MQTYI,
          LS_BGDIM type ZVCXI_FBSP_S_BGDIM.
    data: LT_BGDIM type ZVCXI_FBSP_TT_BGDIM.

***--------------------------------------------------------------------------------------
*** Get Base Quantity
    move ME->R_PR_MANU->GET_MQTYI( )    to LS_MQTYI.
    move LS_MQTYI-MQTYI                 to EF_BMSCH.
    move ABAP_TRUE                      to EF_BMSCH_X.
    move LS_MQTYI-MQTYI_UOM             to EF_MEINH.
    move ABAP_TRUE                      to EF_MEINH_X.

***--------------------------------------------------------------------------------------
*** Get Flow In
    LT_BGDIM = ME->R_PR_MANU_PTP->GET_MFI_BGDIM( ).
    read table LT_BGDIM into LS_BGDIM index 1.

***--------------------------------------------------------------------------------------
*** Get Conversion
    move LS_BGDIM-NOUPS   to EF_UMREZ.
    move ABAP_TRUE        to EF_UMREZ_X.
    move ME->GET_MFRAT( ) to EF_UMREN.
    move ABAP_TRUE        to EF_UMREN_X.

***--------------------------------------------------------------------------------------
*** Alternate Units
    if EF_MEINH ne IF_PLNME.
      call function 'UNIT_CONVERSION_SIMPLE'
        exporting
          INPUT                = 1
          UNIT_IN              = IF_PLNME
          UNIT_OUT             = EF_MEINH
        importing
          DENOMINATOR          = LF_UMREZ
          NUMERATOR            = LF_UMREN
        exceptions
          CONVERSION_NOT_FOUND = 1
          DIVISION_BY_ZERO     = 2
          INPUT_INVALID        = 3
          OUTPUT_INVALID       = 4
          OVERFLOW             = 5
          TYPE_INVALID         = 6
          UNITS_MISSING        = 7
          UNIT_IN_NOT_FOUND    = 8
          UNIT_OUT_NOT_FOUND   = 9.
      if SY-SUBRC ne 0.
***     Ignore Conversion
        LF_UMREZ = 1.
        LF_UMREN = 1.
      endif.

***   Adjust Conversion based on Unit Conversion
      EF_UMREZ = EF_UMREZ * LF_UMREZ.
      EF_UMREN = EF_UMREN * LF_UMREN.
    endif.

  endmethod.
endclass. "ZCL_VCXI_FBSP_EA_MANU_PTP implementation
