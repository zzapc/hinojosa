
class ZCL_VCXI_PFSS_BL_SCSOP_RCUT definition
  public
  inheriting from ZCL_VCXI_P0SS_BL_SCSOP
  create public .

public section.

  constants C_SCSOP_RCUT type ZVCXI_P0S_SCSOP value 'RCUT'. "#EC NOTEXT

  methods CONSTRUCTOR
    importing
      !IF_SHTDI type ZVCXI_P0SS_SHTDI
      !IF_CSCOR type ZVCXI_P0SS_CSCOR
    raising
      /VCXI/CX_CKX .
protected section.

  methods GET_MS_RCUT_PRVDR
    importing
      !IT_PRVDR type /VCXI/VKSR_TT_GATE
    returning
      value(RR_PR_MS_RCUT) type ref to ZCL_VCXI_PFSS_PR_MS_RCUT
    raising
      /VCXI/CX_CKX .
  methods GET_SPEC_UCB_PRVDR
    importing
      !IT_PRVDR type /VCXI/VKSR_TT_GATE
    returning
      value(RR_PR_SPEC_UCB) type ref to ZCL_VCXI_PFSS_PR_SPEC_UCB
    raising
      /VCXI/CX_CKX .

  methods CALC_SCSTR
    redefinition .
  methods GET_DIVDI
    redefinition .
  methods READ_SHT_DIM
    redefinition .
private section.
endclass. "ZCL_VCXI_PFSS_BL_SCSOP_RCUT definition
class ZCL_VCXI_PFSS_BL_SCSOP_RCUT implementation.
method CALC_SCSTR.

  data:          LS_REWID       type        ZVCXI_P0SS_S_REWID,
                 LS_INGRA       type        ZVCXI_PFSS_S_INGRA.
  data:          LT_SCSTR_CALC  type        ZVCXI_P0SS_TT_SCSTR_CALC.
  data:          LR_PR_MS_RCUT  type ref to ZCL_VCXI_PFSS_PR_MS_RCUT,
                 LR_PR_SPEC_UCB type ref to ZCL_VCXI_PFSS_PR_SPEC_UCB.
  field-symbols: <S_SCSTR_CALC> type        ZVCXI_P0SS_S_SCSTR_CALC.

***------------------------------------------------------------------------------------------------
*** Get Reel Width and In Grain Dimensions
  LR_PR_MS_RCUT  = ME->GET_MS_RCUT_PRVDR( IT_PRVDR = IT_PRVDR ).
  LR_PR_SPEC_UCB = ME->GET_SPEC_UCB_PRVDR( IT_PRVDR = IT_PRVDR ).
  check LR_PR_MS_RCUT  is bound and
        LR_PR_SPEC_UCB is bound.
  LS_REWID = LR_PR_MS_RCUT->GET_REWID( ).
  LS_INGRA = LR_PR_SPEC_UCB->GET_INGRA( ).

***------------------------------------------------------------------------------------------------
*** 1 As there is no need of a Score String we just populate the Reel Width or In Grain Dimension!!
  append initial line to LT_SCSTR_CALC assigning <S_SCSTR_CALC>.
  case ME->F_SHTDI.
    when 'A'.
      move LS_REWID-REWID     to <S_SCSTR_CALC>-SCORP.
      move LS_REWID-REWID_UOM to <S_SCSTR_CALC>-SCORP_UOM.
    when 'B'.
      move LS_INGRA-INGRA     to <S_SCSTR_CALC>-SCORP.
      move LS_INGRA-INGRA_UOM to <S_SCSTR_CALC>-SCORP_UOM.
  endcase.

***------------------------------------------------------------------------------------------------
*** 3 Define the right Score String Values ( SCSTR_VALUE ) and return the Hole string
  RF_SCSTR = ME->BUILD_SCSTR( IF_SSTRC      = IR_PR_SCSTR_MGR->F_SSTRC
                              IT_SCSTR_CALC = LT_SCSTR_CALC ).

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IF_SHTDI = IF_SHTDI
                      IF_CSCOR = IF_CSCOR ).

*** Set Score Operation
  move C_SCSOP_RCUT to ME->F_SCSOP.

endmethod.
method GET_DIVDI.

*** Nothing to do!

endmethod.
method GET_MS_RCUT_PRVDR.

  data: LR_GATE             type ref to /VCXI/CL_VKSR_GATE.

***-------------------------------------------------------------------------------------------
*** Read the MS_RCUT
  loop at IT_PRVDR into LR_GATE.
    try.
        move LR_GATE->R_CLSPR ?to RR_PR_MS_RCUT.
        if RR_PR_MS_RCUT is bound.
          exit.
        endif.
      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.
  endloop.

endmethod.
method GET_SPEC_UCB_PRVDR.

  data: LR_GATE             type ref to /VCXI/CL_VKSR_GATE.

***-------------------------------------------------------------------------------------------
*** Read the SPEC_UCB
  loop at IT_PRVDR into LR_GATE.
    try.
        move LR_GATE->R_CLSPR ?to RR_PR_SPEC_UCB.
        if RR_PR_SPEC_UCB is bound.
          exit.
        endif.
      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.
  endloop.

endmethod.
method READ_SHT_DIM.

  data:          LS_INGRA       type        ZVCXI_PFSS_S_INGRA,
                 LS_REWID       type        ZVCXI_P0SS_S_REWID.
  data:          LR_PR_SPEC_UCB type ref to ZCL_VCXI_PFSS_PR_SPEC_UCB,
                 LR_PR_MS_RCUT  type ref to ZCL_VCXI_PFSS_PR_MS_RCUT.

***------------------------------------------------------------------------------------------------
*** Get In Grain Dimensions
  LR_PR_SPEC_UCB = ME->GET_SPEC_UCB_PRVDR( IT_PRVDR = IT_PRVDR ).
  check LR_PR_SPEC_UCB is bound.
  LS_INGRA = LR_PR_SPEC_UCB->GET_INGRA( ).

  move LS_INGRA-INGRA     to ES_SHTLE-SHTLE.
  move LS_INGRA-INGRA_UOM to ES_SHTLE-SHTLE_UOM.

***------------------------------------------------------------------------------------------------
*** Get Reel Width and In Grain Dimensions
  LR_PR_MS_RCUT  = ME->GET_MS_RCUT_PRVDR( IT_PRVDR = IT_PRVDR ).
  check LR_PR_MS_RCUT  is bound.
  LS_REWID = LR_PR_MS_RCUT->GET_REWID( ).

  move LS_REWID-REWID     to ES_SHTWI-SHTWI.
  move LS_REWID-REWID_UOM to ES_SHTWI-SHTWI_UOM.

endmethod.
