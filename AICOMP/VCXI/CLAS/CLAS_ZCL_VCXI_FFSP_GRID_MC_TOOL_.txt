
class ZCL_VCXI_FFSP_GRID_MC_TOOL definition
  public
  inheriting from ZCL_VCXI_XCS_GRID_PRVDR
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_CONTAINER_NAME type SCRFNAME optional
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL default ABAP_TRUE
      !IF_TABNAME type TABNAME default 'ZVCXI_FFSP_S_MC_TOOL_GRID'
    raising
      /VCXI/CX_CKX .
protected section.

  methods CHANGE_STYLE_MC_TOOL
    importing
      !IF_PURCH type ZVCXI_XCS_PURCH
    changing
      !CT_STYL type LVC_T_STYL
    raising
      /VCXI/CX_CKX .
  methods GET_MC_TOOL_VIS
    importing
      !IR_PR_MC_TOOL type ref to ZCL_VCXI_F0SP_PR_MC_TOOL
    returning
      value(RS_MC_TOOL_VIS) type ZVCXI_FFSP_S_MC_TOOL_VIS
    raising
      /VCXI/CX_CKX .
  methods SET_MC_TOOL_VIS
    importing
      !IR_PR_MC_TOOL type ref to ZCL_VCXI_F0SP_PR_MC_TOOL
      !IS_MC_TOOL_VIS type ZVCXI_FFSP_S_MC_TOOL_VIS
    raising
      /VCXI/CX_CKX .

  methods GET_FIELDCAT
    redefinition .
  methods GET_GRID_DATA
    redefinition .
  methods HANDLE_DATA_CHANGED_FINISHED
    redefinition .
private section.
endclass. "ZCL_VCXI_FFSP_GRID_MC_TOOL definition
class ZCL_VCXI_FFSP_GRID_MC_TOOL implementation.
method CHANGE_STYLE_MC_TOOL.

  free CT_STYL.

*** Set Ink Matnr = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MATNR'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Text = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'TEXT'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Average Consumption = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'AVGCO'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Average Consumption per = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'AVGCP'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_GATE           = IR_GATE
                      IR_UIGATE         = IR_UIGATE
                      IF_TABNAME        = IF_TABNAME
                      IF_CONTAINER_NAME = IF_CONTAINER_NAME
                      IF_APPL_EVENTS    = IF_APPL_EVENTS ).

*** Define Provider Type for Button
  ME->F_IPRVT = ZCL_VCXI_F0SP_PR_MC_TOOL=>C_IPRVT_MC_TOOL.

endmethod.
method GET_FIELDCAT.

  data: LF_LOCKED type        /VCXI/CKX_LOCKED,
        LF_INUSE  type        /VCXI/VKSR_INUSE,
        LF_COLPOS type        I.
  field-symbols: <S_FCAT>  type        LVC_S_FCAT.

***----------------------------------------------------------------------------
*** Get Standard Field Catalog
  RT_FCAT = SUPER->GET_FIELDCAT( ).

*** Get Lock and In-Use Status
  LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
  LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***----------------------------------------------------------------------------
*** Adjust Field Catalog
  loop at RT_FCAT assigning <S_FCAT>.
    case <S_FCAT>-FIELDNAME.
      when 'POSID' or 'GATKY' or 'R_GATE' or 'T_STYL'.
        move ABAP_TRUE    to <S_FCAT>-TECH.

***   Set Edit for all fields which should Editable at all
      when 'MATNR' or 'AVGCO' or 'AVGCP' or 'TEXT'.
        if LF_LOCKED eq ABAP_TRUE and
           LF_INUSE  eq ABAP_TRUE.
          move ABAP_TRUE    to <S_FCAT>-EDIT.
        endif.

      when 'LIFNR_DESCR' or 'PRICE' or 'PRICE_CURR' or 'PRICP_UOM' or
           'PURCH' or 'PRICP' or 'LIFNR'.
        move ABAP_TRUE    to <S_FCAT>-NO_OUT.

    endcase.

*** Set Position for each Field
    move 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'MATNR'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'MATNR_DESCR'. <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'CONSU'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'CONSU_UOM'.   <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'TEXT'.        <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'AVGCO'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'AVGCO_UOM'.   <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'AVGCP'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'AVGCP_UOM'.   <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.

  endloop.

  sort RT_FCAT by COL_POS.

endmethod.
method GET_GRID_DATA.

  data: LS_MC_TOOL_GRID type          ZVCXI_FFSP_S_MC_TOOL_GRID.
  data: LT_GATE         type          /VCXI/VKSR_TT_GATE.
  data: LR_GATE         type ref to   /VCXI/CL_VKSR_GATE,
        LR_PR_MC_TOOL   type ref to   ZCL_VCXI_F0SP_PR_MC_TOOL.
  field-symbols: <T_DATA> type standard table,
                 <S_DATA> type          ANY.

*** Dereferencing of Data Reference
  assign IR_DATA->* to <T_DATA>.

*** Get Gates of defined Provider
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ME->F_IPRVT
                                   IF_INUSE  = ABAP_TRUE
                                   IF_SORTED = ABAP_TRUE ).

  loop at LT_GATE into LR_GATE.
    check LR_GATE->IS_INUSE( ) eq ABAP_TRUE and
          LR_GATE->R_CLSPR     is bound.

    clear: LS_MC_TOOL_GRID.

    try.
        move LR_GATE->R_CLSPR ?to LR_PR_MC_TOOL.
        check LR_PR_MC_TOOL is bound.

***--------------------------------------------------------------------------------------
***     Get Data from PR Class
        move LR_GATE->GET_POSID( ) to LS_MC_TOOL_GRID-POSID.
        move LR_GATE->F_GATKY      to LS_MC_TOOL_GRID-GATKY.
        move LR_GATE               to LS_MC_TOOL_GRID-R_GATE.

        LS_MC_TOOL_GRID-S_MC_TOOL_VIS = ME->GET_MC_TOOL_VIS( IR_PR_MC_TOOL = LR_PR_MC_TOOL ).

***--------------------------------------------------------------------------------------
***     Set Style Details
        ME->CHANGE_STYLE_MC_TOOL( exporting IF_PURCH = LS_MC_TOOL_GRID-PURCH
                                  changing  CT_STYL  = LS_MC_TOOL_GRID-T_STYL ).

***     Call DVC Service - Modify Style
        ZCL_VCXI_XCDR_SERVICE=>MODIFY_STYLE( exporting IR_GATE = LR_GATE
                                             changing  CT_STYL = LS_MC_TOOL_GRID-T_STYL ).

***--------------------------------------------------------------------------------------
***     Add Entry
        append initial line to <T_DATA> assigning <S_DATA>.
        move-corresponding LS_MC_TOOL_GRID to <S_DATA>.

      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.
  endloop.

endmethod.
  method GET_MC_TOOL_VIS.

*** Get MaterialNumber, Description and Varnish Flag
    move IR_PR_MC_TOOL->GET_MATNR( )       to RS_MC_TOOL_VIS-MATNR.
    move IR_PR_MC_TOOL->GET_MATNR_DESCR( ) to RS_MC_TOOL_VIS-MATNR_DESCR.

*** Get Consumption
    RS_MC_TOOL_VIS-S_CONSU = IR_PR_MC_TOOL->GET_CONSU( ).

*** Get Average Consumption and Consumption per
    RS_MC_TOOL_VIS-S_AVGCO = IR_PR_MC_TOOL->GET_AVGCO( ).
    RS_MC_TOOL_VIS-S_AVGCP = IR_PR_MC_TOOL->GET_AVGCP( ).

  endmethod.
method HANDLE_DATA_CHANGED_FINISHED.

  data: LS_MC_TOOL_VIS  type          ZVCXI_FFSP_S_MC_TOOL_VIS,
        LS_MC_TOOL_GRID type          ZVCXI_FFSP_S_MC_TOOL_GRID.
  data: LR_PR_MC_TOOL   type ref to   ZCL_VCXI_F0SP_PR_MC_TOOL,
        LR_CX_CKX       type ref to   /VCXI/CX_CKX.
  field-symbols: <T_DATA> type standard table,
                 <S_DATA> type          ANY.

  check E_MODIFIED eq ABAP_TRUE.

  try.

      SUPER->HANDLE_DATA_CHANGED_FINISHED( E_MODIFIED    = E_MODIFIED
                                           ET_GOOD_CELLS = ET_GOOD_CELLS ).

***   Dereferencing of Data Reference
      assign ME->R_DATA->* to <T_DATA>.

      loop at <T_DATA> assigning <S_DATA>.
        move-corresponding <S_DATA> to LS_MC_TOOL_GRID.

        move LS_MC_TOOL_GRID-R_GATE->R_CLSPR ?to LR_PR_MC_TOOL.

***     Set Values to PR Class
        move-corresponding LS_MC_TOOL_GRID to LS_MC_TOOL_VIS.
        ME->SET_MC_TOOL_VIS( IR_PR_MC_TOOL  = LR_PR_MC_TOOL
                             IS_MC_TOOL_VIS = LS_MC_TOOL_VIS ).
      endloop.


      ME->R_GATE->RAISE_EVENTS( ).

    catch /VCXI/CX_CKX into LR_CX_CKX.
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

*** Request Refresh for ALV
  move ABAP_TRUE to ME->F_REFRESH_REQ.

endmethod.
method SET_MC_TOOL_VIS.

  data: LS_PRICE       type ZVCXI_XCS_S_PRICE,
        LS_PRICP       type ZVCXI_XCS_S_PRICP,
        LS_MC_TOOL_VIS type ZVCXI_FFSP_S_MC_TOOL_VIS.

*** Get Current data to do Update only if Value changed.
  LS_MC_TOOL_VIS = ME->GET_MC_TOOL_VIS( IR_PR_MC_TOOL = IR_PR_MC_TOOL ).

*** Material
  if LS_MC_TOOL_VIS-MATNR ne IS_MC_TOOL_VIS-MATNR.
    IR_PR_MC_TOOL->SET_MATNR( IF_MATNR = IS_MC_TOOL_VIS-MATNR
                              IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Average Consumption  & Consumption per
  if LS_MC_TOOL_VIS-AVGCO ne IS_MC_TOOL_VIS-AVGCO.
    IR_PR_MC_TOOL->SET_AVGCO( IS_AVGCO = IS_MC_TOOL_VIS-S_AVGCO
                              IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

  if LS_MC_TOOL_VIS-AVGCP ne IS_MC_TOOL_VIS-AVGCP.
    IR_PR_MC_TOOL->SET_AVGCP( IS_AVGCP = IS_MC_TOOL_VIS-S_AVGCP
                              IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Text
  if LS_MC_TOOL_VIS-TEXT ne IS_MC_TOOL_VIS-TEXT.
    IR_PR_MC_TOOL->SET_TEXT( IF_TEXT = IS_MC_TOOL_VIS-TEXT
                             IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

endmethod.
