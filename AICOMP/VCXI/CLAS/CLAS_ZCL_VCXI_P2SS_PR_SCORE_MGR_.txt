
class ZCL_VCXI_P2SS_PR_SCORE_MGR definition
  public
  inheriting from ZCL_VCXI_PXSS_PR_SCORE_MGR
  create public .

public section.

  methods GET_GRAIO
    returning
      value(RF_GRAIO) type ZVCXI_PFSS_GRAIO
    raising
      /VCXI/CX_CKX .

  methods GET_PRSTY
    redefinition .
  methods GET_PRSTY_ELEMENTS
    redefinition .
protected section.

  data R_CUST_ECMA type ref to ZCL_VCXI_PFSS_CUST_ECMA .

  methods GET_BOXPA
    redefinition .
  methods GET_CUST_SCORES
    redefinition .
  methods GET_JOIPO
    redefinition .
  methods HANDLE_INIT_CUST_PRSTY
    redefinition .
private section.
endclass. "ZCL_VCXI_P2SS_PR_SCORE_MGR definition
class ZCL_VCXI_P2SS_PR_SCORE_MGR implementation.
  method GET_BOXPA.

    case type of ME->R_GATE->GET_PARENT( )->R_CLSPR.
***   Folding Carton FG
      when type ZCL_VCXI_P2SS_PR_FCFG into data(LR_PR_FCFG).
        RF_BOXPA = LR_PR_FCFG->GET_BOXPA( ).

***   Folding Carton Part
      when type ZCL_VCXI_P2SS_PR_FCPART into data(LR_PR_FCPART).
        RF_BOXPA = LR_PR_FCPART->GET_BOXPA( ).

***   Folding Carton Procurement Details
      when type ZCL_VCXI_P2SS_PR_FCPRDTL into data(LR_PR_FCPRDTL).
        RF_BOXPA = LR_PR_FCPRDTL->GET_BOXPA( ).

    endcase.

  endmethod.
  method GET_CUST_SCORES.

    data: LF_PANOR type ZVCXI_P0SS_PANOR,
          LF_WERKS type WERKS_D,
          LF_PTYPE type ZVCXI_P0S_PTYPE,
          LF_JOIPO type ZVCXI_P0S_JOIPO,
          LF_BOXPA type ZVCXI_P0SS_BOXPA,
          LF_FLUTE type ZVCXI_PCS_FLUTE,
          LF_FLUTO type ZVCXI_PCSS_FLUTO,
          LF_DCONF type DATS.
    data: LT_ELEMENT type ZVCXI_P0SS_TT_ELEMENT,
          LT_SCORE   type ZVCXI_P0SS_TT_SCORE.
    field-symbols: <S_SCORE> type ZVCXI_PXSS_S_SCORE.


    if ME->R_CUST_PRSTY is not bound.
      move ABAP_TRUE to EF_CHANGED.
      exit.
    endif.

***----------------------------------------------------------------------------
*** Collect generall Data
    LF_BOXPA = ME->GET_BOXPA( ).
    LF_PANOR = ME->GET_PANOR( ).
    LF_WERKS = ME->GET_WERKS( ).

*** Joint Position and Product Type
    LF_JOIPO = ME->GET_JOIPO( ).
    LF_PTYPE = ME->GET_PTYPE( ).
    LF_DCONF = ME->GET_DCONF( ).

*** Collect Elements
    LT_ELEMENT = ME->GET_PRSTY_ELEMENTS( ).

***----------------------------------------------------------------------------
*** Request Scores
    ME->R_CUST_ECMA->GET_ECMA_SCORES( exporting IF_BOXPA   = LF_BOXPA
                                                IF_PANOR   = LF_PANOR
                                                IF_WERKS   = LF_WERKS
                                                IF_PTYPE   = LF_PTYPE
                                                IF_JOIPO   = LF_JOIPO
                                                IF_GRAIO   = ME->GET_GRAIO( )
                                                IF_DATE    = LF_DCONF
                                                IT_ELEMENT = LT_ELEMENT
                                      importing ET_SCORE   = LT_SCORE
                                                EF_CHANGED = EF_CHANGED ).

    move-corresponding LT_SCORE to ET_SCORE.

*** Get Slot Indicators of each Score
    loop at ET_SCORE assigning <S_SCORE>.
      <S_SCORE>-FSLIN = ME->R_CUST_PRSTY->GET_FSLIN( IF_CSCOR = <S_SCORE>-CSCOR
                                                     IF_NSCOR = <S_SCORE>-NSCOR ).
    endloop.

***----------------------------------------------------------------------------
*** Identify change
    if ET_SCORE ne ME->T_SCORE.
      EF_CHANGED = ABAP_TRUE.
    endif.

  endmethod.
  method GET_GRAIO.

    case type of ME->R_GATE->GET_PARENT( )->R_CLSPR.
***   Folding Carton FG
      when type ZCL_VCXI_P2SS_PR_FCFG into data(LR_PR_FCFG).
        RF_GRAIO = LR_PR_FCFG->GET_GRAIO( ).

***   Folding Carton Part
      when type ZCL_VCXI_P2SS_PR_FCPART into data(LR_PR_FCPART).
        RF_GRAIO = LR_PR_FCPART->GET_GRAIO( ).

***   Folding Carton Procurement Details
      when type ZCL_VCXI_P2SS_PR_FCPRDTL into data(LR_PR_FCPRDTL).
        RF_GRAIO = LR_PR_FCPRDTL->GET_GRAIO( ).

    endcase.

  endmethod.
  method GET_JOIPO.

    case type of ME->R_GATE->GET_PARENT( )->R_CLSPR.
***   Folding Carton FG
      when type ZCL_VCXI_P2SS_PR_FCFG into data(LR_PR_FCFG).
        RF_JOIPO = LR_PR_FCFG->GET_JOIPO( ).

***   Folding Carton Part
      when type ZCL_VCXI_P2SS_PR_FCPART into data(LR_PR_FCPART).
        RF_JOIPO = LR_PR_FCPART->GET_JOIPO( ).

***   Folding Carton Procurement Details
      when type ZCL_VCXI_P2SS_PR_FCPRDTL into data(LR_PR_FCPRDTL).
        RF_JOIPO = LR_PR_FCPRDTL->GET_JOIPO( ).

    endcase.

  endmethod.
  method GET_PRSTY.

    case type of ME->R_GATE->GET_PARENT( )->R_CLSPR.
***   Folding Carton FG
      when type ZCL_VCXI_P2SS_PR_FCFG into data(LR_PR_FCFG).
        RF_PRSTY = LR_PR_FCFG->GET_ECMA( importing ER_CUST_PRSTY = ER_CUST_PRSTY ).

***   Folding Carton Part
      when type ZCL_VCXI_P2SS_PR_FCPART into data(LR_PR_FCPART).
        RF_PRSTY = LR_PR_FCPART->GET_ECMA( importing ER_CUST_PRSTY = ER_CUST_PRSTY ).

***   Folding Carton Procurement Details
      when type ZCL_VCXI_P2SS_PR_FCPRDTL into data(LR_PR_FCPRDTL).
        RF_PRSTY = LR_PR_FCPRDTL->GET_ECMA( importing ER_CUST_PRSTY = ER_CUST_PRSTY ).

    endcase.

  endmethod.
  method GET_PRSTY_ELEMENTS.

    case type of ME->R_GATE->GET_PARENT( )->R_CLSPR.
***   Folding Carton FG
      when type ZCL_VCXI_P2SS_PR_FCFG into data(LR_PR_FCFG).
        RT_ELEMENT = LR_PR_FCFG->GET_PRSTY_ELEMENTS( ).

***   Folding Carton Part
      when type ZCL_VCXI_P2SS_PR_FCPART into data(LR_PR_FCPART).
        RT_ELEMENT = LR_PR_FCPART->GET_PRSTY_ELEMENTS( ).

***   Folding Carton Procurement Details
      when type ZCL_VCXI_P2SS_PR_FCPRDTL into data(LR_PR_FCPRDTL).
        RT_ELEMENT = LR_PR_FCPRDTL->GET_PRSTY_ELEMENTS( ).

    endcase.

  endmethod.
  method HANDLE_INIT_CUST_PRSTY.

    data: LF_ECMA  type        ZVCXI_PFSS_ECMA,
          LF_OWNER type        /VCXI/CKX_STRING,
          LF_SYSOM type        /VCXI/VKS_SYSOM.

    LF_ECMA = ME->GET_PRSTY( ).

    move ME->R_GATE->F_GATKY to LF_OWNER.
    ME->R_GATE->GET_CONFIG_INFO( importing EF_SYSOM = LF_SYSOM ).

*** Reload Scores from scratch on change of Product Style with automatic calculation logic (reset defaults)
    if ME->R_CUST_PRSTY is bound and
       ME->R_CUST_PRSTY->F_PRSTY ne LF_ECMA and
       ZCL_VCXI_P0SS_CUST_PRSTY=>GET_SSCOR_CALC( IF_SSCOR = ME->GET_SSCOR( ) ) eq '1'.  "Automatic
      ME->DEL_ALL_SCORES( ).
      clear: ME->T_SCORE.
    endif.

*** Check if Customizing is the right one
    if ME->R_CUST_PRSTY is not bound or
       ME->R_CUST_PRSTY->F_PRSTY ne LF_ECMA or
       ME->R_CUST_PRSTY->F_OWNER ne LF_OWNER or
       ME->R_CUST_PRSTY->F_SYSOM ne LF_SYSOM.

      free: ME->R_CUST_PRSTY, ME->R_CUST_ECMA.

      if LF_ECMA is not initial.

        try.
            ME->R_CUST_ECMA = ZCL_VCXI_PFSS_CUST_ECMA=>GET_ECMA_INSTANCE( IF_ECMA  = LF_ECMA
                                                                          IF_OWNER = LF_OWNER
                                                                          IF_SYSOM = LF_SYSOM ).

            move ME->R_CUST_ECMA to ME->R_CUST_PRSTY.

          catch /VCXI/CX_CKX.
            free: ME->R_CUST_PRSTY, ME->R_CUST_ECMA.
        endtry.
      endif.
    endif.

  endmethod.
