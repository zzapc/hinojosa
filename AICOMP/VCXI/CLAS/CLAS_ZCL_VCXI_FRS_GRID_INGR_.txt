
class ZCL_VCXI_FRS_GRID_INGR definition
  public
  inheriting from ZCL_VCXI_XCS_GRID_PRVDR
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABNAME type TABNAME default 'ZVCXI_FRS_S_INGR_GRID'
      !IF_CONTAINER_NAME type SCRFNAME
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL default ABAP_TRUE
    raising
      /VCXI/CX_CKX .
protected section.

  methods GET_INGR_VIS
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    returning
      value(RS_INGR_VIS) type ZVCXI_FRS_S_INGR_VIS
    raising
      /VCXI/CX_CKX .
  methods SET_INGR_VIS
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IS_INGR_VIS type ZVCXI_FRS_S_INGR_VIS
    raising
      /VCXI/CX_CKX .

  methods GET_FIELDCAT
    redefinition .
  methods GET_GRID_DATA
    redefinition .
  methods HANDLE_DATA_CHANGED_FINISHED
    redefinition .
private section.
endclass. "ZCL_VCXI_FRS_GRID_INGR definition
class ZCL_VCXI_FRS_GRID_INGR implementation.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_GATE           = IR_GATE
                        IR_UIGATE         = IR_UIGATE
                        IF_TABNAME        = IF_TABNAME
                        IF_CONTAINER_NAME = IF_CONTAINER_NAME
                        IF_APPL_EVENTS    = IF_APPL_EVENTS ).

*** Define Provider Type for Button
    ME->F_IPRVT = ZCL_VCXI_FRS_PR_INGR=>C_IPRVT_INGR.

  endmethod.
  method GET_FIELDCAT.

    data: LF_LOCKED type          /VCXI/CKX_LOCKED,
          LF_INUSE  type          /VCXI/VKSR_INUSE.
    field-symbols: <S_FCAT> type          LVC_S_FCAT.

***----------------------------------------------------------------------------
*** Get Standard Field Catalog
    RT_FCAT = SUPER->GET_FIELDCAT( ).

*** Get Lock and In-Use Status
    LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
    LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***----------------------------------------------------------------------------
*** Adjust Field Catalog
    loop at RT_FCAT assigning <S_FCAT>.
      case <S_FCAT>-FIELDNAME.
        when 'POSID' or 'GATKY' or 'R_GATE' or 'T_STYL'.
          move ABAP_TRUE    to <S_FCAT>-TECH.
          move 0            to <S_FCAT>-COL_POS.

        when 'MATNR'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE  to <S_FCAT>-EDIT.
          endif.

        when 'INRAT'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE  to <S_FCAT>-EDIT.
          endif.

        when 'INDEN_UOM'.
          move ABAP_TRUE to <S_FCAT>-NO_OUT.
      endcase.
    endloop.

  endmethod.
  method GET_GRID_DATA.

    data:          LS_INGR_GRID         type          ZVCXI_FRS_S_INGR_GRID.
    data:          LT_GATE              type          /VCXI/VKSR_TT_GATE.
    data:          LR_GATE              type ref to   /VCXI/CL_VKSR_GATE.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type          ANY.

***--------------------------------------------------------------------------------------
*** Dereferencing of Data Reference
    assign IR_DATA->* to <T_DATA>.

*** Get Gates of Ingredient
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_FRS_PR_INGR=>C_IPRVT_INGR
                                     IF_INUSE  = ABAP_TRUE
                                     IF_SORTED = ABAP_TRUE ).

    loop at LT_GATE into LR_GATE.
      clear: LS_INGR_GRID.

***--------------------------------------------------------------------------------------
***   Get Gate Data
      move LR_GATE->GET_POSID( ) to LS_INGR_GRID-POSID.
      move LR_GATE->F_GATKY      to LS_INGR_GRID-GATKY.
      move LR_GATE               to LS_INGR_GRID-R_GATE.

***   Get Visualization Data
      move ME->GET_INGR_VIS( IR_GATE = LR_GATE ) to LS_INGR_GRID-S_INGR_VIS.

***--------------------------------------------------------------------------------------
***   Add Entry
      append initial line to <T_DATA> assigning <S_DATA>.
      move-corresponding LS_INGR_GRID to <S_DATA>.

    endloop.

  endmethod.
  method GET_INGR_VIS.

    data:  LR_PR_INGR         type ref to  ZCL_VCXI_FRS_PR_INGR.

***--------------------------------------------------------------------------------------
*** Get PR Class
    try.
        move IR_GATE->R_CLSPR ?to LR_PR_INGR.
      catch CX_SY_MOVE_CAST_ERROR.
        clear: LR_PR_INGR.
    endtry.
    check LR_PR_INGR is bound.

***--------------------------------------------------------------------------------------
*** Material
    LR_PR_INGR->GET_MATNR( importing EF_MATNR       = RS_INGR_VIS-MATNR
                                     EF_MATNR_DESCR = RS_INGR_VIS-MATNR_DESCR ).

*** Ingredient Ratio
    LR_PR_INGR->GET_INRAT( importing ES_INRAT = RS_INGR_VIS-S_INRAT ).

*** Ingredient Density
    LR_PR_INGR->GET_INDEN( importing ES_INDEN = RS_INGR_VIS-S_INDEN ).
    move RS_INGR_VIS-INDEN_UOM to RS_INGR_VIS-INDEN_UOMC6.

  endmethod.
  method HANDLE_DATA_CHANGED_FINISHED.

    data:          LS_INGR_GRID         type          ZVCXI_FRS_S_INGR_GRID.
    data:          LR_CX_CKX            type ref to   /VCXI/CX_CKX.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type          ANY.

    check E_MODIFIED eq ABAP_TRUE.

    try.
        SUPER->HANDLE_DATA_CHANGED_FINISHED( E_MODIFIED    = E_MODIFIED
                                             ET_GOOD_CELLS = ET_GOOD_CELLS ).

***     Dereferencing of Data Reference
        assign ME->R_DATA->* to <T_DATA>.

        loop at <T_DATA> assigning <S_DATA>.
          move-corresponding <S_DATA> to LS_INGR_GRID.

          check LS_INGR_GRID-R_GATE is not initial.

***       Set Values to PR Class
          ME->SET_INGR_VIS( IR_GATE     = LS_INGR_GRID-R_GATE
                            IS_INGR_VIS = LS_INGR_GRID-S_INGR_VIS ).

        endloop.

        ME->R_GATE->RAISE_EVENTS( ).

      catch /VCXI/CX_CKX into LR_CX_CKX.
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

*** Request Refresh for ALV
    move ABAP_TRUE to ME->F_REFRESH_REQ.

  endmethod.
  method SET_INGR_VIS.

    data: LS_INGR_VIS type        ZVCXI_FRS_S_INGR_VIS.
    data: LR_PR_INGR  type ref to ZCL_VCXI_FRS_PR_INGR.

***--------------------------------------------------------------------------------------
*** Get PR Class
    try.
        move IR_GATE->R_CLSPR ?to LR_PR_INGR.
      catch CX_SY_MOVE_CAST_ERROR.
        clear: LR_PR_INGR.
    endtry.
    check LR_PR_INGR is bound.

*** Get old Values
    move ME->GET_INGR_VIS( IR_GATE = IR_GATE ) to LS_INGR_VIS.

***--------------------------------------------------------------------------------------
*** Material
    if IS_INGR_VIS-MATNR ne LS_INGR_VIS-MATNR.
      LR_PR_INGR->SET_MATNR( IF_MATNR = IS_INGR_VIS-MATNR
                             IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Ingridient Ratio
    if IS_INGR_VIS-S_INRAT-INRAT ne LS_INGR_VIS-INRAT.
      LR_PR_INGR->SET_INRAT( IS_INRAT = IS_INGR_VIS-S_INRAT
                             IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
