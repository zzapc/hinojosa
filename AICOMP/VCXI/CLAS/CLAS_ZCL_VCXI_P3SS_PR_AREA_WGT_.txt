
class ZCL_VCXI_P3SS_PR_AREA_WGT definition
  public
  inheriting from ZCL_VCXI_PXSS_PR_AREA_WGT
  create public .

public section.

  constants C_IPRVT_P3_AREA_WGT type /VCXI/VKSC_IPRVT value 'ZVCXI_P3SS_AREA_WGT' ##NO_TEXT.

  methods IS_CALC
    redefinition .
protected section.

  methods HANDLE_RECALC_AREA
    redefinition .
  methods HANDLE_RECALC_WEIGHT
    redefinition .
private section.
endclass. "ZCL_VCXI_P3SS_PR_AREA_WGT definition
class ZCL_VCXI_P3SS_PR_AREA_WGT implementation.
  method HANDLE_RECALC_AREA.

    data: LT_GATE  type /VCXI/VKSR_TT_GATE.
    data: LR_BL_ASFPROP type ref to ZCL_VCXI_P3SS_BL_ASFPROP.


***------------------------------------------------------------------------------------------------
*** Find Provider for Communication with Previous
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZIF_VCXI_P3SS_COMM_PREV=>C_IPRVT_COMM_PREV
                                     IF_INUSE = ABAP_TRUE ).
    if LT_GATE is not initial.
***   Get Assembly Forward Property XML (Inbound)
      LR_BL_ASFPROP = cast ZIF_VCXI_P3SS_COMM_PREV( LT_GATE[ 1 ]->R_CLSPR )->GET_ASFPX( ).
    endif.

***------------------------------------------------------------------------------------------------
*** Set Sold Blank Area
    ME->SET_SBLAR( IS_SBLAR = cond #( when LR_BL_ASFPROP is bound then LR_BL_ASFPROP->GET_SBLAR( )
                                                                  else value #( SBLAR_UOM = ME->GET_SBLAR( )-SBLAR_UOM ) ) ).

*** Set Cut Out Area
    ME->SET_COUAR( IS_COUAR = cond #( when LR_BL_ASFPROP is bound then LR_BL_ASFPROP->GET_COUAR( )
                                                                  else value #( COUAR_UOM = ME->GET_COUAR( )-COUAR_UOM ) ) ).


***------------------------------------------------------------------------------------------------
*** Calculate other values using SUPER
    SUPER->HANDLE_RECALC_AREA( ).

  endmethod.
  method HANDLE_RECALC_WEIGHT.

    data: LT_GATE  type /VCXI/VKSR_TT_GATE.
    data: LR_BL_ASFPROP type ref to ZCL_VCXI_P3SS_BL_ASFPROP.


***------------------------------------------------------------------------------------------------
*** Find Provider for Communication with Previous
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZIF_VCXI_P3SS_COMM_PREV=>C_IPRVT_COMM_PREV
                                     IF_INUSE = ABAP_TRUE ).
    if LT_GATE is not initial.
***   Get Assembly Forward Property XML (Inbound)
      LR_BL_ASFPROP = cast ZIF_VCXI_P3SS_COMM_PREV( LT_GATE[ 1 ]->R_CLSPR )->GET_ASFPX( ).
    endif.

***------------------------------------------------------------------------------------------------
*** Set Sold Blank Weight
    ME->SET_SBLWT( IS_SBLWT = cond #( when LR_BL_ASFPROP is bound then LR_BL_ASFPROP->GET_SBLWT( )
                                                                  else value #( SBLWT_UOM = ME->GET_SBLWT( )-SBLWT_UOM ) ) ).

*** Set Cut Out Weight
    ME->SET_COUWT( IS_COUWT = cond #( when LR_BL_ASFPROP is bound then LR_BL_ASFPROP->GET_COUWT( )
                                                                  else value #( COUWT_UOM = ME->GET_COUWT( )-COUWT_UOM ) ) ).

*** Set Additional Weight
    ME->SET_ADDWT( IS_ADDWT = cond #( when LR_BL_ASFPROP is bound then LR_BL_ASFPROP->GET_ADDWT( )
                                                                  else value #( ADDWT_UOM = ME->GET_ADDWT( )-ADDWT_UOM ) ) ).

***------------------------------------------------------------------------------------------------
*** Calculate other values using SUPER
    SUPER->HANDLE_RECALC_WEIGHT( ).

  endmethod.
  method IS_CALC.

*** Always calculated
    RF_CALC = ABAP_TRUE.

  endmethod.
endclass. "ZCL_VCXI_P3SS_PR_AREA_WGT implementation
