
class ZCL_VCXI_P1SS_UI_CSTEP_CLOS definition
  public
  inheriting from ZCL_VCXI_P1SS_UI_CSTEP
  create public .

public section.

  data S_CSTEP_CLOS_VIS type ZVCXI_P1SS_S_CSTEP_CLOS_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
protected section.

  data R_GRID_JOINT type ref to ZCL_VCXI_PXSS_GRID_JOINT .
  data R_PR_CSTEP_CLOS type ref to ZCL_VCXI_P1SS_PR_CSTEP_CLOS .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_P1SS_UI_CSTEP_CLOS definition
class ZCL_VCXI_P1SS_UI_CSTEP_CLOS implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

***--------------------------------------------------------------------------------------
*** Bind UI to Dynpro
    call function 'ZVCXI_P1SS_UI_CSTEP_CLOS_BIND'
      exporting
        IR_UI_CSTEP_CLOS = ME
      importing
        ES_DYNP          = ME->S_DYNP.

***--------------------------------------------------------------------------------------
*** Create Grid for GRID_SCORE
    create object ME->R_GRID_JOINT
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_JOINT'.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_CSTEP_CLOS.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

    if ME->R_GRID_JOINT is bound.
      ME->R_GRID_JOINT->DO_FREE( ).
      clear ME->R_GRID_JOINT.
    endif.

  endmethod.
  method DO_MODIFY_SCREEN.

    data: LF_CADLS type ZVCXI_PXS_CADLS.


    try.
        SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                 IF_DYNNR = IF_DYNNR ).

***     Get CAD Lock Status
        LF_CADLS = ME->R_PR_CSTEP_CLOS->GET_CADLS( ).

***----------------------------------------------------------------------------
***     Modify Screen
        loop at screen.
***       Handling Dimension changeable if Override Flag is set
          if SCREEN-GROUP1 eq 'OHD'.
            if ME->S_CSTEP_CLOS_VIS-FOVRR_HNDDIM eq ABAP_FALSE.
              move 0 to SCREEN-INPUT.
            endif.
          endif.

***       Adjust based on CAD Lock Status
          if SCREEN-GROUP4 eq 'CAD' and
             ( LF_CADLS eq ZCL_VCXI_PXSS_PR_CADIF_PRJ=>C_CADLS_REVISION or
               LF_CADLS eq ZCL_VCXI_PXSS_PR_CADIF_PRJ=>C_CADLS_LOCKED ).
            move 0 to SCREEN-INPUT.
          endif.

          modify screen.
        endloop.

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Super PBO
    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize GRID
    if ME->R_GRID_JOINT is bound.
      ME->R_GRID_JOINT->F_CADLS       = ME->R_PR_CSTEP_CLOS->GET_CADLS( ).
      ME->R_GRID_JOINT->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_JOINT->REFRESH_GRID( ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_CSTEP_CLOS_VIS type ZVCXI_P1SS_S_CSTEP_CLOS_VIS.


*** Call SUPER
    SUPER->DO_UPDATE_VIS( IS_CSTEP_VIS = IS_CSTEP_VIS ).

    check ME->R_PR_CSTEP_CLOS is bound.

*** "Cast" Vis Structure
    move-corresponding IS_CSTEP_VIS to LS_CSTEP_CLOS_VIS.

***--------------------------------------------------------------------------------------
*** Set Sheet Direction
    if ME->S_CSTEP_CLOS_VIS-SHTDI ne LS_CSTEP_CLOS_VIS-SHTDI.
      ME->R_PR_CSTEP_CLOS->SET_SHTDI( IF_SHTDI = LS_CSTEP_CLOS_VIS-SHTDI
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Update Handling Dimension if it is "Overriden"
    if ME->S_CSTEP_CLOS_VIS-FOVRR_HNDDIM eq ABAP_TRUE and
       ME->S_CSTEP_CLOS_VIS-S_HNDDIM ne LS_CSTEP_CLOS_VIS-S_HNDDIM.
      ME->R_PR_CSTEP_CLOS->SET_HNDDIM( IS_HNDDIM = LS_CSTEP_CLOS_VIS-S_HNDDIM
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Flag Override Handling Dimension
    if ME->S_CSTEP_CLOS_VIS-FOVRR_HNDDIM ne LS_CSTEP_CLOS_VIS-FOVRR_HNDDIM.
      ME->R_PR_CSTEP_CLOS->SET_HNDDIM( IF_FOVRR = LS_CSTEP_CLOS_VIS-FOVRR_HNDDIM ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    if ME->R_GRID_JOINT is bound.
      ME->R_GRID_JOINT->CHECK_CHANGED_DATA( ).
    endif.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_CSTEP_CLOS.

  endmethod.
  method INIT_VIS.

    SUPER->INIT_VIS( ).

    free: ME->S_CSTEP_CLOS_VIS.
    ME->S_CSTEP_CLOS_VIS-S_CSTEP_VIS = ME->S_CSTEP_VIS.

***--------------------------------------------------------------------------------------
*** Sheet Direction
    ME->S_CSTEP_CLOS_VIS-SHTDI = ME->R_PR_CSTEP_CLOS->GET_SHTDI( ).

*** Handling Dimension
    ME->S_CSTEP_CLOS_VIS-S_HNDDIM = ME->R_PR_CSTEP_CLOS->GET_HNDDIM( importing EF_FOVRR = ME->S_CSTEP_CLOS_VIS-FOVRR_HNDDIM ).


*** Set Parameter for Product Style Type and Product Style
    data(LF_PRSTY) = ME->R_PR_CSTEP_CLOS->ZIF_VCXI_P1SS_COMM_NEXT~GET_CRBPX( )->F_FEFCO.
    set parameter id 'ZVCXI_P0SS_TPRST' field 'FEFCO'.
    set parameter id 'ZVCXI_P0SS_PRSTY' field LF_PRSTY.

  endmethod.
