
class ZCL_VCXI_P0SP_UI_ACTVY_LAMI definition
  public
  inheriting from /VCXI/CL_VKSRU_CLSUI
  create public .

public section.

  data S_ACTVY_LAMI_VIS type ZVCXI_P0SP_S_ACTVY_LAMI_VIS .
  data F_CURSOR type SCRFNAME .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_ACTVY_LAMI_VIS
    importing
      !IS_ACTVY_LAMI_VIS type ZVCXI_P0SP_S_ACTVY_LAMI_VIS
    raising
      /VCXI/CX_CKX .
  methods SET_CURSOR
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
protected section.

  data R_PR_ACTVY_LAMI type ref to ZCL_VCXI_P0SP_PR_ACTVY_LAMI .

  methods INIT_VIS
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
private section.
endclass. "ZCL_VCXI_P0SP_UI_ACTVY_LAMI definition
class ZCL_VCXI_P0SP_UI_ACTVY_LAMI implementation.
method ACTIVATE.

  SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
  call function 'ZVCXI_P0SP_UI_ACTVY_LAMI_BIND'
    exporting
      IR_UI_ACTVY_LAMI = ME
    importing
      ES_DYNP          = ME->S_DYNP.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                      IR_UIGATE = IR_UIGATE
                      IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
  try.
      move IR_GATE->R_CLSPR ?to ME->R_PR_ACTVY_LAMI.
    catch CX_SY_MOVE_CAST_ERROR.
***   UI ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '801'.
  endtry.

endmethod.
method DO_MODIFY_SCREEN.

  data:  LF_LOCKED type        /VCXI/CKX_LOCKED,
         LF_INUSE  type        /VCXI/VKSR_INUSE.
  data:  LR_CX_CKX type ref to /VCXI/CX_CKX.

  SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                           IF_DYNNR = IF_DYNNR ).

  try.

      if IF_REPID eq ME->S_DYNP-REPID and
         IF_DYNNR eq ME->S_DYNP-DYNNR.

        LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
        LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***     Modify Scree using DynamicVC
        ZCL_VCXI_XCDR_SERVICE=>MODIFY_SCREEN( IR_GATE = ME->R_GATE ).

        loop at screen.
          if LF_LOCKED ne ABAP_TRUE or
             LF_INUSE  ne ABAP_TRUE.
            move 0 to SCREEN-INPUT.
          endif.
          modify screen.
        endloop.
      endif.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

endmethod.
method DO_PBO.

  ME->INIT_VIS( ).

  set cursor field ME->F_CURSOR.
  clear ME->F_CURSOR.

endmethod.
method DO_UPDATE_ACTVY_LAMI_VIS.

  data:  LS_PCLEN   type          ZVCXI_P0SP_S_PCLEN,
         LS_PCWID   type          ZVCXI_P0SP_S_PCWID,
         LS_PCHEI   type          ZVCXI_P0SP_S_PCHEI.

*** Set PC Length and Width
  if ME->S_ACTVY_LAMI_VIS-PCLEN ne IS_ACTVY_LAMI_VIS-PCLEN.
    move-corresponding IS_ACTVY_LAMI_VIS to LS_PCLEN.
    ME->R_PR_ACTVY_LAMI->SET_PCLEN( IS_PCLEN = LS_PCLEN
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

  if ME->S_ACTVY_LAMI_VIS-PCWID ne IS_ACTVY_LAMI_VIS-PCWID.
    move-corresponding IS_ACTVY_LAMI_VIS to LS_PCWID.
    ME->R_PR_ACTVY_LAMI->SET_PCWID( IS_PCWID = LS_PCWID
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

  if ME->S_ACTVY_LAMI_VIS-PCHEI ne IS_ACTVY_LAMI_VIS-PCHEI.
    move-corresponding IS_ACTVY_LAMI_VIS to LS_PCHEI.
    ME->R_PR_ACTVY_LAMI->SET_PCHEI( IS_PCHEI = LS_PCHEI
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Set PC Type
  if ME->S_ACTVY_LAMI_VIS-PCTYP ne IS_ACTVY_LAMI_VIS-PCTYP.
    ME->R_PR_ACTVY_LAMI->SET_PCTYP( IF_PCTYP = IS_ACTVY_LAMI_VIS-PCTYP
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER  ).
  endif.

*** Set PC Direction
  if ME->S_ACTVY_LAMI_VIS-PCDIR ne IS_ACTVY_LAMI_VIS-PCDIR.
    ME->R_PR_ACTVY_LAMI->SET_PCDIR( IF_PCDIR = IS_ACTVY_LAMI_VIS-PCDIR
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER  ).
  endif.

*** Set PC Ups
  if ME->S_ACTVY_LAMI_VIS-PCUPS ne IS_ACTVY_LAMI_VIS-PCUPS.
    ME->R_PR_ACTVY_LAMI->SET_PCUPS( IF_PCUPS = IS_ACTVY_LAMI_VIS-PCUPS
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER  ).
  endif.

*** Set Number of Outs
  if ME->S_ACTVY_LAMI_VIS-NOOUT ne IS_ACTVY_LAMI_VIS-NOOUT.
    ME->R_PR_ACTVY_LAMI->SET_NOOUT( IF_NOOUT = IS_ACTVY_LAMI_VIS-NOOUT
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER  ).
  endif.

endmethod.
method HANDLE_GATE_FREED.

  SUPER->HANDLE_GATE_FREED( ).

  free: ME->R_PR_ACTVY_LAMI.

endmethod.
method INIT_VIS.

  data:          LS_PCLEN             type          ZVCXI_P0SP_S_PCLEN,
                 LS_PCWID             type          ZVCXI_P0SP_S_PCWID,
                 LS_PCHEI             type          ZVCXI_P0SP_S_PCHEI.

  free: ME->S_ACTVY_LAMI_VIS.

***----------------------------------------------------------------------------
*** Get PC Length
  move ME->R_PR_ACTVY_LAMI->GET_PCLEN( )       to LS_PCLEN.
  move-corresponding LS_PCLEN                  to ME->S_ACTVY_LAMI_VIS.

*** Get PC Width
  move ME->R_PR_ACTVY_LAMI->GET_PCWID( )       to LS_PCWID.
  move-corresponding LS_PCWID                  to ME->S_ACTVY_LAMI_VIS.

*** Get PC Height
  move ME->R_PR_ACTVY_LAMI->GET_PCHEI( )       to LS_PCHEI.
  move-corresponding LS_PCHEI                  to ME->S_ACTVY_LAMI_VIS.

*** Get PC Type and Description
  move ME->R_PR_ACTVY_LAMI->GET_PCTYP( )       to ME->S_ACTVY_LAMI_VIS-PCTYP.
  move ME->R_PR_ACTVY_LAMI->GET_PCTYP_DESCR( ) to ME->S_ACTVY_LAMI_VIS-PCTYP_DESCR.


*** Get PC Ups
  move ME->R_PR_ACTVY_LAMI->GET_PCUPS( )       to ME->S_ACTVY_LAMI_VIS-PCUPS.

*** Get PC Direction
  move ME->R_PR_ACTVY_LAMI->GET_PCDIR( )       to ME->S_ACTVY_LAMI_VIS-PCDIR.

*** Get No. of Outs
  move ME->R_PR_ACTVY_LAMI->GET_NOOUT( )       to ME->S_ACTVY_LAMI_VIS-NOOUT.

endmethod.
method SET_CURSOR.

  data:  LF_CURSOR type SCRFNAME.

  get cursor field LF_CURSOR.
  if LF_CURSOR is not initial.
    move LF_CURSOR to ME->F_CURSOR.
  endif.

endmethod.
