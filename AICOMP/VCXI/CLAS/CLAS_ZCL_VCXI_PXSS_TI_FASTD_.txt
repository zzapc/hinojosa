
class ZCL_VCXI_PXSS_TI_FASTD definition
  public
  inheriting from /VCXI/CL_VKSRU_CLSTI
  create public .

public section.

  methods /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ
    redefinition .
  methods /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL
    redefinition .
protected section.

  methods ADD_MENU_FDMV
    importing
      !IR_MENU type ref to CL_CTMENU
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_ADD_FDMV
    importing
      !IF_FCODE type SYUCOMM
    raising
      /VCXI/CX_CKX .
private section.

  constants C_UC_ADD_FDMV type UI_FUNC value 'ADD_FDMV' ##NO_TEXT.
endclass. "ZCL_VCXI_PXSS_TI_FASTD definition
class ZCL_VCXI_PXSS_TI_FASTD implementation.
  method /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ.

    SUPER->/VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ( IR_MENU = IR_MENU ).

*** Add Menu for Fast Data Variants
    IR_MENU->ADD_SEPARATOR( ).
    ME->ADD_MENU_FDMV( IR_MENU = IR_MENU ).

  endmethod.
  method /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL.

    SUPER->/VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL( IF_FCODE = IF_FCODE ).

*** Add Children
    if IF_FCODE cs C_UC_ADD_FDMV.
      ME->HANDLE_UC_ADD_FDMV( IF_FCODE = IF_FCODE ).
    endif.

  endmethod.
  method ADD_MENU_FDMV .

    data: LF_DISABLED type ABAP_BOOL.
    data: LS_PRVDR_DEF type /VCXI/VKSR_S_PRVDR_DEF,
          LS_FDMV      type ZVCXI_PXSS_S_FDMV.
    data: LT_PRVDR_DEF type /VCXI/VKSR_TT_PRVDR_DEF.
    data: LR_MENU type ref to CL_CTMENU.


    check ZCL_VCXI_PXSS_BL_FDMDL=>T_FDMV is not initial.

*** Get Disable-Status
    if ME->R_GATE->IS_LOCKED( ) ne ABAP_TRUE or
       ME->R_GATE->IS_INUSE( )  ne ABAP_TRUE.
      move ABAP_TRUE to LF_DISABLED.
    endif.


*** Get Provider Definitions
    LT_PRVDR_DEF = ME->R_GATE->GET_PRVDR_DEF( IF_IPRVT = ZIF_VCXI_PXSS_FG=>C_IPRVT_FG ).
    check LT_PRVDR_DEF is not initial.

***--------------------------------------------------------------------------------------
*** Multiple different Finished Goods
    if LINES( LT_PRVDR_DEF ) gt 1.
      sort LT_PRVDR_DEF by DESCR.
      loop at LT_PRVDR_DEF into LS_PRVDR_DEF.
***     Create Submenu
        create object LR_MENU.
        IR_MENU->ADD_SUBMENU( MENU     = LR_MENU
                              TEXT     = REPLACE( VAL = 'Add &1'(F21)
                                                  SUB = '&1'
                                                  WITH = LS_PRVDR_DEF-DESCR )
                              DISABLED = LF_DISABLED ).

***     Add Function for all Variants
        loop at ZCL_VCXI_PXSS_BL_FDMDL=>T_FDMV into LS_FDMV
                                              where FDMDL eq LS_PRVDR_DEF-ID.
          LR_MENU->ADD_FUNCTION( FCODE = C_UC_ADD_FDMV && '@' && LS_FDMV-FDMDL && '@' && LS_FDMV-IFDMV
                                 TEXT  = conv #( LS_FDMV-DESCR ) ).
        endloop.
      endloop.

***--------------------------------------------------------------------------------------
*** Single Finished Good
    else.
***   Add Function for all Variants
      loop at ZCL_VCXI_PXSS_BL_FDMDL=>T_FDMV into LS_FDMV
                                            where FDMDL eq LT_PRVDR_DEF[ 1 ]-ID.
        IR_MENU->ADD_FUNCTION( FCODE    = C_UC_ADD_FDMV && '@' && LS_FDMV-FDMDL && '@' && LS_FDMV-IFDMV
                               TEXT     = REPLACE( VAL = 'Add &1'(F21) SUB = '&1' WITH = LS_FDMV-DESCR )
                               DISABLED = LF_DISABLED ).
      endloop.
    endif.

  endmethod.
  method HANDLE_UC_ADD_FDMV .

    data: LF_FDMDL type ZVCXI_PXSS_FDMDL,
          LF_IFDMV type ZVCXI_PXSS_IFDMV.
    data: LR_GATE type ref to /VCXI/CL_VKSR_GATE.


*** Get Fast Data Model and Variant
    split IF_FCODE at '@' into data(DUMMY) LF_FDMDL LF_IFDMV.

*** Create Fast Data Variant
    ZCL_VCXI_PXSS_BL_FDMDL=>CREATE( IR_GATE_FASTD = ME->R_GATE
                                    IF_FDMDL      = LF_FDMDL
                                    IF_IFDMV      = LF_IFDMV ).

*** Open Fast Data Screen
    ME->OPEN_UI_SELF( ).

  endmethod.
