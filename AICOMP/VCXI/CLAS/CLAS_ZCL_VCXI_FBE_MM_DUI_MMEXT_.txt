
class ZCL_VCXI_FBE_MM_DUI_MMEXT definition
  public
  inheriting from ZCL_VCXI_XCA_MWB_CLDUI
  create public .

public section.

  data S_DUI_MMEXT_VIS type ZVCXI_FBE_MM_S_DUI_MMEXT_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to ZCL_VCXI_XCA_MWB_UIFW
      !IR_CLMAT type ref to ZCL_VCXI_XCA_MWB_CLMAT
    raising
      /VCXI/CX_CKX .

  methods DO_UPDATE_VIS
    redefinition .
protected section.

  methods INIT_VIS
    redefinition .
  methods MODIFY_SCREEN
    redefinition .
private section.
endclass. "ZCL_VCXI_FBE_MM_DUI_MMEXT definition
class ZCL_VCXI_FBE_MM_DUI_MMEXT implementation.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW  = IR_UIFW
                        IR_CLMAT = IR_CLMAT  ).

***----------------------------------------------------------------------------
*** Bind UI
    call function 'ZVCXI_FBE_MM_DUI_MMEXT_BIND'
      exporting
        IR_DUI_MMEXT = ME
      importing
        ES_DYNP      = ME->S_DYNP.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_DUI_MMEXT_VIS type ZVCXI_FBE_MM_S_DUI_MMEXT_VIS.


*** Call SUPER
    SUPER->DO_UPDATE_VIS( IS_DUI_VIS = IS_DUI_VIS ).

    check ME->R_CLMAT is bound.

*** "Cast" Vis Structure
    move-corresponding IS_DUI_VIS to LS_DUI_MMEXT_VIS.


***--------------------------------------------------------------------------------------
*** Material Updates
    data(LR_BOB_MAT) = ME->R_CLMAT->GET_BOB_MAT( ).
    if LR_BOB_MAT is bound.
***   Set Material Plant Status
      if ME->S_DUI_MMEXT_VIS-MMSTA ne LS_DUI_MMEXT_VIS-MMSTA.
        LR_BOB_MAT->S_PLANTDATA-PUR_STATUS = LS_DUI_MMEXT_VIS-MMSTA.
        ME->R_CLMAT->MARK_CHANGED( ).
      endif.
    endif.


***--------------------------------------------------------------------------------------
*** Material Master Extension Updates
    data(LR_MMEXT) = cast ZCL_VCXI_FBE_MM_MMEXT( cast ZCL_VCXI_XCE_MM_MAT_MMEXT( ME->R_CLMAT )->GET_MMEXT( ) ).
    if LR_MMEXT is bound.
      LR_MMEXT->SET_MMEXT( IS_MMEXT = LS_DUI_MMEXT_VIS-S_MMEXT ).
    endif.

  endmethod.
  method INIT_VIS.

    check ME->R_CLMAT is bound.

    SUPER->INIT_VIS( ).

    free: ME->S_DUI_MMEXT_VIS.
    ME->S_DUI_MMEXT_VIS = corresponding #( ME->S_DUI_VIS ).

***--------------------------------------------------------------------------------------
    data(LR_BOB_MAT) = ME->R_CLMAT->GET_BOB_MAT( ).
    if LR_BOB_MAT is bound.
***   Get Plant Material Status
      ME->S_DUI_MMEXT_VIS-MMSTA = LR_BOB_MAT->S_PLANTDATA-PUR_STATUS.
    endif.

***--------------------------------------------------------------------------------------
*** Get Material Master Extension
    data(LR_MMEXT) = cast ZCL_VCXI_FBE_MM_MMEXT( cast ZCL_VCXI_XCE_MM_MAT_MMEXT( ME->R_CLMAT )->GET_MMEXT( ) ).
    if LR_MMEXT is bound.
      ME->S_DUI_MMEXT_VIS-S_MMEXT = LR_MMEXT->S_MMEXT.
    endif.

  endmethod.
  method MODIFY_SCREEN.

    SUPER->MODIFY_SCREEN( ).

*** Check if Material has configuration
    data(LF_CONFIG) = ME->R_CLMAT->IS_CONFIGURABLE( ).

    loop at screen.
***   Material Extension is not changeable, if it has a configuration
      if SCREEN-GROUP2 eq 'EXT' and
         LF_CONFIG eq ABAP_TRUE.
        move 0 to SCREEN-INPUT.
      endif.

      modify screen.
    endloop.

  endmethod.
