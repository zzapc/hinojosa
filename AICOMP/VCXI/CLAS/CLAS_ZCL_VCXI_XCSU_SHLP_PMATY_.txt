
class ZCL_VCXI_XCSU_SHLP_PMATY definition
  public
  create public .

public section.

  type-pools ABAP .
  class-methods CALL_SHLP
    importing
      !IF_SHUNT type ZVCXI_XCSU_SHUNT
      !IF_ISVCE type ZVCXI_XCS_ISVCE optional
      !IF_DISPLAY type ABAP_BOOL default ABAP_FALSE
    returning
      value(RF_PMATY) type ZVCXI_XCSU_PMATY
    raising
      /VCXI/CX_CKX .
protected section.
private section.
endclass. "ZCL_VCXI_XCSU_SHLP_PMATY definition
class ZCL_VCXI_XCSU_SHLP_PMATY implementation.
method CALL_SHLP.

  data: LR_CUST_SHIPU  type ref to   ZCL_VCXI_XCSU_CUST_SHIPU.
  data: LT_PMATY_SHUNT type          ZVCXI_XCSU_TT_PMATY,
        LT_PMATY_ISVCE type          ZVCXI_XCSU_TT_PMATY,
        LT_PMATY       type          ZVCXI_XCSU_TT_PMATY,
        LT_RETURN      type table of DDSHRETVAL.
  data: LS_PMATY       type          ZVCXI_XCSU_S_PMATY,
        LS_RETURN      type          DDSHRETVAL.

  LR_CUST_SHIPU = ZCL_VCXI_XCSU_CUST_SHIPU=>GET_INSTANCE( ).

  check LR_CUST_SHIPU is bound.

  LT_PMATY_SHUNT = LR_CUST_SHIPU->GET_PMATY_FOR_SHUNT( IF_SHUNT = IF_SHUNT ).
  if IF_ISVCE is supplied.
    LT_PMATY_ISVCE = LR_CUST_SHIPU->GET_PMATY_FOR_ISVCE( IF_ISVCE = IF_ISVCE ).
  endif.

*** only return pmat. types that
  loop at LT_PMATY_SHUNT into LS_PMATY.
    if IF_ISVCE is supplied.
      read table LT_PMATY_ISVCE with key PMATY = LS_PMATY-PMATY transporting no fields.
      check SY-SUBRC eq 0.
    endif.
    append LS_PMATY to LT_PMATY.
  endloop.


  call function 'F4IF_INT_TABLE_VALUE_REQUEST'
    exporting
      RETFIELD        = 'PMATY'
      VALUE_ORG       = 'S'
      DISPLAY         = IF_DISPLAY
    tables
      VALUE_TAB       = LT_PMATY
      RETURN_TAB      = LT_RETURN
    exceptions
      PARAMETER_ERROR = 1
      NO_VALUES_FOUND = 2
      others          = 3.

  if SY-SUBRC eq 0.
    read table LT_RETURN index 1 into LS_RETURN.
    if SY-SUBRC eq 0.
      RF_PMATY = LS_RETURN-FIELDVAL.
    endif.
  endif.

endmethod.
endclass. "ZCL_VCXI_XCSU_SHLP_PMATY implementation
