
class ZCL_VCXI_FFSR_GRID_RTESEL definition
  public
  inheriting from ZCL_VCXI_XCSR_GRID_RTESEL
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_CONTAINER_NAME type SCRFNAME
      !IF_TABNAME type TABNAME default 'ZVCXI_FFSR_S_RTESEL_GRID'
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
    raising
      /VCXI/CX_CKX .
protected section.

  methods HANDLE_UC_RHICO
    importing
      !IS_ROW type LVC_S_ROW
    raising
      /VCXI/CX_CKX .
  methods GET_SYSOM
    returning
      value(RF_SYSOM) type /VCXI/VKS_SYSOM
    raising
      /VCXI/CX_CKX .

  methods GET_FIELDCAT
    redefinition .
  methods GET_GRID_DATA
    redefinition .
  methods HANDLE_HOTSPOT_CLICK
    redefinition .
private section.
endclass. "ZCL_VCXI_FFSR_GRID_RTESEL definition
class ZCL_VCXI_FFSR_GRID_RTESEL implementation.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_GATE           = IR_GATE
                        IF_CONTAINER_NAME = IF_CONTAINER_NAME
                        IF_TABNAME        = IF_TABNAME
                        IR_UIGATE         = IR_UIGATE ).

  endmethod.
  method GET_FIELDCAT.

    field-symbols: <S_FCAT>        type        LVC_S_FCAT.

    RT_FCAT = SUPER->GET_FIELDCAT( ).

***----------------------------------------------------------------------------
*** Change Field catalog attributes
    loop at RT_FCAT assigning <S_FCAT>.
      case <S_FCAT>-FIELDNAME.
***     Roll Handling Button
        when 'RHICO'.
          move ABAP_TRUE       to <S_FCAT>-HOTSPOT.
          move 'C'             to <S_FCAT>-JUST.
          move ABAP_TRUE       to <S_FCAT>-ICON.

        when 'RDIMU'.
          move ABAP_TRUE       to <S_FCAT>-NO_OUT.

      endcase.
    endloop.

  endmethod.
  method GET_GRID_DATA.

    data: LS_RTESEL_GRID type ZVCXI_FFSR_S_RTESEL_GRID,
          LS_RHNDL       type ZVCXI_FFSR_S_RHNDL.
    data: LT_RTE_PSTEP type /VCXI/VSMR_TT_RTE_PSTEP.
    data: LR_RTE_ARBPL type ref to /VCXI/CL_VSMR_RTE_ARBPL,
          LR_RTE_PSTEP type ref to /VCXI/CL_VSMR_RTE_PSTEP,
          LR_RHNDL     type ref to ZIF_VCXI_FFSR_RHNDL,
          LR_BL_PSTEP  type ref to ZCL_VCXI_FFSR_BL_PSTEP.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type          ANY.


    check ME->R_PR_RTESPV->R_RTESEL is bound.

*** Get Super Data
    SUPER->GET_GRID_DATA( IR_DATA = IR_DATA ).

*** Collect "last" Roll Handling of each machine
    loop at ME->R_PR_RTESPV->R_RTESEL->R_RTE_PRTE->T_RTE_ARBPL into LR_RTE_ARBPL.
      clear LR_RTE_PSTEP.
      loop at LR_RTE_ARBPL->T_RTE_PSTEP into LR_RTE_PSTEP
             where TABLE_LINE->R_BLOBJ is instance of ZIF_VCXI_FFSR_RHNDL.
      endloop.
      if LR_RTE_PSTEP is bound.
        append LR_RTE_PSTEP to LT_RTE_PSTEP.
      endif.
    endloop.

*** Dereferencing of Data Reference
    assign IR_DATA->* to <T_DATA>.

    loop at <T_DATA> assigning <S_DATA>.
      move-corresponding <S_DATA> to LS_RTESEL_GRID.

***--------------------------------------------------------------------------------------
***   Roll Handling of last Production Step per Work Center
      if LINE_EXISTS( LT_RTE_PSTEP[ TABLE_LINE = LS_RTESEL_GRID-R_RTE_PSTEP ] ).
***     Icon for Roll Handling Popup
        LS_RTESEL_GRID-RHICO = ICON_MODIFY.

***     Roll Handling Information
        move LS_RTESEL_GRID-R_RTE_PSTEP->R_BLOBJ ?to LR_RHNDL.
        if LR_RHNDL is bound.
          if LINES( LR_RHNDL->T_RHNDL ) eq 1.
            read table LR_RHNDL->T_RHNDL into LS_RHNDL index 1.
            if SY-SUBRC is initial.
              move: LS_RHNDL-RWSID   to LS_RTESEL_GRID-RWSID,
                    LS_RHNDL-RWDIR   to LS_RTESEL_GRID-RWDIR,
                    LS_RHNDL-EYEMP   to LS_RTESEL_GRID-EYEMP,
                    LS_RHNDL-RDMTA   to LS_RTESEL_GRID-RDMTA,
                    LS_RHNDL-S_RDITA to LS_RTESEL_GRID-S_RDITA,
                    LS_RHNDL-S_RLETA to LS_RTESEL_GRID-S_RLETA,
                    LS_RHNDL-S_RWGTA to LS_RTESEL_GRID-S_RWGTA.
            endif.
          endif.
        endif.
      else.
***     No HotSpot
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'RHICO'
                                    IF_STYLE     = CL_GUI_ALV_GRID=>MC_STYLE_HOTSPOT_NO
                          changing  CT_STYL      = LS_RTESEL_GRID-T_STYL ).
      endif.

      move-corresponding LS_RTESEL_GRID to <S_DATA>.
    endloop.

  endmethod.
  method GET_SYSOM.

    data: LR_APPDATA type ref to /VCXI/CL_VKSR_APPDATA,
          LR_CUPRFL  type ref to /VCXI/CL_VKSC_CUPRFL.

*** Get Application Data
    move ME->R_GATE->GET_APPDATA( ) to LR_APPDATA.

*** Get Instance of Configuration Profile
    LR_CUPRFL = /VCXI/CL_VKSC_CUPRFL=>GET_INSTANCE_BY_CMPRK( IS_CMPRK = LR_APPDATA->S_CMPRK ).
    check LR_CUPRFL is bound.

*** Get System of Measurement for production step plant
    RF_SYSOM = LR_CUPRFL->GET_SYSOM_BY_WERKS( IF_WERKS = LR_APPDATA->F_WERKS ).

  endmethod.
  method HANDLE_HOTSPOT_CLICK.

*** Call Super
    SUPER->HANDLE_HOTSPOT_CLICK( E_ROW_ID    = E_ROW_ID
                                 E_COLUMN_ID = E_COLUMN_ID
                                 ES_ROW_NO   = ES_ROW_NO ).

    try.
        case E_COLUMN_ID-FIELDNAME.
          when 'RHICO'.
            ME->HANDLE_UC_RHICO( IS_ROW = E_ROW_ID ).
        endcase.

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        LR_CX_CKX->SHOW_MESSAGE( ).
    endtry.

  endmethod.
  method HANDLE_UC_RHICO.

    data:  LR_RTE_ARBPL    type ref to   /VCXI/CL_VSMR_RTE_ARBPL.

*** Find Work Center
    LR_RTE_ARBPL = ME->GET_SELECTED_RTE_ARBPL( IS_ROW = IS_ROW ).
    check LR_RTE_ARBPL is bound.

*** Call Popup
    ZCL_VCXI_FFSR_POPUP_RHNDL=>START( IR_RTE_ARBPL = LR_RTE_ARBPL
                                      IF_LOCKED    = ABAP_FALSE
                                      IF_SYSOM     = ME->GET_SYSOM( ) ).

  endmethod.
