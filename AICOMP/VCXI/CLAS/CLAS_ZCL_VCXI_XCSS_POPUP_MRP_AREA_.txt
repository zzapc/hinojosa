
class ZCL_VCXI_XCSS_POPUP_MRP_AREA definition
  public
  create protected .

public section.

  constants C_UC_CLOSE type SYUCOMM value 'CLOSE' ##NO_TEXT.
  data R_GRID_MRP_AREA type ref to ZCL_VCXI_XCSS_GRID_MRP_AREA .

  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods DO_USER_COMMAND
    importing
      !IF_UCOMM type SYUCOMM
    raising
      /VCXI/CX_CKX .
  class-methods START
    importing
      !IF_LOCKED type /VCXI/CKX_LOCKED optional
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
protected section.

  data F_LOCKED type /VCXI/CKX_LOCKED .
  data F_REFRESH_REQ type ABAP_BOOL .
  data R_CONTAINER type ref to CL_GUI_CUSTOM_CONTAINER .
  data R_GATE type ref to /VCXI/CL_VKSR_GATE .
  data R_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE .

  methods CONSTRUCTOR
    importing
      !IF_LOCKED type /VCXI/CKX_LOCKED
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
  methods DO_FREE
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_CLOSE
    raising
      /VCXI/CX_CKX .
  methods SET_PF_STATUS
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_XCSS_POPUP_MRP_AREA definition
class ZCL_VCXI_XCSS_POPUP_MRP_AREA implementation.
  method CONSTRUCTOR.

    move IR_GATE        to ME->R_GATE.
    move IR_UIGATE      to ME->R_UIGATE.
    move IF_LOCKED      to ME->F_LOCKED.

*** Create Grid for MRP Areas
    create object ME->R_GRID_MRP_AREA
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID'
        IF_APPL_EVENTS    = IF_APPL_EVENTS.

  endmethod.
  method DO_FREE.

*** Free Grid
    if ME->R_GRID_MRP_AREA is bound.
      ME->R_GRID_MRP_AREA->DO_FREE( ).
      clear ME->R_GRID_MRP_AREA.
    endif.

  endmethod.
  method DO_PBO.

*** Set PF-Status
    ME->SET_PF_STATUS( ).

*** Refresh Grid
    if ME->R_GRID_MRP_AREA is bound.
      ME->R_GRID_MRP_AREA->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_MRP_AREA->REFRESH_GRID( ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    case IF_UCOMM.
      when C_UC_CLOSE.
        ME->HANDLE_UC_CLOSE( ).

    endcase.

  endmethod.
  method HANDLE_UC_CLOSE.

*** Raise Event
    ME->R_GATE->RAISE_EVENTS( ).

*** Leave Popup
    leave to screen 0.

  endmethod.
  method SET_PF_STATUS.

    set pf-status '0100' of program 'SAPLZVCXI_XCSS_POPUP_MRP_AREA'.

  endmethod.
  method START.

    data:  LR_POPUP_MRP_AREA type ref to ZCL_VCXI_XCSS_POPUP_MRP_AREA.


    check IR_GATE is bound.

*** Create Instance
    create object LR_POPUP_MRP_AREA
      exporting
        IR_GATE        = IR_GATE
        IR_UIGATE      = IR_UIGATE
        IF_LOCKED      = IF_LOCKED
        IF_APPL_EVENTS = IF_APPL_EVENTS.

*** Call Popup
    call function 'ZVCXI_XCSS_POPUP_MRP_AREA_STRT'
      exporting
        IR_POPUP_MRP_AREA = LR_POPUP_MRP_AREA.

*** Free the Popup
    LR_POPUP_MRP_AREA->DO_FREE( ).

  endmethod.
