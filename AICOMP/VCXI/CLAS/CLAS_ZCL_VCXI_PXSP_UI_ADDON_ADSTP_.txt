
class ZCL_VCXI_PXSP_UI_ADDON_ADSTP definition
  public
  inheriting from ZCL_VCXI_XCSP_UI_ADDON
  create public .

public section.

  data S_ADDON_ADSTP_VIS type ZVCXI_PXSP_S_ADDON_ADSTP_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  data R_PR_ADDON_ADSTP type ref to ZCL_VCXI_PXSP_PR_ADDON_ADSTP .
  data R_GRID_MC_ADMAT type ref to ZCL_VCXI_PXSP_GRID_MC_ADMAT .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_PXSP_UI_ADDON_ADSTP definition
class ZCL_VCXI_PXSP_UI_ADDON_ADSTP implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_PXSP_UI_ADDON_ADSTP_BIND'
      exporting
        IR_UI_ADDON_ADSTP = ME
      importing
        ES_DYNP           = ME->S_DYNP.

*** Create Grid for Additional Materials
    create object ME->R_GRID_MC_ADMAT
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_MC_ADMAT'.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_ADDON_ADSTP.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

    if ME->R_GRID_MC_ADMAT is bound.
      ME->R_GRID_MC_ADMAT->DO_FREE( ).
      clear ME->R_GRID_MC_ADMAT.
    endif.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Super PBO
    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize Grid
    if ME->R_GRID_MC_ADMAT is bound.
      ME->R_GRID_MC_ADMAT->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_MC_ADMAT->REFRESH_GRID( ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_ADDON_ADSTP_VIS type ZVCXI_PXSP_S_ADDON_ADSTP_VIS.


*** Super
    SUPER->DO_UPDATE_VIS( IS_VIS = IS_VIS ).
    check ME->R_PR_ADDON_ADSTP is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_ADDON_ADSTP_VIS.

***--------------------------------------------------------------------------------------
*** Set PC Length
    if ME->S_ADDON_ADSTP_VIS-S_PCLEN ne LS_ADDON_ADSTP_VIS-S_PCLEN.
      ME->R_PR_ADDON_ADSTP->SET_PCLEN( IS_PCLEN = LS_ADDON_ADSTP_VIS-S_PCLEN
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set PC Width
    if ME->S_ADDON_ADSTP_VIS-S_PCWID ne LS_ADDON_ADSTP_VIS-S_PCWID.
      ME->R_PR_ADDON_ADSTP->SET_PCWID( IS_PCWID = LS_ADDON_ADSTP_VIS-S_PCWID
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set PC Height
    if ME->S_ADDON_ADSTP_VIS-S_PCHEI ne LS_ADDON_ADSTP_VIS-S_PCHEI.
      ME->R_PR_ADDON_ADSTP->SET_PCHEI( IS_PCHEI = LS_ADDON_ADSTP_VIS-S_PCHEI
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    if ME->R_GRID_MC_ADMAT is bound.
      ME->R_GRID_MC_ADMAT->CHECK_CHANGED_DATA( ).
    endif.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_ADDON_ADSTP.

  endmethod.
  method INIT_VIS.

*** Super
    SUPER->INIT_VIS( ).

    free: ME->S_ADDON_ADSTP_VIS.
    ME->S_ADDON_ADSTP_VIS-S_ADDON_VIS = ME->S_ADDON_VIS.

***--------------------------------------------------------------------------------------
*** PC Dimension
    ME->S_ADDON_ADSTP_VIS-S_PCLEN = ME->R_PR_ADDON_ADSTP->GET_PCLEN( ).
    ME->S_ADDON_ADSTP_VIS-S_PCWID = ME->R_PR_ADDON_ADSTP->GET_PCWID( ).
    ME->S_ADDON_ADSTP_VIS-S_PCHEI = ME->R_PR_ADDON_ADSTP->GET_PCHEI( ).

  endmethod.
