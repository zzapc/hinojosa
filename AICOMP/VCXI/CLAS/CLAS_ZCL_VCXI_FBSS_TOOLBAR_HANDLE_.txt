
class ZCL_VCXI_FBSS_TOOLBAR_HANDLE definition
  public
  inheriting from ZCL_VCXI_FBSS_TOOLBAR_PRVDR
  create public .

public section.

  constants C_PRVDR_PARID type /VCXI/VKS_PARID value 'ZVCXI_FB_THNDL' ##NO_TEXT.
protected section.

  methods GET_MENU_ITEMS
    redefinition .
  methods SET_PRVDR_PARAM
    redefinition .
private section.
endclass. "ZCL_VCXI_FBSS_TOOLBAR_HANDLE definition
class ZCL_VCXI_FBSS_TOOLBAR_HANDLE implementation.
  method GET_MENU_ITEMS.

    data: LF_HANDLE     type /VCXI/CKX_ID.
    data: LS_MENU_ITEMS type /VCXI/VKSR_S_PRVDR_DEF,
          LS_TC110T     type ZVCXI_FBS_TC110T,
          LS_ATTRI      type /VCXI/VKS_S_ATTRI,
          LS_PRVDR_DEF  type /VCXI/VKSR_S_PRVDR_DEF.
    data: LT_HANDLE    type /VCXI/CKX_TT_ID,
          LT_PRVDR_DEF type /VCXI/VKSR_TT_PRVDR_DEF.
    data: LR_PR_BMAKE type ref to ZCL_VCXI_FBSS_PR_BMAKE,
          LR_CUST_BAG type ref to ZCL_VCXI_FBSS_CUST_BAG.


    clear ME->T_MENU_ITEMS.

*** Get Bag Making PR
    try.
        move ME->R_GATE->R_CLSPR ?to LR_PR_BMAKE.
      catch CX_SY_MOVE_CAST_ERROR.
        clear LR_PR_BMAKE.
    endtry.

    if LR_PR_BMAKE is bound.

***   Get Bag Customizing
      move LR_PR_BMAKE->GET_CUST_BAG( ) to LR_CUST_BAG.

***   Get Bag Type - Handle Types
      if LR_CUST_BAG is bound.
        LT_HANDLE = LR_CUST_BAG->GET_HANDLE( ).
      endif.

    endif.

    check LT_HANDLE is not initial.

    LT_PRVDR_DEF = ME->R_GATE->GET_PRVDR_DEF( IF_TPRVR = /VCXI/IF_VKSC_REGELMNT_PRVTY=>C_TPRVR_CHILD ).
    sort LT_PRVDR_DEF.
    delete adjacent duplicates from LT_PRVDR_DEF comparing ID.
    read table LT_PRVDR_DEF into LS_PRVDR_DEF with key ID = 'HANDLE'.

*** Create Menu Items list
    loop at LT_HANDLE into LF_HANDLE.
      clear LS_MENU_ITEMS.

      LS_MENU_ITEMS-ID    = LS_PRVDR_DEF-ID.
      LS_MENU_ITEMS-QTYMI = LS_PRVDR_DEF-QTYMI.
      LS_MENU_ITEMS-QTYMA = LS_PRVDR_DEF-QTYMA.

      read table ZCL_VCXI_FBSSC_BAGTY=>T_TC110T into LS_TC110T with key THNDL = LF_HANDLE.
      if SY-SUBRC = 0.
        move LS_TC110T-DESCR  to  LS_MENU_ITEMS-DESCR.
      else.
        move LF_HANDLE        to  LS_MENU_ITEMS-DESCR.
      endif.

      clear LS_ATTRI.
      move ME->C_PRVDR_PARID  to LS_ATTRI-ATTID.
      move LF_HANDLE          to LS_ATTRI-ATTVA.
      move 'IMPL'             to LS_ATTRI-ATTMO.
      append LS_ATTRI         to LS_MENU_ITEMS-T_ATTRI.
      append LS_MENU_ITEMS    to ME->T_MENU_ITEMS.

    endloop.

  endmethod.
  method SET_PRVDR_PARAM.

    data: LF_THNDL     type        ZVCXI_FBS_THNDL.
    data: LR_PR_HANDLE type ref to ZCL_VCXI_FBSS_PR_HANDLE.

    check IR_GATE is bound.
    try.
        move IR_GATE->R_CLSPR ?to LR_PR_HANDLE.
      catch CX_SY_MOVE_CAST_ERROR.
        clear LR_PR_HANDLE.
    endtry.
    check LR_PR_HANDLE is bound.

*** Set Handle Type
    move IF_PARAM_VAL to LF_THNDL.
    LR_PR_HANDLE->SET_THNDL( IF_THNDL = LF_THNDL ).

  endmethod.
endclass. "ZCL_VCXI_FBSS_TOOLBAR_HANDLE implementation
