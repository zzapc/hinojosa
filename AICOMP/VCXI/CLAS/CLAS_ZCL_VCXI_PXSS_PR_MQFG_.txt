
class ZCL_VCXI_PXSS_PR_MQFG definition
  public
  inheriting from /VCXI/CL_VKSR_CLSPR
  create public .

public section.

  constants C_IEVNT_O_QFACT_CHANGED type /VCXI/VKSC_IEVNT value 'QFACT_CHANGED' ##NO_TEXT.
  constants C_PARID_QFACT type /VCXI/VKS_PARID value 'ZVCXI_XC_QFACT' ##NO_TEXT.

  methods GET_QFACT
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RF_QFACT) type ZVCXI_XCS_QFACT
    raising
      /VCXI/CX_CKX .
  methods SET_QFACT
    importing
      !IF_QFACT type ZVCXI_XCS_QFACT
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .

  methods GET_STATUS
    redefinition .
  methods GET_STATUS_MSG
    redefinition .
protected section.

  methods CHECK_QFACT
    importing
      !IR_MESSAGE type ref to /VCXI/CL_CKXM_MESSAGE optional
    returning
      value(RF_ELMST) type /VCXI/VKSR_ELMST
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_PXSS_PR_MQFG definition
class ZCL_VCXI_PXSS_PR_MQFG implementation.
  method CHECK_QFACT.

*** Check Quantity Factor
    if ME->GET_QFACT( ) is not initial.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G.
      if IR_MESSAGE is bound.
***     Quantity Factor is maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P0SS'
                                 IF_MSGTY = 'S'
                                 IF_MSGNO = '909' ).
      endif.

    else.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
      if IR_MESSAGE is bound.
***     Quantity Factor is not maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P0SS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '910' ).
      endif.
    endif.

  endmethod.
  method GET_QFACT.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_QFACT ).
    RF_QFACT = LS_PARAM-ATFLV.
    EF_SETBY = LS_PARAM-SETBY.

  endmethod.
  method GET_STATUS.

*** Super Status
    RF_ELMST = SUPER->GET_STATUS( ).

***----------------------------------------------------------------------------
*** Check Quantity Factor
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = ME->CHECK_QFACT( ) ).

  endmethod.
  method GET_STATUS_MSG.

    SUPER->GET_STATUS_MSG( IR_MESSAGE = IR_MESSAGE ).

***----------------------------------------------------------------------------
*** Check Quantity Factor
    ME->CHECK_QFACT( IR_MESSAGE = IR_MESSAGE ).

  endmethod.
  method SET_QFACT.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_QFACT ).

    LS_PARAM-ATFLV = IF_QFACT.
    LS_PARAM-SETBY = IF_SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_QFACT_CHANGED ).
    endif.

  endmethod.
