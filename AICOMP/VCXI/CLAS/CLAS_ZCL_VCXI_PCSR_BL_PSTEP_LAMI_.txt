
class ZCL_VCXI_PCSR_BL_PSTEP_LAMI definition
  public
  inheriting from ZCL_VCXI_P0SR_BL_PSTEP_LAMI
  create public .

public section.

  methods COMPARE
    redefinition .
  methods COPY
    redefinition .
  methods DESERIALIZE
    redefinition .
  methods INIT_BY_SPEC
    redefinition .
  methods SERIALIZE
    redefinition .
protected section.

  data F_FLUTE type ZVCXI_PCS_FLUTE .

  methods INIT_BY_SPEC_BOARD
    importing
      !IT_PRVDR type /VCXI/VKSR_TT_GATE
    raising
      /VCXI/CX_CKX .
  methods INIT_BY_SPEC_SHTPR
    importing
      !IT_PRVDR type /VCXI/VKSR_TT_GATE
    raising
      /VCXI/CX_CKX .

  methods GET_MS_LGLUE
    redefinition .
private section.
endclass. "ZCL_VCXI_PCSR_BL_PSTEP_LAMI definition
class ZCL_VCXI_PCSR_BL_PSTEP_LAMI implementation.
method COMPARE.

  data:  LR_BL_PSTEP_LAMI type ref to ZCL_VCXI_PCSR_BL_PSTEP_LAMI.

*** Super Compare
  RF_SIMILAR = SUPER->COMPARE( IR_BL_PSTEP = IR_BL_PSTEP ).
  check RF_SIMILAR eq ABAP_TRUE.

  move IR_BL_PSTEP ?to LR_BL_PSTEP_LAMI.

***----------------------------------------------------------------------------
*** Flute Type
  if ME->F_FLUTE eq LR_BL_PSTEP_LAMI->F_FLUTE.
    move ABAP_TRUE  to RF_SIMILAR.
  else.
    move ABAP_FALSE to RF_SIMILAR.
    exit.
  endif.

endmethod.
method COPY.

  data:  LR_BL_PSTEP_LAMI type ref to ZCL_VCXI_PCSR_BL_PSTEP_LAMI.

*** Super Copy
  RR_BL_PSTEP = SUPER->COPY( ).
  move RR_BL_PSTEP ?to LR_BL_PSTEP_LAMI.

***----------------------------------------------------------------------------
*** Flute Type
  move ME->F_FLUTE       to LR_BL_PSTEP_LAMI->F_FLUTE.

endmethod.
method DESERIALIZE.

  data:  LF_XML   type STRING.

  check IF_XML is not initial.

*** Deserialize
  call transformation ID
       source xml IF_XML
       result SUPER_XML    = LF_XML

              F_FLUTE      = ME->F_FLUTE.

*** Super Deserialize
  SUPER->DESERIALIZE( IF_XML = LF_XML ).

endmethod.
method GET_MS_LGLUE.

  data:  LF_GUID     type        /VCXI/CKX_GUID,
         LF_WERKS    type        WERKS_D,
         LF_ARBPL    type        ARBPL.

*** Perform Super
  RS_MS_LGLUE = SUPER->GET_MS_LGLUE( IR_ARBPL = IR_ARBPL ).

  check ME->F_TLAGC eq ZCL_VCXI_P0SS_CUST_LAMI=>C_TLAGC_FLUTE.

*** Get Plant and WorkCenter
  move IR_ARBPL->/VCXI/IF_CKX_OBJECT~GET_GUID( ) to LF_GUID.
  /VCXI/CL_VSMC_ARBPL=>DECRYPT_GUID( exporting IF_GUID  = LF_GUID
                                     importing EF_WERKS = LF_WERKS
                                               EF_ARBPL = LF_ARBPL ).

*** Get Lamination Glue Consumption Factor
  RS_MS_LGLUE-CONSF = ZCL_VCXI_PCSS_CUST_LAMI=>GET_LAMI_GCONSF( IF_DATS  = ME->F_DATE
                                                                IF_FLUTE = ME->F_FLUTE
                                                                IF_ARBPL = LF_ARBPL
                                                                IF_WERKS = LF_WERKS ).

endmethod.
method INIT_BY_SPEC.

  SUPER->INIT_BY_SPEC( IR_GATE  = IR_GATE
                       IT_PRVDR = IT_PRVDR ).

***------------------------------------------------------------------------------------------------
*** Get Board Informations
  ME->INIT_BY_SPEC_BOARD( IT_PRVDR = IT_PRVDR ).

***------------------------------------------------------------------------------------------------
*** Get Sheet Procurement -> Flute
  ME->INIT_BY_SPEC_SHTPR( IT_PRVDR = IT_PRVDR ).

endmethod.
method INIT_BY_SPEC_BOARD.

  data:  LR_GATE           type ref to /VCXI/CL_VKSR_GATE,
         LR_PR_BOARD       type ref to ZCL_VCXI_PCSS_PR_BOARD.

***------------------------------------------------------------------------------------------------
*** Get Board Reference
  loop at IT_PRVDR into LR_GATE.
    try.
        move LR_GATE->R_CLSPR ?to LR_PR_BOARD.
        if LR_PR_BOARD is bound.
          exit.
        endif.
      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.
  endloop.
  check LR_PR_BOARD is bound.

***------------------------------------------------------------------------------------------------
  move LR_PR_BOARD->GET_FLUTE( ) to ME->F_FLUTE.

endmethod.
method INIT_BY_SPEC_SHTPR.

  data:  LR_GATE           type ref to /VCXI/CL_VKSR_GATE,
         LR_PR_MS_SHTPR    type ref to ZCL_VCXI_PCSS_PR_MS_SHTPR.

***------------------------------------------------------------------------------------------------
*** Get Board Reference
  loop at IT_PRVDR into LR_GATE.
    try.
        move LR_GATE->R_CLSPR ?to LR_PR_MS_SHTPR.
        if LR_PR_MS_SHTPR is bound.
          exit.
        endif.
      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.
  endloop.
  check LR_PR_MS_SHTPR is bound.

***------------------------------------------------------------------------------------------------
  move LR_PR_MS_SHTPR->GET_FLUTE( ) to ME->F_FLUTE.

endmethod.
method SERIALIZE.

*** Super Serialize
  RF_XML = SUPER->SERIALIZE( ).

*** Serialize
  call transformation ID
       source SUPER_XML    = RF_XML

              F_FLUTE      = ME->F_FLUTE

       result xml RF_XML.

endmethod.
