
class ZCL_VCXI_XCSU_SHLP_PMATN definition
  public
  create public .

public section.

  type-pools ABAP .
  class-methods CALL_SHLP
    importing
      !IF_WERKS type WERKS_D optional
      !IF_PMATY type ZVCXI_XCSU_PMATY
      !IF_DISPLAY type ABAP_BOOL default ABAP_FALSE
      !IF_ALL type ABAP_BOOL default ABAP_FALSE
    returning
      value(RF_PMATN) type ZVCXI_XCSU_PMATN
    raising
      /VCXI/CX_CKX .
protected section.
private section.
endclass. "ZCL_VCXI_XCSU_SHLP_PMATN definition
class ZCL_VCXI_XCSU_SHLP_PMATN implementation.
method CALL_SHLP.

  data: LR_CUST_SHIPU type ref to   ZCL_VCXI_XCSU_CUST_SHIPU.
  data: LT_PMATN      type          ZVCXI_XCSU_TT_PMATN,
        LT_RETURN     type table of DDSHRETVAL.
  data: LS_RETURN     type          DDSHRETVAL.

*** Call Standard Material Searchhelp
  if IF_ALL eq ABAP_TRUE.
    call function 'F4IF_FIELD_VALUE_REQUEST'
      exporting
        TABNAME           = 'MARA'
        FIELDNAME         = 'MATNR'
        DISPLAY           = IF_DISPLAY
      tables
        RETURN_TAB        = LT_RETURN
      exceptions
        FIELD_NOT_FOUND   = 1
        NO_HELP_FOR_FIELD = 2
        INCONSISTENT_HELP = 3
        NO_VALUES_FOUND   = 4
        others            = 5.

*** Call Customizing Searchhelp
  else.

    LR_CUST_SHIPU = ZCL_VCXI_XCSU_CUST_SHIPU=>GET_INSTANCE( ).

    check LR_CUST_SHIPU is bound.

    LT_PMATN = LR_CUST_SHIPU->GET_PMATN_FOR_PMATY( IF_WERKS = IF_WERKS
                                                   IF_PMATY = IF_PMATY ).
    call function 'F4IF_INT_TABLE_VALUE_REQUEST'
      exporting
        RETFIELD        = 'PMATN'
        VALUE_ORG       = 'S'
        DISPLAY         = IF_DISPLAY
      tables
        VALUE_TAB       = LT_PMATN
        RETURN_TAB      = LT_RETURN
      exceptions
        PARAMETER_ERROR = 1
        NO_VALUES_FOUND = 2
        others          = 3.
  endif.

  if SY-SUBRC <> 0.
    /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
  endif.

  if SY-SUBRC eq 0.
    read table LT_RETURN index 1 into LS_RETURN.
    if SY-SUBRC eq 0.
      RF_PMATN = LS_RETURN-FIELDVAL.

      call function 'CONVERSION_EXIT_MATN1_INPUT'
        exporting
          INPUT        = RF_PMATN
        importing
          OUTPUT       = RF_PMATN
        exceptions
          LENGTH_ERROR = 1.
      if SY-SUBRC ne 0.
        clear RF_PMATN.
      endif.
    endif.
  endif.

endmethod.
endclass. "ZCL_VCXI_XCSU_SHLP_PMATN implementation
