
class ZCL_VCXI_P2SS_UI_CSTEP_PRINT definition
  public
  inheriting from ZCL_VCXI_P2SS_UI_CSTEP
  create public .

public section.

  data S_CSTEP_PRINT_VIS type ZVCXI_P2SS_S_CSTEP_PRINT_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  constants C_UC_CREATE_DESDO type SYUCOMM value 'CREATE_DESDO' ##NO_TEXT.
  constants C_UC_LINK_DESDO type SYUCOMM value 'LINK_DESDO' ##NO_TEXT.
  data R_GRID_MARK type ref to ZCL_VCXI_PXSS_GRID_MARK .
  data R_GRID_MS_INK type ref to ZCL_VCXI_PXSS_GRID_MS_INK .
  data R_PR_CSTEP_PRINT type ref to ZCL_VCXI_P2SS_PR_CSTEP_PRINT .

  methods HANDLE_UC_CREATE_DESDO
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_LINK_DESDO
    raising
      /VCXI/CX_CKX .
  methods SET_DDVAL_DESDO
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_P2SS_UI_CSTEP_PRINT definition
class ZCL_VCXI_P2SS_UI_CSTEP_PRINT implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

***--------------------------------------------------------------------------------------
*** Bind UI to Dynpro
    call function 'ZVCXI_P2SS_UI_CSTEP_PRINT_BIND'
      exporting
        IR_UI_CSTEP_PRINT = ME
      importing
        ES_DYNP           = ME->S_DYNP.

***--------------------------------------------------------------------------------------
*** Create Grid for Inks
    create object ME->R_GRID_MS_INK
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_MS_INK'.

*** Create Grid for Marking
    create object ME->R_GRID_MARK
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_MARK'.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_CSTEP_PRINT.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

    if ME->R_GRID_MS_INK is bound.
      ME->R_GRID_MS_INK->DO_FREE( ).
      clear ME->R_GRID_MS_INK.
    endif.

    if ME->R_GRID_MARK is bound.
      ME->R_GRID_MARK->DO_FREE( ).
      clear ME->R_GRID_MARK.
    endif.

  endmethod.
  method DO_MODIFY_SCREEN.

    data: LF_PRTEC type ZVCXI_PXS_PRTEC,
          LF_RELEA type ABAP_BOOL.


    try.
        SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                 IF_DYNNR = IF_DYNNR ).

***     Get Print Technology
        ME->R_PR_CSTEP_PRINT->ZIF_VCXI_PXSS_PRINT~GET_TPRIN( importing EF_PRTEC = LF_PRTEC ).

***     Get Release Status of Print Design Document
        ME->R_PR_CSTEP_PRINT->ZIF_VCXI_PXSS_PRINT~GET_DESDO( importing EF_RELEA = LF_RELEA ).

        loop at screen.
***       Hide Design Document Description Override Flag if undefined
          if SCREEN-NAME cp '*-FOVRR_DESDD' and
             ME->S_CSTEP_PRINT_VIS-FOVRR_DESDD eq ABAP_UNDEFINED.
            move 0 to SCREEN-ACTIVE.
          endif.
***       Disable Design Document Description if generated
          if SCREEN-NAME cp '*-DESDD' and
             ME->S_CSTEP_PRINT_VIS-FOVRR_DESDD eq ABAP_FALSE.
            move 0 to SCREEN-INPUT.
          endif.
***       Disable Design Document Desciption for existing Design Document
          if ME->S_CSTEP_PRINT_VIS-DESDO is not initial.
            if SCREEN-NAME cp '*-DESDD'.
              SCREEN-INPUT  = 0.
            endif.
            if SCREEN-NAME cp '*-FOVRR_DESDD'.
              SCREEN-ACTIVE = 0.
            endif.
          endif.

***       Printable Area changeable if Override Flag is set
          if SCREEN-GROUP1 eq 'OPA'.
            if ME->S_CSTEP_PRINT_VIS-FOVRR_PRARE eq ABAP_FALSE.
              move 0 to SCREEN-INPUT.
            endif.
          endif.

***       Hide by Print Technology
          case LF_PRTEC.
***         Traditional Printing
            when ZCL_VCXI_PXSS_CUST_PRINT=>C_PRTEC_ANALOG.
              if SCREEN-GROUP3 eq 'PTD'.    "Digital Print Technology
                SCREEN-ACTIVE = 0.
              endif.

***         Digital Printing
            when ZCL_VCXI_PXSS_CUST_PRINT=>C_PRTEC_DIGITAL.
              if SCREEN-GROUP3 eq 'PTA'.    "Analog Print Technology
                SCREEN-ACTIVE = 0.
              endif.
          endcase.

***       Released Design Document
          if SCREEN-GROUP2 eq 'PDD' and    "Controlled by Design Document
             LF_RELEA eq ABAP_TRUE.
            move 0 to SCREEN-INPUT.
          endif.
          if SCREEN-GROUP2 eq 'FPA' and    " Override Printable Area
             LF_RELEA eq ABAP_TRUE.
            move 0 to SCREEN-INPUT.
            move 0 to SCREEN-ACTIVE.
          endif.

          modify screen.
        endloop.

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Super PBO
    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize GRID
    if ME->R_GRID_MS_INK is bound.
      ME->R_GRID_MS_INK->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_MS_INK->REFRESH_GRID( ).
    endif.

    if ME->R_GRID_MARK is bound.
      ME->R_GRID_MARK->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_MARK->REFRESH_GRID( ).
    endif.

***--------------------------------------------------------------------------------------
*** Define DropDown Values for Design Document
    ME->SET_DDVAL_DESDO( ).

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_CSTEP_PRINT_VIS type ZVCXI_P2SS_S_CSTEP_PRINT_VIS.


*** Call SUPER
    SUPER->DO_UPDATE_VIS( IS_CSTEP_VIS = IS_CSTEP_VIS ).

    check ME->R_PR_CSTEP_PRINT is bound.

*** "Cast" Vis Structure
    move-corresponding IS_CSTEP_VIS to LS_CSTEP_PRINT_VIS.

***--------------------------------------------------------------------------------------
*** Set Print Type
    if ME->S_CSTEP_PRINT_VIS-TPRIN ne LS_CSTEP_PRINT_VIS-TPRIN.
      ME->R_PR_CSTEP_PRINT->SET_TPRIN( IF_TPRIN = LS_CSTEP_PRINT_VIS-TPRIN
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Print Side
    if ME->S_CSTEP_PRINT_VIS-PRSID ne LS_CSTEP_PRINT_VIS-PRSID.
      ME->R_PR_CSTEP_PRINT->SET_PRSID( IF_PRSID = LS_CSTEP_PRINT_VIS-PRSID
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Number of Colors
    if ME->S_CSTEP_PRINT_VIS-NOCOL ne LS_CSTEP_PRINT_VIS-NOCOL.
      ME->R_PR_CSTEP_PRINT->SET_NOCOL( IF_NOCOL = LS_CSTEP_PRINT_VIS-NOCOL ).
    endif.

*** Set Sheet Direction
    if ME->S_CSTEP_PRINT_VIS-SHTDI ne LS_CSTEP_PRINT_VIS-SHTDI.
      ME->R_PR_CSTEP_PRINT->SET_SHTDI( IF_SHTDI = LS_CSTEP_PRINT_VIS-SHTDI
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.


*** Update Printable Area if it is "Overriden"
    if ME->S_CSTEP_PRINT_VIS-FOVRR_PRARE eq ABAP_TRUE and
       ME->S_CSTEP_PRINT_VIS-S_PRARE ne LS_CSTEP_PRINT_VIS-S_PRARE.
      ME->R_PR_CSTEP_PRINT->SET_PRARE( IS_PRARE = LS_CSTEP_PRINT_VIS-S_PRARE
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Flag Override Printable Area
    if ME->S_CSTEP_PRINT_VIS-FOVRR_PRARE ne LS_CSTEP_PRINT_VIS-FOVRR_PRARE.
      ME->R_PR_CSTEP_PRINT->SET_PRARE( IF_FOVRR = LS_CSTEP_PRINT_VIS-FOVRR_PRARE ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Design Design
    if ME->S_CSTEP_PRINT_VIS-DESDO ne LS_CSTEP_PRINT_VIS-DESDO.
      ME->R_PR_CSTEP_PRINT->SET_DESDO( IF_DESDO = LS_CSTEP_PRINT_VIS-DESDO
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Design Design Description
    if ME->S_CSTEP_PRINT_VIS-DESDD        ne LS_CSTEP_PRINT_VIS-DESDD or
       ME->S_CSTEP_PRINT_VIS-FOVRR_DESDD  ne LS_CSTEP_PRINT_VIS-FOVRR_DESDD.
      if LS_CSTEP_PRINT_VIS-FOVRR_DESDD ne ABAP_FALSE.
        ME->R_PR_CSTEP_PRINT->SET_DESDD( IF_DESDD = LS_CSTEP_PRINT_VIS-DESDD
                                         IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
      else.
        ME->R_PR_CSTEP_PRINT->SET_DESDD( IF_DESDD = LS_CSTEP_PRINT_VIS-DESDD
                                         IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ).
      endif.
    endif.

***--------------------------------------------------------------------------------------
*** Set Additional Number of Color Changes
    if ME->S_CSTEP_PRINT_VIS-NOACC ne LS_CSTEP_PRINT_VIS-NOACC.
      ME->R_PR_CSTEP_PRINT->SET_NOACC( IF_NOACC = LS_CSTEP_PRINT_VIS-NOACC
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Additional Number of Ink Tool Changes
    if ME->S_CSTEP_PRINT_VIS-NOATC ne LS_CSTEP_PRINT_VIS-NOATC.
      ME->R_PR_CSTEP_PRINT->SET_NOATC( IF_NOATC = LS_CSTEP_PRINT_VIS-NOATC
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Number of different Designs
    if ME->S_CSTEP_PRINT_VIS-NODDG ne LS_CSTEP_PRINT_VIS-NODDG.
      ME->R_PR_CSTEP_PRINT->SET_NODDG( IF_NODDG = LS_CSTEP_PRINT_VIS-NODDG
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    if ME->R_GRID_MS_INK is bound.
      ME->R_GRID_MS_INK->CHECK_CHANGED_DATA( ).
    endif.
    if ME->R_GRID_MARK is bound.
      ME->R_GRID_MARK->CHECK_CHANGED_DATA( ).
    endif.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    case IF_UCOMM.
      when C_UC_CREATE_DESDO.
        ME->HANDLE_UC_CREATE_DESDO( ).
      when C_UC_LINK_DESDO.
        ME->HANDLE_UC_LINK_DESDO( ).
    endcase.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_CSTEP_PRINT.

  endmethod.
  method HANDLE_UC_CREATE_DESDO.

    data: LF_DOKAR type DOKAR.
    data: LR_BL_DOC type ref to ZCL_VCXI_XCSS_BL_DOC.


***--------------------------------------------------------------------------------------
*** Pick Document Type based on available Provider
    LF_DOKAR = ZCL_VCXI_XCSS_UI_DOC_MGR=>PICK_DOKAR( IR_GATE = ME->R_GATE ).

***--------------------------------------------------------------------------------------
*** Create DMS Document using PopUp
    LR_BL_DOC = ZCL_VCXI_XCSS_POPUP_DOC=>START( IF_DOKAR = LF_DOKAR
                                                IF_DKTXT = ME->R_PR_CSTEP_PRINT->ZIF_VCXI_PXSS_PRINT~GET_DESDD( ) ).
    check LR_BL_DOC          is bound       and
          LR_BL_DOC->F_DOKAR is not initial and
          LR_BL_DOC->F_DOKNR is not initial and
          LR_BL_DOC->F_DOKTL is not initial and
          LR_BL_DOC->F_DOKVR is not initial.

***--------------------------------------------------------------------------------------
*** Set Print Design
    ME->R_PR_CSTEP_PRINT->ZIF_VCXI_PXSS_PRINT~CREATE_DESDO( IR_BL_DOC = LR_BL_DOC ).

  endmethod.
  method HANDLE_UC_LINK_DESDO.

    data: LF_DOKAR type DOKAR,
          LF_DESDO type ZVCXI_PXS_DESDO.
    data: LT_GATE      type /VCXI/VKSR_TT_GATE.
    data: LR_BL_DOC     type ref to ZCL_VCXI_XCSS_BL_DOC,
          LR_GATE       type ref to /VCXI/CL_VKSR_GATE,
          LR_PR_DOC_MGR type ref to ZCL_VCXI_XCSS_PR_DOC_MGR.


***--------------------------------------------------------------------------------------
*** Pick Document Type based on available Provider
    LF_DOKAR = ZCL_VCXI_XCSS_UI_DOC_MGR=>PICK_DOKAR( IR_GATE = ME->R_GATE ).

***--------------------------------------------------------------------------------------
*** Call Pop-Up and Request Document Number, Part, Version
    LR_BL_DOC = ZCL_VCXI_XCSS_POPUP_DOC=>REQUEST_DOC_KEY( exporting IF_DOKAR = LF_DOKAR ).

    check LR_BL_DOC          is bound       and
          LR_BL_DOC->F_DOKAR is not initial and
          LR_BL_DOC->F_DOKNR is not initial and
          LR_BL_DOC->F_DOKTL is not initial and
          LR_BL_DOC->F_DOKVR is not initial.

***--------------------------------------------------------------------------------------
*** Get Document Manager Provider
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCSS_PR_DOC_MGR=>C_IPRVT_DOC_MGR ).
    read table LT_GATE into LR_GATE index 1.
    check SY-SUBRC eq 0.

    move LR_GATE->R_CLSPR ?to LR_PR_DOC_MGR.

*** Add Document Instance
    LR_PR_DOC_MGR->ADD_DOCS( IT_DOC_KEY = value #( ( LR_BL_DOC->GET_DOC_KEY( ) ) ) ).

***--------------------------------------------------------------------------------------
*** Get Design Document Key
    LF_DESDO = ZCL_VCXI_XCSS_BL_DOC=>ENCRYPT_GUID( IF_DOKAR = LR_BL_DOC->F_DOKAR
                                                   IF_DOKNR = LR_BL_DOC->F_DOKNR
                                                   IF_DOKTL = LR_BL_DOC->F_DOKTL
                                                   IF_DOKVR = LR_BL_DOC->F_DOKVR ).

*** Set Print Design
    ME->R_PR_CSTEP_PRINT->SET_DESDO( IF_DESDO = LF_DESDO
                                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).

  endmethod.
  method INIT_VIS.

    SUPER->INIT_VIS( ).

    free: ME->S_CSTEP_PRINT_VIS.
    ME->S_CSTEP_PRINT_VIS-S_CSTEP_VIS = ME->S_CSTEP_VIS.

***--------------------------------------------------------------------------------------
*** Print Type
    ME->S_CSTEP_PRINT_VIS-TPRIN = ME->R_PR_CSTEP_PRINT->ZIF_VCXI_PXSS_PRINT~GET_TPRIN( ).

*** Print Side
    ME->S_CSTEP_PRINT_VIS-PRSID = ME->R_PR_CSTEP_PRINT->GET_PRSID( ).

*** Number of Colors
    ME->S_CSTEP_PRINT_VIS-NOCOL = ME->R_PR_CSTEP_PRINT->ZIF_VCXI_PXSS_PRINT~GET_NOCOL( importing EF_DESCR = ME->S_CSTEP_PRINT_VIS-NOCOL_DESCR ).

*** Printable Area
    ME->S_CSTEP_PRINT_VIS-S_PRARE = ME->R_PR_CSTEP_PRINT->GET_PRARE( importing EF_FOVRR = ME->S_CSTEP_PRINT_VIS-FOVRR_PRARE ).

*** Sheet Direction
    ME->S_CSTEP_PRINT_VIS-SHTDI = ME->R_PR_CSTEP_PRINT->GET_SHTDI( ).

***--------------------------------------------------------------------------------------
*** Design Document
    ME->S_CSTEP_PRINT_VIS-DESDO = ME->R_PR_CSTEP_PRINT->ZIF_VCXI_PXSS_PRINT~GET_DESDO( importing EF_LINK_ICON  = ME->S_CSTEP_PRINT_VIS-DESDO_LINK_ICON
                                                                                                 EF_LINK_DESCR = ME->S_CSTEP_PRINT_VIS-DESDO_LINK_DESCR ).
*** Design Document Description
    ME->S_CSTEP_PRINT_VIS-DESDD = ME->R_PR_CSTEP_PRINT->ZIF_VCXI_PXSS_PRINT~GET_DESDD( importing EF_FOVRR = ME->S_CSTEP_PRINT_VIS-FOVRR_DESDD ).

***--------------------------------------------------------------------------------------
*** Additional Number of Color Changes
    ME->S_CSTEP_PRINT_VIS-NOACC = ME->R_PR_CSTEP_PRINT->GET_NOACC( ).

*** Additional Number of Ink Tool Changes
    ME->S_CSTEP_PRINT_VIS-NOATC = ME->R_PR_CSTEP_PRINT->GET_NOATC( ).

*** Number of different Designs
    ME->S_CSTEP_PRINT_VIS-NODDG = ME->R_PR_CSTEP_PRINT->GET_NODDG( ).

  endmethod.
  method SET_DDVAL_DESDO.

    data: LS_DOC_KEY type ZVCXI_XCS_S_DOC_KEY.
    data: LT_GATE  type /VCXI/VKSR_TT_GATE,
          LT_VALUE type VRM_VALUES.
    data: LR_GATE   type ref to /VCXI/CL_VKSR_GATE,
          LR_PR_DOC type ref to ZCL_VCXI_XCSS_PR_DOC.


***------------------------------------------------------------------------------------------------
*** Get all Print Documents
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCSS_PR_DOC=>C_IPRVT_DOC
                                     IF_INUSE = ABAP_TRUE ).
*** Add to search help structure
    loop at LT_GATE into LR_GATE.
      move LR_GATE->R_CLSPR ?to LR_PR_DOC.
      LS_DOC_KEY = LR_PR_DOC->GET_DOC_KEY( ).
      append value #( KEY = ZCL_VCXI_XCSS_BL_DOC=>ENCRYPT_GUID( IF_DOKAR = LS_DOC_KEY-DOKAR
                                                                IF_DOKNR = LS_DOC_KEY-DOKNR
                                                                IF_DOKTL = LS_DOC_KEY-DOKTL
                                                                IF_DOKVR = LS_DOC_KEY-DOKVR )
                      TEXT = LR_PR_DOC->GET_DESCR( )
                    ) to LT_VALUE.
    endloop.

***------------------------------------------------------------------------------------------------
*** Initialize and set listbox content
    call function 'VRM_REFRESH_VALUES'.
    call function 'VRM_SET_VALUES'
      exporting
        ID     = 'ZVCXI_P2SS_S_CSTEP_PRINT_VIS-DESDO'
        VALUES = LT_VALUE.

  endmethod.
