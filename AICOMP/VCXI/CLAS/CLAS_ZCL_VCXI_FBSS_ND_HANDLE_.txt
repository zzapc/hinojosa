
class ZCL_VCXI_FBSS_ND_HANDLE definition
  public
  inheriting from ZCL_VCXI_F0SS_ND
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
    raising
      /VCXI/CX_CKX .
protected section.

  methods INIT_NODE
    redefinition .
private section.
endclass. "ZCL_VCXI_FBSS_ND_HANDLE definition
class ZCL_VCXI_FBSS_ND_HANDLE implementation.
  method CONSTRUCTOR.

*** Call Super CONSTRUCTOR
    SUPER->CONSTRUCTOR( IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

*** Assign to cluster - Parent
    move C_PARENT_ASSIGN to ME->F_ASSIGN.

*** Set Node as expandable
    move ABAP_TRUE to ME->F_EXPANDABLE.

    ME->INIT_NODE( ).

  endmethod.
  method INIT_NODE.

    data: LS_HNWGT type        ZVCXI_FBS_S_HNWGT,
          LS_HNARE type        ZVCXI_FBS_S_HNARE.
    data:           LR_PR_HANDLE        type ref to ZCL_VCXI_FBSS_PR_HANDLE.
    field-symbols:  <S_NVAL>            type        YS_NVALS.

***--------------------------------------------------------------------------------------
*** Call Super
    SUPER->INIT_NODE( ).

***--------------------------------------------------------------------------------------
*** Check Input Gate is Handle
    try.
        move ME->R_GATE->R_CLSPR ?to LR_PR_HANDLE.
      catch CX_SY_MOVE_CAST_ERROR.
        clear LR_PR_HANDLE.
    endtry.

    check LR_PR_HANDLE is bound.

***--------------------------------------------------------------------------------------
*** Initialize Node Values from Gate
    move 'D' to ME->F_NETTYPE.
    if ME->F_EXPANDED eq ABAP_TRUE.
      move '2' to ME->F_NETTYPE.
    endif.

*** Set Node Description
    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = 0.
    <S_NVAL>-VAL  = LR_PR_HANDLE->GET_DESCR( ).

*** Set additional Values if collapsed
    check ME->F_EXPANDED eq ABAP_FALSE.

***--------------------------------------------------------------------------------------
*** Get Handle Weight
    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = 2.
    move text-D01 to <S_NVAL>-VAL.

    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = 3.
    LR_PR_HANDLE->GET_HNWGT( importing ES_HNWGT = LS_HNWGT ).
    <S_NVAL>-VAL = ME->CONV_ANY_TO_VAL( IF_VALUE = LS_HNWGT-HNWGT
                                        IF_UOM   = LS_HNWGT-HNWGT_UOM ).

***--------------------------------------------------------------------------------------
*** Get Handle Area
    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = 4.
    move text-D02 to <S_NVAL>-VAL.

    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = 5.
    LR_PR_HANDLE->GET_HNARE( importing ES_HNARE = LS_HNARE ).
    <S_NVAL>-VAL = ME->CONV_ANY_TO_VAL( IF_VALUE = LS_HNARE-HNARE
                                        IF_UOM   = LS_HNARE-HNARE_UOM ).

***----------------------------------------------------------------------------
*** Set Status Icon
    ME->SET_STATUS_ICON( ).

  endmethod.
endclass. "ZCL_VCXI_FBSS_ND_HANDLE implementation
