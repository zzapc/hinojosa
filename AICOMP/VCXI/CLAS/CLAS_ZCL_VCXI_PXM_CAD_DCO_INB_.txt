
class ZCL_VCXI_PXM_CAD_DCO_INB definition
  public
  inheriting from /VCXI/CL_CKMR_CLDCO
  create public .

public section.

  data F_FSIDA type ZVCXI_XCI_FSIDA read-only .
  data F_XXML type XSTRING read-only .
  data F_CADPO type ZVCXI_PXS_CADPO read-only .
  data F_CADPI type ZVCXI_PXS_CADPI read-only .

  methods SET_XXML
    importing
      !IF_XXML type XSTRING optional
      !IR_REQUEST type ref to IF_HTTP_REQUEST optional
    raising
      /VCXI/CX_CKX .
  methods SET_FSIDA
    importing
      !IF_FSIDA type ZVCXI_XCI_FSIDA
    raising
      /VCXI/CX_CKX .
protected section.

  methods INIT_PRJ_INFO
    raising
      /VCXI/CX_CKX .
  methods GET_XML_DREF
    returning
      value(RT_DATA) type ABAP_TRANS_SRCBIND_TAB
    raising
      /VCXI/CX_CKX .

  methods DESERIALIZE_INFO
    redefinition .
  methods SERIALIZE_INFO
    redefinition .
private section.
endclass. "ZCL_VCXI_PXM_CAD_DCO_INB definition
class ZCL_VCXI_PXM_CAD_DCO_INB implementation.
  method DESERIALIZE_INFO.

    data: LT_DATA      type ABAP_TRANS_SRCBIND_TAB.


    check IF_XMLDC is not initial.

***--------------------------------------------------------------------------------------
*** Get Data References for XML Serialize/Deserialize
    LT_DATA = ME->GET_XML_DREF( ).

*** Open XML
    call transformation ID
         source xml IF_XMLDC
         result (LT_DATA).

  endmethod.
  method GET_XML_DREF.

    RT_DATA = value #( ( NAME = 'F_XXML'  VALUE = ref #( ME->F_XXML ) )
                       ( NAME = 'F_CADPI' VALUE = ref #( ME->F_CADPI ) )
                       ( NAME = 'F_CADPO' VALUE = ref #( ME->F_CADPO ) )
                       ( NAME = 'F_FSIDA' VALUE = ref #( ME->F_FSIDA ) )
                     ).

  endmethod.
  method INIT_PRJ_INFO.

    data: LS_CADIF_PRJ_IO type ZVCXI_PXSS_S_CADIF_PRJ_IO.


*** Create XML
    call transformation ID
         source xml ME->F_XXML
         result S_CADIF_PRJ_IO = LS_CADIF_PRJ_IO.      "Project

*** Fill Project Information
    ME->F_CADPI = LS_CADIF_PRJ_IO-CADPI.
    ME->F_CADPO = LS_CADIF_PRJ_IO-CADPO.

  endmethod.
  method SERIALIZE_INFO.

    data: LS_DATA type ABAP_TRANS_SRCBIND.
    data: LT_DATA type ABAP_TRANS_SRCBIND_TAB.
    field-symbols: <VALUE> type ANY.

***--------------------------------------------------------------------------------------
*** Add other Data References
    append lines of ME->GET_XML_DREF( ) to LT_DATA.

*** Remove initial Data
    loop at LT_DATA into LS_DATA.
      assign LS_DATA-VALUE->* to <VALUE>.
      if <VALUE> is initial.
        delete LT_DATA.
      endif.
    endloop.

***--------------------------------------------------------------------------------------
*** Create XML
    call transformation ID
         source (LT_DATA)
         result xml RF_XMLDC.

  endmethod.
  method SET_FSIDA.

    check ME->F_FSIDA ne IF_FSIDA.
    ME->F_FSIDA = IF_FSIDA.

*** Raise Change Event
    raise event CHANGED.

  endmethod.
  method SET_XXML.

    data: LF_XXML type XSTRING.


    if IF_XXML is supplied.
      LF_XXML = IF_XXML.
    endif.

    if IR_REQUEST is supplied.
      LF_XXML = IR_REQUEST->GET_DATA( ).
    endif.


    if LF_XXML ne ME->F_XXML.
      ME->F_XXML = LF_XXML.

***   Initialize Project Information
      ME->INIT_PRJ_INFO( ).

***   Raise Change Event
      raise event CHANGED.
    endif.

  endmethod.
