
class ZCL_VCXI_P0SS_UI_TAPE_MGR definition
  public
  inheriting from /VCXI/CL_VKSRU_CLSUI
  create public .

public section.

  methods DO_PBO
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  data R_GRID_MS_TAPE type ref to ZCL_VCXI_P0SS_GRID_MS_TAPE .
private section.
endclass. "ZCL_VCXI_P0SS_UI_TAPE_MGR definition
class ZCL_VCXI_P0SS_UI_TAPE_MGR implementation.
method ACTIVATE.

  SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
  call function 'ZVCXI_P0SS_UI_TAPE_MGR_BIND'
    exporting
      IR_UI_TAPE_MGR = ME
    importing
      ES_DYNP        = ME->S_DYNP.

*** Create Grid for GRID_MS_TAPE
  create object ME->R_GRID_MS_TAPE
    exporting
      IR_GATE           = ME->R_GATE
      IR_UIGATE         = ME->R_UIGATE
      IF_CONTAINER_NAME = '0100CC_GRID_MS_TAPE'.

endmethod.
method DEACTIVATE.

  SUPER->DEACTIVATE( ).

  if ME->R_GRID_MS_TAPE is bound.
    ME->R_GRID_MS_TAPE->DO_FREE( ).
    clear ME->R_GRID_MS_TAPE.
  endif.

endmethod.
method DO_MODIFY_SCREEN.

  data:  LF_LOCKED type        /VCXI/CKX_LOCKED,
         LF_INUSE  type        /VCXI/VKSR_INUSE.
  data:  LR_CX_CKX type ref to /VCXI/CX_CKX.

  SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                           IF_DYNNR = IF_DYNNR ).

  try.

      if IF_REPID eq ME->S_DYNP-REPID and
         IF_DYNNR eq ME->S_DYNP-DYNNR.

        LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
        LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***     Modify Screen using DynamicVC
        ZCL_VCXI_XCDR_SERVICE=>MODIFY_SCREEN( IR_GATE = ME->R_GATE ).

        loop at screen.
          if LF_LOCKED ne ABAP_TRUE or
             LF_INUSE  ne ABAP_TRUE.
            move 0 to SCREEN-INPUT.
          endif.
          modify screen.
        endloop.
      endif.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

endmethod.
method DO_PBO.

*** Init Grid
  if ME->R_GRID_MS_TAPE is bound.
    if ME->R_GRID_MS_TAPE->F_REFRESH_REQ eq ABAP_FALSE.
      move ABAP_TRUE to ME->R_GRID_MS_TAPE->F_REFRESH_REQ.
    endif.
    ME->R_GRID_MS_TAPE->REFRESH_GRID( ).
  endif.

endmethod.
method DO_USER_COMMAND.

  SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

  if ME->R_GRID_MS_TAPE is bound.
    ME->R_GRID_MS_TAPE->CHECK_CHANGED_DATA( ).
  endif.

endmethod.
