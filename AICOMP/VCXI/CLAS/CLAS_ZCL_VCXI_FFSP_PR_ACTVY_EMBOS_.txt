
class ZCL_VCXI_FFSP_PR_ACTVY_EMBOS definition
  public
  inheriting from /VCXI/CL_VKSR_CLSPR
  create public .

public section.

  interfaces ZIF_VCXI_FFSP_ADJ_FIDIM .

  constants C_PARID_EMTIN type /VCXI/VKS_PARID value 'ZVCXI_FF_EMTIN' ##NO_TEXT.

  methods GET_EMTIN
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
      !ES_EMTIN type ZVCXI_FFS_S_EMTIN
    raising
      /VCXI/CX_CKX .
  methods SET_EMTIN
    importing
      !IS_EMTIN type ZVCXI_FFS_S_EMTIN
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .

  methods HANDLE_EVENT
    redefinition .
protected section.

  constants C_IEVNT_I_RECALC_MC_CONSU type /VCXI/VKSC_IEVNT value 'RECALC_MC_CONSU' ##NO_TEXT.

  methods HANDLE_RECALC_MC_CONSU
    importing
      !IT_GATE type /VCXI/VKSR_TT_GATE
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_FFSP_PR_ACTVY_EMBOS definition
class ZCL_VCXI_FFSP_PR_ACTVY_EMBOS implementation.
  method GET_EMTIN.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_EMTIN ).
    move LS_PARAM-ATFLV to ES_EMTIN-EMTIN.
    move LS_PARAM-UNIT  to ES_EMTIN-EMTIN_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method HANDLE_EVENT.

    SUPER->HANDLE_EVENT( IF_IEVNT         = IF_IEVNT
                         IT_GATE_PROVIDER = IT_GATE_PROVIDER ).

    case IF_IEVNT.
      when C_IEVNT_I_RECALC_MC_CONSU.
        ME->HANDLE_RECALC_MC_CONSU( IT_GATE = IT_GATE_PROVIDER ).
    endcase.

  endmethod.
  method HANDLE_RECALC_MC_CONSU.

    data: LT_GATE        type        /VCXI/VKSR_TT_GATE.
    data: LS_MQTYI       type        ZVCXI_XCSP_S_MQTYI.
    data: LF_UPDATE_ALL  type        ABAP_BOOL.
    data: LR_GATE        type ref to /VCXI/CL_VKSR_GATE,
          LR_GATE_PARENT type ref to /VCXI/CL_VKSR_GATE,
          LR_OPERATION   type ref to ZIF_VCXI_XCSP_OPERATION,
          LR_PR_MC       type ref to ZCL_VCXI_XCSP_PR_MC.

***--------------------------------------------------------------------------------------
*** Should all MCs be updated or just the requester
    read table IT_GATE with key TABLE_LINE = ME->R_GATE
                       transporting no fields.
    if SY-SUBRC eq 0.
      LF_UPDATE_ALL = ABAP_TRUE.
    else.
      LR_GATE_PARENT = ME->R_GATE->GET_PARENT( ).
      read table IT_GATE with key TABLE_LINE = LR_GATE_PARENT
                         transporting no fields.
      if SY-SUBRC = 0.
        LF_UPDATE_ALL = ABAP_TRUE.
      endif.
    endif.

***--------------------------------------------------------------------------------------
*** Get Machine Quantity
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZIF_VCXI_XCSP_OPERATION=>C_IPRVT_XCS_OPERATION
                                     IF_INUSE = ABAP_TRUE ).

    read table LT_GATE into LR_GATE index 1.
    if SY-SUBRC = 0.
      clear: LR_OPERATION.
      try.
          move LR_GATE->R_CLSPR ?to LR_OPERATION.
        catch CX_SY_MOVE_CAST_ERROR.
          clear: LR_OPERATION.
      endtry.
      if LR_OPERATION is bound.
        move LR_OPERATION->GET_MQTYI( ) to LS_MQTYI.
      endif.
    endif.

***--------------------------------------------------------------------------------------
*** Get all Material Consumptions
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCSP_PR_MC=>C_IPRVT_XCS_MC
                                     IF_INUSE = ABAP_TRUE ).

***--------------------------------------------------------------------------------------
*** Start Recalc Consumption of all Material Consumptions or just the requester
    loop at LT_GATE into LR_GATE.
      if LF_UPDATE_ALL eq ABAP_FALSE.
        read table IT_GATE with key TABLE_LINE = LR_GATE transporting no fields.
        if SY-SUBRC ne 0.
          continue.
        endif.
      endif.

      try.
          move LR_GATE->R_CLSPR ?to LR_PR_MC.
          check LR_PR_MC is bound.
          LR_PR_MC->CALC_CONSU( IS_MQTYI = LS_MQTYI ).
        catch CX_SY_MOVE_CAST_ERROR.
          continue.
      endtry.
    endloop.

  endmethod.
  method SET_EMTIN.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_EMTIN ).

    move IS_EMTIN-EMTIN     to LS_PARAM-ATFLV.
    move IS_EMTIN-EMTIN_UOM to LS_PARAM-UNIT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = ZIF_VCXI_FFSP_ADJ_FIDIM~C_IEVNT_O_FIDIM_CHANGED ).
    endif.

  endmethod.
  method ZIF_VCXI_FFSP_ADJ_FIDIM~CALC_FIDIM_OUT.

    data:          LS_EMTIN   type ZVCXI_FFS_S_EMTIN.
    field-symbols: <S_FIDIM>  type ZVCXI_FFSP_S_FIDIM.

*** Get Internal Embossing Thickness
    ME->GET_EMTIN( importing ES_EMTIN = LS_EMTIN ).

*** Adjust Film Dimensions by Print Thickness
    loop at CT_FIDIM assigning <S_FIDIM>.
***   Calculate Internal Thickness
      ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_EMTIN-EMTIN
                                                          IF_CALCV_UOM_1 = LS_EMTIN-EMTIN_UOM
                                                          IF_CALCV_2     = <S_FIDIM>-FITIN
                                                          IF_CALCV_UOM_2 = <S_FIDIM>-FITIN_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_ADD
                                                importing EF_CALCV       = <S_FIDIM>-FITIN
                                                          EF_CALCV_UOM   = <S_FIDIM>-FITIN_UOM ).

    endloop.

  endmethod.
