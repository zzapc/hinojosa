
class ZCL_VCXI_XCS_HD_APPDATA definition
  public
  inheriting from /VCXI/CL_VKSRU_CLSHD
  create public .

public section.
  type-pools SLIS .

  data S_APPDATA_VIS type ZVCXI_XCS_S_APPDATA_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_APPDATA type ref to /VCXI/CL_VKSR_APPDATA
    raising
      /VCXI/CX_CKX .

  methods DO_USER_COMMAND
    redefinition .
protected section.

  constants C_UC_SHOW_APPE type SYUCOMM value 'SHOW_APPE'. "#EC NOTEXT

  methods HANDLE_UC_SHOW_APPE
    raising
      /VCXI/CX_CKX .
  methods INIT_APPDATA_VIS
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_XCS_HD_APPDATA definition
class ZCL_VCXI_XCS_HD_APPDATA implementation.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_APPDATA = IR_APPDATA ).

*** Bind Screen
  call function 'ZVCXI_XCS_HD_APPDATA_BIND'
    exporting
      IR_HD_APPDATA = ME
    importing
      ES_DYNP       = ME->S_DYNP.

*** Initialize Application Data Vis.
  ME->INIT_APPDATA_VIS( ).

endmethod.
method DO_USER_COMMAND.

  SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

  case IF_UCOMM.
    when C_UC_SHOW_APPE.
      ME->HANDLE_UC_SHOW_APPE( ).
  endcase.

endmethod.
method HANDLE_UC_SHOW_APPE.

  data:  LT_EXCLUDING  type        SLIS_T_EXTAB.
  data:  LR_APPDATA    type ref to ZCL_VCXI_XCS_APPDATA.

***----------------------------------------------------------------------------
*** Get Application Data
  try.
      move ME->R_APPDATA ?to LR_APPDATA.
    catch CX_SY_MOVE_CAST_ERROR.
      clear: LR_APPDATA.
  endtry.
  check LR_APPDATA is bound.

***----------------------------------------------------------------------------
*** Exclude Functions
  append  '&NT1' to LT_EXCLUDING.
  append  '&ETA' to LT_EXCLUDING.
  append  '&ALL' to LT_EXCLUDING.
  append  '&SAL' to LT_EXCLUDING.
  append  '&OUP' to LT_EXCLUDING.
  append  '&ODN' to LT_EXCLUDING.
  append  '%SC'  to LT_EXCLUDING.
  append  '%SC+' to LT_EXCLUDING.
  append  '&ILT' to LT_EXCLUDING.
  append  '&OL0' to LT_EXCLUDING.
  append  '&CRB' to LT_EXCLUDING.
  append  '&CRL' to LT_EXCLUDING.
  append  '&CRR' to LT_EXCLUDING.
  append  '&CRE' to LT_EXCLUDING.

***----------------------------------------------------------------------------
*** Display Popup
  call function 'REUSE_ALV_POPUP_TO_SELECT'
    exporting
      I_TITLE                       = 'Application Environments'(T01)
      I_SELECTION                   = ABAP_FALSE
      I_ALLOW_NO_SELECTION          = ABAP_TRUE
      I_SCREEN_START_COLUMN         = 10
      I_SCREEN_START_LINE           = 10
      I_TABNAME                     = 'T_APPE'
      I_STRUCTURE_NAME              = 'ZVCXI_XCS_S_APPE'
      IT_EXCLUDING                  = LT_EXCLUDING
    tables
      T_OUTTAB                      = LR_APPDATA->T_APPE.

endmethod.
  method INIT_APPDATA_VIS.

    data:  LR_APPDATA type ref to ZCL_VCXI_XCS_APPDATA.


    try.
        move ME->R_APPDATA ?to LR_APPDATA.
      catch CX_SY_MOVE_CAST_ERROR.
        clear: LR_APPDATA.
    endtry.
    check LR_APPDATA is bound.

    move               LR_APPDATA->F_WERKS         to ME->S_APPDATA_VIS-WERKS.
    move               LR_APPDATA->F_DCONF         to ME->S_APPDATA_VIS-DCONF.

    if LR_APPDATA->S_AUQTY is initial.
***   No VBAP Sales Unit found
      ME->S_APPDATA_VIS-S_OPQTY = LR_APPDATA->S_OPQTY.
    else.
***   Get Sales Unit
      ME->S_APPDATA_VIS-S_OPQTY = LR_APPDATA->S_AUQTY.
    endif.

    move               LR_APPDATA->S_KNA1_AG-KUNNR to ME->S_APPDATA_VIS-KUNAG.
    move               LR_APPDATA->S_KNA1_AG-NAME1 to ME->S_APPDATA_VIS-AGNAME.

    move               LR_APPDATA->S_KNA1_WE-KUNNR to ME->S_APPDATA_VIS-KUNWE.
    move               LR_APPDATA->S_KNA1_WE-NAME1 to ME->S_APPDATA_VIS-WENAME.

  endmethod.
