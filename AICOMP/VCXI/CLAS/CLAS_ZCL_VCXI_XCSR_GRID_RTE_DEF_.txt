
class ZCL_VCXI_XCSR_GRID_RTE_DEF definition
  public
  inheriting from ZCL_VCXI_XCS_GRID
  create public .

public section.

  type-pools ABAP .
  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_CONTAINER_NAME type SCRFNAME
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL default ABAP_TRUE
    raising
      /VCXI/CX_CKX .
protected section.

  data R_PR_RTESPV type ref to ZCL_VCXI_XCSR_PR_RTESPV .

  methods GET_GRID_DATA
    redefinition .
private section.
endclass. "ZCL_VCXI_XCSR_GRID_RTE_DEF definition
class ZCL_VCXI_XCSR_GRID_RTE_DEF implementation.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_GATE           = IR_GATE
                      IR_UIGATE         = IR_UIGATE
                      IF_TABNAME        = 'ZVCXI_XCSR_S_RTE_DEF_GRID'
                      IF_CONTAINER_NAME = IF_CONTAINER_NAME
                      IF_APPL_EVENTS    = IF_APPL_EVENTS ).

*** Assign th RP Class to the UI Class
  try.
      move IR_GATE->R_CLSPR ?to ME->R_PR_RTESPV.
    catch CX_SY_MOVE_CAST_ERROR.
***   UI ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '801'.
  endtry.

endmethod.
method GET_GRID_DATA.

  data:          LS_RTE_DEF           type          ZVCXI_XCSR_S_RTE_DEF,
                 LS_RTE_DEF_GRID      type          ZVCXI_XCSR_S_RTE_DEF_GRID.
  field-symbols: <T_DATA>             type standard table,
                 <S_DATA>             type          any.

*** Dereferencing of Data Reference
  assign IR_DATA->* to <T_DATA>.

  loop at ME->R_PR_RTESPV->T_RTE_DEF into LS_RTE_DEF.

*** Add Entry
    append initial line to <T_DATA> assigning <S_DATA>.

*** Fill Data
    clear: LS_RTE_DEF_GRID.

*** Production Route Data
    if LS_RTE_DEF-R_PRTE is bound.
      move LS_RTE_DEF-R_PRTE->/VCXI/IF_CKX_OBJECT~GET_ID( )    to LS_RTE_DEF_GRID-IPRTE.
      move LS_RTE_DEF-R_PRTE->/VCXI/IF_CKX_OBJECT~GET_DESCR( ) to LS_RTE_DEF_GRID-DESCR.
    endif.

    move-corresponding LS_RTE_DEF_GRID to <S_DATA>.

  endloop.

endmethod.
