
class ZCL_VCXI_XCM_MBQ_STP_S202 definition
  public
  inheriting from ZCL_VCXI_XCM_MBQ_STP_S000
  create public .

public section.

  methods EXECUTE
    redefinition .
protected section.
private section.
endclass. "ZCL_VCXI_XCM_MBQ_STP_S202 definition
class ZCL_VCXI_XCM_MBQ_STP_S202 implementation.
  method EXECUTE.

    data: LS_MATLI    type ZVCXI_XCM_MBQ_S_MATLI,
          LS_BAPIRET2 type BAPIRET2.
    data: LR_BOB_BOM type ref to ZCL_VCXI_XCI_BOB_BOM,
          LR_ACTDAT  type ref to DATA.
    field-symbols: <S_ACTDAT_BOM> type ZVCXI_XCM_MBQ_S_ACTDAT_BOM.


    try.
***     Get Material List Entry of active Material List Entry
        LS_MATLI = ME->R_DCO_INPUT->T_MATLI[ MLCNT = ME->R_DCO_INPUT->F_MLCNT ].

***     Check Activity - Assign to Super BOM
        if not LINE_EXISTS( LS_MATLI-T_ACTCON[ ACTID = ZCL_VCXI_XCM_MBQ_CUST=>C_ACTID_BOM ] ).
***       Set Status to CANCELED
          ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_CANCELED ).
          return.
        endif.

***     Load Activity Control Data
        LR_ACTDAT = LS_MATLI-T_ACTCON[ ACTID = ZCL_VCXI_XCM_MBQ_CUST=>C_ACTID_BOM ]-R_ACTDAT.
        assign LR_ACTDAT->* to <S_ACTDAT_BOM>.


***--------------------------------------------------------------------------------------
***     Load Business Object for Super BOM
        create object LR_BOB_BOM
          exporting
            IF_MATNR = <S_ACTDAT_BOM>-CMATN
            IF_WERKS = <S_ACTDAT_BOM>-WERKS.

***     Assign BOM
        LR_BOB_BOM->ASSIGN_MAT( exporting IF_MATNR    = LS_MATLI-MATNR
                                          IF_STLAN    = <S_ACTDAT_BOM>-STLAN
                                importing ES_BAPIRET2 = LS_BAPIRET2 ).

***     Add Message
        if LS_BAPIRET2 is not initial.
          ME->R_RPSEG->R_MSG->ADD_MESSAGE_BY_BAPIRET2( IS_BAPIRET2 = LS_BAPIRET2 ).
        endif.

***     Set Status to Complete
        ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_COMPLETED ).

***--------------------------------------------------------------------------------------
      catch /VCXI/CX_CKX into data(LR_CX_CKX).
***     Set Status to Error
        ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_ERROR ).

***     Add Message to Message Log
        ME->R_RPSEG->R_MSG->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

*** Perform Save and Commit
    ME->R_RPSEG->SAVE_AND_COMMIT( ).

  endmethod.
endclass. "ZCL_VCXI_XCM_MBQ_STP_S202 definition
endclass. "ZCL_VCXI_XCM_MBQ_STP_S202 implementation
