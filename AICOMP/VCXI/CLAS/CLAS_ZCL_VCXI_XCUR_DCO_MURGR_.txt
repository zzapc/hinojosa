
class ZCL_VCXI_XCUR_DCO_MURGR definition
  public
  inheriting from /VCXI/CL_CKMR_CLDCO
  create public .

public section.

  data F_SYSOM type /VCXI/VKS_SYSOM read-only .
  data R_SMDCNTRL type ref to ZCL_VCXI_XCUR_SMDCNTRL read-only .
  data T_MUROB type ZVCXI_XCUR_TT_MUROB read-only .

  methods SET_MUREL
    importing
      !IT_MUREL type ZVCXI_XCUR_TT_MUREL
    raising
      /VCXI/CX_CKX .
  methods SET_MUROB
    importing
      !IT_MUROB type ZVCXI_XCUR_TT_MUROB
    raising
      /VCXI/CX_CKX .
  methods SET_SMDCNTRL
    importing
      !IR_SMDCNTRL type ref to ZCL_VCXI_XCUR_SMDCNTRL
    raising
      /VCXI/CX_CKX .
  methods SET_SYSOM
    importing
      !IF_SYSOM type /VCXI/VKS_SYSOM
    raising
      /VCXI/CX_CKX .
  methods GET_MUREL
    exporting
      !ET_MUREL_XML type ZVCXI_XCUR_TT_MUREL_XML
    returning
      value(RT_MUREL) type ZVCXI_XCUR_TT_MUREL
    raising
      /VCXI/CX_CKX .
protected section.

  methods HANDLE_SMDCNTRL_CHANGED
    for event CHANGED of ZCL_VCXI_XCUR_SMDCNTRL .

  methods DESERIALIZE_INFO
    redefinition .
  methods SERIALIZE_INFO
    redefinition .
private section.

  data T_MUREL type ZVCXI_XCUR_TT_MUREL .
  data T_MUREL_XML type ZVCXI_XCUR_TT_MUREL_XML .
endclass. "ZCL_VCXI_XCUR_DCO_MURGR definition
class ZCL_VCXI_XCUR_DCO_MURGR implementation.
  method DESERIALIZE_INFO.

    data: LF_XML_SMDCNTRL type STRING.


    check IF_XMLDC is not initial.

***--------------------------------------------------------------------------------------
*** Open XML
    call transformation ID
         source xml IF_XMLDC
         result F_SYSOM        = ME->F_SYSOM
                T_MUROB        = ME->T_MUROB
                T_MUREL_XML    = ME->T_MUREL_XML
                F_XML_SMDCNTRL = LF_XML_SMDCNTRL.

***--------------------------------------------------------------------------------------
*** Deserialize SmartMD Control Settings
    ME->R_SMDCNTRL = ZCL_VCXI_XCUR_SMDCNTRL=>DESERIALIZE( IF_XML = LF_XML_SMDCNTRL ).
    if ME->R_SMDCNTRL is bound.
      set handler ME->HANDLE_SMDCNTRL_CHANGED for ME->R_SMDCNTRL.
    endif.

  endmethod.
  method GET_MUREL.

*** Direct return of XML if only XML is requested
    if ET_MUREL_XML is     requested and
       RT_MUREL     is not requested and
       ME->T_MUREL_XML is not initial.
      ET_MUREL_XML = ME->T_MUREL_XML.
      exit.
    endif.


*** Delayed convertion of XML to Update Element
    if ME->T_MUREL_XML is not initial.
      ME->T_MUREL = ZCL_VCXI_XCUR_MURDE=>CONV_XML_TO_MUREL( IT_MUREL_XML = ME->T_MUREL_XML ).
      clear ME->T_MUREL_XML.
    endif.

*** Return Update Elements
    RT_MUREL = ME->T_MUREL.

    if ET_MUREL_XML is requested.
      ET_MUREL_XML = ZCL_VCXI_XCUR_MURDE=>CONV_MUREL_TO_XML( IT_MUREL = RT_MUREL ).
    endif.

  endmethod.
  method HANDLE_SMDCNTRL_CHANGED.

*** Raise Change Event
    raise event CHANGED.

  endmethod.
  method SERIALIZE_INFO.

    data: LF_XML_SMDCNTRL type STRING.


***--------------------------------------------------------------------------------------
*** Compress Update Elements as XML (and release memory)
    ME->GET_MUREL( importing ET_MUREL_XML = ME->T_MUREL_XML ).
    clear ME->T_MUREL.

*** Serialize SmartMD Control Settings
    if ME->R_SMDCNTRL is bound.
      LF_XML_SMDCNTRL = ME->R_SMDCNTRL->SERIALIZE( ).
    endif.

***--------------------------------------------------------------------------------------
*** Create XML
    call transformation ID
         source F_SYSOM     = ME->F_SYSOM
                T_MUROB     = ME->T_MUROB
                T_MUREL_XML = ME->T_MUREL_XML
         result xml RF_XMLDC.

  endmethod.
  method SET_MUREL.

    check ME->GET_MUREL( ) ne IT_MUREL.
    ME->T_MUREL = IT_MUREL.

*** Raise Change Event
    raise event CHANGED.

  endmethod.
  method SET_MUROB.

    check ME->T_MUROB ne IT_MUROB.
    ME->T_MUROB = IT_MUROB.

*** Raise Change Event
    raise event CHANGED.

  endmethod.
  method SET_SMDCNTRL.

    free ME->R_SMDCNTRL.
    check IR_SMDCNTRL is bound.

*** Fill new Instance based on provided Instance
    ME->R_SMDCNTRL = ZCL_VCXI_XCUR_SMDCNTRL=>DESERIALIZE( IF_XML = IR_SMDCNTRL->SERIALIZE( ) ).

*** Register Change Event Handler
    set handler HANDLE_SMDCNTRL_CHANGED for ME->R_SMDCNTRL.

*** Raise Change Event
    raise event CHANGED.

  endmethod.
  method SET_SYSOM.

    check ME->F_SYSOM ne IF_SYSOM.
    ME->F_SYSOM = IF_SYSOM.

*** Raise Change Event
    raise event CHANGED.

  endmethod.
