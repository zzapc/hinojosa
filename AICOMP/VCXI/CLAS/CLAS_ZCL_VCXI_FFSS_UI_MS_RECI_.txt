
class ZCL_VCXI_FFSS_UI_MS_RECI definition
  public
  inheriting from /VCXI/CL_VKSRU_CLSUI
  create public .

public section.

  data S_MS_RECI_VIS type ZVCXI_FFSS_S_MS_RECI_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_MS_RECI_VIS
    importing
      !IS_MS_RECI_VIS type ZVCXI_FFSS_S_MS_RECI_VIS
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
protected section.

  data R_PR_MS_RECI type ref to ZCL_VCXI_FFSS_PR_MS_RECI .

  methods INIT_VIS
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_FFSS_UI_MS_RECI definition
class ZCL_VCXI_FFSS_UI_MS_RECI implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_FFSS_UI_MS_RECI_BIND'
      exporting
        IR_UI_MS_RECI = ME
      importing
        ES_DYNP       = ME->S_DYNP.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                    IR_UIGATE = IR_UIGATE
                    IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_MS_RECI.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DO_MODIFY_SCREEN.

    data: LF_LOCKED type        /VCXI/CKX_LOCKED,
          LF_INUSE  type        /VCXI/VKSR_INUSE.
    data:  LR_CX_CKX      type ref to /VCXI/CX_CKX.

    try.
        SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                 IF_DYNNR = IF_DYNNR ).

        LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
        LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***     Modify Scree using DynamicVC
        ZCL_VCXI_XCDR_SERVICE=>MODIFY_SCREEN( IR_GATE = ME->R_GATE ).

        loop at screen.
***       Lock everything if not in change mode
          if LF_LOCKED ne ABAP_TRUE or
             LF_INUSE  ne ABAP_TRUE.
            move 0 to SCREEN-INPUT.
          endif.
          modify screen.
        endloop.

      catch /VCXI/CX_CKX into LR_CX_CKX.
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_PBO.

***---------------------------------------------------------------------------
*** Initialize VIS
    ME->INIT_VIS( ).

  endmethod.
  method DO_UPDATE_MS_RECI_VIS.

    check ME->R_PR_MS_RECI is bound.

***--------------------------------------------------------------------------------------
*** Set Material Number
    if ME->S_MS_RECI_VIS-MATNR ne IS_MS_RECI_VIS-MATNR.
      ME->R_PR_MS_RECI->SET_MATNR( IF_MATNR = IS_MS_RECI_VIS-MATNR
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method INIT_VIS.

    free: ME->S_MS_RECI_VIS.
    check ME->R_PR_MS_RECI is bound.

***--------------------------------------------------------------------------------------
*** Get Material Number
    move ME->R_PR_MS_RECI->GET_MATNR( )        to ME->S_MS_RECI_VIS-MATNR.
    move ME->R_PR_MS_RECI->GET_MATNR_DESCR( )  to ME->S_MS_RECI_VIS-MATNR_DESCR.

***--------------------------------------------------------------------------------------
*** Get Density
    ME->R_PR_MS_RECI->GET_FIDEN( importing ES_FIDEN = ME->S_MS_RECI_VIS-S_FIDEN ).
    move ME->S_MS_RECI_VIS-S_FIDEN-FIDEN_UOM to ME->S_MS_RECI_VIS-FIDEN_UOMC6.

*** Get Number of Layer
    ME->R_PR_MS_RECI->GET_NOLAY( importing EF_NOLAY = ME->S_MS_RECI_VIS-NOLAY ).

***--------------------------------------------------------------------------------------
*** Get Min/Max Thickness
    if ME->R_PR_MS_RECI->R_CUST_RECI is bound.
      move ME->R_PR_MS_RECI->R_CUST_RECI->S_FITMI to ME->S_MS_RECI_VIS-S_FITMI.
      move ME->R_PR_MS_RECI->R_CUST_RECI->S_FITMA to ME->S_MS_RECI_VIS-S_FITMA.
    endif.

***--------------------------------------------------------------------------------------
*** Get Flag Film Blocked
    ME->R_PR_MS_RECI->GET_FBLOF( importing EF_FBLOF = ME->S_MS_RECI_VIS-FBLOF ).

  endmethod.
