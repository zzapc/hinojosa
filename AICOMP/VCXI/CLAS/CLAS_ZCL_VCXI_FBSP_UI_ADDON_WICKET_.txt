
class ZCL_VCXI_FBSP_UI_ADDON_WICKET definition
  public
  inheriting from ZCL_VCXI_XCSP_UI_ADDON
  create public .

public section.

  data S_ADDON_WICKET_VIS type ZVCXI_FBSP_S_ADDON_WICKET_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  data R_GRID_MC_WIMAT type ref to ZCL_VCXI_FBSP_GRID_MC_WIMAT .
  data R_PR_ADDON_WICKET type ref to ZCL_VCXI_FBSP_PR_ADDON_WICKET .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_FBSP_UI_ADDON_WICKET definition
class ZCL_VCXI_FBSP_UI_ADDON_WICKET implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_FBSP_UI_ADDON_WICK_BIND'
      exporting
        IR_UI_ADDON_WICKET = ME
      importing
        ES_DYNP            = ME->S_DYNP.

*** Create Grid for MC Wicketing Material
    create object ME->R_GRID_MC_WIMAT
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_MC_WIMAT'.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_ADDON_WICKET.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

    if ME->R_GRID_MC_WIMAT is bound.
      ME->R_GRID_MC_WIMAT->DO_FREE( ).
      clear ME->R_GRID_MC_WIMAT.
    endif.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Super PBO
    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize Grid
    if ME->R_GRID_MC_WIMAT is bound.
      ME->R_GRID_MC_WIMAT->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_MC_WIMAT->REFRESH_GRID( ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_ADDON_WICKET_VIS type ZVCXI_FBSP_S_ADDON_WICKET_VIS.


*** Super
    SUPER->DO_UPDATE_VIS( IS_VIS = IS_VIS ).
    check ME->R_PR_ADDON_WICKET is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_ADDON_WICKET_VIS.

***-------------------------------------------------------------------------------------
*** Set Wicketing Type
    if ME->S_ADDON_WICKET_VIS-TWICK ne LS_ADDON_WICKET_VIS-TWICK.
      ME->R_PR_ADDON_WICKET->SET_TWICK( IF_TWICK = LS_ADDON_WICKET_VIS-TWICK
                                        IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    if ME->R_GRID_MC_WIMAT is bound.
      ME->R_GRID_MC_WIMAT->CHECK_CHANGED_DATA( ).
    endif.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_ADDON_WICKET.

  endmethod.
  method INIT_VIS.

*** Super
    SUPER->INIT_VIS( ).

    free: ME->S_ADDON_WICKET_VIS.
    ME->S_ADDON_WICKET_VIS-S_ADDON_VIS = ME->S_ADDON_VIS.

***--------------------------------------------------------------------------------------
*** Get Wicket Type
    ME->R_PR_ADDON_WICKET->GET_TWICK( importing EF_TWICK = ME->S_ADDON_WICKET_VIS-TWICK ).

  endmethod.
