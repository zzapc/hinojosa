
class ZCL_VCXI_XCS_POPUP_TEXTEDIT definition
  public
  create protected .

public section.

  constants C_UC_OKAY type SYUCOMM value 'OKAY' ##NO_TEXT.
  constants C_UC_CANCEL type SYUCOMM value 'CANCEL' ##NO_TEXT.

  methods DO_USER_COMMAND
    importing
      !IF_UCOMM type SYUCOMM
    raising
      /VCXI/CX_CKX .
  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  class-methods START
    importing
      !IF_TEXT type STRING
    raising
      /VCXI/CX_CKX .
protected section.

  data F_LOCKED type /VCXI/CKX_LOCKED .
  data F_TEXT type STRING .
  data R_CONTAINER type ref to CL_GUI_CUSTOM_CONTAINER .
  data R_TEXTEDIT type ref to CL_GUI_TEXTEDIT .

  methods REFRESH_TEXTEDIT
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_CANCEL
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_OKAY
    raising
      /VCXI/CX_CKX .
  methods SET_PF_STATUS
    raising
      /VCXI/CX_CKX .
  methods CONSTRUCTOR
    importing
      !IF_TEXT type STRING
    raising
      /VCXI/CX_CKX .
private section.

  methods DO_FREE
    raising
      /VCXI/CX_CKX .
  methods INIT_CONTAINER
    importing
      !IF_CONTAINER_NAME type SCRFNAME default '0100CC_TEXTEDIT_POPUP'
    raising
      /VCXI/CX_CKX .
  methods INIT_TEXTEDIT
    raising
      /VCXI/CX_CKX .
endclass. "ZCL_VCXI_XCS_POPUP_TEXTEDIT definition
class ZCL_VCXI_XCS_POPUP_TEXTEDIT implementation.
  method CONSTRUCTOR.

    move IF_TEXT to ME->F_TEXT.

  endmethod.
  method DO_FREE.

*** Free Text Editor
    if ME->R_TEXTEDIT is bound.
      ME->R_TEXTEDIT->FREE( exceptions
                              CNTL_ERROR        = 1
                              CNTL_SYSTEM_ERROR = 2 ).

      if SY-SUBRC ne 0.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
      endif.
      clear ME->R_TEXTEDIT.
    endif.

*** Free Container
    if ME->R_CONTAINER is bound.
      ME->R_CONTAINER->FREE( exceptions
                               CNTL_ERROR        = 1
                               CNTL_SYSTEM_ERROR = 2
                               others            = 3 ).
      if SY-SUBRC ne 0.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
      endif.
      clear ME->R_CONTAINER.
    endif.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Set PF-Status
    ME->SET_PF_STATUS( ).

*** Refresh Textedit
    ME->REFRESH_TEXTEDIT( ).

  endmethod.
  method DO_USER_COMMAND.

    case IF_UCOMM.
      when C_UC_OKAY.
        ME->HANDLE_UC_OKAY( ).

      when C_UC_CANCEL.
        ME->HANDLE_UC_CANCEL( ).

    endcase.

  endmethod.
  method HANDLE_UC_CANCEL.

*** Leave Popup
    leave to screen 0.

  endmethod.
  method HANDLE_UC_OKAY.

*** Leave Popup
    leave to screen 0.

  endmethod.
  method INIT_CONTAINER.

    create object ME->R_CONTAINER
      exporting
        CONTAINER_NAME              = IF_CONTAINER_NAME
      exceptions
        CNTL_ERROR                  = 1
        CNTL_SYSTEM_ERROR           = 2
        CREATE_ERROR                = 3
        LIFETIME_ERROR              = 4
        LIFETIME_DYNPRO_DYNPRO_LINK = 5
        others                      = 6.
    if SY-SUBRC ne 0.
      /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
    endif.

  endmethod.
  method INIT_TEXTEDIT.

    check ME->R_CONTAINER is bound.

    create object R_TEXTEDIT
      exporting
        PARENT                 = ME->R_CONTAINER
      exceptions
        ERROR_CNTL_CREATE      = 1
        ERROR_CNTL_INIT        = 2
        ERROR_CNTL_LINK        = 3
        ERROR_DP_CREATE        = 4
        GUI_TYPE_NOT_SUPPORTED = 5
        others                 = 6.
    if SY-SUBRC <> 0.
      /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
    endif.

    check ME->R_TEXTEDIT is bound.
    ME->R_TEXTEDIT->SET_READONLY_MODE( READONLY_MODE = CL_GUI_TEXTEDIT=>TRUE ).

  endmethod.
  method REFRESH_TEXTEDIT.

*** Init Container and TextEditor
    ME->INIT_CONTAINER( ).
    ME->INIT_TEXTEDIT( ).

    check R_TEXTEDIT is bound.

***----------------------------------------------------------------------------
*** Set Text to Text Editor
    ME->R_TEXTEDIT->SET_TEXTSTREAM( exporting
                                      TEXT                   = ME->F_TEXT
                                    exceptions
                                      ERROR_CNTL_CALL_METHOD = 1
                                      NOT_SUPPORTED_BY_GUI   = 2
                                      others                 = 3 ).
    if SY-SUBRC ne 0.
      /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
    endif.

*** Synchronize Automation Queue after Get Methods
    CL_GUI_CFW=>FLUSH( ).

  endmethod.
  method SET_PF_STATUS.

    set pf-status '0100' of program 'SAPLZVCXI_F0S_POPUP_TEXTEDIT'.

  endmethod.
  method START.

    data: LR_POPUP_TEXTEDIT type ref to ZCL_VCXI_XCS_POPUP_TEXTEDIT.

*** Create Instance
    create object LR_POPUP_TEXTEDIT
      exporting
        IF_TEXT = IF_TEXT.

*** Call Popup
    call function 'ZVCXI_XCS_POPUP_TEXTEDIT_START'
      exporting
        IR_POPUP_TEXTEDIT = LR_POPUP_TEXTEDIT.

*** Free the Popup
    LR_POPUP_TEXTEDIT->DO_FREE( ).

  endmethod.
