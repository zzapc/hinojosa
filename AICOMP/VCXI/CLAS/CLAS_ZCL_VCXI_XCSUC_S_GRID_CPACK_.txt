
class ZCL_VCXI_XCSUC_S_GRID_CPACK definition
  public
  inheriting from ZCL_VCXI_XCSUC_S_GRID
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IF_CONTAINER_NAME type SCRFNAME optional
      !IR_CUSTOMIZING type ref to OBJECT
    raising
      /VCXI/CX_CKX .

  methods DO_FREE
    redefinition .
  methods REFRESH_S_GRID
    redefinition .
  methods CHECK_CHANGED_DATA
    redefinition .
protected section.

  data F_GPSUN type ZVCXI_XCSU_GPSUN .
  data R_CPACK type ref to ZCL_VCXI_XCSUC_CPACK .

  methods HANDLE_DOUBLE_CLICK
    for event DOUBLE_CLICK of ZCL_VCXI_XCSUC_GRID_CP_SHUTY
    importing
      !IF_GPSUN .

  methods INIT_GRID
    redefinition .
private section.

  data R_GRID_LEFT type ref to ZCL_VCXI_XCSUC_GRID_CP_SHUTY .
  data R_GRID_RIGHT type ref to ZCL_VCXI_XCSUC_GRID_CP_PMATY .
endclass. "ZCL_VCXI_XCSUC_S_GRID_CPACK definition
class ZCL_VCXI_XCSUC_S_GRID_CPACK implementation.
method CHECK_CHANGED_DATA.

  if ME->R_GRID_LEFT is bound.
    ME->R_GRID_LEFT->CHECK_CHANGED_DATA( ).
  endif.

  if ME->R_GRID_RIGHT is bound.
    ME->R_GRID_RIGHT->CHECK_CHANGED_DATA( ).
  endif.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IF_CONTAINER_NAME = '0100CC_CPACK_GRID'
                      IR_CUSTOMIZING    =  IR_CUSTOMIZING ).

*** Try to Assign Customizing Reference
  try.
      move IR_CUSTOMIZING ?to ME->R_CPACK.
    catch CX_SY_MOVE_CAST_ERROR.
***   Packaging Code Customizing Reference can't be assigned.
      /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE(
          IF_MSGTY = 'E'
          IF_MSGID = 'ZVCXI_XCSUC'
          IF_MSGNO = '102' ).
  endtry.

endmethod.
method DO_FREE.

***------------------------------------------------------------------
*** Free Left Grid
  if ME->R_GRID_LEFT is bound.
    ME->R_GRID_LEFT->DO_FREE( ).
    clear ME->R_GRID_LEFT.
  endif.

***------------------------------------------------------------------
*** Free Right Grid
  if ME->R_GRID_RIGHT is bound.
    ME->R_GRID_RIGHT->DO_FREE( ).
    clear ME->R_GRID_RIGHT.
  endif.

***------------------------------------------------------------------
*** Free Container
  SUPER->DO_FREE( ).

endmethod.
method HANDLE_DOUBLE_CLICK.

*** Set GUID of Choosen Entry to Packaging Code - Packaging Material Grid
  move IF_GPSUN to ME->R_GRID_RIGHT->F_GPSUN.
  move IF_GPSUN to ME->F_GPSUN.
  move ABAP_TRUE to ME->R_GRID_RIGHT->F_REFRESH_ALL_REQ.
  move ABAP_TRUE to ME->R_GRID_RIGHT->F_REFRESH_REQ.
  CL_GUI_CFW=>SET_NEW_OK_CODE( 'REFRESH' ).

endmethod.
method INIT_GRID.

  if ME->R_GRID_LEFT is not bound.
    create object ME->R_GRID_LEFT
      exporting
        IR_CUSTOMIZING = ME->R_CPACK
        IR_S_CONTAINER = ME->R_CONTAINER_LEFT.
  endif.

  if ME->R_GRID_RIGHT is not bound.
    create object ME->R_GRID_RIGHT
      exporting
        IR_CUSTOMIZING = ME->R_CPACK
        IR_S_CONTAINER = ME->R_CONTAINER_RIGHT.
  endif.

  set handler ME->HANDLE_DOUBLE_CLICK for ME->R_GRID_LEFT.

endmethod.
method REFRESH_S_GRID.

  data:   LF_WIDTH             type I.

  if ME->F_REFRESH_ALL_REQ eq ABAP_TRUE.
    ME->DO_FREE( ).
  endif.

  ME->INIT_CONTAINER( ).

*** Init Grid
  ME->INIT_GRID( ).

*** Do Refresh
  if ME->R_GRID_LEFT is bound.
    move ABAP_TRUE to ME->R_GRID_LEFT->F_REFRESH_REQ.
    if ME->F_REFRESH_ALL_REQ = ABAP_TRUE.
      move ABAP_TRUE to ME->R_GRID_LEFT->F_REFRESH_ALL_REQ.
    endif.
    ME->R_GRID_LEFT->REFRESH_GRID( ).
  endif.

  if ME->R_GRID_RIGHT is bound.
    move ME->F_GPSUN to ME->R_GRID_RIGHT->F_GPSUN.
    move ABAP_TRUE to ME->R_GRID_RIGHT->F_REFRESH_REQ.
    if ME->F_REFRESH_ALL_REQ = ABAP_TRUE.
      move ABAP_TRUE to ME->R_GRID_RIGHT->F_REFRESH_ALL_REQ.
    endif.
    ME->R_GRID_RIGHT->REFRESH_GRID( ).
  endif.

  read table ME->R_CPACK->T_CPACK_SHUTY with key GPSUN = ME->F_GPSUN transporting no fields.
  if SY-SUBRC ne 0.
    clear: ME->F_GPSUN.
  endif.

*** Change Container Settings
  if ME->F_GPSUN is initial.
    clear LF_WIDTH.
  else.
*** Default Width
    move 50 to LF_WIDTH.
  endif.

  ME->R_SPLITTER->SET_COLUMN_WIDTH(
    exporting
      ID                = 2
      WIDTH             = LF_WIDTH
    exceptions
      CNTL_ERROR        = 1
      CNTL_SYSTEM_ERROR = 2
      others            = 3 ).
  if SY-SUBRC ne 0.
    /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
  endif.

  move ABAP_FALSE to ME->F_REFRESH_ALL_REQ.

endmethod.
