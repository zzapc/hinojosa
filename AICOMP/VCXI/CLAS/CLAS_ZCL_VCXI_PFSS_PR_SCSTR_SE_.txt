
class ZCL_VCXI_PFSS_PR_SCSTR_SE definition
  public
  inheriting from ZCL_VCXI_PFSS_PR_SCSTR
  create public .

public section.

  methods CHECK_SEP_SCSTR
    redefinition .
  methods DO_RECALC
    redefinition .
protected section.

  data R_PR_SCSTR_MGR_SE type ref to ZCL_VCXI_PFSS_PR_SCSTR_MGR_SE .

  methods HANDLE_INIT_REFERENCES
    redefinition .
private section.
endclass. "ZCL_VCXI_PFSS_PR_SCSTR_SE definition
class ZCL_VCXI_PFSS_PR_SCSTR_SE implementation.
method CHECK_SEP_SCSTR.
*** 0 - Score String containes only valid Separators.
*** 1 - Score String containes invalid Separators.

*** Do Standard checks if we have Components
  if ME->R_PR_SCSTR_MGR_SE is bound and
     ME->R_PR_SCSTR_MGR_SE->F_NO_CALC eq ABAP_FALSE.

    SUPER->CHECK_SEP_SCSTR( importing EF_RETURN  = EF_RETURN
                                      ET_INDIC_I = ET_INDIC_I
                                      ET_INDIC_V = ET_INDIC_V ).

  else.
    move 0 to EF_RETURN.
  endif.

endmethod.
method DO_RECALC.

***------------------------------------------------------------------------------------------------
*** Check if BL Score Operation is bound
  check ME->R_BL_SCSOP is bound.
  ME->R_BL_SCSOP->DO_RECALC( IT_PRVDR = IT_PRVDR ).

***------------------------------------------------------------------------------------------------
*** Set Values
  ME->SET_DIVDI( IF_DIVDI = ME->R_BL_SCSOP->F_DIVDI ).

  if ( IF_SPLIT         eq ABAP_FALSE                                    and
       IF_NO_CALC       eq ABAP_FALSE                                  ) or
       ME->GET_SCSOP( ) eq ZCL_VCXI_P0SS_BL_SCSOP_CUT=>C_SCSOP_CUTTING .
    ME->SET_SCSTR( IF_SCSTR = ME->R_BL_SCSOP->F_SCSTR ).
  endif.

  ME->SET_SHTWI( IS_SHTWI = ME->R_BL_SCSOP->S_SHTWI ).
  ME->SET_SHTLE( IS_SHTLE = ME->R_BL_SCSOP->S_SHTLE ).

endmethod.
method HANDLE_INIT_REFERENCES.

  data:  LT_GATE            type        /VCXI/VKSR_TT_GATE.
  data:  LR_GATE            type ref to /VCXI/CL_VKSR_GATE.

*** Do Super
  SUPER->HANDLE_INIT_REFERENCES( IT_PRVDR = IT_PRVDR ).

***------------------------------------------------------------------------------------------------
*** Get Score String Manager Reference
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_PFSS_PR_SCSTR_MGR_SE=>C_IPRVT_PF_SCSTR_MGR_SE
                                   IF_INUSE = ABAP_TRUE ).

  read table LT_GATE into LR_GATE index 1.
  try.
      if LR_GATE          is bound and
       LR_GATE->R_CLSPR is bound.
        move LR_GATE->R_CLSPR ?to ME->R_PR_SCSTR_MGR_SE.
      endif.
    catch CX_SY_MOVE_CAST_ERROR.
      clear: ME->R_PR_SCSTR_MGR_SE.
  endtry.

  if ME->R_PR_SCSTR_MGR_SE is not bound.
*** Provider &1 is not available for &2.
    /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE(
        IF_MSGTY = 'E'
        IF_MSGID = 'ZVCXI_XCS'
        IF_MSGNO = '010'
        IF_MSGV1 = ZCL_VCXI_PFSS_PR_SCSTR_MGR_SE=>C_IPRVT_PF_SCSTR_MGR_SE
        IF_MSGV2 = ME->R_GATE->F_ID ).
  endif.

endmethod.
endclass. "ZCL_VCXI_PFSS_PR_SCSTR_SE implementation
