
class ZCL_VCXI_P0SP_GRID_PCOUT definition
  public
  inheriting from ZCL_VCXI_XCS_GRID_PRVDR
  create public .

public section.

  type-pools ABAP .
  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABNAME type TABNAME default 'ZVCXI_P0SP_S_PCOUT_GRID'
      !IF_CONTAINER_NAME type SCRFNAME
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL default ABAP_TRUE
    raising
      /VCXI/CX_CKX .
protected section.

  methods CHANGE_STYLE_PCOUT
    changing
      !CT_STYL type LVC_T_STYL
    raising
      /VCXI/CX_CKX .
  methods GET_PCOUT_VIS
    importing
      !IR_PR_PCOUT type ref to ZCL_VCXI_P0SP_PR_PCOUT
    returning
      value(RS_PCOUT_VIS) type ZVCXI_P0SP_S_PCOUT_VIS
    raising
      /VCXI/CX_CKX .
  methods SET_PCOUT_VIS
    importing
      !IR_PR_PCOUT type ref to ZCL_VCXI_P0SP_PR_PCOUT
      !IS_PCOUT_VIS type ZVCXI_P0SP_S_PCOUT_VIS
    raising
      /VCXI/CX_CKX .

  methods GET_FIELDCAT
    redefinition .
  methods GET_GRID_DATA
    redefinition .
  methods HANDLE_DATA_CHANGED_FINISHED
    redefinition .
private section.
endclass. "ZCL_VCXI_P0SP_GRID_PCOUT definition
class ZCL_VCXI_P0SP_GRID_PCOUT implementation.
method CHANGE_STYLE_PCOUT.

  free CT_STYL.

*** Set PC Type = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PCTYP'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set PC Type Description = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PCTYP_DESCR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set PC Length = Editable if needed
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PCLEN'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set PC Length Unit = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PCLEN_UOM'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set PC Width = Editable if needed
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PCWID'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set PC Width Unit = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PCWID_UOM'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set PC Height = Editable if needed
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PCHEI'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set PC Height Unit = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PCHEI_UOM'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set PC Ups = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PCUPS'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Number of Outs = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'NOOUT'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set PC Direction = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PCDIR'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_GATE           = IR_GATE
                      IR_UIGATE         = IR_UIGATE
                      IF_TABNAME        = IF_TABNAME
                      IF_CONTAINER_NAME = IF_CONTAINER_NAME
                      IF_APPL_EVENTS    = IF_APPL_EVENTS ).

*** Define Provider Type for Button
  ME->F_IPRVT = ZCL_VCXI_P0SP_PR_PCOUT=>C_IPRVT_P0_PCOUT.

endmethod.
method GET_FIELDCAT.

  data:          LF_LOCKED type        /VCXI/CKX_LOCKED,
                 LF_INUSE  type        /VCXI/VKSR_INUSE.
  field-symbols: <S_FCAT>  type        LVC_S_FCAT.

***----------------------------------------------------------------------------
*** Get Standard Field Catalog
  RT_FCAT = SUPER->GET_FIELDCAT( ).

*** Get Lock and In-Use Status
  LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
  LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***----------------------------------------------------------------------------
*** Adjust Field Catalog
  loop at RT_FCAT assigning <S_FCAT>.
    case <S_FCAT>-FIELDNAME.
      when 'POSID' or 'GATKY' or 'R_GATE' or 'T_STYL'.
        move ABAP_TRUE    to <S_FCAT>-TECH.

      when 'PCTYP'.
        if LF_LOCKED eq ABAP_TRUE and
           LF_INUSE  eq ABAP_TRUE.
          move ABAP_TRUE    to <S_FCAT>-EDIT.
        endif.

      when 'PCLEN' or 'PCWID' or 'PCHEI'.
        if LF_LOCKED eq ABAP_TRUE and
           LF_INUSE  eq ABAP_TRUE.
          move ABAP_TRUE    to <S_FCAT>-EDIT.
        endif.

      when 'PCUPS'.
        if LF_LOCKED eq ABAP_TRUE and
           LF_INUSE  eq ABAP_TRUE.
          move ABAP_TRUE    to <S_FCAT>-EDIT.
        endif.

      when 'PCDIR'.
        if LF_LOCKED eq ABAP_TRUE and
           LF_INUSE  eq ABAP_TRUE.
          move ABAP_TRUE    to <S_FCAT>-EDIT.
        endif.

      when 'NOOUT'.
        if LF_LOCKED eq ABAP_TRUE and
           LF_INUSE  eq ABAP_TRUE.
          move ABAP_TRUE    to <S_FCAT>-EDIT.
        endif.

    endcase.
  endloop.

endmethod.
method GET_GRID_DATA.

  data:          LS_PCOUT_VIS          type          ZVCXI_P0SP_S_PCOUT_VIS,
                 LS_PCOUT_GRID         type          ZVCXI_P0SP_S_PCOUT_GRID.
  data:          LT_GATE               type          /VCXI/VKSR_TT_GATE.
  data:          LR_GATE               type ref to   /VCXI/CL_VKSR_GATE,
                 LR_PR_PCOUT           type ref to   ZCL_VCXI_P0SP_PR_PCOUT.
  field-symbols: <T_DATA>              type standard table,
                 <S_DATA>              type          any.

*** Dereferencing of Data Reference
  assign IR_DATA->* to <T_DATA>.

*** Get Gates of MC Ink
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_P0SP_PR_PCOUT=>C_IPRVT_P0_PCOUT
                                   IF_INUSE  = ABAP_TRUE
                                   IF_SORTED = ABAP_TRUE ).

  loop at LT_GATE into LR_GATE.
    clear: LS_PCOUT_GRID.
    try.
        move LR_GATE->R_CLSPR ?to LR_PR_PCOUT.
        check LR_PR_PCOUT is bound.

*** --------------------------------------------------------------------------------------------
***     Get Data from PR Class
        move LR_GATE->GET_POSID( ) to LS_PCOUT_GRID-POSID.
        move LR_GATE->F_GATKY      to LS_PCOUT_GRID-GATKY.
        move LR_GATE               to LS_PCOUT_GRID-R_GATE.

        LS_PCOUT_VIS = ME->GET_PCOUT_VIS( IR_PR_PCOUT = LR_PR_PCOUT ).
        move-corresponding LS_PCOUT_VIS to LS_PCOUT_GRID.

*** --------------------------------------------------------------------------------------------
***     Set Styles
        ME->CHANGE_STYLE_PCOUT( changing CT_STYL = LS_PCOUT_GRID-T_STYL ).

***     Call DVC Service - Modify Style
        ZCL_VCXI_XCDR_SERVICE=>MODIFY_STYLE( exporting IR_GATE = LR_GATE
                                             changing  CT_STYL = LS_PCOUT_GRID-T_STYL ).

*** --------------------------------------------------------------------------------------------
***     Add Entry
        append initial line to <T_DATA> assigning <S_DATA>.
        move-corresponding LS_PCOUT_GRID to <S_DATA>.

      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.
  endloop.

endmethod.
method GET_PCOUT_VIS.

  data:  LS_PCDIM type ZVCXI_P0SP_S_PCDIM.

*** Get PC Dimension
  move IR_PR_PCOUT->GET_PCDIM( )       to LS_PCDIM.
  move-corresponding LS_PCDIM          to RS_PCOUT_VIS.

** Get PC Type Description
  move IR_PR_PCOUT->GET_PCTYP_DESCR( ) to RS_PCOUT_VIS-PCTYP_DESCR.

** Get Number Outs
  move IR_PR_PCOUT->GET_NOOUT( )       to RS_PCOUT_VIS-NOOUT.

endmethod.
method HANDLE_DATA_CHANGED_FINISHED.

  data:          LS_PCOUT_VIS        type          ZVCXI_P0SP_S_PCOUT_VIS,
                 LS_PCOUT_GRID       type          ZVCXI_P0SP_S_PCOUT_GRID.
  data:          LR_PR_PCOUT         type ref to   ZCL_VCXI_P0SP_PR_PCOUT,
                 LR_CX_CKX           type ref to   /VCXI/CX_CKX.
  field-symbols: <T_DATA>            type standard table,
                 <S_DATA>            type          any.

  check E_MODIFIED eq ABAP_TRUE.

  try.

      SUPER->HANDLE_DATA_CHANGED_FINISHED( E_MODIFIED    = E_MODIFIED
                                           ET_GOOD_CELLS = ET_GOOD_CELLS ).

***   Dereferencing of Data Reference
      assign ME->R_DATA->* to <T_DATA>.

      loop at <T_DATA> assigning <S_DATA>.
        move-corresponding <S_DATA> to LS_PCOUT_GRID.
        check LS_PCOUT_GRID-R_GATE is bound.

        move LS_PCOUT_GRID-R_GATE->R_CLSPR ?to LR_PR_PCOUT.

***     Set Values to PR Class
        move-corresponding LS_PCOUT_GRID to LS_PCOUT_VIS.
        ME->SET_PCOUT_VIS( IR_PR_PCOUT  = LR_PR_PCOUT
                           IS_PCOUT_VIS = LS_PCOUT_VIS ).
      endloop.

      ME->R_GATE->RAISE_EVENTS( ).

    catch /VCXI/CX_CKX into LR_CX_CKX.
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

*** Request Refresh for ALV
  move ABAP_TRUE to ME->F_REFRESH_REQ.

endmethod.
method SET_PCOUT_VIS.

  data:  LS_PCOUT_VIS   type ZVCXI_P0SP_S_PCOUT_VIS,
         LS_PCLEN       type ZVCXI_P0SP_S_PCLEN,
         LS_PCWID       type ZVCXI_P0SP_S_PCWID,
         LS_PCHEI       type ZVCXI_P0SP_S_PCHEI.

*** Get Current data to do Update only if Value changed.
  LS_PCOUT_VIS = ME->GET_PCOUT_VIS( IR_PR_PCOUT = IR_PR_PCOUT ).

***----------------------------------------------------------------------------
*** Set PC length
  if LS_PCOUT_VIS-PCLEN ne IS_PCOUT_VIS-PCLEN.
    move-corresponding IS_PCOUT_VIS to LS_PCLEN.
    IR_PR_PCOUT->SET_PCLEN( IS_PCLEN = LS_PCLEN
                            IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Set PC Width
  if LS_PCOUT_VIS-PCWID ne IS_PCOUT_VIS-PCWID.
    move-corresponding IS_PCOUT_VIS to LS_PCWID.
    IR_PR_PCOUT->SET_PCWID( IS_PCWID = LS_PCWID
                            IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Set PC Height
  if LS_PCOUT_VIS-PCHEI ne IS_PCOUT_VIS-PCHEI.
    move-corresponding IS_PCOUT_VIS to LS_PCHEI.
    IR_PR_PCOUT->SET_PCHEI( IS_PCHEI = LS_PCHEI
                            IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Set PC Ups
  if LS_PCOUT_VIS-PCUPS ne IS_PCOUT_VIS-PCUPS.
    IR_PR_PCOUT->SET_PCUPS( IF_PCUPS = IS_PCOUT_VIS-PCUPS
                            IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Set PC Direction
  if LS_PCOUT_VIS-PCDIR ne IS_PCOUT_VIS-PCDIR.
    IR_PR_PCOUT->SET_PCDIR( IF_PCDIR = IS_PCOUT_VIS-PCDIR
                            IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Set PC Type
  if LS_PCOUT_VIS-PCTYP ne IS_PCOUT_VIS-PCTYP.
    IR_PR_PCOUT->SET_PCTYP( IF_PCTYP = IS_PCOUT_VIS-PCTYP
                            IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Set Number Out
  if LS_PCOUT_VIS-NOOUT ne IS_PCOUT_VIS-NOOUT.
    IR_PR_PCOUT->SET_NOOUT( IF_NOOUT = IS_PCOUT_VIS-NOOUT
                            IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

endmethod.
