
class ZCL_VCXI_P3SS_BL_ASBPROP definition
  public
  inheriting from ZCL_VCXI_PXSS_BL_XBPROP
  create protected .

public section.

  data S_PRODIM type ZVCXI_PXS_S_PRODIM read-only .

  class-methods FACTORY
    importing
      !IF_ASBPX type ZVCXI_P3S_ASBPX optional
    returning
      value(RR_BL_ASBPROP) type ref to ZCL_VCXI_P3SS_BL_ASBPROP
    raising
      /VCXI/CX_CKX .
  methods GET_ASBPX
    returning
      value(RF_ASBPX) type ZVCXI_P3S_ASBPX
    raising
      /VCXI/CX_CKX .
  methods MERGE
    importing
      !IR_BL_ASBPROP type ref to ZCL_VCXI_P3SS_BL_ASBPROP
    raising
      /VCXI/CX_CKX .
  methods SET_ASBPX
    importing
      !IF_ASBPX type ZVCXI_P3S_ASBPX
    raising
      /VCXI/CX_CKX .
  methods SET_PRODIM
    importing
      !IS_PRODIM type ZVCXI_PXS_S_PRODIM
    raising
      /VCXI/CX_CKX .
protected section.

  methods GET_XML_DREF
    redefinition .
private section.
endclass. "ZCL_VCXI_P3SS_BL_ASBPROP definition
class ZCL_VCXI_P3SS_BL_ASBPROP implementation.
  method FACTORY.

*** Create BL Reference
ENHANCEMENT-SECTION FACTORY SPOTS ZVCXI_P3SS_ES_BL_ASBPROP .
    create object RR_BL_ASBPROP.
end-enhancement-section.

*** Set Assembly Backward Property XML
    RR_BL_ASBPROP->SET_ASBPX( IF_ASBPX = IF_ASBPX ).

  endmethod.
  method GET_ASBPX.

    data: LT_DATA type ABAP_TRANS_SRCBIND_TAB.

***--------------------------------------------------------------------------------------
*** Get Data References for XML Serialize/Deserialize
    LT_DATA = ME->GET_XML_DREF( ).
    check LT_DATA is not initial.

*** Serialize to XML
    call transformation ID
         source (LT_DATA)
         result xml RF_ASBPX.

  endmethod.
  method GET_XML_DREF.

    RT_DATA = SUPER->GET_XML_DREF( ).

    append value #( NAME = 'S_PRODIM'   VALUE = ref #( ME->S_PRODIM   ) ) to RT_DATA.

  endmethod.
  method MERGE.

***--------------------------------------------------------------------------------------
*** Cost Split Distribution
    ME->MERGE_CSPLIT( IT_CSPLIT = IR_BL_ASBPROP->T_CSPLIT ).

***--------------------------------------------------------------------------------------
*** Product Dimension
    ME->SET_PRODIM( IS_PRODIM = IR_BL_ASBPROP->S_PRODIM ).

  endmethod.
  method SET_ASBPX.

    data: LT_DATA type ABAP_TRANS_SRCBIND_TAB.


***--------------------------------------------------------------------------------------
*** Deserialize XML
    if IF_ASBPX is not initial.

***   Get Data References for XML Serialize/Deserialize
      LT_DATA = ME->GET_XML_DREF( ).
      check LT_DATA is not initial.

***   Open XML
      call transformation ID
           source xml IF_ASBPX
           result (LT_DATA).

    endif.

  endmethod.
  method SET_PRODIM.

    move IS_PRODIM to ME->S_PRODIM.

  endmethod.
