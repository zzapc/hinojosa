
class ZCL_VCXI_PCSP_GRID_MFSO_R definition
  public
  inheriting from ZCL_VCXI_XCS_GRID_PRVDR
  create public .

public section.

  type-pools ABAP .
  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_CONTAINER_NAME type SCRFNAME
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL default ABAP_TRUE
    raising
      /VCXI/CX_CKX .
protected section.

  methods GET_MFSO_R_VIS
    importing
      !IR_PR_MFSO_R type ref to ZCL_VCXI_PCSP_PR_MFSO_R
    returning
      value(RS_MFSO_R_VIS) type ZVCXI_PCSP_S_MFSO_R_VIS
    raising
      /VCXI/CX_CKX .
  methods CHANGE_STYLE_MFSO_R
    changing
      !CT_STYL type LVC_T_STYL
    raising
      /VCXI/CX_CKX .
  methods SET_MFSO_R_VIS
    importing
      !IR_PR_MFSO_R type ref to ZCL_VCXI_PCSP_PR_MFSO_R
      !IS_MFSO_R_VIS type ZVCXI_PCSP_S_MFSO_R_VIS
    raising
      /VCXI/CX_CKX .

  methods GET_FIELDCAT
    redefinition .
  methods GET_GRID_DATA
    redefinition .
  methods HANDLE_DATA_CHANGED_FINISHED
    redefinition .
private section.
endclass. "ZCL_VCXI_PCSP_GRID_MFSO_R definition
class ZCL_VCXI_PCSP_GRID_MFSO_R implementation.
method CHANGE_STYLE_MFSO_R.

  free CT_STYL.

*** Set ID of MF x-connection = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'IMFXC'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

***--------------------------------------------------------------------------------------------
*** Set Style Details -> all other DISABLED because nothing needs to be edit in MFSO
*** Set Sheet Matnr = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MATNR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Consumption = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'CONSU'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Material Flow Ratio = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MFRAT'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Low Level Code = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'DISST'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Flute = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'FLUTE'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set PC Type = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PCTYP'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Roll length = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'ROLEN'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Roll width = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'ROWID'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Roll Diameter = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'RODIA'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set PC Ups = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PCUPS'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Number of Outs = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'NOOUT'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set PC Direction = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PCDIR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_GATE           = IR_GATE
                      IR_UIGATE         = IR_UIGATE
                      IF_TABNAME        = 'ZVCXI_PCSP_S_MFSO_R_GRID'
                      IF_CONTAINER_NAME = IF_CONTAINER_NAME
                      IF_APPL_EVENTS    = IF_APPL_EVENTS ).

*** Define Provider Type for Button
  ME->F_IPRVT = ZCL_VCXI_PCSP_PR_MFSO_R=>C_IPRVT_PC_MFSO_R.

endmethod.
method GET_FIELDCAT.

  data:          LF_LOCKED   type        /VCXI/CKX_LOCKED,
                 LF_INUSE    type        /VCXI/VKSR_INUSE.
  field-symbols: <S_FCAT>    type        LVC_S_FCAT.

  RT_FCAT = SUPER->GET_FIELDCAT( ).

*** Get Lock and In-Use Status
  LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
  LF_INUSE  = ME->R_GATE->IS_INUSE( ).

  loop at RT_FCAT assigning <S_FCAT>.
    case <S_FCAT>-FIELDNAME.
      when 'POSID' or 'GATKY'.
        move 'X' to <S_FCAT>-TECH.

***   ID for MF x-connect
      when 'IMFXC'.
        if LF_LOCKED eq ABAP_TRUE and
           LF_INUSE  eq ABAP_TRUE.
          move ABAP_TRUE    to <S_FCAT>-EDIT.
        endif.

    endcase.
  endloop.

endmethod.
method GET_GRID_DATA.

  data:          LS_MFSO_R_VIS        type          ZVCXI_PCSP_S_MFSO_R_VIS,
                 LS_MFSO_R_GRID       type          ZVCXI_PCSP_S_MFSO_R_GRID.
  data:          LT_GATE              type          /VCXI/VKSR_TT_GATE.
  data:          LR_GATE              type ref to   /VCXI/CL_VKSR_GATE,
                 LR_PR_MFSO_R         type ref to   ZCL_VCXI_PCSP_PR_MFSO_R.
  field-symbols: <T_DATA>             type standard table,
                 <S_DATA>             type          any.


*** Dereferencing of Data Reference
  assign IR_DATA->* to <T_DATA>.

*** Get Gates of MFSO_R
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_PCSP_PR_MFSO_R=>C_IPRVT_PC_MFSO_R
                                   IF_INUSE  = ABAP_TRUE
                                   IF_SORTED = ABAP_TRUE ).

  loop at LT_GATE into LR_GATE.
    try.
        move LR_GATE->R_CLSPR ?to LR_PR_MFSO_R.
        check LR_PR_MFSO_R is bound.
        clear: LS_MFSO_R_GRID.

***------------------------------------------------------------------------------------------------
***     Get Data from PR Class
        move LR_GATE->GET_POSID( ) to LS_MFSO_R_GRID-POSID.
        move LR_GATE->F_GATKY      to LS_MFSO_R_GRID-GATKY.
        move LR_GATE               to LS_MFSO_R_GRID-R_GATE.

        LS_MFSO_R_VIS = ME->GET_MFSO_R_VIS( IR_PR_MFSO_R = LR_PR_MFSO_R ).
        move-corresponding LS_MFSO_R_VIS to LS_MFSO_R_GRID.

***------------------------------------------------------------------------------------------------
***     Change Style
***     Set Style Details -> all DISABLED because nothing needs to be edit in MFSO
        ME->CHANGE_STYLE_MFSO_R( changing CT_STYL = LS_MFSO_R_GRID-T_STYL ).

***     Call DVC Service - Modify Style
        ZCL_VCXI_XCDR_SERVICE=>MODIFY_STYLE( exporting IR_GATE = LR_GATE
                                             changing  CT_STYL = LS_MFSO_R_GRID-T_STYL ).

***------------------------------------------------------------------------------------------------
***     Add Entry
        append initial line to <T_DATA> assigning <S_DATA>.
        move-corresponding LS_MFSO_R_GRID to <S_DATA>.

      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.
  endloop.

endmethod.
method GET_MFSO_R_VIS.

  data:  LS_CONSU type ZVCXI_XCSP_S_CONSU,
         LS_RODIM type ZVCXI_PCSP_S_RODIM.

*** Get MaterialNumber, Description, Low Level Code and X-connect
  move IR_PR_MFSO_R->GET_MATNR( )       to RS_MFSO_R_VIS-MATNR.
  move IR_PR_MFSO_R->GET_MATNR_DESCR( ) to RS_MFSO_R_VIS-MATNR_DESCR.
  move IR_PR_MFSO_R->GET_DISST( )       to RS_MFSO_R_VIS-DISST.
  move IR_PR_MFSO_R->GET_IMFXC( )       to RS_MFSO_R_VIS-IMFXC.

*** Get Consumption
  move IR_PR_MFSO_R->GET_CONSU( )       to LS_CONSU.
  move-corresponding LS_CONSU           to RS_MFSO_R_VIS.

*** MF Ratio
  move IR_PR_MFSO_R->GET_MFRAT( )       to RS_MFSO_R_VIS-MFRAT.

*** Get Flute
  move IR_PR_MFSO_R->GET_FLUTE( )       to RS_MFSO_R_VIS-FLUTE.

*** Get Roll Dimensions
  move IR_PR_MFSO_R->GET_RODIM( )       to LS_RODIM.
  move-corresponding LS_RODIM           to RS_MFSO_R_VIS.

endmethod.
method HANDLE_DATA_CHANGED_FINISHED.

  data:          LS_MFSO_R_VIS    type          ZVCXI_PCSP_S_MFSO_R_VIS,
                 LS_MFSO_R_GRID   type          ZVCXI_PCSP_S_MFSO_R_GRID.
  data:          LR_PR_MFSO_R     type ref to   ZCL_VCXI_PCSP_PR_MFSO_R,
                 LR_CX_CKX        type ref to   /VCXI/CX_CKX.
  field-symbols: <T_DATA>         type standard table,
                 <S_DATA>         type          any.

  check E_MODIFIED eq ABAP_TRUE.

  try.

      SUPER->HANDLE_DATA_CHANGED_FINISHED( E_MODIFIED    = E_MODIFIED
                                           ET_GOOD_CELLS = ET_GOOD_CELLS ).

***   Dereferencing of Data Reference
      assign ME->R_DATA->* to <T_DATA>.

      loop at <T_DATA> assigning <S_DATA>.
        move-corresponding <S_DATA> to LS_MFSO_R_GRID.
        check LS_MFSO_R_GRID-R_GATE is bound.

        move LS_MFSO_R_GRID-R_GATE->R_CLSPR ?to LR_PR_MFSO_R.

***     Set Values to PR Class
        move-corresponding LS_MFSO_R_GRID to LS_MFSO_R_VIS.
        ME->SET_MFSO_R_VIS( IR_PR_MFSO_R  = LR_PR_MFSO_R
                            IS_MFSO_R_VIS = LS_MFSO_R_VIS ).
      endloop.


      ME->R_GATE->RAISE_EVENTS( ).

    catch /VCXI/CX_CKX into LR_CX_CKX.
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

*** Request Refresh for ALV
  move ABAP_TRUE to ME->F_REFRESH_REQ.

endmethod.
method SET_MFSO_R_VIS.

  data:  LS_MFSO_R_VIS     type ZVCXI_PCSP_S_MFSO_R_VIS.

*** Get Current data to do Update only if Value changed.
  LS_MFSO_R_VIS = ME->GET_MFSO_R_VIS( IR_PR_MFSO_R = IR_PR_MFSO_R ).

*** ID for MF x-connection
  if LS_MFSO_R_VIS-IMFXC ne IS_MFSO_R_VIS-IMFXC.
    IR_PR_MFSO_R->SET_IMFXC( IF_IMFXC = IS_MFSO_R_VIS-IMFXC
                             IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

endmethod.
