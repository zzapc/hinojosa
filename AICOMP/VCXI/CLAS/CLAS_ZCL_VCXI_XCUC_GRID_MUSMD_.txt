
class ZCL_VCXI_XCUC_GRID_MUSMD definition
  public
  final
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_MUCEL type ref to ZCL_VCXI_XCUC_MUCEL
    raising
      /VCXI/CX_CKX .
  methods DO_FREE
    raising
      /VCXI/CX_CKX .
  methods REFRESH_GRID
    raising
      /VCXI/CX_CKX .
protected section.

  data T_MUSMD_GRID type ZVCXI_XCUC_TT_MUSMD_GRID .
  data R_CONTAINER type ref to CL_GUI_CUSTOM_CONTAINER .
  data R_GRID type ref to CL_GUI_ALV_GRID .
  data T_FIELDCAT type LVC_T_FCAT .
  data R_MUCEL type ref to ZCL_VCXI_XCUC_MUCEL .

  methods BUILD_FIELDCAT
    raising
      /VCXI/CX_CKX .
  methods GET_LAYOUT
    returning
      value(RS_LAYOUT) type LVC_S_LAYO
    raising
      /VCXI/CX_CKX .
  methods HANDLE_TOOLBAR
    for event TOOLBAR of CL_GUI_ALV_GRID
    importing
      !E_OBJECT
      !E_INTERACTIVE .
  methods HANDLE_UCOMM
    for event USER_COMMAND of CL_GUI_ALV_GRID
    importing
      !E_UCOMM .
  methods HANDLE_UC_ADD
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_DEL
    raising
      /VCXI/CX_CKX .
  methods INIT_CONTAINER
    raising
      /VCXI/CX_CKX .
  methods INIT_DATA
    raising
      /VCXI/CX_CKX .
  methods INIT_GRID
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_XCUC_GRID_MUSMD definition
class ZCL_VCXI_XCUC_GRID_MUSMD implementation.
  method BUILD_FIELDCAT .

    call function 'LVC_FIELDCATALOG_MERGE'
      exporting
        I_STRUCTURE_NAME       = 'ZVCXI_XCUC_S_MUSMD_GRID'
      changing
        CT_FIELDCAT            = ME->T_FIELDCAT
      exceptions
        INCONSISTENT_INTERFACE = 1
        PROGRAM_ERROR          = 2
        others                 = 3.

    if SY-SUBRC ne 0.
      /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
    endif.

  endmethod.
  method CONSTRUCTOR.

    ME->R_MUCEL = IR_MUCEL.

  endmethod.
method DO_FREE .

*** Check for Changes
  if ME->R_GRID is bound.
    ME->R_GRID->CHECK_CHANGED_DATA( ).
  endif.

  if ME->R_GRID is bound.
    ME->R_GRID->FREE( exceptions
                        CNTL_ERROR        = 1
                        CNTL_SYSTEM_ERROR = 2
                        others            = 3 ).
    if SY-SUBRC <> 0.
      /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
    endif.
    clear ME->R_GRID.
  endif.

  if ME->R_CONTAINER is bound.
    ME->R_CONTAINER->FREE( exceptions
                             CNTL_ERROR        = 1
                             CNTL_SYSTEM_ERROR = 2
                             others            = 3 ).
    if SY-SUBRC <> 0.
      /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
    endif.
    clear ME->R_CONTAINER.
  endif.

  clear ME->R_MUCEL.

endmethod.
  method GET_LAYOUT.

    RS_LAYOUT-CWIDTH_OPT = 'X'.
    RS_LAYOUT-SEL_MODE   = 'A'.

  endmethod.
  method HANDLE_TOOLBAR.

    data:  LF_DISABLED type ABAP_BOOL.

    try.
***     Lock Status
        if ME->R_MUCEL->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ) eq ABAP_FALSE.
          move ABAP_TRUE to LF_DISABLED.
        endif.

***----------------------------------------------------------------------------
***     Insert Toolbar elements
        append value #( FUNCTION  = 'ADD'
                        TEXT      = 'Add'(F01)
                        ICON      = ICON_INSERT_ROW
                        DISABLED  = LF_DISABLED
                      ) to E_OBJECT->MT_TOOLBAR.

        append value #( FUNCTION  = 'DEL'
                        TEXT      = 'Delete'(F02)
                        ICON      = ICON_DELETE_ROW
                        DISABLED  = LF_DISABLED
                      ) to E_OBJECT->MT_TOOLBAR.

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        LR_CX_CKX->SHOW_MESSAGE( ).
    endtry.

  endmethod.
  method HANDLE_UC_ADD.

    data: LT_MUSMD         type        /VCXI/CKX_TT_OBJECT.
    data: LR_ELMTY  type ref to /VCXI/CL_CKR_ELMTY,
          LR_CMODEL type ref to /VCXI/CL_CKMC_CMODEL,
          LR_RECORD type ref to /VCXI/CL_CKMR_RECORD.

***--------------------------------------------------------------------------------------
*** Set Registry Element Type
    LR_ELMTY ?= /VCXI/CL_CKR_ELMTY=>/VCXI/IF_CKX_OBJECT~GET_INSTANCE( IF_GUID = conv /VCXI/CKX_GUID( /VCXI/CL_CKMC_CMODEL=>C_ELMTY_CMODEL ) ).
    check LR_ELMTY is bound.

*** Start Search Help
    LR_CMODEL ?= /VCXI/CL_CKRU_SHLP_REGELMNT=>CALL_SHLP( IT_ELMTY = value #( ( LR_ELMTY ) ) ).
    check LR_CMODEL is bound.

***------------------------------------------------------------------------------------------------
*** Add selected SmartMD Model
    LT_MUSMD = ME->R_MUCEL->T_MUSMD.
    append LR_CMODEL to LT_MUSMD.
    ME->R_MUCEL->SET_MUSMD( IT_MUSMD = LT_MUSMD ).

  endmethod.
  method HANDLE_UC_DEL.

    data: LS_ROW   type LVC_S_ROW.
    data: LT_ROW   type LVC_T_ROW,
          LT_MUSMD type /VCXI/CKX_TT_OBJECT.


*** Get selected Rows
    ME->R_GRID->GET_SELECTED_ROWS( importing ET_INDEX_ROWS = LT_ROW ).

    LT_MUSMD = ME->R_MUCEL->T_MUSMD.

*** Remove selected SmartMD Model
    loop at LT_ROW into LS_ROW.
      delete LT_MUSMD where TABLE_LINE eq ME->T_MUSMD_GRID[ LS_ROW-INDEX ]-R_CMODEL.
    endloop.

*** Set remaining SmartMD Models
    ME->R_MUCEL->SET_MUSMD( IT_MUSMD = LT_MUSMD ).

  endmethod.
method HANDLE_UCOMM.

  try.
      case E_UCOMM.
        when 'ADD'.
          ME->HANDLE_UC_ADD( ).
        when 'DEL'.
          ME->HANDLE_UC_DEL( ).
      endcase.

    catch /VCXI/CX_CKX into data(LR_CX_CKX).
      LR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

endmethod.
method INIT_CONTAINER .

  if ME->R_CONTAINER is not bound.
    create object ME->R_CONTAINER
      exporting
        CONTAINER_NAME              = '0140CC_MUSMD'
      exceptions
        CNTL_ERROR                  = 1
        CNTL_SYSTEM_ERROR           = 2
        CREATE_ERROR                = 3
        LIFETIME_ERROR              = 4
        LIFETIME_DYNPRO_DYNPRO_LINK = 5
        others                      = 6.
    if SY-SUBRC ne 0.
      /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
    endif.
  endif.

endmethod.
  method INIT_DATA.

    data: LR_CMODEL type ref to /VCXI/CL_CKMC_CMODEL,
          LR_OBJECT type ref to /VCXI/IF_CKX_OBJECT.

    clear ME->T_MUSMD_GRID.

    loop at ME->R_MUCEL->T_MUSMD into LR_OBJECT.
      LR_CMODEL ?= LR_OBJECT.

      append value #( R_CMODEL = LR_CMODEL
                      ICMDL    = LR_CMODEL->/VCXI/IF_CKX_OBJECT~GET_ID( )
                      DESCR    = LR_CMODEL->/VCXI/IF_CKX_OBJECT~GET_DESCR( )
                    ) to ME->T_MUSMD_GRID.
    endloop.

  endmethod.
method INIT_GRID .

  if ME->R_GRID is not bound.
    create object ME->R_GRID
      exporting
        I_PARENT          = ME->R_CONTAINER
        I_APPL_EVENTS     = 'X'
      exceptions
        ERROR_CNTL_CREATE = 1
        ERROR_CNTL_INIT   = 2
        ERROR_CNTL_LINK   = 3
        ERROR_DP_CREATE   = 4
        others            = 5.
    if SY-SUBRC ne 0.
      /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
    endif.

*** Set Eventhandler for Toolbar
    set handler ME->HANDLE_TOOLBAR             for ME->R_GRID.
    set handler ME->HANDLE_UCOMM               for ME->R_GRID.

  endif.

endmethod.
  method REFRESH_GRID .

*** Initialized Data
    ME->INIT_DATA( ).

*** Build Field Catalog
    ME->BUILD_FIELDCAT( ).

*** Initial Display
    if ME->R_GRID is not bound.
      ME->INIT_CONTAINER( ).
      ME->INIT_GRID( ).

      ME->R_GRID->SET_TABLE_FOR_FIRST_DISPLAY( exporting
                                                 IT_TOOLBAR_EXCLUDING          = value #( ( CL_GUI_ALV_GRID=>MC_FC_EXCL_ALL ) )
                                                 IS_LAYOUT                     = ME->GET_LAYOUT( )
                                               changing
                                                 IT_FIELDCATALOG               = ME->T_FIELDCAT
                                                 IT_OUTTAB                     = ME->T_MUSMD_GRID
                                               exceptions
                                                 INVALID_PARAMETER_COMBINATION = 1
                                                 PROGRAM_ERROR                 = 2
                                                 TOO_MANY_LINES                = 3
                                                 others                        = 4 ).
      if SY-SUBRC ne 0.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
      endif.

*** Update of Data
    else.
      ME->R_GRID->SET_FRONTEND_FIELDCATALOG( IT_FIELDCATALOG = ME->T_FIELDCAT ).
      ME->R_GRID->SET_FRONTEND_LAYOUT( IS_LAYOUT = ME->GET_LAYOUT( ) ).
      ME->R_GRID->REFRESH_TABLE_DISPLAY( exceptions FINISHED = 1 ).
      if SY-SUBRC ne 0.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
      endif.
    endif.

  endmethod.
