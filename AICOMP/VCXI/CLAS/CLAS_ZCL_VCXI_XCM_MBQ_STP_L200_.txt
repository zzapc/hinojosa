
class ZCL_VCXI_XCM_MBQ_STP_L200 definition
  public
  inheriting from ZCL_VCXI_XCM_STP_L000
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_RPSEG type ref to /VCXI/CL_CKMR_RPSEG
    raising
      /VCXI/CX_CKX .
protected section.

  data R_DCO_INPUT type ref to ZCL_VCXI_XCM_MBQ_DCO_SDOC .
  data R_DCO_OUTPUT type ref to ZCL_VCXI_XCM_MBQ_DCO_SDOC .

  methods CHECK_ACTCON
    importing
      !IT_ACTCON type ZVCXI_XCM_MBQ_TT_ACTCON
    exporting
      !EF_FULL_MATLI type ABAP_BOOL
    returning
      value(RF_RELEVANT) type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
  methods SORT_MATLI
    importing
      !IT_MATLI type ZVCXI_XCM_MBQ_TT_MATLI
    returning
      value(RT_MATLI) type ZVCXI_XCM_MBQ_TT_MATLI .

  methods ANALYZE_RESULT_DCO
    redefinition .
  methods GET_SUBPROCESS_DCO
    redefinition .
private section.
endclass. "ZCL_VCXI_XCM_MBQ_STP_L200 definition
class ZCL_VCXI_XCM_MBQ_STP_L200 implementation.
  method ANALYZE_RESULT_DCO.

    data: LR_DCO_SDOC type ref to ZCL_VCXI_XCM_MBQ_DCO_SDOC.


*** Map Resulting DCO of SubProcess
    IR_RPROC->MAP_DCO( changing CR_DCO = LR_DCO_SDOC ).
    check LR_DCO_SDOC is bound.

*** Update active Material List entry only
    ME->R_DCO_OUTPUT->UPDATE_MATLI( IS_MATLI = LR_DCO_SDOC->T_MATLI[ MLCNT = LR_DCO_SDOC->F_MLCNT ] ).

  endmethod.
  method CHECK_ACTCON.

*** Check Relevancy
    check LINE_EXISTS( IT_ACTCON[ ACTID = ZCL_VCXI_XCM_MBQ_CUST=>C_ACTID_MAT  ] ) or
          LINE_EXISTS( IT_ACTCON[ ACTID = ZCL_VCXI_XCM_MBQ_CUST=>C_ACTID_BOM  ] ) or
          LINE_EXISTS( IT_ACTCON[ ACTID = ZCL_VCXI_XCM_MBQ_CUST=>C_ACTID_RTE  ] ) or
          LINE_EXISTS( IT_ACTCON[ ACTID = ZCL_VCXI_XCM_MBQ_CUST=>C_ACTID_PVER ] ) or
          LINE_EXISTS( IT_ACTCON[ ACTID = ZCL_VCXI_XCM_MBQ_CUST=>C_ACTID_MCL  ] ) or
          LINE_EXISTS( IT_ACTCON[ ACTID = ZCL_VCXI_XCM_MBQ_CUST=>C_ACTID_QMAT ] ) or
          LINE_EXISTS( IT_ACTCON[ ACTID = ZCL_VCXI_XCM_MBQ_CUST=>C_ACTID_QPLN ] ).

    RF_RELEVANT = ABAP_TRUE.

  endmethod.
  method CONSTRUCTOR.

*** Super Constructor
    SUPER->CONSTRUCTOR( IR_RPSEG = IR_RPSEG ).

*** Map DCO to Required Input/Output
    ME->R_RPSEG->MAP_DCO( changing CR_DCO = ME->R_DCO_INPUT ).
    ME->R_RPSEG->MAP_DCO( changing CR_DCO = ME->R_DCO_OUTPUT ).

  endmethod.
  method GET_SUBPROCESS_DCO.

    data: LF_FULL_MATLI type ABAP_BOOL.
    data: LS_MATLI type ZVCXI_XCM_MBQ_S_MATLI.
    data: LT_MATLI type ZVCXI_XCM_MBQ_TT_MATLI.
    data: LR_DCO_SDOC type ref to ZCL_VCXI_XCM_MBQ_DCO_SDOC.


***--------------------------------------------------------------------------------------
*** Sort Material List
    LT_MATLI = ME->SORT_MATLI( IT_MATLI = ME->R_DCO_INPUT->T_MATLI ).

***--------------------------------------------------------------------------------------
*** Create subprocess for each Material
    loop at LT_MATLI into LS_MATLI.
***   Check for relevant Activitiy
      check ME->CHECK_ACTCON( exporting IT_ACTCON     = LS_MATLI-T_ACTCON
                              importing EF_FULL_MATLI = LF_FULL_MATLI ).

***   Copy DCO by Serialize-Deserialize
      LR_DCO_SDOC ?= /VCXI/CL_CKMR_CLDCO=>DESERIALIZE( IF_XMLDC = ME->R_DCO_INPUT->SERIALIZE( ) ).

***   Set only current Material into Material List or full list if requested by Activity
      if LF_FULL_MATLI ne ABAP_TRUE.
        LR_DCO_SDOC->SET_MATLI( IT_MATLI = value #( ( LS_MATLI ) ) ).
      else.
        LR_DCO_SDOC->SET_MATLI( IT_MATLI = LT_MATLI ).
      endif.
      LR_DCO_SDOC->SET_MLCNT( IF_MLCNT = LS_MATLI-MLCNT ).

***   Add DCO to return list
      append LR_DCO_SDOC to RT_DCO.
    endloop.

  endmethod.
  method SORT_MATLI.

    RT_MATLI = IT_MATLI.
    sort RT_MATLI by DISST.

  endmethod.
