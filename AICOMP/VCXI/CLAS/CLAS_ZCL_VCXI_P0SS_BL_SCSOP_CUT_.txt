
class ZCL_VCXI_P0SS_BL_SCSOP_CUT definition
  public
  inheriting from ZCL_VCXI_P0SS_BL_SCSOP
  create public .

public section.

  constants C_SCSOP_CUTTING type ZVCXI_P0S_SCSOP value 'CUT'. "#EC NOTEXT

  methods CONSTRUCTOR
    importing
      !IF_SHTDI type ZVCXI_P0SS_SHTDI
      !IF_CSCOR type ZVCXI_P0SS_CSCOR
    raising
      /VCXI/CX_CKX .
protected section.

  methods CALC_SCSTR
    redefinition .
  methods GET_DIVDI
    redefinition .
  methods READ_SHT_DIM
    redefinition .
private section.
endclass. "ZCL_VCXI_P0SS_BL_SCSOP_CUT definition
class ZCL_VCXI_P0SS_BL_SCSOP_CUT implementation.
method CALC_SCSTR.

  data:          LF_PARID_TRIM  type        /VCXI/VKS_PARID.
  data:          LS_PCDIM_IN    type        ZVCXI_P0SP_S_PCDIM,
                 LS_TRIRB       type        ZVCXI_P0SS_S_TRIRB,
                 LS_TRILT       type        ZVCXI_P0SS_S_TRILT.
  data:          LT_SCSTR_CALC  type        ZVCXI_P0SS_TT_SCSTR_CALC.
  data:          LR_SHTDIM      type ref to ZIF_VCXI_P0SS_SHTDIM.
  field-symbols: <S_SCSTR_CALC> type        ZVCXI_P0SS_S_SCSTR_CALC.

***------------------------------------------------------------------------------------------------
*** Get Sheet Dimension Net Without Trim
  LR_SHTDIM = ME->GET_SHTDIM_PRVDR( IT_PRVDR = IT_PRVDR ).
  check LR_SHTDIM is bound.
  LS_PCDIM_IN = LR_SHTDIM->GET_PCDIM( IF_POOTI = ZIF_VCXI_P0SS_SHTDIM=>C_POOTI_NET ).

***------------------------------------------------------------------------------------------------
*** 1 As there is no need of a Score String we just populate the Sheet Dimensions!!
  append initial line to LT_SCSTR_CALC assigning <S_SCSTR_CALC>.

  case ME->F_SHTDI.
    when 'A'.
      move LS_PCDIM_IN-PCWID     to <S_SCSTR_CALC>-SCORP.
      move LS_PCDIM_IN-PCWID_UOM to <S_SCSTR_CALC>-SCORP_UOM.
    when 'B'.
      move LS_PCDIM_IN-PCLEN     to <S_SCSTR_CALC>-SCORP.
      move LS_PCDIM_IN-PCLEN_UOM to <S_SCSTR_CALC>-SCORP_UOM.
  endcase.

***------------------------------------------------------------------------------------------------
*** 2 Get the production trim side or length and add it
  case ME->F_SHTDI.
    when 'A'.
***   Load Side Trim
      LF_PARID_TRIM = ZCL_VCXI_P0SS_PR_SCSTR_MGR=>C_TRIM_PARID_TRIPS.
    when 'B'.
***   Load Length Trim
      LF_PARID_TRIM = ZCL_VCXI_P0SS_PR_SCSTR_MGR=>C_TRIM_PARID_TRIPL.
  endcase.

  IR_PR_SCSTR_MGR->GET_TRIM( exporting IF_PARID    = LF_PARID_TRIM
                             importing ES_TRILT_IO = LS_TRILT
                                       ES_TRIRB_IO = LS_TRIRB ).

  ME->ADD_TRIM( exporting IS_TRILT      = LS_TRILT
                          IS_TRIRB      = LS_TRIRB
                changing  CT_SCSTR_CALC = LT_SCSTR_CALC ).

***------------------------------------------------------------------------------------------------
*** 3 Define the right Score String Values ( SCSTR_VALUE ) and return the Hole string
  RF_SCSTR = ME->BUILD_SCSTR( IF_SSTRC      = IR_PR_SCSTR_MGR->F_SSTRC
                              IT_SCSTR_CALC = LT_SCSTR_CALC ).

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IF_SHTDI = IF_SHTDI
                      IF_CSCOR = IF_CSCOR ).

*** Set Score Operation
  move C_SCSOP_CUTTING to ME->F_SCSOP.

endmethod.
method GET_DIVDI.

*** Nothing to do!

endmethod.
method READ_SHT_DIM.

  data: LS_PCDIM_IN  type        ZVCXI_P0SP_S_PCDIM.
  data: LR_SHTDIM    type ref to ZIF_VCXI_P0SS_SHTDIM.

***------------------------------------------------------------------------------------------------
*** Get Sheet Dimension Net With Trim
  LR_SHTDIM = ME->GET_SHTDIM_PRVDR( IT_PRVDR = IT_PRVDR ).
  check LR_SHTDIM is bound.
  LS_PCDIM_IN = LR_SHTDIM->GET_PCDIM( IF_POOTI = ZIF_VCXI_P0SS_SHTDIM=>C_POOTI_NETWT ).

  move LS_PCDIM_IN-PCLEN     to ES_SHTLE-SHTLE.
  move LS_PCDIM_IN-PCLEN_UOM to ES_SHTLE-SHTLE_UOM.

  move LS_PCDIM_IN-PCWID     to ES_SHTWI-SHTWI.
  move LS_PCDIM_IN-PCWID_UOM to ES_SHTWI-SHTWI_UOM.

endmethod.
