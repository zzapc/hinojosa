
class ZCL_VCXI_FFSP_GRID_MC_RECI definition
  public
  inheriting from ZCL_VCXI_XCS_GRID_PRVDR
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_CONTAINER_NAME type SCRFNAME optional
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL default ABAP_TRUE
      !IF_TABNAME type TABNAME default 'ZVCXI_FFSP_S_MC_RECI_GRID'
    raising
      /VCXI/CX_CKX .
protected section.

  methods CHANGE_STYLE_MC_RECI
    changing
      !CT_STYL type LVC_T_STYL
    raising
      /VCXI/CX_CKX .
  methods GET_MC_RECI_VIS
    importing
      !IR_PR_MC_RECI type ref to ZCL_VCXI_FFSP_PR_MC_RECI
    returning
      value(RS_MC_RECI_VIS) type ZVCXI_FFSP_S_MC_RECI_VIS
    raising
      /VCXI/CX_CKX .
  methods SET_MC_RECI_VIS
    importing
      !IR_PR_MC_RECI type ref to ZCL_VCXI_FFSP_PR_MC_RECI
      !IS_MC_RECI_VIS type ZVCXI_FFSP_S_MC_RECI_VIS
    raising
      /VCXI/CX_CKX .

  methods GET_FIELDCAT
    redefinition .
  methods GET_GRID_DATA
    redefinition .
  methods HANDLE_DATA_CHANGED_FINISHED
    redefinition .
private section.
endclass. "ZCL_VCXI_FFSP_GRID_MC_RECI definition
class ZCL_VCXI_FFSP_GRID_MC_RECI implementation.
method CHANGE_STYLE_MC_RECI.

  free CT_STYL.

*** --------------------------------------------------------------------------------------------
*** Set Style Details
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MATNR'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Text = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'TEXT'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** --------------------------------------------------------------------------------------------
*** Set Vendor Price and Price Unit = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** --------------------------------------------------------------------------------------------
*** Set Average Consumption = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'AVGCO'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Average Consumption per = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'AVGCP'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_GATE           = IR_GATE
                      IR_UIGATE         = IR_UIGATE
                      IF_TABNAME        = IF_TABNAME
                      IF_CONTAINER_NAME = IF_CONTAINER_NAME
                      IF_APPL_EVENTS    = IF_APPL_EVENTS ).

*** Define Provider Type for Button
  ME->F_IPRVT = ZCL_VCXI_FFSP_PR_MC_RECI=>C_IPRVT_MC_RECI.

endmethod.
method GET_FIELDCAT.

  data: LF_LOCKED type        /VCXI/CKX_LOCKED,
        LF_INUSE  type        /VCXI/VKSR_INUSE,
        LF_COLPOS type        I.
  field-symbols: <S_FCAT>  type        LVC_S_FCAT.

*** -------------------------------------------------------------------------------------
*** Get Standard Field Catalog
  RT_FCAT = SUPER->GET_FIELDCAT( ).

*** Get Lock and In-Use Status
  LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
  LF_INUSE  = ME->R_GATE->IS_INUSE( ).

*** -------------------------------------------------------------------------------------
*** Adjust Field Catalog
  loop at RT_FCAT assigning <S_FCAT>.
    case <S_FCAT>-FIELDNAME.
      when 'POSID' or 'GATKY' or 'R_GATE' or 'T_STYL'.
        move ABAP_TRUE    to <S_FCAT>-TECH.

***   Set Edit for all fields which should Editable at all
      when 'MATNR' or 'AVGCO' or 'AVGCP' or 'TEXT'.

        if LF_LOCKED eq ABAP_TRUE and
           LF_INUSE  eq ABAP_TRUE.
          move ABAP_TRUE    to <S_FCAT>-EDIT.
        endif.

      when 'SURWT' or 'FITIN' or 'FITEX' or 'EXWID'.
        move ABAP_FALSE to <S_FCAT>-EDIT.

      when 'SURWT_UOM'.
        move ABAP_TRUE to <S_FCAT>-NO_OUT.

      when 'LIFNR' or 'LIFNR_DESCR' or
           'PRICE' or 'PRICE_CURR' or
           'PRICP' or 'PRICP_UOM' or 'PURCH'.
        move ABAP_TRUE    to <S_FCAT>-TECH.

    endcase.

*** Set Position for each Field
    move 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'MATNR'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'MATNR_DESCR'. <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'CONSU'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'CONSU_UOM'.   <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'FITIN'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'FITIN_UOM'.   <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'FITEX'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'FITEX_UOM'.   <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'EXWID'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'EXWID_UOM'.   <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'AVGCO'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'AVGCO_UOM'.   <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'AVGCP'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'AVGCP_UOM'.   <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'SURWT'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'SURWT_UOMC6'. <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'TEXT'.        <S_FCAT>-COL_POS = LF_COLPOS.  endif.

  endloop.

endmethod.
method GET_GRID_DATA.

  data: LS_MC_RECI_VIS    type          ZVCXI_FFSP_S_MC_RECI_VIS,
        LS_MC_RECI_GRID   type          ZVCXI_FFSP_S_MC_RECI_GRID.
  data: LT_GATE           type          /VCXI/VKSR_TT_GATE.
  data: LR_GATE           type ref to   /VCXI/CL_VKSR_GATE,
        LR_PR_MC_RECI     type ref to   ZCL_VCXI_FFSP_PR_MC_RECI.
  field-symbols: <T_DATA> type standard table,
                 <S_DATA> type          ANY.

*** Dereferencing of Data Reference
  assign IR_DATA->* to <T_DATA>.

*** Get Gates of MC RECI
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_XCSP_PR_MC=>C_IPRVT_XCS_MC
                                   IF_INUSE  = ABAP_TRUE
                                   IF_SORTED = ABAP_TRUE ).

  loop at LT_GATE into LR_GATE.
    check LR_GATE->IS_INUSE( ) eq ABAP_TRUE
    and   LR_GATE->R_CLSPR     is bound.

    clear: LS_MC_RECI_GRID.

    try.
        move LR_GATE->R_CLSPR ?to LR_PR_MC_RECI.
        check LR_PR_MC_RECI is bound.

*** -------------------------------------------------------------------------------------
***     Get Data from PR Class
        move LR_GATE->GET_POSID( ) to LS_MC_RECI_GRID-POSID.
        move LR_GATE->F_GATKY      to LS_MC_RECI_GRID-GATKY.
        move LR_GATE               to LS_MC_RECI_GRID-R_GATE.

        LS_MC_RECI_VIS = ME->GET_MC_RECI_VIS( IR_PR_MC_RECI = LR_PR_MC_RECI ).
        move-corresponding LS_MC_RECI_VIS to LS_MC_RECI_GRID.

*** -------------------------------------------------------------------------------------
***     Set Style Details
        ME->CHANGE_STYLE_MC_RECI( changing CT_STYL  = LS_MC_RECI_GRID-T_STYL ).

***     Call DVC Service - Modify Style
        ZCL_VCXI_XCDR_SERVICE=>MODIFY_STYLE( exporting IR_GATE = LR_GATE
                                             changing  CT_STYL = LS_MC_RECI_GRID-T_STYL ).

*** -------------------------------------------------------------------------------------
***     Add Entry
        append initial line to <T_DATA> assigning <S_DATA>.
        move-corresponding LS_MC_RECI_GRID to <S_DATA>.

      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.
  endloop.

endmethod.
method GET_MC_RECI_VIS.

  data: LS_CONSU type ZVCXI_XCSP_S_CONSU,
        LS_PRICE type ZVCXI_XCS_S_PRICE,
        LS_PRICP type ZVCXI_XCS_S_PRICP.

*** Get MaterialNumber and Description
  move IR_PR_MC_RECI->GET_MATNR( )       to RS_MC_RECI_VIS-MATNR.
  move IR_PR_MC_RECI->GET_MATNR_DESCR( ) to RS_MC_RECI_VIS-MATNR_DESCR.

*** Get Consumption
  move IR_PR_MC_RECI->GET_CONSU( )       to LS_CONSU.
  move-corresponding LS_CONSU            to RS_MC_RECI_VIS.

*** Get Average Consumption and Consumption per
  IR_PR_MC_RECI->GET_AVGCO( importing ES_AVGCO = RS_MC_RECI_VIS-S_AVGCO ).
  IR_PR_MC_RECI->GET_AVGCP( importing ES_AVGCP = RS_MC_RECI_VIS-S_AVGCP ).

*** Get Surface Weight
  move IR_PR_MC_RECI->GET_SURWT( ) to RS_MC_RECI_VIS-S_SURWT.
  move RS_MC_RECI_VIS-S_SURWT-SURWT_UOM to RS_MC_RECI_VIS-SURWT_UOMC6.

*** Get Film Thickness Int
  move IR_PR_MC_RECI->GET_FITIN( ) to RS_MC_RECI_VIS-S_FITIN.

*** Get Film Thickness Ext
  move IR_PR_MC_RECI->GET_FITEX( ) to RS_MC_RECI_VIS-S_FITEX.

*** Get Extrusion Width
  IR_PR_MC_RECI->GET_EXWID( importing ES_EXWID = RS_MC_RECI_VIS-S_EXWID ).

*** Get Vendor, Description and Purchase Flag and Text
  move IR_PR_MC_RECI->GET_LIFNR( )       to RS_MC_RECI_VIS-LIFNR.
  move IR_PR_MC_RECI->GET_LIFNR_DESCR( ) to RS_MC_RECI_VIS-LIFNR_DESCR.
  move IR_PR_MC_RECI->GET_PURCH( )       to RS_MC_RECI_VIS-PURCH.
  move IR_PR_MC_RECI->GET_TEXT( )        to RS_MC_RECI_VIS-TEXT.

*** Get Price
  move IR_PR_MC_RECI->GET_PRICE( )       to LS_PRICE.
  move-corresponding LS_PRICE            to RS_MC_RECI_VIS.

*** Get Price Unit
  move IR_PR_MC_RECI->GET_PRICP( )       to LS_PRICP.
  move-corresponding LS_PRICP            to RS_MC_RECI_VIS.

endmethod.
method HANDLE_DATA_CHANGED_FINISHED.

  data: LS_MC_RECI_VIS  type          ZVCXI_FFSP_S_MC_RECI_VIS,
        LS_MC_RECI_GRID type          ZVCXI_FFSP_S_MC_RECI_GRID.
  data: LR_PR_MC_RECI type ref to   ZCL_VCXI_FFSP_PR_MC_RECI,
        LR_CX_CKX     type ref to   /VCXI/CX_CKX.
  field-symbols: <T_DATA> type standard table,
                 <S_DATA> type          ANY.

  check E_MODIFIED eq ABAP_TRUE.

  try.

      SUPER->HANDLE_DATA_CHANGED_FINISHED( E_MODIFIED    = E_MODIFIED
                                           ET_GOOD_CELLS = ET_GOOD_CELLS ).

***   Dereferencing of Data Reference
      assign ME->R_DATA->* to <T_DATA>.

      loop at <T_DATA> assigning <S_DATA>.
        move-corresponding <S_DATA> to LS_MC_RECI_GRID.

        move LS_MC_RECI_GRID-R_GATE->R_CLSPR ?to LR_PR_MC_RECI.

***     Set Values to PR Class
        move-corresponding LS_MC_RECI_GRID to LS_MC_RECI_VIS.
        ME->SET_MC_RECI_VIS( IR_PR_MC_RECI  = LR_PR_MC_RECI
                             IS_MC_RECI_VIS = LS_MC_RECI_VIS ).
      endloop.


      ME->R_GATE->RAISE_EVENTS( ).

    catch /VCXI/CX_CKX into LR_CX_CKX.
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

*** Request Refresh for ALV
  move ABAP_TRUE to ME->F_REFRESH_REQ.

endmethod.
method SET_MC_RECI_VIS.

  data: LS_PRICE       type ZVCXI_XCS_S_PRICE,
        LS_PRICP       type ZVCXI_XCS_S_PRICP,
        LS_MC_RECI_VIS type ZVCXI_FFSP_S_MC_RECI_VIS.

*** Get Current data to do Update only if Value changed.
  LS_MC_RECI_VIS = ME->GET_MC_RECI_VIS( IR_PR_MC_RECI = IR_PR_MC_RECI ).

*** Material
  if LS_MC_RECI_VIS-MATNR ne IS_MC_RECI_VIS-MATNR.
    IR_PR_MC_RECI->SET_MATNR( IF_MATNR = IS_MC_RECI_VIS-MATNR
                              IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Average Consumption & Consumption per
  if LS_MC_RECI_VIS-AVGCO ne IS_MC_RECI_VIS-AVGCO.
    IR_PR_MC_RECI->SET_AVGCO( IS_AVGCO = IS_MC_RECI_VIS-S_AVGCO
                              IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

  if LS_MC_RECI_VIS-AVGCP ne IS_MC_RECI_VIS-AVGCP.
    IR_PR_MC_RECI->SET_AVGCP( IS_AVGCP = IS_MC_RECI_VIS-S_AVGCP
                              IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Text
  if LS_MC_RECI_VIS-TEXT ne IS_MC_RECI_VIS-TEXT.
    IR_PR_MC_RECI->SET_TEXT(  IF_TEXT = IS_MC_RECI_VIS-TEXT
                              IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Purchase Flag
  if LS_MC_RECI_VIS-PURCH ne IS_MC_RECI_VIS-PURCH.
    IR_PR_MC_RECI->SET_PURCH( IF_PURCH = IS_MC_RECI_VIS-PURCH
                              IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Vendor
  if LS_MC_RECI_VIS-LIFNR ne IS_MC_RECI_VIS-LIFNR.
    IR_PR_MC_RECI->SET_LIFNR( IF_LIFNR = IS_MC_RECI_VIS-LIFNR
                              IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Price
  if LS_MC_RECI_VIS-PRICE ne IS_MC_RECI_VIS-PRICE.
    move-corresponding IS_MC_RECI_VIS to LS_PRICE.
    IR_PR_MC_RECI->SET_PRICE( IS_PRICE = LS_PRICE
                              IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

  if LS_MC_RECI_VIS-PRICP ne IS_MC_RECI_VIS-PRICP.
    move-corresponding IS_MC_RECI_VIS to LS_PRICP.
    IR_PR_MC_RECI->SET_PRICP( IS_PRICP = LS_PRICP
                              IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Film Thickness Int
  if LS_MC_RECI_VIS-FITIN ne IS_MC_RECI_VIS-FITIN.
    IR_PR_MC_RECI->SET_FITIN( IS_FITIN = IS_MC_RECI_VIS-S_FITIN
                              IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Film Thickness Ext
  if LS_MC_RECI_VIS-FITEX ne IS_MC_RECI_VIS-FITEX.
    IR_PR_MC_RECI->SET_FITEX( IS_FITEX = IS_MC_RECI_VIS-S_FITEX
                              IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Extrusion Width
  if LS_MC_RECI_VIS-EXWID ne IS_MC_RECI_VIS-EXWID.
    IR_PR_MC_RECI->SET_EXWID( IS_EXWID = IS_MC_RECI_VIS-S_EXWID
                              IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

endmethod.
