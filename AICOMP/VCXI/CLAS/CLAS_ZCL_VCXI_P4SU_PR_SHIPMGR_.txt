
class ZCL_VCXI_P4SU_PR_SHIPMGR definition
  public
  inheriting from ZCL_VCXI_PXSU_PR_SHIPMGR
  create public .

public section.

  methods GET_BASE_DIMEN
    redefinition .
  methods GET_BASE_PRQTY
    redefinition .
  methods GET_NOUPS
    redefinition .
  methods GET_WERKS
    redefinition .
protected section.
private section.
endclass. "ZCL_VCXI_P4SU_PR_SHIPMGR definition
class ZCL_VCXI_P4SU_PR_SHIPMGR implementation.
  method GET_BASE_DIMEN.

    data: LS_HNDDIM type ZVCXI_PXS_S_HNDDIM,
          LS_PROWT  type ZVCXI_PXS_S_PROWT.
    data: LR_PR_PRFG     type ref to ZCL_VCXI_P4SS_PR_PRFG,
          LR_PR_AREA_WGT type ref to ZCL_VCXI_PXSS_PR_AREA_WGT.


***--------------------------------------------------------------------------------------
*** Get Handling Dimensions
    case type of ME->R_GATE->GET_PARENT( )->R_CLSPR.
      when type ZCL_VCXI_P4SS_PR_PRFG into LR_PR_PRFG.
        LS_HNDDIM = LR_PR_PRFG->ZIF_VCXI_PXSS_FG~GET_HNDDIM( ).
    endcase.

*** Set Dimensions
    RT_DIMEN = value #( ( DIMEN     = ZCL_VCXI_XCSU_CUST_SHIPU=>C_DIMEN_LENGTH
                          DIMVA     = LS_HNDDIM-HNDLE
                          DIMVA_UOM = LS_HNDDIM-HNDLE_UOM )
                        ( DIMEN     = ZCL_VCXI_XCSU_CUST_SHIPU=>C_DIMEN_WIDTH
                          DIMVA     = LS_HNDDIM-HNDWI
                          DIMVA_UOM = LS_HNDDIM-HNDWI_UOM )
                        ( DIMEN     = ZCL_VCXI_XCSU_CUST_SHIPU=>C_DIMEN_HEIGHT
                          DIMVA     = LS_HNDDIM-HNDHE
                          DIMVA_UOM = LS_HNDDIM-HNDHE_UOM )
                       ).

***--------------------------------------------------------------------------------------
*** Add Product Weight
    LR_PR_AREA_WGT = ME->GET_PRVDR_AREA_WGT( ).
    if LR_PR_AREA_WGT is bound.
      LS_PROWT = LR_PR_AREA_WGT->GET_PROWT( ).

      append value #( DIMEN     = ZCL_VCXI_XCSU_CUST_SHIPU=>C_DIMEN_WEIGHT
                      DIMVA     = LS_PROWT-PROWT
                      DIMVA_UOM = LS_PROWT-PROWT_UOM
                    ) to RT_DIMEN.
    endif.

  endmethod.
  method GET_BASE_PRQTY.

    RS_PRQTY-PRQTY     = 1.
    RS_PRQTY-PRQTY_UOM = 'ST'.

  endmethod.
  method GET_NOUPS.

    RF_NOUPS = 1.

  endmethod.
  method GET_WERKS.

*** Get Handling Dimensions
    case type of ME->R_GATE->GET_PARENT( )->R_CLSPR.
      when type ZCL_VCXI_P4SS_PR_PRFG into data(LR_PR_PRFG).
        RF_WERKS = LR_PR_PRFG->GET_WERKS( ).
    endcase.

  endmethod.
endclass. "ZCL_VCXI_P4SU_PR_SHIPMGR implementation
