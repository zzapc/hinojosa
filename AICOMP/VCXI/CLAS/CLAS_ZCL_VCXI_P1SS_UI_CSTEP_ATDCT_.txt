
class ZCL_VCXI_P1SS_UI_CSTEP_ATDCT definition
  public
  inheriting from ZCL_VCXI_P1SS_UI_CSTEP
  create public .

public section.

  data S_CSTEP_ATDCT_VIS type ZVCXI_P1SS_S_CSTEP_ATDCT_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  data R_PR_CSTEP_ATDCT type ref to ZCL_VCXI_P1SS_PR_CSTEP_ATDCT .
  data R_GRID_MS_ATDCTT type ref to ZCL_VCXI_PXSS_GRID_MS_ATDCTT .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_P1SS_UI_CSTEP_ATDCT definition
class ZCL_VCXI_P1SS_UI_CSTEP_ATDCT implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

***--------------------------------------------------------------------------------------
*** Bind UI to Dynpro
    call function 'ZVCXI_P1SS_UI_CSTEP_ATDCT_BIND'
      exporting
        IR_UI_CSTEP_ATDCT = ME
      importing
        ES_DYNP           = ME->S_DYNP.

***--------------------------------------------------------------------------------------
*** Create Grid for Additional Materials
    create object ME->R_GRID_MS_ATDCTT
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_MS_ATDCTT'.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_CSTEP_ATDCT.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

    if ME->R_GRID_MS_ATDCTT is bound.
      ME->R_GRID_MS_ATDCTT->DO_FREE( ).
      clear ME->R_GRID_MS_ATDCTT.
    endif.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Super PBO
    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize Grid
    if ME->R_GRID_MS_ATDCTT is bound.
      ME->R_GRID_MS_ATDCTT->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_MS_ATDCTT->REFRESH_GRID( ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_CSTEP_ATDCT_VIS type ZVCXI_P1SS_S_CSTEP_ATDCT_VIS.


*** Call SUPER
    SUPER->DO_UPDATE_VIS( IS_CSTEP_VIS = IS_CSTEP_VIS ).

    check ME->R_PR_CSTEP_ATDCT is bound.

*** "Cast" Vis Structure
    move-corresponding IS_CSTEP_VIS to LS_CSTEP_ATDCT_VIS.

***--------------------------------------------------------------------------------------
*** Set Sheet Direction
    if ME->S_CSTEP_ATDCT_VIS-SHTDI ne LS_CSTEP_ATDCT_VIS-SHTDI.
      ME->R_PR_CSTEP_ATDCT->SET_SHTDI( IF_SHTDI = LS_CSTEP_ATDCT_VIS-SHTDI
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

***--------------------------------------------------------------------------------------
*** Process Changes in Grid
    if ME->R_GRID_MS_ATDCTT is bound.
      ME->R_GRID_MS_ATDCTT->CHECK_CHANGED_DATA( ).
    endif.

***--------------------------------------------------------------------------------------
*** Call SUPER
    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_CSTEP_ATDCT.

  endmethod.
  method INIT_VIS.

    SUPER->INIT_VIS( ).

    free: ME->S_CSTEP_ATDCT_VIS.
    ME->S_CSTEP_ATDCT_VIS-S_CSTEP_VIS = ME->S_CSTEP_VIS.

***--------------------------------------------------------------------------------------
*** Sheet Direction
    ME->S_CSTEP_ATDCT_VIS-SHTDI   = ME->R_PR_CSTEP_ATDCT->GET_SHTDI( ).

  endmethod.
