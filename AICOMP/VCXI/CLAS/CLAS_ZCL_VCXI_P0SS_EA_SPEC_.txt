
class ZCL_VCXI_P0SS_EA_SPEC definition
  public
  inheriting from /VCXI/CL_VKSR_CLSEA
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods GET_MAIN_JOINT
    exporting
      value(EF_JOITY) type ZVCXI_P0S_JOITY
      value(EF_JOITY_DESCR) type /VCXI/CKX_DESCR
      value(EF_JOIPO) type ZVCXI_P0S_JOIPO
      value(EF_JOIPO_DESCR) type /VCXI/CKX_DESCR
      value(ES_JOIWI) type ZVCXI_P0S_S_JOIWI
      value(ES_JOILE) type ZVCXI_P0S_S_JOILE
      value(EF_NOSTI) type ZVCXI_P0SS_NOSTI
      value(ES_STITP) type ZVCXI_P0SS_S_STITP
    raising
      /VCXI/CX_CKX .
  methods GET_CUSTMAT_INFO
    exporting
      value(EF_CMATN) type ZVCXI_XCSS_CMATN
      value(EF_MATDE) type ZVCXI_XCSS_MATDE
    raising
      /VCXI/CX_CKX .
  methods GET_PROD_DIM
    exporting
      value(ES_PROLE) type ZVCXI_P0S_S_PROLE
      value(ES_PROWI) type ZVCXI_P0S_S_PROWI
      value(ES_PROHE) type ZVCXI_P0S_S_PROHE
    raising
      /VCXI/CX_CKX .
  methods GET_PRSTY
    exporting
      value(EF_TPRST) type ZVCXI_P0SS_TPRST
      value(EF_PRSTY) type ZVCXI_P0SS_PRSTY
      value(EF_PRSTY_DESCR) type /VCXI/CKX_DESCR
      value(EF_BOXPA) type ZVCXI_P0SS_BOXPA
      value(EF_BOXPA_DESCR) type /VCXI/CKX_DESCR
    raising
      /VCXI/CX_CKX .
  methods GET_PTYPE
    exporting
      value(EF_PTYPE) type ZVCXI_P0S_PTYPE
      value(EF_PTYPE_DESCR) type /VCXI/CKX_DESCR
    raising
      /VCXI/CX_CKX .
  type-pools ABAP .
  methods SET_CUSTMAT_INFO
    importing
      !IF_CMATN type ZVCXI_XCSS_CMATN optional
      !IF_MATDE type ZVCXI_XCSS_MATDE optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
      !IF_RAISE_EVENTS type ABAP_BOOL default ABAP_TRUE
    raising
      /VCXI/CX_CKX .
  methods SET_PROD_DIM
    importing
      !IS_PROLE type ZVCXI_P0S_S_PROLE optional
      !IS_PROWI type ZVCXI_P0S_S_PROWI optional
      !IS_PROHE type ZVCXI_P0S_S_PROHE optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
      !IF_RAISE_EVENTS type ABAP_BOOL default ABAP_TRUE
    raising
      /VCXI/CX_CKX .
  methods SET_PRSTY
    importing
      !IF_PRSTY type ZVCXI_P0SS_PRSTY
      !IF_BOXPA type ZVCXI_P0SS_BOXPA optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
      !IF_RAISE_EVENTS type ABAP_BOOL default ABAP_TRUE
    raising
      /VCXI/CX_CKX .
  methods SET_PTYPE
    importing
      !IF_PTYPE type ZVCXI_P0S_PTYPE
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
      !IF_RAISE_EVENTS type ABAP_BOOL default ABAP_TRUE
    raising
      /VCXI/CX_CKX .
protected section.

  data R_PR_SPEC type ref to ZCL_VCXI_P0SS_PR_SPEC .
private section.
endclass. "ZCL_VCXI_P0SS_EA_SPEC definition
class ZCL_VCXI_P0SS_EA_SPEC implementation.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_GATE = IR_GATE ).

*** Assign the PR Class to the Element API Class
  try.
      move ME->R_GATE->R_CLSPR ?to ME->R_PR_SPEC.
    catch CX_SY_MOVE_CAST_ERROR.
***   Element API ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '804'.
  endtry.

endmethod.
method GET_CUSTMAT_INFO.

  check ME->R_PR_SPEC is bound.

***------------------------------------------------------------------
*** Get Customer Material Number
  if EF_CMATN is requested.
    EF_CMATN = ME->R_PR_SPEC->GET_CMATN( ).
  endif.

*** Get Customer Material Description
  if EF_MATDE is requested.
    EF_MATDE = ME->R_PR_SPEC->GET_MATDE( ).
  endif.

endmethod.
method GET_MAIN_JOINT.

  check ME->R_PR_SPEC is bound.

***----------------------------------------------------------------------------
*** Get Joint Type
  if EF_JOITY is requested.
    move ME->R_PR_SPEC->GET_JOITY( )       to EF_JOITY.
  endif.

  if EF_JOITY_DESCR is requested.
    move ME->R_PR_SPEC->GET_JOITY_DESCR( ) to EF_JOITY_DESCR.
  endif.

***----------------------------------------------------------------------------
*** Get Joint Position
  if EF_JOIPO is requested.
    move ME->R_PR_SPEC->GET_JOIPO( )       to EF_JOIPO.
  endif.

  if EF_JOIPO_DESCR is requested.
    move ME->R_PR_SPEC->GET_JOIPO_DESCR( ) to EF_JOIPO_DESCR.
  endif.

***----------------------------------------------------------------------------
*** Get Joint Width
  if ES_JOIWI is requested.
    move ME->R_PR_SPEC->GET_JOIWI( )       to ES_JOIWI.
  endif.

*** Get Joint Length
  if ES_JOILE is requested.
    move ME->R_PR_SPEC->GET_JOILE( )       to ES_JOILE.
  endif.

***----------------------------------------------------------------------------
*** Get Stitch Length
  if EF_NOSTI is requested.
    move ME->R_PR_SPEC->GET_NOSTI( )       to EF_NOSTI.
  endif.

*** Stitch per
  if ES_STITP is requested.
    move ME->R_PR_SPEC->GET_STITP( )       to ES_STITP.
  endif.

endmethod.
method GET_PROD_DIM.

  check ME->R_PR_SPEC is bound.

***----------------------------------------------------------------------------
*** Get Product Length
  if ES_PROLE is requested.
    move ME->R_PR_SPEC->GET_PROLE( ) to ES_PROLE.
  endif.

*** Get Product Width
  if ES_PROWI is requested.
    move ME->R_PR_SPEC->GET_PROWI( ) to ES_PROWI.
  endif.

*** Get Product Height
  if ES_PROHE is requested.
    move ME->R_PR_SPEC->GET_PROHE( ) to ES_PROHE.
  endif.

endmethod.
method GET_PRSTY.

  check ME->R_PR_SPEC is bound.

***----------------------------------------------------------------------------
*** Get Product Style Type
  if EF_TPRST is requested.
    move ME->R_PR_SPEC->GET_TPRST( ) to EF_TPRST.
  endif.

***----------------------------------------------------------------------------
*** Get Product Style
  if EF_PRSTY is requested.
    move ME->R_PR_SPEC->GET_PRSTY( ) to EF_PRSTY.
  endif.

*** Get Product Style Description
  if EF_PRSTY_DESCR is requested.
    move ME->R_PR_SPEC->GET_PRSTY_DESCR( ) to EF_PRSTY_DESCR.
  endif.

***----------------------------------------------------------------------------
*** Get Box Part
  if EF_BOXPA is requested.
    move ME->R_PR_SPEC->GET_BOXPA( ) to EF_BOXPA.
  endif.

*** Get Box Part Description
  if EF_BOXPA_DESCR is requested.
    move ME->R_PR_SPEC->GET_BOXPA_DESCR( ) to EF_BOXPA_DESCR.
  endif.

endmethod.
method GET_PTYPE.

  check ME->R_PR_SPEC is bound.

***----------------------------------------------------------------------------
*** Get Product Type
  if EF_PTYPE is requested.
    move ME->R_PR_SPEC->GET_PTYPE( ) to EF_PTYPE.
  endif.

*** Get Product Type Description
  if EF_PTYPE_DESCR is requested.
    move ME->R_PR_SPEC->GET_PTYPE_DESCR( ) to EF_PTYPE_DESCR.
  endif.

endmethod.
method SET_CUSTMAT_INFO.

  check ME->R_PR_SPEC            is bound     and
        ME->R_GATE->IS_LOCKED( ) eq ABAP_TRUE.

***----------------------------------------------------------------------------
*** Set Customer Material Number
  if IF_CMATN is supplied.
    ME->R_PR_SPEC->SET_CMATN( IF_CMATN = IF_CMATN
                              IF_SETBY = IF_SETBY ).
  endif.

*** Set Customer Material Description
  if IF_MATDE is supplied.
    ME->R_PR_SPEC->SET_MATDE( IF_MATDE = IF_MATDE
                              IF_SETBY = IF_SETBY ).
  endif.

***----------------------------------------------------------------------------
*** Process Events
  if IF_RAISE_EVENTS eq ABAP_TRUE.
    ME->R_GATE->RAISE_EVENTS( ).
  endif.

endmethod.
method SET_PROD_DIM.

  check ME->R_PR_SPEC            is bound     and
        ME->R_GATE->IS_LOCKED( ) eq ABAP_TRUE.

***----------------------------------------------------------------------------
*** Set Product Length
  if IS_PROLE is supplied.
    ME->R_PR_SPEC->SET_PROLE( IS_PROLE = IS_PROLE
                              IF_SETBY = IF_SETBY ).
  endif.

*** Set Product Width
  if IS_PROWI is supplied.
    ME->R_PR_SPEC->SET_PROWI( IS_PROWI = IS_PROWI
                              IF_SETBY = IF_SETBY ).
  endif.

*** Set Product Height
  if IS_PROHE is supplied.
    ME->R_PR_SPEC->SET_PROHE( IS_PROHE = IS_PROHE
                              IF_SETBY = IF_SETBY ).
  endif.

***----------------------------------------------------------------------------
*** Process Events
  if IF_RAISE_EVENTS eq ABAP_TRUE.
    ME->R_GATE->RAISE_EVENTS( ).
  endif.

endmethod.
method SET_PRSTY.

  check ME->R_PR_SPEC            is bound     and
        ME->R_GATE->IS_LOCKED( ) eq ABAP_TRUE.

***----------------------------------------------------------------------------
*** Set Product Style
  ME->R_PR_SPEC->SET_PRSTY( IF_PRSTY = IF_PRSTY
                            IF_SETBY = IF_SETBY ).


***----------------------------------------------------------------------------
*** Set Part of Box
  if IF_BOXPA is supplied.
    ME->R_PR_SPEC->SET_BOXPA( IF_BOXPA = IF_BOXPA
                              IF_SETBY = IF_SETBY ).
  endif.

***----------------------------------------------------------------------------
*** Process Events
  if IF_RAISE_EVENTS eq ABAP_TRUE.
    ME->R_GATE->RAISE_EVENTS( ).
  endif.

endmethod.
method SET_PTYPE.

  check ME->R_PR_SPEC            is bound     and
        ME->R_GATE->IS_LOCKED( ) eq ABAP_TRUE.

***----------------------------------------------------------------------------
*** Set Product Type
  ME->R_PR_SPEC->SET_PTYPE( IF_PTYPE = IF_PTYPE
                            IF_SETBY = IF_SETBY ).

***----------------------------------------------------------------------------
*** Process Events
  if IF_RAISE_EVENTS eq ABAP_TRUE.
    ME->R_GATE->RAISE_EVENTS( ).
  endif.

endmethod.
