
class ZCL_VCXI_P4SS_PR_AREA_WGT definition
  public
  inheriting from ZCL_VCXI_PXSS_PR_AREA_WGT
  create public .

public section.

  constants C_IPRVT_P4_AREA_WGT type /VCXI/VKSC_IPRVT value 'ZVCXI_P4SS_AREA_WGT' ##NO_TEXT.

  methods IS_CALC
    redefinition .
protected section.

  methods GET_PRVDR_PRDTL
    exporting
      !ER_GATE type ref to /VCXI/CL_VKSR_GATE
    returning
      value(RR_PRDTL) type ref to ZIF_VCXI_P4SS_PRDTL
    raising
      /VCXI/CX_CKX .
  methods GET_PRVDR_PRFG
    exporting
      !ER_GATE type ref to /VCXI/CL_VKSR_GATE
    returning
      value(RR_PR_PRFG) type ref to ZCL_VCXI_P4SS_PR_PRFG
    raising
      /VCXI/CX_CKX .
  methods GET_PRVDR_PRPART
    exporting
      !ER_GATE type ref to /VCXI/CL_VKSR_GATE
    returning
      value(RR_PR_PRPART) type ref to ZCL_VCXI_P4SS_PR_PRPART
    raising
      /VCXI/CX_CKX .

  methods HANDLE_RECALC_AREA
    redefinition .
  methods HANDLE_RECALC_WEIGHT
    redefinition .
private section.
endclass. "ZCL_VCXI_P4SS_PR_AREA_WGT definition
class ZCL_VCXI_P4SS_PR_AREA_WGT implementation.
  method GET_PRVDR_PRDTL.

    data:  LT_GATE  type /VCXI/VKSR_TT_GATE.

    clear: ER_GATE,
           RR_PRDTL.

***------------------------------------------------------------------------------------------------
*** Find Provider for Procurement Finished Good
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZIF_VCXI_P4SS_PRDTL=>C_IPRVT_P4_PRDTL
                                     IF_INUSE = ABAP_TRUE ).
    read table LT_GATE into ER_GATE index 1.
    check SY-SUBRC is initial.

    move ER_GATE->R_CLSPR ?to RR_PRDTL.

  endmethod.
  method GET_PRVDR_PRFG.

    data:  LT_GATE  type /VCXI/VKSR_TT_GATE.

    clear: ER_GATE,
           RR_PR_PRFG.

***------------------------------------------------------------------------------------------------
*** Find Provider for Procurement Finished Good
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_P4SS_PR_PRFG=>C_IPRVT_P4_PRFG
                                     IF_INUSE = ABAP_TRUE ).
    read table LT_GATE into ER_GATE index 1.
    check SY-SUBRC is initial.

    move ER_GATE->R_CLSPR ?to RR_PR_PRFG.

  endmethod.
  method GET_PRVDR_PRPART.

    data:  LT_GATE  type /VCXI/VKSR_TT_GATE.

    clear: ER_GATE,
           RR_PR_PRPART.

***------------------------------------------------------------------------------------------------
*** Find Provider for Procurement Part
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_P4SS_PR_PRPART=>C_IPRVT_P4_PRPART
                                     IF_INUSE = ABAP_TRUE ).
    read table LT_GATE into ER_GATE index 1.
    check SY-SUBRC is initial.

    move ER_GATE->R_CLSPR ?to RR_PR_PRPART.

  endmethod.
  method HANDLE_RECALC_AREA.

    data: LR_PRDET type ref to ZIF_VCXI_P4SS_PRDTL.


***------------------------------------------------------------------------------------------------
*** Get Area from Procurement Details
    LR_PRDET = ME->GET_PRVDR_PRDTL( ).
    if LR_PRDET is bound.
***   Get Sold Blank Area
      ME->SET_SBLAR( IS_SBLAR = LR_PRDET->GET_SBLAR( IF_UOM = ME->GET_SBLAR( )-SBLAR_UOM ) ).
***   Get Cut Out Area
      ME->SET_COUAR( IS_COUAR = LR_PRDET->GET_COUAR( IF_UOM = ME->GET_COUAR( )-COUAR_UOM ) ).
    endif.

***------------------------------------------------------------------------------------------------
*** Calculate other values using SUPER
    SUPER->HANDLE_RECALC_AREA( ).

  endmethod.
  method HANDLE_RECALC_WEIGHT.

    data: LR_PRDET type ref to ZIF_VCXI_P4SS_PRDTL.


***------------------------------------------------------------------------------------------------
*** Get Weight from Procurement Details
    LR_PRDET = ME->GET_PRVDR_PRDTL( ).
    if LR_PRDET is bound.
***   Get Sold Blank Weight
      ME->SET_SBLWT( IS_SBLWT = LR_PRDET->GET_SBLWT( IF_UOM = ME->GET_SBLWT( )-SBLWT_UOM ) ).
***   Get Cut Out Weight
      ME->SET_COUWT( IS_COUWT = LR_PRDET->GET_COUWT( IF_UOM = ME->GET_COUWT( )-COUWT_UOM ) ).
***   Get Additional Weight
      ME->SET_ADDWT( IS_ADDWT = LR_PRDET->GET_ADDWT( IF_UOM = ME->GET_ADDWT( )-ADDWT_UOM ) ).
    endif.

***------------------------------------------------------------------------------------------------
*** Calculate other values using SUPER
    SUPER->HANDLE_RECALC_WEIGHT( ).

  endmethod.
  method IS_CALC.

    data: LR_PR_PRFG   type ref to ZCL_VCXI_P4SS_PR_PRFG,
          LR_PR_PRPART type ref to ZCL_VCXI_P4SS_PR_PRPART.


    LR_PR_PRFG   = ME->GET_PRVDR_PRFG( ).
    LR_PR_PRPART = ME->GET_PRVDR_PRPART( ).


    RF_CALC = cond #( when ME->GET_PRVDR_PRDTL( ) is bound        then ABAP_TRUE
                      when LR_PR_PRFG is bound and
                           LR_PR_PRFG->IS_DUMMY( ) eq ABAP_FALSE  then ABAP_TRUE
                      when LR_PR_PRPART is bound and
                           LR_PR_PRPART->IS_DUMMY( ) ne ABAP_TRUE then ABAP_TRUE
                                                                  else ABAP_FALSE ).

  endmethod.
