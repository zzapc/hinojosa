
class ZCL_VCXI_XCSUC_GRID_DFLT_PMATY definition
  public
  inheriting from ZCL_VCXI_XCSUC_GRID
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IF_TABNAME type TABNAME optional
      !IF_CONTAINER_NAME type SCRFNAME optional
      !IR_CUSTOMIZING type ref to OBJECT
      !IR_S_CONTAINER type ref to CL_GUI_CONTAINER optional
    raising
      /VCXI/CX_CKX .
protected section.

  data R_DFLT type ref to ZCL_VCXI_XCSUC_DFLT .

  methods CHECK_SELECTION
    returning
      value(RF_OKAY) type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
  methods HANDLE_TOOLBAR
    for event TOOLBAR of CL_GUI_ALV_GRID
    importing
      !E_OBJECT
      !E_INTERACTIVE .
  methods HANDLE_USER_COMMAND
    for event USER_COMMAND of CL_GUI_ALV_GRID
    importing
      !E_UCOMM .

  methods GET_FIELDCAT
    redefinition .
  methods GET_GRID_DATA
    redefinition .
  methods GET_LAYOUT
    redefinition .
  methods SET_EVENT
    redefinition .
  methods HANDLE_DATA_CHANGED_FINISHED
    redefinition .
private section.

  constants C_FUNC_ADD type UI_FUNC value 'ADD'. "#EC NOTEXT
  constants C_FUNC_DEL type UI_FUNC value 'DELETE'. "#EC NOTEXT
endclass. "ZCL_VCXI_XCSUC_GRID_DFLT_PMATY definition
class ZCL_VCXI_XCSUC_GRID_DFLT_PMATY implementation.
method CHECK_SELECTION.

  data: LT_CELL type LVC_T_CELL,
        LT_ROW  type LVC_T_ROW.

  ME->R_GRID->GET_SELECTED_CELLS( importing ET_CELL = LT_CELL ).
  ME->R_GRID->GET_SELECTED_ROWS( importing ET_INDEX_ROWS = LT_ROW ).

*** Delete is only available if Cells are selected
  if lines( LT_CELL ) gt 0 or
     lines( LT_ROW )  gt 0.
    move ABAP_TRUE to RF_OKAY.
  else.
    move ABAP_FALSE to RF_OKAY.
  endif.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( exporting IF_TABNAME        = 'ZVCXI_XCSUC_S_DFLT_PMATY_GRID'
                                IF_CONTAINER_NAME = '0100CC_DFLT_PMATY_GRID'
                                IR_CUSTOMIZING    = IR_CUSTOMIZING
                                IR_S_CONTAINER    = IR_S_CONTAINER ).

*** Try to Assign Customizing Reference
  try.
      move IR_CUSTOMIZING ?to ME->R_DFLT.
    catch CX_SY_MOVE_CAST_ERROR.
***   Default Customizing Reference can't be assigned.
      /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE(
          IF_MSGTY = 'E'
          IF_MSGID = 'ZVCXI_XCSUC'
          IF_MSGNO = '203' ).
  endtry.

  check ME->R_DFLT is bound.
  ME->F_LOCKED = ME->R_DFLT->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ).

endmethod.
method GET_FIELDCAT.

  field-symbols: <S_FCAT>           type        LVC_S_FCAT.

***----------------------------------------------------------------------------
*** Get Standard Field Catalog
  RT_FCAT = SUPER->GET_FIELDCAT( ).

***----------------------------------------------------------------------------
*** Adjust Field Catalog
  loop at RT_FCAT assigning <S_FCAT>.
    case <S_FCAT>-FIELDNAME.
***   Set GUID to only technical Fields
      when 'GDFLT'.
        move ABAP_TRUE to <S_FCAT>-TECH.
***   Make Packaging Material Quantity Editable
      when 'PMQTY'.
        if ME->R_DFLT->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ) eq ABAP_TRUE.
          move ABAP_TRUE to <S_FCAT>-EDIT.
        endif.
    endcase.
  endloop.

endmethod.
method GET_GRID_DATA.

  data:          LS_DFLT_PMATY_GRID   type          ZVCXI_XCSUC_S_DFLT_PMATY_GRID,
                 LS_DFLT_PMATY        type          ZVCXI_XCSUC_S_PMATY_DFLT.
  field-symbols: <T_DATA>             type standard table,
                 <S_DATA>             type          any.

*** Dereferencing of Data Reference
  assign IR_DATA->* to <T_DATA>.

  free: <T_DATA>.

*** --------------------------------------------------------------------------------------------
*** Get all Packaging Material Type Entries
  loop at ME->R_DFLT->T_PMATY_DFLT into LS_DFLT_PMATY.

    clear: LS_DFLT_PMATY_GRID.
    move-corresponding LS_DFLT_PMATY to LS_DFLT_PMATY_GRID.

*** --------------------------------------------------------------------------------------------
*** Add Entry
    append initial line to <T_DATA> assigning <S_DATA>.
    move-corresponding LS_DFLT_PMATY_GRID to <S_DATA>.
  endloop.

endmethod.
method GET_LAYOUT.

  RS_LAYOUT = SUPER->GET_LAYOUT( ).

*** Set Toolbar to Activ
  move ABAP_FALSE  to RS_LAYOUT-NO_TOOLBAR.
  move 'A'         to RS_LAYOUT-SEL_MODE.

endmethod.
method HANDLE_DATA_CHANGED_FINISHED.

  data:          LS_DFLT_PMATY        type          ZVCXI_XCSUC_S_PMATY_DFLT.
  data:          LR_CX_CKX            type ref to   /VCXI/CX_CKX.
  field-symbols: <T_DATA>             type standard table,
                 <S_DATA>             type          any.

  check E_MODIFIED eq ABAP_TRUE.

  try.

      SUPER->HANDLE_DATA_CHANGED_FINISHED( exporting E_MODIFIED    = E_MODIFIED
                                                     ET_GOOD_CELLS = ET_GOOD_CELLS ).

***   Dereferencing of Data Reference
      assign ME->R_DATA->* to <T_DATA>.


      loop at <T_DATA> assigning <S_DATA>.
        read table ET_GOOD_CELLS with key ROW_ID = SY-TABIX transporting no fields.
        check SY-SUBRC eq 0.
        move-corresponding <S_DATA> to LS_DFLT_PMATY.

***     convert material to internal format
        call function 'CONVERSION_EXIT_MATN1_INPUT'
          exporting
            INPUT        = LS_DFLT_PMATY-PMATN
          importing
            OUTPUT       = LS_DFLT_PMATY-PMATN
          exceptions
            LENGTH_ERROR = 1
            others       = 2.
        if SY-SUBRC <> 0.
          /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
        endif.

        ME->R_DFLT->UPDATE_PMATY_DFLT( IS_PMATY_DFLT = LS_DFLT_PMATY ).
      endloop.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      LR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

*** Request Refresh for ALV
  move ABAP_TRUE to ME->F_REFRESH_REQ.

endmethod.
method HANDLE_TOOLBAR.

  data:  LS_TOOLBAR      type        STB_BUTTON.
  data:  LF_DISABLED     type        ABAP_BOOL.
  data:  LR_CX_CKX       type ref to /VCXI/CX_CKX.

  try.

      if ME->R_DFLT->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ) eq ABAP_FALSE.
        move ABAP_TRUE to LF_DISABLED.
      endif.

***----------------------------------------------------------------------------
***   Insert Toolbar elements Add and Delete
      clear LS_TOOLBAR.
      move C_FUNC_ADD                 to LS_TOOLBAR-FUNCTION.
      move 'Add Entry'(001)           to LS_TOOLBAR-QUICKINFO.
      move C_BTYPE_BUTTON             to LS_TOOLBAR-BUTN_TYPE.
      move ICON_INSERT_ROW            to LS_TOOLBAR-ICON.
      move LF_DISABLED                to LS_TOOLBAR-DISABLED.
      append LS_TOOLBAR               to E_OBJECT->MT_TOOLBAR.

      clear LS_TOOLBAR.
      move C_FUNC_DEL                 to LS_TOOLBAR-FUNCTION.
      move 'Delete Entry'(002)        to LS_TOOLBAR-QUICKINFO.
      move C_BTYPE_BUTTON             to LS_TOOLBAR-BUTN_TYPE.
      move ICON_DELETE_ROW            to LS_TOOLBAR-ICON.
      move LF_DISABLED                to LS_TOOLBAR-DISABLED.
      append LS_TOOLBAR               to E_OBJECT->MT_TOOLBAR.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      LR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

endmethod.
method HANDLE_USER_COMMAND.

  data:          LF_DISABLED         type               ABAP_BOOL.
  data:          LS_CELL             type               LVC_S_CELL,
                 LS_ROW              type               LVC_S_ROW,
                 LS_DFLT_PMATY       type               ZVCXI_XCSUC_S_PMATY_DFLT,
                 LS_DFLT_PMATY_NEW   type               ZVCXI_XCSUC_S_DFLT_PMATY_SHLP,
                 LS_DFLT_PMATY_OLD   type               ZVCXI_XCSUC_S_DFLT_PMATY_SHLP,
                 LS_DFLT_PMATY_CURR  type               ZVCXI_XCSUC_S_DFLT_PMATY_SHLP.
  data:          LT_CELL             type               LVC_T_CELL,
                 LT_ROW              type               LVC_T_ROW,
                 LT_DFLT_PMATY       type               ZVCXI_XCSUC_TT_DFLT_PMATY_SHLP,
                 LT_DFLT_PMATY_NEW   type               ZVCXI_XCSUC_TT_DFLT_PMATY_SHLP,
                 LT_DFLT_PMATY_CURR  type               ZVCXI_XCSUC_TT_DFLT_PMATY_SHLP.
  data:          LR_CX_CKX           type ref to        /VCXI/CX_CKX.
  field-symbols: <T_DATA>            type standard      table,
                 <S_DFLT_PMATY>      type               ZVCXI_XCSUC_S_DFLT_PMATY_SHLP,
                 <S_DATA>            type               any.

  try.

*** ---------------------------------------------------------------------------
***   Get Value Data
      assign ME->R_DATA->* to <T_DATA>.
      free: <T_DATA>.
      ME->GET_GRID_DATA( IR_DATA = ME->R_DATA ).

      ME->R_GRID->GET_SELECTED_CELLS( importing ET_CELL = LT_CELL ).
      ME->R_GRID->GET_SELECTED_ROWS( importing ET_INDEX_ROWS = LT_ROW ).

*** ---------------------------------------------------------------------------
***   Get Selected Packaging Material Type Assignment
      loop at LT_CELL into LS_CELL.
        read table <T_DATA> assigning <S_DATA> index LS_CELL-ROW_ID-INDEX.
        check SY-SUBRC = 0.

        append initial line to LT_DFLT_PMATY assigning <S_DFLT_PMATY>.
        move-corresponding <S_DATA> to <S_DFLT_PMATY>.
      endloop.

      loop at LT_ROW into LS_ROW.
        read table <T_DATA> assigning <S_DATA> index LS_ROW-INDEX.
        check SY-SUBRC = 0.

        append initial line to LT_DFLT_PMATY assigning <S_DFLT_PMATY>.
        move-corresponding <S_DATA> to <S_DFLT_PMATY>.
      endloop.

*** ---------------------------------------------------------------------------
***   handle UserCommand
      case E_UCOMM.

        when C_FUNC_ADD.
***       Add new Packaging Material Type Assignment
          if ME->R_DFLT->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ) eq ABAP_FALSE.
            move ABAP_TRUE to LF_DISABLED.
          endif.

***       Get all Current Packaging Material Types
          loop at ME->R_DFLT->T_PMATY_DFLT into LS_DFLT_PMATY.
            move-corresponding LS_DFLT_PMATY to LS_DFLT_PMATY_CURR.
            append LS_DFLT_PMATY_CURR to LT_DFLT_PMATY_CURR.
          endloop.
          clear: LS_DFLT_PMATY.

          LT_DFLT_PMATY_NEW = ZCL_VCXI_XCSUC_SHLP_DFLT_PMATY=>CALL_SHLP( IF_DISPLAY    = LF_DISABLED
                                                                         IF_SYSOM      = ME->R_DFLT->S_DFLT-SYSOM
                                                                         IT_DFLT_PMATY = LT_DFLT_PMATY_CURR
                                                                         IT_WERKS      = ME->R_DFLT->T_WERKS ).
          loop at LT_DFLT_PMATY_NEW into LS_DFLT_PMATY_NEW.
            move-corresponding LS_DFLT_PMATY_NEW to LS_DFLT_PMATY.
            ME->R_DFLT->ADD_PMATY_DFLT( IS_PMATY_DFLT = LS_DFLT_PMATY ).
          endloop.

        when C_FUNC_DEL.
***       Delete Packaging Material Type Assignment
          if ME->CHECK_SELECTION( ) eq ABAP_FALSE.
***         No Row Selected. Please Select all Row's you wanna Delete.
            raise exception type /VCXI/CX_CKX
             exporting
                F_MSGTY = 'E'
                F_MSGID = 'ZVCXI_XCSUC'
                F_MSGNO = '003'.
          endif.

***       Delete all Packaging Material Type Assignments
          loop at LT_DFLT_PMATY into LS_DFLT_PMATY_OLD.
            move-corresponding LS_DFLT_PMATY_OLD to LS_DFLT_PMATY.
            ME->R_DFLT->DELETE_PMATY_DFLT( IS_PMATY_DFLT = LS_DFLT_PMATY ).
          endloop.
      endcase.

      ME->F_REFRESH_REQ = ABAP_TRUE.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      LR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

endmethod.
method SET_EVENT.

  SUPER->SET_EVENT( ).

*** Add Event for F4Help, UserCommand and Toolbar
  set handler ME->HANDLE_USER_COMMAND for ME->R_GRID.
  set handler ME->HANDLE_TOOLBAR      for ME->R_GRID.

endmethod.
