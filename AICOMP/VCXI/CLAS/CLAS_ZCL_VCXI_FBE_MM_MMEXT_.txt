
class ZCL_VCXI_FBE_MM_MMEXT definition
  public
  inheriting from ZCL_VCXI_F0E_MM_MMEXT
  create public .

public section.

  constants C_CNTXT_FB type ZVCXI_XCE_MM_CNTXT value 'FB' ##NO_TEXT.
  data S_MMEXT type ZVCXI_FBE_MM_S_MMEXT read-only .
  data T_BPINFO type ZVCXI_FBSS_TT_BPINFO read-only .
  data T_BGICS type ZVCXI_FBSS_TT_BGICS read-only .

  methods SET_MMEXT
    importing
      !IS_MMEXT type ZVCXI_FBE_MM_S_MMEXT
    raising
      /VCXI/CX_CKX .

  methods SAVE
    redefinition .
  methods SET_EXTVAL
    redefinition .
  methods UPDATE_BY_FG
    redefinition .
  methods COPY
    redefinition .
protected section.

  methods LOAD_FROM_DB
    redefinition .
private section.
endclass. "ZCL_VCXI_FBE_MM_MMEXT definition
class ZCL_VCXI_FBE_MM_MMEXT implementation.
  method COPY.

    RR_MMEXT = SUPER->COPY( IF_MATNR = IF_MATNR
                            IF_WERKS = IF_WERKS ).

    check RR_MMEXT is bound.

*** Copy Values
    cast ZCL_VCXI_FBE_MM_MMEXT( RR_MMEXT )->S_MMEXT  = ME->S_MMEXT.
    cast ZCL_VCXI_FBE_MM_MMEXT( RR_MMEXT )->T_BGICS  = ME->T_BGICS.
    cast ZCL_VCXI_FBE_MM_MMEXT( RR_MMEXT )->T_BPINFO = ME->T_BPINFO.

  endmethod.
  method LOAD_FROM_DB.

    data: LS_TD100 type ZVCXI_FBE_TD100.


***----------------------------------------------------------------------------
*** Load from SUPER
    SUPER->LOAD_FROM_DB( ).

***----------------------------------------------------------------------------
*** Load Material Master Extension
    select single *
           into LS_TD100
           from ZVCXI_FBE_TD100
          where MATNR eq ME->F_MATNR
           and  WERKS eq ME->F_WERKS.
    if SY-SUBRC eq 0.
***   Load current values
      ME->S_MMEXT = LS_TD100-S_MMEXT.
    endif.

*** Load Bag Parts
    select *
           into corresponding fields of table ME->T_BPINFO
           from ZVCXI_FBE_TD110
          where MATNR eq ME->F_MATNR
           and  WERKS eq ME->F_WERKS
          order by IBPAR.

*** Load Included Conversion Steps
    select *
           into corresponding fields of table ME->T_BGICS
           from ZVCXI_FBE_TD120
          where MATNR eq ME->F_MATNR
           and  WERKS eq ME->F_WERKS
          order by ICSTP.

  endmethod.
  method SAVE.

    data: LS_TD100 type ZVCXI_FBE_TD100.
    data: LT_TD110 type table of ZVCXI_FBE_TD110,
          LT_TD120 type table of ZVCXI_FBE_TD120.


*** Save SUPER
    SUPER->SAVE( ).

***----------------------------------------------------------------------------
*** Save basic Material Master Extension
    LS_TD100 = value #( MATNR   = ME->F_MATNR
                        WERKS   = ME->F_WERKS
                        S_MMEXT = ME->S_MMEXT ).
    modify ZVCXI_FBE_TD100 from LS_TD100.

*** Save Bag Parts
    LT_TD110 = value #( for LS_BPINFO in ME->T_BPINFO ( MATNR   = ME->F_MATNR
                                                        WERKS   = ME->F_WERKS
                                                        IBPAR   = LS_BPINFO-IBPAR
                                                        S_BPARE = LS_BPINFO-S_BPARE
                                                        S_SURWT = LS_BPINFO-S_SURWT ) ).
    delete from ZVCXI_FBE_TD110 where MATNR eq ME->F_MATNR
                                 and  WERKS eq ME->F_WERKS.
    modify ZVCXI_FBE_TD110 from table LT_TD110.

*** Save Included Conv. Steps
    LT_TD120 = value #( for LS_BGICS in ME->T_BGICS ( MATNR = ME->F_MATNR
                                                      WERKS = ME->F_WERKS
                                                      ICSTP = LS_BGICS-ICSTP
                                                      CCSTP = LS_BGICS-CCSTP ) ).
    delete from ZVCXI_FBE_TD120 where MATNR eq ME->F_MATNR
                                 and  WERKS eq ME->F_WERKS.
    modify ZVCXI_FBE_TD120 from table LT_TD120.

  endmethod.
  method SET_EXTVAL.

    data: LS_EXTVAL type ZVCXI_FBE_MM_S_EXTVAL.


    SUPER->SET_EXTVAL( IS_EXTVAL = IS_EXTVAL ).

***----------------------------------------------------------------------------
*** "Cast" Extension Values
    LS_EXTVAL = corresponding #( IS_EXTVAL ).

*** Extension Header
    if ME->S_MMEXT ne LS_EXTVAL-S_MMEXT.
      ME->S_MMEXT = LS_EXTVAL-S_MMEXT.
      raise event /VCXI/IF_CKX_OBJECT~CHANGED.
    endif.

*** Bag Parts
    if ME->T_BPINFO ne LS_EXTVAL-T_BPINFO.
      ME->T_BPINFO = LS_EXTVAL-T_BPINFO.
      raise event /VCXI/IF_CKX_OBJECT~CHANGED.
    endif.

*** Included Conv. Steps
    if ME->T_BGICS ne LS_EXTVAL-T_BGICS.
      ME->T_BGICS = LS_EXTVAL-T_BGICS.
      raise event /VCXI/IF_CKX_OBJECT~CHANGED.
    endif.

  endmethod.
  method SET_MMEXT.

    check ME->S_MMEXT ne IS_MMEXT.
    ME->S_MMEXT = IS_MMEXT.
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method UPDATE_BY_FG.

    data: LR_EA_BFG     type ref to ZCL_VCXI_FBSS_EA_BFG.


***----------------------------------------------------------------------------
*** Get Element API Reference
    try.
        LR_EA_BFG ?= IR_SVCEL->GET_CLSEA( ).
      catch CX_SY_MOVE_CAST_ERROR into data(LR_CX_SY_MOVE_CAST_ERROR).
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_SY_MOVE_CAST_ERROR ).
    endtry.
    check LR_EA_BFG is bound.

***----------------------------------------------------------------------------
*** Set Extension Values
    ME->SET_EXTVAL( IS_EXTVAL = LR_EA_BFG->GET_EXTVAL( ) ).

  endmethod.
