
class ZCL_VCXI_PXE_MM_CNTXT_MGR definition
  public
  inheriting from ZCL_VCXI_XCE_MM_CNTXT_MGR
  create public .

public section.

  class-methods CLASS_CONSTRUCTOR .

  methods GET_FG
    redefinition .
  methods GET_FG_INFO
    redefinition .
protected section.
private section.
endclass. "ZCL_VCXI_PXE_MM_CNTXT_MGR definition
class ZCL_VCXI_PXE_MM_CNTXT_MGR implementation.
  method CLASS_CONSTRUCTOR.

*** Corrugated
    REGISTER_CNTXT( IF_CNTXT = ZCL_VCXI_P1E_MM_MMEXT=>C_CNTXT_P1
                    IF_CLSNA = 'ZCL_VCXI_P1E_MM_MMEXT' ).

*** Folding Carton
    REGISTER_CNTXT( IF_CNTXT = ZCL_VCXI_P2E_MM_MMEXT=>C_CNTXT_P2
                    IF_CLSNA = 'ZCL_VCXI_P2E_MM_MMEXT' ).

*** Assembly
    REGISTER_CNTXT( IF_CNTXT = ZCL_VCXI_P3E_MM_MMEXT=>C_CNTXT_P3
                    IF_CLSNA = 'ZCL_VCXI_P3E_MM_MMEXT' ).

*** Procurement
    REGISTER_CNTXT( IF_CNTXT = ZCL_VCXI_P4E_MM_MMEXT=>C_CNTXT_P4
                    IF_CLSNA = 'ZCL_VCXI_P4E_MM_MMEXT' ).

*** Roll
    REGISTER_CNTXT( IF_CNTXT = ZCL_VCXI_P5E_MM_MMEXT=>C_CNTXT_P5
                    IF_CLSNA = 'ZCL_VCXI_P5E_MM_MMEXT' ).

  endmethod.
  method GET_FG.

*** Get Finished Goods which are marked as Multi Quantity Components
    RT_SVCEL = IR_SVC->GET_SVC_ELEMENTS( IF_ATTID = 'MQ_COMP'
                                         IF_INUSE = ABAP_TRUE ).

  endmethod.
  method GET_FG_INFO.

    data: LR_EA_FG  type ref to ZCL_VCXI_PXSS_EA_FG.


*** Get Reference to Finished Good
    try.
        LR_EA_FG ?= IR_SVCEL_FG->GET_CLSEA( ).
      catch CX_SY_MOVE_CAST_ERROR.
        clear LR_EA_FG.
    endtry.
    check LR_EA_FG is bound.

*** Get Material Number
    EF_MATNR = LR_EA_FG->GET_MATNR( ).

*** Get Context
    EF_CNTXT = switch #( IR_SVCEL_FG->/VCXI/IF_CKX_OBJECT~GET_ID( ) when 'CFG'   then ZCL_VCXI_P1E_MM_MMEXT=>C_CNTXT_P1    "Corrugated Finished Good
                                                                    when 'FCFG'  then ZCL_VCXI_P2E_MM_MMEXT=>C_CNTXT_P2    "Folding Carton Finished Good
                                                                    when 'AFG'   then ZCL_VCXI_P3E_MM_MMEXT=>C_CNTXT_P3    "Assembled Finished Good
                                                                    when 'PRFG'  then ZCL_VCXI_P4E_MM_MMEXT=>C_CNTXT_P4    "Procurement Finished Good
                                                                    when 'RLFG'  then ZCL_VCXI_P5E_MM_MMEXT=>C_CNTXT_P5    "Roll Finished Good
                                                                    else throw /VCXI/CX_CKX( ) ).

  endmethod.
endclass. "ZCL_VCXI_PXE_MM_CNTXT_MGR implementation
