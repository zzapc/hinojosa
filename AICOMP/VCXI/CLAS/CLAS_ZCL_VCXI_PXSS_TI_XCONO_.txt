
class ZCL_VCXI_PXSS_TI_XCONO definition
  public
  inheriting from /VCXI/CL_VKSRU_CLSTI
  create public .

public section.

  methods /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ
    redefinition .
  methods /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL
    redefinition .
protected section.

  constants C_UC_MERGE type UI_FUNC value 'MERGE' ##NO_TEXT.

  methods IS_MERGE_DISABLED
    returning
      value(RF_DISABLED) type CUA_ACTIVE
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_MERGE
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_PXSS_TI_XCONO definition
class ZCL_VCXI_PXSS_TI_XCONO implementation.
  method /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ.

    SUPER->/VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ( IR_MENU = IR_MENU ).

*** Add Merge Function
    IR_MENU->ADD_SEPARATOR( ).
    IR_MENU->ADD_FUNCTION( FCODE    = C_UC_MERGE
                           TEXT     = 'Merge'(F01)
                           DISABLED = ME->IS_MERGE_DISABLED( ) ).

  endmethod.
  method /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL.

    SUPER->/VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL( IF_FCODE = IF_FCODE ).

    case IF_FCODE.
      when C_UC_MERGE.
        ME->HANDLE_UC_MERGE( ).
    endcase.

  endmethod.
  method HANDLE_UC_MERGE .

*** Merge with connected Component
    cast ZCL_VCXI_PXSS_PR_XCONO( ME->R_GATE->R_CLSPR )->MERGE( ).

  endmethod.
  method IS_MERGE_DISABLED.

    data(LR_GATE_XCONI) = cast ZCL_VCXI_PXSS_PR_XCONO( ME->R_GATE->R_CLSPR )->GET_CONNECTED_GATE( ).

*** Check if Merge is disabled
    RF_DISABLED = cond #( when ME->R_GATE->IS_LOCKED( ) ne ABAP_TRUE                                                   then ABAP_TRUE   "Configuration is locked
                          when cast ZCL_VCXI_PXSS_PR_XCONO( ME->R_GATE->R_CLSPR )->IS_MERGE_ALLOWED( ) ne ABAP_TRUE    then ABAP_TRUE   "Merge is allowed by Connection Out
                          when LR_GATE_XCONI is not bound                                                              then ABAP_TRUE   "Not connected
                          when cast ZCL_VCXI_PXSS_PR_XCONI( LR_GATE_XCONI->R_CLSPR )->IS_MERGE_ALLOWED( ) ne ABAP_TRUE then ABAP_TRUE   "Merge is allowed by Connection In
                                                                                                                       else ABAP_FALSE ).

  endmethod.
endclass. "ZCL_VCXI_PXSS_TI_XCONO implementation
