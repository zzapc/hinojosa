
class ZCL_VCXI_FFSP_UI_ADDON_EMBOS definition
  public
  inheriting from ZCL_VCXI_XCSP_UI_ADDON
  create public .

public section.

  data S_ADDON_EMBOS_VIS type ZVCXI_FFSP_S_ADDON_EMBOS_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
protected section.

  data R_PR_ADDON_EMBOS type ref to ZCL_VCXI_FFSP_PR_ADDON_EMBOS .

  methods GET_PRVDR_MC_TOOL
    exporting
      !ER_GATE type ref to /VCXI/CL_VKSR_GATE
      !ER_PR_MC_TOOL type ref to ZCL_VCXI_XCSP_PR_MC_TOOL
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_FFSP_UI_ADDON_EMBOS definition
class ZCL_VCXI_FFSP_UI_ADDON_EMBOS implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_FFSP_UI_ADDON_EMBOS_BIND'
      exporting
        IR_UI_ADDON_EMBOS = ME
      importing
        ES_DYNP           = ME->S_DYNP.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_ADDON_EMBOS.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DO_MODIFY_SCREEN.

    data: LF_LOCKED type        /VCXI/CKX_LOCKED,
          LF_INUSE  type        /VCXI/VKSR_INUSE.
    data:  LR_CX_CKX type ref to /VCXI/CX_CKX.

    SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                             IF_DYNNR = IF_DYNNR ).

    try.

        if IF_REPID eq ME->S_DYNP-REPID and
           IF_DYNNR eq ME->S_DYNP-DYNNR.

          LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
          LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***       Modify Scree using DynamicVC
          ZCL_VCXI_XCDR_SERVICE=>MODIFY_SCREEN( IR_GATE = ME->R_GATE ).

          loop at screen.
            if LF_LOCKED ne ABAP_TRUE or
               LF_INUSE  ne ABAP_TRUE.
              move 0 to SCREEN-INPUT.
            endif.
            modify screen.
          endloop.
        endif.

      catch /VCXI/CX_CKX into LR_CX_CKX.
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_ADDON_EMBOS_VIS type ZVCXI_FFSP_S_ADDON_EMBOS_VIS.
    data: LR_PR_MC_TOOL type ref to ZCL_VCXI_XCSP_PR_MC_TOOL.


*** Super
    SUPER->DO_UPDATE_VIS( IS_VIS = IS_VIS ).
    check ME->R_PR_ADDON_EMBOS is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_ADDON_EMBOS_VIS.

***--------------------------------------------------------------------------------------
*** Set Embossing Thickness
    if ME->S_ADDON_EMBOS_VIS-S_EMTIN ne LS_ADDON_EMBOS_VIS-S_EMTIN.
      ME->R_PR_ADDON_EMBOS->SET_EMTIN( IS_EMTIN = LS_ADDON_EMBOS_VIS-S_EMTIN
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Get Provider MC Tool
    ME->GET_PRVDR_MC_TOOL( importing ER_PR_MC_TOOL = LR_PR_MC_TOOL ).
    if LR_PR_MC_TOOL is bound.

***   Set Material Number
      if ME->S_ADDON_EMBOS_VIS-MATNR ne LS_ADDON_EMBOS_VIS-MATNR.
        LR_PR_MC_TOOL->SET_MATNR( IF_MATNR = LS_ADDON_EMBOS_VIS-MATNR
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
      endif.

***   Set Average Consumption
      if ME->S_ADDON_EMBOS_VIS-S_AVGCO ne LS_ADDON_EMBOS_VIS-S_AVGCO.
        LR_PR_MC_TOOL->SET_AVGCO( IS_AVGCO = LS_ADDON_EMBOS_VIS-S_AVGCO
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
      endif.

***   Set Average Consumption Per
      if ME->S_ADDON_EMBOS_VIS-S_AVGCP ne LS_ADDON_EMBOS_VIS-S_AVGCP.
        LR_PR_MC_TOOL->SET_AVGCP( IS_AVGCP = LS_ADDON_EMBOS_VIS-S_AVGCP
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
      endif.
    endif.

  endmethod.
  method GET_PRVDR_MC_TOOL.

    data: LT_GATE         type          /VCXI/VKSR_TT_GATE.
    data: LR_GATE         type ref to   /VCXI/CL_VKSR_GATE.

*** Get MC Tool Provider
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_F0SP_PR_MC_TOOL=>C_IPRVT_MC_TOOL
                                     IF_INUSE = ABAP_TRUE ).


    read table LT_GATE into LR_GATE index 1.
    check LR_GATE is bound.
    try.
        move LR_GATE->R_CLSPR ?to ER_PR_MC_TOOL.
      catch CX_SY_MOVE_CAST_ERROR.
        exit.
    endtry.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_ADDON_EMBOS.

  endmethod.
  method INIT_VIS.

    data: LR_PR_MC_TOOL type ref to ZCL_VCXI_XCSP_PR_MC_TOOL.


*** Super
    SUPER->INIT_VIS( ).

    free: ME->S_ADDON_EMBOS_VIS.
    ME->S_ADDON_EMBOS_VIS-S_ADDON_VIS = ME->S_ADDON_VIS.

***--------------------------------------------------------------------------------------
*** Get Embossing Thickness
    ME->R_PR_ADDON_EMBOS->GET_EMTIN( importing ES_EMTIN = ME->S_ADDON_EMBOS_VIS-S_EMTIN ).

***--------------------------------------------------------------------------------------
*** Get Provider MC Tool
    ME->GET_PRVDR_MC_TOOL( importing ER_PR_MC_TOOL = LR_PR_MC_TOOL ).
    if LR_PR_MC_TOOL is bound.
***   Get Material Number
      ME->S_ADDON_EMBOS_VIS-MATNR       = LR_PR_MC_TOOL->GET_MATNR( ).

***   Get Material Description
      ME->S_ADDON_EMBOS_VIS-MATNR_DESCR = LR_PR_MC_TOOL->GET_MATNR_DESCR( ).

***   Get Average Consumption
      ME->S_ADDON_EMBOS_VIS-S_AVGCO     = LR_PR_MC_TOOL->GET_AVGCO( ).
      ME->S_ADDON_EMBOS_VIS-S_AVGCP     = LR_PR_MC_TOOL->GET_AVGCP( ).

***   Get Consumption
      ME->S_ADDON_EMBOS_VIS-S_CONSU     = LR_PR_MC_TOOL->GET_CONSU( ).

    endif.

  endmethod.
