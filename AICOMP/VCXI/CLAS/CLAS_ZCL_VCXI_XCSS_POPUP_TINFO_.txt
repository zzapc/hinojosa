
class ZCL_VCXI_XCSS_POPUP_TINFO definition
  public
  create protected .

public section.

  constants C_UC_CLOSE type SYUCOMM value 'CLOSE' ##NO_TEXT.
  data R_GRID_TINFO type ref to ZCL_VCXI_XCSS_GRID_TINFO .

  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods DO_USER_COMMAND
    importing
      !IF_UCOMM type SYUCOMM
    raising
      /VCXI/CX_CKX .
  class-methods START
    importing
      !IF_LOCKED type /VCXI/CKX_LOCKED optional
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
protected section.

  data F_LOCKED type /VCXI/CKX_LOCKED .
  data F_REFRESH_REQ type ABAP_BOOL .
  data R_CONTAINER type ref to CL_GUI_CUSTOM_CONTAINER .
  data R_GATE type ref to /VCXI/CL_VKSR_GATE .
  data R_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE .

  methods CONSTRUCTOR
    importing
      !IF_LOCKED type /VCXI/CKX_LOCKED
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
  methods DO_FREE
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_CLOSE
    raising
      /VCXI/CX_CKX .
  methods SET_PF_STATUS
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_XCSS_POPUP_TINFO definition
class ZCL_VCXI_XCSS_POPUP_TINFO implementation.
  method CONSTRUCTOR.

    move IR_GATE        to ME->R_GATE.
    move IR_UIGATE      to ME->R_UIGATE.
    move IF_LOCKED      to ME->F_LOCKED.

*** Create Grid for Tooling Information
    create object ME->R_GRID_TINFO
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID'
        IF_APPL_EVENTS    = IF_APPL_EVENTS.

  endmethod.
  method DO_FREE.

*** Free Grid
    if ME->R_GRID_TINFO is bound.
      ME->R_GRID_TINFO->DO_FREE( ).
      clear ME->R_GRID_TINFO.
    endif.

  endmethod.
  method DO_PBO.

*** Set PF-Status
    ME->SET_PF_STATUS( ).

*** Refresh Grid
    if ME->R_GRID_TINFO is bound.
      ME->R_GRID_TINFO->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_TINFO->REFRESH_GRID( ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    case IF_UCOMM.
      when C_UC_CLOSE.
        ME->HANDLE_UC_CLOSE( ).

    endcase.

  endmethod.
  method HANDLE_UC_CLOSE.

*** Raise Event
    ME->R_GATE->RAISE_EVENTS( ).

*** Leave Popup
    leave to screen 0.

  endmethod.
  method SET_PF_STATUS.

    set pf-status '0100' of program 'SAPLZVCXI_XCSS_POPUP_TINFO'.

  endmethod.
  method START.

    data:  LR_POPUP_TINFO type ref to ZCL_VCXI_XCSS_POPUP_TINFO.

*** Create Instance
    create object LR_POPUP_TINFO
      exporting
        IR_GATE        = IR_GATE
        IR_UIGATE      = IR_UIGATE
        IF_LOCKED      = IF_LOCKED
        IF_APPL_EVENTS = IF_APPL_EVENTS.

*** Call Popup
    call function 'ZVCXI_XCSS_POPUP_TINFO_START'
      exporting
        IR_POPUP_TINFO = LR_POPUP_TINFO.

*** Free the Popup
    LR_POPUP_TINFO->DO_FREE( ).

  endmethod.
