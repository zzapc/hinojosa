
class ZCL_VCXI_XCSP_EA_MR_TOOL definition
  public
  inheriting from ZCL_VCXI_XCSP_EA_MR
  create public .

public section.

  methods /VCXI/IF_VKCR_EXPL_BOM~GET_BOM
    redefinition .
  methods /VCXI/IF_VKCR_EXPL_BOM~FILTER_CPBRG
    redefinition .
protected section.

  methods GET_BOM_CONSU
    redefinition .
private section.
endclass. "ZCL_VCXI_XCSP_EA_MR_TOOL definition
class ZCL_VCXI_XCSP_EA_MR_TOOL implementation.
  method /VCXI/IF_VKCR_EXPL_BOM~FILTER_CPBRG.

*** Perform SUPER
    SUPER->/VCXI/IF_VKCR_EXPL_BOM~FILTER_CPBRG( changing CT_CPBRG = CT_CPBRG ).

*** Get Material Consumption Provider
    data(LR_PR_MC) = ME->R_PR_MR->GET_PRVDR_MC( ).
    check LR_PR_MC is bound.

*** No BOM explosion if tool is not Batch Managed
    if cast ZCL_VCXI_XCSP_PR_MC_TOOL( LR_PR_MC )->GET_XCHPF( ) ne ABAP_TRUE.
      free CT_CPBRG.
    endif.

  endmethod.
  method /VCXI/IF_VKCR_EXPL_BOM~GET_BOM.

*** Call Super Implementation
    if IS_EXQTY is supplied.
      RS_EXBOM = SUPER->/VCXI/IF_VKCR_EXPL_BOM~GET_BOM( IS_EXQTY = IS_EXQTY
                                                        IF_BMENG = IF_BMENG
                                                        IF_BMEIN = IF_BMEIN ).
    else.
      RS_EXBOM = SUPER->/VCXI/IF_VKCR_EXPL_BOM~GET_BOM( IF_BMENG = IF_BMENG
                                                        IF_BMEIN = IF_BMEIN ).
    endif.

*** Mark as Fixed Quantity
    move ABAP_TRUE to RS_EXBOM-FMENG.
    move ABAP_TRUE to RS_EXBOM-FMENG_X.

*** Mark as not costing relevant
    move ABAP_FALSE to RS_EXBOM-SANKA.
    move ABAP_TRUE  to RS_EXBOM-SANKA_X.

  endmethod.
  method GET_BOM_CONSU.

*** No adjustment to Explosion Quantity due to "Fix Quantity" flag
    RS_CONSU = IR_PR_MC->GET_CONSU( ).
    RS_CONSU-CONSU *= -1.

  endmethod.
endclass. "ZCL_VCXI_XCSP_EA_MR_TOOL implementation
