
class ZCL_VCXI_FBSS_TI_BFG definition
  public
  inheriting from ZCL_VCXI_XCS_TI
  create public .

public section.

  constants C_BAG_CUST type UI_FUNC value 'BAG_CUST' ##NO_TEXT.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
    raising
      /VCXI/CX_CKX .

  methods /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ
    redefinition .
  methods /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL
    redefinition .
protected section.

  data R_PR_BFG type ref to ZCL_VCXI_FBSS_PR_BFG .

  methods HANDLE_UC_IBAGT_CUST
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_FBSS_TI_BFG definition
class ZCL_VCXI_FBSS_TI_BFG implementation.
  method /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ.

    data: LF_DISABLED type CUA_ACTIVE,
          LF_IBAGT    type ZVCXI_FBS_IBAGT.

    SUPER->/VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ( IR_MENU = IR_MENU ).

*** Get Bag Type
    ME->R_PR_BFG->GET_IBAGT( importing EF_IBAGT = LF_IBAGT ).

*** Disable if Bag Type empty
    if LF_IBAGT is initial.
      LF_DISABLED = ABAP_TRUE.
    endif.

    IR_MENU->ADD_FUNCTION( exporting FCODE    = C_BAG_CUST
                                     TEXT     = 'Bag Type Customizing'(001)
                                     DISABLED = LF_DISABLED ).

  endmethod.
  method /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL.

    SUPER->/VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL( IF_FCODE = IF_FCODE ).

    case IF_FCODE.
      when C_BAG_CUST.
        ME->HANDLE_UC_IBAGT_CUST( ).
    endcase.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

    try.
        move ME->R_GATE->R_CLSPR ?to R_PR_BFG.
      catch CX_SY_MOVE_CAST_ERROR.
***   TI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '802'.
    endtry.

  endmethod.
  method HANDLE_UC_IBAGT_CUST.

    data: LF_IBAGT type ZVCXI_FBS_IBAGT,
          LF_PARAM type STRING.

*** Get Bag Type
    ME->R_PR_BFG->GET_IBAGT( importing EF_IBAGT = LF_IBAGT ).

*** Check if exists
    select count(*) from ZVCXI_FBS_TC500 where IBAGT eq LF_IBAGT.
    check SY-SUBRC is initial.

*** Open Bag Type Customizing
    concatenate 'PA_IBAGT=' LF_IBAGT into LF_PARAM.

*** Create new Mode and start Transaction
    call function 'TH_CREATE_MODE'
      exporting
        TRANSAKTION    = 'ZVCXI_FBSSC_BAGTY'
        PARAMETERS     = LF_PARAM
        PROCESS_DARK   = ABAP_TRUE
      exceptions
        MAX_SESSIONS   = 1
        INTERNAL_ERROR = 2
        NO_AUTHORITY   = 3
        others         = 4.
    case SY-SUBRC.
      when 0.
      when 1.
***   Maximum number of sessions reached
        /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( IF_MSGTY = 'I'
                                              IF_MSGID = '14'
                                              IF_MSGNO = '027' ).
      when others.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
    endcase.

  endmethod.
