
class ZCL_VCXI_P0SP_GRID_MFSI_P definition
  public
  inheriting from ZCL_VCXI_XCS_GRID_PRVDR
  create public .

public section.

  type-pools ABAP .
  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_CONTAINER_NAME type SCRFNAME
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL default ABAP_TRUE
    raising
      /VCXI/CX_CKX .
protected section.

  methods GET_MFSI_P_VIS
    importing
      !IR_PR_MFSI_P type ref to ZCL_VCXI_P0SP_PR_MFSI_P
    returning
      value(RS_MFSI_P_VIS) type ZVCXI_P0SP_S_MFSI_P_VIS
    raising
      /VCXI/CX_CKX .
  methods SET_MFSI_P_VIS
    importing
      !IR_PR_MFSI_P type ref to ZCL_VCXI_P0SP_PR_MFSI_P
      !IS_MFSI_P_VIS type ZVCXI_P0SP_S_MFSI_P_VIS
    raising
      /VCXI/CX_CKX .
  methods CHANGE_STYLE_MFSI_P
    changing
      !CT_STYL type LVC_T_STYL
    raising
      /VCXI/CX_CKX .

  methods GET_FIELDCAT
    redefinition .
  methods GET_GRID_DATA
    redefinition .
  methods HANDLE_DATA_CHANGED_FINISHED
    redefinition .
private section.
endclass. "ZCL_VCXI_P0SP_GRID_MFSI_P definition
class ZCL_VCXI_P0SP_GRID_MFSI_P implementation.
method CHANGE_STYLE_MFSI_P.

  free CT_STYL.

*** Make PC Direction changeable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PCDIR'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set ID of MF x-connection = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'IMFXC'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Material Flow in Factor = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MFICF'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Material Flow in Factor per = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MFICP'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Material Flow in Setup Percentage = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MFISP'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Next Low Level Code = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'NEXTL'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

***--------------------------------------------------------------------------------------------
*** Set Style Details -> all other DISABLED because nothing needs to be edit in MFSI
*** Set Sheet Matnr = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MATNR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Consumption = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'CONSU'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Component Scrap = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'KAUSF'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Material Flow Ratio = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MFRAT'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Low Level Code = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'DISST'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set PC Type = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PCTYP'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set PC length = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PCLEN'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set PC width = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PCWID'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set PC height = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PCHEI'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set PC Ups = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PCUPS'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Number of Outs = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'NOOUT'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).


endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_GATE           = IR_GATE
                      IR_UIGATE         = IR_UIGATE
                      IF_TABNAME        = 'ZVCXI_P0SP_S_MFSI_P_GRID'
                      IF_CONTAINER_NAME = IF_CONTAINER_NAME
                      IF_APPL_EVENTS    = IF_APPL_EVENTS ).

*** Define Provider Type for Button
  ME->F_IPRVT = ZIF_VCXI_XCSP_MFI=>C_IPRVT_XCS_MFI.

endmethod.
method GET_FIELDCAT.

  data:          LF_LOCKED type /VCXI/CKX_LOCKED,
                 LF_INUSE  type /VCXI/VKSR_INUSE.
  field-symbols: <S_FCAT>  type LVC_S_FCAT.

  RT_FCAT = SUPER->GET_FIELDCAT( ).

*** Get Lock and In-Use Status
  LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
  LF_INUSE  = ME->R_GATE->IS_INUSE( ).

  loop at RT_FCAT assigning <S_FCAT>.
    case <S_FCAT>-FIELDNAME.
      when 'POSID' or 'GATKY'.
        move 'X' to <S_FCAT>-TECH.

      when 'PCDIR'.
        if LF_LOCKED eq ABAP_TRUE and
           LF_INUSE  eq ABAP_TRUE.
          move ABAP_TRUE    to <S_FCAT>-EDIT.
        endif.

    endcase.
  endloop.

endmethod.
method GET_GRID_DATA.

  data:          LS_MFSI_P_VIS        type          ZVCXI_P0SP_S_MFSI_P_VIS,
                 LS_MFSI_P_GRID       type          ZVCXI_P0SP_S_MFSI_P_GRID.
  data:          LT_GATE              type          /VCXI/VKSR_TT_GATE.
  data:          LR_GATE              type ref to   /VCXI/CL_VKSR_GATE,
                 LR_PR_MFSI_P         type ref to   ZCL_VCXI_P0SP_PR_MFSI_P.
  field-symbols: <T_DATA>             type standard table,
                 <S_DATA>             type          any.


*** Dereferencing of Data Reference
  assign IR_DATA->* to <T_DATA>.

*** Get Gates of MFSO_P
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_P0SP_PR_MFSI_P=>C_IPRVT_P0_MFSI_P
                                   IF_INUSE  = ABAP_TRUE
                                   IF_SORTED = ABAP_TRUE ).

  loop at LT_GATE into LR_GATE.
    try.
        move LR_GATE->R_CLSPR ?to LR_PR_MFSI_P.
        check LR_PR_MFSI_P is bound.

        move LR_GATE->GET_POSID( ) to LS_MFSI_P_GRID-POSID.
        move LR_GATE->F_GATKY      to LS_MFSI_P_GRID-GATKY.
        move LR_GATE               to LS_MFSI_P_GRID-R_GATE.

        LS_MFSI_P_VIS = ME->GET_MFSI_P_VIS( IR_PR_MFSI_P = LR_PR_MFSI_P ).
        move-corresponding LS_MFSI_P_VIS to LS_MFSI_P_GRID.

***--------------------------------------------------------------------------------------
***     Change Style
***     Set Style Details -> all DISABLED because nothing needs to be edit in MFSI
        ME->CHANGE_STYLE_MFSI_P( changing CT_STYL = LS_MFSI_P_GRID-T_STYL ).

***     Call DVC Service - Modify Style
        ZCL_VCXI_XCDR_SERVICE=>MODIFY_STYLE( exporting IR_GATE = LR_GATE
                                             changing  CT_STYL = LS_MFSI_P_GRID-T_STYL ).

***--------------------------------------------------------------------------------------
***     Add Entry
        append initial line to <T_DATA> assigning <S_DATA>.
        move-corresponding LS_MFSI_P_GRID to <S_DATA>.

      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.
  endloop.

endmethod.
method GET_MFSI_P_VIS.

  data:  LS_CONSU type ZVCXI_XCSP_S_CONSU,
         LS_MFISP type ZVCXI_XCSP_S_MFISP,
         LS_KAUSF type ZVCXI_XCSP_S_KAUSF,
         LS_PCDIM type ZVCXI_P0SP_S_PCDIM.

*** Get MaterialNumber, Description and Low Level Code
  move IR_PR_MFSI_P->GET_MATNR( )       to RS_MFSI_P_VIS-MATNR.
  move IR_PR_MFSI_P->GET_MATNR_DESCR( ) to RS_MFSI_P_VIS-MATNR_DESCR.
  move IR_PR_MFSI_P->GET_DISST( )       to RS_MFSI_P_VIS-DISST.

*** Get Consumption
  move IR_PR_MFSI_P->GET_CONSU( )       to LS_CONSU.
  move-corresponding LS_CONSU           to RS_MFSI_P_VIS.

*** Get Consumption Factor and per
  move IR_PR_MFSI_P->GET_MFICF( )       to RS_MFSI_P_VIS-MFICF.
  move IR_PR_MFSI_P->GET_MFICP( )       to RS_MFSI_P_VIS-MFICP.

*** Get Setup Percentage
  move IR_PR_MFSI_P->GET_MFISP( )       to LS_MFISP.
  move-corresponding LS_MFISP           to RS_MFSI_P_VIS.

*** Get Component Scrap
  move IR_PR_MFSI_P->GET_KAUSF( )       to LS_KAUSF.
  move-corresponding LS_KAUSF           to RS_MFSI_P_VIS.

*** Get PC Dimensions
  move IR_PR_MFSI_P->GET_PCDIM( )       to LS_PCDIM.
  move-corresponding LS_PCDIM           to RS_MFSI_P_VIS.

endmethod.
method HANDLE_DATA_CHANGED_FINISHED.

  data:          LS_MFSI_P_VIS       type          ZVCXI_P0SP_S_MFSI_P_VIS,
                 LS_MFSI_P_GRID      type          ZVCXI_P0SP_S_MFSI_P_GRID.
  data:          LR_PR_MFSI_P        type ref to   ZCL_VCXI_P0SP_PR_MFSI_P,
                 LR_CX_CKX           type ref to   /VCXI/CX_CKX.
  field-symbols: <T_DATA>            type standard table,
                 <S_DATA>            type          any.

  check E_MODIFIED eq ABAP_TRUE.

  try.

      SUPER->HANDLE_DATA_CHANGED_FINISHED( E_MODIFIED    = E_MODIFIED
                                           ET_GOOD_CELLS = ET_GOOD_CELLS ).

***   Dereferencing of Data Reference
      assign ME->R_DATA->* to <T_DATA>.

      loop at <T_DATA> assigning <S_DATA>.
        move-corresponding <S_DATA> to LS_MFSI_P_GRID.
        check LS_MFSI_P_GRID-R_GATE is bound.

        move LS_MFSI_P_GRID-R_GATE->R_CLSPR ?to LR_PR_MFSI_P.

***     Set Values to PR Class
        move-corresponding LS_MFSI_P_GRID to LS_MFSI_P_VIS.
        ME->SET_MFSI_P_VIS( IR_PR_MFSI_P  = LR_PR_MFSI_P
                            IS_MFSI_P_VIS = LS_MFSI_P_VIS ).
      endloop.

      ME->R_GATE->RAISE_EVENTS( ).

    catch /VCXI/CX_CKX into LR_CX_CKX.
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

*** Request Refresh for ALV
  move ABAP_TRUE to ME->F_REFRESH_REQ.


endmethod.
method SET_MFSI_P_VIS.

  data:  LS_MFSI_P_VIS   type ZVCXI_P0SP_S_MFSI_P_VIS,
         LS_PCDIM        type ZVCXI_P0SP_S_PCDIM.

*** Get Current data to do Update only if Value changed.
  LS_MFSI_P_VIS = ME->GET_MFSI_P_VIS( IR_PR_MFSI_P = IR_PR_MFSI_P ).

***----------------------------------------------------------------------------
*** Set PC Direction
  if LS_MFSI_P_VIS-PCDIR ne IS_MFSI_P_VIS-PCDIR.
    move-corresponding LS_MFSI_P_VIS to LS_PCDIM.
    move               IS_MFSI_P_VIS-PCDIR to LS_PCDIM-PCDIR.
    IR_PR_MFSI_P->SET_PCDIM( IS_PCDIM = LS_PCDIM
                             IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

endmethod.
