
class ZCL_VCXI_FFSP_PR_ACTVY_FOLD definition
  public
  inheriting from /VCXI/CL_VKSR_CLSPR
  create public .

public section.

  constants C_PARID_TFOLD type /VCXI/VKS_PARID value 'ZVCXI_FF_TFOLD' ##NO_TEXT.

  methods GET_TFOLD
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
      !EF_TFOLD type ZVCXI_FFS_TFOLD
      !EF_DESCR type /VCXI/CKX_DESCR
    raising
      /VCXI/CX_CKX .
  methods SET_TFOLD
    importing
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
      !IF_TFOLD type ZVCXI_FFS_TFOLD
    raising
      /VCXI/CX_CKX .
  methods GET_NOLAY
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
      !EF_NOLAY type ZVCXI_F0S_NOLAY
    raising
      /VCXI/CX_CKX .
protected section.

  constants C_IEVNT_O_TFOLD_CHANGED type /VCXI/VKSC_IEVNT value 'TFOLD_CHANGED' ##NO_TEXT.
private section.
endclass. "ZCL_VCXI_FFSP_PR_ACTVY_FOLD definition
class ZCL_VCXI_FFSP_PR_ACTVY_FOLD implementation.
  method GET_NOLAY.

    data: LF_TFOLD type ZVCXI_FFS_TFOLD.

    clear EF_NOLAY.

*** Get Folding Type
    ME->GET_TFOLD( importing EF_TFOLD = LF_TFOLD ).

    check LF_TFOLD is not initial.

*** Get Number of Base Layers from Customizing
    select single NOLAY
             from ZVCXI_FFS_TC140
             into EF_NOLAY
            where TFOLD eq LF_TFOLD.

  endmethod.
  method GET_TFOLD.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TFOLD ).
    move LS_PARAM-ATWRT to EF_TFOLD.
    move LS_PARAM-SETBY to EF_SETBY.

    check EF_TFOLD is not initial.

    if EF_DESCR is requested.
      select single DESCR
             from ZVCXI_FFS_TC140T
             into EF_DESCR
            where LANGU eq SY-LANGU
             and  TFOLD eq EF_TFOLD.
    endif.

  endmethod.
  method SET_TFOLD.

    data: LF_CHANGED type ABAP_BOOL.
    data: LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TFOLD ).

    move IF_TFOLD to LS_PARAM-ATWRT.
    move IF_SETBY to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_TFOLD_CHANGED ).
    endif.

  endmethod.
