
class ZCL_VCXI_F0SS_EA_ALTUOM definition
  public
  inheriting from /VCXI/CL_VKSR_CLSEA
  create public .

public section.

  methods GET_ALTUOM_DEF
    returning
      value(RT_ALTUOM_DEF) type ZVCXI_F0SS_TT_ALTUOM_DEF
    raising
      /VCXI/CX_CKX .
  methods GET_QITEM_AS_ALTUOM
    importing
      !IF_UOM type /VCXI/CKX_UOM
    returning
      value(RS_QITEM) type ZVCXI_XCSS_S_QITEM
    raising
      /VCXI/CX_CKX .
  methods GET_UMREZ_UMREN
    importing
      !IF_UOM type /VCXI/CKX_UOM optional
    exporting
      !EF_UMREZ type UMREZ
      !EF_UMREN type UMREN
    raising
      /VCXI/CX_CKX .
  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
protected section.

  data R_PR_ALTUOM type ref to ZCL_VCXI_F0SS_PR_ALTUOM .
private section.
endclass. "ZCL_VCXI_F0SS_EA_ALTUOM definition
class ZCL_VCXI_F0SS_EA_ALTUOM implementation.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_GATE = IR_GATE ).

*** Assign the PR Class to the Element API Class
    try.
        move ME->R_GATE->R_CLSPR ?to ME->R_PR_ALTUOM.
      catch CX_SY_MOVE_CAST_ERROR.
***     Element API ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '804'.
    endtry.

  endmethod.
  method GET_ALTUOM_DEF.

    check ME->R_PR_ALTUOM is bound and
          ME->R_PR_ALTUOM->R_BL_ALTUOM is bound.
    RT_ALTUOM_DEF = ME->R_PR_ALTUOM->R_BL_ALTUOM->T_ALTUOM_DEF.

  endmethod.
  method GET_QITEM_AS_ALTUOM.

    check ME->R_PR_ALTUOM is bound.
    RS_QITEM = ME->R_PR_ALTUOM->GET_QITEM_AS_ALTUOM( IF_UOM = IF_UOM ).

  endmethod.
  method GET_UMREZ_UMREN.

    data:  LF_UOM         type        /VCXI/CKX_UOM.
    data:  LR_APPDATA     type ref to ZCL_VCXI_XCS_APPDATA.

    clear: EF_UMREZ,
           EF_UMREN.

    check ME->R_PR_ALTUOM is bound.

    if IF_UOM is not supplied.
***   Get Conversion Factor for Operating Quantity Unit
***   Get Application Data
      move ME->R_GATE->GET_APPDATA( ) ?to LR_APPDATA.
      check LR_APPDATA is bound.
      if LR_APPDATA->S_AUQTY is initial.
***     No VBAP Sales Unit found
        move LR_APPDATA->S_OPQTY-OPQTY_UOM to LF_UOM.
      else.
***     Get Sales Unit
        move LR_APPDATA->S_AUQTY-AUQTY_UOM to LF_UOM.
      endif.

    else.
***   Get Conversion Factor for supplied Unit
      move IF_UOM to LF_UOM.
    endif.

***--------------------------------------------------------------------------------------
*** Get Conversion Factors
    ME->R_PR_ALTUOM->GET_UMREZ_UMREN( exporting IF_UOM   = LF_UOM
                                      importing EF_UMREZ = EF_UMREZ
                                                EF_UMREN = EF_UMREN ).

  endmethod.
