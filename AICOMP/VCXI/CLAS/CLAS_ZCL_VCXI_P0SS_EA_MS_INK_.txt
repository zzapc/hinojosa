
class ZCL_VCXI_P0SS_EA_MS_INK definition
  public
  inheriting from ZCL_VCXI_XCSS_EA_MS
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods GET_COLCO
    exporting
      value(EF_COLCO) type ZVCXI_P0SS_COLCO
      value(EF_COLCO_DESCR) type /VCXI/CKX_DESCR
    raising
      /VCXI/CX_CKX .
  methods GET_COVRG
    returning
      value(RS_COVRG) type ZVCXI_P0S_S_COVRG
    raising
      /VCXI/CX_CKX .
  methods GET_TOOL
    exporting
      value(EF_INKTN) type ZVCXI_P0SS_INKTN
      value(ES_LTITO) type ZVCXI_P0S_S_LTITO
    raising
      /VCXI/CX_CKX .
  methods SET_TOOL
    importing
      !IF_INKTN type ZVCXI_P0SS_INKTN
      !IS_LTITO type ZVCXI_P0S_S_LTITO optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
      !IF_FORCE_LOAD type ABAP_BOOL default ABAP_FALSE
      !IF_RAISE_EVENTS type ABAP_BOOL default ABAP_TRUE
    raising
      /VCXI/CX_CKX .
protected section.

  data R_PR_MS_INK type ref to ZCL_VCXI_P0SS_PR_MS_INK .
private section.
endclass. "ZCL_VCXI_P0SS_EA_MS_INK definition
class ZCL_VCXI_P0SS_EA_MS_INK implementation.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_GATE = IR_GATE ).

*** Assign the PR Class to the Element API Class
  try.
      move ME->R_GATE->R_CLSPR ?to ME->R_PR_MS_INK.
    catch CX_SY_MOVE_CAST_ERROR.
***   Element API ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '804'.
  endtry.

endmethod.
method GET_COLCO.

  check ME->R_PR_MS_INK is bound.

***----------------------------------------------------------------------------
*** Get Color Code
  ME->R_PR_MS_INK->GET_COLCO( importing EF_COLCO = EF_COLCO ).

*** Get Color Code Description
  if EF_COLCO_DESCR is requested and
     EF_COLCO is not initial.
    move ME->R_PR_MS_INK->GET_COLCO_DESCR( IF_COLCO = EF_COLCO ) to EF_COLCO_DESCR.
  endif.

endmethod.
method GET_COVRG.

  check ME->R_PR_MS_INK is bound.

*** Coverage
  ME->R_PR_MS_INK->GET_COVRG( importing ES_COVRG = RS_COVRG ).

endmethod.
method GET_TOOL.

  check ME->R_PR_MS_INK is bound.

***----------------------------------------------------------------------------
*** Get Ink Tool Number
  if EF_INKTN is requested.
    move ME->R_PR_MS_INK->GET_INKTN( ) to EF_INKTN.
  endif.

*** Get Tool Life Time
  if ES_LTITO is requested.
    move ME->R_PR_MS_INK->GET_LTITO( ) to ES_LTITO.
  endif.

endmethod.
method SET_TOOL.

  check ME->R_PR_MS_INK is bound and
        ME->R_GATE->IS_LOCKED( ) eq ABAP_TRUE.

***----------------------------------------------------------------------------
*** Set Ink Tool Number
  ME->R_PR_MS_INK->SET_INKTN( IF_INKTN = IF_INKTN
                              IF_SETBY = IF_SETBY ).
  if IF_FORCE_LOAD eq ABAP_TRUE.
    ME->R_GATE->SET_EVENT( IF_IEVNT = ZCL_VCXI_P0SS_PR_MS_INK=>C_IEVNT_O_TOOL_CHANGED ).
  endif.

*** Set Tool Life Time
  if IS_LTITO is supplied.
    ME->R_PR_MS_INK->SET_LTITO( IS_LTITO = IS_LTITO
                                IF_SETBY = IF_SETBY ).
  endif.

***----------------------------------------------------------------------------
*** Process Events
  if IF_RAISE_EVENTS eq ABAP_TRUE.
    ME->R_GATE->RAISE_EVENTS( ).
  endif.

endmethod.
