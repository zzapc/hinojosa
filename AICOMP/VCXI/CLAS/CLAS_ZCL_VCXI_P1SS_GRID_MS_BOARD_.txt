
class ZCL_VCXI_P1SS_GRID_MS_BOARD definition
  public
  inheriting from ZCL_VCXI_XCS_GRID
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABNAME type TABNAME default 'ZVCXI_P1SS_S_MS_BOARD_GRID'
      !IF_CONTAINER_NAME type SCRFNAME
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL default ABAP_TRUE
    raising
      /VCXI/CX_CKX .
protected section.

  constants C_FIELDNAME_RGATE type FIELDNAME value 'R_GATE' ##NO_TEXT.
  data F_SUBPA_IS_ACTIV type ABAP_BOOL .
  data R_PR_BOARD type ref to ZCL_VCXI_P1SS_PR_BOARD .

  methods CHANGE_STYLE_PAPER
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IS_DATA type ANY
    changing
      !CT_STYL type LVC_T_STYL
    raising
      /VCXI/CX_CKX .
  methods CHANGE_STYLE_STARCH
    importing
      !IF_PURCH type ZVCXI_XCS_PURCH
    changing
      !CT_STYL type LVC_T_STYL
    raising
      /VCXI/CX_CKX .
  methods CHANGE_STYLE_WAX
    importing
      !IF_PURCH type ZVCXI_XCS_PURCH
    changing
      !CT_STYL type LVC_T_STYL
    raising
      /VCXI/CX_CKX .
  methods GET_MS_PAPER_VIS
    importing
      !IR_PR_MS_PAPER type ref to ZCL_VCXI_P1SS_PR_MS_PAPER
    returning
      value(RS_MS_PAPER_VIS) type ZVCXI_P1SS_S_MS_BOARD_VIS
    raising
      /VCXI/CX_CKX .
  methods GET_MS_STARCH_VIS
    importing
      !IR_PR_MS_STARCH type ref to ZCL_VCXI_P1SS_PR_MS_STARCH
    returning
      value(RS_MS_STARCH_VIS) type ZVCXI_P1SS_S_MS_BOARD_VIS
    raising
      /VCXI/CX_CKX .
  methods GET_MS_WAX_VIS
    importing
      !IR_PR_MS_WAX type ref to ZCL_VCXI_P1SS_PR_MS_WAX
    returning
      value(RS_MS_WAX_VIS) type ZVCXI_P1SS_S_MS_BOARD_VIS
    raising
      /VCXI/CX_CKX .
  methods HANDLE_DOUBLE_CLICK
    for event DOUBLE_CLICK of CL_GUI_ALV_GRID
    importing
      !E_ROW
      !E_COLUMN
      !ES_ROW_NO .
  methods HANDLE_HOTSPOT_CLICK
    for event HOTSPOT_CLICK of CL_GUI_ALV_GRID
    importing
      !E_ROW_ID
      !E_COLUMN_ID
      !ES_ROW_NO .
  methods SET_MS_PAPER_VIS
    importing
      !IR_PR_MS_PAPER type ref to ZCL_VCXI_P1SS_PR_MS_PAPER
      !IS_MS_PAPER_VIS type ZVCXI_P1SS_S_MS_BOARD_VIS
    raising
      /VCXI/CX_CKX .
  methods SET_MS_STARCH_VIS
    importing
      !IR_PR_MS_STARCH type ref to ZCL_VCXI_P1SS_PR_MS_STARCH
      !IS_MS_STARCH_VIS type ZVCXI_P1SS_S_MS_BOARD_VIS
    raising
      /VCXI/CX_CKX .
  methods SET_MS_WAX_VIS
    importing
      !IR_PR_MS_WAX type ref to ZCL_VCXI_P1SS_PR_MS_WAX
      !IS_MS_WAX_VIS type ZVCXI_P1SS_S_MS_BOARD_VIS
    raising
      /VCXI/CX_CKX .

  methods GET_FIELDCAT
    redefinition .
  methods GET_GRID_DATA
    redefinition .
  methods HANDLE_DATA_CHANGED_FINISHED
    redefinition .
  methods SET_EVENT
    redefinition .
private section.
endclass. "ZCL_VCXI_P1SS_GRID_MS_BOARD definition
class ZCL_VCXI_P1SS_GRID_MS_BOARD implementation.
  method CHANGE_STYLE_PAPER.

    data: LF_DUMMY         type ABAP_BOOL,
          LF_STYLE_SUBPA   type LVC_STYLE,
          LF_STYLE_HOTSPOT type LVC_STYLE.
    data: LS_MS_PAPER_VIS type ZVCXI_P1SS_S_MS_PAPER_VIS.
    data: LR_PR_MS_PAPER type ref to ZCL_VCXI_P1SS_PR_MS_PAPER.

    free CT_STYL.

***--------------------------------------------------------------------------------------
*** Get PR Class
    try.
        move IR_GATE->R_CLSPR ?to LR_PR_MS_PAPER.
      catch CX_SY_MOVE_CAST_ERROR.
    endtry.

*** "Cast" generic structure to Vis Structure
    move-corresponding IS_DATA to LS_MS_PAPER_VIS.

    check LR_PR_MS_PAPER is bound.
    LF_DUMMY = LR_PR_MS_PAPER->IS_DUMMY( ).

***--------------------------------------------------------------------------------------
*** Set Style Details -> only Editable if Substitute Paper is activ
    if LS_MS_PAPER_VIS-SUBPA            eq ABAP_TRUE and
       ME->F_STYLE_ENABLED eq CL_GUI_ALV_GRID=>MC_STYLE_ENABLED.
      move ME->F_STYLE_ENABLED to LF_STYLE_SUBPA.
    elseif LS_MS_PAPER_VIS-SUBPA eq ABAP_FALSE.
      move ME->F_STYLE_DISABLED to LF_STYLE_SUBPA.
    elseif LS_MS_PAPER_VIS-SUBPA eq ABAP_UNDEFINED.
      move ME->F_STYLE_DISABLED to LF_STYLE_SUBPA.
    endif.

    LF_STYLE_HOTSPOT = ME->F_STYLE_ENABLED + switch #( ME->F_STYLE_ENABLED when CL_GUI_ALV_GRID=>MC_STYLE_ENABLED then CL_GUI_ALV_GRID=>MC_STYLE_HOTSPOT
                                                                                                                  else CL_GUI_ALV_GRID=>MC_STYLE_HOTSPOT_NO ).

*** Set Material = Editable if it is substituted
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MATNR'
                                IF_STYLE     = LF_STYLE_SUBPA
                      changing  CT_STYL      = CT_STYL ).

*** Set Material Description = Not Editable
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MATNR_DESCR'
                                IF_STYLE     = ME->F_STYLE_DISABLED
                      changing  CT_STYL      = CT_STYL ).

*** Set Substitute Paper = Editable
    if LS_MS_PAPER_VIS-SUBPA ne ABAP_UNDEFINED.
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'SUBPA'
                                  IF_STYLE     = LF_STYLE_HOTSPOT
                        changing  CT_STYL      = CT_STYL ).
    else.
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'SUBPA'
                                  IF_STYLE     = conv #( ME->F_STYLE_DISABLED + CL_GUI_ALV_GRID=>MC_STYLE_HOTSPOT_NO )
                        changing  CT_STYL      = CT_STYL ).
    endif.

***------------------------------------------------------------------
*** Check if Dummy Material
    if LF_DUMMY eq ABAP_TRUE.
***   Set Base Weight, Base Weight Unit and Caliper = Editable
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGT'
                                  IF_STYLE     = ME->F_STYLE_ENABLED
                        changing  CT_STYL      = CT_STYL ).
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGP'
                                  IF_STYLE     = ME->F_STYLE_ENABLED
                        changing  CT_STYL      = CT_STYL ).
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'CALIP'
                                  IF_STYLE     = ME->F_STYLE_ENABLED
                        changing  CT_STYL      = CT_STYL ).
    else.
***   Set Base Weight, Base Weight Unit and Caliper = Editable
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGT'
                                  IF_STYLE     = ME->F_STYLE_DISABLED
                        changing  CT_STYL      = CT_STYL ).
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGP'
                                  IF_STYLE     = ME->F_STYLE_DISABLED
                        changing  CT_STYL      = CT_STYL ).
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'CALIP'
                                  IF_STYLE     = ME->F_STYLE_DISABLED
                        changing  CT_STYL      = CT_STYL ).
    endif.

***------------------------------------------------------------------
*** Purchase Paper
    case LS_MS_PAPER_VIS-PURCH.
      when ZCL_VCXI_XCSS_PR_MS=>C_PURCH_DIRECT_PRICE.
***     Set Vendor = Enable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR'
                                    IF_STYLE     = ME->F_STYLE_ENABLED
                          changing  CT_STYL      = CT_STYL ).
***     Set Price = Enable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                                    IF_STYLE     = ME->F_STYLE_ENABLED
                          changing  CT_STYL      = CT_STYL ).
***     Set Price Per = Enable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                                    IF_STYLE     = ME->F_STYLE_ENABLED
                          changing  CT_STYL      = CT_STYL ).

      when ZCL_VCXI_XCSS_PR_MS=>C_PURCH_DIRECT.
***     Set Vendor = Enable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR'
                                    IF_STYLE     = ME->F_STYLE_ENABLED
                          changing  CT_STYL      = CT_STYL ).
***     Set Price = Disable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                                    IF_STYLE     = ME->F_STYLE_DISABLED
                          changing  CT_STYL      = CT_STYL ).
***     Set Price Per = Disable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                                    IF_STYLE     = ME->F_STYLE_DISABLED
                          changing  CT_STYL      = CT_STYL ).

      when ZCL_VCXI_XCSS_PR_MS=>C_PURCH_STOCK.
***     Set Vendor = Disable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR'
                                    IF_STYLE     = ME->F_STYLE_DISABLED
                          changing  CT_STYL      = CT_STYL ).
***     Set Price = Disable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                                    IF_STYLE     = ME->F_STYLE_DISABLED
                          changing  CT_STYL      = CT_STYL ).
***     Set Price Per = Disable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                                    IF_STYLE     = ME->F_STYLE_DISABLED
                          changing  CT_STYL      = CT_STYL ).
    endcase.

***------------------------------------------------------------------
*** Purchase Flag
    if LF_DUMMY eq ABAP_TRUE or
       LS_MS_PAPER_VIS-SUBPA eq ABAP_UNDEFINED.
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PURCH'
                                  IF_STYLE     = ME->F_STYLE_DISABLED
                        changing  CT_STYL      = CT_STYL ).
    else.
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PURCH'
                                  IF_STYLE     = ME->F_STYLE_ENABLED
                        changing  CT_STYL      = CT_STYL ).
    endif.

    if LF_DUMMY eq ABAP_TRUE.
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'FPTOS'
                                  IF_STYLE     = ME->F_STYLE_ENABLED
                        changing  CT_STYL      = CT_STYL ).
    else.
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'FPTOS'
                                  IF_STYLE     = ME->F_STYLE_DISABLED
                        changing  CT_STYL      = CT_STYL ).
    endif.

  endmethod.
method CHANGE_STYLE_STARCH.

  data: LF_STYLE_ENABLED        type LVC_STYLE,
        LF_STYLE_DISABLED_SUBPA type LVC_STYLE.

  free: CT_STYL.

***--------------------------------------------------------------------------------------
*** Set Style Details -> only Editable if Material is purchased
  if IF_PURCH            eq ABAP_TRUE and
     ME->F_STYLE_ENABLED eq CL_GUI_ALV_GRID=>MC_STYLE_ENABLED.
    move ME->F_STYLE_ENABLED  to LF_STYLE_ENABLED.
  elseif IF_PURCH eq ABAP_FALSE.
    move ME->F_STYLE_DISABLED to LF_STYLE_ENABLED.
  endif.

  LF_STYLE_DISABLED_SUBPA = ME->F_STYLE_DISABLED + CL_GUI_ALV_GRID=>MC_STYLE_HOTSPOT_NO.

*** Set Material = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MATNR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Purchase = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PURCH'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'FPTOS'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Price and Price Unit = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                              IF_STYLE     = LF_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE_CURR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                              IF_STYLE     = LF_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP_UOM'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Vendor and Description = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR_DESCR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Substitute Paper = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'SUBPA'
                              IF_STYLE     = LF_STYLE_DISABLED_SUBPA
                    changing  CT_STYL      = CT_STYL ).

*** Set Base Weight, Base Weight Unit and Caliper = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGT'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGT_UOM'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGP'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGP_UOM'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'CALIP'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'CALIP_UOM'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'SLDCO'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'SLDCO_UOM'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

endmethod.
method CHANGE_STYLE_WAX.

  data: LF_STYLE_ENABLED        type LVC_STYLE,
        LF_STYLE_DISABLED_SUBPA type LVC_STYLE.

  free: CT_STYL.

***--------------------------------------------------------------------------------------
*** Set Style Details -> only Editable if Material is purchased
  if IF_PURCH            eq ABAP_TRUE and
     ME->F_STYLE_ENABLED eq CL_GUI_ALV_GRID=>MC_STYLE_ENABLED.
    move ME->F_STYLE_ENABLED  to LF_STYLE_ENABLED.
  elseif IF_PURCH eq ABAP_FALSE.
    move ME->F_STYLE_DISABLED to LF_STYLE_ENABLED.
  endif.

  LF_STYLE_DISABLED_SUBPA = ME->F_STYLE_DISABLED + CL_GUI_ALV_GRID=>MC_STYLE_HOTSPOT_NO.

*** Set Material = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MATNR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Material Description = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MATNR_DESCR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Purchase = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PURCH'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'FPTOS'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Price and Price Unit = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                              IF_STYLE     = LF_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE_CURR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                              IF_STYLE     = LF_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP_UOM'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Vendor and Description = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR_DESCR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Substitute Paper = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'SUBPA'
                              IF_STYLE     = LF_STYLE_DISABLED_SUBPA
                    changing  CT_STYL      = CT_STYL ).

*** Set Base Weight and Base Weight Unit = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGT'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGT_UOM'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGP'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGP_UOM'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'CALIP'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'CALIP_UOM'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'SLDCO'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'SLDCO_UOM'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_GATE           = IR_GATE
                        IR_UIGATE         = IR_UIGATE
                        IF_TABNAME        = IF_TABNAME
                        IF_CONTAINER_NAME = IF_CONTAINER_NAME
                        IF_APPL_EVENTS    = IF_APPL_EVENTS ).

    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_BOARD.
      catch CX_SY_MOVE_CAST_ERROR.
        clear: ME->R_PR_BOARD.
    endtry.

  endmethod.
  method GET_FIELDCAT.

    data: LF_LOCKED type /VCXI/CKX_LOCKED,
          LF_INUSE  type /VCXI/VKSR_INUSE,
          LF_LIFNR  type ABAP_BOOL,
          LF_PRICE  type ABAP_BOOL,
          LF_SUBPA  type ZVCXI_PCSS_SUBPA,
          LF_FPRPR  type ZVCXI_PCSS_FPRPR,
          LF_DUMMY  type ABAP_BOOL,
          LF_COLPOS type I.
    data: LS_MS_BOARD_GRID type ZVCXI_P1SS_S_MS_BOARD_GRID.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type ANY,
                   <S_FCAT> type LVC_S_FCAT.

***--------------------------------------------------------------------------------------
*** Get Standard Field Catalog
    RT_FCAT = SUPER->GET_FIELDCAT( ).

*** Get Lock and In-Use Status
    LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
    LF_INUSE  = ME->R_GATE->IS_INUSE( ).

*** Dereferencing of Data Reference
    assign ME->R_DATA->* to <T_DATA>.

    loop at <T_DATA> assigning <S_DATA>.
      move-corresponding <S_DATA> to LS_MS_BOARD_GRID.
***   Find Purchased
      case LS_MS_BOARD_GRID-PURCH.
        when ZCL_VCXI_XCSS_PR_MS=>C_PURCH_DIRECT_PRICE.
          LF_LIFNR = ABAP_TRUE.
          LF_PRICE = ABAP_TRUE.
        when ZCL_VCXI_XCSS_PR_MS=>C_PURCH_DIRECT.
          LF_LIFNR = ABAP_TRUE.
      endcase.
***   Find Substitution
      if LS_MS_BOARD_GRID-SUBPA eq ABAP_TRUE.
        move ABAP_TRUE to LF_SUBPA.
      endif.
***   Find Preprint
      if LS_MS_BOARD_GRID-FPRPR eq ABAP_TRUE.
        move ABAP_TRUE to LF_FPRPR.
      endif.
***   Find Dummy
      if cast ZCL_VCXI_XCSS_PR_MS( LS_MS_BOARD_GRID-R_GATE->R_CLSPR )->IS_DUMMY( ).
        LF_DUMMY = ABAP_TRUE.
      endif.
    endloop.

***--------------------------------------------------------------------------------------
*** Adjust Field Catalog
    loop at RT_FCAT assigning <S_FCAT>.
      case <S_FCAT>-FIELDNAME.
        when 'POSID' or 'GATKY' or 'R_GATE' or 'INFO_FNAME' or 'ROWCOLOR'.
          move ABAP_TRUE    to <S_FCAT>-TECH.

        when 'BPAID' or 'SLDCO' or 'SLDCO_UOM'.
          <S_FCAT>-NO_OUT = ABAP_TRUE.

        when 'MATNR'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            <S_FCAT>-EDIT = ABAP_TRUE.
          endif.

***     Editable Substitute Paper Info
        when 'SUBPA'.
          <S_FCAT>-CHECKBOX = ABAP_TRUE.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE  to <S_FCAT>-EDIT.
            move ABAP_TRUE  to <S_FCAT>-HOTSPOT.
          endif.

        when 'BAWGT' or 'BAWGP' or 'CALIP'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            <S_FCAT>-EDIT = ABAP_TRUE.
          endif.

***     Editable Purchasing Info
        when 'PURCH'.
          if LF_LOCKED          eq ABAP_TRUE and
             LF_INUSE           eq ABAP_TRUE.
            <S_FCAT>-EDIT = ABAP_TRUE.
          endif.

        when 'FPTOS'.
          if LF_DUMMY eq ABAP_TRUE.
            <S_FCAT>-CHECKBOX = ABAP_TRUE.
            if LF_LOCKED          eq ABAP_TRUE and
               LF_INUSE           eq ABAP_TRUE.
              <S_FCAT>-EDIT = ABAP_TRUE.
            endif.
          else.
            move ABAP_TRUE to <S_FCAT>-NO_OUT.
          endif.

        when 'PRICE' or 'PRICP' or 'LIFNR'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            <S_FCAT>-EDIT = ABAP_TRUE.
          endif.

***    Preprint
        when 'FPRPR'.
          <S_FCAT>-CHECKBOX = ABAP_TRUE.
          if LF_FPRPR ne ABAP_TRUE.
            move ABAP_TRUE to <S_FCAT>-NO_OUT.
          endif.
        when 'SBSTR'.
          if LF_FPRPR ne ABAP_TRUE.
            move ABAP_TRUE to <S_FCAT>-NO_OUT.
          endif.

      endcase.

***----------------------------------------------------------------------------
***   Hide Field if not purchase
      if LF_LIFNR ne ABAP_TRUE.
        if <S_FCAT>-FIELDNAME eq 'LIFNR' or
           <S_FCAT>-FIELDNAME eq 'LIFNR_DESCR'.
          move ABAP_TRUE to <S_FCAT>-NO_OUT.
        endif.
      endif.
      if LF_PRICE ne ABAP_TRUE.
        if <S_FCAT>-FIELDNAME eq 'PRICE' or
           <S_FCAT>-FIELDNAME eq 'PRICE_CURR' or
           <S_FCAT>-FIELDNAME eq 'PRICP' or
           <S_FCAT>-FIELDNAME eq 'PRICP_UOM'.
          move ABAP_TRUE to <S_FCAT>-NO_OUT.
        endif.
      endif.

***   Hide Field if not Substitution
      if LF_SUBPA ne ABAP_TRUE.
        if <S_FCAT>-FIELDNAME eq 'BAWGT' or
           <S_FCAT>-FIELDNAME eq 'BAWGT_UOM' or
           <S_FCAT>-FIELDNAME eq 'BAWGP' or
           <S_FCAT>-FIELDNAME eq 'BAWGP_UOM' or
           <S_FCAT>-FIELDNAME eq 'CALIP' or
           <S_FCAT>-FIELDNAME eq 'CALIP_UOM'.
          move ABAP_TRUE to <S_FCAT>-NO_OUT.
        endif.
      endif.

***--------------------------------------------------------------------------------------
***   Set Position for each Field
      move 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'MATNR'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'MATNR_DESCR'. <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'SUBPA'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'BAWGT'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'BAWGT_UOM'.   <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'BAWGP'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'BAWGP_UOM'.   <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'CALIP'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'CALIP_UOM'.   <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'PURCH'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'PRICE'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'PRICE_CURR'.  <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'PRICP'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'PRICP_UOM'.   <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'LIFNR'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'LIFNR_DESCR'. <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'FPRPR'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'SBSTR'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.

    endloop.

  endmethod.
  method GET_GRID_DATA.

    data: LS_MS_BOARD_VIS  type          ZVCXI_P1SS_S_MS_BOARD_VIS,
          LS_MS_BOARD_GRID type          ZVCXI_P1SS_S_MS_BOARD_GRID.
    data: LT_GATE      type          /VCXI/VKSR_TT_GATE.
    data: LR_GATE         type ref to   /VCXI/CL_VKSR_GATE,
          LR_PR_MS_PAPER  type ref to   ZCL_VCXI_P1SS_PR_MS_PAPER,
          LR_PR_MS_STARCH type ref to   ZCL_VCXI_P1SS_PR_MS_STARCH,
          LR_PR_MS_WAX    type ref to   ZCL_VCXI_P1SS_PR_MS_WAX.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type          ANY.

*** Clear Indicator for Substitute Paper
    clear: ME->F_SUBPA_IS_ACTIV.

*** Dereferencing of Data Reference
    assign IR_DATA->* to <T_DATA>.

***--------------------------------------------------------------------------------------
*** Get Gates of MS Paper
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_P1SS_PR_MS_PAPER=>C_IPRVT_P1_MS_PAPER
                                     IF_INUSE  = ABAP_TRUE
                                     IF_SORTED = ABAP_TRUE ).

    loop at LT_GATE into LR_GATE.
      clear: LS_MS_BOARD_GRID, LR_PR_MS_PAPER.

***   Get Paper
      try.
          move LR_GATE->R_CLSPR ?to LR_PR_MS_PAPER.
          check LR_PR_MS_PAPER is bound.

          move LR_GATE->GET_POSID( ) to LS_MS_BOARD_GRID-POSID.
          move LR_GATE->F_GATKY      to LS_MS_BOARD_GRID-GATKY.
          move LR_GATE               to LS_MS_BOARD_GRID-R_GATE.

          LS_MS_BOARD_VIS = ME->GET_MS_PAPER_VIS( IR_PR_MS_PAPER = LR_PR_MS_PAPER ).
          move-corresponding LS_MS_BOARD_VIS to LS_MS_BOARD_GRID.

          if LS_MS_BOARD_VIS-SUBPA eq ABAP_TRUE.
            move ABAP_TRUE to ME->F_SUBPA_IS_ACTIV.
          endif.

***       Set Style Details
          ME->CHANGE_STYLE_PAPER( exporting IR_GATE = LS_MS_BOARD_GRID-R_GATE
                                            IS_DATA = LS_MS_BOARD_GRID
                                  changing  CT_STYL = LS_MS_BOARD_GRID-T_STYL ).

***       Call DVC Service - Modify Style
          ZCL_VCXI_XCDR_SERVICE=>MODIFY_STYLE( exporting IR_GATE = LR_GATE
                                               changing  CT_STYL = LS_MS_BOARD_GRID-T_STYL ).

***       Add Entry
          append initial line to <T_DATA> assigning <S_DATA>.
          move-corresponding LS_MS_BOARD_GRID to <S_DATA>.
          continue.

        catch CX_SY_MOVE_CAST_ERROR.
          continue.
      endtry.
    endloop.

***--------------------------------------------------------------------------------------
*** Get Gates of MS Starch
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_P1SS_PR_MS_STARCH=>C_IPRVT_P1_MS_STARCH
                                     IF_INUSE  = ABAP_TRUE
                                     IF_SORTED = ABAP_TRUE ).

    loop at LT_GATE into LR_GATE.
      clear: LS_MS_BOARD_GRID, LR_PR_MS_STARCH.

***   Get Starch
      try.
          move LR_GATE->R_CLSPR ?to LR_PR_MS_STARCH.
          check LR_PR_MS_STARCH is bound.

          move LR_GATE->GET_POSID( ) to LS_MS_BOARD_GRID-POSID.
          move LR_GATE->F_GATKY      to LS_MS_BOARD_GRID-GATKY.
          move LR_GATE               to LS_MS_BOARD_GRID-R_GATE.

          LS_MS_BOARD_VIS = ME->GET_MS_STARCH_VIS( IR_PR_MS_STARCH = LR_PR_MS_STARCH ).
          move-corresponding LS_MS_BOARD_VIS to LS_MS_BOARD_GRID.

***       Set Style Details
          ME->CHANGE_STYLE_STARCH( exporting IF_PURCH = LS_MS_BOARD_GRID-PURCH
                                   changing  CT_STYL  = LS_MS_BOARD_GRID-T_STYL ).

***       Call DVC Service - Modify Style
          ZCL_VCXI_XCDR_SERVICE=>MODIFY_STYLE( exporting IR_GATE = LR_GATE
                                               changing  CT_STYL = LS_MS_BOARD_GRID-T_STYL ).

***       Add Entry
          append initial line to <T_DATA> assigning <S_DATA>.
          move-corresponding LS_MS_BOARD_GRID to <S_DATA>.
          continue.

        catch CX_SY_MOVE_CAST_ERROR.
          continue.
      endtry.

    endloop.

***--------------------------------------------------------------------------------------
*** Get Gates of MS Wax
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_P1SS_PR_MS_WAX=>C_IPRVT_P1_MS_WAX
                                     IF_INUSE  = ABAP_TRUE
                                     IF_SORTED = ABAP_TRUE ).

    loop at LT_GATE into LR_GATE.
      clear: LS_MS_BOARD_GRID, LR_PR_MS_WAX.

***   Get Wax
      try.
          move LR_GATE->R_CLSPR ?to LR_PR_MS_WAX.
          check LR_PR_MS_WAX is bound.

          move LR_GATE->GET_POSID( ) to LS_MS_BOARD_GRID-POSID.
          move LR_GATE->F_GATKY      to LS_MS_BOARD_GRID-GATKY.
          move LR_GATE               to LS_MS_BOARD_GRID-R_GATE.

          LS_MS_BOARD_VIS = ME->GET_MS_WAX_VIS( IR_PR_MS_WAX = LR_PR_MS_WAX ).
          move-corresponding LS_MS_BOARD_VIS to LS_MS_BOARD_GRID.

***       Set Style Details
          ME->CHANGE_STYLE_WAX( exporting IF_PURCH = LS_MS_BOARD_GRID-PURCH
                                changing  CT_STYL  = LS_MS_BOARD_GRID-T_STYL ).

***       Call DVC Service - Modify Style
          ZCL_VCXI_XCDR_SERVICE=>MODIFY_STYLE( exporting IR_GATE = LR_GATE
                                               changing  CT_STYL = LS_MS_BOARD_GRID-T_STYL ).

***       Add Entry
          append initial line to <T_DATA> assigning <S_DATA>.
          move-corresponding LS_MS_BOARD_GRID to <S_DATA>.
          continue.

        catch CX_SY_MOVE_CAST_ERROR.
          continue.
      endtry.
    endloop.

  endmethod.
  method GET_MS_PAPER_VIS.

*** Material
    RS_MS_PAPER_VIS-MATNR       = IR_PR_MS_PAPER->GET_MATNR( ).
    RS_MS_PAPER_VIS-MATNR_DESCR = IR_PR_MS_PAPER->GET_MATNR_DESCR( ).

*** Procurement Option
    RS_MS_PAPER_VIS-PURCH = IR_PR_MS_PAPER->GET_PURCH( ).
    RS_MS_PAPER_VIS-FPTOS = IR_PR_MS_PAPER->GET_FPTOS( ).

*** Price
    RS_MS_PAPER_VIS-S_PRICE = IR_PR_MS_PAPER->GET_PRICE( ).
    RS_MS_PAPER_VIS-S_PRICP = IR_PR_MS_PAPER->GET_PRICP( ).

*** Vendor
    RS_MS_PAPER_VIS-LIFNR       = IR_PR_MS_PAPER->GET_LIFNR( ).
    RS_MS_PAPER_VIS-LIFNR_DESCR = IR_PR_MS_PAPER->GET_LIFNR_DESCR( ).

*** Board Paper ID
    RS_MS_PAPER_VIS-BPAID = IR_PR_MS_PAPER->GET_BPAID( ).

*** Substitute Paper
    RS_MS_PAPER_VIS-SUBPA = IR_PR_MS_PAPER->GET_SUBPA( ).

*** Base Weight
    RS_MS_PAPER_VIS-S_BAWGT = IR_PR_MS_PAPER->GET_BAWGT( ).
    RS_MS_PAPER_VIS-S_BAWGP = IR_PR_MS_PAPER->GET_BAWGP( ).

*** Caliper
    RS_MS_PAPER_VIS-S_CALIP = IR_PR_MS_PAPER->GET_CALIP( ).

***--------------------------------------------------------------------------------------
*** Preprint
    RS_MS_PAPER_VIS-FPRPR = IR_PR_MS_PAPER->GET_FPRPR( importing EF_SBSTR = RS_MS_PAPER_VIS-SBSTR ).

  endmethod.
method GET_MS_STARCH_VIS.

*** Material
  move IR_PR_MS_STARCH->GET_MATNR( )         to RS_MS_STARCH_VIS-MATNR.
  move IR_PR_MS_STARCH->GET_MATNR_DESCR( )   to RS_MS_STARCH_VIS-MATNR_DESCR.

*** Solid Content
  move IR_PR_MS_STARCH->GET_SLDCO( )         to RS_MS_STARCH_VIS-S_SLDCO .

endmethod.
method GET_MS_WAX_VIS.

*** Material
  move IR_PR_MS_WAX->GET_MATNR( )         to RS_MS_WAX_VIS-MATNR.
  move IR_PR_MS_WAX->GET_MATNR_DESCR( )   to RS_MS_WAX_VIS-MATNR_DESCR.

*** Solid Content
  move IR_PR_MS_WAX->GET_SLDCO( )         to RS_MS_WAX_VIS-S_SLDCO .

endmethod.
  method HANDLE_DATA_CHANGED_FINISHED.

    data: LS_MS_BOARD_VIS  type          ZVCXI_P1SS_S_MS_BOARD_VIS,
          LS_MS_BOARD_GRID type          ZVCXI_P1SS_S_MS_BOARD_GRID.
    data: LR_PR_MS_PAPER  type ref to   ZCL_VCXI_P1SS_PR_MS_PAPER,
          LR_PR_MS_STARCH type ref to   ZCL_VCXI_P1SS_PR_MS_STARCH,
          LR_PR_MS_WAX    type ref to   ZCL_VCXI_P1SS_PR_MS_WAX,
          LR_CX_CKX       type ref to   /VCXI/CX_CKX.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type          ANY.

    check E_MODIFIED eq ABAP_TRUE.

    try.
        SUPER->HANDLE_DATA_CHANGED_FINISHED( E_MODIFIED    = E_MODIFIED
                                             ET_GOOD_CELLS = ET_GOOD_CELLS ).

***     Dereferencing of Data Reference
        assign ME->R_DATA->* to <T_DATA>.

        loop at <T_DATA> assigning <S_DATA>.

***       Refresh References
          clear: LR_PR_MS_PAPER, LR_PR_MS_STARCH, LR_PR_MS_WAX.

          move-corresponding <S_DATA> to LS_MS_BOARD_GRID.
          check LS_MS_BOARD_GRID-R_GATE is bound.

          case type of LS_MS_BOARD_GRID-R_GATE->R_CLSPR.
            when type ZCL_VCXI_P1SS_PR_MS_PAPER into LR_PR_MS_PAPER.
***           Set Values to PR Class
              move-corresponding LS_MS_BOARD_GRID to LS_MS_BOARD_VIS.
              ME->SET_MS_PAPER_VIS( IR_PR_MS_PAPER  = LR_PR_MS_PAPER
                                    IS_MS_PAPER_VIS = LS_MS_BOARD_VIS ).

            when type ZCL_VCXI_P1SS_PR_MS_STARCH into LR_PR_MS_STARCH.
***           Set Starch Values to PR Class
              move-corresponding LS_MS_BOARD_GRID to LS_MS_BOARD_VIS.
              ME->SET_MS_STARCH_VIS( IR_PR_MS_STARCH  = LR_PR_MS_STARCH
                                     IS_MS_STARCH_VIS = LS_MS_BOARD_VIS ).

            when type ZCL_VCXI_P1SS_PR_MS_WAX into LR_PR_MS_WAX.
***           Set Wax Values to PR Class
              move-corresponding LS_MS_BOARD_GRID to LS_MS_BOARD_VIS.
              ME->SET_MS_WAX_VIS( IR_PR_MS_WAX  = LR_PR_MS_WAX
                                  IS_MS_WAX_VIS = LS_MS_BOARD_VIS ).

          endcase.
        endloop.

***     Request Refresh for ALV
        move ABAP_TRUE to ME->F_REFRESH_REQ.

        ME->R_GATE->RAISE_EVENTS( ).

      catch /VCXI/CX_CKX into LR_CX_CKX.
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

*** Request Refresh for ALV
    move ABAP_TRUE to ME->F_REFRESH_REQ.

  endmethod.
method HANDLE_DOUBLE_CLICK.

  data: LR_CX_CKX type ref to /VCXI/CX_CKX,
        LR_GATE   type ref to /VCXI/CL_VKSR_GATE.
  field-symbols: <T_DATA>  type standard table,
                 <S_DATA>  type ANY,
                 <F_FIELD> type ANY.

  try.

***   Dereferencing of Data Reference
      assign ME->R_DATA->* to <T_DATA>.

      loop at <T_DATA> assigning <S_DATA>.
        check SY-TABIX  eq E_ROW-INDEX.

***     Open Subscreen
        assign component C_FIELDNAME_RGATE of structure <S_DATA> to <F_FIELD>.
        check <F_FIELD> is assigned.
        move <F_FIELD> to LR_GATE.

***     Check Gate is bound and Provider has an own UI
        check LR_GATE is bound and
              LR_GATE->GET_CLSUI( ) is not initial.
***     Open UI
        ME->R_UIGATE->OPEN_SUBSCREEN( IR_GATE = LR_GATE ).

***     Expand and select in Tree
        ME->R_UIGATE->EXPAND_TREE_NODE( IF_GATKY = LR_GATE->F_GATKY ).
        ME->R_UIGATE->SELECT_TREE_NODE( IF_GATKY = LR_GATE->F_GATKY ).

      endloop.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

endmethod.
method HANDLE_HOTSPOT_CLICK.

  data:          LS_MS_BOARD_GRID     type          ZVCXI_P1SS_S_MS_BOARD_GRID.
  data:          LR_PR_MS_PAPER       type ref to   ZCL_VCXI_P1SS_PR_MS_PAPER.
  field-symbols: <T_DATA> type standard table,
                 <S_DATA> type          ANY.

  try.

***   Dereferencing of Data Reference
      assign ME->R_DATA->* to <T_DATA>.

      loop at <T_DATA> assigning <S_DATA>.
        check SY-TABIX  eq E_ROW_ID.

        try.
            move-corresponding <S_DATA> to LS_MS_BOARD_GRID.

            move LS_MS_BOARD_GRID-R_GATE->R_CLSPR ?to LR_PR_MS_PAPER.
            check LR_PR_MS_PAPER is bound.

***         Set Values to MS Paper PR Class
            case E_COLUMN_ID-FIELDNAME.

              when 'SUBPA'.
                case LS_MS_BOARD_GRID-SUBPA.
                  when ABAP_TRUE.  move ABAP_FALSE to LS_MS_BOARD_GRID-SUBPA.
                  when ABAP_FALSE. move ABAP_TRUE  to LS_MS_BOARD_GRID-SUBPA.
                endcase.
                LR_PR_MS_PAPER->SET_SUBPA( IF_SUBPA = LS_MS_BOARD_GRID-SUBPA ).

            endcase.

          catch CX_SY_MOVE_CAST_ERROR.
            clear: LR_PR_MS_PAPER.
        endtry.
      endloop.

      ME->R_GATE->RAISE_EVENTS( ).

    catch /VCXI/CX_CKX into data(LR_CX_CKX).
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

*** Request Refresh for ALV
  move ABAP_TRUE to ME->F_REFRESH_REQ.

endmethod.
  method SET_EVENT.

    SUPER->SET_EVENT( ).

*** Set Handler for Doubleclick
    set handler ME->HANDLE_DOUBLE_CLICK        for ME->R_GRID.

*** Set Hotspot
    set handler ME->HANDLE_HOTSPOT_CLICK       for ME->R_GRID.

  endmethod.
  method SET_MS_PAPER_VIS.

    data: LS_BAWGT        type ZVCXI_PCS_S_BAWGT,
          LS_BAWGP        type ZVCXI_PCS_S_BAWGP,
          LS_CALIP        type ZVCXI_P0SS_S_CALIP,
          LS_MS_PAPER_VIS type ZVCXI_P1SS_S_MS_BOARD_VIS.


***--------------------------------------------------------------------------------------
*** Get Current data to do Update only if Value changed.
    LS_MS_PAPER_VIS = ME->GET_MS_PAPER_VIS( IR_PR_MS_PAPER = IR_PR_MS_PAPER ).

***--------------------------------------------------------------------------------------
*** Material
    if IS_MS_PAPER_VIS-MATNR ne LS_MS_PAPER_VIS-MATNR.
      IR_PR_MS_PAPER->SET_MATNR( IF_MATNR = IS_MS_PAPER_VIS-MATNR
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Procurement Option
    if IS_MS_PAPER_VIS-PURCH ne LS_MS_PAPER_VIS-PURCH.
      IR_PR_MS_PAPER->SET_PURCH( IF_PURCH = IS_MS_PAPER_VIS-PURCH
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

    if IS_MS_PAPER_VIS-FPTOS ne LS_MS_PAPER_VIS-FPTOS.
      IR_PR_MS_PAPER->SET_FPTOS( IF_FPTOS = IS_MS_PAPER_VIS-FPTOS
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Price
    if IS_MS_PAPER_VIS-S_PRICE ne LS_MS_PAPER_VIS-S_PRICE.
      IR_PR_MS_PAPER->SET_PRICE( IS_PRICE = IS_MS_PAPER_VIS-S_PRICE
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

    if IS_MS_PAPER_VIS-S_PRICP ne LS_MS_PAPER_VIS-S_PRICP.
      IR_PR_MS_PAPER->SET_PRICP( IS_PRICP = IS_MS_PAPER_VIS-S_PRICP
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Vendor
    if IS_MS_PAPER_VIS-LIFNR ne LS_MS_PAPER_VIS-LIFNR.
      IR_PR_MS_PAPER->SET_LIFNR( IF_LIFNR = IS_MS_PAPER_VIS-LIFNR
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Base Weight
    if IS_MS_PAPER_VIS-BAWGT ne LS_MS_PAPER_VIS-BAWGT.
      IR_PR_MS_PAPER->SET_BAWGT( IS_BAWGT = IS_MS_PAPER_VIS-S_BAWGT
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

    if IS_MS_PAPER_VIS-BAWGP ne LS_MS_PAPER_VIS-BAWGP.
      IR_PR_MS_PAPER->SET_BAWGP( IS_BAWGP = IS_MS_PAPER_VIS-S_BAWGP
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Caliper
    if IS_MS_PAPER_VIS-CALIP ne LS_MS_PAPER_VIS-CALIP.
      IR_PR_MS_PAPER->SET_CALIP( IS_CALIP = IS_MS_PAPER_VIS-S_CALIP
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
method SET_MS_STARCH_VIS.

  data: LS_MS_STARCH_VIS  type ZVCXI_P1SS_S_MS_BOARD_VIS.


*** Get Current data to do Update only if Value changed.
  LS_MS_STARCH_VIS = ME->GET_MS_STARCH_VIS( IR_PR_MS_STARCH = IR_PR_MS_STARCH ).

*** Procurement Option
  if IS_MS_STARCH_VIS-PURCH ne LS_MS_STARCH_VIS-PURCH.
    IR_PR_MS_STARCH->SET_PURCH( IF_PURCH = IS_MS_STARCH_VIS-PURCH
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

  if IS_MS_STARCH_VIS-FPTOS ne LS_MS_STARCH_VIS-FPTOS.
    IR_PR_MS_STARCH->SET_FPTOS( IF_FPTOS = IS_MS_STARCH_VIS-FPTOS
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Price
  if IS_MS_STARCH_VIS-PRICE ne LS_MS_STARCH_VIS-PRICE.
    IR_PR_MS_STARCH->SET_PRICE( IS_PRICE = IS_MS_STARCH_VIS-S_PRICE
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.
  if IS_MS_STARCH_VIS-PRICP ne LS_MS_STARCH_VIS-PRICP.
    IR_PR_MS_STARCH->SET_PRICP( IS_PRICP = IS_MS_STARCH_VIS-S_PRICP
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Solid Content
  if IS_MS_STARCH_VIS-SLDCO ne LS_MS_STARCH_VIS-SLDCO.
    IR_PR_MS_STARCH->SET_SLDCO( IS_SLDCO = IS_MS_STARCH_VIS-S_SLDCO
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

endmethod.
method SET_MS_WAX_VIS.

  data: LS_MS_WAX_VIS  type ZVCXI_P1SS_S_MS_BOARD_VIS.


*** Get Current data to do Update only if Value changed.
  LS_MS_WAX_VIS = ME->GET_MS_WAX_VIS( IR_PR_MS_WAX = IR_PR_MS_WAX ).

*** Procurement Option
  if IS_MS_WAX_VIS-PURCH ne LS_MS_WAX_VIS-PURCH.
    IR_PR_MS_WAX->SET_PURCH( IF_PURCH = IS_MS_WAX_VIS-PURCH
                             IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

  if IS_MS_WAX_VIS-FPTOS ne LS_MS_WAX_VIS-FPTOS.
    IR_PR_MS_WAX->SET_FPTOS( IF_FPTOS = IS_MS_WAX_VIS-FPTOS
                             IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Price
  if IS_MS_WAX_VIS-PRICE ne LS_MS_WAX_VIS-PRICE.
    IR_PR_MS_WAX->SET_PRICE( IS_PRICE = IS_MS_WAX_VIS-S_PRICE
                             IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.
  if IS_MS_WAX_VIS-PRICP ne LS_MS_WAX_VIS-PRICP.
    IR_PR_MS_WAX->SET_PRICP( IS_PRICP = IS_MS_WAX_VIS-S_PRICP
                             IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Solid Content
  if IS_MS_WAX_VIS-SLDCO ne LS_MS_WAX_VIS-SLDCO.
    IR_PR_MS_WAX->SET_SLDCO( IS_SLDCO = IS_MS_WAX_VIS-S_SLDCO
                             IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

endmethod.
