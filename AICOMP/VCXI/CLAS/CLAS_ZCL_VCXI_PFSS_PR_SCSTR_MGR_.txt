
class ZCL_VCXI_PFSS_PR_SCSTR_MGR definition
  public
  inheriting from ZCL_VCXI_P0SS_PR_SCSTR_MGR
  create public .

public section.

  constants C_SCSOP_RCUT type ZVCXI_P0S_SCSOP value 'RCUT'. "#EC NOTEXT

  methods GET_SCSOP
    redefinition .
  methods GET_ORIEN
    redefinition .
protected section.

  types:
    YT_PTYPE type standard table of ZVCXI_P0S_PTYPE with default key .

  methods GET_PRVDR_MS_RCUT
    importing
      !IR_GATE_SPEC type ref to /VCXI/CL_VKSR_GATE
    returning
      value(RR_GATE_MS_RCUT) type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods GET_PRVDR_UCB
    importing
      !IR_GATE_SPEC type ref to /VCXI/CL_VKSR_GATE
    returning
      value(RR_GATE_SPEC_UCB) type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods GET_PRVDR_SHEET
    importing
      !IR_GATE_SPEC type ref to /VCXI/CL_VKSR_GATE
    returning
      value(RR_GATE_SHEET) type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods GET_SCSOP_RCUT
    changing
      value(CT_SCSOP) type ZVCXI_P0SS_TT_SCSOP
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_PFSS_PR_SCSTR_MGR definition
class ZCL_VCXI_PFSS_PR_SCSTR_MGR implementation.
method GET_ORIEN.

  data:  LR_GATE_SHEET type ref to /VCXI/CL_VKSR_GATE,
         LR_PR_SHEET   type ref to ZCL_VCXI_PFSS_PR_SHEET.

  clear EF_ORIEN.

*** Get Sheet Provider
  LR_GATE_SHEET = ME->GET_PRVDR_SHEET( IR_GATE_SPEC = ME->R_GATE_SPEC ).
  check LR_GATE_SHEET is bound.

  try.
      move LR_GATE_SHEET->R_CLSPR ?to LR_PR_SHEET.
    catch CX_SY_MOVE_CAST_ERROR.
      clear LR_PR_SHEET.
  endtry.
  check LR_PR_SHEET is bound.

*** Get Grain Orientation
  move LR_PR_SHEET->GET_GRAIO( ) to EF_ORIEN.

endmethod.
method GET_PRVDR_MS_RCUT.

  data:  LT_GATE_MS_RCUT   type        /VCXI/VKSR_TT_GATE.
  data:  LR_GATE_MS_RCUT   type ref to /VCXI/CL_VKSR_GATE.

***------------------------------------------------------------------------------------------------
*** Find Provider for Material Specification Reel Cutting
  LT_GATE_MS_RCUT = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_PFSS_PR_MS_RCUT=>C_IPRVT_PF_MS_RCUT
                                           IF_INUSE  = ABAP_TRUE
                                           IF_SORTED = ABAP_TRUE ).

  loop at LT_GATE_MS_RCUT into LR_GATE_MS_RCUT.
*** Check is inline with Specification
    check LR_GATE_MS_RCUT->IS_IN_LINE( IR_GATE = IR_GATE_SPEC ) eq ABAP_TRUE.
    move LR_GATE_MS_RCUT to RR_GATE_MS_RCUT.
    exit.
  endloop.

endmethod.
method GET_PRVDR_SHEET.

  data:  LT_GATE_SHEET type        /VCXI/VKSR_TT_GATE.
  data:  LR_GATE_SHEET type ref to /VCXI/CL_VKSR_GATE.

***------------------------------------------------------------------------------------------------
*** Get all Sheet Provider
  LT_GATE_SHEET = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_PFSS_PR_SHEET=>C_IPRVT_PF_SHEET
                                         IF_INUSE  = ABAP_TRUE
                                         IF_SORTED = ABAP_TRUE ).

  loop at LT_GATE_SHEET into LR_GATE_SHEET.
*** Check is child of Specification
    check LR_GATE_SHEET->GET_PARENT( ) eq IR_GATE_SPEC.
    move LR_GATE_SHEET to RR_GATE_SHEET.
    exit.
  endloop.

endmethod.
method GET_PRVDR_UCB.

  data:  LT_GATE_SPEC_UCB   type        /VCXI/VKSR_TT_GATE.
  data:  LR_GATE_SPEC_UCB   type ref to /VCXI/CL_VKSR_GATE.

***------------------------------------------------------------------------------------------------
*** Find Provider for Material Specification Reel Cutting
  LT_GATE_SPEC_UCB = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_PFSS_PR_SPEC_UCB=>C_IPRVT_PF_SPEC_UCB
                                            IF_INUSE  = ABAP_TRUE
                                            IF_SORTED = ABAP_TRUE ).

  loop at LT_GATE_SPEC_UCB into LR_GATE_SPEC_UCB.
*** Check is inline with Specification
    check LR_GATE_SPEC_UCB->IS_IN_LINE( IR_GATE = IR_GATE_SPEC ) eq ABAP_TRUE.
    move LR_GATE_SPEC_UCB to RR_GATE_SPEC_UCB.
    exit.
  endloop.

endmethod.
method GET_SCSOP.

***------------------------------------------------------------------------------------------------
*** Get Score String Operation NONE
  ME->GET_SCSOP_NONE( changing CT_SCSOP = RT_SCSOP ).


***------------------------------------------------------------------------------------------------
*** Get Score String Operation Reel Cutting
  ME->GET_SCSOP_RCUT( changing CT_SCSOP = RT_SCSOP ).

***------------------------------------------------------------------------------------------------
*** Get Score String Operation Sheet Procurement
  ME->GET_SCSOP_SHTPR( changing CT_SCSOP = RT_SCSOP ).

***------------------------------------------------------------------------------------------------
*** Get Score String Operation Slit/Score Manually choosen one! for Length scores
  ME->GET_SCSOP_SLIT_SCORE_MAN( exporting IF_CSCOR = ZCL_VCXI_P0SS_CUST_PRSTY=>C_CSCOR_LENGTH
                                changing  CT_SCSOP = RT_SCSOP ).

***------------------------------------------------------------------------------------------------
*** Get Score String Operation Chopping
  ME->GET_SCSOP_CHOP( changing CT_SCSOP = RT_SCSOP ).

***------------------------------------------------------------------------------------------------
*** Get Score String Operation Slit/Score
  ME->GET_SCSOP_SLIT_SCORE( changing CT_SCSOP = RT_SCSOP ).

***------------------------------------------------------------------------------------------------
*** Get Score String Operation Slit/Score Manually choosen one! for cross scores
  ME->GET_SCSOP_SLIT_SCORE_MAN( exporting IF_CSCOR = ZCL_VCXI_P0SS_CUST_PRSTY=>C_CSCOR_CROSS
                                changing  CT_SCSOP = RT_SCSOP ).

***------------------------------------------------------------------------------------------------
*** Get Score String Operation Cutting
  ME->GET_SCSOP_CUTTING( changing CT_SCSOP = RT_SCSOP ).

***------------------------------------------------------------------------------------------------
*** Get Score String Operation Attachment (manually)
  ME->GET_SCSOP_ATTACH( exporting IF_CSCOR = ZCL_VCXI_P0SS_CUST_PRSTY=>C_CSCOR_LENGTH
                        changing  CT_SCSOP = RT_SCSOP ).

***------------------------------------------------------------------------------------------------
*** Get Score String Operation Attachment (manually)
  ME->GET_SCSOP_ATTACH( exporting IF_CSCOR = ZCL_VCXI_P0SS_CUST_PRSTY=>C_CSCOR_CROSS
                        changing  CT_SCSOP = RT_SCSOP ).

endmethod.
method GET_SCSOP_RCUT.

  data:          LT_PRVDR_RCUT     type        /VCXI/VKSR_TT_GATE.
  data:          LR_GATE_MS_RCUT   type ref to /VCXI/CL_VKSR_GATE,
                 LR_GATE_UCB       type ref to /VCXI/CL_VKSR_GATE.
  field-symbols: <S_SCSOP>         type        ZVCXI_P0SS_S_SCSOP.

***------------------------------------------------------------------------------------------------
*** Check Provider for Specification
  check ME->R_GATE_SPEC is bound.

*** Find Provider for MS_RCUT
  LR_GATE_MS_RCUT = ME->GET_PRVDR_MS_RCUT( IR_GATE_SPEC = ME->R_GATE_SPEC ).
  check LR_GATE_MS_RCUT is bound.
  append LR_GATE_MS_RCUT to LT_PRVDR_RCUT.

*** Find Provider for Unconverted Board
  LR_GATE_UCB = ME->GET_PRVDR_UCB( IR_GATE_SPEC = ME->R_GATE_SPEC ).
  check LR_GATE_UCB is bound.
  append LR_GATE_UCB to LT_PRVDR_RCUT.

  append ME->R_GATE to LT_PRVDR_RCUT.

***------------------------------------------------------------------------------------------------
*** Add Operation for Length
  append initial line to CT_SCSOP assigning <S_SCSOP>.

  <S_SCSOP>-SCSOP = C_SCSOP_RCUT.
  <S_SCSOP>-SHTDI = 'A'.
  <S_SCSOP>-CSCOR = ZCL_VCXI_P0SS_CUST_PRSTY=>C_CSCOR_LENGTH.
  if ME->GET_SOPLE( ) eq C_SCSOP_RCUT.
    <S_SCSOP>-ACTIV = ABAP_TRUE.
  endif.
  <S_SCSOP>-T_PRVDR = LT_PRVDR_RCUT.

***------------------------------------------------------------------------------------------------
*** Add Operation for Cross
  append initial line to CT_SCSOP assigning <S_SCSOP>.

  <S_SCSOP>-SCSOP = C_SCSOP_RCUT.
  <S_SCSOP>-SHTDI = 'B'.
  <S_SCSOP>-CSCOR = ZCL_VCXI_P0SS_CUST_PRSTY=>C_CSCOR_CROSS.
  if ME->GET_SOPLE( ) eq C_SCSOP_RCUT.
    <S_SCSOP>-ACTIV = ABAP_TRUE.
  endif.
  <S_SCSOP>-T_PRVDR = LT_PRVDR_RCUT.

endmethod.                    "GET_SCSOP_CORR
