
class ZCL_VCXI_PCSS_BL_SCSOP_CORR_CR definition
  public
  inheriting from ZCL_VCXI_P0SS_BL_SCSOP
  create public .

public section.

  constants C_SCSOP_CORR_CR type ZVCXI_P0S_SCSOP value 'CORR_CR'. "#EC NOTEXT

  methods CONSTRUCTOR
    importing
      !IF_SHTDI type ZVCXI_P0SS_SHTDI
      !IF_CSCOR type ZVCXI_P0SS_CSCOR
    raising
      /VCXI/CX_CKX .
protected section.

  methods GET_SPEC_CR_PRVDR
    importing
      !IT_PRVDR type /VCXI/VKSR_TT_GATE
    returning
      value(RR_PR_SPEC_CR) type ref to ZCL_VCXI_PCSS_PR_SPEC_CR
    raising
      /VCXI/CX_CKX .

  methods CALC_SCSTR
    redefinition .
  methods GET_DIVDI
    redefinition .
  methods READ_SHT_DIM
    redefinition .
private section.
endclass. "ZCL_VCXI_PCSS_BL_SCSOP_CORR_CR definition
class ZCL_VCXI_PCSS_BL_SCSOP_CORR_CR implementation.
method CALC_SCSTR.

  data:          LS_SCSTR_CALC       type        ZVCXI_P0SS_S_SCSTR_CALC.
  data:          LT_SCSTR_CALC       type        ZVCXI_P0SS_TT_SCSTR_CALC,
                 LT_SCSTR_CALC_TAPE  type        ZVCXI_P0SS_TT_SCSTR_CALC.

***------------------------------------------------------------------------------------------------
*** 1 Get Values of Scores and Tapes
  LT_SCSTR_CALC      = ME->GET_SCORES_OF_SCSTR( IT_PRVDR = IT_PRVDR ).
  LT_SCSTR_CALC_TAPE = ME->GET_TAPES_OF_SCSTR( IT_PRVDR = IT_PRVDR ).
  append lines of LT_SCSTR_CALC_TAPE to LT_SCSTR_CALC.

***------------------------------------------------------------------------------------------------
*** If we do not handle Scores -> just take the Sheet Width
*** as absolute value
  if  ME->F_CSCOR is initial.
    clear: LS_SCSTR_CALC, LT_SCSTR_CALC.
*** Save Values
    move ME->S_SHTWI-SHTWI                          to LS_SCSTR_CALC-SCORP.
    move ME->S_SHTWI-SHTWI_UOM                      to LS_SCSTR_CALC-SCORP_UOM.

    append          LS_SCSTR_CALC      to LT_SCSTR_CALC.
    append lines of LT_SCSTR_CALC_TAPE to LT_SCSTR_CALC.
  endif.

***------------------------------------------------------------------------------------------------
*** 2 Sort Values
  sort LT_SCSTR_CALC by SCORP.

***------------------------------------------------------------------------------------------------
*** 3 Define the right Score String Values ( SCSTR_VALUE ) and return the Hole string
  RF_SCSTR = ME->BUILD_SCSTR( IF_SSTRC      = IR_PR_SCSTR_MGR->F_SSTRC
                              IT_SCSTR_CALC = LT_SCSTR_CALC ).

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IF_SHTDI = IF_SHTDI
                      IF_CSCOR = IF_CSCOR ).

*** Set Score Operation
  move C_SCSOP_CORR_CR to ME->F_SCSOP.

endmethod.
method GET_DIVDI.

*** Nothing to do!

endmethod.
method GET_SPEC_CR_PRVDR.

  data: LR_GATE             type ref to /VCXI/CL_VKSR_GATE.

***------------------------------------------------------------------------------------------------
*** Read the right Sheet Dimensions
*** Get Sheet Reference
  loop at IT_PRVDR into LR_GATE.
    try.
        move LR_GATE->R_CLSPR ?to RR_PR_SPEC_CR.
        if RR_PR_SPEC_CR is bound.
          exit.
        endif.
      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.
  endloop.

endmethod.
method READ_SHT_DIM.

  data: LS_ROWID       type        ZVCXI_PCS_S_ROWID.
  data: LR_PR_SPEC_CR  type ref to ZCL_VCXI_PCSS_PR_SPEC_CR.

***------------------------------------------------------------------------------------------------
*** Get Role Dimension
  LR_PR_SPEC_CR = ME->GET_SPEC_CR_PRVDR( IT_PRVDR = IT_PRVDR ).
  LS_ROWID = LR_PR_SPEC_CR->GET_ROWID( ).

  move LS_ROWID-ROWID     to ES_SHTWI-SHTWI.
  move LS_ROWID-ROWID_UOM to ES_SHTWI-SHTWI_UOM.

endmethod.
