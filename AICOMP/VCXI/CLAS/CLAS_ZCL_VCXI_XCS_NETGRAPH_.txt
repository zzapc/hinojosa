
class ZCL_VCXI_XCS_NETGRAPH definition
  public
  create public .

public section.

  data F_REFRESH_REQ type ABAP_BOOL .

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_CONTAINER_NAME type SCRFNAME optional
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE optional
      !IF_APPL_EVENTS type ABAP_BOOL default ABAP_TRUE
    raising
      /VCXI/CX_CKX .
  methods DO_FREE
    raising
      /VCXI/CX_CKX .
  methods REFRESH_NETGRAPH
    raising
      /VCXI/CX_CKX .
protected section.

  data R_CONTAINER type ref to CL_GUI_CUSTOM_CONTAINER .
  data R_GATE type ref to /VCXI/CL_VKSR_GATE .
  data R_HTML_VIEWER type ref to CL_GUI_HTML_VIEWER .
  data R_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE .
  data F_CONTAINER_NAME type SCRFNAME .
  data F_APPL_EVENTS type ABAP_BOOL .
  data F_GRAPH_SETTINGS type STRING .

  methods DISPLAY_DATA
    importing
      !IF_JSON type STRING
    raising
      /VCXI/CX_CKX .
  methods GET_DATA
    returning
      value(RF_JSON) type STRING
    raising
      /VCXI/CX_CKX .
  methods INIT_CONTAINER
    raising
      /VCXI/CX_CKX .
  methods INIT_HTML_VIEWER
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_XCS_NETGRAPH definition
class ZCL_VCXI_XCS_NETGRAPH implementation.
  method CONSTRUCTOR.

    move IR_GATE           to ME->R_GATE.
    move IR_UIGATE         to ME->R_UIGATE.
    move IF_CONTAINER_NAME to ME->F_CONTAINER_NAME.
    move IF_APPL_EVENTS    to ME->F_APPL_EVENTS.

  endmethod.
  method DISPLAY_DATA.

    data: LF_URL  type C length 5000.
    data: LT_JSON           type W3HTMLTAB,
          LT_GRAPH_SETTINGS type W3HTMLTAB,
          LT_MERGE_TABLE    type SWWW_T_MERGE_TABLE.

    check ME->R_HTML_VIEWER is bound.

    call function 'CONVERT_STRING_TO_TABLE'
      exporting
        I_STRING         = IF_JSON
        I_TABLINE_LENGTH = 255
      tables
        ET_TABLE         = LT_JSON.

    call function 'CONVERT_STRING_TO_TABLE'
      exporting
        I_STRING         = ME->F_GRAPH_SETTINGS
        I_TABLINE_LENGTH = 255
      tables
        ET_TABLE         = LT_GRAPH_SETTINGS.

    LT_MERGE_TABLE = value #( ( NAME = '// <! merge json!>'   COMMAND = 'A' HTML = LT_JSON )
                              ( NAME = '// <! merge graph !>' COMMAND = 'A' HTML = LT_GRAPH_SETTINGS ) ).

*** Load Web Object Repository Document
    ME->R_HTML_VIEWER->LOAD_HTML_DOCUMENT( exporting  DOCUMENT_ID            = 'ZVCXI_XCS_NETGRAPH'   "maintained in SMW0
                                           importing  ASSIGNED_URL           = LF_URL
                                           changing   MERGE_TABLE            = LT_MERGE_TABLE
                                           exceptions DOCUMENT_NOT_FOUND     = 1
                                                      DP_ERROR_GENERAL       = 2
                                                      DP_INVALID_PARAMETER   = 3
                                                      HTML_SYNTAX_NOTCORRECT = 4 ).


    if SY-SUBRC <> 0.
      /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
    endif.

*** Display URL
    ME->R_HTML_VIEWER->SHOW_URL( URL = LF_URL ).

    ME->R_HTML_VIEWER->DO_REFRESH( ).
    CL_GUI_CFW=>DISPATCH( ).

  endmethod.
  method DO_FREE.

***--------------------------------------------------------------------------------------
*** Free HTML Viewer
    if ME->R_HTML_VIEWER is bound.
      ME->R_HTML_VIEWER->FREE( exceptions
                                CNTL_ERROR        = 1
                                CNTL_SYSTEM_ERROR = 2 ).

      if SY-SUBRC ne 0.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
      endif.
      clear ME->R_HTML_VIEWER.
    endif.

***--------------------------------------------------------------------------------------
*** Free Container
    if ME->R_CONTAINER is bound.
      ME->R_CONTAINER->FREE( exceptions
                              CNTL_ERROR        = 1
                              CNTL_SYSTEM_ERROR = 2
                              others            = 3 ).
      if SY-SUBRC ne 0.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
      endif.
      clear ME->R_CONTAINER.
    endif.

  endmethod.
  method GET_DATA.

    data: LR_NETGRAPH_JSON type ref to ZCL_VCXI_XCS_NETGRAPH_JSON.

*** Make sure instance can provide data
    check ME->R_GATE->R_CLSPR is instance of ZIF_VCXI_XCS_NGREP.

    LR_NETGRAPH_JSON = new ZCL_VCXI_XCS_NETGRAPH_JSON( ).

*** Delegate Filling of JSON by Gate
    cast ZIF_VCXI_XCS_NGREP( ME->R_GATE->R_CLSPR )->FILL( changing CR_NETGRAPH_JSON = LR_NETGRAPH_JSON ).

*** Return JSON Data
    RF_JSON = LR_NETGRAPH_JSON->GET_JSON( ).

  endmethod.
  method INIT_CONTAINER.

    check ME->R_CONTAINER is not bound and
          ME->F_CONTAINER_NAME is not initial.

*** Create Custom Container
    create object ME->R_CONTAINER
      exporting
        CONTAINER_NAME              = ME->F_CONTAINER_NAME
      exceptions
        CNTL_ERROR                  = 1
        CNTL_SYSTEM_ERROR           = 2
        CREATE_ERROR                = 3
        LIFETIME_ERROR              = 4
        LIFETIME_DYNPRO_DYNPRO_LINK = 5
        others                      = 6.
    if SY-SUBRC ne 0.
      /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
    endif.

  endmethod.
  method INIT_HTML_VIEWER.

    if ME->R_HTML_VIEWER is not bound.
      create object ME->R_HTML_VIEWER
        exporting
          PARENT             = cond #( when ME->R_CONTAINER is bound then ME->R_CONTAINER
                                                                     else CL_GUI_CONTAINER=>DEFAULT_SCREEN )
        exceptions
          CNTL_ERROR         = 1
          CNTL_INSTALL_ERROR = 2
          DP_INSTALL_ERROR   = 3
          DP_ERROR           = 4
          others             = 5.
      if SY-SUBRC <> 0.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
      endif.
    endif.

  endmethod.
  method REFRESH_NETGRAPH.

*** Do only if we need to Refresh
    check ME->F_REFRESH_REQ eq ABAP_TRUE.
    move ABAP_FALSE to ME->F_REFRESH_REQ.

*** Refresh is not working thereby we need to free the old HTML Viewer
    if ME->R_HTML_VIEWER is bound.
      ME->R_HTML_VIEWER->FREE( exceptions
                                CNTL_ERROR        = 1
                                CNTL_SYSTEM_ERROR = 2 ).

      if SY-SUBRC ne 0.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
      endif.
      clear ME->R_HTML_VIEWER.
    endif.

*** Init Container and HTML Viewer
    ME->INIT_CONTAINER( ).
    ME->INIT_HTML_VIEWER( ).

*** Get Data for Network Graphic
    data(LF_JSON) = ME->GET_DATA( ).

*** Display Data
    ME->DISPLAY_DATA( LF_JSON ).

  endmethod.
