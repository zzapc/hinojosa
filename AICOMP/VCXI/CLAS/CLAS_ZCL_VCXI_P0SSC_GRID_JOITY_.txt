
class ZCL_VCXI_P0SSC_GRID_JOITY definition
  public
  inheriting from ZCL_VCXI_P0SSC_GRID
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IF_TABNAME type TABNAME
      !IF_CONTAINER_NAME type SCRFNAME
      !IR_CUSTOMIZING type ref to OBJECT
    raising
      /VCXI/CX_CKX .
protected section.

  types:
    YT_JOITY_T type standard table of ZVCXI_P0S_TC340T with default key .

  constants C_FUNC_ADD type UI_FUNC value 'ADD'. "#EC NOTEXT
  constants C_FUNC_DEL type UI_FUNC value 'DELETE'. "#EC NOTEXT
  data T_JOITY_T type YT_JOITY_T .
  data R_PRSTY type ref to ZCL_VCXI_P0SSC_PRSTY .

  methods ADD_JOITY
    raising
      /VCXI/CX_CKX .
  methods CHANGE_STYLE_JOITY
    changing
      !CT_STYL type LVC_T_STYL
    raising
      /VCXI/CX_CKX .
  methods CHECK_SEL_DELETE
    returning
      value(RF_OKAY) type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
  methods DELETE_JOITY
    importing
      !IT_PRSTY_JOITY type ZVCXI_P0SSC_TT_PRSTY_JOITY
    raising
      /VCXI/CX_CKX .
  methods HANDLE_TOOLBAR
    for event TOOLBAR of CL_GUI_ALV_GRID
    importing
      !E_OBJECT
      !E_INTERACTIVE .
  methods HANDLE_USER_COMMAND
    for event USER_COMMAND of CL_GUI_ALV_GRID
    importing
      !E_UCOMM .

  methods GET_FIELDCAT
    redefinition .
  methods GET_GRID_DATA
    redefinition .
  methods GET_LAYOUT
    redefinition .
  methods SET_EVENT
    redefinition .
private section.
endclass. "ZCL_VCXI_P0SSC_GRID_JOITY definition
class ZCL_VCXI_P0SSC_GRID_JOITY implementation.
method ADD_JOITY.

  data:  LS_RETURN       type                   DDSHRETVAL,
         LS_PRSTY_JOITY  type                   ZVCXI_P0SSC_S_PRSTY_JOITY,
         LS_JOITY        type                   ZVCXI_P0SSC_S_PRSTY_JOITY_SHLP,
         LS_JOITY_T      type                   ZVCXI_P0S_TC340T.
  data:  LT_RETURN       type table of          DDSHRETVAL,
         LT_JOITY        type standard table of ZVCXI_P0SSC_S_PRSTY_JOITY_SHLP.


  loop at ME->T_JOITY_T into LS_JOITY_T.
    move-corresponding LS_JOITY_T to LS_JOITY.
    append LS_JOITY to LT_JOITY.
  endloop.

  call function 'F4IF_INT_TABLE_VALUE_REQUEST'
    exporting
      DDIC_STRUCTURE  = 'ZVCXI_P0SSC_S_PRSTY_JOITY_SHLP'
      RETFIELD        = 'JOITY'
      VALUE_ORG       = 'S'
      MULTIPLE_CHOICE = 'X'
    tables
      VALUE_TAB       = LT_JOITY
      RETURN_TAB      = LT_RETURN
    exceptions
      PARAMETER_ERROR = 1
      NO_VALUES_FOUND = 2
      others          = 3.
  if SY-SUBRC ne 0.
    /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
  endif.

*** Process Result
  loop at LT_RETURN into LS_RETURN.

    move-corresponding ME->R_PRSTY->S_PRSTY to LS_PRSTY_JOITY.
    move               LS_RETURN-FIELDVAL   to LS_PRSTY_JOITY-JOITY.

*** Add Product Type
    ME->R_PRSTY->ADD_JOITY( IS_PRSTY_JOITY = LS_PRSTY_JOITY ).

  endloop.


endmethod.
method CHANGE_STYLE_JOITY.

*** Set Product Type = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'JOITY'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Product Type Description = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'JOITY_DESCR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).
endmethod.
method CHECK_SEL_DELETE.

  data: LT_CELL type LVC_T_CELL,
        LT_ROW  type LVC_T_ROW.

  ME->R_GRID->GET_SELECTED_CELLS( importing ET_CELL = LT_CELL ).
  ME->R_GRID->GET_SELECTED_ROWS( importing ET_INDEX_ROWS = LT_ROW ).

*** Delete is only available if Cells are selected
  if lines( LT_CELL ) gt 0 or
     lines( LT_ROW )  gt 0.
    move ABAP_TRUE to RF_OKAY.
  else.
    move ABAP_FALSE to RF_OKAY.
  endif.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IF_TABNAME        = IF_TABNAME
                      IF_CONTAINER_NAME = IF_CONTAINER_NAME
                      IR_CUSTOMIZING    = IR_CUSTOMIZING ).

*** Try to Assign Customizing Reference
  try.
      move IR_CUSTOMIZING ?to R_PRSTY.
    catch CX_SY_MOVE_CAST_ERROR.
*** Product Style Customizing Reference can't be assigned.
      /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE(
          IF_MSGTY = 'E'
          IF_MSGID = 'ZVCXI_P0SSC'
          IF_MSGNO = '101' ).
  endtry.

  move 'Joint Type'(001) to ME->F_DESCR.

*** Get Joint Type Descriptions
  select * from ZVCXI_P0S_TC340T
  into corresponding fields of table ME->T_JOITY_T
  where LANGU = SY-LANGU.

endmethod.
method DELETE_JOITY.

  data:  LS_PRSTY_JOITY  type          ZVCXI_P0SSC_S_PRSTY_JOITY.

*** Process Result
  loop at IT_PRSTY_JOITY into LS_PRSTY_JOITY.

*** Delete Product Type
    ME->R_PRSTY->DELETE_JOITY( IS_PRSTY_JOITY = LS_PRSTY_JOITY ).

  endloop.

endmethod.
method GET_FIELDCAT.

  field-symbols: <S_FCAT>           type        LVC_S_FCAT.

***----------------------------------------------------------------------------
*** Get Standard Field Catalog
  RT_FCAT = SUPER->GET_FIELDCAT( ).

***----------------------------------------------------------------------------
*** Adjust Field Catalog
  loop at RT_FCAT assigning <S_FCAT>.
    case <S_FCAT>-FIELDNAME.
      when 'TPRST' or 'PRSTY' or 'T_STYL'.
        move ABAP_TRUE    to <S_FCAT>-TECH.

    endcase.
  endloop.

endmethod.
method GET_GRID_DATA.

  data:          LS_PRSTY_JOITY       type          ZVCXI_P0SSC_S_PRSTY_JOITY,
                 LS_PRSTY_JOITY_GRID  type          ZVCXI_P0SSC_S_PRSTY_JOITY_GRID,
                 LS_JOITY_T           type          ZVCXI_P0S_TC340T.
  field-symbols: <T_DATA>             type standard table,
                 <S_DATA>             type          any.

*** Dereferencing of Data Reference
  assign IR_DATA->* to <T_DATA>.

***--------------------------------------------------------------------------------------------
*** Get Product Style - Joint Type Entries
  loop at ME->R_PRSTY->T_PRSTY_JOITY into LS_PRSTY_JOITY.
    clear: LS_PRSTY_JOITY_GRID.

    move-corresponding LS_PRSTY_JOITY to LS_PRSTY_JOITY_GRID.

***--------------------------------------------------------------------------------------------
*** Set Description
    read table ME->T_JOITY_T
      into LS_JOITY_T
      with key JOITY = LS_PRSTY_JOITY-JOITY.

    if SY-SUBRC = 0.
      move LS_JOITY_T-DESCR to LS_PRSTY_JOITY_GRID-JOITY_DESCR.
    endif.

***--------------------------------------------------------------------------------------------
*** Set Style Details
    ME->CHANGE_STYLE_JOITY( changing CT_STYL = LS_PRSTY_JOITY_GRID-T_STYL ).

***--------------------------------------------------------------------------------------------
*** Add Entry
    append initial line to <T_DATA> assigning <S_DATA>.
    move-corresponding LS_PRSTY_JOITY_GRID to <S_DATA>.

  endloop.

*** Sort data by Joint Type before output
  sort <T_DATA> by ('JOITY').

endmethod.
method GET_LAYOUT.

  RS_LAYOUT = SUPER->GET_LAYOUT( ).

*** Set Toolbar to Activ
  move ABAP_FALSE to RS_LAYOUT-NO_TOOLBAR.
  move 'A'        to RS_LAYOUT-SEL_MODE.

endmethod.
method HANDLE_TOOLBAR.

  data:  LS_TOOLBAR      type        STB_BUTTON.
  data:  LF_DISABLED     type        ABAP_BOOL.
  data:  LR_CX_CKX       type ref to /VCXI/CX_CKX.

  try.

      if ME->R_PRSTY->IS_LOCKED( ) eq ABAP_FALSE.
        move ABAP_TRUE to LF_DISABLED.
      endif.

***----------------------------------------------------------------------------
***   Insert Toolbar elements Add and Delete
      clear LS_TOOLBAR.
      move C_FUNC_ADD                 to LS_TOOLBAR-FUNCTION.
      move 'Add'(002)                 to LS_TOOLBAR-TEXT.
      move C_BTYPE_BUTTON             to LS_TOOLBAR-BUTN_TYPE.
      move ICON_INSERT_ROW            to LS_TOOLBAR-ICON.
      move LF_DISABLED                to LS_TOOLBAR-DISABLED.
      append LS_TOOLBAR               to E_OBJECT->MT_TOOLBAR.

      clear LS_TOOLBAR.
      move C_FUNC_DEL                 to LS_TOOLBAR-FUNCTION.
      move 'Delete'(003)              to LS_TOOLBAR-TEXT.
      move C_BTYPE_BUTTON             to LS_TOOLBAR-BUTN_TYPE.
      move ICON_DELETE_ROW            to LS_TOOLBAR-ICON.
      move LF_DISABLED                to LS_TOOLBAR-DISABLED.
      append LS_TOOLBAR               to E_OBJECT->MT_TOOLBAR.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      LR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

endmethod.
method HANDLE_USER_COMMAND.

  data:          LS_CELL         type          LVC_S_CELL,
                 LS_ROW          type          LVC_S_ROW.
  data:          LT_CELL         type          LVC_T_CELL,
                 LT_ROW          type          LVC_T_ROW,
                 LT_PRSTY_JOITY  type          ZVCXI_P0SSC_TT_PRSTY_JOITY.
  data:          LR_CX_CKX       type ref to   /VCXI/CX_CKX.
  field-symbols: <T_DATA>        type standard table,
                 <S_PRSTY_JOITY> type          ZVCXI_P0SSC_S_PRSTY_JOITY,
                 <S_DATA>        type          any.

  try.

***   Get Product Style Data
      assign ME->R_DATA->* to <T_DATA>.
      free: <T_DATA>.
      ME->GET_GRID_DATA( IR_DATA = ME->R_DATA ).

      ME->R_GRID->GET_SELECTED_CELLS( importing ET_CELL = LT_CELL ).
      ME->R_GRID->GET_SELECTED_ROWS( importing ET_INDEX_ROWS = LT_ROW ).

      case E_UCOMM.

        when C_FUNC_ADD.
***     Add new Joint Type
          ME->ADD_JOITY( ).

        when C_FUNC_DEL.
***     Delete Joint Type
          if ME->CHECK_SEL_DELETE( ) eq ABAP_FALSE.
***         No Row Selected. Please Select all Row's you wanna Delete.
            raise exception type /VCXI/CX_CKX
             exporting
                F_MSGTY = 'E'
                F_MSGID = 'ZVCXI_P0SSC'
                F_MSGNO = '002'.
          endif.

***       Get Selected Joint Type
          loop at LT_CELL into LS_CELL.
            read table <T_DATA> assigning <S_DATA> index LS_CELL-ROW_ID-INDEX.
            check SY-SUBRC = 0.

            append initial line to LT_PRSTY_JOITY assigning <S_PRSTY_JOITY>.
            move-corresponding <S_DATA> to <S_PRSTY_JOITY>.
          endloop.

          loop at LT_ROW into LS_ROW.
            read table <T_DATA> assigning <S_DATA> index LS_ROW-INDEX.
            check SY-SUBRC = 0.

            append initial line to LT_PRSTY_JOITY assigning <S_PRSTY_JOITY>.
            move-corresponding <S_DATA> to <S_PRSTY_JOITY>.
          endloop.

***       Delete Selected Joint Types
          ME->DELETE_JOITY( IT_PRSTY_JOITY = LT_PRSTY_JOITY ).

      endcase.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      LR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

  ME->F_REFRESH_REQ = ABAP_TRUE.

endmethod.
method SET_EVENT.

  SUPER->SET_EVENT( ).

*** Add Event for Double Click, UserCommand and Toolbar
  set handler ME->HANDLE_USER_COMMAND for ME->R_GRID.
  set handler ME->HANDLE_TOOLBAR      for ME->R_GRID.

endmethod.
