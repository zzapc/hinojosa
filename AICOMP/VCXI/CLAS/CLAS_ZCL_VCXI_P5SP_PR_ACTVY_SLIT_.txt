
class ZCL_VCXI_P5SP_PR_ACTVY_SLIT definition
  public
  inheriting from /VCXI/CL_VKSR_CLSPR
  create public .

public section.

  interfaces ZIF_VCXI_P5SP_ADJ_RLDIM .

  constants C_IAATY_NOOUT type /VCXI/VSMC_IAATY value 'P5_AA_NOOUT' ##NO_TEXT.

  methods GET_NOOUT
    returning
      value(RF_NOOUT) type ZVCXI_XCS_NOOUT
    raising
      /VCXI/CX_CKX .

  methods HANDLE_EVENT
    redefinition .
protected section.

  constants C_IEVNT_I_RECALC_RLDIM type /VCXI/VKSC_IEVNT value 'RECALC_RLDIM' ##NO_TEXT.
private section.
endclass. "ZCL_VCXI_P5SP_PR_ACTVY_SLIT definition
class ZCL_VCXI_P5SP_PR_ACTVY_SLIT implementation.
  method GET_NOOUT.

    data: LR_GATE     type ref to /VCXI/CL_VKSR_GATE.

***--------------------------------------------------------------------------------------
*** Get all Roll Dimensions
    loop at ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_P5SP_PR_RLOUT=>C_IPRVT_P5_RLOUT
                                   IF_INUSE  = ABAP_TRUE ) into LR_GATE.
      RF_NOOUT += cast ZCL_VCXI_P5SP_PR_RLOUT( LR_GATE->R_CLSPR )->GET_NOOUT( ).
    endloop.

  endmethod.
  method HANDLE_EVENT.

    SUPER->HANDLE_EVENT( IF_IEVNT         = IF_IEVNT
                         IT_GATE_PROVIDER = IT_GATE_PROVIDER ).

    case IF_IEVNT.
***   Roll Dimensions
      when C_IEVNT_I_RECALC_RLDIM.
        ME->R_GATE->SET_EVENT( IF_IEVNT = ZIF_VCXI_P5SP_ADJ_RLDIM~C_IEVNT_O_RLDIM_CHANGED ).

    endcase.

  endmethod.
  method ZIF_VCXI_P5SP_ADJ_RLDIM~CALC_RLDIM_OUT.

    data: LS_RLDIM    type        ZVCXI_P5SP_S_RLDIM.
    data: LR_GATE     type ref to /VCXI/CL_VKSR_GATE,
          LR_PR_RLOUT type ref to ZCL_VCXI_P5SP_PR_RLOUT.
    field-symbols: <S_RLDIM>     type  ZVCXI_P5SP_S_RLDIM.


***--------------------------------------------------------------------------------------
*** Get first inbound Dimensions as base
    read table CT_RLDIM into LS_RLDIM index 1.

***--------------------------------------------------------------------------------------
*** Define all new Dimensions
    free CT_RLDIM.

*** Get all Roll Dimensions (sorted)
    loop at ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_P5SP_PR_RLOUT=>C_IPRVT_P5_RLOUT
                                   IF_INUSE  = ABAP_TRUE
                                   IF_SORTED = ABAP_TRUE ) into LR_GATE.
      move LR_GATE->R_CLSPR ?to LR_PR_RLOUT.
      check LR_PR_RLOUT is bound.

      append LS_RLDIM to CT_RLDIM assigning <S_RLDIM>.
      <S_RLDIM>-S_WEBWI = LR_PR_RLOUT->GET_WEBWI( ).
      <S_RLDIM>-NOUPS   = LR_PR_RLOUT->GET_NOUPS( ).
      <S_RLDIM>-NOOUT   = LR_PR_RLOUT->GET_NOOUT( ).
    endloop.

  endmethod.
