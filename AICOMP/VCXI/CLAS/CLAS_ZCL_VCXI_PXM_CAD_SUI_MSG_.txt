
class ZCL_VCXI_PXM_CAD_SUI_MSG definition
  public
  inheriting from ZCL_VCXI_XCM_SUI_MSG
  create public .

public section.
  type-pools ICON .

  methods CONSTRUCTOR
    importing
      !IR_RPSEG type ref to /VCXI/CL_CKMR_RPSEG
    raising
      /VCXI/CX_CKX .
protected section.

  constants C_UC_SHOW_MAT type SALV_DE_FUNCTION value '_20_SHOW_MAT' ##NO_TEXT.
  constants C_UC_SHOW_SDOC_SVC type SALV_DE_FUNCTION value '_10_SHOW_SDOC_SVC' ##NO_TEXT.
  data R_DCO_INPUT type ref to ZCL_VCXI_PXM_CAD_DCO_INB .
  data R_DCO_OUTPUT type ref to ZCL_VCXI_PXM_CAD_DCO_INB .

  methods HANDLE_UC_SHOW_MAT
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_SHOW_SDOC_SVC
    raising
      /VCXI/CX_CKX .

  methods ADJUST_FUNCTIONS
    redefinition .
  methods HANDLE_ADDED_FUNCTION
    redefinition .
private section.
endclass. "ZCL_VCXI_PXM_CAD_SUI_MSG definition
class ZCL_VCXI_PXM_CAD_SUI_MSG implementation.
  method ADJUST_FUNCTIONS.

    try.
***     Standard Functions for Messages
        SUPER->ADJUST_FUNCTIONS( IR_FUNCTIONS_LIST = IR_FUNCTIONS_LIST ).

***--------------------------------------------------------------------------------------
***     Sales Document Position based Functions
        if ME->R_DCO_INPUT->F_CADPO cp 'VBAP*'.
          IR_FUNCTIONS_LIST->ADD_FUNCTION( NAME     = C_UC_SHOW_SDOC_SVC
                                           ICON     = conv #( ICON_CONFIGURATION )
                                           TEXT     = conv #( 'Sales Document SmartVC'(T01) )
                                           TOOLTIP  = conv #( 'Show Sales Document SmartVC'(Q01) )
                                           POSITION = IF_SALV_C_FUNCTION_POSITION=>RIGHT_OF_SALV_FUNCTIONS ).
        endif.

***--------------------------------------------------------------------------------------
***     Material List based Functions
        if ME->R_DCO_INPUT->F_CADPO cp 'MARC*'.
          IR_FUNCTIONS_LIST->ADD_FUNCTION( NAME     = C_UC_SHOW_MAT
                                           ICON     = conv #( ICON_MATERIAL )
                                           TOOLTIP  = conv #( 'Show Material'(Q02) )
                                           POSITION = IF_SALV_C_FUNCTION_POSITION=>RIGHT_OF_SALV_FUNCTIONS ).
        endif.

      catch CX_SALV_WRONG_CALL CX_SALV_EXISTING.
    endtry.

  endmethod.
  method CONSTRUCTOR.

*** Super Constructor
    SUPER->CONSTRUCTOR( IR_RPSEG = IR_RPSEG ).

*** Map DCO to Required Input/Output
    ME->R_RPSEG->MAP_DCO( changing CR_DCO = ME->R_DCO_INPUT ).
    ME->R_RPSEG->MAP_DCO( changing CR_DCO = ME->R_DCO_OUTPUT ).

  endmethod.
  method HANDLE_ADDED_FUNCTION.

    try.
        case E_SALV_FUNCTION.
          when C_UC_SHOW_SDOC_SVC.
            ME->HANDLE_UC_SHOW_SDOC_SVC( ).
          when C_UC_SHOW_MAT.
            ME->HANDLE_UC_SHOW_MAT( ).
          when others.
            SUPER->HANDLE_ADDED_FUNCTION( E_SALV_FUNCTION = E_SALV_FUNCTION ).
        endcase.

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        LR_CX_CKX->SHOW_MESSAGE( ).
    endtry.

  endmethod.
  method HANDLE_UC_SHOW_MAT.

    data: LF_MATNR type MATNR,
          LF_WERKS type WERKS_D.


***------------------------------------------------------------------------------------------------
*** Check Project Owner is a Material
    check ME->R_DCO_INPUT->F_CADPO cp 'MARC*'.

*** Deserialize Owner
    LF_MATNR = ME->R_DCO_INPUT->F_CADPO+4(18).
    LF_WERKS = ME->R_DCO_INPUT->F_CADPO+22(4).

***------------------------------------------------------------------------------------------------
*** Create new Mode and start Transaction
    call function 'TH_CREATE_MODE'
      exporting
        TRANSAKTION    = 'MM03'
        PARAMETERS     = 'RMMG1-MATNR=' && LF_MATNR
        PROCESS_DARK   = ABAP_TRUE
      exceptions
        MAX_SESSIONS   = 1
        INTERNAL_ERROR = 2
        NO_AUTHORITY   = 3
        others         = 4.
    case SY-SUBRC.
      when 0.
      when 1.
***     Maximum number of sessions reached
        /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( IF_MSGTY = 'I'
                                              IF_MSGID = '14'
                                              IF_MSGNO = '027' ).
      when others.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
    endcase.

  endmethod.
  method HANDLE_UC_SHOW_SDOC_SVC.

    data: LF_VBELN type VBELN,
          LF_POSNR type POSNR.


***------------------------------------------------------------------------------------------------
*** Check Project Owner is a Sales Document
    check ME->R_DCO_INPUT->F_CADPO cp 'VBAP*'.

*** Deserialize Owner
    LF_VBELN = ME->R_DCO_INPUT->F_CADPO+4(10).
    LF_POSNR = ME->R_DCO_INPUT->F_CADPO+14(6).


***------------------------------------------------------------------------------------------------
*** Create a new Sale Document Business Object Instance
    data(LR_BOB_SDOC) = new ZCL_VCXI_XCI_BOB_SDOC( ).

*** Load existing Sales Document
    LR_BOB_SDOC->INIT_FROM_DB( IF_VBELN = LF_VBELN ).

*** Get configuration for current position
    data(LR_SVC) = LR_BOB_SDOC->GET_SVC( IF_POSNR = LF_POSNR ).
    check LR_SVC is bound.

*** Start SmartVC UI
    LR_SVC->START_UI( ).

  endmethod.
