
class ZCL_VCXI_PXE_CAD_SRVH_INB definition
  public
  create public .

public section.

  interfaces IF_HTTP_EXTENSION .
protected section.

  data F_BAL_HANDLE type BALLOGHNDL .
  constants C_BALPROBCL_IMP type BALPROBCL value '2' ##NO_TEXT.
  constants C_BALPROBCL_INFO type BALPROBCL value '4' ##NO_TEXT.

  methods SET_RESPONSE
    importing
      !IR_RESPONSE type ref to IF_HTTP_RESPONSE
      !IR_RECORD type ref to /VCXI/CL_CKMR_RECORD
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_PXE_CAD_SRVH_INB definition
class ZCL_VCXI_PXE_CAD_SRVH_INB implementation.
  method IF_HTTP_EXTENSION~HANDLE_REQUEST.

    try.
***     Get Server Request
        if SERVER->REQUEST->GET_METHOD( ) eq 'POST'.

***       Process Inbound directly or as Job
          data(LR_RECORD) = new ZCL_VCXI_PXE_CAD_INB( )->RECEIVE( IF_XXML = SERVER->REQUEST->GET_DATA( ) ).

***       Send Response
          ME->SET_RESPONSE( IR_RESPONSE = SERVER->RESPONSE
                            IR_RECORD   = LR_RECORD ).
        endif.

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        SERVER->RESPONSE->SET_CDATA( DATA = LR_CX_CKX->GET_TEXT( ) ).
    endtry.

  endmethod.
  method SET_RESPONSE.

    data: LR_DCO_INB type ref to ZCL_VCXI_PXM_CAD_DCO_INB.


*** Map DCO to access Project Owner
    IR_RECORD->R_RPROC->MAP_DCO( changing CR_DCO = LR_DCO_INB ).

***------------------------------------------------------------------------------------------------
    case IR_RECORD->R_RPROC->F_SRPRO.
***   Status - Successfully Completed
      when /VCXI/CL_CKMR_RPROC=>C_SRPRO_COMPLETED.
***     Set Payload or Body as XML
        IR_RESPONSE->SET_CONTENT_TYPE( CONTENT_TYPE = IF_REST_MEDIA_TYPE=>GC_APPL_XML ).
        IR_RESPONSE->SET_DATA( DATA = LR_DCO_INB->F_XXML ).

***   Status - Error
      when /VCXI/CL_CKMR_RPROC=>C_SRPRO_ERROR.
        data(LR_OBJMSG_MGR) = IR_RECORD->R_RPROC->GET_MSG( II_MSGTY = value #( ( SIGN = 'E' OPTION = 'EQ' LOW = 'S' ) ) ).
        data(LT_BAPIRET2) = LR_OBJMSG_MGR->R_MESSAGE->GET_BAPIRET2( ).
        IR_RESPONSE->SET_CDATA( DATA = |Error: { value #( LT_BAPIRET2[ 1 ]-MESSAGE optional ) }| ).

      when others.
***     When Request was started with a Project Owner responde with Record GUID
        IR_RESPONSE->SET_CDATA( DATA = |Record { IR_RECORD->/VCXI/IF_CKX_OBJECT~GET_GUID( ) } created| ) .
    endcase.

  endmethod.
endclass. "ZCL_VCXI_PXE_CAD_SRVH_INB implementation
