
class ZCL_VCXI_XCE_MM_CNTXT_MGR definition
  public
  abstract
  create public .

public section.

  class-methods CLASS_CONSTRUCTOR .
  class-methods GET_CNTXT_INFO
    importing
      !IF_CNTXT type ZVCXI_XCE_MM_CNTXT
    exporting
      !EF_CLSNA type SEOCLSNAME
    raising
      /VCXI/CX_CKX .
  methods GET_FG
  abstract
    importing
      !IR_SVC type ref to /VCXI/CL_VKSRA_SVC
    returning
      value(RT_SVCEL) type /VCXI/CKX_TT_OBJECT
    raising
      /VCXI/CX_CKX .
  methods GET_FG_INFO
  abstract
    importing
      !IR_SVCEL_FG type ref to /VCXI/CL_VKSRA_SVC_ELEMENT
    exporting
      !EF_MATNR type MATNR
      !EF_CNTXT type ZVCXI_XCE_MM_CNTXT
    raising
      /VCXI/CX_CKX .
  class-methods GET_INSTANCE
    importing
      !IF_CMATN type /VCXI/VKC_CMATN
    returning
      value(RR_CNTXT_MGR) type ref to ZCL_VCXI_XCE_MM_CNTXT_MGR
    raising
      /VCXI/CX_CKX .
protected section.

  class-methods REGISTER_CNTXT
    importing
      !IF_CNTXT type ZVCXI_XCE_MM_CNTXT
      !IF_CLSNA type SEOCLSNAME .
private section.

  types:
    begin of YS_CNTXT_MGR,
      CMATN       type /VCXI/VKC_CMATN,
      R_CNTXT_MGR type ref to ZCL_VCXI_XCE_MM_CNTXT_MGR,
    end   of YS_CNTXT_MGR.
  types:
    begin of YS_CNTXT,
      CNTXT type ZVCXI_XCE_MM_CNTXT,
      CLSNA type SEOCLSNAME,
    end   of YS_CNTXT .

  class-data:
    T_CNTXT_MGR type sorted table of YS_CNTXT_MGR with unique key CMATN.
  class-data:
    T_CNTXT type sorted table of YS_CNTXT with unique key CNTXT .
endclass. "ZCL_VCXI_XCE_MM_CNTXT_MGR definition
class ZCL_VCXI_XCE_MM_CNTXT_MGR implementation.
  method CLASS_CONSTRUCTOR.

*** Register Context Manger per Configurable Material and load instance of sub-class
    T_CNTXT_MGR = value #( ( CMATN = 'VCXI_FF_FILM_CNF'     R_CNTXT_MGR = new ZCL_VCXI_F0E_MM_CNTXT_MGR( ) )
                           ( CMATN = 'VCXI_FF_SE_M_CNF'     R_CNTXT_MGR = new ZCL_VCXI_F0E_MM_CNTXT_MGR( ) )
                           ( CMATN = 'VCXI_FB_BAG_CNF'      R_CNTXT_MGR = new ZCL_VCXI_F0E_MM_CNTXT_MGR( ) )
                           ( CMATN = 'VCXI_PX_SINGLE_CNF'   R_CNTXT_MGR = new ZCL_VCXI_PXE_MM_CNTXT_MGR( ) )
                           ( CMATN = 'VCXI_PX_SRA_CNF'      R_CNTXT_MGR = new ZCL_VCXI_PXE_MM_CNTXT_MGR( ) )
                         ).

  endmethod.
  method GET_CNTXT_INFO.

    EF_CLSNA = value #( T_CNTXT[ CNTXT = IF_CNTXT ]-CLSNA optional ).

  endmethod.
  method GET_INSTANCE.

    RR_CNTXT_MGR = value #( T_CNTXT_MGR[ CMATN = IF_CMATN ]-R_CNTXT_MGR optional ).

  endmethod.
  method REGISTER_CNTXT.

    insert value #( CNTXT = IF_CNTXT
                    CLSNA = IF_CLSNA ) into table T_CNTXT.

  endmethod.
