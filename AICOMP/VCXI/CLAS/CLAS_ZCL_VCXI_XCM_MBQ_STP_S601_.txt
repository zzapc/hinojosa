
class ZCL_VCXI_XCM_MBQ_STP_S601 definition
  public
  inheriting from ZCL_VCXI_XCM_MBQ_STP_S000
  create public .

public section.

  methods EXECUTE
    redefinition .
protected section.
private section.
endclass. "ZCL_VCXI_XCM_MBQ_STP_S601 definition
class ZCL_VCXI_XCM_MBQ_STP_S601 implementation.
  method EXECUTE.

    data: LS_DMSLI    type ZVCXI_XCM_MBQ_S_DMSLI,
          LS_BAPIRET2 type BAPIRET2.
    data: LR_BL_DOC type ref to ZCL_VCXI_XCSS_BL_DOC,
          LR_ACTDAT type ref to DATA.
    field-symbols: <S_ACTDAT_DMS> type ZVCXI_XCM_MBQ_S_ACTDAT_DMS.


    try.
***     Get DMS Document List Entry of active DMS Document List Entry
        LS_DMSLI = ME->R_DCO_INPUT->T_DMSLI[ DLCNT = ME->R_DCO_INPUT->F_DLCNT ].

***     Check Activity - Create DMS Document
        if not LINE_EXISTS( LS_DMSLI-T_ACTCON[ ACTID = ZCL_VCXI_XCM_MBQ_CUST=>C_ACTID_DMS ] ).
***       Set Status to CANCELED
          ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_CANCELED ).
          return.
        endif.

***     Load Activity Control Data
        LR_ACTDAT = LS_DMSLI-T_ACTCON[ ACTID = ZCL_VCXI_XCM_MBQ_CUST=>C_ACTID_DMS ]-R_ACTDAT.
        assign LR_ACTDAT->* to <S_ACTDAT_DMS>.


***--------------------------------------------------------------------------------------
***     Create DMS Document
        LR_BL_DOC = ZCL_VCXI_XCSS_BL_DOC=>CREATE( IF_DOKAR  = LS_DMSLI-S_DOC_KEY-DOKAR
                                                  IF_DOKNR  = LS_DMSLI-S_DOC_KEY-DOKNR
                                                  IF_DOKTL  = LS_DMSLI-S_DOC_KEY-DOKTL
                                                  IF_DOKVR  = LS_DMSLI-S_DOC_KEY-DOKVR
                                                  IT_DKTXT  = corresponding #( <S_ACTDAT_DMS>-T_DMSDAT_DRAT ) ).

***     Update based on assigned Number
        LS_DMSLI-S_DOC_KEY-DOKAR = LR_BL_DOC->F_DOKAR.
        LS_DMSLI-S_DOC_KEY-DOKNR = LR_BL_DOC->F_DOKNR.
        LS_DMSLI-S_DOC_KEY-DOKTL = LR_BL_DOC->F_DOKTL.
        LS_DMSLI-S_DOC_KEY-DOKVR = LR_BL_DOC->F_DOKVR.

***     Update DMS Document List in DCO
        ME->R_DCO_OUTPUT->UPDATE_DMSLI( IS_DMSLI = LS_DMSLI ).

***     Set Status to Complete
        ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_COMPLETED ).

***--------------------------------------------------------------------------------------
      catch /VCXI/CX_CKX into data(LR_CX_CKX).
***     Set Status to Error
        ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_ERROR ).

***     Add Message to Message Log
        ME->R_RPSEG->R_MSG->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

*** Perform Save and Commit
    ME->R_RPSEG->SAVE_AND_COMMIT( ).

  endmethod.
endclass. "ZCL_VCXI_XCM_MBQ_STP_S601 definition
endclass. "ZCL_VCXI_XCM_MBQ_STP_S601 implementation
