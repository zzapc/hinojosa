
class ZCL_VCXI_P2SR_BL_PSTEP definition
  public
  inheriting from ZCL_VCXI_PXSR_BL_PSTEP
  create public .

public section.
protected section.

  methods GET_PCDIM_IN
    importing
      !IT_PRVDR type /VCXI/VKSR_TT_GATE
    returning
      value(RT_PCDIM) type ZVCXI_P0SP_TT_PCDIM
    raising
      /VCXI/CX_CKX .
  methods GET_PCDIM_OUT
    importing
      !IT_PRVDR type /VCXI/VKSR_TT_GATE
    returning
      value(RT_PCDIM) type ZVCXI_P0SP_TT_PCDIM
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_P2SR_BL_PSTEP definition
class ZCL_VCXI_P2SR_BL_PSTEP implementation.
  method GET_PCDIM_IN.

    data: LS_HNDDIM type ZVCXI_PXS_S_HNDDIM.
    data: LR_GATE       type ref to /VCXI/CL_VKSR_GATE,
          LR_BL_FCFPROP type ref to ZCL_VCXI_P2SS_BL_FCFPROP.

***--------------------------------------------------------------------------------------
*** Process Inbound Provider
    loop at IT_PRVDR into LR_GATE
                    where TABLE_LINE->R_CLSPR is instance of ZIF_VCXI_P2SS_COMM_PREV.

***   Get Handling Dimensions based on Folding Carton Forward Property (Inbound)
      LR_BL_FCFPROP = cast ZIF_VCXI_P2SS_COMM_PREV( LR_GATE->R_CLSPR )->GET_FCFPX( ).
      if LR_BL_FCFPROP is bound.
        LS_HNDDIM = LR_BL_FCFPROP->S_HNDDIM.
      else.
        clear LS_HNDDIM.
      endif.

***   Add PC Dimension
      append value #( PCLEN     = LS_HNDDIM-HNDLE
                      PCLEN_UOM = LS_HNDDIM-HNDLE_UOM
                      PCWID     = LS_HNDDIM-HNDWI
                      PCWID_UOM = LS_HNDDIM-HNDWI_UOM
                      PCHEI     = LS_HNDDIM-HNDHE
                      PCHEI_UOM = LS_HNDDIM-HNDHE_UOM

                      PCUPS     = cast ZIF_VCXI_P2SS_COMM_PREV( LR_GATE->R_CLSPR )->GET_NOUPS( )
                      NOOUT     = '1'
                      PCDIR     = 'A'
                      PCTYP     = ZCL_VCXI_P2SR_PR_RTESPV=>GET_PCTYP_BY_BL_FCBPROP( IR_BL_FCBPROP = cast ZIF_VCXI_P2SS_COMM_PREV( LR_GATE->R_CLSPR )->GET_FCBPX( ) )
                    ) to RT_PCDIM.
    endloop.

  endmethod.
  method GET_PCDIM_OUT.

    data: LF_PCTYP type ZVCXI_P0SP_PCTYP.
    data: LS_HNDDIM type ZVCXI_PXS_S_HNDDIM.
    data: LR_GATE       type ref to /VCXI/CL_VKSR_GATE,
          LR_BL_FCFPROP type ref to ZCL_VCXI_P2SS_BL_FCFPROP.

***--------------------------------------------------------------------------------------
*** Get PC Type and Direction
    ZCL_VCXI_P0SR_CUST_PSTEP=>GET_PSTEP_INFO( exporting IF_IPSTP = ME->F_IPSTP
                                              importing EF_PCTYP = LF_PCTYP ).

***--------------------------------------------------------------------------------------
*** Process Outbound Provider
    loop at IT_PRVDR into LR_GATE
                    where TABLE_LINE->R_CLSPR is instance of ZIF_VCXI_P2SS_COMM_NEXT.

*** Get Handling Dimensions based on Folding Carton Forward Property (Outbound)
      LR_BL_FCFPROP = cast ZIF_VCXI_P2SS_COMM_NEXT( LR_GATE->R_CLSPR )->GET_FCFPX( ).
      if LR_BL_FCFPROP is bound.
        LS_HNDDIM = LR_BL_FCFPROP->S_HNDDIM.
      else.
        clear LS_HNDDIM.
      endif.

***   Add PC Dimension
      append value #( PCTYP     = LF_PCTYP
                      PCLEN     = LS_HNDDIM-HNDLE
                      PCLEN_UOM = LS_HNDDIM-HNDLE_UOM
                      PCWID     = LS_HNDDIM-HNDWI
                      PCWID_UOM = LS_HNDDIM-HNDWI_UOM
                      PCHEI     = LS_HNDDIM-HNDHE
                      PCHEI_UOM = LS_HNDDIM-HNDHE_UOM

                      PCUPS     = cast ZIF_VCXI_P2SS_COMM_NEXT( LR_GATE->R_CLSPR )->GET_NOUPS( )
                      NOOUT     = '1'
                      PCDIR     = 'A'
                    ) to RT_PCDIM.

    endloop.

  endmethod.
endclass. "ZCL_VCXI_P2SR_BL_PSTEP implementation
