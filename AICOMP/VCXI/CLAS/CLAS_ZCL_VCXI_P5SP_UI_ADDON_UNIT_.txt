
class ZCL_VCXI_P5SP_UI_ADDON_UNIT definition
  public
  inheriting from ZCL_VCXI_XCSP_UI_ADDON
  create public .

public section.

  data S_ADDON_UNIT_VIS type ZVCXI_P5SP_S_ADDON_UNIT_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  data R_PR_ADDON_UNIT type ref to ZCL_VCXI_P5SP_PR_ADDON_UNIT .
  data R_GRID_MC_PACKMAT type ref to ZCL_VCXI_XCSP_GRID_MC_PACKMAT .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_P5SP_UI_ADDON_UNIT definition
class ZCL_VCXI_P5SP_UI_ADDON_UNIT implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_P5SP_UI_ADDON_UNIT_BIND'
      exporting
        IR_UI_ADDON_UNIT = ME
      importing
        ES_DYNP          = ME->S_DYNP.

*** Create Grid for GRID_MC_Packaging Material
    create object ME->R_GRID_MC_PACKMAT
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_MC_PACKMAT'.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_ADDON_UNIT.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

    if ME->R_GRID_MC_PACKMAT is bound.
      ME->R_GRID_MC_PACKMAT->DO_FREE( ).
      clear ME->R_GRID_MC_PACKMAT.
    endif.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Super PBO
    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize Grid
    if ME->R_GRID_MC_PACKMAT is bound.
      ME->R_GRID_MC_PACKMAT->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_MC_PACKMAT->REFRESH_GRID( ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_ADDON_UNIT_VIS type ZVCXI_P5SP_S_ADDON_UNIT_VIS.


*** Super
    SUPER->DO_UPDATE_VIS( IS_VIS = IS_VIS ).
    check ME->R_PR_ADDON_UNIT is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_ADDON_UNIT_VIS.

***-------------------------------------------------------------------------------------
*** Set Number of Ups
    if ME->S_ADDON_UNIT_VIS-NOUPS ne LS_ADDON_UNIT_VIS-NOUPS.
      ME->R_PR_ADDON_UNIT->SET_NOUPS( IF_NOUPS = LS_ADDON_UNIT_VIS-NOUPS
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Shipping Unit
    if ME->S_ADDON_UNIT_VIS-SHUNT ne LS_ADDON_UNIT_VIS-SHUNT.
      ME->R_PR_ADDON_UNIT->SET_SHUNT( IF_SHUNT = LS_ADDON_UNIT_VIS-SHUNT
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Previous Quantity
    if ME->S_ADDON_UNIT_VIS-PRQTY ne LS_ADDON_UNIT_VIS-PRQTY.
      ME->R_PR_ADDON_UNIT->SET_PRQTY( IS_PRQTY = LS_ADDON_UNIT_VIS-S_PRQTY
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    if ME->R_GRID_MC_PACKMAT is bound.
      ME->R_GRID_MC_PACKMAT->CHECK_CHANGED_DATA( ).
    endif.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_ADDON_UNIT.

  endmethod.
  method INIT_VIS.

*** Super
    SUPER->INIT_VIS( ).

    free: ME->S_ADDON_UNIT_VIS.
    ME->S_ADDON_UNIT_VIS-S_ADDON_VIS = ME->S_ADDON_VIS.

***--------------------------------------------------------------------------------------
*** Get Number of Ups
    ME->S_ADDON_UNIT_VIS-NOUPS = ME->R_PR_ADDON_UNIT->GET_NOUPS( ).

*** Get Shipping Unit and Description
    ME->S_ADDON_UNIT_VIS-SHUNT = ME->R_PR_ADDON_UNIT->GET_SHUNT( importing EF_DESCR = ME->S_ADDON_UNIT_VIS-SHUNT_DESCR ).

*** Get Previous Quantity
    ME->S_ADDON_UNIT_VIS-S_PRQTY = ME->R_PR_ADDON_UNIT->GET_PRQTY( ).

  endmethod.
