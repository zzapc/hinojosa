
class ZCL_VCXI_P5SS_UI_RLFG definition
  public
  inheriting from ZCL_VCXI_PXSS_UI_XFG
  create public .

public section.

  data S_RLFG_VIS type ZVCXI_P5SS_S_RLFG_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABPT type ZVCXI_XCS_TABPT default '0100S_FG_TAB-TAB'
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  constants C_UC_RLPROP type SYUCOMM value 'RLFG_RLPROP' ##NO_TEXT.
  data R_PR_RLFG type ref to ZCL_VCXI_P5SS_PR_RLFG .
  data R_GRID_PRINT_COM type ref to ZCL_VCXI_PXSS_GRID_PRINT_COM .

  methods HANDLE_UC_RLPROP
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_P5SS_UI_RLFG definition
class ZCL_VCXI_P5SS_UI_RLFG implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_P5SS_UI_RLFG_BIND'
      exporting
        IR_UI_RLFG    = ME
      importing
        ES_DYNP       = ME->S_DYNP
        EF_ACTIVE_TAB = ME->F_ACTIVE_TAB.

***--------------------------------------------------------------------------------------
*** Create Grid for Print
    create object ME->R_GRID_PRINT_COM
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_PRINT_COM'
        IR_PRINT_COM      = ME->R_PR_RLFG.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE
                        IF_TABPT  = IF_TABPT ).

*** Assign th RP Class to the UI Class
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_RLFG.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

***--------------------------------------------------------------------------------------
*** Free Printing Grid
    if ME->R_GRID_PRINT_COM is bound.
      ME->R_GRID_PRINT_COM->DO_FREE( ).
      clear ME->R_GRID_PRINT_COM.
    endif.

  endmethod.
  method DO_MODIFY_SCREEN.

    try.
        SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                 IF_DYNNR = IF_DYNNR ).

***----------------------------------------------------------------------------
***     Modify Screen
        loop at screen.
***       Hide Print Override Flag if undefined
          if SCREEN-NAME cp '*-FOVRR_PRINT' and
             ME->S_RLFG_VIS-FOVRR_PRINT eq ABAP_UNDEFINED.
            move 0 to SCREEN-ACTIVE.
          endif.

***       Reopen Info Buttons
          if SCREEN-GROUP1 eq 'INF'.
            move 1 to SCREEN-INPUT.
          endif.

          modify screen.
        endloop.

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Super PBO
    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize Grid
    if ME->R_GRID_PRINT_COM is bound.
      ME->R_GRID_PRINT_COM->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_PRINT_COM->REFRESH_GRID( ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_RLFG_VIS  type ZVCXI_P5SS_S_RLFG_VIS.


*** Super
    SUPER->DO_UPDATE_VIS( IS_VIS = IS_VIS ).
    check ME->R_PR_RLFG is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_RLFG_VIS.

***--------------------------------------------------------------------------------------
*** Set Web Width
    if ME->S_RLFG_VIS-S_WEBWI ne LS_RLFG_VIS-S_WEBWI.
      ME->R_PR_RLFG->SET_WEBWI( IS_WEBWI = LS_RLFG_VIS-S_WEBWI
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Override for Print
    if ME->S_RLFG_VIS-FOVRR_PRINT ne LS_RLFG_VIS-FOVRR_PRINT.
      ME->R_PR_RLFG->SET_FOVRR_PRINT( IF_FOVRR = LS_RLFG_VIS-FOVRR_PRINT ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    if ME->R_GRID_PRINT_COM is bound.
      ME->R_GRID_PRINT_COM->CHECK_CHANGED_DATA( ).
    endif.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    case IF_UCOMM.
      when C_UC_RLPROP.
        ME->HANDLE_UC_RLPROP( ).

    endcase.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_RLFG.

  endmethod.
  method HANDLE_UC_RLPROP.

*** Start Popup
    ZCL_VCXI_P5SS_POPUP_RLPROP=>START( IR_BL_RLBPROP = ME->R_PR_RLFG->GET_RLBPX( )
                                       IR_BL_RLFPROP = ME->R_PR_RLFG->GET_RLFPX( )
                                       IF_WERKS      = ME->R_PR_RLFG->GET_WERKS( )
                                       IS_WEBWI      = ME->R_PR_RLFG->GET_WEBWI( )
                                       IS_SURWT      = ME->R_PR_RLFG->GET_SURWT( ) ).

  endmethod.
  method INIT_VIS.

    data: LR_BL_RLFPROP type ref to ZCL_VCXI_P5SS_BL_RLFPROP.


    free: ME->S_RLFG_VIS.

    SUPER->INIT_VIS( ).
    ME->S_RLFG_VIS-S_XFG_VIS = ME->S_XFG_VIS.
    check ME->R_PR_RLFG is bound.

*** Web Width (Outbound)
    ME->S_RLFG_VIS-S_WEBWI = ME->R_PR_RLFG->GET_WEBWI( ).

*** Repeat Length (Outbound)
    ME->S_RLFG_VIS-S_REPLE = ME->R_PR_RLFG->GET_REPLE( ).

***--------------------------------------------------------------------------------------
*** Web Width (Outbound)
    ME->S_RLFG_VIS-S_IOSEC-S_WEBWI = ME->R_PR_RLFG->GET_WEBWI( ).

*** Get Surface Weight (Outbound)
    ME->S_RLFG_VIS-S_IOSEC-S_SURWT = ME->R_PR_RLFG->GET_SURWT( ).

*** Number Ups (Outbound)
    ME->S_RLFG_VIS-S_IOSEC-NOUPS   = ME->R_PR_RLFG->GET_NOUPS( ).

*** Get Substrate and Caliper (Outbound)
    LR_BL_RLFPROP = ME->R_PR_RLFG->GET_RLFPX( ).
    if LR_BL_RLFPROP is bound.
      ME->S_RLFG_VIS-S_IOSEC-SBSTR   = LR_BL_RLFPROP->GET_SBSTR( ).
      ME->S_RLFG_VIS-S_IOSEC-S_CALIP = LR_BL_RLFPROP->S_CALIP.
    endif.

***------------------------------------------------------------------------------------------------
*** Override for Print
    ME->S_RLFG_VIS-FOVRR_PRINT = ME->R_PR_RLFG->GET_FOVRR_PRINT( ).

  endmethod.
