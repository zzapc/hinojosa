
class ZCL_VCXI_PFSS_CUST_SHTPR definition
  public
  inheriting from ZCL_VCXI_P0SS_CUST_SHTPR
  create protected .

public section.

  data F_PAPGR type ZVCXI_PFSS_PAPGR read-only .
  data S_GRAMM type ZVCXI_P0SS_S_GRAMM read-only .

  methods CONSTRUCTOR
    importing
      !IF_MATNR type MATNR
      !IF_WERKS type WERKS_D
      !IF_DATE type DATS
    raising
      /VCXI/CX_CKX .
  class-methods GET_INSTANCE_PF_SHTPR
    importing
      !IF_MATNR type MATNR
      !IF_WERKS type WERKS_D
      !IF_DATE type DATS optional
    returning
      value(RR_INSTANCE) type ref to ZCL_VCXI_PFSS_CUST_SHTPR
    raising
      /VCXI/CX_CKX .
protected section.

  constants C_GRAMM_UOM type UNIT value 'GM2'. "#EC NOTEXT
  constants C_SETID_PFS_SHTPR type /VCXI/CKXC_SETID value 'ZVCXI_PFS_SHTPR'. "#EC NOTEXT
private section.
endclass. "ZCL_VCXI_PFSS_CUST_SHTPR definition
class ZCL_VCXI_PFSS_CUST_SHTPR implementation.
method CONSTRUCTOR.

  data:  LF_OBJNUM    type        OBJNUM.
  data:  LS_SETVA     type        /VCXI/CKXC_S_SETVA,
         LS_MAPVC     type        /VCXI/CKXC_S_MAPVC,
         LS_MAPVN     type        /VCXI/CKXC_S_MAPVN.
  data:  LT_SETVA     type        /VCXI/CKXC_TT_SETVA.

  try.
      SUPER->CONSTRUCTOR( IF_MATNR = IF_MATNR
                          IF_WERKS = IF_WERKS
                          IF_DATE  = IF_DATE ).

***   Get Sheet Procurement Info
      move ME->F_MATNR to LF_OBJNUM.
      LT_SETVA = /VCXI/CL_CKXC_CCM=>GET_SETVA( IF_SETID  = C_SETID_PFS_SHTPR
                                               IF_OBJNUM = LF_OBJNUM ).


***   Check if Classification is active
      read table LT_SETVA into LS_SETVA index 1.
      if  LS_SETVA-ATNAM is initial.
        clear: LF_OBJNUM.
        concatenate ME->F_MATNR ME->F_WERKS into LF_OBJNUM.
        LT_SETVA = /VCXI/CL_CKXC_CCM=>GET_SETVA( IF_SETID  = C_SETID_PFS_SHTPR
                                                 IF_OBJNUM = LF_OBJNUM ).
      endif.

      loop at LT_SETVA into LS_SETVA.
        clear: LS_MAPVC.

        case LS_SETVA-MAPID.
          when 'PAPGR'.
            read table LS_SETVA-T_MAPVC into LS_MAPVC index 1.
            move LS_MAPVC-VAMAP     to ME->F_PAPGR.

          when 'GRAMM'.
            read table LS_SETVA-T_MAPVN into LS_MAPVN index 1.
            move LS_MAPVN-ATFLV     to ME->S_GRAMM-GRAMM.
            move LS_MAPVN-ATFLV_UOM to ME->S_GRAMM-GRAMM_UOM.

        endcase.
      endloop.

      if ME->S_BAWGT-BAWGT is not initial and
         ME->S_BAWGP-BAWGP is not initial and
         ME->S_GRAMM-GRAMM is initial.

***     Get BaseWeight in Grammage
        ME->S_GRAMM-GRAMM_UOM = C_GRAMM_UOM.

***     Calculate Weight
        ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = ME->S_BAWGT-BAWGT
                                                            IF_CALCV_UOM_1 = ME->S_BAWGT-BAWGT_UOM
                                                            IF_CALCV_2     = ME->S_BAWGP-BAWGP
                                                            IF_CALCV_UOM_2 = ME->S_BAWGP-BAWGP_UOM
                                                            IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_DIVIDE
                                                            IF_UOM         = ME->S_GRAMM-GRAMM_UOM
                                                  importing EF_CALCV       = ME->S_GRAMM-GRAMM
                                                            EF_CALCV_UOM   = ME->S_GRAMM-GRAMM_UOM ).
      endif.

    cleanup.
      ME->DO_FREE( ).
  endtry.

endmethod.
method GET_INSTANCE_PF_SHTPR.

  data:  LS_INSTANCE type YS_INSTANCE.

  read table ZCL_VCXI_XCS_CUST_MATNR=>T_INSTANCE
             into LS_INSTANCE
             with key MATNR = IF_MATNR
                      WERKS = IF_WERKS
                      DATE  = IF_DATE.

  if SY-SUBRC eq 0.
    try.
        move LS_INSTANCE-R_INSTANCE ?to RR_INSTANCE.
      catch CX_SY_MOVE_CAST_ERROR.
        delete ZCL_VCXI_XCS_CUST_MATNR=>T_INSTANCE where R_INSTANCE eq LS_INSTANCE-R_INSTANCE.
        clear RR_INSTANCE.
    endtry.
  endif.

  if RR_INSTANCE is initial.
    try.
        create object RR_INSTANCE
          exporting
            IF_MATNR = IF_MATNR
            IF_WERKS = IF_WERKS
            IF_DATE  = IF_DATE.
      cleanup.
        clear RR_INSTANCE.
    endtry.
  endif.

endmethod.
