
class ZCL_VCXI_PXSS_UI_XFG definition
  public
  inheriting from ZCL_VCXI_XCS_UI_TAB
  create public .

public section.

  data S_XFG_VIS type ZVCXI_PXSS_S_XFG_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABPT type ZVCXI_XCS_TABPT default '0100S_AFG_TAB-TAB'
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_VIS
    importing
      !IS_VIS type ANY
    raising
      /VCXI/CX_CKX .

  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_PBO
    redefinition .
protected section.

  data R_PR_XFG type ref to ZCL_VCXI_PXSS_PR_XFG .

  methods INIT_VIS
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
private section.
endclass. "ZCL_VCXI_PXSS_UI_XFG definition
class ZCL_VCXI_PXSS_UI_XFG implementation.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE
                        IF_TABPT  = IF_TABPT ).

*** Assign th RP Class to the UI Class
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_XFG.
      catch CX_SY_MOVE_CAST_ERROR.

***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DO_MODIFY_SCREEN.

    data: LF_ALLOW_SEP type ABAP_BOOL,
          LF_ALLOW_MFG type ABAP_BOOL.


    try.
        SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                 IF_DYNNR = IF_DYNNR ).

***     Modify Scree using DynamicVC
        ZCL_VCXI_XCDR_SERVICE=>MODIFY_SCREEN( IR_GATE = ME->R_GATE ).

***     Is separation and/or component ratio allowed
        ME->R_PR_XFG->GET_NOUTT( importing EF_ALLOW_MULTI = LF_ALLOW_SEP ).
        ME->R_PR_XFG->ZIF_VCXI_PXSS_FG~GET_COMPR( importing EF_ALLOWED = LF_ALLOW_MFG ).

        loop at screen.
***       Hide Material Description Override Flag if undefined
          if SCREEN-NAME cp '*-FOVRR_MAKTX' and
             ME->S_XFG_VIS-FOVRR_MAKTX eq ABAP_UNDEFINED.
            move 0 to SCREEN-ACTIVE.
          endif.
***       Disable Material Description if generated
          if SCREEN-NAME cp '*-MAKTX' and
             ME->S_XFG_VIS-FOVRR_MAKTX eq ABAP_FALSE.
            move 0 to SCREEN-INPUT.
          endif.

***       Disable Product Hierarchy if not overwritten
          if SCREEN-NAME cp '*-PRODH' and
             ME->S_XFG_VIS-FOVRR_PRODH eq ABAP_FALSE.
            move 0 to SCREEN-INPUT.
          endif.

***       Hide Separation information if no Separation Step is active
          if SCREEN-GROUP2 = 'SEP' and
             LF_ALLOW_SEP ne ABAP_TRUE.
            SCREEN-INPUT  = 0.
            SCREEN-ACTIVE = 0.
          endif.

***       Hide Component Ratio if not allowed
          if SCREEN-GROUP2 = 'MFG' and
             LF_ALLOW_MFG ne ABAP_TRUE.
            SCREEN-INPUT  = 0.
            SCREEN-ACTIVE = 0.
          endif.

***       Hide Customer Material Info if material exists
          if SCREEN-GROUP2 = 'CMI' and
             ME->S_XFG_VIS-MATNR is not initial.
            SCREEN-INPUT  = 0.
            SCREEN-ACTIVE = 0.
          endif.

***       Hide MTS Flag if not allowed
          if SCREEN-GROUP2 = 'MTS' and
             ME->S_XFG_VIS-FMTSA eq ABAP_UNDEFINED.
            SCREEN-INPUT  = 0.
            SCREEN-ACTIVE = 0.
          endif.

***       Reopen Info Buttons
          if SCREEN-GROUP1 eq 'INF'.
            move 1 to SCREEN-INPUT.
          endif.

          modify screen.

***       Set Cursor
          if SCREEN-NAME eq ME->F_CURSOR.
            set cursor field ME->F_CURSOR.
          endif.
        endloop.

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_PBO.

    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize VIS
    ME->INIT_VIS( ).

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_XFG_VIS  type ZVCXI_PXSS_S_XFG_VIS.


    check ME->R_PR_XFG is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_XFG_VIS.

***--------------------------------------------------------------------------------------
*** Set Material Description
    if ME->S_XFG_VIS-MAKTX        ne LS_XFG_VIS-MAKTX or
       ME->S_XFG_VIS-FOVRR_MAKTX  ne LS_XFG_VIS-FOVRR_MAKTX.
      if LS_XFG_VIS-FOVRR_MAKTX ne ABAP_FALSE.
        ME->R_PR_XFG->ZIF_VCXI_PXSS_FG~SET_MAKTX( IF_MAKTX = LS_XFG_VIS-MAKTX
                                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
      else.
        ME->R_PR_XFG->ZIF_VCXI_PXSS_FG~SET_MAKTX( IF_MAKTX = LS_XFG_VIS-MAKTX
                                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ).
      endif.
    endif.

*** Set Material Belonging to the Customer
    if ME->S_XFG_VIS-KDMAT ne LS_XFG_VIS-KDMAT.
      ME->R_PR_XFG->ZIF_VCXI_PXSS_FG~SET_KDMAT( IF_KDMAT = LS_XFG_VIS-KDMAT
                                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Customer Description of Material
    if ME->S_XFG_VIS-KDPTX ne LS_XFG_VIS-KDPTX.
      ME->R_PR_XFG->ZIF_VCXI_PXSS_FG~SET_KDPTX( IF_KDPTX = LS_XFG_VIS-KDPTX
                                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Product Hierarchy if it is "Overwriten"
    if ME->S_XFG_VIS-FOVRR_PRODH eq ABAP_TRUE and
       ME->S_XFG_VIS-PRODH ne LS_XFG_VIS-PRODH.
      ME->R_PR_XFG->ZIF_VCXI_PXSS_FG~SET_PRODH( IF_PRODH = LS_XFG_VIS-PRODH
                                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Flag Overwrite for Product Hierarchy
    if ME->S_XFG_VIS-FOVRR_PRODH ne LS_XFG_VIS-FOVRR_PRODH.
      ME->R_PR_XFG->ZIF_VCXI_PXSS_FG~SET_PRODH( IF_FOVRR = LS_XFG_VIS-FOVRR_PRODH ).
    endif.


***------------------------------------------------------------------------------------------------
*** Set Component Ratio
    if ME->S_XFG_VIS-COMPR ne LS_XFG_VIS-COMPR.
      ME->R_PR_XFG->ZIF_VCXI_PXSS_FG~SET_COMPR( IF_COMPR = LS_XFG_VIS-COMPR
                                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Distribution Key
    if ME->S_XFG_VIS-S_DIKEY ne LS_XFG_VIS-S_DIKEY.
      ME->R_PR_XFG->SET_DIKEY( IS_DIKEY = LS_XFG_VIS-S_DIKEY
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Number of Outs (total)
    if ME->S_XFG_VIS-NOUTT ne LS_XFG_VIS-NOUTT.
      ME->R_PR_XFG->SET_NOUTT( IF_NOUTT = LS_XFG_VIS-NOUTT
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***------------------------------------------------------------------------------------------------
*** Make-to-Stock Flag
    if ME->S_XFG_VIS-FMTSA ne LS_XFG_VIS-FMTSA.
      ME->R_PR_XFG->ZIF_VCXI_XCSS_MTS~SET_FMTSA( IF_FMTSA = LS_XFG_VIS-FMTSA ).
    endif.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_XFG.

  endmethod.
  method INIT_VIS.

    free: ME->S_XFG_VIS.
    check ME->R_PR_XFG is bound.

***------------------------------------------------------------------------------------------------
*** Get Material Number
    ME->S_XFG_VIS-MATNR = ME->R_PR_XFG->ZIF_VCXI_PXSS_FG~GET_MATNR( ).

*** Get Material Description
    ME->S_XFG_VIS-MAKTX = ME->R_PR_XFG->ZIF_VCXI_PXSS_FG~GET_MAKTX( importing EF_FOVRR = ME->S_XFG_VIS-FOVRR_MAKTX ).

*** Get Material Belonging to the Customer
    ME->S_XFG_VIS-KDMAT = ME->R_PR_XFG->ZIF_VCXI_PXSS_FG~GET_KDMAT( ).

*** Get Customer Description of Material
    ME->S_XFG_VIS-KDPTX = ME->R_PR_XFG->ZIF_VCXI_PXSS_FG~GET_KDPTX( ).

*** Number of Outs (total)
    ME->S_XFG_VIS-NOUTT = ME->R_PR_XFG->GET_NOUTT( ).

*** Get Product Hierarchy
    ME->S_XFG_VIS-PRODH = ME->R_PR_XFG->ZIF_VCXI_PXSS_FG~GET_PRODH( importing EF_FOVRR = ME->S_XFG_VIS-FOVRR_PRODH
                                                                              EF_DESCR = ME->S_XFG_VIS-PRODH_DESCR ).

***------------------------------------------------------------------------------------------------
*** Get Component Ratio
    ME->S_XFG_VIS-COMPR = ME->R_PR_XFG->ZIF_VCXI_PXSS_FG~GET_COMPR( ).

*** Get Distribution Key
    ME->S_XFG_VIS-S_DIKEY = ME->R_PR_XFG->GET_DIKEY( ).

***------------------------------------------------------------------------------------------------
*** Make-to-Stock Flag
    ME->S_XFG_VIS-FMTSA = ME->R_PR_XFG->ZIF_VCXI_XCSS_MTS~GET_FMTSA( ).

  endmethod.
