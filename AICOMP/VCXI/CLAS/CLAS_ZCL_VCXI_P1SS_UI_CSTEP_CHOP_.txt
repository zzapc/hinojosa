
class ZCL_VCXI_P1SS_UI_CSTEP_CHOP definition
  public
  inheriting from ZCL_VCXI_P1SS_UI_CSTEP
  create public .

public section.

  data S_CSTEP_CHOP_VIS type ZVCXI_P1SS_S_CSTEP_CHOP_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
protected section.

  data R_PR_CSTEP_CHOP type ref to ZCL_VCXI_P1SS_PR_CSTEP_CHOP .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_P1SS_UI_CSTEP_CHOP definition
class ZCL_VCXI_P1SS_UI_CSTEP_CHOP implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

***--------------------------------------------------------------------------------------
*** Bind UI to Dynpro
    call function 'ZVCXI_P1SS_UI_CSTEP_CHOP_BIND'
      exporting
        IR_UI_CSTEP_CHOP = ME
      importing
        ES_DYNP          = ME->S_DYNP.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_CSTEP_CHOP.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_CSTEP_CHOP_VIS type ZVCXI_P1SS_S_CSTEP_CHOP_VIS.


*** Call SUPER
    SUPER->DO_UPDATE_VIS( IS_CSTEP_VIS = IS_CSTEP_VIS ).

    check ME->R_PR_CSTEP_CHOP is bound.

*** "Cast" Vis Structure
    move-corresponding IS_CSTEP_VIS to LS_CSTEP_CHOP_VIS.

***--------------------------------------------------------------------------------------
*** Set Dimension
    if ME->S_CSTEP_CHOP_VIS-DIMEN ne LS_CSTEP_CHOP_VIS-DIMEN.
      ME->R_PR_CSTEP_CHOP->SET_DIMEN( IF_DIMEN = LS_CSTEP_CHOP_VIS-DIMEN
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Number of Outs
    if ME->S_CSTEP_CHOP_VIS-NOUTT ne LS_CSTEP_CHOP_VIS-NOUTT.
      ME->R_PR_CSTEP_CHOP->SET_NOUTT( IF_NOUTT = LS_CSTEP_CHOP_VIS-NOUTT
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Trim on Chopping
    if ME->S_CSTEP_CHOP_VIS-S_TRCHO ne LS_CSTEP_CHOP_VIS-S_TRCHO.
      ME->R_PR_CSTEP_CHOP->SET_TRCHO( IS_TRCHO = LS_CSTEP_CHOP_VIS-S_TRCHO
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Trim Top
    if ME->S_CSTEP_CHOP_VIS-S_TRTOP ne LS_CSTEP_CHOP_VIS-S_TRTOP.
      ME->R_PR_CSTEP_CHOP->SET_TRTOP( IS_TRTOP = LS_CSTEP_CHOP_VIS-S_TRTOP
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Trim Bottom
    if ME->S_CSTEP_CHOP_VIS-S_TRBOT ne LS_CSTEP_CHOP_VIS-S_TRBOT.
      ME->R_PR_CSTEP_CHOP->SET_TRBOT( IS_TRBOT = LS_CSTEP_CHOP_VIS-S_TRBOT
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_CSTEP_CHOP.

  endmethod.
  method INIT_VIS.

    SUPER->INIT_VIS( ).

    free: ME->S_CSTEP_CHOP_VIS.
    move ME->S_CSTEP_VIS to S_CSTEP_CHOP_VIS-S_CSTEP_VIS.

***--------------------------------------------------------------------------------------
*** Dimension
    ME->S_CSTEP_CHOP_VIS-DIMEN = ME->R_PR_CSTEP_CHOP->ZIF_VCXI_PXSS_CHOP~GET_DIMEN( ).

*** Number of Outs
    ME->S_CSTEP_CHOP_VIS-NOUTT = ME->R_PR_CSTEP_CHOP->GET_NOUTT( ).

*** Trim on Chopping
    ME->S_CSTEP_CHOP_VIS-S_TRCHO = ME->R_PR_CSTEP_CHOP->ZIF_VCXI_PXSS_CHOP~GET_TRCHO( importing EF_DESCR = ME->S_CSTEP_CHOP_VIS-TRCHO_DESCR ).

*** Trim Top
    ME->S_CSTEP_CHOP_VIS-S_TRTOP = ME->R_PR_CSTEP_CHOP->ZIF_VCXI_PXSS_CHOP~GET_TRTOP( ).

*** Trim Bottom
    ME->S_CSTEP_CHOP_VIS-S_TRBOT = ME->R_PR_CSTEP_CHOP->ZIF_VCXI_PXSS_CHOP~GET_TRBOT( ).

  endmethod.
