
class ZCL_VCXI_FRE_MM_DUI_EXTR definition
  public
  inheriting from ZCL_VCXI_XCA_MWB_CLDUI
  create public .

public section.

  data S_DUI_EXTR_VIS type ZVCXI_FRE_MM_S_DUI_EXTR_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to ZCL_VCXI_XCA_MWB_UIFW
      !IR_CLMAT type ref to ZCL_VCXI_XCA_MWB_CLMAT
    raising
      /VCXI/CX_CKX .

  methods DO_FREE
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
  methods DO_PBO
    redefinition .
protected section.

  data R_TEXTEDIT_BDATA type ref to ZCL_VCXI_XCI_TEXTEDIT_MLTX .

  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_FRE_MM_DUI_EXTR definition
class ZCL_VCXI_FRE_MM_DUI_EXTR implementation.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW  = IR_UIFW
                        IR_CLMAT = IR_CLMAT  ).

*** Create Text Editor for Basic Data
    create object ME->R_TEXTEDIT_BDATA
      exporting
        IF_CONTAINER_NAME = '0100CC_TEXTEDIT_BDATA'
        IR_BOB_MAT        = IR_CLMAT->GET_BOB_MAT( )
        IF_TDID           = ZCL_VCXI_XCI_BOB_MAT=>C_TDID_BDATA.

***----------------------------------------------------------------------------
*** Bind UI
    call function 'ZVCXI_FRE_MM_DUI_EXTR_BIND'
      exporting
        IR_DUI_EXTR = ME
      importing
        ES_DYNP     = ME->S_DYNP.

  endmethod.
  method DO_FREE.

    SUPER->DO_FREE( ).

*** Basic Data Text
    if ME->R_TEXTEDIT_BDATA is bound.
      ME->R_TEXTEDIT_BDATA->DO_FREE( ).
      clear ME->R_TEXTEDIT_BDATA.
    endif.

  endmethod.
  method DO_PBO.

*** Call SUPER
    SUPER->DO_PBO( ).

*** TextEdit Basic Data
    if ME->R_TEXTEDIT_BDATA is bound.
      ME->R_TEXTEDIT_BDATA->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_TEXTEDIT_BDATA->REFRESH_TEXTEDIT( IF_READONLY_MODE = switch #( ME->R_CLMAT->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ) when ABAP_TRUE then CL_GUI_TEXTEDIT=>FALSE
                                                                                                                                                else CL_GUI_TEXTEDIT=>TRUE )
                                              IR_BOB_MAT       = ME->R_CLMAT->GET_BOB_MAT( ) ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_DUI_EXTR_VIS type ZVCXI_FRE_MM_S_DUI_EXTR_VIS.


*** Call SUPER
    SUPER->DO_UPDATE_VIS( IS_DUI_VIS = IS_DUI_VIS ).

    check ME->R_CLMAT is bound.

*** "Cast" Vis Structure
    move-corresponding IS_DUI_VIS to LS_DUI_EXTR_VIS.


***--------------------------------------------------------------------------------------
*** Material Updates
    data(LR_BOB_MAT) = ME->R_CLMAT->GET_BOB_MAT( ).
    if LR_BOB_MAT is bound.
***   Set Material Plant Status
      if ME->S_DUI_EXTR_VIS-MMSTA ne LS_DUI_EXTR_VIS-MMSTA.
        LR_BOB_MAT->S_PLANTDATA-PUR_STATUS = LS_DUI_EXTR_VIS-MMSTA.
        ME->R_CLMAT->MARK_CHANGED( ).
      endif.
    endif.

***--------------------------------------------------------------------------------------
*** Basic Data Text
    if ME->R_TEXTEDIT_BDATA is bound.
      if ME->R_TEXTEDIT_BDATA->UPDATE_TEXT( ) eq ABAP_TRUE.
        ME->R_CLMAT->MARK_CHANGED( ).
      endif.
    endif.

  endmethod.
  method INIT_VIS.

    check ME->R_CLMAT is bound.

    SUPER->INIT_VIS( ).

    free: ME->S_DUI_EXTR_VIS.
    ME->S_DUI_EXTR_VIS = corresponding #( ME->S_DUI_VIS ).

***--------------------------------------------------------------------------------------
    data(LR_BOB_MAT) = ME->R_CLMAT->GET_BOB_MAT( ).
    if LR_BOB_MAT is bound.
***   Get Plant Material Status
      ME->S_DUI_EXTR_VIS-MMSTA = LR_BOB_MAT->S_PLANTDATA-PUR_STATUS.
    endif.

  endmethod.
