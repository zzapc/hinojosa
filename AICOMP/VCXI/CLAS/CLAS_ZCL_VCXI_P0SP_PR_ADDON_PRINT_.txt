
class ZCL_VCXI_P0SP_PR_ADDON_PRINT definition
  public
  inheriting from ZCL_VCXI_XCSP_PR_ADDON
  create public .

public section.

  constants C_IAATY_NOCOL type /VCXI/VSMC_IAATY value 'P0_AA_NOCOL'. "#EC NOTEXT
  constants C_PARID_FPRFS type /VCXI/VKS_PARID value 'ZVCXI_P0_FPRFS'. "#EC NOTEXT
  constants C_PARID_PRARE type /VCXI/VKS_PARID value 'ZVCXI_P0_PRARE'. "#EC NOTEXT
  constants C_PARID_PRSID type /VCXI/VKS_PARID value 'ZVCXI_P0_PRSID'. "#EC NOTEXT
  constants C_PARID_PRTYP type /VCXI/VKS_PARID value 'ZVCXI_P0_PRTYP'. "#EC NOTEXT
  constants C_PARID_SUTYP type /VCXI/VKS_PARID value 'ZVCXI_P0_SUTYP'. "#EC NOTEXT

  methods GET_NOCOL
    returning
      value(RF_NOCOL) type ZVCXI_P0S_NOCOL
    raising
      /VCXI/CX_CKX .
  methods GET_PRARE
    returning
      value(RS_PRARE) type ZVCXI_P0S_S_PRARE
    raising
      /VCXI/CX_CKX .
  methods GET_PRSID
    returning
      value(RF_PRSID) type ZVCXI_P0S_PRSID
    raising
      /VCXI/CX_CKX .
  methods GET_PRSID_DESCR
    returning
      value(RF_DESCR) type /VCXI/CKX_DESCR
    raising
      /VCXI/CX_CKX .
  methods GET_PRTYP
    returning
      value(RF_PRTYP) type ZVCXI_P0S_PRTYP
    raising
      /VCXI/CX_CKX .
  methods GET_PRTYP_DESCR
    returning
      value(RF_DESCR) type /VCXI/CKX_DESCR
    raising
      /VCXI/CX_CKX .
  methods GET_SUTYP
    returning
      value(RF_SUTYP) type ZVCXI_P0S_SUTYP
    raising
      /VCXI/CX_CKX .
  methods GET_FPRFS
    returning
      value(RF_FPRFS) type ZVCXI_P0SS_FPRFS
    raising
      /VCXI/CX_CKX .
  methods GET_SUTYP_DESCR
    returning
      value(RF_DESCR) type /VCXI/CKX_DESCR
    raising
      /VCXI/CX_CKX .
  methods SET_PRARE
    importing
      !IS_PRARE type ZVCXI_P0S_S_PRARE
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_PRSID
    importing
      !IF_PRSID type ZVCXI_P0S_PRSID
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_PRTYP
    importing
      !IF_PRTYP type ZVCXI_P0S_PRTYP
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_FPRFS
    importing
      !IF_FPRFS type ZVCXI_P0SS_FPRFS
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_SUTYP
    importing
      !IF_SUTYP type ZVCXI_P0S_SUTYP
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .

  methods GET_STATUS
    redefinition .
  methods GET_STATUS_MSG
    redefinition .
  methods HANDLE_EVENT
    redefinition .
protected section.

  constants C_IAATY_FULLSPRINT type /VCXI/VSMC_IAATY value 'P0_AA_FULLSPRINT'. "#EC NOTEXT
  constants C_IEVNT_I_RECALC_MC_CONSU type /VCXI/VKSC_IEVNT value 'RECALC_MC_CONSU'. "#EC NOTEXT
  constants C_IEVNT_O_PRINT_DATA_CHANGED type /VCXI/VKSC_IEVNT value 'PRINT_DATA_CHANGED'. "#EC NOTEXT
  constants C_IEVNT_O_RECALC_CONSU_REQ type /VCXI/VKSC_IEVNT value 'RECALC_CONSU_REQUESTED'. "#EC NOTEXT

  methods HANDLE_RECALC_MC_CONSU
    importing
      !IT_GATE type /VCXI/VKSR_TT_GATE
    raising
      /VCXI/CX_CKX .

  methods GET_RTE_SAREC
    redefinition .
  methods GET_RTE_SAVAL
    redefinition .
private section.
endclass. "ZCL_VCXI_P0SP_PR_ADDON_PRINT definition
class ZCL_VCXI_P0SP_PR_ADDON_PRINT implementation.
method GET_FPRFS.

  data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

  LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_FPRFS ).
  move LS_PARAM-ATWRT to RF_FPRFS.

endmethod.
method GET_NOCOL.

  data:  LT_GATE   type /VCXI/VKSR_TT_GATE.

*** Get all Inks
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_P0SP_PR_MC_INK=>C_IPRVT_P0_MC_INK
                                   IF_INUSE = ABAP_TRUE ).

  move LINES( LT_GATE ) to RF_NOCOL.

endmethod.
method GET_PRARE.

  data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

  LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PRARE ).
  move LS_PARAM-ATFLV to RS_PRARE-PRARE.
  move LS_PARAM-UNIT  to RS_PRARE-PRARE_UOM.

endmethod.
method GET_PRSID.

  data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

  LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PRSID ).
  move LS_PARAM-ATWRT to RF_PRSID.

endmethod.
method GET_PRSID_DESCR.

  data:  LF_PRSID type ZVCXI_P0S_PRSID.

  LF_PRSID = ME->GET_PRSID( ).
  RF_DESCR = ZCL_VCXI_P0SS_CUST_PRINT=>GET_PRSID_DESCR( IF_PRSID = LF_PRSID ).

endmethod.
method GET_PRTYP.

  data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

  LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PRTYP ).
  move LS_PARAM-ATWRT to RF_PRTYP.

endmethod.
method GET_PRTYP_DESCR.

  data:  LF_PRTYP type ZVCXI_P0S_PRTYP.

  LF_PRTYP = ME->GET_PRTYP( ).
  RF_DESCR = ZCL_VCXI_P0SS_CUST_PRINT=>GET_PRTYP_DESCR( IF_PRTYP = LF_PRTYP ).

endmethod.
method GET_RTE_SAREC.

  data:          LF_GAATY      type /VCXI/VSMC_GAATY.
  field-symbols: <S_RTE_SAREC> type /VCXI/VSMR_S_RTE_SAREC,
                 <S_ARECN>     type /VCXI/VSMR_S_RTE_RANGE_ARECN.

*** Get Super
  RT_RTE_SAREC = SUPER->GET_RTE_SAREC( ).

  check ME->R_RTE_CADDON          is bound and
        ME->R_RTE_CADDON->R_ATYPE is bound.

*** Aspect - P0_AA_NOCOL
  try.
      LF_GAATY = ME->R_RTE_CADDON->R_ATYPE->GET_GAATY_BY_IAATY( IF_IAATY = C_IAATY_NOCOL
                                                                IF_MAATY = /VCXI/CL_VSMC_ATYPE=>C_MAATY_REC ).
      append initial line to RT_RTE_SAREC assigning <S_RTE_SAREC>.
      move LF_GAATY                                     to <S_RTE_SAREC>-GAATY.
      move 1                                            to <S_RTE_SAREC>-MAXSEL.
      move /VCXI/CL_VSMR_RTE_CADDON=>C_ASORT_DESCENDING to <S_RTE_SAREC>-ASORT.

      append initial line      to <S_RTE_SAREC>-I_ARECN assigning <S_ARECN>.
      move 'I'                 to <S_ARECN>-SIGN.
      move 'LE'                to <S_ARECN>-OPTION.
      move ME->GET_NOCOL( )    to <S_ARECN>-LOW.


    catch /VCXI/CX_CKX.
  endtry.

endmethod.
method GET_RTE_SAVAL.

  data:          LF_GAATY      type /VCXI/VSMC_GAATY.
  field-symbols: <S_RTE_SAVAL> type /VCXI/VSMR_S_RTE_SAVAL,
                 <S_AVVAL>     type /VCXI/VSMR_S_RTE_RANGE_AVVAL.

*** Get Super
  RT_RTE_SAVAL = SUPER->GET_RTE_SAVAL( ).

  check ME->R_RTE_CADDON          is bound and
        ME->R_RTE_CADDON->R_ATYPE is bound.

*** Aspect - P0_AA_FULLSPRINT
  try.
      LF_GAATY = ME->R_RTE_CADDON->R_ATYPE->GET_GAATY_BY_IAATY( IF_IAATY = C_IAATY_FULLSPRINT
                                                                IF_MAATY = /VCXI/CL_VSMC_ATYPE=>C_MAATY_VALUE ).
      append initial line to RT_RTE_SAVAL assigning <S_RTE_SAVAL>.
      move LF_GAATY            to <S_RTE_SAVAL>-GAATY.
      append initial line      to <S_RTE_SAVAL>-I_AVVAL assigning <S_AVVAL>.
      move 'I'                 to <S_AVVAL>-SIGN.
      move 'EQ'                to <S_AVVAL>-OPTION.
      move ME->GET_FPRFS( )    to <S_AVVAL>-LOW.
    catch /VCXI/CX_CKX.
  endtry.

endmethod.
method GET_STATUS.

  data: LS_PRARE type ZVCXI_P0S_S_PRARE.

  RF_ELMST = SUPER->GET_STATUS( ).

***------------------------------------------------------------------
*** Check Print Data
  LS_PRARE = ME->GET_PRARE( ).

*** Check Printable Area
  if LS_PRARE-PRARE is not initial.
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G ).
  else.
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R ).
  endif.

*** Check Print Side
  if ME->GET_PRSID( ) is not initial.
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G ).
  else.
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R ).
  endif.

*** Check Number of Colors
  if ME->GET_NOCOL( ) is not initial.
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G ).
  else.
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R ).
  endif.

*** Check Print Type
  if ME->GET_PRTYP( ) is not initial.
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G ).
  else.
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R ).
  endif.

*** Check Surface Type
  if ME->GET_SUTYP( ) is not initial.
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G ).
  else.
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R ).
  endif.

endmethod.
method GET_STATUS_MSG.

  data: LS_PRARE type ZVCXI_P0S_S_PRARE.

  SUPER->GET_STATUS_MSG( IR_MESSAGE = IR_MESSAGE ).

***------------------------------------------------------------------
*** Check Print Data
  LS_PRARE = ME->GET_PRARE( ).

*** Check Printable Area
  if LS_PRARE-PRARE is not initial.
***   Printable Area is maintained.
    IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P0SP'
                             IF_MSGTY = 'S'
                             IF_MSGNO = '041' ).
  else.
***   Printable Area is not maintained.
    IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P0SP'
                             IF_MSGTY = 'E'
                             IF_MSGNO = '042' ).
  endif.

*** Check Print Side
  if ME->GET_PRSID( ) is not initial.
***   Print Side is maintained.
    IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P0SP'
                             IF_MSGTY = 'S'
                             IF_MSGNO = '043' ).
  else.
***   Print Side is not maintained.
    IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P0SP'
                             IF_MSGTY = 'E'
                             IF_MSGNO = '044' ).
  endif.

*** Check Number of Colors
  if ME->GET_NOCOL( ) is not initial.
***   Number of Colors is maintained.
    IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P0SP'
                             IF_MSGTY = 'S'
                             IF_MSGNO = '045' ).
  else.
***   Number of Colors is not maintained.
    IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P0SP'
                             IF_MSGTY = 'E'
                             IF_MSGNO = '046' ).
  endif.

*** Check Print Type
  if ME->GET_PRTYP( ) is not initial.
***   Print Type is maintained.
    IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P0SP'
                             IF_MSGTY = 'S'
                             IF_MSGNO = '047' ).
  else.
***   Print Type is not maintained.
    IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P0SP'
                             IF_MSGTY = 'E'
                             IF_MSGNO = '048' ).
  endif.

*** Check Surface Type
  if ME->GET_SUTYP( ) is not initial.
***   Surface Type is maintained.
    IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P0SP'
                             IF_MSGTY = 'S'
                             IF_MSGNO = '049' ).
  else.
***   Surface Type is not maintained.
    IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P0SP'
                             IF_MSGTY = 'E'
                             IF_MSGNO = '050' ).
  endif.

endmethod.
method GET_SUTYP.

  data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

  LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SUTYP ).
  move LS_PARAM-ATWRT to RF_SUTYP.

endmethod.
method GET_SUTYP_DESCR.

  data:  LF_SUTYP type ZVCXI_P0S_SUTYP.

  LF_SUTYP = ME->GET_SUTYP( ).
  RF_DESCR = ZCL_VCXI_P0SS_CUST_PRINT=>GET_SUTYP_DESCR( IF_SUTYP = LF_SUTYP ).

endmethod.
method HANDLE_EVENT.

  SUPER->HANDLE_EVENT( IF_IEVNT         = IF_IEVNT
                       IT_GATE_PROVIDER = IT_GATE_PROVIDER ).

  case IF_IEVNT.
    when C_IEVNT_I_RECALC_MC_CONSU.
      ME->HANDLE_RECALC_MC_CONSU( IT_GATE = IT_GATE_PROVIDER ).
  endcase.

endmethod.
method HANDLE_RECALC_MC_CONSU.

  data:  LT_GATE            type        /VCXI/VKSR_TT_GATE.
  data:  LS_MQTYI           type        ZVCXI_XCSP_S_MQTYI,
         LS_MQTYI_C         type        ZVCXI_XCSP_S_MQTYI,
         LS_PRARE           type        ZVCXI_P0S_S_PRARE.
  data:  LF_UPDATE_ALL      type        ABAP_BOOL.
  data:  LR_GATE            type ref to /VCXI/CL_VKSR_GATE,
         LR_GATE_PARENT     type ref to /VCXI/CL_VKSR_GATE,
         LR_PR_MACHINE      type ref to ZCL_VCXI_XCSP_PR_MACHINE,
         LR_PR_MC           type ref to ZCL_VCXI_XCSP_PR_MC.

***------------------------------------------------------------------
*** Should all MC Ink's updated or just the requester
  read table IT_GATE with key TABLE_LINE = ME->R_GATE
  transporting no fields.
  if SY-SUBRC = 0.
    LF_UPDATE_ALL = ABAP_TRUE.
  else.
    LR_GATE_PARENT = ME->R_GATE->GET_PARENT( ).
    read table IT_GATE with key TABLE_LINE = LR_GATE_PARENT
  transporting no fields.
    if SY-SUBRC = 0.
      LF_UPDATE_ALL = ABAP_TRUE.
    endif.
  endif.

***------------------------------------------------------------------
*** Get Pritable Area
  LS_PRARE = ME->GET_PRARE( ).

***------------------------------------------------------------------
*** Get all Machine Quantity
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCSP_PR_MACHINE=>C_IPRVT_XCS_MACHINE
                                   IF_INUSE = ABAP_TRUE ).

  read table LT_GATE into LR_GATE index 1.
  if SY-SUBRC = 0.

    clear: LR_PR_MACHINE.
    try.
        move LR_GATE->R_CLSPR ?to LR_PR_MACHINE.
      catch CX_SY_MOVE_CAST_ERROR.
        clear: LR_PR_MACHINE.
    endtry.
    if LR_PR_MACHINE is bound.
      move LR_PR_MACHINE->GET_MQTYI( ) to LS_MQTYI.
    endif.

  endif.

*** Calc total Printable Area
  LS_MQTYI_C-MQTYI     = LS_MQTYI-MQTYI * LS_PRARE-PRARE.
  LS_MQTYI_C-MQTYI_UOM = LS_PRARE-PRARE_UOM.

***------------------------------------------------------------------
*** Get all Material Consumptions
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCSP_PR_MC=>C_IPRVT_XCS_MC
                                   IF_INUSE = ABAP_TRUE ).

***----------------------------------------------------------------------------
*** Start Recalc Consumption of all Material Consumptions or just the requester
  loop at LT_GATE into LR_GATE.
    if LF_UPDATE_ALL eq ABAP_FALSE.
      read table IT_GATE with key TABLE_LINE = LR_GATE transporting no fields.
      if SY-SUBRC ne 0.
        continue.
      endif.
    endif.

    try.

        clear: LR_PR_MC.
        move LR_GATE->R_CLSPR ?to LR_PR_MC.
        check LR_PR_MC is bound.
        if LR_GATE->F_ID = 'MC_INK'.
          LR_PR_MC->CALC_CONSU( IS_MQTYI = LS_MQTYI_C ).
        else.
          LR_PR_MC->CALC_CONSU( IS_MQTYI = LS_MQTYI ).
        endif.


      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.

  endloop.

endmethod.
method SET_FPRFS.

  data:  LF_CHANGED type ABAP_BOOL.
  data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

  LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_FPRFS ).

  move IF_FPRFS to LS_PARAM-ATWRT.
  move IF_SETBY to LS_PARAM-SETBY.

  LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

  if LF_CHANGED eq ABAP_TRUE.
*** Send Event
    ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_PRINT_DATA_CHANGED ).
  endif.

endmethod.
method SET_PRARE.

  data:  LF_CHANGED type ABAP_BOOL.
  data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

  LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PRARE ).

  move IS_PRARE-PRARE     to LS_PARAM-ATFLV.
  move IS_PRARE-PRARE_UOM to LS_PARAM-UNIT.
  move IF_SETBY           to LS_PARAM-SETBY.

  LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

  if LF_CHANGED eq ABAP_TRUE.
*** Send Event
    ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_RECALC_CONSU_REQ ).
  endif.

endmethod.
method SET_PRSID.

  data:  LF_CHANGED type ABAP_BOOL.
  data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

  LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PRSID ).

  move IF_PRSID to LS_PARAM-ATWRT.
  move IF_SETBY to LS_PARAM-SETBY.

  LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

  if LF_CHANGED eq ABAP_TRUE.
*** Send Event
    ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_PRINT_DATA_CHANGED ).
  endif.

endmethod.
method SET_PRTYP.

  data:  LF_CHANGED type ABAP_BOOL.
  data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

  LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PRTYP ).

  move IF_PRTYP to LS_PARAM-ATWRT.
  move IF_SETBY to LS_PARAM-SETBY.

  LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

  if LF_CHANGED eq ABAP_TRUE.
*** Send Event
    ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_PRINT_DATA_CHANGED ).
  endif.

endmethod.
method SET_SUTYP.

  data:  LF_CHANGED type ABAP_BOOL.
  data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

  LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SUTYP ).

  move IF_SUTYP to LS_PARAM-ATWRT.
  move IF_SETBY to LS_PARAM-SETBY.

  LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

  if LF_CHANGED eq ABAP_TRUE.
*** Send Event
    ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_PRINT_DATA_CHANGED ).
  endif.

endmethod.
