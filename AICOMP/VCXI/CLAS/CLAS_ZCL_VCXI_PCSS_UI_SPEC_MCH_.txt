
class ZCL_VCXI_PCSS_UI_SPEC_MCH definition
  public
  inheriting from ZCL_VCXI_P0SS_UI_SPEC_MCH
  create public .

public section.

  data S_PC_SPEC_MCH_VIS type ZVCXI_PCSS_S_SPEC_MCH_VIS .

  methods DO_UPDATE_PC_SPEC_MCH_VIS
    importing
      !IS_PC_SPEC_MCH_VIS type ZVCXI_PCSS_S_SPEC_MCH_VIS
    raising
      /VCXI/CX_CKX .
  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
protected section.

  data R_PR_PC_SPEC_MCH type ref to ZCL_VCXI_PCSS_PR_SPEC_MCH .

  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_PCSS_UI_SPEC_MCH definition
class ZCL_VCXI_PCSS_UI_SPEC_MCH implementation.
method ACTIVATE.

  SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
  call function 'ZVCXI_PCSS_UI_SPEC_MCH_BIND'
    exporting
      IR_UI_SPEC_MCH = ME
    importing
      ES_DYNP        = ME->S_DYNP.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                      IR_UIGATE = IR_UIGATE
                      IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
  try.
      move IR_GATE->R_CLSPR ?to ME->R_PR_PC_SPEC_MCH.
    catch CX_SY_MOVE_CAST_ERROR.
***   UI ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '801'.
  endtry.

endmethod.
method DO_UPDATE_PC_SPEC_MCH_VIS.

  data: LS_SPEC_MCH_VIS type ZVCXI_P0SS_S_SPEC_MCH_VIS.

***--------------------------------------------------------------------------------------
*** Update General Data
  move-corresponding IS_PC_SPEC_MCH_VIS to LS_SPEC_MCH_VIS.
  ME->DO_UPDATE_SPEC_MCH_VIS( IS_SPEC_MCH_VIS = LS_SPEC_MCH_VIS ).

*** Set Fefco
  if ME->S_PC_SPEC_MCH_VIS-FEFCO ne IS_PC_SPEC_MCH_VIS-FEFCO.
    ME->R_PR_PC_SPEC_MCH->SET_FEFCO( IF_FEFCO = IS_PC_SPEC_MCH_VIS-FEFCO
                                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

***--------------------------------------------------------------------------------------
*** Update Board Description
  if ME->S_PC_SPEC_MCH_VIS-BDESC ne IS_PC_SPEC_MCH_VIS-BDESC.
    ME->R_PR_PC_SPEC_MCH->SET_BDESC( IF_BDESC = IS_PC_SPEC_MCH_VIS-BDESC
                                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

***--------------------------------------------------------------------------------------
*** Update Flute
  if ME->S_PC_SPEC_MCH_VIS-FLUTE ne IS_PC_SPEC_MCH_VIS-FLUTE.
    ME->R_PR_PC_SPEC_MCH->SET_FLUTE( IF_FLUTE = IS_PC_SPEC_MCH_VIS-FLUTE
                                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

endmethod.
method INIT_VIS.

  SUPER->INIT_VIS( ).

  free: ME->S_PC_SPEC_MCH_VIS.
  move-corresponding ME->S_SPEC_MCH_VIS to ME->S_PC_SPEC_MCH_VIS.

***--------------------------------------------------------------------------------------
*** Get Fefco
  move ME->R_PR_PC_SPEC_MCH->GET_FEFCO( )       to ME->S_PC_SPEC_MCH_VIS-FEFCO.

  set parameter id 'ZVCXI_P0SS_TPRST' field ZCL_VCXI_PCSS_PR_SPEC_MCH=>C_TPRST_FEFCO.

***--------------------------------------------------------------------------------------
*** Get Boardgrade Dscription and Flute
  move ME->R_PR_PC_SPEC_MCH->GET_BDESC( )       to ME->S_PC_SPEC_MCH_VIS-BDESC.
  move ME->R_PR_PC_SPEC_MCH->GET_FLUTE( )       to ME->S_PC_SPEC_MCH_VIS-FLUTE.
  move ME->R_PR_PC_SPEC_MCH->GET_FLUTE_DESCR( ) to ME->S_PC_SPEC_MCH_VIS-FLUTE_DESCR.

endmethod.
