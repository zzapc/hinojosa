
class ZCL_VCXI_FRS_TI_VERS definition
  public
  inheriting from ZCL_VCXI_XCS_TI
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
    raising
      /VCXI/CX_CKX .

  methods /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ
    redefinition .
  methods GET_INFO
    redefinition .
  methods /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL
    redefinition .
protected section.

  data R_PR_VERS type ref to ZCL_VCXI_FRS_PR_VERS .

  methods ADD_UC_DUPLICATE
    importing
      !IR_MENU type ref to CL_CTMENU
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_DUPLICATE
    raising
      /VCXI/CX_CKX .
private section.

  constants C_UC_DUPLICATE type UI_FUNC value 'DUPLICATE' ##NO_TEXT.
endclass. "ZCL_VCXI_FRS_TI_VERS definition
class ZCL_VCXI_FRS_TI_VERS implementation.
  method /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ.

*** Call Super
    SUPER->/VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ( IR_MENU = IR_MENU ).

*** Add User Command for Duplicate Version
    ME->ADD_UC_DUPLICATE( exporting IR_MENU = IR_MENU ).

  endmethod.
method /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL.

  case IF_FCODE.
    when C_UC_DUPLICATE.
      ME->HANDLE_UC_DUPLICATE( ).

    when others.
      SUPER->/VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL( IF_FCODE = IF_FCODE ).
  endcase.

endmethod.
  method ADD_UC_DUPLICATE.

    data: LF_DESCR    type        /VCXI/CKX_DESCR,
          LF_TEXT     type        GUI_TEXT,
          LF_DISABLED type        CUA_ACTIVE.
    data: LS_QTY      type        /VCXI/VKSC_S_QTY.

    LS_QTY = ME->R_GATE->GET_ALLOWED_RECURRENCES( ).
    if LS_QTY-QTYMA gt 1.

***   Disable in Display Mode
      if ME->R_GATE->IS_LOCKED( ) ne ABAP_TRUE.
        move ABAP_TRUE to LF_DISABLED.
      else.
        if LS_QTY-QTYMA gt ME->R_GATE->GET_NUM_OF_RECURRENCES( ).
          move ABAP_FALSE to LF_DISABLED.
        else.
          move ABAP_TRUE  to LF_DISABLED.
        endif.
      endif.

***   Add Copy Functionality
      LF_DESCR = ME->R_GATE->GET_DESCR( ).
      move 'Duplicate &'(F10) to LF_TEXT.
      replace first occurrence of '&' in LF_TEXT with LF_DESCR.

***   Set Menu
      IR_MENU->ADD_FUNCTION( FCODE    = C_UC_DUPLICATE
                             DISABLED = LF_DISABLED
                             TEXT     = LF_TEXT ).

***   Set Seperator
      IR_MENU->ADD_SEPARATOR( ).

    endif.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_VERS.
      catch CX_SY_MOVE_CAST_ERROR.
***     TI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '802'.
    endtry.

  endmethod.
  method GET_INFO.

    field-symbols:  <S_ITEM>    type        TREEMCITEN.

    SUPER->GET_INFO( importing  ES_NODE   = ES_NODE
                                ET_ITEM   = ET_ITEM
                                EF_EXPAND = EF_EXPAND ) .

***----------------------------------------------------------------------------
*** If version status is released -> make in_use read-only
    read table ET_ITEM assigning <S_ITEM> with key ITEM_NAME = /VCXI/CL_VKSRU_TREE=>C_COLUMN_INUSE.
    check SY-SUBRC eq 0.

    check ME->R_PR_VERS is bound and
          ME->R_PR_VERS->IS_RELEASED( ) eq ABAP_TRUE.
*** Set Read-Only for In-Use Flag
    move ABAP_FALSE to <S_ITEM>-EDITABLE.

  endmethod.
  method HANDLE_UC_DUPLICATE.

    data:  LR_GATE type ref to /VCXI/CL_VKSR_GATE.

*** Duplicate Version
    ME->R_PR_VERS->DUPLICATE( importing ER_GATE = LR_GATE ).

*** Open UI of new Version
    ME->R_UIGATE->OPEN_SUBSCREEN( IR_GATE = LR_GATE ).

  endmethod.
