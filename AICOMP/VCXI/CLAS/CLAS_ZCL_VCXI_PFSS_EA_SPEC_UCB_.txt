
class ZCL_VCXI_PFSS_EA_SPEC_UCB definition
  public
  inheriting from /VCXI/CL_VKSR_CLSEA
  create public .

public section.

  methods GET_SBINFO
    exporting
      !EF_PAPGR type ZVCXI_PFSS_PAPGR
      !ES_GRAMM type ZVCXI_P0SS_S_GRAMM
      !ES_CALIP type ZVCXI_P0SS_S_CALIP
      !ES_AGGRA type ZVCXI_PFSS_S_AGGRA
      !ES_INGRA type ZVCXI_PFSS_S_INGRA
      !EF_GRDIR type ZVCXI_PFSS_GRDIR
    raising
      /VCXI/CX_CKX .
  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods SET_CUSTMAT_INFO
    importing
      !IF_CMATN type ZVCXI_XCSS_CMATN optional
      !IF_MATDE type ZVCXI_XCSS_MATDE optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
      !IF_RAISE_EVENTS type ABAP_BOOL default ABAP_TRUE
    raising
      /VCXI/CX_CKX .
  methods GET_CUSTMAT_INFO
    exporting
      !EF_CMATN type ZVCXI_XCSS_CMATN
      !EF_MATDE type ZVCXI_XCSS_MATDE
    raising
      /VCXI/CX_CKX .
protected section.

  data R_PR_SPEC_UCB type ref to ZCL_VCXI_PFSS_PR_SPEC_UCB .
private section.
endclass. "ZCL_VCXI_PFSS_EA_SPEC_UCB definition
class ZCL_VCXI_PFSS_EA_SPEC_UCB implementation.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_GATE = IR_GATE ).

*** Assign the PR Class to the Element API Class
  try.
      move ME->R_GATE->R_CLSPR ?to ME->R_PR_SPEC_UCB.
    catch CX_SY_MOVE_CAST_ERROR.
***   Element API ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '804'.
  endtry.

endmethod.
method GET_CUSTMAT_INFO.

  check ME->R_PR_SPEC_UCB is bound.

***------------------------------------------------------------------
*** Get Customer Material Number
  if EF_CMATN is requested.
    EF_CMATN = ME->R_PR_SPEC_UCB->GET_CMATN( ).
  endif.

*** Get Customer Material Description
  if EF_MATDE is requested.
    EF_MATDE = ME->R_PR_SPEC_UCB->GET_MATDE( ).
  endif.

endmethod.
method GET_SBINFO.

  check ME->R_PR_SPEC_UCB is bound.

***--------------------------------------------------------------------------------------
*** Paper Grade
  if EF_PAPGR is requested.
    move ME->R_PR_SPEC_UCB->GET_PAPGR( ) to EF_PAPGR.
  endif.

*** Grammage
  if ES_GRAMM is requested.
    move ME->R_PR_SPEC_UCB->GET_GRAMM( ) to ES_GRAMM.
  endif.

*** Caliper
  if ES_CALIP is requested.
    move ME->R_PR_SPEC_UCB->GET_CALIP( IF_POOTI = '*' ) to ES_CALIP.
  endif.

***--------------------------------------------------------------------------------------
*** Against Grain
  if ES_AGGRA is requested.
    move ME->R_PR_SPEC_UCB->GET_AGGRA( ) to ES_AGGRA.
  endif.

*** In Grain
  if ES_INGRA is requested.
    move ME->R_PR_SPEC_UCB->GET_INGRA( ) to ES_INGRA.
  endif.

*** Grain Direction
  if EF_GRDIR is requested.
    move ME->R_PR_SPEC_UCB->GET_GRDIR( ) to EF_GRDIR.
  endif.

endmethod.
method SET_CUSTMAT_INFO.

  check ME->R_PR_SPEC_UCB        is bound     and
        ME->R_GATE->IS_LOCKED( ) eq ABAP_TRUE.

***----------------------------------------------------------------------------
*** Set Customer Material Number
  if IF_CMATN is supplied.
    ME->R_PR_SPEC_UCB->SET_CMATN( IF_CMATN = IF_CMATN
                                  IF_SETBY = IF_SETBY ).
  endif.

*** Set Customer Material Description
  if IF_MATDE is supplied.
    ME->R_PR_SPEC_UCB->SET_MATDE( IF_MATDE = IF_MATDE
                                  IF_SETBY = IF_SETBY ).
  endif.

***----------------------------------------------------------------------------
*** Process Events
  if IF_RAISE_EVENTS eq ABAP_TRUE.
    ME->R_GATE->RAISE_EVENTS( ).
  endif.

endmethod.
