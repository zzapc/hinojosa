
class ZCL_VCXI_FFSS_EA_MS_FPROC definition
  public
  inheriting from ZCL_VCXI_F0SS_EA_MS_XPROC
  create public .

public section.

  methods GET_EXTVAL
    returning
      value(RS_EXTVAL) type ZVCXI_FFE_MM_S_EXTVAL
    raising
      /VCXI/CX_CKX .
  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods GET_LOTMI
    returning
      value(RS_LOTMI) type ZVCXI_F0S_S_LOTMI
    raising
      /VCXI/CX_CKX .
protected section.

  data R_PR_MS_FPROC type ref to ZCL_VCXI_FFSS_PR_MS_FPROC .
private section.
endclass. "ZCL_VCXI_FFSS_EA_MS_FPROC definition
class ZCL_VCXI_FFSS_EA_MS_FPROC implementation.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_GATE = IR_GATE ).

*** Assign the PR Class to the Element API Class
    try.
        move ME->R_GATE->R_CLSPR ?to ME->R_PR_MS_FPROC.
      catch CX_SY_MOVE_CAST_ERROR.

***     Element API ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '804'.

    endtry.

  endmethod.
  method GET_EXTVAL.

    data: LR_BL_FISTRUCT type ref to ZCL_VCXI_FFSS_BL_FISTRUCT,
          LR_BL_FIFPROP  type ref to ZCL_VCXI_FFSS_BL_FIFPROP.


*** Get Standard Values
    ME->R_PR_MS_FPROC->GET_FIWID( importing ES_FIWID = RS_EXTVAL-S_FIWID ).
    ME->R_PR_MS_FPROC->GET_RELEN( importing ES_RELEN = RS_EXTVAL-S_RELEN ).
    ME->R_PR_MS_FPROC->GET_FITIN( importing ES_FITIN = RS_EXTVAL-S_FITIN ).
    ME->R_PR_MS_FPROC->GET_FITEX( importing ES_FITEX = RS_EXTVAL-S_FITEX ).
    ME->R_PR_MS_FPROC->GET_TREQW( importing ES_TREQW = RS_EXTVAL-S_TREQW ).
    ME->R_PR_MS_FPROC->GET_SURWT( importing ES_SURWT = RS_EXTVAL-S_SURWT ).

*** Film Structure
    ME->R_PR_MS_FPROC->GET_FISTX( importing EF_FISTX       = RS_EXTVAL-FISTX
                                            ER_BL_FISTRUCT = LR_BL_FISTRUCT ).
    if LR_BL_FISTRUCT is bound.
      LR_BL_FISTRUCT->GET_TFILM( importing EF_TFILM = RS_EXTVAL-TFILM ).
    endif.

*** Film Forward Properties
    ME->R_PR_MS_FPROC->ZIF_VCXI_FFSS_COMM_NEXT~GET_FIFPX( importing ER_BL_FIFPROP = LR_BL_FIFPROP ).
    if LR_BL_FIFPROP is bound.
      RS_EXTVAL-FISTY   = LR_BL_FIFPROP->F_FISTY.
      RS_EXTVAL-S_GULEF = LR_BL_FIFPROP->S_GULEF.
      RS_EXTVAL-S_GURIG = LR_BL_FIFPROP->S_GURIG.
      RS_EXTVAL-TFOLD   = LR_BL_FIFPROP->F_TFOLD.
    endif.

*** Roll Dimensions
    RS_EXTVAL-S_RDITA = ME->R_PR_MS_FPROC->GET_RDITA( ).

  endmethod.
  method GET_LOTMI.

    check ME->R_PR_MS_FPROC is bound.
    ME->R_PR_MS_FPROC->GET_LOTMI( importing ES_LOTMI = RS_LOTMI ).

  endmethod.
