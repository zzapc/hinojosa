
class ZCL_VCXI_FFS_PICTURE_RHNDL definition
  public
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IF_CONTAINER_NAME type SCRFNAME
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE optional
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE optional
    raising
      /VCXI/CX_CKX .
  methods DO_FREE
    raising
      /VCXI/CX_CKX .
  methods REFRESH_PICTURE
    importing
      !IF_EYEMP type ZVCXI_FFS_EYEMP
      !IF_RWDIR type ZVCXI_FFS_RWDIR
      !IF_RWSID type ZVCXI_FFS_RWSID
    raising
      /VCXI/CX_CKX .
protected section.

  data F_W3OBJID type W3OBJID .
  data R_PICTURE type ref to CL_GUI_PICTURE .
  data R_PICTURE_CONTAINER type ref to CL_GUI_CUSTOM_CONTAINER .
  data R_GATE type ref to /VCXI/CL_VKSR_GATE .
  data R_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE .
  data F_CONTAINER_NAME type SCRFNAME .
private section.
endclass. "ZCL_VCXI_FFS_PICTURE_RHNDL definition
class ZCL_VCXI_FFS_PICTURE_RHNDL implementation.
  method CONSTRUCTOR.

    move IR_GATE   to ME->R_GATE.
    move IR_UIGATE to ME->R_UIGATE.
    move IF_CONTAINER_NAME to ME->F_CONTAINER_NAME.

  endmethod.
  method DO_FREE.

    free: ME->R_GATE, ME->R_UIGATE.

*** Free Picture
    if ME->R_PICTURE is bound.
      ME->R_PICTURE->FREE( exceptions
                             CNTL_ERROR        = 1
                             CNTL_SYSTEM_ERROR = 2
                             others            = 3 ).
      if SY-SUBRC ne 0.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
      endif.
      clear ME->R_PICTURE.
      clear ME->F_W3OBJID.
    endif.

*** Free Container
    if ME->R_PICTURE_CONTAINER is bound.
      ME->R_PICTURE_CONTAINER->FREE( exceptions CNTL_ERROR        = 1
                                                CNTL_SYSTEM_ERROR = 2
                                                others            = 3 ).
      if SY-SUBRC ne 0.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
      endif.
      clear ME->R_PICTURE_CONTAINER.
    endif.

  endmethod.
  method REFRESH_PICTURE.

    data: LF_RWGNA type ZVCXI_FFS_RWGNA,
          LF_URL   type CNDP_URL.


***--------------------------------------------------------------------------------------
*** Initialize Container
    if ME->R_PICTURE_CONTAINER is not bound.
      create object ME->R_PICTURE_CONTAINER
        exporting
          CONTAINER_NAME = ME->F_CONTAINER_NAME
        exceptions
          others         = 6.
      if SY-SUBRC ne 0.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
      endif.
    endif.

*** Initialize Picture
    if ME->R_PICTURE is not bound.
      create object ME->R_PICTURE
        exporting
          PARENT = ME->R_PICTURE_CONTAINER
        exceptions
          others = 2.
      if SY-SUBRC ne 0.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
      endif.
    endif.

***--------------------------------------------------------------------------------------
    check ME->R_PICTURE is bound.

*** Select Picture Details
    LF_RWGNA = ZCL_VCXI_FFSS_CUST_RHNDL=>GET_RWGNA( IF_RWSID = IF_RWSID
                                                    IF_RWDIR = IF_RWDIR
                                                    IF_EYEMP = IF_EYEMP ).

*** Check if new URL needs to be created
    check LF_RWGNA ne ME->F_W3OBJID.
    move LF_RWGNA to ME->F_W3OBJID.

*** Get new URL
    if LF_RWGNA is not initial.
      call function 'DP_PUBLISH_WWW_URL'
        exporting
          OBJID    = ME->F_W3OBJID
          LIFETIME = CNDP_LIFETIME_TRANSACTION
        importing
          URL      = LF_URL
        exceptions
          others   = 1.
      if SY-SUBRC ne 0.
        clear LF_URL.
      endif.
    else.
      clear LF_URL.
    endif.

***--------------------------------------------------------------------------------------
    if LF_URL is not initial.
***   Load Picture
      ME->R_PICTURE->LOAD_PICTURE_FROM_URL_ASYNC( exporting  URL    = LF_URL
                                                  exceptions ERROR  = 1
                                                             others = 2 ).
      if SY-SUBRC ne 0.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
      endif.

      ME->R_PICTURE->SET_DISPLAY_MODE( DISPLAY_MODE = CL_GUI_PICTURE=>DISPLAY_MODE_FIT_CENTER ).
    else.
***   Don't show any Picture in case of Error
      ME->R_PICTURE->CLEAR_PICTURE( exceptions ERROR  = 1
                                               others = 2 ).
      if SY-SUBRC ne 0.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
      endif.
    endif.

  endmethod.
