
class ZCL_VCXI_P1SR_BL_PSTEP_SSS definition
  public
  inheriting from ZCL_VCXI_P1SR_BL_PSTEP
  create public .

public section.

  interfaces ZIF_VCXI_PXSR_PCDIR .
  interfaces ZIF_VCXI_PXSR_ADJ_PCDIM .
  interfaces ZIF_VCXI_XCSR_BL_OUTSO .

  constants C_IPSTP_SSS type /VCXI/VSMC_IPSTP value 'PX_PS_SSS' ##NO_TEXT.

  methods CONSTRUCTOR
    importing
      !IF_IPSTP type /VCXI/VSMC_IPSTP default C_IPSTP_SSS
      !IF_DATE type DATS
      !IR_APPDATA type ref to /VCXI/CL_VKSR_APPDATA
    raising
      /VCXI/CX_CKX .

  methods ADD_ASPECTS_TO_RTE_PSTEP
    redefinition .
  methods ADD_LIMITS_TO_RTE_PSTEP
    redefinition .
  methods COMPARE
    redefinition .
  methods COPY
    redefinition .
  methods GET_ARBPL_INFO
    redefinition .
  methods GET_SPVRE_ACTVY
    redefinition .
  methods INIT_BY_SPEC
    redefinition .
  methods ADJ_FEATURE
    redefinition .
protected section.

  data F_FASCO type ZVCXI_PXS_FASCO .
  data F_FASLI type ZVCXI_PXS_FASLI .
  data F_FASLO type ZVCXI_PXS_FASLO .
  data S_PCDIM_IN type ZVCXI_P0SP_S_PCDIM .
  data T_PCDIM_OUT type ZVCXI_P0SP_TT_PCDIM .
  data F_FLUTE type ZVCXI_PCS_FLUTE .

  methods INIT_BY_CSTEP_SSS
    importing
      !IT_PRVDR type /VCXI/VKSR_TT_GATE
    raising
      /VCXI/CX_CKX .
  methods FILL_SPVRE_ADDON_SSS
    importing
      !IR_BL_RTESPV type ref to ZCL_VCXI_XCSR_BL_RTESPV
      !IR_SPVRE_ADDON type ref to /VCXI/CL_VKSR_SPVRE
      !IR_ARBPL type ref to /VCXI/CL_VSMC_ARBPL
    changing
      !CT_SPVRE type /VCXI/VKSR_TT_SPVRE
    raising
      /VCXI/CX_CKX .

  methods FILL_SPVRE_ADDON
    redefinition .
  methods GET_PCDIM_OUT
    redefinition .
  methods GET_XML_DREF
    redefinition .
private section.
endclass. "ZCL_VCXI_P1SR_BL_PSTEP_SSS definition
class ZCL_VCXI_P1SR_BL_PSTEP_SSS implementation.
  method ADD_ASPECTS_TO_RTE_PSTEP.

***----------------------------------------------------------------------------
*** Get Super
    SUPER->ADD_ASPECTS_TO_RTE_PSTEP( IR_RTE_PSTEP = IR_RTE_PSTEP ).


***----------------------------------------------------------------------------
*** Aspect - PC_AA_FLUTE
    try.
        IR_RTE_PSTEP->SET_RTE_SAVAL( IF_IAATY  = ZCL_VCXI_P1SP_PR_ADDON_SSS=>C_IAATY_FLUTE
                                     II_AVVAL  = value #( ( SIGN = 'I'
                                                            OPTION = 'EQ'
                                                            LOW    = ME->F_FLUTE ) ) ).

      catch /VCXI/CX_CKX.
    endtry.

  endmethod.
  method ADD_LIMITS_TO_RTE_PSTEP.

    data: LF_LDUOM type /VCXI/VSMC_LDUOM,
          LF_LDVAL type /VCXI/VSMR_LDVAL.
    data: LI_LVVAL type /VCXI/VSMR_TT_RTE_RANGE_LVVAL.
    data: LT_LTYPE type /VCXI/CKX_TT_OBJECT.
    data: LR_LTYPE  type ref to /VCXI/CL_VSMC_LTYPE,
          LR_OBJECT type ref to /VCXI/IF_CKX_OBJECT.
    field-symbols: <S_LVVAL> type /VCXI/VSMR_S_RTE_RANGE_LVVAL.


    SUPER->ADD_LIMITS_TO_RTE_PSTEP( IR_RTE_PSTEP = IR_RTE_PSTEP ).

***----------------------------------------------------------------------------
*** Get all linked Limits
    LT_LTYPE = ME->R_PSTEP->/VCXI/IF_VSMC_LINK_LTYPE~GET_LINKED_LTYPE( ).
    loop at LT_LTYPE into LR_OBJECT.
      move LR_OBJECT ?to LR_LTYPE.

      clear: LF_LDVAL,
             LF_LDUOM,
             LI_LVVAL.

***----------------------------------------------------------------------------
      case LR_LTYPE->/VCXI/IF_CKX_OBJECT~GET_ID( ).
***     Sheet Length (Inbound)
        when 'P0_LT_DI_SHTLEN'.
          move ME->S_PCDIM_IN-PCLEN     to LF_LDVAL.
          move ME->S_PCDIM_IN-PCLEN_UOM to LF_LDUOM.

***     Sheet Width (Inbound)
        when 'P0_LT_DI_SHTWID'.
          move ME->S_PCDIM_IN-PCWID     to LF_LDVAL.
          move ME->S_PCDIM_IN-PCWID_UOM to LF_LDUOM.

***     Slot/Slit/Score Activity
        when 'PX_LT_VP_SSS_ACTVY'.
          if ME->F_FASLO eq ABAP_TRUE.
            append value #( SIGN = 'I' OPTION = 'EQ' LOW = 'SLO' ) to LI_LVVAL.
          endif.
          if ME->F_FASLI eq ABAP_TRUE.
            append value #( SIGN = 'I' OPTION = 'EQ' LOW = 'SLI' ) to LI_LVVAL.
          endif.
          if ME->F_FASCO eq ABAP_TRUE.
            append value #( SIGN = 'I' OPTION = 'EQ' LOW = 'SCO' ) to LI_LVVAL.
          endif.

        when others.
          continue.
      endcase.


***----------------------------------------------------------------------------
***   Set
      case LR_LTYPE->F_MLTYP.
        when /VCXI/CL_VSMC_LTYPE=>C_MLTYP_D.
          IR_RTE_PSTEP->SET_RTE_LDIM( IR_LTYPE = LR_LTYPE
                                      IF_LDVAL = LF_LDVAL
                                      IF_LDUOM = LF_LDUOM ).
        when /VCXI/CL_VSMC_LTYPE=>C_MLTYP_V.
          IR_RTE_PSTEP->SET_RTE_LVAL( IR_LTYPE = LR_LTYPE
                                      II_LVVAL = LI_LVVAL ).
      endcase.
    endloop.

  endmethod.
  method ADJ_FEATURE.

    SUPER->ADJ_FEATURE( changing CT_FEATURE = CT_FEATURE ).

*** Activity Slit
    /VCXI/CL_CKI_IQC=>MODIFY_FEATURE( exporting IS_FEATURE = value #( FTNAM = 'activitySlit'     FTVAL = ME->F_FASLI )
                                      changing  CT_FEATURE = CT_FEATURE ).
*** Activity Slot
    /VCXI/CL_CKI_IQC=>MODIFY_FEATURE( exporting IS_FEATURE = value #( FTNAM = 'activitySlot'     FTVAL = ME->F_FASLO )
                                      changing  CT_FEATURE = CT_FEATURE ).
*** Activity Score
    /VCXI/CL_CKI_IQC=>MODIFY_FEATURE( exporting IS_FEATURE = value #( FTNAM = 'activityScore'    FTVAL = ME->F_FASCO )
                                      changing  CT_FEATURE = CT_FEATURE ).

  endmethod.
  method COMPARE.

    data:  LR_BL_PSTEP_SSS type ref to ZCL_VCXI_P1SR_BL_PSTEP_SSS.


*** Super Compare
    RF_SIMILAR = SUPER->COMPARE( IR_BL_PSTEP = IR_BL_PSTEP ).
    check RF_SIMILAR eq ABAP_TRUE.

    move IR_BL_PSTEP ?to LR_BL_PSTEP_SSS.

*** PC Dimensions
    if ME->S_PCDIM_IN  eq LR_BL_PSTEP_SSS->S_PCDIM_IN  and
       ME->T_PCDIM_OUT eq LR_BL_PSTEP_SSS->T_PCDIM_OUT.
      move ABAP_TRUE  to RF_SIMILAR.
    else.
      move ABAP_FALSE to RF_SIMILAR.
      exit.
    endif.

*** Activity
    if ME->F_FASLO eq LR_BL_PSTEP_SSS->F_FASLO and
       ME->F_FASLI eq LR_BL_PSTEP_SSS->F_FASLI and
       ME->F_FASCO eq LR_BL_PSTEP_SSS->F_FASCO.
      move ABAP_TRUE  to RF_SIMILAR.
    else.
      move ABAP_FALSE to RF_SIMILAR.
      exit.
    endif.

*** Flute
    if ME->F_FLUTE eq LR_BL_PSTEP_SSS->F_FLUTE.
      move ABAP_TRUE  to RF_SIMILAR.
    else.
      move ABAP_FALSE to RF_SIMILAR.
      exit.
    endif.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IF_IPSTP   = IF_IPSTP
                        IF_DATE    = IF_DATE
                        IR_APPDATA = IR_APPDATA ).

*** Initialize Outsourcing Info
    move ZCL_VCXI_XCSP_PR_OUTSO=>C_PARID_PRICP_P to ME->ZIF_VCXI_XCSR_BL_OUTSO~F_PARID_PRICP.
    ME->INIT_OUTSO( ).

  endmethod.
  method COPY.

    data:  LR_BL_PSTEP_SSS type ref to ZCL_VCXI_P1SR_BL_PSTEP_SSS.


*** Super Copy
    RR_BL_PSTEP = SUPER->COPY( ).
    move RR_BL_PSTEP ?to LR_BL_PSTEP_SSS.

*** PC Dimensions
    move ME->S_PCDIM_IN    to LR_BL_PSTEP_SSS->S_PCDIM_IN.
    move ME->T_PCDIM_OUT   to LR_BL_PSTEP_SSS->T_PCDIM_OUT.

*** Activity
    move ME->F_FASLO       to LR_BL_PSTEP_SSS->F_FASLO.
    move ME->F_FASLI       to LR_BL_PSTEP_SSS->F_FASLI.
    move ME->F_FASCO       to LR_BL_PSTEP_SSS->F_FASCO.

*** Flute
    move ME->F_FLUTE       to LR_BL_PSTEP_SSS->F_FLUTE.

  endmethod.
  method FILL_SPVRE_ADDON.

    SUPER->FILL_SPVRE_ADDON( exporting IR_BL_RTESPV   = IR_BL_RTESPV
                                       IR_SPVRE_ADDON = IR_SPVRE_ADDON
                                       IR_ARBPL       = IR_ARBPL
                             changing  CT_SPVRE       = CT_SPVRE ).

    case IR_SPVRE_ADDON->GET_ID( ).
      when 'PX_AT_SSS'.
        ME->FILL_SPVRE_ADDON_SSS( exporting IR_BL_RTESPV   = IR_BL_RTESPV
                                            IR_SPVRE_ADDON = IR_SPVRE_ADDON
                                            IR_ARBPL       = IR_ARBPL
                                  changing  CT_SPVRE       = CT_SPVRE ).
    endcase.

  endmethod.
  method FILL_SPVRE_ADDON_SSS.

***------------------------------------------------------------------------------------------------
*** Set Activities
    IR_SPVRE_ADDON->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_PXSP_PR_ADDON_SSS=>C_PARID_FASLO
                                                   PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR
                                                   ATWRT = ME->F_FASLO
                                                   SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).

    IR_SPVRE_ADDON->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_PXSP_PR_ADDON_SSS=>C_PARID_FASLI
                                                   PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR
                                                   ATWRT = ME->F_FASLI
                                                   SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).

    IR_SPVRE_ADDON->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_PXSP_PR_ADDON_SSS=>C_PARID_FASCO
                                                   PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR
                                                   ATWRT = ME->F_FASCO
                                                   SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).


***------------------------------------------------------------------------------------------------
*** Add PC Out
    ME->ADD_SPVRE_PCOUT( exporting IR_BL_RTESPV    = IR_BL_RTESPV
                                   IR_SPVRE_PARENT = IR_SPVRE_ADDON
                                   IT_PCDIM        = ME->T_PCDIM_OUT
                         changing  CT_SPVRE        = CT_SPVRE ).

  endmethod.
  method GET_ARBPL_INFO.

***------------------------------------------------------------------
*** Don't overrule existing
    check CF_ID_GRP   is initial and
          CF_ID_ARBPL is initial.

***------------------------------------------------------------------
*** Standard
    move 'CONV'     to CF_ID_ARBPL.

**** If Manual
*    if ME->ZIF_VCXI_XCSR_BL_MANU~F_MANWC eq ABAP_TRUE.
*      move 'MANU'     to CF_ID_ARBPL.
*    endif.

*** If Outsourcing
    if ME->ZIF_VCXI_XCSR_BL_OUTSO~F_OUTSO eq ABAP_TRUE.
      move 'OUTSO'    to CF_ID_ARBPL.
    endif.

  endmethod.
  method GET_PCDIM_OUT.

    data: LF_PCTYP type ZVCXI_P0SP_PCTYP.
    data: LS_HNDDIM type ZVCXI_PXS_S_HNDDIM.
    data: LR_GATE       type ref to /VCXI/CL_VKSR_GATE,
          LR_BL_CRFPROP type ref to ZCL_VCXI_P1SS_BL_CRFPROP.

***--------------------------------------------------------------------------------------
*** Get PC Type and Direction
    if ME->F_FASCO eq ABAP_TRUE.
      ZCL_VCXI_P0SR_CUST_PSTEP=>GET_PSTEP_INFO( exporting IF_IPSTP = ME->F_IPSTP && '@SCORE'
                                                importing EF_PCTYP = LF_PCTYP ).
    else.
      ZCL_VCXI_P0SR_CUST_PSTEP=>GET_PSTEP_INFO( exporting IF_IPSTP = ME->F_IPSTP
                                                importing EF_PCTYP = LF_PCTYP ).
    endif.

***--------------------------------------------------------------------------------------
*** Process Outbound Provider
    loop at IT_PRVDR into LR_GATE
                    where TABLE_LINE->R_CLSPR is instance of ZCL_VCXI_P1SS_PR_CSTEP_SSS.

*** Get Handling Dimensions based on Corrugator Forward Property (Outbound)
      LR_BL_CRFPROP = cast ZIF_VCXI_P1SS_COMM_NEXT( LR_GATE->R_CLSPR )->GET_CRFPX( ).
      if LR_BL_CRFPROP is bound.
        LS_HNDDIM = LR_BL_CRFPROP->S_HNDDIM.
      else.
        clear LS_HNDDIM.
      endif.

***   Add PC Dimension
      append value #( PCTYP     = LF_PCTYP
                      PCLEN     = LS_HNDDIM-HNDLE
                      PCLEN_UOM = LS_HNDDIM-HNDLE_UOM
                      PCWID     = LS_HNDDIM-HNDWI
                      PCWID_UOM = LS_HNDDIM-HNDWI_UOM
                      PCHEI     = LS_HNDDIM-HNDHE
                      PCHEI_UOM = LS_HNDDIM-HNDHE_UOM

                      PCUPS     = cast ZIF_VCXI_P1SS_COMM_NEXT( LR_GATE->R_CLSPR )->GET_NOUPS( )
                      NOOUT     = cast ZCL_VCXI_P1SS_PR_CSTEP_SSS( LR_GATE->R_CLSPR )->GET_NOUTT( )
                      PCDIR     = 'A'
                    ) to RT_PCDIM.

    endloop.

  endmethod.
  method GET_SPVRE_ACTVY.

    data: LR_SPVCE       type ref to /VCXI/CL_VKSC_SPVCE,
          LR_SPVRE_ACTVY type ref to /VCXI/CL_VKSR_SPVRE.

***------------------------------------------------------------------------------------------------
*** Get Customizing Element for Activity Slit/Slot/Score
    LR_SPVCE = IR_BL_RTESPV->GET_SPVCE( IF_ID           = 'ACTVY_SSS'
                                        IR_SPVCE_PARENT = IR_SPVRE_ARBPL->R_SPVCE ).

*** Create Supervisor Runtime Element
    create object LR_SPVRE_ACTVY
      exporting
        IR_SPVCE  = LR_SPVCE
        IR_PARENT = IR_SPVRE_ARBPL.
    append LR_SPVRE_ACTVY to RT_SPVRE.

*** Set In Use
    LR_SPVRE_ACTVY->SET_INUSE( IF_INUSE = ABAP_TRUE ).

***------------------------------------------------------------------------------------------------
*** Set Activities
    LR_SPVRE_ACTVY->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_PXSP_PR_ACTVY_SSS=>C_PARID_FASLO
                                                   PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR
                                                   ATWRT = ME->F_FASLO
                                                   SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).

    LR_SPVRE_ACTVY->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_PXSP_PR_ACTVY_SSS=>C_PARID_FASLI
                                                   PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR
                                                   ATWRT = ME->F_FASLI
                                                   SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).

    LR_SPVRE_ACTVY->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_PXSP_PR_ACTVY_SSS=>C_PARID_FASCO
                                                   PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR
                                                   ATWRT = ME->F_FASCO
                                                   SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).


***------------------------------------------------------------------------------------------------
*** Add PC Out
    ME->ADD_SPVRE_PCOUT( exporting IR_BL_RTESPV    = IR_BL_RTESPV
                                   IR_SPVRE_PARENT = LR_SPVRE_ACTVY
                                   IT_PCDIM        = ME->T_PCDIM_OUT
                         changing  CT_SPVRE        = RT_SPVRE ).

  endmethod.
  method GET_XML_DREF.

*** Super References
    RT_DATA = SUPER->GET_XML_DREF( ).

*** Add additional References
    append value #( NAME = 'S_PCDIM_IN'   VALUE = ref #( ME->S_PCDIM_IN  ) ) to RT_DATA.
    append value #( NAME = 'T_PCDIM_OUT'  VALUE = ref #( ME->T_PCDIM_OUT ) ) to RT_DATA.

    append value #( NAME = 'F_FASLO'  VALUE = ref #( ME->F_FASLO ) ) to RT_DATA.
    append value #( NAME = 'F_FASLI'  VALUE = ref #( ME->F_FASLI ) ) to RT_DATA.
    append value #( NAME = 'F_FASCO'  VALUE = ref #( ME->F_FASCO ) ) to RT_DATA.

    append value #( NAME = 'F_FLUTE'  VALUE = ref #( ME->F_FLUTE ) ) to RT_DATA.

  endmethod.
  method INIT_BY_CSTEP_SSS.

    data: LR_GATE         type ref to /VCXI/CL_VKSR_GATE,
          LR_PR_CSTEP_SSS type ref to ZCL_VCXI_P1SS_PR_CSTEP_SSS,
          LR_BL_CRFPROP   type ref to ZCL_VCXI_P1SS_BL_CRFPROP.


***--------------------------------------------------------------------------------------
*** Get Reference for CStep Slot/Slit/Score
    loop at IT_PRVDR into LR_GATE
                    where TABLE_LINE->R_CLSPR is instance of ZCL_VCXI_P1SS_PR_CSTEP_SSS.
      LR_PR_CSTEP_SSS ?= LR_GATE->R_CLSPR.
      exit.
    endloop.
    check LR_PR_CSTEP_SSS  is bound.


***--------------------------------------------------------------------------------------
*** Activities
    ME->F_FASLO = LR_PR_CSTEP_SSS->ZIF_VCXI_PXSS_SSS~IS_SLOT_ACTIVE( ).
    ME->F_FASLI = LR_PR_CSTEP_SSS->ZIF_VCXI_PXSS_SSS~IS_SLIT_ACTIVE( ).
    ME->F_FASCO = LR_PR_CSTEP_SSS->ZIF_VCXI_PXSS_SSS~IS_SCORE_ACTIVE( ).

*** Sheet Direction
    LR_PR_CSTEP_SSS->ZIF_VCXI_PXSS_SSS~GET_DIMEN( importing EF_SHTDI = ME->ZIF_VCXI_PXSR_PCDIR~F_PCDIR ).
    ME->ZIF_VCXI_PXSR_PCDIR~F_PCDOP = ZIF_VCXI_PXSR_PCDIR=>C_PCDOP_MANDATORY.

*** Flute based on Get Corrugated Forward Property
    LR_BL_CRFPROP = LR_PR_CSTEP_SSS->ZIF_VCXI_P1SS_COMM_NEXT~GET_CRFPX( ).
    if LR_BL_CRFPROP is bound.
      ME->F_FLUTE = LR_BL_CRFPROP->F_FLUTE.
    endif.

  endmethod.
  method INIT_BY_SPEC.

    data: LT_PCDIM type ZVCXI_P0SP_TT_PCDIM.

*** Initialize by CStep Slot/Slit/Score
    ME->INIT_BY_CSTEP_SSS( IT_PRVDR = IT_PRVDR ).

*** Get Inbound PC Dimension
    LT_PCDIM = ME->GET_PCDIM_IN( IT_PRVDR = IT_PRVDR ).
    ME->S_PCDIM_IN = value #( LT_PCDIM[ 1 ] optional ).

*** Get Outbound PC Dimension
    ME->T_PCDIM_OUT = ME->GET_PCDIM_OUT( IT_PRVDR = IT_PRVDR ).

*** Call SUPER
    SUPER->INIT_BY_SPEC( IR_GATE  = IR_GATE
                         IT_PRVDR = IT_PRVDR ).

  endmethod.
  method ZIF_VCXI_PXSR_ADJ_PCDIM~CALC_PCDIM_IN.
  endmethod.
  method ZIF_VCXI_PXSR_ADJ_PCDIM~CALC_PCDIM_OUT.

    CT_PCDIM = ME->T_PCDIM_OUT.

  endmethod.
  method ZIF_VCXI_PXSR_PCDIR~GET_PCDIM.

    RT_PCDIM = value #( ( ME->S_PCDIM_IN ) ).

  endmethod.
  method ZIF_VCXI_PXSR_PCDIR~SET_PCDIR.

    field-symbols: <S_PCDIM_OUT>  type ZVCXI_P0SP_S_PCDIM.

    move IF_PCDIR to ME->ZIF_VCXI_PXSR_PCDIR~F_PCDIR.

*** Turn PC Dimension IN
    ME->S_PCDIM_IN  = ME->TURN_PCDIM( IS_PCDIM = ME->S_PCDIM_IN
                                      IF_PCDIR = IF_PCDIR ).

*** Turn PC Dimension OUT
    loop at ME->T_PCDIM_OUT assigning <S_PCDIM_OUT>.
      <S_PCDIM_OUT> = ME->TURN_PCDIM( IS_PCDIM = <S_PCDIM_OUT>
                                      IF_PCDIR = IF_PCDIR ).
    endloop.

  endmethod.
