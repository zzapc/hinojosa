
class ZCL_VCXI_XCSP_EA_RELOCATE definition
  public
  inheriting from /VCXI/CL_VKSR_CLSEA
  create public .

public section.

  interfaces /VCXI/IF_VKCR_EXPL_BOM .
  interfaces /VCXI/IF_VKCR_EXPL_SFG .

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
protected section.

  data R_PR_RELOCATE type ref to ZCL_VCXI_XCSP_PR_RELOCATE .

  methods FILTER_CPBRG_BY_ICSPA
    changing
      !CT_CPBRG type /VCXI/CKX_TT_OBJECT
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_XCSP_EA_RELOCATE definition
class ZCL_VCXI_XCSP_EA_RELOCATE implementation.
  method /VCXI/IF_VKCR_EXPL_BOM~FILTER_CPBRG.

*** Filter BOM Groups by active Cost Split
    ME->FILTER_CPBRG_BY_ICSPA( changing CT_CPBRG = CT_CPBRG ).

  endmethod.
  method /VCXI/IF_VKCR_EXPL_BOM~GET_BOM.

    data: LS_KAUSF type ZVCXI_XCSP_S_KAUSF.

*** Material Number
    RS_EXBOM-IDNRK   = ME->R_PR_RELOCATE->ZIF_VCXI_XCSP_MFS~GET_MATNR( ).
    RS_EXBOM-IDNRK_X = ABAP_TRUE.

*** Item Category
    move 'L'                             to RS_EXBOM-POSTP.
    move ABAP_TRUE                       to RS_EXBOM-POSTP_X.

*** 1-to-1 forwarding BOM Consumption
    move IF_BMENG                        to RS_EXBOM-MENGE.
    move ABAP_TRUE                       to RS_EXBOM-MENGE_X.
    move IF_BMEIN                        to RS_EXBOM-MEINS.
    move ABAP_TRUE                       to RS_EXBOM-MEINS_X.

*** Component Scrap in Percent
    LS_KAUSF = ME->R_PR_RELOCATE->ZIF_VCXI_XCSP_MFI~GET_KAUSF( ).
    move LS_KAUSF-KAUSF                  to RS_EXBOM-AUSCH.
    move ABAP_TRUE                       to RS_EXBOM-AUSCH_X.

*** Set Special Procurement Type
    move ME->R_PR_RELOCATE->GET_SOBSL( ) to RS_EXBOM-ITSOB.
    move ABAP_TRUE                       to RS_EXBOM-ITSOB_X.

  endmethod.
  method /VCXI/IF_VKCR_EXPL_SFG~GET_DISST.

*** Return MFSO Low-Level Code
    move 0 to RF_DISST.

  endmethod.
  method /VCXI/IF_VKCR_EXPL_SFG~GET_EXQTY.

    data: LS_CONSU type ZVCXI_XCSP_S_CONSU,
          LS_KAUSF type ZVCXI_XCSP_S_KAUSF.

***----------------------------------------------------------------------------
*** Check is not Inactive via Cost Split
    check ME->R_PR_RELOCATE->ZIF_VCXI_XCSP_MFSO~IS_ACTIVE_BY_CSPLIT(  ) ne 2.

*** Consumption
    move ME->R_PR_RELOCATE->ZIF_VCXI_XCSP_MFO~GET_CONSU( ) to LS_CONSU.
    move LS_CONSU-CONSU     to RS_EXQTY-EXQTY.
    move LS_CONSU-CONSU_UOM to RS_EXQTY-EXQTY_UOM.

***----------------------------------------------------------------------------
*** Don't consider Component Scrap for Low-Level Code 0
    check ME->/VCXI/IF_VKCR_EXPL_SFG~GET_DISST( ) ne 0.

*** Component Scrap
    move ME->R_PR_RELOCATE->ZIF_VCXI_XCSP_MFSO~GET_KAUSF( ) to LS_KAUSF.

*** Calculate Explosion Quantity including Scrap
    RS_EXQTY-EXQTY = RS_EXQTY-EXQTY + ( RS_EXQTY-EXQTY * LS_KAUSF-KAUSF / 100 ).

  endmethod.
  method /VCXI/IF_VKCR_EXPL_SFG~GET_MATNR.

    data:          LR_APPDATA       type ref to /VCXI/CL_VKSR_APPDATA.

*** Get Application Data
    try.
        move ME->R_GATE->GET_APPDATA( ) ?to LR_APPDATA.
      catch CX_SY_MOVE_CAST_ERROR.
        clear LR_APPDATA.
    endtry.
    check LR_APPDATA is bound.

*** Use Configurable Material as Material Number
    move LR_APPDATA->S_CMPRK-CMATN to RF_MATNR.

  endmethod.
  method /VCXI/IF_VKCR_EXPL_SFG~GET_WERKS.

    RF_WERKS = ME->R_PR_RELOCATE->ZIF_VCXI_XCSP_MFO~GET_WERKS( ).

  endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_GATE = IR_GATE ).

*** Assign the PR Class to the Element API Class
  try.
      move ME->R_GATE->R_CLSPR ?to ME->R_PR_RELOCATE.
    catch CX_SY_MOVE_CAST_ERROR.
***   Element API ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '804'.
  endtry.

endmethod.
  method FILTER_CPBRG_BY_ICSPA.

    data:  LF_ICSPA     type        ZVCXI_XCSP_ICSPA.
    data:  LS_DCSPL     type        ZVCXI_XCSP_S_DCSPL.

***--------------------------------------------------------------------------------------
*** Get Cost Split
    ME->R_PR_RELOCATE->ZIF_VCXI_XCSP_MFI~GET_CSPLIT( importing EF_ICSPA = LF_ICSPA
                                                               ES_DCSPL = LS_DCSPL ).
    check LF_ICSPA is not initial.

***--------------------------------------------------------------------------------------
*** Do not exploded if Consumption is not Costing Relevant
    if LS_DCSPL-DCSPL le 0.
      free: CT_CPBRG.
    endif.

  endmethod.
