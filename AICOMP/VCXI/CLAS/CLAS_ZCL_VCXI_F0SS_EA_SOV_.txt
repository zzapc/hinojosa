
class ZCL_VCXI_F0SS_EA_SOV definition
  public
  inheriting from /VCXI/CL_VKSR_CLSEA
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods GET_XCONO
    importing
      !IR_SVCEL_XCONI type ref to /VCXI/CL_VKSRA_SVC_ELEMENT
    returning
      value(RR_SVCEL_XCONO) type ref to /VCXI/CL_VKSRA_SVC_ELEMENT
    raising
      /VCXI/CX_CKX .
  methods GET_XCONE
    returning
      value(RT_XCONE) type ZVCXI_F0SS_TT_XCONE
    raising
      /VCXI/CX_CKX .
protected section.

  data R_PR_SOV type ref to ZCL_VCXI_F0SS_PR_SOV .
private section.
endclass. "ZCL_VCXI_F0SS_EA_SOV definition
class ZCL_VCXI_F0SS_EA_SOV implementation.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_GATE = IR_GATE ).

*** Assign the PR Class to the Element API Class
    try.
        move ME->R_GATE->R_CLSPR ?to ME->R_PR_SOV.
      catch CX_SY_MOVE_CAST_ERROR.

***     Element API ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '804'.
    endtry.

  endmethod.
  method GET_XCONE.

    data: LS_XCONR  type  ZVCXI_F0SS_S_XCONR.
    data: LT_XCONR  type  ZVCXI_F0SS_TT_XCONR.


*** Get X-Connections (Gate References)
    check ME->R_PR_SOV is bound.
    LT_XCONR = ME->R_PR_SOV->GET_XCONR( ).

*** Get SVC API Element by Gate Key
    loop at LT_XCONR into LS_XCONR.
      append value #( R_SVCEL_IN  = cast #( /VCXI/CL_VKSRA_SVC_ELEMENT=>/VCXI/IF_CKX_OBJECT~GET_INSTANCE( IF_GUID = conv /VCXI/CKX_GUID( LS_XCONR-R_GATE_IN->F_GATKY  ) ) )
                      R_SVCEL_OUT = cast #( /VCXI/CL_VKSRA_SVC_ELEMENT=>/VCXI/IF_CKX_OBJECT~GET_INSTANCE( IF_GUID = conv /VCXI/CKX_GUID( LS_XCONR-R_GATE_OUT->F_GATKY ) ) )
                    ) to RT_XCONE.
    endloop.

  endmethod.
  method GET_XCONO.

    data: LF_GATKY type /VCXI/VKSR_GATKY,
          LF_GUID  type /VCXI/CKX_GUID.
    data: LS_XCONR type ZVCXI_F0SS_S_XCONR.
    data: LT_XCONR type ZVCXI_F0SS_TT_XCONR.


    check IR_SVCEL_XCONI is bound.
    move IR_SVCEL_XCONI->/VCXI/IF_CKX_OBJECT~GET_GUID( ) to LF_GATKY.

*** Get X-Connections
    check ME->R_PR_SOV is bound.
    LT_XCONR = ME->R_PR_SOV->GET_XCONR( ).

*** Find Connection by Gate Key
    loop at LT_XCONR into LS_XCONR where R_GATE_IN->F_GATKY = LF_GATKY.
      exit.
    endloop.
    check LS_XCONR-R_GATE_OUT is bound.

*** Get SVC API Element by Gate Key
    move LS_XCONR-R_GATE_OUT->F_GATKY to LF_GUID.
    RR_SVCEL_XCONO ?= /VCXI/CL_VKSRA_SVC_ELEMENT=>/VCXI/IF_CKX_OBJECT~GET_INSTANCE( IF_GUID = LF_GUID ).

  endmethod.
