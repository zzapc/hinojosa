
class ZCL_VCXI_P5E_MM_MMEXT definition
  public
  inheriting from ZCL_VCXI_PXE_MM_MMEXT
  create public .

public section.

  data S_MMEXT type ZVCXI_P5E_MM_S_MMEXT .
  constants C_CNTXT_P5 type ZVCXI_XCE_MM_CNTXT value 'P5' ##NO_TEXT.

  methods SET_MMEXT
    importing
      !IS_MMEXT type ZVCXI_P5E_MM_S_MMEXT
    raising
      /VCXI/CX_CKX .

  methods SAVE
    redefinition .
  methods SET_EXTVAL
    redefinition .
  methods UPDATE_BY_FG
    redefinition .
  methods COPY
    redefinition .
protected section.

  methods LOAD_FROM_DB
    redefinition .
private section.
endclass. "ZCL_VCXI_P5E_MM_MMEXT definition
class ZCL_VCXI_P5E_MM_MMEXT implementation.
  method COPY.

    RR_MMEXT = SUPER->COPY( IF_MATNR = IF_MATNR
                            IF_WERKS = IF_WERKS ).

    check RR_MMEXT is bound.

*** Copy Values
    cast ZCL_VCXI_P5E_MM_MMEXT( RR_MMEXT )->S_MMEXT = ME->S_MMEXT.

  endmethod.
  method LOAD_FROM_DB.

    data: LS_TD100 type ZVCXI_P5E_TD100.


***----------------------------------------------------------------------------
*** Load from SUPER
    SUPER->LOAD_FROM_DB( ).

***----------------------------------------------------------------------------
*** Load Material Master Extension
    select single *
           into LS_TD100
           from ZVCXI_P5E_TD100
          where MATNR eq ME->F_MATNR
           and  WERKS eq ME->F_WERKS.
    if SY-SUBRC eq 0.
***   Load current values
      ME->S_MMEXT = LS_TD100-S_MMEXT.
    endif.

  endmethod.
  method SAVE.

    data: LS_TD100 type ZVCXI_P5E_TD100.


*** Save SUPER
    SUPER->SAVE( ).

***----------------------------------------------------------------------------
*** Save Material Master Extension (Header)
    LS_TD100 = value #( MATNR   = ME->F_MATNR
                        WERKS   = ME->F_WERKS
                        S_MMEXT = ME->S_MMEXT ).
    modify ZVCXI_P5E_TD100 from LS_TD100.

  endmethod.
  method SET_EXTVAL.

    data: LS_EXTVAL type ZVCXI_P5E_MM_S_EXTVAL.


    SUPER->SET_EXTVAL( IS_EXTVAL = IS_EXTVAL ).

***----------------------------------------------------------------------------
*** "Cast" Extension Values
    LS_EXTVAL = corresponding #( IS_EXTVAL ).

*** Extension Header
    ME->SET_MMEXT( IS_MMEXT = LS_EXTVAL-S_MMEXT ).

  endmethod.
  method SET_MMEXT.

    check ME->S_MMEXT ne IS_MMEXT.
    ME->S_MMEXT = IS_MMEXT.
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method UPDATE_BY_FG.

    data: LR_EA_RLFG    type ref to ZCL_VCXI_P5SS_EA_RLFG,
          LR_BL_RLFPROP type ref to ZCL_VCXI_P5SS_BL_RLFPROP.


***----------------------------------------------------------------------------
*** Get Element API Reference
    try.
        LR_EA_RLFG ?= IR_SVCEL->GET_CLSEA( ).
      catch CX_SY_MOVE_CAST_ERROR into data(LR_CX_SY_MOVE_CAST_ERROR).
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_SY_MOVE_CAST_ERROR ).
    endtry.
    check LR_EA_RLFG is bound.

*** Get Roll Forward Property XML (Outbound)
    LR_BL_RLFPROP = LR_EA_RLFG->GET_RLFPX( ).


***----------------------------------------------------------------------------
*** Set Extension Values
    ME->SET_EXTVAL( IS_EXTVAL = value ZVCXI_P5E_MM_S_EXTVAL( CCBGN       = LR_BL_RLFPROP->F_CCBGN
                                                             S_GRAMM     = LR_BL_RLFPROP->S_GRAMM
                                                             T_PRINT_COM = LR_BL_RLFPROP->T_PRINT ) ).

  endmethod.
