
class ZCL_VCXI_XCSU_SHLP_LIMIT_DIMEN definition
  public
  create public .

public section.

  type-pools ABAP .
  class-methods CALL_SHLP
    importing
      !IF_SHUTY type ZVCXI_XCSU_SHUTY optional
      !IF_SHUNT type ZVCXI_XCSU_SHUNT optional
      !IF_DISPLAY type ABAP_BOOL default ABAP_FALSE
    exporting
      !EF_LIMIT type ZVCXI_XCSU_LIMIT
      !EF_DIMEN type ZVCXI_XCSU_DIMEN
      !EF_DIMVA_UOM_M type UNIT
      !EF_DIMVA_UOM_I type UNIT
    raising
      /VCXI/CX_CKX .
protected section.
private section.
endclass. "ZCL_VCXI_XCSU_SHLP_LIMIT_DIMEN definition
class ZCL_VCXI_XCSU_SHLP_LIMIT_DIMEN implementation.
method CALL_SHLP.

  data: LR_CUST_SHIPU type ref to            ZCL_VCXI_XCSU_CUST_SHIPU.
  data: LT_LIMIT      type                   ZVCXI_XCSU_TT_SHUTY_LIMIT,
        LT_RETURN     type table of          DDSHRETVAL,
        LT_MAP        type standard table of DSELC.
  data: LS_RETURN     type                   DDSHRETVAL,
        LS_MAP        type                   DSELC,
        LS_LIMIT      type                   ZVCXI_XCS_TC172.

  LR_CUST_SHIPU = ZCL_VCXI_XCSU_CUST_SHIPU=>GET_INSTANCE( ).

  check LR_CUST_SHIPU is bound.

  if IF_SHUNT is supplied.
    LT_LIMIT = LR_CUST_SHIPU->GET_LIMIT_FOR_SHUNT( IF_SHUNT = IF_SHUNT ).
  elseif IF_SHUTY is supplied.
    LT_LIMIT = LR_CUST_SHIPU->GET_LIMIT_FOR_SHUTY( IF_SHUTY = IF_SHUTY ).
  endif.

  LS_MAP-FLDNAME   = 'F0003'. "Field for Position 1
  LS_MAP-DYFLDNAME = 'LIMIT'.
  append LS_MAP to LT_MAP.

  LS_MAP-FLDNAME   = 'F0004'. "Field for Position 2
  LS_MAP-DYFLDNAME = 'DIMEN'.
  append LS_MAP to LT_MAP.

  call function 'F4IF_INT_TABLE_VALUE_REQUEST'
    exporting
      RETFIELD        = 'LIMIT'
      VALUE_ORG       = 'S'
      DISPLAY         = IF_DISPLAY
    tables
      VALUE_TAB       = LT_LIMIT
      DYNPFLD_MAPPING = LT_MAP
      RETURN_TAB      = LT_RETURN
    exceptions
      PARAMETER_ERROR = 1
      NO_VALUES_FOUND = 2
      others          = 3.

  if SY-SUBRC eq 0.
    loop at LT_RETURN into LS_RETURN.
      if LS_RETURN-FIELDNAME = 'F0003'.
        EF_LIMIT = LS_RETURN-FIELDVAL.
      elseif LS_RETURN-FIELDNAME = 'F0004'.
        EF_DIMEN = LS_RETURN-FIELDVAL.
      endif.
    endloop.

    read table LT_LIMIT into LS_LIMIT with key LIMIT = EF_LIMIT
                                               DIMEN = EF_DIMEN.
    check SY-SUBRC = 0.
    move LS_LIMIT-UOMME to EF_DIMVA_UOM_M.
    move LS_LIMIT-UOMIM to EF_DIMVA_UOM_I.
  endif.

endmethod.
endclass. "ZCL_VCXI_XCSU_SHLP_LIMIT_DIMEN implementation
