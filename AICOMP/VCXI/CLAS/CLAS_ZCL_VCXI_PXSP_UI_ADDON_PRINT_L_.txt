
class ZCL_VCXI_PXSP_UI_ADDON_PRINT_L definition
  public
  inheriting from ZCL_VCXI_PXSP_UI_ADDON_PRINT
  create public .

public section.

  data S_ADDON_PRINT_L_VIS type ZVCXI_PXSP_S_ADDON_PRINT_L_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
protected section.

  data R_PR_ADDON_PRINT_L type ref to ZCL_VCXI_PXSP_PR_ADDON_PRINT_L .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_PXSP_UI_ADDON_PRINT_L definition
class ZCL_VCXI_PXSP_UI_ADDON_PRINT_L implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_PXSP_UI_ADDON_PR_L_BIND'
      exporting
        IR_UI_ADDON_PRINT_L = ME
      importing
        ES_DYNP             = ME->S_DYNP.

  endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                      IR_UIGATE = IR_UIGATE
                      IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
  try.
      move IR_GATE->R_CLSPR ?to ME->R_PR_ADDON_PRINT_L.
    catch CX_SY_MOVE_CAST_ERROR.
***   UI ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '801'.
  endtry.

endmethod.
  method DO_UPDATE_VIS.

    data: LS_ADDON_PRINT_L_VIS type ZVCXI_PXSP_S_ADDON_PRINT_L_VIS.


*** Super
    SUPER->DO_UPDATE_VIS( IS_VIS = IS_VIS ).
    check ME->R_PR_ADDON_PRINT_L is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_ADDON_PRINT_L_VIS.

***--------------------------------------------------------------------------------------
*** Set Printable Width
    if ME->S_ADDON_PRINT_L_VIS-S_PRWID ne LS_ADDON_PRINT_L_VIS-S_PRWID.
      ME->R_PR_ADDON_PRINT_L->SET_PRWID( IS_PRWID = LS_ADDON_PRINT_L_VIS-S_PRWID
                                         IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_ADDON_PRINT_L.

  endmethod.
  method INIT_VIS.


    SUPER->INIT_VIS( ).

    free: ME->S_ADDON_PRINT_L_VIS.
    ME->S_ADDON_PRINT_L_VIS-S_ADDON_PRINT_VIS = ME->S_ADDON_PRINT_VIS.


***----------------------------------------------------------------------------
*** Get Printable Width
    ME->S_ADDON_PRINT_L_VIS-S_PRWID = cast ZCL_VCXI_PXSP_PR_ADDON_PRINT_L( ME->R_PR_ADDON_PRINT )->GET_PRWID( ).

  endmethod.
