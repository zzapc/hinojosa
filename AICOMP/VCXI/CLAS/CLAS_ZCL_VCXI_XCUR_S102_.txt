
class ZCL_VCXI_XCUR_S102 definition
  public
  inheriting from /VCXI/CL_CKMR_CLSTP
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_RPSEG type ref to /VCXI/CL_CKMR_RPSEG
    raising
      /VCXI/CX_CKX .

  methods EXECUTE
    redefinition .
protected section.

  data R_DCO_INPUT type ref to ZCL_VCXI_XCUR_DCO_MUROB .
  data R_DCO_OUTPUT type ref to ZCL_VCXI_XCUR_DCO_MUROB .
private section.
endclass. "ZCL_VCXI_XCUR_S102 definition
class ZCL_VCXI_XCUR_S102 implementation.
  method CONSTRUCTOR.

*** Super Constructor
    SUPER->CONSTRUCTOR( IR_RPSEG = IR_RPSEG ).

*** Map DCO to Required Input/Output
    ME->R_RPSEG->MAP_DCO( changing CR_DCO = ME->R_DCO_INPUT ).
    ME->R_RPSEG->MAP_DCO( changing CR_DCO = ME->R_DCO_OUTPUT ).

  endmethod.
  method EXECUTE.

    data: LR_BOB_SDOC type ref to ZCL_VCXI_XCI_BOB_SDOC,
          LR_MSG      type ref to /VCXI/CL_CKXM_MESSAGE.


    try.
***     Save previous Information before continuing
        ME->R_RPSEG->SAVE_AND_COMMIT( ).

***     Create Business Object for Sales Document
        create object LR_BOB_SDOC.
        LR_BOB_SDOC->INIT_FROM_DB( IF_VBELN = ME->R_DCO_INPUT->S_MUROB-VBELN ).

***------------------------------------------------------------------------------------------------
***     Set Integration Data to Buffer for Profile Run
        call function 'ZVCXI_XCC_IDAT_SET_NIDAT'
          exporting
            IF_POSNR = ME->R_DCO_INPUT->S_MUROB-POSNR
            IF_NIDAT = conv ZVCXI_XCI_NIDAT( ME->R_DCO_INPUT->R_IDAT_SVC->/VCXI/IF_CKX_OBJECT~GET_GUID( ) ).

***     Update Flag for Configuration
        append value #( POSNR = ME->R_DCO_INPUT->S_MUROB-POSNR
                        UPDKZ = 'U'
                      ) to LR_BOB_SDOC->T_CFGS_UPDKZ.

***     Save Sales Document
        LR_MSG = LR_BOB_SDOC->SAVE( IF_TESTRUN = ABAP_FALSE ).

***     Initialize Integration Data Buffer for Profile Run
        call function 'ZVCXI_XCC_IDAT_INIT_NIDAT'.

***------------------------------------------------------------------------------------------------
***     Check Status based on Messages
        if not LINE_EXISTS( LR_MSG->T_MSG[ MSGTY = 'E' ] ) and
           not LINE_EXISTS( LR_MSG->T_MSG[ MSGTY = 'A' ] ).
***       Set Status to Complete
          ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_COMPLETED ).
        else.
***       Set Status to Error
          ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_ERROR ).
        endif.

***     Add Messages to Message Log
        ME->R_RPSEG->R_MSG->MERGE( changing CR_MESSAGE = LR_MSG ).

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
***     Set Status to Error
        ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_ERROR ).

***     Add Message to Message Log
        ME->R_RPSEG->R_MSG->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
endclass. "ZCL_VCXI_XCUR_S102 implementation
