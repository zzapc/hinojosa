
class ZCL_VCXI_P0SP_UI_ACTVY_UNIT definition
  public
  inheriting from /VCXI/CL_VKSRU_CLSUI
  create public .

public section.

  data S_ACTVY_UNIT_VIS type ZVCXI_P0SP_S_ACTVY_UNIT_VIS .
  data F_CURSOR type SCRFNAME .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_ACTVY_UNIT_VIS
    importing
      !IS_ACTVY_UNIT_VIS type ZVCXI_P0SP_S_ACTVY_UNIT_VIS
    raising
      /VCXI/CX_CKX .
  methods SET_CURSOR
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  data R_PR_ACTVY_UNIT type ref to ZCL_VCXI_P0SP_PR_ACTVY_UNIT .
  data R_GRID_MC_PACKMAT type ref to ZCL_VCXI_XCSP_GRID_MC_PACKMAT .

  methods INIT_VIS
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
private section.
endclass. "ZCL_VCXI_P0SP_UI_ACTVY_UNIT definition
class ZCL_VCXI_P0SP_UI_ACTVY_UNIT implementation.
method ACTIVATE.

  SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
  call function 'ZVCXI_P0SP_UI_ACTVY_UNIT_BIND'
    exporting
      IR_UI_ACTVY_UNIT = ME
    importing
      ES_DYNP          = ME->S_DYNP.

*** Create Grid for GRID_MC_Packaging Material
  create object ME->R_GRID_MC_PACKMAT
    exporting
      IR_GATE           = ME->R_GATE
      IR_UIGATE         = ME->R_UIGATE
      IF_CONTAINER_NAME = '0100CC_GRID_MC_PACKMAT'.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                      IR_UIGATE = IR_UIGATE
                      IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
  try.
      move IR_GATE->R_CLSPR ?to ME->R_PR_ACTVY_UNIT.
    catch CX_SY_MOVE_CAST_ERROR.
***   UI ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '801'.
  endtry.

endmethod.
method DEACTIVATE.

  SUPER->DEACTIVATE( ).

  if ME->R_GRID_MC_PACKMAT is bound.
    ME->R_GRID_MC_PACKMAT->DO_FREE( ).
    clear ME->R_GRID_MC_PACKMAT.
  endif.

endmethod.
method DO_MODIFY_SCREEN.

  data:  LF_LOCKED type        /VCXI/CKX_LOCKED,
         LF_INUSE  type        /VCXI/VKSR_INUSE.
  data:  LR_CX_CKX type ref to /VCXI/CX_CKX.

  SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                           IF_DYNNR = IF_DYNNR ).

  try.

      if IF_REPID eq ME->S_DYNP-REPID and
         IF_DYNNR eq ME->S_DYNP-DYNNR.

        LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
        LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***     Modify Scree using DynamicVC
        ZCL_VCXI_XCDR_SERVICE=>MODIFY_SCREEN( IR_GATE = ME->R_GATE ).

        loop at screen.
          if LF_LOCKED ne ABAP_TRUE or
             LF_INUSE  ne ABAP_TRUE.
            move 0 to SCREEN-INPUT.
          endif.
          modify screen.
        endloop.
      endif.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

endmethod.
method DO_PBO.

  ME->INIT_VIS( ).

  if ME->R_GRID_MC_PACKMAT is bound.
    if ME->R_GRID_MC_PACKMAT->F_REFRESH_REQ eq ABAP_FALSE.
      move ABAP_TRUE to ME->R_GRID_MC_PACKMAT->F_REFRESH_REQ.
    endif.
    ME->R_GRID_MC_PACKMAT->REFRESH_GRID( ).
  endif.

  set cursor field ME->F_CURSOR.
  clear ME->F_CURSOR.

endmethod.
method DO_UPDATE_ACTVY_UNIT_VIS.

  data:  LS_PCLEN   type          ZVCXI_P0SP_S_PCLEN,
         LS_PCWID   type          ZVCXI_P0SP_S_PCWID,
         LS_PCHEI   type          ZVCXI_P0SP_S_PCHEI.

*** Set PC Length and Width
  if ME->S_ACTVY_UNIT_VIS-PCLEN ne IS_ACTVY_UNIT_VIS-PCLEN.
    move-corresponding IS_ACTVY_UNIT_VIS to LS_PCLEN.
    ME->R_PR_ACTVY_UNIT->SET_PCLEN( IS_PCLEN = LS_PCLEN
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

  if ME->S_ACTVY_UNIT_VIS-PCWID ne IS_ACTVY_UNIT_VIS-PCWID.
    move-corresponding IS_ACTVY_UNIT_VIS to LS_PCWID.
    ME->R_PR_ACTVY_UNIT->SET_PCWID( IS_PCWID = LS_PCWID
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

  if ME->S_ACTVY_UNIT_VIS-PCHEI ne IS_ACTVY_UNIT_VIS-PCHEI.
    move-corresponding IS_ACTVY_UNIT_VIS to LS_PCHEI.
    ME->R_PR_ACTVY_UNIT->SET_PCHEI( IS_PCHEI = LS_PCHEI
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Set PC Ups
  if ME->S_ACTVY_UNIT_VIS-PCUPS ne IS_ACTVY_UNIT_VIS-PCUPS.
    ME->R_PR_ACTVY_UNIT->SET_PCUPS( IF_PCUPS = IS_ACTVY_UNIT_VIS-PCUPS
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Set PC Type
  if ME->S_ACTVY_UNIT_VIS-PCTYP ne IS_ACTVY_UNIT_VIS-PCTYP.
    ME->R_PR_ACTVY_UNIT->SET_PCTYP( IF_PCTYP = IS_ACTVY_UNIT_VIS-PCTYP
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER  ).
  endif.

endmethod.
method DO_USER_COMMAND.

  SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

  if ME->R_GRID_MC_PACKMAT is bound.
    ME->R_GRID_MC_PACKMAT->CHECK_CHANGED_DATA( ).
  endif.

endmethod.
method HANDLE_GATE_FREED.

  SUPER->HANDLE_GATE_FREED( ).

  free: ME->R_PR_ACTVY_UNIT.

endmethod.
method INIT_VIS.

  data:          LS_PCLEN             type          ZVCXI_P0SP_S_PCLEN,
                 LS_PCWID             type          ZVCXI_P0SP_S_PCWID,
                 LS_PCHEI             type          ZVCXI_P0SP_S_PCHEI.

  free: ME->S_ACTVY_UNIT_VIS.

***----------------------------------------------------------------------------
*** Get PC Length
  move ME->R_PR_ACTVY_UNIT->GET_PCLEN( )       to LS_PCLEN.
  move-corresponding LS_PCLEN                  to ME->S_ACTVY_UNIT_VIS.

*** Get PC Width
  move ME->R_PR_ACTVY_UNIT->GET_PCWID( )       to LS_PCWID.
  move-corresponding LS_PCWID                  to ME->S_ACTVY_UNIT_VIS.

*** Get PC Height
  move ME->R_PR_ACTVY_UNIT->GET_PCHEI( )       to LS_PCHEI.
  move-corresponding LS_PCHEI                  to ME->S_ACTVY_UNIT_VIS.

*** Get PC Ups
  move ME->R_PR_ACTVY_UNIT->GET_PCUPS( )       to ME->S_ACTVY_UNIT_VIS-PCUPS.

*** Get PC Type and Description
  move ME->R_PR_ACTVY_UNIT->GET_PCTYP( )       to ME->S_ACTVY_UNIT_VIS-PCTYP.
  move ME->R_PR_ACTVY_UNIT->GET_PCTYP_DESCR( ) to ME->S_ACTVY_UNIT_VIS-PCTYP_DESCR.

endmethod.
method SET_CURSOR.

  data:  LF_CURSOR type SCRFNAME.

  get cursor field LF_CURSOR.
  if LF_CURSOR is not initial.
    move LF_CURSOR to ME->F_CURSOR.
  endif.

endmethod.
