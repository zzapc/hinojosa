
class ZCL_VCXI_FBSS_BL_BGBPROP definition
  public
  create protected .

public section.

  data S_FODIM type ZVCXI_FBSS_S_FODIM read-only .
  data S_GUDIM type ZVCXI_FBSS_S_GUDIM read-only .
  data S_OLDIM type ZVCXI_FBSS_S_OLDIM read-only .
  data S_WIDIM type ZVCXI_FBSS_S_WIDIM read-only .
  data S_WNDIM type ZVCXI_FBSS_S_WNDIM read-only .

  class-methods FACTORY
    importing
      !IF_BGBPX type ZVCXI_FBS_BGBPX optional
    returning
      value(RR_BL_BGBPROP) type ref to ZCL_VCXI_FBSS_BL_BGBPROP
    raising
      /VCXI/CX_CKX .
  methods GET_BGBPX
    returning
      value(RF_BGBPX) type ZVCXI_FBS_BGBPX
    raising
      /VCXI/CX_CKX .
  methods MERGE
    importing
      !IR_BL_BGBPROP type ref to ZCL_VCXI_FBSS_BL_BGBPROP
    raising
      /VCXI/CX_CKX .
  methods SET_BGBPX
    importing
      !IF_BGBPX type ZVCXI_FBS_BGBPX
    raising
      /VCXI/CX_CKX .
  methods SET_FODIM
    importing
      !IS_FODIM type ZVCXI_FBSS_S_FODIM
    raising
      /VCXI/CX_CKX .
  methods SET_GUDIM
    importing
      !IS_GUDIM type ZVCXI_FBSS_S_GUDIM
    raising
      /VCXI/CX_CKX .
  methods SET_OLDIM
    importing
      !IS_OLDIM type ZVCXI_FBSS_S_OLDIM
    raising
      /VCXI/CX_CKX .
  methods SET_WIDIM
    importing
      !IS_WIDIM type ZVCXI_FBSS_S_WIDIM
    raising
      /VCXI/CX_CKX .
  methods SET_WNDIM
    importing
      !IS_WNDIM type ZVCXI_FBSS_S_WNDIM
    raising
      /VCXI/CX_CKX .
protected section.

  methods GET_XML_DREF
    returning
      value(RT_DATA) type ABAP_TRANS_SRCBIND_TAB
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_FBSS_BL_BGBPROP definition
class ZCL_VCXI_FBSS_BL_BGBPROP implementation.
  method FACTORY.

*** Create BL Reference
enhancement-section FACTORY spots ZVCXI_FBSS_ES_BL_BGBPROP .
    create object RR_BL_BGBPROP.
end-enhancement-section.

*** Set Bag Backward Property XML
    RR_BL_BGBPROP->SET_BGBPX( IF_BGBPX = IF_BGBPX ).

  endmethod.
  method GET_BGBPX.

    data: LT_DATA type ABAP_TRANS_SRCBIND_TAB.

***--------------------------------------------------------------------------------------
*** Get Data References for XML Serialize/Deserialize
    LT_DATA = ME->GET_XML_DREF( ).

*** Serialize to XML
    call transformation ID
         source (LT_DATA)
         result xml RF_BGBPX.

  endmethod.
  method GET_XML_DREF.

    RT_DATA = value #( ( NAME = 'S_OLDIM'  VALUE = ref #( ME->S_OLDIM ) )
                       ( NAME = 'S_GUDIM'  VALUE = ref #( ME->S_GUDIM ) )
                       ( NAME = 'S_FODIM'  VALUE = ref #( ME->S_FODIM ) )
                       ( NAME = 'S_WIDIM'  VALUE = ref #( ME->S_WIDIM ) )
                       ( NAME = 'S_WNDIM'  VALUE = ref #( ME->S_WNDIM ) )
                     ).

  endmethod.
  method MERGE.

*** Overlap Dimension
    ME->SET_OLDIM( IS_OLDIM = IR_BL_BGBPROP->S_OLDIM ).

*** Gusset Dimension
    ME->SET_GUDIM( IS_GUDIM = IR_BL_BGBPROP->S_GUDIM ).

*** Fold Dimension
    ME->SET_FODIM( IS_FODIM = IR_BL_BGBPROP->S_FODIM ).

*** Wicket Dimension
    ME->SET_WIDIM( IS_WIDIM = IR_BL_BGBPROP->S_WIDIM ).

*** Winding Dimension
    ME->SET_WNDIM( IS_WNDIM = IR_BL_BGBPROP->S_WNDIM ).

  endmethod.
  method SET_BGBPX.

    data: LT_DATA type ABAP_TRANS_SRCBIND_TAB.


***--------------------------------------------------------------------------------------
*** Deserialize XML
    if IF_BGBPX is not initial.

***   Get Data References for XML Serialize/Deserialize
      LT_DATA = ME->GET_XML_DREF( ).

***   Open XML
      call transformation ID
           source xml IF_BGBPX
           result (LT_DATA).

    endif.

  endmethod.
  method SET_FODIM.

    move IS_FODIM to ME->S_FODIM.

  endmethod.
  method SET_GUDIM.

    move IS_GUDIM to ME->S_GUDIM.

  endmethod.
  method SET_OLDIM.

    move IS_OLDIM to ME->S_OLDIM.

  endmethod.
  method SET_WIDIM.

    move IS_WIDIM to ME->S_WIDIM.

  endmethod.
  method SET_WNDIM.

    move IS_WNDIM to ME->S_WNDIM.

  endmethod.
