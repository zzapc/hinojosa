
class ZCL_VCXI_XCUR_WUI_MUR definition
  public
  inheriting from /VCXI/CL_CKMRU_CLWUI
  final
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_RECORD type ref to /VCXI/CL_CKMR_RECORD
    raising
      /VCXI/CX_CKX .
  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods GET_DYNP_SMDCNTRL
    returning
      value(RS_DYNP_KEY) type /VCXI/CKX_S_DYNP_KEY
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
protected section.

  data R_CONTAINER_MURGR type ref to CL_GUI_CUSTOM_CONTAINER .
  data R_SALV_MURGR type ref to CL_SALV_TABLE .
  data T_MURGR_GRID type ZVCXI_XCUR_TT_MURGR_GRID .
  data R_UI_SMDCNTRL type ref to ZCL_VCXI_XCUR_UI_SMDCNTRL .
  data R_DCO type ref to ZCL_VCXI_XCUR_DCO_MURDE .

  methods INIT_SALV_MURGR
    raising
      /VCXI/CX_CKX .
  methods INIT_SALV_MURGR_DATA
    raising
      /VCXI/CX_CKX .
  methods HANDLE_DOUBLE_CLICK
    for event DOUBLE_CLICK of CL_SALV_EVENTS_TABLE
    importing
      !ROW
      !COLUMN .
  methods INIT_UI_SMDCNTRL
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_XCUR_WUI_MUR definition
class ZCL_VCXI_XCUR_WUI_MUR implementation.
  method ACTIVATE.

*** Bind UI
    call function 'ZVCXI_XCUR_WUI_MUR_BIND'
      exporting
        IR_WUI_MUR = ME
      importing
        ES_DYNP    = ME->S_DYNP.

  endmethod.
  method CONSTRUCTOR.

*** Super Constructor
    SUPER->CONSTRUCTOR( IR_RECORD = IR_RECORD ).

*** Map DCO
    ME->R_RECORD->MAP_DCO( changing CR_DCO = ME->R_DCO ).

*** Initialize UI Instance for SmartMD Control Settings
    ME->INIT_UI_SMDCNTRL( ).

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

*** Free Container
    if ME->R_CONTAINER_MURGR is bound.
      ME->R_CONTAINER_MURGR->FREE( exceptions others = 0 ).
      clear ME->R_CONTAINER_MURGR.
    endif.

  endmethod.
  method DO_PBO.

**** Set PF-Status
*    ME->SET_PF_STATUS( ).
*
**** Initialize Visualization
*    ME->INIT_VIS( ).

*** Initialize/Refresh ALV Data
    ME->INIT_SALV_MURGR_DATA( ).

*** Initialize/Refresh ALV Grid
    ME->INIT_SALV_MURGR( ).

**** Initialize UI Instance for SmartMD Control Settings
*    ME->INIT_UI_SMDCNTRL( ).

  endmethod.
  method GET_DYNP_SMDCNTRL.

*** Get Subscreen via SmartMD Control Instance
    if ME->R_UI_SMDCNTRL is bound.
      RS_DYNP_KEY = ME->R_UI_SMDCNTRL->GET_DYNP( ).
    endif.

    if RS_DYNP_KEY is initial.
***   Fallback to Emtpy Subscreen
      RS_DYNP_KEY = value #( REPID = 'SAPLZVCXI_XCUR_WUI_MUR'
                             DYNNR = '9999' ).
    endif.

  endmethod.
  method HANDLE_DOUBLE_CLICK.

    data: LS_MURGR type ZVCXI_XCUR_S_MURGR.


    try.
***     Get select Update Group
        data(LT_MURGR) = ME->R_DCO->GET_MURGR( ).
        check LINE_EXISTS( LT_MURGR[ ROW ] ).
        LS_MURGR = LT_MURGR[ ROW ].

        case COLUMN.
          when 'NOOBJ'.
***         Call Popup for Objects
            ZCL_VCXI_XCUR_POPUP_MUROB=>START( exporting IF_LOCKED = ABAP_FALSE
                                              changing  CT_MUROB  = LS_MURGR-T_MUROB ).

          when others.
***         Call Popup for Update Group
            ZCL_VCXI_XCUR_POPUP_MURGR=>START( IS_MURGR  = LS_MURGR
                                              IF_LOCKED = ABAP_FALSE ).

        endcase.

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        LR_CX_CKX->SHOW_MESSAGE( ).
    endtry.

*** Refresh ALV - Set New Ok Code to force PBO
    CL_GUI_CFW=>SET_NEW_OK_CODE( NEW_CODE = 'REFRESH' ).

  endmethod.
  method INIT_SALV_MURGR.

***--------------------------------------------------------------------------------------
*** Initialize Container
    if ME->R_CONTAINER_MURGR is not bound.
      create object ME->R_CONTAINER_MURGR
        exporting
          CONTAINER_NAME              = '0100CC_MURGR'
          LIFETIME                    = CL_GUI_CUSTOM_CONTAINER=>LIFETIME_DYNPRO
        exceptions
          CNTL_ERROR                  = 1
          CNTL_SYSTEM_ERROR           = 2
          CREATE_ERROR                = 3
          LIFETIME_ERROR              = 4
          LIFETIME_DYNPRO_DYNPRO_LINK = 5
          others                      = 6.
      if SY-SUBRC ne 0.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
      endif.
    endif.

***--------------------------------------------------------------------------------------
*** Initialize SALV
    try.

        if ME->R_SALV_MURGR is not bound.
          CL_SALV_TABLE=>FACTORY( exporting R_CONTAINER  = ME->R_CONTAINER_MURGR
                                  importing R_SALV_TABLE = ME->R_SALV_MURGR
                                  changing  T_TABLE      = ME->T_MURGR_GRID ).

          ME->R_SALV_MURGR->GET_COLUMNS( )->SET_OPTIMIZE( ABAP_TRUE ).

          ME->R_SALV_MURGR->GET_SELECTIONS( )->SET_SELECTION_MODE( IF_SALV_C_SELECTION_MODE=>ROW_COLUMN ).

          set handler ME->HANDLE_DOUBLE_CLICK   for ME->R_SALV_MURGR->GET_EVENT( ).

***       Display Simple ALV
          ME->R_SALV_MURGR->DISPLAY( ).

        else.
***       Refresh Grid Content
          ME->R_SALV_MURGR->REFRESH( ).
        endif.

      catch CX_SALV_ERROR into data(LR_CX_SALV_ERROR).
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_SALV_ERROR ).
    endtry.

  endmethod.
  method INIT_SALV_MURGR_DATA.

    data: LF_DESCR type /VCXI/CKX_DESCR.
    field-symbols: <S_MURGR>      type ZVCXI_XCUR_S_MURGR,
                   <S_MURGR_GRID> type ZVCXI_XCUR_S_MURGR_GRID,
                   <S_MUREL>      type ZVCXI_XCUR_S_MUREL.


    refresh ME->T_MURGR_GRID.
    check ME->R_DCO is bound.

*** Fill ALV Data
    loop at ME->R_DCO->GET_MURGR( ) assigning <S_MURGR>.
      append initial line to ME->T_MURGR_GRID assigning <S_MURGR_GRID>.

***   Fill ALV Data - Counter for Update Group and System of Measurement
      move-corresponding <S_MURGR> to <S_MURGR_GRID>.

***   Fill ALV Data - Number of Objects
      <S_MURGR_GRID>-NOOBJ = LINES( <S_MURGR>-T_MUROB ).

***   Fill ALV Data - Update Elements
      loop at <S_MURGR>-T_MUREL assigning <S_MUREL>.
        if <S_MURGR_GRID>-DESCR is not initial.
          <S_MURGR_GRID>-DESCR = <S_MURGR_GRID>-DESCR && ','.
        endif.

        LF_DESCR = <S_MUREL>-R_MUCEL->/VCXI/IF_CKX_OBJECT~GET_DESCR( ).
        if LF_DESCR is initial.
          LF_DESCR = <S_MUREL>-R_MUCEL->/VCXI/IF_CKX_OBJECT~GET_ID( ).
        endif.

        <S_MURGR_GRID>-DESCR = |{ <S_MURGR_GRID>-DESCR } { LF_DESCR }|.
      endloop.
    endloop.

  endmethod.
  method INIT_UI_SMDCNTRL.

    data: LF_CLSNA type SEOCLSNAME.


    check ME->R_RECORD is bound and
          ME->R_RECORD->R_CMODEL is bound.

*** Get Class Names for UI of SmartMD Control Settings
    ZCL_VCXI_XCUC_MUCEL=>GET_CLSNA_SMDCNTRL( exporting IR_CMODEL   = ME->R_RECORD->R_CMODEL
                                             importing EF_CLSNA_UI = LF_CLSNA ).
    check LF_CLSNA is not initial.

***--------------------------------------------------------------------------------------
*** Create Instance
    try.
        create object ME->R_UI_SMDCNTRL
               type (LF_CLSNA)
               exporting
                 IF_LOCKED   = ME->R_RECORD->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( )
                 IR_SMDCNTRL = ME->R_DCO->R_SMDCNTRL.

***   Catch Creation Errors
      catch CX_SY_CREATE_OBJECT_ERROR into data(LR_CX_SY_CREATE_OBJECT_ERROR).
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_SY_CREATE_OBJECT_ERROR ).
    endtry.

  endmethod.
