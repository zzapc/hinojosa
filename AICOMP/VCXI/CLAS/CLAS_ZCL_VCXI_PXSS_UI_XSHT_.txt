
class ZCL_VCXI_PXSS_UI_XSHT definition
  public
  inheriting from ZCL_VCXI_XCS_UI_TAB
  create public .

public section.

  data S_XSHT_VIS type ZVCXI_PXSS_S_XSHT_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABPT type ZVCXI_XCS_TABPT default '0100S_XP_TAB-TAB'
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_VIS
    importing
      !IS_VIS type ANY
    raising
      /VCXI/CX_CKX .

  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_PBO
    redefinition .
protected section.

  data R_PR_XSHT type ref to ZCL_VCXI_PXSS_PR_XSHT .

  methods INIT_VIS
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
private section.
endclass. "ZCL_VCXI_PXSS_UI_XSHT definition
class ZCL_VCXI_PXSS_UI_XSHT implementation.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE
                        IF_TABPT  = IF_TABPT ).

*** Assign th RP Class to the UI Class
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_XSHT.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DO_MODIFY_SCREEN.

    try.
        SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                 IF_DYNNR = IF_DYNNR ).

***----------------------------------------------------------------------------
***     Modify Screen
        loop at screen.
***       Reopen Info Buttons
          if SCREEN-GROUP1 eq 'INF'.
            move 1 to SCREEN-INPUT.
          endif.

          modify screen.
        endloop.

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_PBO.

    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize VIS
    ME->INIT_VIS( ).

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_XSHT_VIS  type ZVCXI_PXSS_S_XSHT_VIS.


    check ME->R_PR_XSHT is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_XSHT_VIS.

***--------------------------------------------------------------------------------------
*** Set Dimension Turned Flag
    if ME->S_XSHT_VIS-FDIMT ne LS_XSHT_VIS-FDIMT.
      ME->R_PR_XSHT->SET_FDIMT( IF_FDIMT = LS_XSHT_VIS-FDIMT
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Reduction on Width (Total)
    if ME->S_XSHT_VIS-S_RDWIT ne LS_XSHT_VIS-S_RDWIT.
      ME->R_PR_XSHT->ZIF_VCXI_PXSS_REDUC~SET_RDWIT( IS_RDWIT = LS_XSHT_VIS-S_RDWIT
                                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Reduction on Length (Total)
    if ME->S_XSHT_VIS-S_RDLET ne LS_XSHT_VIS-S_RDLET.
      ME->R_PR_XSHT->ZIF_VCXI_PXSS_REDUC~SET_RDLET( IS_RDLET = LS_XSHT_VIS-S_RDLET
                                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.


*** Set Reduction Left
    if ME->S_XSHT_VIS-S_RDLEF ne LS_XSHT_VIS-S_RDLEF.
      ME->R_PR_XSHT->ZIF_VCXI_PXSS_REDUC~SET_RDLEF( IS_RDLEF = LS_XSHT_VIS-S_RDLEF
                                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Reduction Right
    if ME->S_XSHT_VIS-S_RDRIG ne LS_XSHT_VIS-S_RDRIG.
      ME->R_PR_XSHT->ZIF_VCXI_PXSS_REDUC~SET_RDRIG( IS_RDRIG = LS_XSHT_VIS-S_RDRIG
                                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Reduction Top
    if ME->S_XSHT_VIS-S_RDTOP ne LS_XSHT_VIS-S_RDTOP.
      ME->R_PR_XSHT->ZIF_VCXI_PXSS_REDUC~SET_RDTOP( IS_RDTOP = LS_XSHT_VIS-S_RDTOP
                                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Reduction Bottom
    if ME->S_XSHT_VIS-S_RDBOT ne LS_XSHT_VIS-S_RDBOT.
      ME->R_PR_XSHT->ZIF_VCXI_PXSS_REDUC~SET_RDBOT( IS_RDBOT = LS_XSHT_VIS-S_RDBOT
                                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_XSHT.

  endmethod.
  method INIT_VIS.

    free: ME->S_XSHT_VIS.
    check ME->R_PR_XSHT is bound.


*** Dimension Turned
    ME->S_XSHT_VIS-FDIMT = ME->R_PR_XSHT->GET_FDIMT( ).

***------------------------------------------------------------------------------------------------
*** Reduction
    ME->S_XSHT_VIS-S_RDWIT = ME->R_PR_XSHT->ZIF_VCXI_PXSS_REDUC~GET_RDWIT( ).
    ME->S_XSHT_VIS-S_RDLET = ME->R_PR_XSHT->ZIF_VCXI_PXSS_REDUC~GET_RDLET( ).
    ME->S_XSHT_VIS-S_RDLEF = ME->R_PR_XSHT->ZIF_VCXI_PXSS_REDUC~GET_RDLEF( ).
    ME->S_XSHT_VIS-S_RDRIG = ME->R_PR_XSHT->ZIF_VCXI_PXSS_REDUC~GET_RDRIG( ).
    ME->S_XSHT_VIS-S_RDTOP = ME->R_PR_XSHT->ZIF_VCXI_PXSS_REDUC~GET_RDTOP( ).
    ME->S_XSHT_VIS-S_RDBOT = ME->R_PR_XSHT->ZIF_VCXI_PXSS_REDUC~GET_RDBOT( ).

*** Get Handling Dimension
    ME->S_XSHT_VIS-S_HNDDIM = ME->R_PR_XSHT->GET_HNDDIM( ).

  endmethod.
