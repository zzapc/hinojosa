
class ZCL_VCXI_XCSUC_SHLP_DFLT_SHSCH definition
  public
  create public .

public section.

  class-methods CALL_SHLP
    importing
      !IF_DISPLAY type ABAP_BOOL default ABAP_FALSE
      !IT_DFLT_SHSCH type ZVCXI_XCSUC_TT_DFLT_SHSCH_SHLP
      !IF_MULTIPLE_CHOICE type ABAP_BOOL default ABAP_FALSE
    returning
      value(RT_DFLT_SHSCH) type ZVCXI_XCSUC_TT_DFLT_SHSCH_SHLP
    raising
      /VCXI/CX_CKX .
protected section.
private section.
endclass. "ZCL_VCXI_XCSUC_SHLP_DFLT_SHSCH definition
class ZCL_VCXI_XCSUC_SHLP_DFLT_SHSCH implementation.
method CALL_SHLP.

  data:          LS_SHSCH_COMBI      type                   ZVCXI_XCSU_S_SHSCH_COMBI,
                 LS_RETURN           type                   DDSHRETVAL,
                 LS_MAP              type                   DSELC.
  data:          LT_DFLT_SHSCH_SHLP  type                   ZVCXI_XCSUC_TT_DFLT_SHSCH_SHLP,
                 LT_SHSCH_COMBI      type                   ZVCXI_XCSU_TT_SHSCH_COMBI,
                 LT_RETURN           type table of          DDSHRETVAL,
                 LT_MAP              type standard table of DSELC.
  data:          LR_CUST_SHIPU       type ref to            ZCL_VCXI_XCSU_CUST_SHIPU.
  field-symbols: <F_FIELD>           type                   any,
                 <S_DFLT_SHSCH_SHLP> type                   ZVCXI_XCSUC_S_DFLT_SHSCH_SHLP.

  LR_CUST_SHIPU = ZCL_VCXI_XCSU_CUST_SHIPU=>GET_INSTANCE( ).

  check LR_CUST_SHIPU is bound.
  LT_SHSCH_COMBI = LR_CUST_SHIPU->GET_SHSCH_COMBI( ).

  loop at LT_SHSCH_COMBI into LS_SHSCH_COMBI.
    read table IT_DFLT_SHSCH with key CMATN = LS_SHSCH_COMBI-CMATN
                                      PRFID = LS_SHSCH_COMBI-PRFID
                                      CNTXT = LS_SHSCH_COMBI-CNTXT
                             transporting no fields.
    check SY-SUBRC ne 0.
    append initial line to LT_DFLT_SHSCH_SHLP assigning <S_DFLT_SHSCH_SHLP>.
    move-corresponding LS_SHSCH_COMBI to <S_DFLT_SHSCH_SHLP>.
  endloop.

  sort LT_DFLT_SHSCH_SHLP by CMATN PRFID CNTXT SHSCH.

  LS_MAP-FLDNAME   = 'F0001'. "Field for Position 1
  LS_MAP-DYFLDNAME = 'CMATN'.
  append LS_MAP to LT_MAP.

  LS_MAP-FLDNAME   = 'F0002'. "Field for Position 2
  LS_MAP-DYFLDNAME = 'PRFID'.
  append LS_MAP to LT_MAP.

  LS_MAP-FLDNAME   = 'F0003'. "Field for Position 3
  LS_MAP-DYFLDNAME = 'CNTXT'.
  append LS_MAP to LT_MAP.

  LS_MAP-FLDNAME   = 'F0004'. "Field for Position 4
  LS_MAP-DYFLDNAME = 'SHSCH'.
  append LS_MAP to LT_MAP.

  call function 'F4IF_INT_TABLE_VALUE_REQUEST'
    exporting
      RETFIELD        = 'CMATN'
      VALUE_ORG       = 'S'
      MULTIPLE_CHOICE = IF_MULTIPLE_CHOICE
      DISPLAY         = IF_DISPLAY
    tables
      VALUE_TAB       = LT_DFLT_SHSCH_SHLP
      DYNPFLD_MAPPING = LT_MAP
      RETURN_TAB      = LT_RETURN
    exceptions
      PARAMETER_ERROR = 1
      NO_VALUES_FOUND = 2
      others          = 3.

  if SY-SUBRC eq 0.
    sort LT_RETURN by RECORDPOS.
    loop at LT_RETURN into LS_RETURN.
      read table RT_DFLT_SHSCH assigning <S_DFLT_SHSCH_SHLP> index LS_RETURN-RECORDPOS.
      if SY-SUBRC ne 0.
        append initial line to RT_DFLT_SHSCH assigning <S_DFLT_SHSCH_SHLP>.
      endif.
      assign component LS_RETURN-RETFIELD of structure <S_DFLT_SHSCH_SHLP> to <F_FIELD>.
      check SY-SUBRC = 0.
      move LS_RETURN-FIELDVAL to <F_FIELD>.
      unassign <F_FIELD>.
    endloop.
  endif.

endmethod.
endclass. "ZCL_VCXI_XCSUC_SHLP_DFLT_SHSCH implementation
