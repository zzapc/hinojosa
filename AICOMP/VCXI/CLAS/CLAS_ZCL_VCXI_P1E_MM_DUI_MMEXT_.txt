
class ZCL_VCXI_P1E_MM_DUI_MMEXT definition
  public
  inheriting from ZCL_VCXI_XCA_MWB_CLDUI
  create public .

public section.

  data S_DUI_MMEXT_VIS type ZVCXI_P1E_MM_S_DUI_MMEXT_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to ZCL_VCXI_XCA_MWB_UIFW
      !IR_CLMAT type ref to ZCL_VCXI_XCA_MWB_CLMAT
    raising
      /VCXI/CX_CKX .

  methods DO_FREE
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  constants C_UC_SCORE_L type SYUCOMM value 'SCORE_L' ##NO_TEXT.
  constants C_UC_SCORE_W type SYUCOMM value 'SCORE_W' ##NO_TEXT.
  data R_GRID_PRINT_COM type ref to ZCL_VCXI_PXE_MM_GRID_PRINT_COM .

  methods HANDLE_UC_SCORE
    importing
      !IF_DIMEN type ZVCXI_PXS_DIMEN
    raising
      /VCXI/CX_CKX .

  methods INIT_VIS
    redefinition .
  methods MODIFY_SCREEN
    redefinition .
private section.
endclass. "ZCL_VCXI_P1E_MM_DUI_MMEXT definition
class ZCL_VCXI_P1E_MM_DUI_MMEXT implementation.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW  = IR_UIFW
                        IR_CLMAT = IR_CLMAT  ).


*** Create Grid
    create object ME->R_GRID_PRINT_COM
      exporting
        IR_MMEXT          = cast ZCL_VCXI_P1E_MM_MMEXT( cast ZCL_VCXI_XCE_MM_MAT_MMEXT( ME->R_CLMAT )->GET_MMEXT( ) )
        IF_CONTAINER_NAME = '0100CC_PRINT_COM'.

***----------------------------------------------------------------------------
*** Bind UI
    call function 'ZVCXI_P1E_MM_DUI_MMEXT_BIND'
      exporting
        IR_DUI_MMEXT = ME
      importing
        ES_DYNP      = ME->S_DYNP.

  endmethod.
  method DO_FREE.

    SUPER->DO_FREE( ).

*** Printing Grid
    if ME->R_GRID_PRINT_COM is bound.
      ME->R_GRID_PRINT_COM->DO_FREE( ).
      clear ME->R_GRID_PRINT_COM.
    endif.

  endmethod.
  method DO_PBO.

    data: LF_EDIT type LVC_EDIT.


    SUPER->DO_PBO( ).

*** Printing Grid
    if ME->R_GRID_PRINT_COM is bound.
      if ME->R_CLMAT->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ) and
         ME->R_CLMAT->IS_CONFIGURABLE( ) ne ABAP_TRUE.
        LF_EDIT = ABAP_TRUE.
      endif.

      ME->R_GRID_PRINT_COM->REFRESH_GRID( IF_EDIT = LF_EDIT ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_DUI_MMEXT_VIS type ZVCXI_P1E_MM_S_DUI_MMEXT_VIS.


*** Call SUPER
    SUPER->DO_UPDATE_VIS( IS_DUI_VIS = IS_DUI_VIS ).

    check ME->R_CLMAT is bound.

*** "Cast" Vis Structure
    move-corresponding IS_DUI_VIS to LS_DUI_MMEXT_VIS.


***--------------------------------------------------------------------------------------
*** Material Updates
    data(LR_BOB_MAT) = ME->R_CLMAT->GET_BOB_MAT( ).
    if LR_BOB_MAT is bound.
***   Set Material Plant Status
      if ME->S_DUI_MMEXT_VIS-MMSTA ne LS_DUI_MMEXT_VIS-MMSTA.
        LR_BOB_MAT->S_PLANTDATA-PUR_STATUS = LS_DUI_MMEXT_VIS-MMSTA.
        ME->R_CLMAT->MARK_CHANGED( ).
      endif.
    endif.


***--------------------------------------------------------------------------------------
*** Material Master Extension Updates
    data(LR_MMEXT) = cast ZCL_VCXI_P1E_MM_MMEXT( cast ZCL_VCXI_XCE_MM_MAT_MMEXT( ME->R_CLMAT )->GET_MMEXT( ) ).
    if LR_MMEXT is bound.
      LR_MMEXT->SET_MMEXT( IS_MMEXT = LS_DUI_MMEXT_VIS-S_MMEXT ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    case IF_UCOMM.
      when C_UC_SCORE_W.
        ME->HANDLE_UC_SCORE( IF_DIMEN = ZCL_VCXI_PXSS_BL_SCORE=>C_DIMEN_WIDTH ).
      when C_UC_SCORE_L.
        ME->HANDLE_UC_SCORE( IF_DIMEN = ZCL_VCXI_PXSS_BL_SCORE=>C_DIMEN_LENGTH ).
    endcase.

  endmethod.
  method HANDLE_UC_SCORE.

*** Get Material Master Extension
    data(LR_MMEXT) = cast ZCL_VCXI_P1E_MM_MMEXT( cast ZCL_VCXI_XCE_MM_MAT_MMEXT( ME->R_CLMAT )->GET_MMEXT( ) ).
    check LR_MMEXT is bound.

    data(LT_SCORE_COM) = LR_MMEXT->T_SCORE_COM.
    data(LF_EDIT) = cond #( when ME->R_CLMAT->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ) eq ABAP_TRUE and
                                 ME->R_CLMAT->IS_CONFIGURABLE( )                       ne ABAP_TRUE
                                 then ABAP_TRUE ).

*** Call Score Popup
    if ZCL_VCXI_PXSS_POPUP_SCORE_COM=>START( exporting IF_EDIT      = LF_EDIT
                                                       IF_DIMEN     = IF_DIMEN
                                             changing  CT_SCORE_COM = LT_SCORE_COM ) ne ABAP_TRUE.
      LR_MMEXT->SET_SCORE_COM( IT_SCORE_COM = LT_SCORE_COM ).
    endif.

  endmethod.
  method INIT_VIS.

    check ME->R_CLMAT is bound.

    SUPER->INIT_VIS( ).

    free: ME->S_DUI_MMEXT_VIS.
    ME->S_DUI_MMEXT_VIS = corresponding #( ME->S_DUI_VIS ).

***--------------------------------------------------------------------------------------
    data(LR_BOB_MAT) = ME->R_CLMAT->GET_BOB_MAT( ).
    if LR_BOB_MAT is bound.
***   Get Plant Material Status
      ME->S_DUI_MMEXT_VIS-MMSTA = LR_BOB_MAT->S_PLANTDATA-PUR_STATUS.
    endif.

***--------------------------------------------------------------------------------------
*** Get Material Master Extension
    data(LR_MMEXT) = cast ZCL_VCXI_P1E_MM_MMEXT( cast ZCL_VCXI_XCE_MM_MAT_MMEXT( ME->R_CLMAT )->GET_MMEXT( ) ).
    if LR_MMEXT is bound.
      ME->S_DUI_MMEXT_VIS-S_MMEXT = LR_MMEXT->S_MMEXT.

***   Procuremnt Sheet Size and Score Strings
      ME->S_DUI_MMEXT_VIS-SSTRL = ZCL_VCXI_PXSS_BL_SCORE=>GET_SCSTR_COM( IT_SCORE_COM = LR_MMEXT->T_SCORE_COM
                                                                         IF_DIMEN     = ZCL_VCXI_PXSS_BL_SCORE=>C_DIMEN_LENGTH ).
      ME->S_DUI_MMEXT_VIS-SSTRC = ZCL_VCXI_PXSS_BL_SCORE=>GET_SCSTR_COM( IT_SCORE_COM = LR_MMEXT->T_SCORE_COM
*                                                                       IT_TAPE_COM  = ME->R_PR_MS_CRPROC->GET_TAPES( )
                                                                         IF_DIMEN     = ZCL_VCXI_PXSS_BL_SCORE=>C_DIMEN_WIDTH ).
      ZCL_VCXI_PXSS_BL_SCORE=>GET_SHTDIM( exporting IT_SCORE_COM = LR_MMEXT->T_SCORE_COM
                                          importing ES_SHTLE     = ME->S_DUI_MMEXT_VIS-S_PRSLE
                                                    ES_SHTWI     = ME->S_DUI_MMEXT_VIS-S_PRSWI ).
    endif.

  endmethod.
  method MODIFY_SCREEN.

    SUPER->MODIFY_SCREEN( ).

*** Check if Material has configuration
    data(LF_CONFIG) = ME->R_CLMAT->IS_CONFIGURABLE( ).

    loop at screen.
***   Material Extension is not changeable, if it has a configuration
      if SCREEN-GROUP2 eq 'EXT' and
         LF_CONFIG eq ABAP_TRUE.
        move 0 to SCREEN-INPUT.
      endif.

      modify screen.
    endloop.

  endmethod.
