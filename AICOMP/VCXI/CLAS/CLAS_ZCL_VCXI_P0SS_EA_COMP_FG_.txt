
class ZCL_VCXI_P0SS_EA_COMP_FG definition
  public
  inheriting from /VCXI/CL_VKSR_CLSEA
  create public .

public section.

  interfaces /VCXI/IF_VKCR_EXPL_BOM .

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
protected section.

  data R_PR_COMP_FG type ref to ZCL_VCXI_P0SS_PR_COMP_FG .

  methods GET_EXQTY
    returning
      value(RS_EXQTY) type /VCXI/VKCR_S_EXQTY
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_P0SS_EA_COMP_FG definition
class ZCL_VCXI_P0SS_EA_COMP_FG implementation.
method /VCXI/IF_VKCR_EXPL_BOM~FILTER_CPBRG.
endmethod.
method /VCXI/IF_VKCR_EXPL_BOM~GET_BOM.

  data:  LF_COMPR     type ZVCXI_P0SS_COMPR.
  data:  LS_EXQTY     type /VCXI/VKCR_S_EXQTY.

  if IS_EXQTY is supplied.
    move IS_EXQTY         to LS_EXQTY.
  else.
    move ME->GET_EXQTY( ) to LS_EXQTY.
  endif.

*** Get Component Ratio
  LF_COMPR = ME->R_PR_COMP_FG->GET_COMPR( ).

*** BOM Consumption
  RS_EXBOM-MENGE = LS_EXQTY-EXQTY * LF_COMPR.
  move ABAP_TRUE                    to RS_EXBOM-MENGE_X.
  move LS_EXQTY-EXQTY_UOM           to RS_EXBOM-MEINS.
  move ABAP_TRUE                    to RS_EXBOM-MEINS_X.

*** Make it a FIX Quantity
  move ABAP_TRUE                    to RS_EXBOM-FMENG.
  move ABAP_TRUE                    to RS_EXBOM-FMENG_X.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_GATE = IR_GATE ).

*** Assign the PR Class to the Element API Class
  try.
      move ME->R_GATE->R_CLSPR ?to ME->R_PR_COMP_FG.
    catch CX_SY_MOVE_CAST_ERROR.
***   Element API ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '804'.
  endtry.

endmethod.
method GET_EXQTY.

  data:  LR_APPDATA type ref to /VCXI/CL_VKSR_APPDATA.

  LR_APPDATA = ME->R_GATE->GET_APPDATA( ).

  if LR_APPDATA is bound and
     LR_APPDATA->S_OPQTY-OPQTY is not initial.
    move LR_APPDATA->S_OPQTY-OPQTY     to RS_EXQTY-EXQTY.
    move LR_APPDATA->S_OPQTY-OPQTY_UOM to RS_EXQTY-EXQTY_UOM.
  endif.

endmethod.
