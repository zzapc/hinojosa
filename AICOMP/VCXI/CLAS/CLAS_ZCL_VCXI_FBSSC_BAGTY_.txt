
class ZCL_VCXI_FBSSC_BAGTY definition
  public
  create protected .

public section.

  interfaces /VCXI/IF_CKX_OBJECT .
  interfaces /VCXI/IF_CKX_OBJECT_ACTION .
  interfaces /VCXI/IF_CKX_OBJECT_CD .
  interfaces /VCXI/IF_CKX_OBJECT_LOCKING .

  types:
    YT_TC010  type standard table of ZVCXI_FBS_TC010 .
  types:
    YT_TC010T type standard table of ZVCXI_FBS_TC010T .
  types:
    YT_TC020 type standard table of ZVCXI_FBS_TC020 .
  types:
    YT_TC020T type standard table of ZVCXI_FBS_TC020T .
  types:
    YT_TC030 type standard table of ZVCXI_FBS_TC030 .
  types:
    YT_TC030T type standard table of ZVCXI_FBS_TC030T .
  types:
    YT_TC110  type standard table of ZVCXI_FBS_TC110 .
  types:
    YT_TC110T type standard table of ZVCXI_FBS_TC110T .
  types:
    YT_TC112  type standard table of ZVCXI_FBS_TC112 .
  types:
    YT_TC111T type standard table of ZVCXI_FBS_TC111T .
  types:
    YT_TC210  type standard table of ZVCXI_FBS_TC210 .
  types:
    YT_TC210T type standard table of ZVCXI_FBS_TC210T .
  types:
    YT_TC211  type standard table of ZVCXI_FBS_TC211 .
  types:
    YT_TC211T type standard table of ZVCXI_FBS_TC211T .

  constants C_ACTVT_CREATE type ACTIV_AUTH value '01' ##NO_TEXT.
  constants C_ACTVT_DELETE type ACTIV_AUTH value '06' ##NO_TEXT.
  constants C_ACTVT_DISPLAY type ACTIV_AUTH value '03' ##NO_TEXT.
  constants C_ACTVT_EDIT type ACTIV_AUTH value '02' ##NO_TEXT.
  constants C_BGDIM_BGHEI type ZVCXI_FBS_IBDIM value 'B_HEI' ##NO_TEXT.
  constants C_BGDIM_BGWID type ZVCXI_FBS_IBDIM value 'B_WID' ##NO_TEXT.
  constants C_CDOCL_IBAGT type CDOBJECTCL value 'ZVCXI_FBSSC_IB' ##NO_TEXT.
  constants C_COUNT_MIN1 type /VCXI/VKSC_QTYMI value 1 ##NO_TEXT.
  constants C_CSTEP_RECLO type ZVCXI_FBS_ICSTP value 'RECLO' ##NO_TEXT.
  constants C_FCODE_BAGTY_CHECK type UI_FUNC value 'CHECK' ##NO_TEXT.
  constants C_FCODE_BAGTY_DELETE type UI_FUNC value 'DELETE' ##NO_TEXT.
  constants C_FCODE_BAGTY_FREE type UI_FUNC value 'FREE' ##NO_TEXT.
  constants C_FCODE_BAGTY_SAVE type UI_FUNC value 'SAVE' ##NO_TEXT.
  constants C_OPER_ADD type ZVCXI_FBS_OPER value '+' ##NO_TEXT.
  data F_CCCORACTIV type CCCORACTIV read-only .
  data F_CHANGED type ABAP_BOOL read-only value ABAP_FALSE ##NO_TEXT.
  data F_DELETED type ABAP_BOOL read-only value ABAP_FALSE ##NO_TEXT.
  data F_IBAGT type ZVCXI_FBS_IBAGT read-only .
  data F_NOTVALID type ABAP_BOOL read-only .
  data T_BAGTY_BGDIM type ZVCXI_FBSSC_TT_BAGTY_BGDIM read-only .
  data T_BAGTY_BGHDC type ZVCXI_FBSSC_TT_BAGTY_BGHDC read-only .
  data T_BAGTY_BPART type ZVCXI_FBSSC_TT_BAGTY_BPART read-only .
  data T_BAGTY_CSTEP type ZVCXI_FBSSC_TT_BAGTY_CSTEP read-only .
  data T_BAGTY_HANDL type ZVCXI_FBSSC_TT_BAGTY_HANDL read-only .
  data T_BAGTY_RECLO type ZVCXI_FBSSC_TT_BAGTY_RECLO read-only .
  data T_DESCR type /VCXI/CKX_TT_DESCR read-only .
  class-data T_TC010 type YT_TC010 read-only .
  class-data T_TC010T type YT_TC010T read-only .
  class-data T_TC020 type YT_TC020 read-only .
  class-data T_TC020T type YT_TC020T read-only .
  class-data T_TC030 type YT_TC030 read-only .
  class-data T_TC030T type YT_TC030T read-only .
  class-data T_TC110 type YT_TC110 read-only .
  class-data T_TC110T type YT_TC110T read-only .
  class-data T_TC111T type YT_TC111T read-only .
  class-data T_TC112 type YT_TC112 read-only .
  class-data T_TC210 type YT_TC210 read-only .
  class-data T_TC210T type YT_TC210T read-only .
  class-data T_TC211 type YT_TC211 read-only .
  class-data T_TC211T type YT_TC211T read-only .
  constants C_IELEM_BP_LENGTH type ZVCXI_FBS_IELEM value 'BP_LENGTH' ##NO_TEXT.
  constants C_IELEM_BP_WIDTH type ZVCXI_FBS_IELEM value 'BP_WIDTH' ##NO_TEXT.
  constants C_IELEM_BP_THICK type ZVCXI_FBS_IELEM value 'BP_THICK' ##NO_TEXT.

  class-methods GET_IELEM_DESCR
    importing
      !IF_IELEM type ZVCXI_FBS_IELEM
    returning
      value(RF_DESCR) type /VCXI/CKX_DESCR
    raising
      /VCXI/CX_CKX .
  methods ADD_BGDIM
    importing
      !IS_BAGTY_BGDIM type ZVCXI_FBSSC_S_BAGTY_BGDIM
    raising
      /VCXI/CX_CKX .
  methods ADD_BGHDC
    importing
      !IS_BAGTY_BGHDC type ZVCXI_FBSSC_S_BAGTY_BGHDC
    raising
      /VCXI/CX_CKX .
  methods UPDATE_BGHDC
    importing
      !IS_BAGTY_BGHDC type ZVCXI_FBSSC_S_BAGTY_BGHDC
    raising
      /VCXI/CX_CKX .
  methods ADD_BPART
    importing
      !IS_BAGTY_BPART type ZVCXI_FBSSC_S_BAGTY_BPART
    raising
      /VCXI/CX_CKX .
  methods ADD_BPCAL
    importing
      !IS_BAGTY_BPCAL type ZVCXI_FBSSC_S_BAGTY_BPCAL
      !IF_IBPAR type ZVCXI_FBS_IBPAR
    raising
      /VCXI/CX_CKX .
  methods ADD_CSTEP
    importing
      !IS_BAGTY_CSTEP type ZVCXI_FBSSC_S_BAGTY_CSTEP
    raising
      /VCXI/CX_CKX .
  methods ADD_HANDL
    importing
      !IS_BAGTY_HANDL type ZVCXI_FBSSC_S_BAGTY_HANDL
    raising
      /VCXI/CX_CKX .
  methods ADD_RECLO
    importing
      !IS_BAGTY_RECLO type ZVCXI_FBSSC_S_BAGTY_RECLO
    raising
      /VCXI/CX_CKX .
  class-methods CHECK_AUTH
    importing
      !IF_ACTVT type ACTIV_AUTH
      !IR_BAGTY type ref to ZCL_VCXI_FBSSC_BAGTY optional
      !IF_ID type ZVCXI_FBS_IBAGT optional
    raising
      /VCXI/CX_CKX .
  methods CHECK_BPART_ADD
    importing
      !IF_IBPAR type ZVCXI_FBS_IBPAR
    returning
      value(RF_DISABLE) type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
  methods CHECK_CSTEP
    importing
      !IF_ICSTP type ZVCXI_FBS_ICSTP
    returning
      value(RF_FOUND) type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
  class-methods CLASS_CONSTRUCTOR .
  class-methods COPY
    importing
      !IF_IBAGT_NEW type ZVCXI_FBS_IBAGT
      !IF_IBAGT_OLD type ZVCXI_FBS_IBAGT
    returning
      value(RR_BAGTY) type ref to ZCL_VCXI_FBSSC_BAGTY
    raising
      /VCXI/CX_CKX .
  class-methods CREATE
    importing
      !IF_IBAGT type ZVCXI_FBS_IBAGT
    returning
      value(RR_BAGTY) type ref to ZCL_VCXI_FBSSC_BAGTY
    raising
      /VCXI/CX_CKX .
  class-methods DECRYPT_GUID
    importing
      !IF_GUID type /VCXI/CKX_GUID
    exporting
      !EF_IBAGT type ZVCXI_FBS_IBAGT
    raising
      /VCXI/CX_CKX .
  methods DELETE_BGDIM
    importing
      !IS_BAGTY_BGDIM type ZVCXI_FBSSC_S_BAGTY_BGDIM
    raising
      /VCXI/CX_CKX .
  methods DELETE_BGHDC
    importing
      !IS_BAGTY_BGHDC type ZVCXI_FBSSC_S_BAGTY_BGHDC
    raising
      /VCXI/CX_CKX .
  methods DELETE_BPART
    importing
      !IS_BAGTY_BPART type ZVCXI_FBSSC_S_BAGTY_BPART
    raising
      /VCXI/CX_CKX .
  methods DELETE_BPCAL
    importing
      !IS_BAGTY_BPCAL type ZVCXI_FBSSC_S_BAGTY_BPCAL
      !IF_IBPAR type ZVCXI_FBS_IBPAR
    raising
      /VCXI/CX_CKX .
  methods DELETE_CSTEP
    importing
      !IS_BAGTY_CSTEP type ZVCXI_FBSSC_S_BAGTY_CSTEP
    raising
      /VCXI/CX_CKX .
  methods DELETE_HANDL
    importing
      !IS_BAGTY_HANDL type ZVCXI_FBSSC_S_BAGTY_HANDL
    raising
      /VCXI/CX_CKX .
  methods DELETE_RECLO
    importing
      !IS_BAGTY_RECLO type ZVCXI_FBSSC_S_BAGTY_RECLO
    raising
      /VCXI/CX_CKX .
  methods DELETE_RECLO_ALL
    raising
      /VCXI/CX_CKX .
  class-methods ENCRYPT_GUID
    importing
      !IF_IBAGT type ZVCXI_FBS_IBAGT
    returning
      value(RF_GUID) type /VCXI/CKX_GUID
    raising
      /VCXI/CX_CKX .
  class-methods GET_IBPAR_DESCR
    importing
      !IF_IBPAR type ZVCXI_FBS_IBPAR
    returning
      value(RF_DESCR) type /VCXI/CKX_DESCR
    raising
      /VCXI/CX_CKX .
  class-methods LOAD_BASIC_CUST .
  methods SET_DESCR
    importing
      !IF_DESCR type /VCXI/CKX_DESCR
      !IF_LANGU type LANGU default SY-LANGU
    raising
      /VCXI/CX_CKX .
  methods UPDATE_BGDIM
    importing
      !IS_BAGTY_BGDIM type ZVCXI_FBSSC_S_BAGTY_BGDIM
    raising
      /VCXI/CX_CKX .
  methods UPDATE_BPART
    importing
      !IS_BAGTY_BPART type ZVCXI_FBSSC_S_BAGTY_BPART
    raising
      /VCXI/CX_CKX .
  methods UPDATE_BPCAL
    importing
      !IS_BAGTY_BPCAL type ZVCXI_FBSSC_S_BAGTY_BPCAL
      !IF_IBPAR type ZVCXI_FBS_IBPAR
    raising
      /VCXI/CX_CKX .
  methods UPDATE_CSTEP
    importing
      !IS_BAGTY_CSTEP type ZVCXI_FBSSC_S_BAGTY_CSTEP
    raising
      /VCXI/CX_CKX .
  methods UPDATE_HANDL
    importing
      !IS_BAGTY_HANDL type ZVCXI_FBSSC_S_BAGTY_HANDL
    raising
      /VCXI/CX_CKX .
  methods UPDATE_RECLO
    importing
      !IS_BAGTY_RECLO type ZVCXI_FBSSC_S_BAGTY_RECLO
    raising
      /VCXI/CX_CKX .
protected section.

  data F_TRANS_REQ type ABAP_BOOL .

  methods CONSTRUCTOR
    importing
      !IF_IBAGT type ZVCXI_FBS_IBAGT
      !IF_NEW type ABAP_BOOL default ABAP_FALSE
    raising
      /VCXI/CX_CKX .
  methods HANDLE_CHECK
    raising
      /VCXI/CX_CKX .
  methods HANDLE_DELETE
    raising
      /VCXI/CX_CKX .
  methods HANDLE_FREE
    raising
      /VCXI/CX_CKX .
  methods HANDLE_SAVE
    raising
      /VCXI/CX_CKX .
  methods LOAD_FROM_DB
    raising
      /VCXI/CX_CKX .
  methods SET_LOCK
    importing
      !IF_LOCKED type /VCXI/CKX_LOCKED
    raising
      /VCXI/CX_CKX .
private section.

  data F_LOCKED type /VCXI/CKX_LOCKED .
  data S_LOG type /VCXI/CKX_S_LOG .
  class-data T_INSTANCE type /VCXI/CKX_TT_OBJECT_INSTANCE .

  methods HANDLE_CHANGED
    for event CHANGED of /VCXI/IF_CKX_OBJECT .
  methods INIT_AFTER_CREATE
    raising
      /VCXI/CX_CKX .
endclass. "ZCL_VCXI_FBSSC_BAGTY definition
class ZCL_VCXI_FBSSC_BAGTY implementation.
  method /VCXI/IF_CKX_OBJECT_ACTION~GET_ACTIONS.

    data: LF_DISABLED type CUA_ACTIVE,
          LF_TEXT     type GUI_TEXT.

*** Create Menu
    create object RR_MENU.

***--------------------------------------------------------------------------------------
*** Delete Add-on Type
    if ME->F_CCCORACTIV ne 2.          "Customizing in this client cannot be changed
      move ABAP_FALSE to LF_DISABLED.
    else.
      move ABAP_TRUE  to LF_DISABLED.
    endif.

***--------------------------------------------------------------------------------------
*** Delete ID of Bag Type
    clear: LF_TEXT.
    move text-D01 to LF_TEXT.
    RR_MENU->ADD_FUNCTION( FCODE       = ZCL_VCXI_FBSSC_BAGTY=>C_FCODE_BAGTY_DELETE
                           TEXT        = LF_TEXT
                           DISABLED    = LF_DISABLED ).

***--------------------------------------------------------------------------------------
*** Check ID of Bag Type
    clear: LF_TEXT.
    move text-C01 to LF_TEXT.
    RR_MENU->ADD_FUNCTION( FCODE       = ZCL_VCXI_FBSSC_BAGTY=>C_FCODE_BAGTY_CHECK
                           TEXT        = LF_TEXT
                           DISABLED    = ABAP_FALSE ).

  endmethod.
  method /VCXI/IF_CKX_OBJECT_ACTION~HANDLE_ACTION.

    case IF_FCODE.
      when ZCL_VCXI_FBSSC_BAGTY=>C_FCODE_BAGTY_SAVE.
        ME->HANDLE_SAVE( ).
      when ZCL_VCXI_FBSSC_BAGTY=>C_FCODE_BAGTY_DELETE.
        ME->HANDLE_DELETE( ).
      when ZCL_VCXI_FBSSC_BAGTY=>C_FCODE_BAGTY_FREE.
        ME->HANDLE_FREE( ).
      when ZCL_VCXI_FBSSC_BAGTY=>C_FCODE_BAGTY_CHECK.
        ME->HANDLE_CHECK( ).
    endcase.

  endmethod.
  method /VCXI/IF_CKX_OBJECT_CD~GET_INFO.

    move C_CDOCL_IBAGT   to EF_CDOCL.
    move ME->/VCXI/IF_CKX_OBJECT~GET_GUID( ) to EF_CDOID.

  endmethod.
  method /VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED.

***--------------------------------------------------------------------------------------
*** Check of the Lock-Status of the Object
    check  ME->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ) ne ABAP_TRUE.

*** Object is not locked - No changes possible!
    raise exception type /VCXI/CX_CKX
      exporting
        F_MSGTY = 'E'
        F_MSGID = '/VCXI/CKX'
        F_MSGNO = '201'.

  endmethod.
  method /VCXI/IF_CKX_OBJECT_LOCKING~DO_LOCK.

***--------------------------------------------------------------------------------------
*** Check Authorization
    ZCL_VCXI_FBSSC_BAGTY=>CHECK_AUTH( IF_ACTVT = ZCL_VCXI_FBSSC_BAGTY=>C_ACTVT_EDIT
                                      IR_BAGTY = ME ).

***--------------------------------------------------------------------------------------
*** Customizing Changes allowed in Client
    check ME->F_CCCORACTIV ne '2'.

***--------------------------------------------------------------------------------------
*** Set Lock Entry
    ME->SET_LOCK( IF_LOCKED = ABAP_TRUE ).

***--------------------------------------------------------------------------------------
    try.
***     Load from DB
        ME->LOAD_FROM_DB( ).

      catch /VCXI/CX_CKX.
        ME->HANDLE_FREE( ).
    endtry.

  endmethod.
  method /VCXI/IF_CKX_OBJECT_LOCKING~DO_UNLOCK.

***--------------------------------------------------------------------------------------
*** Remove Lock Entry
    ME->SET_LOCK( IF_LOCKED = ABAP_FALSE ).

***--------------------------------------------------------------------------------------
    try.
***     Load from DB
        ME->LOAD_FROM_DB( ).

      catch /VCXI/CX_CKX.
        ME->HANDLE_FREE( ).

    endtry.

  endmethod.
  method /VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED.

    move ME->F_LOCKED to RF_LOCKED.

  endmethod.
  method /VCXI/IF_CKX_OBJECT~GET_DESCR.

    data:  LT_DESCR type /VCXI/CKX_TT_DESCR.

    try.
        LT_DESCR = ME->/VCXI/IF_CKX_OBJECT~GET_DESCR_ALL( ).
        RF_DESCR = /VCXI/CL_CKX_SERVICE=>GET_DESCR_TRANSLATION( IT_DESCR = LT_DESCR ).
      catch /VCXI/CX_CKX.
        clear RF_DESCR.
    endtry.

  endmethod.
  method /VCXI/IF_CKX_OBJECT~GET_DESCR_ALL.

    move ME->T_DESCR to RT_DESCR.

  endmethod.
  method /VCXI/IF_CKX_OBJECT~GET_GUID.

    RF_GUID = ZCL_VCXI_FBSSC_BAGTY=>ENCRYPT_GUID( IF_IBAGT = ME->F_IBAGT ).

  endmethod.
  method /VCXI/IF_CKX_OBJECT~GET_ID.

    move ME->F_IBAGT to RF_ID.

  endmethod.
  method /VCXI/IF_CKX_OBJECT~GET_INSTANCE.

    data: LF_IBAGT    type        ZVCXI_FBS_IBAGT.
    data: LS_INSTANCE type        /VCXI/CKX_S_OBJECT_INSTANCE.
    data: LR_BAGTY    type ref to ZCL_VCXI_FBSSC_BAGTY.

    check IF_GUID is not initial.

    read table ZCL_VCXI_FBSSC_BAGTY=>T_INSTANCE into LS_INSTANCE with key GUID = IF_GUID.
    if SY-SUBRC is initial.
      move LS_INSTANCE-R_OBJECT to RR_OBJECT.
    else.

***   Decrypt GUID into ID of Bag Type
      ZCL_VCXI_FBSSC_BAGTY=>DECRYPT_GUID( exporting IF_GUID  = IF_GUID
                                          importing EF_IBAGT = LF_IBAGT ).

***   Get new Instance
      create object LR_BAGTY
        exporting
          IF_IBAGT = LF_IBAGT.

      move LR_BAGTY ?to RR_OBJECT.
    endif.

  endmethod.
  method /VCXI/IF_CKX_OBJECT~GET_LOG.

    move ME->S_LOG to RS_LOG.

  endmethod.
  method ADD_BGDIM.

    field-symbols: <S_BAGTY_BGDIM> type ZVCXI_FBSSC_S_BAGTY_BGDIM.

*** Make sure that object is locked
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

    read table ME->T_BAGTY_BGDIM with key IBAGT = IS_BAGTY_BGDIM-IBAGT
                                          IBDIM = IS_BAGTY_BGDIM-IBDIM
                                          transporting no fields.
    if SY-SUBRC is not initial.
      insert initial line into table ME->T_BAGTY_BGDIM assigning <S_BAGTY_BGDIM>.
      move: IS_BAGTY_BGDIM-IBAGT to <S_BAGTY_BGDIM>-IBAGT,
            IS_BAGTY_BGDIM-IBDIM to <S_BAGTY_BGDIM>-IBDIM,
            IS_BAGTY_BGDIM-OBDIM to <S_BAGTY_BGDIM>-OBDIM.
    endif.

***--------------------------------------------------------------------------------------
*** Raise Change Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method ADD_BGHDC.

*** Make sure that object is locked
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

    check not LINE_EXISTS( ME->T_BAGTY_BGHDC[ HNDDI = IS_BAGTY_BGHDC-HNDDI
                                              IBPAR = IS_BAGTY_BGHDC-IBPAR
                                              IELEM = IS_BAGTY_BGHDC-IELEM ] ).

*** Add Entry
    insert IS_BAGTY_BGHDC into table ME->T_BAGTY_BGHDC.

***--------------------------------------------------------------------------------------
*** Raise Change Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method ADD_BPART.

    data: LS_TC020       type ZVCXI_FBS_TC020,
          LS_BAGTY_BPART type ZVCXI_FBSSC_S_BAGTY_BPART.
    data: LT_BAGTY_BPART type ZVCXI_FBSSC_TT_BAGTY_BPART.
    field-symbols: <S_BAGTY_BPART> type ZVCXI_FBSSC_S_BAGTY_BPART.

*** Make sure that object is locked
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

    read table ME->T_BAGTY_BPART with key IBAGT = IS_BAGTY_BPART-IBAGT
                                          IBPAR = IS_BAGTY_BPART-IBPAR
                                          transporting no fields.
    if SY-SUBRC is not initial.

      insert initial line into table ME->T_BAGTY_BPART assigning <S_BAGTY_BPART>.
      move: IS_BAGTY_BPART-IBAGT to <S_BAGTY_BPART>-IBAGT,
            IS_BAGTY_BPART-IBPAR to <S_BAGTY_BPART>-IBPAR,
            IS_BAGTY_BPART-ODIRL to <S_BAGTY_BPART>-ODIRL,
            IS_BAGTY_BPART-ODIRT to <S_BAGTY_BPART>-ODIRT,
            IS_BAGTY_BPART-BPDIR to <S_BAGTY_BPART>-BPDIR,
            IS_BAGTY_BPART-BPCAL[] to <S_BAGTY_BPART>-BPCAL[].

***   Reorganize Indexes according to Bag Part Sequence
      if LINES( ME->T_BAGTY_BPART ) gt 1.
        refresh LT_BAGTY_BPART.
        loop at T_TC020 into LS_TC020.
          read table ME->T_BAGTY_BPART into LS_BAGTY_BPART with key IBPAR = LS_TC020-IBPAR.
          check SY-SUBRC is initial.

          insert initial line into table LT_BAGTY_BPART assigning <S_BAGTY_BPART>.
          move-corresponding LS_BAGTY_BPART to <S_BAGTY_BPART>.
        endloop.

        refresh ME->T_BAGTY_BPART.
        move LT_BAGTY_BPART[] to ME->T_BAGTY_BPART[].
      endif.

    endif.

***--------------------------------------------------------------------------------------
*** Raise Change Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method ADD_BPCAL.

    field-symbols: <S_BAGTY_BPART> type ZVCXI_FBSSC_S_BAGTY_BPART,
                   <S_BAGTY_BPCAL> type ZVCXI_FBSSC_S_BAGTY_BPCAL.

*** Make sure that object is locked
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

    read table ME->T_BAGTY_BPART assigning <S_BAGTY_BPART> with key IBPAR = IF_IBPAR.
    if SY-SUBRC is initial.
      read table <S_BAGTY_BPART>-BPCAL  with key BPDIM = IS_BAGTY_BPCAL-BPDIM
                                                 IBDIM = IS_BAGTY_BPCAL-IBDIM
                                                 transporting no fields.
      if SY-SUBRC is not initial.
        append initial line to <S_BAGTY_BPART>-BPCAL assigning <S_BAGTY_BPCAL>.
        move: IS_BAGTY_BPCAL-BPDIM  to <S_BAGTY_BPCAL>-BPDIM,
              IS_BAGTY_BPCAL-IBDIM  to <S_BAGTY_BPCAL>-IBDIM,
              IS_BAGTY_BPCAL-OPER   to <S_BAGTY_BPCAL>-OPER,
              IS_BAGTY_BPCAL-ELEMN  to <S_BAGTY_BPCAL>-ELEMN,
              IS_BAGTY_BPCAL-ELEMD  to <S_BAGTY_BPCAL>-ELEMD.
      endif.
    endif.

***--------------------------------------------------------------------------------------
*** Raise Change Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method ADD_CSTEP.

    field-symbols: <S_BAGTY_CSTEP> type ZVCXI_FBSSC_S_BAGTY_CSTEP.

*** Make sure that object is locked
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

    read table ME->T_BAGTY_CSTEP with key IBAGT = IS_BAGTY_CSTEP-IBAGT
                                          ICSTP = IS_BAGTY_CSTEP-ICSTP
                                          transporting no fields.
    if SY-SUBRC is not initial.
      insert initial line into table ME->T_BAGTY_CSTEP assigning <S_BAGTY_CSTEP>.
      move: IS_BAGTY_CSTEP-IBAGT to <S_BAGTY_CSTEP>-IBAGT,
            IS_BAGTY_CSTEP-ICSTP to <S_BAGTY_CSTEP>-ICSTP,
            IS_BAGTY_CSTEP-QTYMI to <S_BAGTY_CSTEP>-QTYMI,
            IS_BAGTY_CSTEP-QTYMA to <S_BAGTY_CSTEP>-QTYMA.
    endif.

***--------------------------------------------------------------------------------------
*** Raise Change Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method ADD_HANDL.

    field-symbols: <S_BAGTY_HANDL> type ZVCXI_FBSSC_S_BAGTY_HANDL.

*** Make sure that object is locked
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

    read table ME->T_BAGTY_HANDL with key IBAGT = IS_BAGTY_HANDL-IBAGT
                                          THNDL = IS_BAGTY_HANDL-THNDL
                                          transporting no fields.
    if SY-SUBRC is not initial.
      insert initial line into table ME->T_BAGTY_HANDL assigning <S_BAGTY_HANDL>.
      move: IS_BAGTY_HANDL-IBAGT to <S_BAGTY_HANDL>-IBAGT,
            IS_BAGTY_HANDL-THNDL to <S_BAGTY_HANDL>-THNDL.
    endif.

***--------------------------------------------------------------------------------------
*** Raise Change Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method ADD_RECLO.

    field-symbols: <S_BAGTY_RECLO> type ZVCXI_FBSSC_S_BAGTY_RECLO.

*** Make sure that object is locked
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

    read table ME->T_BAGTY_RECLO with key IBAGT = IS_BAGTY_RECLO-IBAGT
                                          TRECL = IS_BAGTY_RECLO-TRECL
                                          transporting no fields.
    if SY-SUBRC is not initial.
      insert initial line into table ME->T_BAGTY_RECLO assigning <S_BAGTY_RECLO>.
      move: IS_BAGTY_RECLO-IBAGT to <S_BAGTY_RECLO>-IBAGT,
            IS_BAGTY_RECLO-TRECL to <S_BAGTY_RECLO>-TRECL.
    endif.

***--------------------------------------------------------------------------------------
*** Raise Change Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method CHECK_AUTH.

    data: LF_ID    type /VCXI/CKX_ID,
          LF_LTEXT type XUTXT30.

***--------------------------------------------------------------------------------------
*** Get ID of Bag Type
    if IR_BAGTY is bound.
      move IR_BAGTY->/VCXI/IF_CKX_OBJECT~GET_ID( ) to LF_ID.
    elseif IF_ID is not initial.
      move IF_ID to LF_ID.
    endif.

***--------------------------------------------------------------------------------------
**** Check Authority for ID of Bag Type Customizing
    authority-check object 'Z_FB_BAGTY'
             id 'ACTVT'       field IF_ACTVT.

    check SY-SUBRC ne 0.

*** Get Activity Description
    select single LTEXT
           from TACTT
           into LF_LTEXT
          where SPRAS eq SY-LANGU and
                ACTVT eq IF_ACTVT.
    if SY-SUBRC ne 0.
      move IF_ACTVT to LF_LTEXT.
    endif.

*** No authorization to & &.
    /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( IF_MSGTY = 'E'
                                          IF_MSGID = 'ZVCXI_FBSSC'
                                          IF_MSGNO = '053'
                                          IF_MSGV1 = LF_LTEXT
                                          IF_MSGV2 = LF_ID ).

  endmethod.
  method CHECK_BPART_ADD.

    move ABAP_FALSE to RF_DISABLE.
    read table ME->T_BAGTY_BPART with key IBPAR = IF_IBPAR
                                          transporting no fields.
    if SY-SUBRC is initial.
      move ABAP_TRUE to RF_DISABLE.
    endif.

  endmethod.
  method CHECK_CSTEP.

    data: LS_BAGTY_CSTEP type ZVCXI_FBSSC_S_BAGTY_CSTEP.

    move ABAP_FALSE to RF_FOUND.
    loop at ME->T_BAGTY_CSTEP into LS_BAGTY_CSTEP where ICSTP cs IF_ICSTP.
      move ABAP_TRUE to RF_FOUND.
      exit.
    endloop.

  endmethod.
  method CLASS_CONSTRUCTOR.

    LOAD_BASIC_CUST( ).

  endmethod.
  method CONSTRUCTOR.

    data:          LR_CX_CKX     type ref to /VCXI/CX_CKX.
    field-symbols: <S_INSTANCE>  type        /VCXI/CKX_S_OBJECT_INSTANCE.

    try.
***--------------------------------------------------------------------------------------
***     Set ID of Bag Type of Object
        move IF_IBAGT to ME->F_IBAGT.

***--------------------------------------------------------------------------------------
***     Load from DB for existing entry
        if IF_NEW eq ABAP_FALSE.
          ME->LOAD_FROM_DB( ).
        endif.

***--------------------------------------------------------------------------------------
***     Add instance to instance buffer
        append initial line to ZCL_VCXI_FBSSC_BAGTY=>T_INSTANCE assigning <S_INSTANCE>.
        move ME->/VCXI/IF_CKX_OBJECT~GET_GUID( )  to <S_INSTANCE>-GUID.
        move ME                                  ?to <S_INSTANCE>-R_OBJECT.

***   Set Event Handler
        set handler ME->HANDLE_CHANGED for ME.

***--------------------------------------------------------------------------------------
***     Get System/Client Parameters
        call function 'TR_SYS_PARAMS'
          importing
            SYSTEM_CLIENT_EDIT = ME->F_CCCORACTIV
          exceptions
            NO_SYSTEMNAME      = 1
            NO_SYSTEMTYPE      = 2.
        if SY-SUBRC is not initial.
          /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
        endif.

      catch /VCXI/CX_CKX into LR_CX_CKX.
        ME->HANDLE_FREE( ).
        raise exception LR_CX_CKX.
    endtry.

  endmethod.
  method COPY.

    data: LF_GUID        type /VCXI/CKX_GUID.
    data: LS_BAGTY_BGDIM type ZVCXI_FBSSC_S_BAGTY_BGDIM,
          LS_BAGTY_BPART type ZVCXI_FBSSC_S_BAGTY_BPART,
          LS_BAGTY_BGHDC type ZVCXI_FBSSC_S_BAGTY_BGHDC,
          LS_BAGTY_HANDL type ZVCXI_FBSSC_S_BAGTY_HANDL,
          LS_BAGTY_CSTEP type ZVCXI_FBSSC_S_BAGTY_CSTEP,
          LS_BAGTY_RECLO type ZVCXI_FBSSC_S_BAGTY_RECLO.
    data: LR_BAGTY_OLD   type ref to ZCL_VCXI_FBSSC_BAGTY.


    check IF_IBAGT_NEW is not initial and
          IF_IBAGT_OLD is not initial.

***--------------------------------------------------------------------------------------
*** Get old Id of Bag Type
    try.
        LF_GUID = ZCL_VCXI_FBSSC_BAGTY=>ENCRYPT_GUID( IF_IBAGT = IF_IBAGT_OLD ).
        LR_BAGTY_OLD ?= ZCL_VCXI_FBSSC_BAGTY=>/VCXI/IF_CKX_OBJECT~GET_INSTANCE( IF_GUID = LF_GUID ).
      catch /VCXI/CX_CKX.
***     Old Bag Type can't be loaded for copy.
        /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( IF_MSGTY = 'E'
                                              IF_MSGID = 'ZVCXI_FBSSC'
                                              IF_MSGNO = '054' ).
    endtry.

***--------------------------------------------------------------------------------------
*** Create new Id of Bag Type
    RR_BAGTY = ZCL_VCXI_FBSSC_BAGTY=>CREATE( IF_IBAGT = IF_IBAGT_NEW ).

***--------------------------------------------------------------------------------------
*** Add all Dimension Elements
    loop at LR_BAGTY_OLD->T_BAGTY_BGDIM into LS_BAGTY_BGDIM.
      move IF_IBAGT_NEW to LS_BAGTY_BGDIM-IBAGT.
      RR_BAGTY->ADD_BGDIM( IS_BAGTY_BGDIM = LS_BAGTY_BGDIM ).
    endloop.

***--------------------------------------------------------------------------------------
*** Add all Bag Parts
    loop at LR_BAGTY_OLD->T_BAGTY_BPART into LS_BAGTY_BPART.
      move IF_IBAGT_NEW to LS_BAGTY_BPART-IBAGT.
      RR_BAGTY->ADD_BPART( IS_BAGTY_BPART = LS_BAGTY_BPART ).
    endloop.

***--------------------------------------------------------------------------------------
*** Add all Bag Handling Dimension Calculation
    loop at LR_BAGTY_OLD->T_BAGTY_BGHDC into LS_BAGTY_BGHDC.
      RR_BAGTY->ADD_BGHDC( IS_BAGTY_BGHDC = LS_BAGTY_BGHDC ).
    endloop.

***--------------------------------------------------------------------------------------
*** Add all Handles
    loop at LR_BAGTY_OLD->T_BAGTY_HANDL into LS_BAGTY_HANDL.
      move IF_IBAGT_NEW to LS_BAGTY_HANDL-IBAGT.
      RR_BAGTY->ADD_HANDL( IS_BAGTY_HANDL = LS_BAGTY_HANDL ).
    endloop.

***--------------------------------------------------------------------------------------
*** Add all Converstion Steps
    loop at LR_BAGTY_OLD->T_BAGTY_CSTEP into LS_BAGTY_CSTEP.
      move IF_IBAGT_NEW to LS_BAGTY_CSTEP-IBAGT.
      RR_BAGTY->ADD_CSTEP( IS_BAGTY_CSTEP = LS_BAGTY_CSTEP ).
    endloop.

***--------------------------------------------------------------------------------------
*** Add all Reclosing
    loop at LR_BAGTY_OLD->T_BAGTY_RECLO into LS_BAGTY_RECLO.
      move IF_IBAGT_NEW to LS_BAGTY_RECLO-IBAGT.
      RR_BAGTY->ADD_RECLO( IS_BAGTY_RECLO = LS_BAGTY_RECLO ).
    endloop.

  endmethod.
  method CREATE.

    data:  LF_GUID type /VCXI/CKX_GUID.

    check IF_IBAGT is not initial.

***--------------------------------------------------------------------------------------
*** Check Authorization
    ZCL_VCXI_FBSSC_BAGTY=>CHECK_AUTH( IF_ACTVT = ZCL_VCXI_FBSSC_BAGTY=>C_ACTVT_CREATE
                                      IF_ID    = IF_IBAGT ).

*** Make sure that ID of Bag Type does not exist yet
    LF_GUID = ZCL_VCXI_FBSSC_BAGTY=>ENCRYPT_GUID( IF_IBAGT = IF_IBAGT ).

    read table ZCL_VCXI_FBSSC_BAGTY=>T_INSTANCE with key GUID = LF_GUID
                                                transporting no fields.
    if SY-SUBRC is not initial.

***   Create new Bag Type
      create object RR_BAGTY
        exporting
          IF_IBAGT = IF_IBAGT
          IF_NEW   = ABAP_TRUE.
    else.
***   ID of Bag Type already exist.
      /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE(
          IF_MSGTY = 'E'
          IF_MSGID = 'ZVCXI_FBSSC'
          IF_MSGNO = '052' ).
    endif.

***----------------------------------------------------------------------------
*** Initialization after creation of new object
    RR_BAGTY->INIT_AFTER_CREATE( ).

  endmethod.
  method DECRYPT_GUID.

    CL_ABAP_CONTAINER_UTILITIES=>READ_CONTAINER_C( exporting IM_CONTAINER = IF_GUID
                                                   importing EX_VALUE     = EF_IBAGT ).

    check EF_IBAGT is initial.

*** GUID of ID of Bag Type invalid. (Decrypt not possible)
    /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( IF_MSGTY = 'E'
                                          IF_MSGID = 'ZVCXI_FBSSC'
                                          IF_MSGNO = '051' ).
  endmethod.
  method DELETE_BGDIM.

*** Make sure that object is locked
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

    read table ME->T_BAGTY_BGDIM with key IBAGT = IS_BAGTY_BGDIM-IBAGT
                                          IBDIM = IS_BAGTY_BGDIM-IBDIM
                                          transporting no fields.
    if SY-SUBRC is initial.
      delete ME->T_BAGTY_BGDIM where IBAGT eq IS_BAGTY_BGDIM-IBAGT and
                                     IBDIM eq IS_BAGTY_BGDIM-IBDIM.
    endif.

***--------------------------------------------------------------------------------------
*** Raise Change Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method DELETE_BGHDC.

*** Make sure that object is locked
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

    check LINE_EXISTS( ME->T_BAGTY_BGHDC[ HNDDI = IS_BAGTY_BGHDC-HNDDI
                                          IBPAR = IS_BAGTY_BGHDC-IBPAR
                                          IELEM = IS_BAGTY_BGHDC-IELEM ] ).

*** Delete Entry
    delete ME->T_BAGTY_BGHDC where HNDDI = IS_BAGTY_BGHDC-HNDDI
                              and  IBPAR = IS_BAGTY_BGHDC-IBPAR
                              and  IELEM = IS_BAGTY_BGHDC-IELEM.

***--------------------------------------------------------------------------------------
*** Raise Change Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method DELETE_BPART.

*** Make sure that object is locked
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

    read table ME->T_BAGTY_BPART with key IBAGT = IS_BAGTY_BPART-IBAGT
                                          IBPAR = IS_BAGTY_BPART-IBPAR
                                          transporting no fields.
    if SY-SUBRC is initial.
      delete ME->T_BAGTY_BPART where IBAGT eq IS_BAGTY_BPART-IBAGT and
                                     IBPAR eq IS_BAGTY_BPART-IBPAR.
    endif.

***--------------------------------------------------------------------------------------
*** Raise Change Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method DELETE_BPCAL.

    field-symbols: <S_BAGTY_BPART> type ZVCXI_FBSSC_S_BAGTY_BPART.

*** Make sure that object is locked
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

    read table ME->T_BAGTY_BPART assigning <S_BAGTY_BPART> with key IBPAR = IF_IBPAR.
    if SY-SUBRC is initial.
      read table <S_BAGTY_BPART>-BPCAL with key BPDIM = IS_BAGTY_BPCAL-BPDIM
                                                IBDIM = IS_BAGTY_BPCAL-IBDIM
                                                transporting no fields.
      if SY-SUBRC is initial.
        delete <S_BAGTY_BPART>-BPCAL where BPDIM eq IS_BAGTY_BPCAL-BPDIM and
                                           IBDIM eq IS_BAGTY_BPCAL-IBDIM.
      endif.
    endif.

***--------------------------------------------------------------------------------------
*** Raise Change Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method DELETE_CSTEP.

*** Make sure that object is locked
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

    read table ME->T_BAGTY_CSTEP with key IBAGT = IS_BAGTY_CSTEP-IBAGT
                                          ICSTP = IS_BAGTY_CSTEP-ICSTP
                                          transporting no fields.
    if SY-SUBRC is initial.
      delete ME->T_BAGTY_CSTEP where IBAGT eq IS_BAGTY_CSTEP-IBAGT and
                                     ICSTP eq IS_BAGTY_CSTEP-ICSTP.

      if SY-SUBRC is initial.
        if CHECK_CSTEP( ZCL_VCXI_FBSSC_BAGTY=>C_CSTEP_RECLO ) ne ABAP_TRUE.
          ME->DELETE_RECLO_ALL( ).
        endif.
      endif.
    endif.

***--------------------------------------------------------------------------------------
*** Raise Change Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method DELETE_HANDL.

*** Make sure that object is locked
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

    read table ME->T_BAGTY_HANDL with key IBAGT = IS_BAGTY_HANDL-IBAGT
                                          THNDL = IS_BAGTY_HANDL-THNDL
                                          transporting no fields.
    if SY-SUBRC is initial.
      delete ME->T_BAGTY_HANDL where IBAGT eq IS_BAGTY_HANDL-IBAGT and
                                     THNDL eq IS_BAGTY_HANDL-THNDL.
    endif.

***--------------------------------------------------------------------------------------
*** Raise Change Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method DELETE_RECLO.

*** Make sure that object is locked
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

    read table ME->T_BAGTY_RECLO with key IBAGT = IS_BAGTY_RECLO-IBAGT
                                          TRECL = IS_BAGTY_RECLO-TRECL
                                          transporting no fields.
    if SY-SUBRC is initial.
      delete ME->T_BAGTY_RECLO where IBAGT eq IS_BAGTY_RECLO-IBAGT and
                                     TRECL eq IS_BAGTY_RECLO-TRECL.
    endif.

***--------------------------------------------------------------------------------------
*** Raise Change Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method DELETE_RECLO_ALL.

*** Make sure that object is locked
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

    refresh ME->T_BAGTY_RECLO.

***--------------------------------------------------------------------------------------
*** Raise Change Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method ENCRYPT_GUID.

    CL_ABAP_CONTAINER_UTILITIES=>FILL_CONTAINER_C( exporting IM_VALUE     = IF_IBAGT
                                                   importing EX_CONTAINER = RF_GUID ).

  endmethod.
  method GET_IBPAR_DESCR.

    data: LS_TC020T type ZVCXI_FBS_TC020T.

    read table T_TC020T into LS_TC020T with key IBPAR = IF_IBPAR.
    if SY-SUBRC is initial.
      move LS_TC020T-DESCR to RF_DESCR.
    else.
      move IF_IBPAR to RF_DESCR.
    endif.

  endmethod.
  method GET_IELEM_DESCR.

    RF_DESCR = switch #( IF_IELEM when C_IELEM_BP_LENGTH then text-BPL
                                  when C_IELEM_BP_WIDTH  then text-BPW
                                  when C_IELEM_BP_THICK  then text-BPT
                                  else |{ IF_IELEM } { value #( ZCL_VCXI_FBSSC_BAGTY=>T_TC010T[ IBDIM = IF_IELEM ]-DESCR optional ) }| ).

  endmethod.
  method HANDLE_CHANGED.

    data:  LR_CX_CKX type ref to /VCXI/CX_CKX.

    try.
        move SY-UNAME     to ME->S_LOG-CHABY.
        get time stamp field ME->S_LOG-CHAAT.

***     Set the Change Indicator
        move ABAP_TRUE to ME->F_CHANGED.

***     Transport
        if ME->F_CCCORACTIV eq '1'.
          move ABAP_TRUE to ME->F_TRANS_REQ.
        endif.

      catch /VCXI/CX_CKX into LR_CX_CKX.
        LR_CX_CKX->SHOW_MESSAGE( ).
    endtry.

  endmethod.
  method HANDLE_CHECK.

    data: LS_BGDIM  type ZVCXI_FBSSC_S_BAGTY_BGDIM,
          LS_BPART  type ZVCXI_FBSSC_S_BAGTY_BPART,
          LS_HANDL  type ZVCXI_FBSSC_S_BAGTY_HANDL,
          LS_RECLO  type ZVCXI_FBSSC_S_BAGTY_RECLO,
          LS_CSTEP  type ZVCXI_FBSSC_S_BAGTY_CSTEP.
    data: LR_MESSAGE type ref to /VCXI/CL_CKXM_MESSAGE.

    create object LR_MESSAGE.

***--------------------------------------------------------------------------------------
*** Check Bag Dimensions Elements
    loop at ME->T_BAGTY_BGDIM into LS_BGDIM.
***   Bag Dimension '&1' does not exist.
      read table ME->T_TC010 with key IBDIM = LS_BGDIM-IBDIM transporting no fields.
      if  SY-SUBRC ne 0.
        LR_MESSAGE->ADD_MESSAGE( exporting  IF_MSGID = 'ZVCXI_FBSSC'
                                            IF_MSGTY = 'E'
                                            IF_MSGNO = '402'
                                            IF_MSGV1 =  LS_BGDIM-IBDIM ).
        continue.
      endif.

***   Conversion Ctep Wicketing is mandatory
      break HARDCODED_VALUE_W_LIP.
      if LS_BGDIM-IBDIM eq 'W_LIP'.
        read table ME->T_BAGTY_CSTEP with key ICSTP = 'CSTEP_WICKET' transporting no fields.
        if SY-SUBRC ne 0.
          LR_MESSAGE->ADD_MESSAGE( exporting  IF_MSGID = 'ZVCXI_FBSSC'
                                              IF_MSGTY = 'E'
                                              IF_MSGNO = '403' ).
        endif.
      endif.
    endloop.

***--------------------------------------------------------------------------------------
*** Check Bag Parts
    loop at ME->T_BAGTY_BPART into LS_BPART.
***   Bag Part '&1' does not exist.
      read table ME->T_TC020 with key IBPAR = LS_BPART-IBPAR transporting no fields.
      if  SY-SUBRC ne 0.
        LR_MESSAGE->ADD_MESSAGE( exporting  IF_MSGID = 'ZVCXI_FBSSC'
                                            IF_MSGTY = 'E'
                                            IF_MSGNO = '407'
                                            IF_MSGV1 =  LS_BPART-IBPAR ).
        continue.
      endif.

      if LS_BPART-ODIRL is initial and
         LS_BPART-ODIRT is initial.
***     Select at least one Bag Part Direction for '&1'
        LR_MESSAGE->ADD_MESSAGE( exporting  IF_MSGID = 'ZVCXI_FBSSC'
                                            IF_MSGTY = 'E'
                                            IF_MSGNO = '404'
                                            IF_MSGV1 =  ME->GET_IBPAR_DESCR( LS_BPART-IBPAR ) ).
      endif.
    endloop.

***--------------------------------------------------------------------------------------
*** Check Handles
    loop at ME->T_BAGTY_HANDL into LS_HANDL.
***   Handle '&1' does not exist.
      read table ME->T_TC110 with key THNDL = LS_HANDL-THNDL transporting no fields.
      if  SY-SUBRC ne 0.
        LR_MESSAGE->ADD_MESSAGE( exporting  IF_MSGID = 'ZVCXI_FBSSC'
                                            IF_MSGTY = 'E'
                                            IF_MSGNO = '405'
                                            IF_MSGV1 =  LS_HANDL-THNDL ).
        continue.
      endif.
    endloop.

***--------------------------------------------------------------------------------------
*** Check Conversion Steps
    loop at ME->T_BAGTY_CSTEP into LS_CSTEP.
***   Conversion Step '&1' does not exist.
      read table ME->T_TC030 with key ICSTP = LS_CSTEP-ICSTP transporting no fields.
      if  SY-SUBRC ne 0.
        LR_MESSAGE->ADD_MESSAGE( exporting  IF_MSGID = 'ZVCXI_FBSSC'
                                            IF_MSGTY = 'E'
                                            IF_MSGNO = '408'
                                            IF_MSGV1 =  LS_CSTEP-ICSTP ).
        continue.
      endif.
    endloop.

***--------------------------------------------------------------------------------------
*** Check Reclosing
    loop at ME->T_BAGTY_RECLO into LS_RECLO.
***   Reclosing '&1' does not exist.
      read table ME->T_TC210 with key TRECL = LS_RECLO-TRECL transporting no fields.
      if  SY-SUBRC ne 0.
        LR_MESSAGE->ADD_MESSAGE( exporting  IF_MSGID = 'ZVCXI_FBSSC'
                                            IF_MSGTY = 'E'
                                            IF_MSGNO = '406'
                                            IF_MSGV1 =  LS_RECLO-TRECL ).
        continue.
      endif.
    endloop.

***--------------------------------------------------------------------------------------
*** Show messages
    if LR_MESSAGE->T_MSG is not initial.
      LR_MESSAGE->DISPLAY_MSG( ).

      read table LR_MESSAGE->T_MSG with key MSGTY = 'E' transporting no fields.
      if SY-SUBRC eq 0.

***     Bag Type can't be saved.
        /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( exporting IF_MSGID = 'ZVCXI_FBSSC'
                                                        IF_MSGTY = 'E'
                                                        IF_MSGNO = '401' ).

      endif.
    endif.

  endmethod.
  method HANDLE_DELETE.

***--------------------------------------------------------------------------------------
*** Check Authorization
    ZCL_VCXI_FBSSC_BAGTY=>CHECK_AUTH( IF_ACTVT = ZCL_VCXI_FBSSC_BAGTY=>C_ACTVT_DELETE
                                      IR_BAGTY = ME ).

*** If not locked try to lock
    if ME->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ) ne ABAP_TRUE.
      ME->/VCXI/IF_CKX_OBJECT_LOCKING~DO_LOCK( ).
    endif.

*** Mark as deleted
    move ABAP_TRUE to ME->F_DELETED.
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method HANDLE_FREE.

    data:  LF_GUID type /VCXI/CKX_GUID.

***--------------------------------------------------------------------------------------
*** Remove Instance from Buffer
    LF_GUID = ME->/VCXI/IF_CKX_OBJECT~GET_GUID( ).
    delete ZCL_VCXI_FBSSC_BAGTY=>T_INSTANCE where GUID = LF_GUID.

  endmethod.
  method HANDLE_SAVE.

    data: LF_KORRNR         type        TRKORR.
    data: LR_CX_CKX         type ref to /VCXI/CX_CKX.

    try.
***--------------------------------------------------------------------------------------
***     Popup to pick Transport
        if ME->F_TRANS_REQ eq ABAP_TRUE.
          call function 'G_TRANS_NUMBER'
            exporting
              CATEGORY  = 'CUST'
            importing
              KORRNR    = LF_KORRNR
            exceptions
              NO_NUMBER = 1
              others    = 2.
          if SY-SUBRC ne 0.
            /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
          endif.

***       Open Transport
          call function 'ZVCXI_FBSSC_DB_OPEN_TRANSPORT'
            in update task
            exporting
              IF_TRKORR = LF_KORRNR.
        endif.

***     Save only if changed
        check ME->F_CHANGED eq ABAP_TRUE.

***--------------------------------------------------------------------------------------
***     Set Changed Info to Log
        move SY-UNAME     to ME->S_LOG-CHABY.
        get time stamp field ME->S_LOG-CHAAT.

***--------------------------------------------------------------------------------------
        if ME->F_DELETED eq ABAP_FALSE.
***       Save Bag Type
          call function 'ZVCXI_FBSSC_DB_SAVE_BAGTY'
            in update task
            exporting
              IF_IBAGT       = ME->F_IBAGT
              IT_BAGTY_BGDIM = ME->T_BAGTY_BGDIM
              IT_BAGTY_BPART = ME->T_BAGTY_BPART
              IT_BAGTY_BGHDC = ME->T_BAGTY_BGHDC
              IT_BAGTY_HANDL = ME->T_BAGTY_HANDL
              IT_BAGTY_CSTEP = ME->T_BAGTY_CSTEP
              IT_BAGTY_RECLO = ME->T_BAGTY_RECLO
              IS_LOG         = ME->S_LOG
              IT_DESCR       = ME->T_DESCR
              IF_WRITE_TR    = ME->F_TRANS_REQ.
        else.
***       Delete Bag Type
          call function 'ZVCXI_FBSSC_DB_DELETE_BAGTY'
            in update task
            exporting
              IF_IBAGT    = ME->F_IBAGT
              IF_WRITE_TR = ME->F_TRANS_REQ.
        endif.

***--------------------------------------------------------------------------------------
***     Write Transport
        call function 'ZVCXI_FBSSC_DB_CLOSE_TRANSPORT'
          in update task.

***--------------------------------------------------------------------------------------
***     All changes are handled
        move ABAP_FALSE to ME->F_CHANGED.

        call function 'BAPI_TRANSACTION_COMMIT'
          exporting
            WAIT = ABAP_TRUE.

***--------------------------------------------------------------------------------------
***     Relock all locked Objects after Commit
        ME->/VCXI/IF_CKX_OBJECT_LOCKING~DO_LOCK( ).

      catch /VCXI/CX_CKX into LR_CX_CKX.
        call function 'BAPI_TRANSACTION_ROLLBACK'.
        raise exception LR_CX_CKX.
    endtry.

  endmethod.
  method INIT_AFTER_CREATE.

    field-symbols: <S_BAGTY_BGDIM> type ZVCXI_FBSSC_S_BAGTY_BGDIM.

*** Set Lock for new object
    ME->SET_LOCK( IF_LOCKED = ABAP_TRUE ).

    move SY-UNAME     to ME->S_LOG-CREBY.
    get time stamp field ME->S_LOG-CREAT.

    raise event /VCXI/IF_CKX_OBJECT~CREATED.
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

*** Init Default values for Bag Dimension Elements
    insert initial line into table ME->T_BAGTY_BGDIM assigning <S_BAGTY_BGDIM>.
    move: ME->F_IBAGT   to <S_BAGTY_BGDIM>-IBAGT,
          C_BGDIM_BGHEI to <S_BAGTY_BGDIM>-IBDIM,
          'M'           to <S_BAGTY_BGDIM>-OBDIM.

    insert initial line into table ME->T_BAGTY_BGDIM assigning <S_BAGTY_BGDIM>.
    move: ME->F_IBAGT   to <S_BAGTY_BGDIM>-IBAGT,
          C_BGDIM_BGWID to <S_BAGTY_BGDIM>-IBDIM,
          'M'           to <S_BAGTY_BGDIM>-OBDIM.

  endmethod.
  method LOAD_BASIC_CUST.

    refresh:  T_TC010,
              T_TC010T,
              T_TC110,
              T_TC110T,
              T_TC112,
              T_TC111T,
              T_TC030,
              T_TC030T,
              T_TC210,
              T_TC210T,
              T_TC211,
              T_TC211T,
              T_TC020,
              T_TC020T.

***--------------------------------------------------------------------------------------
*** Buffer Bag Type - Dimension Element
    select * from ZVCXI_FBS_TC010
             into table T_TC010.                                     "#EC CI_SUBRC

***--------------------------------------------------------------------------------------
*** Buffer Bag Type - Dimension Element (Text)
    select * from ZVCXI_FBS_TC010T
             into table T_TC010T
            where LANGU eq SY-LANGU.                                 "#EC CI_SUBRC

***--------------------------------------------------------------------------------------
*** Buffer Bag Type - Handles
    select * from ZVCXI_FBS_TC110
             into table T_TC110.                                     "#EC CI_SUBRC

***--------------------------------------------------------------------------------------
*** Buffer Bag Type - Handles (Text)
    select * from ZVCXI_FBS_TC110T
             into table T_TC110T
            where LANGU eq SY-LANGU.                                 "#EC CI_SUBRC

***--------------------------------------------------------------------------------------
*** Buffer Handle Types - Handle Parts
    select * from ZVCXI_FBS_TC112
             into table T_TC112.                                     "#EC CI_SUBRC

***--------------------------------------------------------------------------------------
*** Buffer Handle Parts (Text)
    select * from ZVCXI_FBS_TC111T
             into table T_TC111T
            where LANGU eq SY-LANGU.                                 "#EC CI_SUBRC

***--------------------------------------------------------------------------------------
*** Buffer Bag Type - Conversion Steps
    select * from ZVCXI_FBS_TC030
             into table T_TC030.                                     "#EC CI_SUBRC

***--------------------------------------------------------------------------------------
*** Buffer Bag Type - Conversion Steps (Text)
    select * from ZVCXI_FBS_TC030T
             into table T_TC030T
            where LANGU eq SY-LANGU.                                 "#EC CI_SUBRC

***--------------------------------------------------------------------------------------
*** Buffer Bag Type - Reclosing
    select * from ZVCXI_FBS_TC210
             into table T_TC210.                                     "#EC CI_SUBRC

***--------------------------------------------------------------------------------------
*** Buffer Bag Type - Reclosing (Text)
    select * from ZVCXI_FBS_TC210T
             into table T_TC210T
            where LANGU eq SY-LANGU.                                 "#EC CI_SUBRC

***--------------------------------------------------------------------------------------
*** Buffer Reclosing Part
    select * from ZVCXI_FBS_TC211
             into table T_TC211.                                     "#EC CI_SUBRC

***--------------------------------------------------------------------------------------
*** Buffer Reclosing Part (Text)
    select * from ZVCXI_FBS_TC211T
             into table T_TC211T
            where LANGU eq SY-LANGU.                                 "#EC CI_SUBRC

***--------------------------------------------------------------------------------------
*** Buffer Bag Type - Bag Part
    select * from ZVCXI_FBS_TC020
             into table T_TC020
             order by BPSEQ.                                         "#EC CI_SUBRC

***--------------------------------------------------------------------------------------
*** Buffer Bag Type - Bag Part (Text)
    select * from ZVCXI_FBS_TC020T
             into table T_TC020T
            where LANGU eq SY-LANGU.                                 "#EC CI_SUBRC

  endmethod.
  method LOAD_FROM_DB.

    data: LS_TC521  type ZVCXI_FBS_TC521,
          LS_TC500  type ZVCXI_FBS_TC500,
          LS_TC500T type ZVCXI_FBS_TC500T,
          LT_TC521  type table of ZVCXI_FBS_TC521.
    field-symbols: <S_BPART> type ZVCXI_FBSSC_S_BAGTY_BPART,
                   <S_BPCAL> type ZVCXI_FBSSC_S_BAGTY_BPCAL.

    clear: ME->S_LOG, ME->F_CHANGED, ME->F_NOTVALID.
    refresh: ME->T_DESCR.

***--------------------------------------------------------------------------------------
*** Load Header
    select single * from ZVCXI_FBS_TC500
      into corresponding fields of LS_TC500
      where IBAGT eq ME->F_IBAGT.
    if SY-SUBRC is not initial.
      move ABAP_TRUE to ME->F_NOTVALID.
      return.
    endif.

*** Log
    move LS_TC500-S_LOG to ME->S_LOG.

***--------------------------------------------------------------------------------------
*** Load Description
    select LANGU DESCR
           from ZVCXI_FBS_TC500T
           into table ME->T_DESCR
          where IBAGT eq ME->F_IBAGT.                                  "#EC CI_SUBRC

*** Bag Type  Dimension Elements
    select IBAGT IBDIM OBDIM
           from ZVCXI_FBS_TC510
           into table ME->T_BAGTY_BGDIM
          where IBAGT eq ME->F_IBAGT.                                  "#EC CI_SUBRC

***--------------------------------------------------------------------------------------
*** Bag Type - Bag Part
    select IBAGT IBPAR ODIRL ODIRT BPDIR
           from ZVCXI_FBS_TC520 ##TOO_MANY_ITAB_FIELDS
           into corresponding fields of table ME->T_BAGTY_BPART
          where IBAGT eq ME->F_IBAGT.

    if SY-SUBRC is initial.
***   Retrieve Bag Type - Bag Part Calculation data
      select * from  ZVCXI_FBS_TC521
               into table LT_TC521
               where IBAGT eq ME->F_IBAGT.

***   Fill Bag Type - Bag Part Calculation data
      loop at ME->T_BAGTY_BPART assigning <S_BPART>.
        loop at LT_TC521 into LS_TC521 where IBAGT eq <S_BPART>-IBAGT and
                                             IBPAR eq <S_BPART>-IBPAR.
          insert initial line into table <S_BPART>-BPCAL assigning <S_BPCAL>.
          move: LS_TC521-BPDIM to <S_BPCAL>-BPDIM,
                LS_TC521-IBDIM to <S_BPCAL>-IBDIM,
                LS_TC521-OPER  to <S_BPCAL>-OPER,
                LS_TC521-ELEMN to <S_BPCAL>-ELEMN,
                LS_TC521-ELEMD to <S_BPCAL>-ELEMD.
        endloop.
      endloop.
    endif.

***--------------------------------------------------------------------------------------
*** Bag Type  Handle Type
    select IBAGT THNDL
           from ZVCXI_FBS_TC530
           into table ME->T_BAGTY_HANDL
          where IBAGT eq ME->F_IBAGT.                                  "#EC CI_SUBRC

***--------------------------------------------------------------------------------------
*** Bag Type  Conversion Step
    select IBAGT ICSTP QTYMI QTYMA
           from ZVCXI_FBS_TC540
           into table ME->T_BAGTY_CSTEP
          where IBAGT eq ME->F_IBAGT.                                  "#EC CI_SUBRC

***--------------------------------------------------------------------------------------
*** Bag Type  Reclosing Type
    select IBAGT TRECL
           from ZVCXI_FBS_TC550
           into table ME->T_BAGTY_RECLO
          where IBAGT eq ME->F_IBAGT.                                  "#EC CI_SUBRC

***--------------------------------------------------------------------------------------
*** Bag Type  Bag Handling Dimension Calculation
    select HNDDI IBPAR IELEM OPER ELEMN ELEMD
           from ZVCXI_FBS_TC560
           into table ME->T_BAGTY_BGHDC
          where IBAGT eq ME->F_IBAGT.                                  "#EC CI_SUBRC

  endmethod.
  method SET_DESCR.

    field-symbols: <S_DESCR> type /VCXI/CKX_S_DESCR.

*** Check of the Lock-Status of the Object
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

*** Set Description
    read table ME->T_DESCR assigning <S_DESCR> with key LANGU = IF_LANGU.
    if SY-SUBRC is not initial.
      insert initial line into table ME->T_DESCR assigning <S_DESCR>.
      move IF_LANGU to <S_DESCR>-LANGU.
    endif.
    move IF_DESCR   to <S_DESCR>-DESCR.

*** Raise Changed Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method SET_LOCK.

    data:  LR_CX_CKX type ref to /VCXI/CX_CKX.

    try.
***--------------------------------------------------------------------------------------
        if IF_LOCKED eq ABAP_TRUE.
***       Try to Set Write Lock
          call function 'ENQUEUE_EZVCXI_FBS_C500E'
            exporting
              IBAGT          = ME->F_IBAGT
            exceptions
              FOREIGN_LOCK   = 1
              SYSTEM_FAILURE = 2.

          if SY-SUBRC is not initial.
            /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
          endif.

***--------------------------------------------------------------------------------------
        else.
***       Remove Write Lock
          call function 'DEQUEUE_EZVCXI_FBS_C500E'
            exporting
              IBAGT = ME->F_IBAGT.

        endif.

        move IF_LOCKED to ME->F_LOCKED.

      catch /VCXI/CX_CKX into LR_CX_CKX.
        ME->/VCXI/IF_CKX_OBJECT_LOCKING~DO_UNLOCK( ).
        raise exception LR_CX_CKX.
    endtry.

  endmethod.
  method UPDATE_BGDIM.

    field-symbols: <S_BAGTY_BGDIM> type ZVCXI_FBSSC_S_BAGTY_BGDIM.

*** Make sure that object is locked
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

    read table ME->T_BAGTY_BGDIM assigning <S_BAGTY_BGDIM> with key IBAGT = IS_BAGTY_BGDIM-IBAGT
                                                                    IBDIM = IS_BAGTY_BGDIM-IBDIM.
    if SY-SUBRC is initial.
      move: IS_BAGTY_BGDIM-IBAGT to <S_BAGTY_BGDIM>-IBAGT,
            IS_BAGTY_BGDIM-IBDIM to <S_BAGTY_BGDIM>-IBDIM,
            IS_BAGTY_BGDIM-OBDIM to <S_BAGTY_BGDIM>-OBDIM.
    endif.

***--------------------------------------------------------------------------------------
*** Raise Change Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method UPDATE_BGHDC.

*** Make sure that object is locked
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

    check LINE_EXISTS( ME->T_BAGTY_BGHDC[ HNDDI = IS_BAGTY_BGHDC-HNDDI
                                          IBPAR = IS_BAGTY_BGHDC-IBPAR
                                          IELEM = IS_BAGTY_BGHDC-IELEM ] ).

*** Update Entry
    ME->T_BAGTY_BGHDC[ HNDDI = IS_BAGTY_BGHDC-HNDDI
                       IBPAR = IS_BAGTY_BGHDC-IBPAR
                       IELEM = IS_BAGTY_BGHDC-IELEM ] = IS_BAGTY_BGHDC.

***--------------------------------------------------------------------------------------
*** Raise Change Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method UPDATE_BPART.

    field-symbols: <S_BAGTY_BPART> type ZVCXI_FBSSC_S_BAGTY_BPART.

*** Make sure that object is locked
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

    read table ME->T_BAGTY_BPART assigning <S_BAGTY_BPART> with key IBAGT = IS_BAGTY_BPART-IBAGT
                                                                    IBPAR = IS_BAGTY_BPART-IBPAR.
    if SY-SUBRC is initial.
      move: IS_BAGTY_BPART-IBAGT to <S_BAGTY_BPART>-IBAGT,
            IS_BAGTY_BPART-IBPAR to <S_BAGTY_BPART>-IBPAR,
            IS_BAGTY_BPART-ODIRL to <S_BAGTY_BPART>-ODIRL,
            IS_BAGTY_BPART-ODIRT to <S_BAGTY_BPART>-ODIRT,
            IS_BAGTY_BPART-BPDIR to <S_BAGTY_BPART>-BPDIR,
            IS_BAGTY_BPART-BPCAL[] to <S_BAGTY_BPART>-BPCAL[].
    endif.

***--------------------------------------------------------------------------------------
*** Raise Change Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method UPDATE_BPCAL.

    field-symbols: <S_BAGTY_BPART> type ZVCXI_FBSSC_S_BAGTY_BPART,
                   <S_BAGTY_BPCAL> type ZVCXI_FBSSC_S_BAGTY_BPCAL.

*** Make sure that object is locked
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

    read table ME->T_BAGTY_BPART assigning <S_BAGTY_BPART> with key IBPAR = IF_IBPAR.
    if SY-SUBRC is initial.
      read table <S_BAGTY_BPART>-BPCAL assigning <S_BAGTY_BPCAL> with key BPDIM = IS_BAGTY_BPCAL-BPDIM
                                                                          IBDIM = IS_BAGTY_BPCAL-IBDIM.
      if SY-SUBRC is initial.
        move: IS_BAGTY_BPCAL-BPDIM to <S_BAGTY_BPCAL>-BPDIM,
              IS_BAGTY_BPCAL-IBDIM to <S_BAGTY_BPCAL>-IBDIM,
              IS_BAGTY_BPCAL-OPER  to <S_BAGTY_BPCAL>-OPER,
              IS_BAGTY_BPCAL-ELEMN  to <S_BAGTY_BPCAL>-ELEMN,
              IS_BAGTY_BPCAL-ELEMD  to <S_BAGTY_BPCAL>-ELEMD.
      endif.
    endif.

***--------------------------------------------------------------------------------------
*** Raise Change Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method UPDATE_CSTEP.

    field-symbols: <S_BAGTY_CSTEP> type ZVCXI_FBSSC_S_BAGTY_CSTEP.

*** Make sure that object is locked
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

    read table ME->T_BAGTY_CSTEP assigning <S_BAGTY_CSTEP> with key IBAGT = IS_BAGTY_CSTEP-IBAGT
                                                                    ICSTP = IS_BAGTY_CSTEP-ICSTP.
    if SY-SUBRC is initial.
      move: IS_BAGTY_CSTEP-IBAGT to <S_BAGTY_CSTEP>-IBAGT,
            IS_BAGTY_CSTEP-ICSTP to <S_BAGTY_CSTEP>-ICSTP,
            IS_BAGTY_CSTEP-QTYMI to <S_BAGTY_CSTEP>-QTYMI,
            IS_BAGTY_CSTEP-QTYMA to <S_BAGTY_CSTEP>-QTYMA.
    endif.

***--------------------------------------------------------------------------------------
*** Raise Change Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method UPDATE_HANDL.

    field-symbols: <S_BAGTY_HANDL> type ZVCXI_FBSSC_S_BAGTY_HANDL.

*** Make sure that object is locked
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

    read table ME->T_BAGTY_HANDL assigning <S_BAGTY_HANDL> with key IBAGT = IS_BAGTY_HANDL-IBAGT
                                                                    THNDL = IS_BAGTY_HANDL-THNDL.
    if SY-SUBRC is initial.
      move: IS_BAGTY_HANDL-IBAGT to <S_BAGTY_HANDL>-IBAGT,
            IS_BAGTY_HANDL-THNDL to <S_BAGTY_HANDL>-THNDL.
    endif.

***--------------------------------------------------------------------------------------
*** Raise Change Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method UPDATE_RECLO.

    field-symbols: <S_BAGTY_RECLO> type ZVCXI_FBSSC_S_BAGTY_RECLO.

*** Make sure that object is locked
    ME->/VCXI/IF_CKX_OBJECT_LOCKING~CHECK_IS_LOCKED( ).

    read table ME->T_BAGTY_RECLO assigning <S_BAGTY_RECLO> with key IBAGT = IS_BAGTY_RECLO-IBAGT
                                                                    TRECL = IS_BAGTY_RECLO-TRECL.
    if SY-SUBRC is initial.
      move: IS_BAGTY_RECLO-IBAGT to <S_BAGTY_RECLO>-IBAGT,
            IS_BAGTY_RECLO-TRECL to <S_BAGTY_RECLO>-TRECL.
    endif.

***--------------------------------------------------------------------------------------
*** Raise Change Event
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
