
class ZCL_VCXI_P4SS_UI_FDMDL definition
  public
  inheriting from ZCL_VCXI_PXSS_UI_FDMDL
  create public .

public section.

  interfaces /VCXI/IF_VKSU_UIFW .

  data S_DYNP_PRDTL type /VCXI/CKX_S_DYNP_KEY read-only .
  data S_FDMDL_VIS type ZVCXI_P4SS_S_FDMDL_VIS read-only .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_PBO
    redefinition .
  methods GET_REGISTERED_UCOMM
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  data R_GRID_PSCALE type ref to ZCL_VCXI_XCS_GRID_PSCALE .
  data R_UI_FDMDL_PRDTL type ref to ZCL_VCXI_PXSS_UI_FDMDL .

  methods INIT_DYNP_PRDTL
    raising
      /VCXI/CX_CKX .
  methods GET_PRVDR_PRFG
    exporting
      !ER_GATE type ref to /VCXI/CL_VKSR_GATE
    returning
      value(RR_PR_PRFG) type ref to ZCL_VCXI_P4SS_PR_PRFG
    raising
      /VCXI/CX_CKX .
  methods MODIFY_SCREEN_PRFG
    changing
      !CS_SCREEN type SCREEN
    raising
      /VCXI/CX_CKX .
  methods OPEN_SUBSCREEN
    importing
      !IR_BL_FDMDL type ref to ZCL_VCXI_PXSS_BL_FDMDL
    returning
      value(RR_UI_FDMDL) type ref to ZCL_VCXI_PXSS_UI_FDMDL
    raising
      /VCXI/CX_CKX .
  methods MODIFY_SCREEN_SPLR
    changing
      !CS_SCREEN type SCREEN
    raising
      /VCXI/CX_CKX .

  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_P4SS_UI_FDMDL definition
class ZCL_VCXI_P4SS_UI_FDMDL implementation.
  method /VCXI/IF_VKSU_UIFW~DO_MODIFY_SCREEN.

    if ME->R_UI_FDMDL_PRDTL is bound.
      ME->R_UI_FDMDL_PRDTL->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                              IF_DYNNR = IF_DYNNR ).
    endif.

  endmethod.
  method /VCXI/IF_VKSU_UIFW~DO_USER_COMMAND.

    try.
        if ME->R_UI_FDMDL_PRDTL is bound.
          ME->R_UI_FDMDL_PRDTL->DO_USER_COMMAND( IF_UCOMM = CF_UCOMM ).
        endif.

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        LR_CX_CKX->SHOW_MESSAGE( ).
    endtry.

  endmethod.
  method /VCXI/IF_VKSU_UIFW~REFRESH_SCREEN.

    ME->R_UIFW->REFRESH_SCREEN( ).

  endmethod.
  method /VCXI/IF_VKSU_UIFW~REGISTER_UI.

    do.
      if ME->R_UI_FDMDL_PRDTL is not bound.
        exit.
      endif.
      ME->R_UI_FDMDL_PRDTL->DEACTIVATE( ).
    enddo.

    move IR_UI ?to ME->R_UI_FDMDL_PRDTL.

  endmethod.
  method /VCXI/IF_VKSU_UIFW~UNREGISTER_UI.

    if IR_UI eq ME->R_UI_FDMDL_PRDTL.
      clear ME->R_UI_FDMDL_PRDTL.
    endif.

  endmethod.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

***--------------------------------------------------------------------------------------
*** Bind UI to Dynpro
    call function 'ZVCXI_P4SS_UI_FDMDL_BIND'
      exporting
        IR_UI_FDMDL   = ME
      importing
        ES_DYNP       = ME->S_DYNP
        ES_DYNP_BLANK = ME->S_DYNP_BLANK.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

*** Deactivate Subscreen
    if ME->R_UI_FDMDL_PRDTL is bound.
      ME->R_UI_FDMDL_PRDTL->DEACTIVATE( ).
    endif.

***--------------------------------------------------------------------------------------
*** Free Price Scale Grid
    if ME->R_GRID_PSCALE is bound.
      ME->R_GRID_PSCALE->DO_FREE( ).
      clear ME->R_GRID_PSCALE.
    endif.

  endmethod.
  method DO_MODIFY_SCREEN.

    try.
        SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                 IF_DYNNR = IF_DYNNR ).

***------------------------------------------------------------------------------------------------
        loop at screen.
          case SCREEN-GROUP1.
***         Finished Good Information
            when 'PFG'.
              ME->MODIFY_SCREEN_PRFG( changing CS_SCREEN = SCREEN ).
***         Supplier Information
            when 'SPL'.
              ME->MODIFY_SCREEN_SPLR( changing CS_SCREEN = SCREEN ).

          endcase.
          modify screen.
        endloop.

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_PBO.

    data: LR_GATE type ref to /VCXI/CL_VKSR_GATE.


    check ME->R_GATE_OUT is bound and
          ME->R_GATE_OUT->F_FREED is initial.

*** Call SUPER
    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Refresh Price Scale Grid
    if ME->S_FDMDL_VIS-PURCH eq ZCL_VCXI_P4SS_PR_PRFG=>C_PURCH_DIRECT_PRICE.
***   Create Grid for Price Scale
      if ME->R_GRID_PSCALE is not bound.
***     Get Supplier Information
        case type of ME->R_GATE_OUT->R_CLSPR.
          when type ZCL_VCXI_P4SS_PR_PRFG into data(LR_PR_PRFG).
            LR_PR_PRFG->GET_PRVDR_SPLR( importing ER_GATE = LR_GATE ).
          when type ZCL_VCXI_P4SS_PR_PRPART into data(LR_PR_PRPART).
            LR_PR_PRPART->GET_PRVDR_SPLR( importing ER_GATE = LR_GATE ).
        endcase.
        check LR_GATE is bound.

        create object ME->R_GRID_PSCALE
          exporting
            IR_GATE           = LR_GATE
            IR_UIGATE         = ME->R_UIGATE
            IF_CONTAINER_NAME = '0110CC_PSCALE'
            IF_APPL_EVENTS    = ME->F_APPL_EVENTS.
      endif.
***   Refresh Grid
      ME->R_GRID_PSCALE->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_PSCALE->REFRESH_GRID( ).
    else.
      if ME->R_GRID_PSCALE is bound.
        ME->R_GRID_PSCALE->DO_FREE( ).
        clear ME->R_GRID_PSCALE.
      endif.
    endif.

***--------------------------------------------------------------------------------------
*** Open Subscreen for Procurement Detail
    if ME->R_UI_FDMDL_PRDTL is not bound or
       ( ME->R_UI_FDMDL_PRDTL is bound and
         ME->R_UI_FDMDL_PRDTL->R_BL_FDMDL ne cast ZCL_VCXI_P4SS_BL_FDMDL( ME->R_BL_FDMDL )->R_BL_FDMDL_PRDTL ).
      ME->OPEN_SUBSCREEN( IR_BL_FDMDL = cast ZCL_VCXI_P4SS_BL_FDMDL( ME->R_BL_FDMDL )->R_BL_FDMDL_PRDTL ).
    endif.

*** Initialize Procurement Detail Screen Information
    ME->INIT_DYNP_PRDTL( ).

  endmethod.
  method DO_USER_COMMAND.

    data:  LF_UCOMM      type SYUCOMM.


***--------------------------------------------------------------------------------------
*** Process Changes in Grid
    if ME->R_GRID_PSCALE is bound.
      ME->R_GRID_PSCALE->CHECK_CHANGED_DATA( ).
    endif.

***--------------------------------------------------------------------------------------
*** Call SUPER
    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    move IF_UCOMM to LF_UCOMM.
    ME->/VCXI/IF_VKSU_UIFW~DO_USER_COMMAND( changing CF_UCOMM = LF_UCOMM ).

  endmethod.
  method GET_PRVDR_PRFG.

    data: LS_FASTD_XCOMP type ZVCXI_PXSS_S_FASTD_XCOMP,
          LS_DATA        type ZVCXI_PXSS_S_FASTD_XCOMP_DATA.
    data: LT_FASTD_XCOMP type ZVCXI_PXSS_TT_FASTD_XCOMP.


***------------------------------------------------------------------------------------------------
*** Get Configuration Structure
    LT_FASTD_XCOMP = ME->R_BL_FDMDL->R_BL_FASTD->GET_STRC( IR_BL_FDMDL = ME->R_BL_FDMDL ).

*** Find Finish Good
    LS_FASTD_XCOMP = value #( LT_FASTD_XCOMP[ 1 ] optional ).
    LS_DATA = value #( LS_FASTD_XCOMP-T_DATA[ 1 ] optional ).
    check LS_DATA-R_GATE is bound and
          LS_DATA-R_GATE->R_CLSPR is instance of ZCL_VCXI_P4SS_PR_PRFG.

    ER_GATE     = LS_DATA-R_GATE.
    RR_PR_PRFG ?= ER_GATE->R_CLSPR.

  endmethod.
  method GET_REGISTERED_UCOMM.

*** Registered User Commands for Subscreen processing
    if ME->R_UI_FDMDL_PRDTL is bound.
      append lines of ME->R_UI_FDMDL_PRDTL->GET_REGISTERED_UCOMM( ) to RT_UCOMM.
    endif.

    append C_UC_FOVRR_MAKTX to RT_UCOMM.

  endmethod.
  method INIT_DYNP_PRDTL.

    if ME->R_UI_FDMDL_PRDTL is bound and
       ME->R_UI_FDMDL_PRDTL->S_DYNP-REPID is not initial and
       ME->R_UI_FDMDL_PRDTL->S_DYNP-DYNNR is not initial.
***   Get Screen Info of active Procurement Detail
      ME->S_DYNP_PRDTL = ME->R_UI_FDMDL_PRDTL->S_DYNP.
    else.
      ME->S_DYNP_PRDTL = ME->S_DYNP_BLANK.
    endif.

  endmethod.
  method INIT_VIS.

    data: LS_FASTD_XCOMP      type ZVCXI_PXSS_S_FASTD_XCOMP,
          LS_FASTD_XCOMP_DATA type ZVCXI_PXSS_S_FASTD_XCOMP_DATA.
    data: LT_FASTD_XCOMP  type ZVCXI_PXSS_TT_FASTD_XCOMP,
          LT_GATE_EXCLUDE type /VCXI/VKSR_TT_GATE.


    free: ME->S_FDMDL_VIS.
    check ME->R_PR_FASTD is bound.

***------------------------------------------------------------------------------------------------
*** Get Configuration Structure
    LT_FASTD_XCOMP = ME->R_BL_FDMDL->R_BL_FASTD->GET_STRC( IR_BL_FDMDL = ME->R_BL_FDMDL ).

*** Find Component of Gate and its linked Components
    LT_GATE_EXCLUDE = ME->R_BL_FDMDL->GET_GATE_EXCLUDE( IR_GATE_OUT    = ME->R_GATE_OUT
                                                        IT_FASTD_XCOMP = LT_FASTD_XCOMP ).

*** Fill VIS Structure based on Fast Data Integration Information
    loop at LT_FASTD_XCOMP into LS_FASTD_XCOMP.
      check not LINE_EXISTS( LT_GATE_EXCLUDE[ TABLE_LINE = LS_FASTD_XCOMP-R_GATE ] ).

      loop at LS_FASTD_XCOMP-T_DATA into LS_FASTD_XCOMP_DATA where R_DATA is bound.
        check not LINE_EXISTS( LT_GATE_EXCLUDE[ TABLE_LINE = LS_FASTD_XCOMP_DATA-R_GATE ] ).
        ME->MAPPING_TO_VIS( exporting IS_FASTD_XCOMP_DATA = LS_FASTD_XCOMP_DATA
                            changing  CS_FDMDL_VIS        = ME->S_FDMDL_VIS ).
      endloop.
    endloop.

*** Get Vendor Name
    ME->S_FDMDL_VIS-NAME1 = ZCL_VCXI_XCS_SERVICE=>GET_LIFNR_NAME1( IF_LIFNR = ME->S_FDMDL_VIS-LIFNR ).

  endmethod.
  method MODIFY_SCREEN_PRFG.


*** Hide Material Description Override Flag if undefined
    if CS_SCREEN-NAME cp '*-FOVRR_MAKTX' and
       ME->S_FDMDL_VIS-FOVRR_MAKTX eq ABAP_UNDEFINED.
      move 0 to CS_SCREEN-ACTIVE.
    endif.
*** Disable Material Description if generated
    if SCREEN-NAME cp '*-MAKTX' and
       ME->S_FDMDL_VIS-FOVRR_MAKTX eq ABAP_FALSE.
      move 0 to CS_SCREEN-INPUT.
    endif.

*** If no Dummy Material, then set to read only
    if ME->S_FDMDL_VIS-DUMMY eq ABAP_FALSE.
      if CS_SCREEN-GROUP2            eq 'DUM'.
        move 0 to CS_SCREEN-INPUT.
        if CS_SCREEN-GROUP3 eq 'HID'.  "Hide by Screen Group
          move 1 to CS_SCREEN-INVISIBLE.
          move 0 to CS_SCREEN-ACTIVE.
        endif.
      endif.
    else.
***   If Dummy Material block the Procurement Option
      if CS_SCREEN-GROUP2 eq 'PUR'.
        move 0 to CS_SCREEN-INPUT.
      endif.
    endif.

*** Hide Component Ratio
    if CS_SCREEN-NAME cp '*-COMPR' and
       ME->S_FDMDL_VIS-COMPR_ALLOWED ne ABAP_TRUE.
      CS_SCREEN-ACTIVE = 0.
    endif.

*** Hide Handling Dimension Override Flag if undefined
    if CS_SCREEN-NAME cp '*-FOVRR_HNDDIM' and
       ME->S_FDMDL_VIS-FOVRR_HNDDIM eq ABAP_UNDEFINED.
      CS_SCREEN-ACTIVE = 0.
    endif.
*** Handling Dimension changeable if Override Flag is set
    if CS_SCREEN-GROUP3 eq 'OHD'.
      if ME->S_FDMDL_VIS-FOVRR_HNDDIM eq ABAP_FALSE.
        CS_SCREEN-INPUT = 0.
      endif.
    endif.

  endmethod.
  method MODIFY_SCREEN_SPLR.

*** No Supplier Info for Procurement Option "Stock Item"
    if ME->S_FDMDL_VIS-PURCH eq ZCL_VCXI_P4SS_PR_PRFG=>C_PURCH_STOCK.
      CS_SCREEN-INPUT = 0.
      if CS_SCREEN-GROUP3 eq 'HID'.  "Hide by Screen Group
        CS_SCREEN-INVISIBLE = 1.
        CS_SCREEN-ACTIVE    = 0.
      endif.
    endif.

    if ME->S_FDMDL_VIS-PURCH eq ZCL_VCXI_P4SS_PR_PRFG=>C_PURCH_DIRECT and
       CS_SCREEN-GROUP2 eq 'PRI'.
      CS_SCREEN-INPUT = 0.
      if CS_SCREEN-GROUP3 eq 'HID'.  "Hide by Screen Group
        CS_SCREEN-INVISIBLE = 1.
        CS_SCREEN-ACTIVE    = 0.
      endif.
    endif.

  endmethod.
  method OPEN_SUBSCREEN.

    data: LF_CLSUI  type /VCXI/VKSC_CLSUI.
    data: LT_UCOMM  type table of SYUCOMM.


*** Get UI class name of Fast Data Model
    if IR_BL_FDMDL is bound.
      LF_CLSUI = IR_BL_FDMDL->GET_CLSUI( ).
    endif.

    if LF_CLSUI is not initial.
      try.
***       Create Subscreen
          create object RR_UI_FDMDL
            type
              (LF_CLSUI)
            exporting
              IR_UIFW        = ME
              IR_UIGATE      = ME->R_UIGATE
              IR_GATE        = ME->R_GATE
              IR_BL_FDMDL    = IR_BL_FDMDL
              IF_APPL_EVENTS = ME->F_APPL_EVENTS
              IR_GATE_OUT    = IR_BL_FDMDL->T_GATE_OUT[ 1 ].
        catch CX_SY_CREATE_OBJECT_ERROR into data(LR_CX_SY_CREATE_OBJECT_ERROR).
          /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( LR_CX_SY_CREATE_OBJECT_ERROR ).
      endtry.

      RR_UI_FDMDL->ACTIVATE( ).

    else.
***   Clear Subscreen
      if ME->R_UI_FDMDL_PRDTL is bound.
        ME->R_UI_FDMDL_PRDTL->DEACTIVATE( ).
      endif.
    endif.

  endmethod.
