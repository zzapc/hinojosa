
class ZCL_VCXI_FFSP_UI_ADDON_PRINT definition
  public
  inheriting from ZCL_VCXI_XCSP_UI_ADDON
  create public .

public section.

  data S_ADDON_PRINT_VIS type ZVCXI_FFSP_S_ADDON_PRINT_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  data R_GRID_MC_INK type ref to ZCL_VCXI_FFSP_GRID_MC_INK .
  data R_GRID_MC_TOOL type ref to ZCL_VCXI_FFSP_GRID_MC_TOOL .
  data R_PR_ADDON_PRINT type ref to ZCL_VCXI_FFSP_PR_ADDON_PRINT .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_FFSP_UI_ADDON_PRINT definition
class ZCL_VCXI_FFSP_UI_ADDON_PRINT implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_FFSP_UI_ADDON_PRINT_BIND'
      exporting
        IR_UI_ADDON_PRINT = ME
      importing
        ES_DYNP           = ME->S_DYNP.

*** Create Grid for Ink
    create object ME->R_GRID_MC_INK
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_MC_INK'.

*** Create Grid for Tools
    create object ME->R_GRID_MC_TOOL
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_MC_TOOL'.

  endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                      IR_UIGATE = IR_UIGATE
                      IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
  try.
      move IR_GATE->R_CLSPR ?to ME->R_PR_ADDON_PRINT.
    catch CX_SY_MOVE_CAST_ERROR.
***   UI ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '801'.
  endtry.

endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

    if ME->R_GRID_MC_INK is bound.
      ME->R_GRID_MC_INK->DO_FREE( ).
      clear ME->R_GRID_MC_INK.
    endif.

    if ME->R_GRID_MC_TOOL is bound.
      ME->R_GRID_MC_TOOL->DO_FREE( ).
      clear ME->R_GRID_MC_TOOL.
    endif.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Super PBO
    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize Grid
    if ME->R_GRID_MC_INK is bound.
      ME->R_GRID_MC_INK->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_MC_INK->REFRESH_GRID( ).
    endif.

    if ME->R_GRID_MC_TOOL is bound.
      ME->R_GRID_MC_TOOL->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_MC_TOOL->REFRESH_GRID( ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_ADDON_PRINT_VIS TYPE ZVCXI_FFSP_S_ADDON_PRINT_VIS.


*** Super
    SUPER->DO_UPDATE_VIS( IS_VIS = IS_VIS ).
    check ME->R_PR_ADDON_PRINT is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_ADDON_PRINT_VIS.

***-------------------------------------------------------------------------------------
*** Set Print Type
    if ME->S_ADDON_PRINT_VIS-TPRIN ne LS_ADDON_PRINT_VIS-TPRIN.
      ME->R_PR_ADDON_PRINT->SET_TPRIN( IF_TPRIN = LS_ADDON_PRINT_VIS-TPRIN
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Print Category
    if ME->S_ADDON_PRINT_VIS-PRCAT ne LS_ADDON_PRINT_VIS-PRCAT.
      ME->R_PR_ADDON_PRINT->SET_PRCAT( IF_PRCAT = LS_ADDON_PRINT_VIS-PRCAT
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Print Width
    if ME->S_ADDON_PRINT_VIS-PRWID ne LS_ADDON_PRINT_VIS-PRWID.
      ME->R_PR_ADDON_PRINT->SET_PRWID( IS_PRWID = LS_ADDON_PRINT_VIS-S_PRWID
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Print Repeat Length
    if ME->S_ADDON_PRINT_VIS-PRRLE ne LS_ADDON_PRINT_VIS-PRRLE.
      ME->R_PR_ADDON_PRINT->SET_PRRLE( IS_PRRLE = LS_ADDON_PRINT_VIS-S_PRRLE
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Internal Print Thickness
    if ME->S_ADDON_PRINT_VIS-PRTIN ne LS_ADDON_PRINT_VIS-PRTIN.
      ME->R_PR_ADDON_PRINT->SET_PRTIN( IS_PRTIN = LS_ADDON_PRINT_VIS-S_PRTIN
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Additional Number of Color Changes
    if ME->S_ADDON_PRINT_VIS-NOACC ne LS_ADDON_PRINT_VIS-NOACC.
      ME->R_PR_ADDON_PRINT->SET_NOACC( IF_NOACC = LS_ADDON_PRINT_VIS-NOACC
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Additional Number of Ink Tool Changes
    if ME->S_ADDON_PRINT_VIS-NOATC ne LS_ADDON_PRINT_VIS-NOATC.
      ME->R_PR_ADDON_PRINT->SET_NOATC( IF_NOATC = LS_ADDON_PRINT_VIS-NOATC
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Number of different Design Changes
    if ME->S_ADDON_PRINT_VIS-NODDG ne LS_ADDON_PRINT_VIS-NODDG.
      ME->R_PR_ADDON_PRINT->SET_NODDG( IF_NODDG = LS_ADDON_PRINT_VIS-NODDG
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    if ME->R_GRID_MC_INK is bound.
      ME->R_GRID_MC_INK->CHECK_CHANGED_DATA( ).
    endif.

    if ME->R_GRID_MC_TOOL is bound.
      ME->R_GRID_MC_TOOL->CHECK_CHANGED_DATA( ).
    endif.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_ADDON_PRINT.

  endmethod.
  method INIT_VIS.

*** Super
    SUPER->INIT_VIS( ).

    free: ME->S_ADDON_PRINT_VIS.
    ME->S_ADDON_PRINT_VIS-S_ADDON_VIS = ME->S_ADDON_VIS.

***--------------------------------------------------------------------------------------
*** Get Print Type
    ME->R_PR_ADDON_PRINT->GET_TPRIN( importing EF_TPRIN = ME->S_ADDON_PRINT_VIS-TPRIN ).

*** Get Print Category
    ME->R_PR_ADDON_PRINT->GET_PRCAT( importing EF_PRCAT = ME->S_ADDON_PRINT_VIS-PRCAT ).

*** Get Number of Colors
    move ME->R_PR_ADDON_PRINT->GET_NOCOL( ) to ME->S_ADDON_PRINT_VIS-NOCOL.

*** Get Print Width
    ME->R_PR_ADDON_PRINT->GET_PRWID( importing ES_PRWID = ME->S_ADDON_PRINT_VIS-S_PRWID ).

*** Get Print Repeat Length
    ME->R_PR_ADDON_PRINT->GET_PRRLE( importing ES_PRRLE = ME->S_ADDON_PRINT_VIS-S_PRRLE ).

*** Get Internal Print Thickness
    ME->R_PR_ADDON_PRINT->GET_PRTIN( importing ES_PRTIN = ME->S_ADDON_PRINT_VIS-S_PRTIN ).

*** Get Additional Number of Color Changes
    ME->R_PR_ADDON_PRINT->GET_NOACC( importing EF_NOACC = ME->S_ADDON_PRINT_VIS-NOACC ).

*** Get Additional Number of Ink Tool Changes
    ME->R_PR_ADDON_PRINT->GET_NOATC( importing EF_NOATC = ME->S_ADDON_PRINT_VIS-NOATC ).

*** Get Number of different Design Changes
    ME->R_PR_ADDON_PRINT->GET_NODDG( importing EF_NODDG = ME->S_ADDON_PRINT_VIS-NODDG ).

  endmethod.
