
class ZCL_VCXI_PXSP_UI_ADDON_CTAPE definition
  public
  inheriting from ZCL_VCXI_XCSP_UI_ADDON
  create public .

public section.

  data S_ADDON_CTAPE_VIS type ZVCXI_PXSP_S_ADDON_CTAPE_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  data R_GRID_MC_CTAPE type ref to ZCL_VCXI_PXSP_GRID_MC_CTAPE .
  data R_PR_ADDON_CTAPE type ref to ZCL_VCXI_PXSP_PR_ADDON_CTAPE .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_PXSP_UI_ADDON_CTAPE definition
class ZCL_VCXI_PXSP_UI_ADDON_CTAPE implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_PXSP_UI_ADDON_CTAPE_BIND'
      exporting
        IR_UI_ADDON_CTAPE = ME
      importing
        ES_DYNP           = ME->S_DYNP.

*** Create Grid for Additional Materials
    create object ME->R_GRID_MC_CTAPE
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_MC_CTAPE'.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_ADDON_CTAPE.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

    if ME->R_GRID_MC_CTAPE is bound.
      ME->R_GRID_MC_CTAPE->DO_FREE( ).
      clear ME->R_GRID_MC_CTAPE.
    endif.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Super PBO
    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize Grid
    if ME->R_GRID_MC_CTAPE is bound.
      ME->R_GRID_MC_CTAPE->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_MC_CTAPE->REFRESH_GRID( ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    if ME->R_GRID_MC_CTAPE is bound.
      ME->R_GRID_MC_CTAPE->CHECK_CHANGED_DATA( ).
    endif.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_ADDON_CTAPE.

  endmethod.
  method INIT_VIS.

*** Super
    SUPER->INIT_VIS( ).

    free: ME->S_ADDON_CTAPE_VIS.
    ME->S_ADDON_CTAPE_VIS-S_ADDON_VIS = ME->S_ADDON_VIS.

  endmethod.
