
class ZCL_VCXI_XCSU_TI_SHIPMGR definition
  public
  inheriting from ZCL_VCXI_XCS_TI
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ
    redefinition .
  methods /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL
    redefinition .
protected section.

  constants C_UC_RELOAD_DEF type UI_FUNC value 'RELOAD_DEF'. "#EC NOTEXT
  data R_PR_SHIPMGR type ref to ZCL_VCXI_XCSU_PR_SHIPMGR .

  methods RELOAD_DEF
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_XCSU_TI_SHIPMGR definition
class ZCL_VCXI_XCSU_TI_SHIPMGR implementation.
method /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ.

  data: LF_DISABLED  type CUA_ACTIVE.

*** Call SUPER
  SUPER->/VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ( IR_MENU = IR_MENU ).

***------------------------------------------------------------------------------------------------
*** Reload Defaults
*** Set Seperator
  IR_MENU->ADD_SEPARATOR( ).

  if ME->R_GATE->IS_LOCKED( ) ne ABAP_TRUE or
     ME->R_GATE->IS_INUSE( )  ne ABAP_TRUE.
    LF_DISABLED = ABAP_TRUE.
  endif.

*** Set Menu
  IR_MENU->ADD_FUNCTION( FCODE    = C_UC_RELOAD_DEF
                         TEXT     = 'Reload Defaults'(F10)
                         DISABLED = LF_DISABLED ).

endmethod.
method /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL.

  case IF_FCODE.
    when C_UC_RELOAD_DEF.
      ME->RELOAD_DEF( ).

    when others.
      SUPER->/VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL( IF_FCODE = IF_FCODE ).
  endcase.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_UIGATE = IR_UIGATE
                      IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
  try.
      move IR_GATE->R_CLSPR ?to ME->R_PR_SHIPMGR.
    catch CX_SY_MOVE_CAST_ERROR.
***   TI ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '802'.
  endtry.

endmethod.
method RELOAD_DEF.

  data:  LF_ANSWER          type C.

  check ME->R_PR_SHIPMGR is bound.

*** Double Check if user really wanna to reload Defaults
  call function 'POPUP_TO_CONFIRM'
    exporting
      TITLEBAR       = 'Reload Defaults'(F10)
      TEXT_QUESTION  = 'Are you sure to reload all Shipping data? All user specific data will be lost!'(Q10)
    importing
      ANSWER         = LF_ANSWER
    exceptions
      TEXT_NOT_FOUND = 1
      others         = 2.
  if SY-SUBRC ne 0.
    /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
  endif.

  case LF_ANSWER.
    when '1'.
***   Just do it!
    when others.
***   Cancel
      exit.
  endcase.

**** Reload Defaults
  ME->R_PR_SHIPMGR->RELAOD_DEF( ).

endmethod.
