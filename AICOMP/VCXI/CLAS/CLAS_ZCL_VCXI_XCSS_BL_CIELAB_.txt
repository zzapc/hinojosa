
class ZCL_VCXI_XCSS_BL_CIELAB definition
  public
  create public .

public section.

  class-methods POPUP_CIELAB
    changing
      !CS_CIELAB type ZVCXI_XCS_S_CIELAB
    raising
      /VCXI/CX_CKX .
  class-methods CALC_CIEDE
    importing
      !IS_CIELAB_SRC type ZVCXI_XCS_S_CIELAB
      !IS_CIELAB_TRG type ZVCXI_XCS_S_CIELAB
    returning
      value(RF_CIEDE) type ZVCXI_XCS_CIEDE
    raising
      /VCXI/CX_CKX .
protected section.
private section.
endclass. "ZCL_VCXI_XCSS_BL_CIELAB definition
class ZCL_VCXI_XCSS_BL_CIELAB implementation.
  method CALC_CIEDE.

    try.
***     Calculate CIELAB #E* based on CIE76: sqrt( (L2 - L1)^2 + (a2 - a1)^2 + (b2 - b1)^2) )
        RF_CIEDE = SQRT( ( IS_CIELAB_SRC-CIESL - IS_CIELAB_TRG-CIESL ) ** 2 +
                         ( IS_CIELAB_SRC-CIESA - IS_CIELAB_TRG-CIESA ) ** 2 +
                         ( IS_CIELAB_SRC-CIESB - IS_CIELAB_TRG-CIESB ) ** 2 ).
      catch CX_SY_CONVERSION_OVERFLOW into data(LR_CX_SY_CONVERSION_OVERFLOW).
        RF_CIEDE = '999.99'.
    endtry.

  endmethod.
  method POPUP_CIELAB.

    data: LF_RETURN type C.
    data: LT_SVAL   type standard table of SVAL.

*** Create PopUp for User-Entry
    LT_SVAL = value #( ( TABNAME = 'ZVCXI_XCS_S_CIELAB'  FIELDNAME = 'CIESL'  VALUE = CS_CIELAB-CIESL )
                       ( TABNAME = 'ZVCXI_XCS_S_CIELAB'  FIELDNAME = 'CIESA'  VALUE = CS_CIELAB-CIESA )
                       ( TABNAME = 'ZVCXI_XCS_S_CIELAB'  FIELDNAME = 'CIESB'  VALUE = CS_CIELAB-CIESB )
                      ).

*** Call Popup
    call function 'POPUP_GET_VALUES'
      exporting
        POPUP_TITLE     = 'CEILAB Color Code'(100)
      importing
        RETURNCODE      = LF_RETURN
      tables
        FIELDS          = LT_SVAL
      exceptions
        ERROR_IN_FIELDS = 1
        others          = 2.
    if SY-SUBRC ne 0 or
      LF_RETURN eq 'A'.
      exit.
    endif.

*** Get Values of Popup
    CS_CIELAB-CIESL = value #( LT_SVAL[ FIELDNAME = 'CIESL' ]-VALUE optional ).
    CS_CIELAB-CIESA = value #( LT_SVAL[ FIELDNAME = 'CIESA' ]-VALUE optional ).
    CS_CIELAB-CIESB = value #( LT_SVAL[ FIELDNAME = 'CIESB' ]-VALUE optional ).

  endmethod.
endclass. "ZCL_VCXI_XCSS_BL_CIELAB implementation
