
class ZCL_VCXI_FFE_MM_DUI_MMEXT definition
  public
  inheriting from ZCL_VCXI_XCA_MWB_CLDUI
  create public .

public section.

  data S_DUI_MMEXT_VIS type ZVCXI_FFE_MM_S_DUI_MMEXT_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to ZCL_VCXI_XCA_MWB_UIFW
      !IR_CLMAT type ref to ZCL_VCXI_XCA_MWB_CLMAT
    raising
      /VCXI/CX_CKX .

  methods DO_UPDATE_VIS
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  constants C_UC_FSTRC type SYUCOMM value 'FSTRC' ##NO_TEXT.

  methods HANDLE_UC_FSTRC
    raising
      /VCXI/CX_CKX .

  methods INIT_VIS
    redefinition .
  methods MODIFY_SCREEN
    redefinition .
private section.
endclass. "ZCL_VCXI_FFE_MM_DUI_MMEXT definition
class ZCL_VCXI_FFE_MM_DUI_MMEXT implementation.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW  = IR_UIFW
                        IR_CLMAT = IR_CLMAT  ).

***----------------------------------------------------------------------------
*** Bind UI
    call function 'ZVCXI_FFE_MM_DUI_MMEXT_BIND'
      exporting
        IR_DUI_MMEXT = ME
      importing
        ES_DYNP      = ME->S_DYNP.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_DUI_MMEXT_VIS type ZVCXI_FFE_MM_S_DUI_MMEXT_VIS.


*** Call SUPER
    SUPER->DO_UPDATE_VIS( IS_DUI_VIS = IS_DUI_VIS ).

    check ME->R_CLMAT is bound.

*** "Cast" Vis Structure
    move-corresponding IS_DUI_VIS to LS_DUI_MMEXT_VIS.


***--------------------------------------------------------------------------------------
*** Material Updates
    data(LR_BOB_MAT) = ME->R_CLMAT->GET_BOB_MAT( ).
    if LR_BOB_MAT is bound.
***   Set Material Plant Status
      if ME->S_DUI_MMEXT_VIS-MMSTA ne LS_DUI_MMEXT_VIS-MMSTA.
        LR_BOB_MAT->S_PLANTDATA-PUR_STATUS = LS_DUI_MMEXT_VIS-MMSTA.
        ME->R_CLMAT->MARK_CHANGED( ).
      endif.
    endif.


***--------------------------------------------------------------------------------------
*** Material Master Extension Updates
    data(LR_MMEXT) = cast ZCL_VCXI_FFE_MM_MMEXT( cast ZCL_VCXI_XCE_MM_MAT_MMEXT( ME->R_CLMAT )->GET_MMEXT( ) ).
    if LR_MMEXT is bound.
      LR_MMEXT->SET_MMEXT( IS_MMEXT = corresponding #( base ( LR_MMEXT->S_MMEXT ) LS_DUI_MMEXT_VIS ) ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    case IF_UCOMM.
      when C_UC_FSTRC.
        ME->HANDLE_UC_FSTRC( ).
    endcase.

  endmethod.
  method HANDLE_UC_FSTRC.

*** Get Material Master Extension
    data(LR_MMEXT) = cast ZCL_VCXI_FFE_MM_MMEXT( cast ZCL_VCXI_XCE_MM_MAT_MMEXT( ME->R_CLMAT )->GET_MMEXT( ) ).
    check LR_MMEXT is bound.

*** Get Film Structure
    data(LR_BL_FISTRUCT) = ZCL_VCXI_FFSS_BL_FISTRUCT=>FACTORY( IF_FISTX = LR_MMEXT->S_MMEXT-FISTX ).
    check LR_BL_FISTRUCT is bound.

*** Get Lock Status for Popup
    data(LF_LOCKED) = cond #( when ME->R_CLMAT->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ) eq ABAP_TRUE and
                                   ME->R_CLMAT->IS_CONFIGURABLE( )                       ne ABAP_TRUE
                                   then ABAP_TRUE ).

*** Start Popup
    ZCL_VCXI_FFSS_POPUP_FISTRUCT=>START( IR_BL_FISTRUCT = LR_BL_FISTRUCT
                                         IF_LOCKED      = LF_LOCKED ).

*** Update MM Extension
    data(LS_MMEXT) = LR_MMEXT->S_MMEXT.
    LR_BL_FISTRUCT->GET_FISTX( importing EF_FISTX = LS_MMEXT-FISTX ).
    LR_BL_FISTRUCT->GET_TFILM( importing EF_TFILM = LS_MMEXT-TFILM ).
    LR_MMEXT->SET_MMEXT( IS_MMEXT = LS_MMEXT ).

  endmethod.
  method INIT_VIS.

    check ME->R_CLMAT is bound.

    SUPER->INIT_VIS( ).

    free: ME->S_DUI_MMEXT_VIS.
    ME->S_DUI_MMEXT_VIS = corresponding #( ME->S_DUI_VIS ).

***--------------------------------------------------------------------------------------
    data(LR_BOB_MAT) = ME->R_CLMAT->GET_BOB_MAT( ).
    if LR_BOB_MAT is bound.
***   Get Plant Material Status
      ME->S_DUI_MMEXT_VIS-MMSTA = LR_BOB_MAT->S_PLANTDATA-PUR_STATUS.
    endif.

***--------------------------------------------------------------------------------------
*** Get Material Master Extension
    data(LR_MMEXT) = cast ZCL_VCXI_FFE_MM_MMEXT( cast ZCL_VCXI_XCE_MM_MAT_MMEXT( ME->R_CLMAT )->GET_MMEXT( ) ).
    if LR_MMEXT is bound.
      ME->S_DUI_MMEXT_VIS = corresponding #( base ( ME->S_DUI_MMEXT_VIS ) LR_MMEXT->S_MMEXT ).
    endif.

  endmethod.
  method MODIFY_SCREEN.

    SUPER->MODIFY_SCREEN( ).

*** Check if Material has configuration
    data(LF_CONFIG) = ME->R_CLMAT->IS_CONFIGURABLE( ).

    loop at screen.
***   Material Extension is not changeable, if it has a configuration
      if SCREEN-GROUP2 eq 'EXT' and
         LF_CONFIG eq ABAP_TRUE.
        move 0 to SCREEN-INPUT.
      endif.

      modify screen.
    endloop.

  endmethod.
