
class ZCL_VCXI_PFSS_CUST_REEL definition
  public
  inheriting from ZCL_VCXI_XCS_CUST_MATNR
  create protected .

public section.

  data F_PAPGR type ZVCXI_PFSS_PAPGR read-only .
  data S_GRAMM type ZVCXI_P0SS_S_GRAMM read-only .
  data S_REWID type ZVCXI_P0SS_S_REWID read-only .
  data S_CALIP type ZVCXI_P0SS_S_CALIP read-only .

  methods CONSTRUCTOR
    importing
      !IF_MATNR type MATNR
      !IF_WERKS type WERKS_D
      !IF_DATE type DATS
    raising
      /VCXI/CX_CKX .
  class-methods GET_INSTANCE_PF_REEL
    importing
      !IF_MATNR type MATNR
      !IF_WERKS type WERKS_D
      !IF_DATE type DATS default SY-DATUM
    returning
      value(RR_INSTANCE) type ref to ZCL_VCXI_PFSS_CUST_REEL
    raising
      /VCXI/CX_CKX .
protected section.

  types:
    begin of YS_KAUSF,
           MATNR    type MATNR,
           WERKS    type WERKS_D,
           DATE     type DATS,
           S_EVQTY  type ZVCXI_XCSR_S_EVQTY,
           S_KAUSF  type ZVCXI_XCSP_S_KAUSF,
         end   of YS_KAUSF .
  types:
    YT_KAUSF type standard table of YS_KAUSF .

  constants C_SETID_PFS_REEL type /VCXI/CKXC_SETID value 'ZVCXI_PFS_REEL'. "#EC NOTEXT
private section.
endclass. "ZCL_VCXI_PFSS_CUST_REEL definition
class ZCL_VCXI_PFSS_CUST_REEL implementation.
method CONSTRUCTOR.

  data:  LF_OBJNUM    type        OBJNUM.
  data:  LS_SETVA     type        /VCXI/CKXC_S_SETVA,
         LS_MAPVC     type        /VCXI/CKXC_S_MAPVC,
         LS_MAPVN     type        /VCXI/CKXC_S_MAPVN.
  data:  LT_SETVA     type        /VCXI/CKXC_TT_SETVA.

  try.
      SUPER->CONSTRUCTOR( IF_MATNR = IF_MATNR
                          IF_WERKS = IF_WERKS
                          IF_DATE  = IF_DATE ).

***   Get Reel Cut Info
      move ME->F_MATNR to LF_OBJNUM.
      LT_SETVA = /VCXI/CL_CKXC_CCM=>GET_SETVA( IF_SETID  = C_SETID_PFS_REEL
                                               IF_OBJNUM = LF_OBJNUM ).

      loop at LT_SETVA into LS_SETVA.
        clear: LS_MAPVC,
               LS_MAPVN.

        case LS_SETVA-MAPID.
          when 'PAPGR'.
            read table LS_SETVA-T_MAPVC into LS_MAPVC index 1.
            move LS_MAPVC-VAMAP     to ME->F_PAPGR.

          when 'GRAMM'.
            read table LS_SETVA-T_MAPVN into LS_MAPVN index 1.
            move LS_MAPVN-ATFLV     to ME->S_GRAMM-GRAMM.
            move LS_MAPVN-ATFLV_UOM to ME->S_GRAMM-GRAMM_UOM.

          when 'REWID'.
            read table LS_SETVA-T_MAPVN into LS_MAPVN index 1.
            move LS_MAPVN-ATFLV     to ME->S_REWID-REWID.
            move LS_MAPVN-ATFLV_UOM to ME->S_REWID-REWID_UOM.

          when 'CALIP'.
            read table LS_SETVA-T_MAPVN into LS_MAPVN index 1.
            move LS_MAPVN-ATFLV     to ME->S_CALIP-CALIP.
            move LS_MAPVN-ATFLV_UOM to ME->S_CALIP-CALIP_UOM.

        endcase.
      endloop.

    cleanup.
      ME->DO_FREE( ).
  endtry.

endmethod.
method GET_INSTANCE_PF_REEL.

  data:  LS_INSTANCE type YS_INSTANCE.

  read table ZCL_VCXI_XCS_CUST_MATNR=>T_INSTANCE
             into LS_INSTANCE
             with key MATNR = IF_MATNR
                      WERKS = IF_WERKS
                      DATE  = IF_DATE.

  if SY-SUBRC eq 0.
    try.
        move LS_INSTANCE-R_INSTANCE ?to RR_INSTANCE.
      catch CX_SY_MOVE_CAST_ERROR.
        delete ZCL_VCXI_XCS_CUST_MATNR=>T_INSTANCE where R_INSTANCE eq LS_INSTANCE-R_INSTANCE.
        clear RR_INSTANCE.
    endtry.
  endif.

  if RR_INSTANCE is initial.
    try.
        create object RR_INSTANCE
          exporting
            IF_MATNR = IF_MATNR
            IF_WERKS = IF_WERKS
            IF_DATE  = IF_DATE.
      cleanup.
        clear RR_INSTANCE.
    endtry.
  endif.

endmethod.
