
class ZCL_VCXI_P3SS_PR_AFG definition
  public
  inheriting from ZCL_VCXI_PXSS_PR_XFG
  create public .

public section.

  interfaces ZIF_VCXI_P3SS_COMM_PREV .

  constants C_IPRVT_AFG type /VCXI/VKSC_IPRVT value 'ZVCXI_P3SS_AFG' ##NO_TEXT.
  constants C_PARID_PROHE type /VCXI/VKS_PARID value 'ZVCXI_P0_PROHE' ##NO_TEXT.
  constants C_PARID_PROLE type /VCXI/VKS_PARID value 'ZVCXI_P0_PROLE' ##NO_TEXT.
  constants C_PARID_PROWI type /VCXI/VKS_PARID value 'ZVCXI_P0_PROWI' ##NO_TEXT.

  methods GET_ASBPX
    exporting
      !EF_ASBPX type ZVCXI_P3S_ASBPX
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RR_BL_ASBPROP) type ref to ZCL_VCXI_P3SS_BL_ASBPROP
    raising
      /VCXI/CX_CKX .
  methods GET_PRODIM_DESCR
    returning
      value(RF_DESCR) type /VCXI/CKX_DESCR
    raising
      /VCXI/CX_CKX .
  methods GET_ASFPX
    exporting
      !EF_ASFPX type ZVCXI_P3S_ASFPX
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RR_BL_ASFPROP) type ref to ZCL_VCXI_P3SS_BL_ASFPROP
    raising
      /VCXI/CX_CKX .
  methods GET_PROHE
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_PROHE) type ZVCXI_P0S_S_PROHE
    raising
      /VCXI/CX_CKX .
  methods GET_PROLE
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_PROLE) type ZVCXI_P0S_S_PROLE
    raising
      /VCXI/CX_CKX .
  methods GET_PROWI
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_PROWI) type ZVCXI_P0S_S_PROWI
    raising
      /VCXI/CX_CKX .
  methods SET_PROHE
    importing
      !IS_PROHE type ZVCXI_P0S_S_PROHE
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_PROLE
    importing
      !IS_PROLE type ZVCXI_P0S_S_PROLE
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_PROWI
    importing
      !IS_PROWI type ZVCXI_P0S_S_PROWI
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .

  methods GET_STATUS
    redefinition .
  methods GET_STATUS_MSG
    redefinition .
  methods ZIF_VCXI_PXSS_COMM_PREV~DO_FORWARD
    redefinition .
  methods ZIF_VCXI_PXSS_FDI~GET_DATA
    redefinition .
  methods ZIF_VCXI_PXSS_FDI~SET_DATA
    redefinition .
  methods ZIF_VCXI_PXSS_FG~GET_HNDDIM
    redefinition .
  methods ZIF_VCXI_XCS_NGREP~FILL
    redefinition .
protected section.

  constants C_IEVNT_O_ASFPX_CHANGED type /VCXI/VKSC_IEVNT value 'ASFPX_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_PROHE_CHANGED type /VCXI/VKSC_IEVNT value 'PROHE_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_PROLE_CHANGED type /VCXI/VKSC_IEVNT value 'PROLE_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_PROWI_CHANGED type /VCXI/VKSC_IEVNT value 'PROWI_CHANGED' ##NO_TEXT.
  constants C_PARID_ASFPX type /VCXI/VKS_PARID value 'ZVCXI_P3_ASFPX' ##NO_TEXT.

  methods SET_ASFPX
    importing
      !IF_ASFPX type ZVCXI_P3S_ASFPX
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods CHECK_PRODIM
    importing
      !IR_MESSAGE type ref to /VCXI/CL_CKXM_MESSAGE optional
    returning
      value(RF_ELMST) type /VCXI/VKSR_ELMST
    raising
      /VCXI/CX_CKX .

  methods HANDLE_RECALC_PRODH
    redefinition .
private section.
endclass. "ZCL_VCXI_P3SS_PR_AFG definition
class ZCL_VCXI_P3SS_PR_AFG implementation.
  method CHECK_PRODIM.

*** Start positive...
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G.

***--------------------------------------------------------------------------------------
*** Check Product Length
    if ME->GET_PROLE( )-PROLE is not initial.
      if IR_MESSAGE is bound.
***     Product Length is maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P3SS'
                                 IF_MSGTY = 'S'
                                 IF_MSGNO = '151' ).
      endif.

    else.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
      if IR_MESSAGE is bound.
***     Product Length is not maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P3SS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '152' ).
      endif.
    endif.

***--------------------------------------------------------------------------------------
*** Check Product Width
    if ME->GET_PROWI( )-PROWI is not initial.
      if IR_MESSAGE is bound.
***     Product Width is maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P3SS'
                                 IF_MSGTY = 'S'
                                 IF_MSGNO = '153' ).
      endif.

    else.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
      if IR_MESSAGE is bound.
***     Product Width is not maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P3SS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '154' ).
      endif.
    endif.

***--------------------------------------------------------------------------------------
*** Check Product Height
    if ME->GET_PROHE( )-PROHE is not initial.
      if IR_MESSAGE is bound.
***     Product Height is maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P3SS'
                                 IF_MSGTY = 'S'
                                 IF_MSGNO = '155' ).
      endif.

    else.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
      if IR_MESSAGE is bound.
***     Product Height is not maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P3SS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '156' ).
      endif.
    endif.

  endmethod.
  method GET_ASBPX.

***--------------------------------------------------------------------------------------
*** Create fresh instance of Assembly Backward Property
    RR_BL_ASBPROP = ZCL_VCXI_P3SS_BL_ASBPROP=>FACTORY( ).

    if RR_BL_ASBPROP is bound.
***   Cost Split Distribution
      RR_BL_ASBPROP->SET_CSPLIT( IT_CSPLIT = ME->ZIF_VCXI_PXSS_FG~GET_CSPLIT( ) ).

***   Product Dimensions
      RR_BL_ASBPROP->SET_PRODIM( IS_PRODIM = value #( S_PROLE = ME->GET_PROLE( )
                                                      S_PROWI = ME->GET_PROWI( )
                                                      S_PROHE = ME->GET_PROHE( ) ) ).
    endif.

***--------------------------------------------------------------------------------------
*** Return Corrugator Backward Property XML (Outbound)
    if EF_ASBPX is requested.
      move RR_BL_ASBPROP->GET_ASBPX( ) to EF_ASBPX.
    endif.

  endmethod.
  method GET_ASFPX.

***--------------------------------------------------------------------------------------
*** Get Assembly Forward Property (Inbound)
    RR_BL_ASFPROP = ME->ZIF_VCXI_P3SS_COMM_PREV~GET_ASFPX( importing EF_SETBY = EF_SETBY ).

***--------------------------------------------------------------------------------------
*** Product Hierarchy
    RR_BL_ASFPROP->SET_PRODH( IF_PRODH = ME->ZIF_VCXI_PXSS_FG~GET_PRODH( ) ).

***--------------------------------------------------------------------------------------
*** Get Assembly Forward Property XML
    if EF_ASFPX is requested.
      EF_ASFPX = RR_BL_ASFPROP->GET_ASFPX( ).
    endif.

  endmethod.
  method GET_PRODIM_DESCR.

*** Define Dimensions
    RF_DESCR = 'L/W/H'.

*** Full Description String
    RF_DESCR = 'Product Dim. ('(T01) && RF_DESCR && ')'.

  endmethod.
  method GET_PROHE.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PROHE ).
    move LS_PARAM-ATFLV to RS_PROHE-PROHE.
    move LS_PARAM-UNIT  to RS_PROHE-PROHE_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_PROLE.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PROLE ).
    move LS_PARAM-ATFLV to RS_PROLE-PROLE.
    move LS_PARAM-UNIT  to RS_PROLE-PROLE_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_PROWI.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PROWI ).
    move LS_PARAM-ATFLV to RS_PROWI-PROWI.
    move LS_PARAM-UNIT  to RS_PROWI-PROWI_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_STATUS.

    RF_ELMST = SUPER->GET_STATUS( ).

***--------------------------------------------------------------------------------------
*** Check Product Dimensions
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = ME->CHECK_PRODIM( ) ).

  endmethod.
  method GET_STATUS_MSG.

    SUPER->GET_STATUS_MSG( IR_MESSAGE = IR_MESSAGE ).

***--------------------------------------------------------------------------------------
*** Check Product Dimensions
    ME->CHECK_PRODIM( IR_MESSAGE = IR_MESSAGE ).

  endmethod.
  method HANDLE_RECALC_PRODH.

    data: LF_SETBY type /VCXI/VKSR_SETBY.
    data: LR_BL_ASFPROP type ref to ZCL_VCXI_P3SS_BL_ASFPROP.


    ME->ZIF_VCXI_PXSS_FG~GET_PRODH( importing EF_SETBY = LF_SETBY ).

*** Only go on if not set by user
    check LF_SETBY ne /VCXI/CL_VKSR_GATE=>C_SETBY_USER.

*** Set Forwarded Product Hierarchy
    LR_BL_ASFPROP = ME->ZIF_VCXI_P3SS_COMM_PREV~GET_ASFPX( ).
    if LR_BL_ASFPROP is bound.
      ME->ZIF_VCXI_PXSS_FG~SET_PRODH( IF_PRODH = LR_BL_ASFPROP->F_PRODH ).
    endif.

  endmethod.
  method SET_ASFPX.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_ASFPX ).

    move IF_ASFPX   to LS_PARAM-STRNG.
    move IF_SETBY   to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_ASFPX_CHANGED ).
    endif.

  endmethod.
  method SET_PROHE.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PROHE ).

    move IS_PROHE-PROHE     to LS_PARAM-ATFLV.
    move IS_PROHE-PROHE_UOM to LS_PARAM-UNIT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_PROHE_CHANGED ).
    endif.

  endmethod.
  method SET_PROLE.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PROLE ).

    move IS_PROLE-PROLE     to LS_PARAM-ATFLV.
    move IS_PROLE-PROLE_UOM to LS_PARAM-UNIT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_PROLE_CHANGED ).
    endif.

  endmethod.
  method SET_PROWI.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PROWI ).

    move IS_PROWI-PROWI     to LS_PARAM-ATFLV.
    move IS_PROWI-PROWI_UOM to LS_PARAM-UNIT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_PROWI_CHANGED ).
    endif.

  endmethod.
  method ZIF_VCXI_P3SS_COMM_PREV~GET_ASBPX.

***--------------------------------------------------------------------------------------
*** Get Assembly Backward Property (Outbound)
    RR_BL_ASBPROP = ME->GET_ASBPX( importing EF_SETBY = EF_SETBY ).

***--------------------------------------------------------------------------------------
*** Apply Distribution Key to Cost Split
    RR_BL_ASBPROP->ADJ_CSPLIT_BY_DIKEY( IS_DIKEY = ME->GET_DIKEY( ) ).

***--------------------------------------------------------------------------------------
*** Get Assembly Backward Property Instance
    if EF_ASBPX is requested.
      EF_ASBPX = RR_BL_ASBPROP->GET_ASBPX( ).
    endif.

  endmethod.
  method ZIF_VCXI_P3SS_COMM_PREV~GET_ASFPX.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

***--------------------------------------------------------------------------------------
*** Get Parameter
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_ASFPX ).
    move LS_PARAM-STRNG to EF_ASFPX.
    move LS_PARAM-SETBY to EF_SETBY.

***--------------------------------------------------------------------------------------
*** Get Assembly Forward Property Instance
    if RR_BL_ASFPROP is requested.
      RR_BL_ASFPROP = ZCL_VCXI_P3SS_BL_ASFPROP=>FACTORY( IF_ASFPX = EF_ASFPX ).
    endif.

  endmethod.
  method ZIF_VCXI_PXSS_COMM_PREV~DO_FORWARD.

    data: LR_GATE            type ref to /VCXI/CL_VKSR_GATE,
          LR_COMM_NEXT       type ref to ZIF_VCXI_P3SS_COMM_NEXT,
          LR_BL_ASFPROP      type ref to ZCL_VCXI_P3SS_BL_ASFPROP,
          LR_BL_ASFPROP_NEXT type ref to ZCL_VCXI_P3SS_BL_ASFPROP.

***------------------------------------------------------------------------------------------------
*** Process all provided Gates
    loop at IT_GATE into LR_GATE.
      try.
          move LR_GATE->R_CLSPR ?to LR_COMM_NEXT.
        catch CX_SY_MOVE_CAST_ERROR.
          clear LR_COMM_NEXT.
      endtry.
      check LR_COMM_NEXT is bound.

***   Combine Assembly Forward Property
      LR_BL_ASFPROP_NEXT = LR_COMM_NEXT->GET_ASFPX( ).
      if LR_BL_ASFPROP is not bound.
        move LR_BL_ASFPROP_NEXT to LR_BL_ASFPROP.
      else.
        LR_BL_ASFPROP->MERGE( IR_BL_ASFPROP = LR_BL_ASFPROP_NEXT ).
      endif.

    endloop.

*** Forward Assembly Forward Property XML
    if LR_BL_ASFPROP is bound.
      ME->SET_ASFPX( IF_ASFPX = LR_BL_ASFPROP->GET_ASFPX( ) ).
    else.
      ME->SET_ASFPX( IF_ASFPX = SPACE ).
    endif.

  endmethod.
  method ZIF_VCXI_PXSS_FDI~GET_DATA.

    data: LR_DATA_SUPER type ref to DATA.
    field-symbols: <S_FDI_SUPER> type ANY,
                   <S_FDI_AFG>   type ZVCXI_P3SS_S_FDI_AFG.


***--------------------------------------------------------------------------------------
*** Create Data Reference
    create data RR_DATA type ZVCXI_P3SS_S_FDI_AFG.
    assign RR_DATA->* to <S_FDI_AFG>.

*** Get SUPER Data
    LR_DATA_SUPER = SUPER->ZIF_VCXI_PXSS_FDI~GET_DATA( importing ET_FDMEX = ET_FDMEX ).
    if LR_DATA_SUPER is bound.
      assign LR_DATA_SUPER->* to <S_FDI_SUPER>.
      <S_FDI_AFG> = corresponding #( <S_FDI_SUPER> ).
    endif.


***--------------------------------------------------------------------------------------
*** Get Product Dimension
    <S_FDI_AFG>-PRODIM_DESCR = ME->GET_PRODIM_DESCR( ).
    <S_FDI_AFG>-S_PROWI      = ME->GET_PROWI( ).
    <S_FDI_AFG>-S_PROLE      = ME->GET_PROLE( ).
    <S_FDI_AFG>-S_PROHE      = ME->GET_PROHE( ).

  endmethod.
  method ZIF_VCXI_PXSS_FDI~SET_DATA.

    data: LS_FDI_AFG_OLD type ZVCXI_P3SS_S_FDI_AFG,
          LS_FDI_AFG_NEW type ZVCXI_P3SS_S_FDI_AFG.
    data: LR_DATA_OLD  type ref to DATA.
    field-symbols: <S_DATA> type ANY.


***--------------------------------------------------------------------------------------
*** Dereference Data
    LR_DATA_OLD = cond #( when IR_DATA_OLD is supplied then IR_DATA_OLD
                                                       else ME->ZIF_VCXI_PXSS_FDI~GET_DATA( ) ).
    if LR_DATA_OLD is bound.
      assign LR_DATA_OLD->* to <S_DATA>.
      LS_FDI_AFG_OLD = corresponding #( <S_DATA> ).
    endif.

    if IR_DATA is bound.
      assign IR_DATA->* to <S_DATA>.
      LS_FDI_AFG_NEW = corresponding #( <S_DATA> ).
    endif.

*** Set SUPER Data
    SUPER->ZIF_VCXI_PXSS_FDI~SET_DATA( IR_DATA     = IR_DATA
                                       IR_DATA_OLD = LR_DATA_OLD
                                       IT_FDMEX    = IT_FDMEX ).


***--------------------------------------------------------------------------------------
*** Set Product Length
    if LS_FDI_AFG_OLD-S_PROLE ne LS_FDI_AFG_NEW-S_PROLE.
      ME->SET_PROLE( IS_PROLE = LS_FDI_AFG_NEW-S_PROLE
                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Product Width
    if LS_FDI_AFG_OLD-S_PROWI ne LS_FDI_AFG_NEW-S_PROWI.
      ME->SET_PROWI( IS_PROWI = LS_FDI_AFG_NEW-S_PROWI
                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Product Height
    if LS_FDI_AFG_OLD-S_PROHE ne LS_FDI_AFG_NEW-S_PROHE.
      ME->SET_PROHE( IS_PROHE = LS_FDI_AFG_NEW-S_PROHE
                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method ZIF_VCXI_PXSS_FG~GET_HNDDIM.

    data: LR_BL_ASFPROP type ref to ZCL_VCXI_P3SS_BL_ASFPROP.


*** Get Assembly Forward Property XML (Outbound)
    LR_BL_ASFPROP = ME->GET_ASFPX( ).
    check LR_BL_ASFPROP is bound.

*** Get Handling Dimension
    RS_HNDDIM = LR_BL_ASFPROP->S_HNDDIM.

  endmethod.
  method ZIF_VCXI_XCS_NGREP~FILL.

    data: LT_ATTR type CR_NETGRAPH_JSON->YT_ATTRIBUTE.


***------------------------------------------------------------------------------------------------
*** Get Attribute Data
    CR_NETGRAPH_JSON->ADD_ATTR( exporting IF_LABEL = ME->GET_PRODIM_DESCR( )
                                          IF_VALUE = |{ ME->GET_PROLE( )-PROLE style = simple }x| &&
                                                     |{ ME->GET_PROWI( )-PROWI style = simple }x| &&
                                                     |{ ME->GET_PROHE( )-PROHE style = simple } |
                                          IF_UOM   = ME->GET_PROLE( )-PROLE_UOM
                                changing  CT_ATTR  = LT_ATTR ).

*** Add Node
    data(LF_NODE_KEY) = CR_NETGRAPH_JSON->ADD_NODE( IF_TITLE      = |{ ME->GET_DESCR( ) }|
                                                    IF_DESCR      = |{ ME->GET_DESCR( ) }|
                                                    IF_ICON       = 'sap-icon://form'
                                                    IF_STATUS     = CR_NETGRAPH_JSON->GET_STATUS_BY_GATE( ME->R_GATE )
                                                    IF_GROUP_KEY  = IF_GROUP_KEY
                                                    IT_ATTR       = LT_ATTR ).

*** Register Gate to Node
    CR_NETGRAPH_JSON->REGISTER_TO_NODE( IR_OBJECT   = ME->R_GATE
                                        IF_NODE_KEY = LF_NODE_KEY ).

***------------------------------------------------------------------------------------------------
*** Find Provider for Shipping Manager
    data(LR_PR_SHIPMGR) = cast ZCL_VCXI_PXSU_PR_SHIPMGR( ME->GET_PRVDR_SHIPMGR( ) ).
    if LR_PR_SHIPMGR is bound.
      LR_PR_SHIPMGR->ZIF_VCXI_XCS_NGREP~FILL( exporting IF_GROUP_KEY     = IF_GROUP_KEY
                                              changing  CR_NETGRAPH_JSON = CR_NETGRAPH_JSON ).
    endif.

  endmethod.
