
class ZCL_VCXI_P0SS_EA_SPEC_AS definition
  public
  inheriting from /VCXI/CL_VKSR_CLSEA
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  type-pools ABAP .
  methods SET_CUSTMAT_INFO
    importing
      !IF_CMATN type ZVCXI_XCSS_CMATN optional
      !IF_MATDE type ZVCXI_XCSS_MATDE optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
      !IF_RAISE_EVENTS type ABAP_BOOL default ABAP_TRUE
    raising
      /VCXI/CX_CKX .
  methods GET_CUSTMAT_INFO
    exporting
      !EF_CMATN type ZVCXI_XCSS_CMATN
      !EF_MATDE type ZVCXI_XCSS_MATDE
    raising
      /VCXI/CX_CKX .
protected section.

  data R_PR_SPEC_AS type ref to ZCL_VCXI_P0SS_PR_SPEC_AS .
private section.
endclass. "ZCL_VCXI_P0SS_EA_SPEC_AS definition
class ZCL_VCXI_P0SS_EA_SPEC_AS implementation.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_GATE = IR_GATE ).

*** Assign the PR Class to the Element API Class
  try.
      move ME->R_GATE->R_CLSPR ?to ME->R_PR_SPEC_AS.
    catch CX_SY_MOVE_CAST_ERROR.
***   Element API ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '804'.
  endtry.

endmethod.
method GET_CUSTMAT_INFO.

  check ME->R_PR_SPEC_AS is bound.

***------------------------------------------------------------------
*** Get Customer Material Number
  if EF_CMATN is requested.
    EF_CMATN = ME->R_PR_SPEC_AS->GET_CMATN( ).
  endif.

*** Get Customer Material Description
  if EF_MATDE is requested.
    EF_MATDE = ME->R_PR_SPEC_AS->GET_MATDE( ).
  endif.

endmethod.
method SET_CUSTMAT_INFO.

  check ME->R_PR_SPEC_AS         is bound     and
        ME->R_GATE->IS_LOCKED( ) eq ABAP_TRUE.

***----------------------------------------------------------------------------
*** Set Customer Material Number
  if IF_CMATN is supplied.
    ME->R_PR_SPEC_AS->SET_CMATN( IF_CMATN = IF_CMATN
                                 IF_SETBY = IF_SETBY ).
  endif.

*** Set Customer Material Description
  if IF_MATDE is supplied.
    ME->R_PR_SPEC_AS->SET_MATDE( IF_MATDE = IF_MATDE
                                 IF_SETBY = IF_SETBY ).
  endif.

***----------------------------------------------------------------------------
*** Process Events
  if IF_RAISE_EVENTS eq ABAP_TRUE.
    ME->R_GATE->RAISE_EVENTS( ).
  endif.

endmethod.
