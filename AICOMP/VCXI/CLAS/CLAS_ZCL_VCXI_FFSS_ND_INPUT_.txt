
class ZCL_VCXI_FFSS_ND_INPUT definition
  public
  inheriting from ZCL_VCXI_F0SS_ND
  create public .

public section.

  constants C_UC_ADD type SYUCOMM value 'ADD' ##NO_TEXT.

  methods CONSTRUCTOR
    importing
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods GET_FCODE
    redefinition .
  methods HANDLE_UC_POPUP_SEL
    redefinition .
protected section.

  data T_PRVDR_DEF type ZVCXI_F0SS_TT_PRVDR_DEF .

  methods ADD_PRVDR
    importing
      !IF_ID type /VCXI/CKX_ID
    returning
      value(RR_GATE) type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods INIT_PRVDR_DEF
    raising
      /VCXI/CX_CKX .

  methods INIT_NODE
    redefinition .
private section.
endclass. "ZCL_VCXI_FFSS_ND_INPUT definition
class ZCL_VCXI_FFSS_ND_INPUT implementation.
  method ADD_PRVDR.

    data: LF_POSID type /VCXI/VKSR_POSID,
          LF_ID    type /VCXI/CKX_ID.
    data: LS_PRVDR_DEF  type ZVCXI_F0SS_S_PRVDR_DEF.

***--------------------------------------------------------------------------------------
*** Check ID
    check IF_ID is not initial.
    move IF_ID to LF_ID.

    read table ME->T_PRVDR_DEF into LS_PRVDR_DEF with key ID = LF_ID.

    LF_POSID = LINES( ME->T_PRVDR_DEF ) * 10.

    if LS_PRVDR_DEF-R_PARENT_GATE->F_ID eq 'CONV'.
      move 5 to LF_POSID.
    else.
***   Add after selected position
      add 5 to LF_POSID.
    endif.

***--------------------------------------------------------------------------------------
*** Create Provider
    check LS_PRVDR_DEF-R_PARENT_GATE is bound.

    RR_GATE = LS_PRVDR_DEF-R_PARENT_GATE->CREATE_ELEMENT( IF_ID = LF_ID ).
    if RR_GATE->IS_INUSE( ) eq ABAP_FALSE.
      RR_GATE->SET_INUSE( IF_INUSE = ABAP_TRUE ).
    endif.
    RR_GATE->SET_POSID( IF_POSID = LF_POSID ).

*** Reorg position ID's
    RR_GATE->REORG_POSID( ).

  endmethod.
  method CONSTRUCTOR.

*** call SUPER CONSTRUCTOR
    SUPER->CONSTRUCTOR( IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

  endmethod.
  method GET_FCODE.

    data:           LS_PRVDR_DEF  type      ZVCXI_F0SS_S_PRVDR_DEF.
    data:           LR_GATE       type ref to /VCXI/CL_VKSR_GATE.
    field-symbols:  <S_FCODE>     type      YS_FCODE.

***--------------------------------------------------------------------------------------
*** Create Popup Menu Entries for Node
    SUPER->GET_FCODE( importing ET_FCODE = ET_FCODE ).
    check ME->R_GATE->IS_LOCKED( ) eq ABAP_TRUE.

***--------------------------------------------------------------------------------------
*** Create Separation Line
    append initial line to ET_FCODE assigning <S_FCODE>.
    move C_UC_BLANK                      to <S_FCODE>-FCODE.
    move C_TEXT_SEP                      to <S_FCODE>-TEXT.

*** Add Split option
    append initial line to ET_FCODE assigning <S_FCODE>.
    move C_UC_SPLIT                      to <S_FCODE>-FCODE.
    move text-S01                        to <S_FCODE>-TEXT.

***--------------------------------------------------------------------------------------
*** Initialize Provider Definitions
    ME->INIT_PRVDR_DEF( ).

    if LINES( ME->T_PRVDR_DEF ) is not initial.
***   Create Separation Line
      append initial line to ET_FCODE assigning <S_FCODE>.
      move C_UC_BLANK                      to <S_FCODE>-FCODE.
      move C_TEXT_SEP                      to <S_FCODE>-TEXT.
    endif.

*** Add Provider Relations
    read table ME->T_PRVDR_DEF into LS_PRVDR_DEF index 1.
    if LS_PRVDR_DEF-R_PARENT_GATE is bound.
      move LS_PRVDR_DEF-R_PARENT_GATE to LR_GATE.
    endif.

    loop at ME->T_PRVDR_DEF into LS_PRVDR_DEF.
      if LS_PRVDR_DEF-R_PARENT_GATE ne LR_GATE.
        append initial line to ET_FCODE assigning <S_FCODE>.
        move C_UC_BLANK                      to <S_FCODE>-FCODE.
        move C_TEXT_SEP                      to <S_FCODE>-TEXT.
      endif.

      append initial line to ET_FCODE assigning <S_FCODE>.
      concatenate C_UC_ADD LS_PRVDR_DEF-ID   into <S_FCODE>-FCODE separated by '@'.
      concatenate text-M01 LS_PRVDR_DEF-DESCR into <S_FCODE>-TEXT separated by SPACE.

      move LS_PRVDR_DEF-R_PARENT_GATE to LR_GATE.
    endloop.

  endmethod.
  method HANDLE_UC_POPUP_SEL.

    data: LF_ID       type        /VCXI/CKX_ID.
    data: LR_CX_CKX   type ref to /VCXI/CX_CKX.

    SUPER->HANDLE_UC_POPUP_SEL( IF_FCODE = IF_FCODE ).

***--------------------------------------------------------------------------------------
*** Determine Action
    try.
***     Trigger specific Add command
        if IF_FCODE cp 'ADD@*'.
          move IF_FCODE+4 to LF_ID.
          ME->ADD_PRVDR( IF_ID = LF_ID ).
        endif.

      catch /VCXI/CX_CKX into LR_CX_CKX.
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method INIT_NODE.

    data:           LS_FITIN            type        ZVCXI_FFS_S_FITIN,
                    LS_SURWT            type        ZVCXI_F0S_S_SURWT.
    data:           LR_PR_INPUT         type ref to ZCL_VCXI_FFSS_PR_INPUT.
    field-symbols:  <S_NVAL>            type        YS_NVALS.

***--------------------------------------------------------------------------------------
*** Call Super
    SUPER->INIT_NODE( ).

***--------------------------------------------------------------------------------------
*** Check Input Gate is INPUT
    try.
        move ME->R_GATE->R_CLSPR ?to LR_PR_INPUT.
      catch CX_SY_MOVE_CAST_ERROR.
        clear LR_PR_INPUT.
    endtry.

    check LR_PR_INPUT is bound.

*** Initialize Node Values from Gate
    move 2 to ME->F_NETTYPE.

*** Set Node Description
    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = 0.
    <S_NVAL>-VAL  = LR_PR_INPUT->GET_DESCR( ).

***--------------------------------------------------------------------------------------
*** Get Film Thickness and UOM
    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = 2.
    move text-D01 to <S_NVAL>-VAL.

    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = 3.
    LR_PR_INPUT->ZIF_VCXI_FFSS_COMM_NEXT~GET_FITIN( importing ES_FITIN = LS_FITIN ).
    <S_NVAL>-VAL = ME->CONV_ANY_TO_VAL( IF_VALUE = LS_FITIN-FITIN
                                        IF_UOM   = LS_FITIN-FITIN_UOM ).

***--------------------------------------------------------------------------------------
*** Get Surface Weight and UOM
    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = 4.
    move text-D02 to <S_NVAL>-VAL.

    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = 5.
    LR_PR_INPUT->ZIF_VCXI_FFSS_COMM_NEXT~GET_SURWT( importing ES_SURWT = LS_SURWT ).
    <S_NVAL>-VAL = ME->CONV_ANY_TO_VAL( IF_VALUE = LS_SURWT-SURWT
                                        IF_UOM   = LS_SURWT-SURWT_UOM ).

***--------------------------------------------------------------------------------------
*** Set Status Icon
    ME->SET_STATUS_ICON( ).

  endmethod.
  method INIT_PRVDR_DEF.

    data: LF_ACTIV_PRVDR       type        NUMC3.
    data: LS_PRVDR_DEF type        /VCXI/VKSR_S_PRVDR_DEF,
          LS_PRVDR     type        ZVCXI_F0SS_S_PRVDR_DEF.
    data: LT_GATE      type        /VCXI/VKSR_TT_GATE,
          LT_PRVDR_DEF type        /VCXI/VKSR_TT_PRVDR_DEF,
          LT_PRVDR     type        /VCXI/VKSR_TT_GATE.
    data: LR_GATE      type ref to /VCXI/CL_VKSR_GATE,
          LR_GATE_SPEC type ref to /VCXI/CL_VKSR_GATE,
          LR_PR_SPEC   type ref to ZCL_VCXI_F0SS_PR_SPEC.

    refresh: ME->T_PRVDR_DEF.

    LT_PRVDR_DEF = ME->R_GATE->GET_PRVDR_DEF( IF_TPRVR = /VCXI/IF_VKSC_REGELMNT_PRVTY=>C_TPRVR_CHILD ).
    sort LT_PRVDR_DEF.
    delete adjacent duplicates from LT_PRVDR_DEF comparing ID.

    loop at LT_PRVDR_DEF into LS_PRVDR_DEF.
      clear: LF_ACTIV_PRVDR, LS_PRVDR.

***   Get Gates of Provider
      LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = LS_PRVDR_DEF-IPRVT
                                       IF_INUSE = ABAP_TRUE ).

      loop at LT_GATE into LR_GATE.
***     Get all Active Provider with searched ID
        check LR_GATE->F_ID eq LS_PRVDR_DEF-ID.
        add 1 to LF_ACTIV_PRVDR.
      endloop.

***   Check if there are less then maximal Provider
      check LF_ACTIV_PRVDR < LS_PRVDR_DEF-QTYMA.

      move LS_PRVDR_DEF to LS_PRVDR-S_PRVDR_DEF.
      move LT_GATE      to LS_PRVDR-T_GATE_SEQ.
      move ME->R_GATE   to LS_PRVDR-R_PARENT_GATE.

      append LS_PRVDR to ME->T_PRVDR_DEF.
    endloop.

*** Get Specification
    LT_PRVDR = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_F0SS_PR_SPEC=>C_IPRVT_F0_SPEC ).
    read table LT_PRVDR into LR_GATE_SPEC index 1.
    check LR_GATE_SPEC is bound.

    try .
        move LR_GATE_SPEC->R_CLSPR ?to LR_PR_SPEC.
      catch CX_SY_MOVE_CAST_ERROR.
        clear LR_PR_SPEC.
    endtry.
    check LR_PR_SPEC is bound.

*** Get Conversion Steps
    LR_PR_SPEC->GET_CSTEPS_DEF( importing ET_PRVDR_CSTEPS = ME->T_PRVDR_DEF ).

  endmethod.
