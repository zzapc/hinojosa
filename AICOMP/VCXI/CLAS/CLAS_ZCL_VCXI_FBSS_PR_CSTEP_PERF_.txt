
class ZCL_VCXI_FBSS_PR_CSTEP_PERF definition
  public
  inheriting from ZCL_VCXI_FBSS_PR_CSTEP
  create public .

public section.

  constants C_PARID_COARE type /VCXI/VKS_PARID value 'ZVCXI_FB_COARE' ##NO_TEXT.
  constants C_PARID_DCUTT type /VCXI/VKS_PARID value 'ZVCXI_F0_DCUTT' ##NO_TEXT.
  constants C_PARID_LCOUT type /VCXI/VKS_PARID value 'ZVCXI_FB_LCOUT' ##NO_TEXT.
  constants C_PARID_NOLAS type /VCXI/VKS_PARID value 'ZVCXI_F0_NOLAS' ##NO_TEXT.
  constants C_PARID_TPERF type /VCXI/VKS_PARID value 'ZVCXI_F0_TPERF' ##NO_TEXT.

  methods CHECK_COARE
    returning
      value(RF_RETURN) type I
    raising
      /VCXI/CX_CKX .
  methods CHECK_LCOUT
    returning
      value(RF_RETURN) type I
    raising
      /VCXI/CX_CKX .
  methods CHECK_TPERF
    returning
      value(RF_RETURN) type I
    raising
      /VCXI/CX_CKX .
  methods GET_COARE
    exporting
      !ES_COARE type ZVCXI_FBS_S_COARE
      !EF_SETBY type /VCXI/VKSR_SETBY
    raising
      /VCXI/CX_CKX .
  methods GET_COWGT
    exporting
      !ES_COWGT type ZVCXI_FBS_S_COWGT
    raising
      /VCXI/CX_CKX .
  methods GET_DCUTT
    exporting
      !EF_DCUTT type ZVCXI_F0S_DCUTT
      !EF_SETBY type /VCXI/VKSR_SETBY
      !ER_CUST_MATNR type ref to ZCL_VCXI_XCS_CUST_MATNR
      !EF_DESCR type /VCXI/CKX_DESCR
    raising
      /VCXI/CX_CKX .
  methods GET_NOLAS
    exporting
      !EF_NOLAS type ZVCXI_F0S_NOLAS
      !EF_SETBY type /VCXI/VKSR_SETBY
    raising
      /VCXI/CX_CKX .
  methods GET_TPERF
    exporting
      !EF_TPERF type ZVCXI_F0S_TPERF
      !EF_SETBY type /VCXI/VKSR_SETBY
      !EF_DESCR type /VCXI/CKX_DESCR
    raising
      /VCXI/CX_CKX .
  methods GET_TPERF_FLASR
    returning
      value(RF_FLASR) type ZVCXI_F0S_FLASR
    raising
      /VCXI/CX_CKX .
  methods SET_COARE
    importing
      !IS_COARE type ZVCXI_FBS_S_COARE
      !IF_SETBY type /VCXI/VKSR_SETBY default 'USER'
    raising
      /VCXI/CX_CKX .
  methods SET_DCUTT
    importing
      !IF_DCUTT type ZVCXI_F0S_DCUTT
      !IF_SETBY type /VCXI/VKSR_SETBY default 'USER'
    raising
      /VCXI/CX_CKX .
  methods SET_LCOUT
    importing
      !IF_LCOUT type ZVCXI_FBS_LCOUT
      !IF_SETBY type /VCXI/VKSR_SETBY default 'USER'
    raising
      /VCXI/CX_CKX .
  methods SET_NOLAS
    importing
      !IF_NOLAS type ZVCXI_F0S_NOLAS
      !IF_SETBY type /VCXI/VKSR_SETBY default 'USER'
    raising
      /VCXI/CX_CKX .
  methods SET_TPERF
    importing
      !IF_TPERF type ZVCXI_F0S_TPERF
      !IF_SETBY type /VCXI/VKSR_SETBY default 'USER'
    raising
      /VCXI/CX_CKX .
  methods GET_LCOUT
    exporting
      !EF_LCOUT type ZVCXI_FBS_LCOUT
      !EF_SETBY type /VCXI/VKSR_SETBY
    raising
      /VCXI/CX_CKX .

  methods GET_STATUS
    redefinition .
  methods GET_STATUS_MSG
    redefinition .
  methods HANDLE_EVENT
    redefinition .
  methods ZIF_VCXI_XCS_NGREP~FILL
    redefinition .
protected section.

  constants C_IEVNT_I_CHECK_LCOUT type /VCXI/VKSC_IEVNT value 'CHECK_LCOUT' ##NO_TEXT.
  constants C_IEVNT_O_COARE_CHANGED type /VCXI/VKSC_IEVNT value 'COARE_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_DCUTT_CHANGED type /VCXI/VKSC_IEVNT value 'DCUTT_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_LCOUT_CHANGED type /VCXI/VKSC_IEVNT value 'LCOUT_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_NOLAS_CHANGED type /VCXI/VKSC_IEVNT value 'NOLAS_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_TPERF_CHANGED type /VCXI/VKSC_IEVNT value 'TPERF_CHANGED' ##NO_TEXT.

  methods CHECK_DCUTT
    returning
      value(RF_RETURN) type I
    raising
      /VCXI/CX_CKX .
  methods CHECK_NOLAS
    returning
      value(RF_RETURN) type I
    raising
      /VCXI/CX_CKX .
  methods HANDLE_CHECK_LCOUT
    raising
      /VCXI/CX_CKX .

  methods GET_CUTOUT
    redefinition .
private section.
endclass. "ZCL_VCXI_FBSS_PR_CSTEP_PERF definition
class ZCL_VCXI_FBSS_PR_CSTEP_PERF implementation.
method CHECK_COARE.

*** 0 - Cutout Area is maintained.
*** 1 - Cutout Area is not maintained.
*** 2 - Do not check.

  data: LF_LCOUT type  ZVCXI_FBS_LCOUT.
  data: LS_COARE type  ZVCXI_FBS_S_COARE.

  move 2 to RF_RETURN.

  ME->GET_COARE( importing ES_COARE = LS_COARE ).
  if LS_COARE-COARE is not initial.
    move 0 to RF_RETURN.
    return.
  endif.

*** Get Cutout Location
  ME->GET_LCOUT( importing EF_LCOUT = LF_LCOUT ).

*** Check if Mandatory
  if LF_LCOUT is not initial and LS_COARE-COARE is initial.
    move 1 to RF_RETURN.
  endif.

endmethod.
method CHECK_DCUTT.
*** 0 - Do not check.
*** 1 - Die Cut Tool Number is maintained and valid.
*** 2 - Die Cut Tool Number is not valid.
*** 3 = Die Cut Tool Number is not valid in Plant.

  data: LR_CUST_MATNR type ref to ZCL_VCXI_XCS_CUST_MATNR.

  move 0 to RF_RETURN.

***--------------------------------------------------------------------------------------
*** Perform Check only in case of no laser
  check ME->GET_TPERF_FLASR( ) eq ABAP_FALSE.

***--------------------------------------------------------------------------------------
*** Get Die Cut Tool
  ME->GET_DCUTT( importing ER_CUST_MATNR = LR_CUST_MATNR ).
  check LR_CUST_MATNR is bound.

*** Die Cut Tool Number is valid
  if LR_CUST_MATNR->F_CHECK_MATNR ne ABAP_TRUE.
    move 2 to RF_RETURN.
    exit.
  endif.

*** Die Cut Tool Number is valid in Plant
  if LR_CUST_MATNR->F_CHECK_MAT_MARC ne ABAP_TRUE.
    move 3 to RF_RETURN.
    exit.
  endif.

*** Die Cut Tool Number is maintained and valid
  move 1 to RF_RETURN.

endmethod.
method CHECK_LCOUT.

*** 0 - Location Cutout is maintained.
*** 1 - Location Cutout is not maintained.
*** 2 - Do not check.

  data:  LF_LCOUT type  ZVCXI_FBS_LCOUT.
  data:  LS_COARE type  ZVCXI_FBS_S_COARE.

  move 2 to RF_RETURN.

***--------------------------------------------------------------------------------------
*** Get Cutout Location
  ME->GET_LCOUT( importing EF_LCOUT = LF_LCOUT ).

  if LF_LCOUT is not initial.
    move 0 to RF_RETURN.
    return.
  endif.

*** Get Cutout Area
  ME->GET_COARE( importing ES_COARE = LS_COARE ).
  if LS_COARE-COARE is not initial and
     LF_LCOUT       is initial.
    move 1 to RF_RETURN.
    return.
  endif.


endmethod.
method CHECK_NOLAS.

*** 0 - Number of Laser Lines is maintained.
*** 1 - Number of Laser Lines is not maintained.
*** 2 - Do not check.

  data: LF_NOLAS type  ZVCXI_F0S_NOLAS,
        LF_FLASR type  ZVCXI_F0S_FLASR.

  move 2 to RF_RETURN.

  move ME->GET_TPERF_FLASR( )     to LF_FLASR.
  check LF_FLASR eq ABAP_TRUE.

*** Get Number of Laser Lines
  ME->GET_NOLAS( importing EF_NOLAS = LF_NOLAS ).
  if LF_NOLAS is not initial.
    move 0 to RF_RETURN.
    return.
  endif.

*** Check if Mandatory
  if LF_FLASR eq ABAP_TRUE and LF_NOLAS is initial.
    move 1 to RF_RETURN.
  endif.

endmethod.
method CHECK_TPERF.

*** 0 - Perforation Type is maintained.
*** 1 - Perforation Type is not maintained.

  data: LF_TPERF  type  ZVCXI_F0S_TPERF.

  move 1 to RF_RETURN.

  ME->GET_TPERF( importing EF_TPERF = LF_TPERF ).
  if LF_TPERF is not initial.
    move 0 to RF_RETURN.
  endif.

endmethod.
  method GET_COARE.

    data: LS_PARAM  type  /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_COARE ).
    move LS_PARAM-ATFLV to ES_COARE-COARE.
    move LS_PARAM-UNIT  to ES_COARE-COARE_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_COWGT.

    data: LF_LCOUT       type ZVCXI_FBS_LCOUT.
    data: LS_COARE       type ZVCXI_FBS_S_COARE,
          LS_BPINFO_PREV type ZVCXI_FBSS_S_BPINFO,
          LS_BPINFO_NEXT type ZVCXI_FBSS_S_BPINFO,
          LS_BPWGT_PREV  type ZVCXI_FBS_S_BPWGT,
          LS_BPWGT_NEXT  type ZVCXI_FBS_S_BPWGT,
          LS_BPWGT_DIFF  type ZVCXI_FBS_S_BPWGT,
          LS_BGWGT       type ZVCXI_FBS_S_BGWGT.
    data: LR_BL_BGFPROP_PREV type ref to ZCL_VCXI_FBSS_BL_BGFPROP,
          LR_BL_BGFPROP_NEXT type ref to ZCL_VCXI_FBSS_BL_BGFPROP.


*** Get Bag Weight (Inbound)
    ME->ZIF_VCXI_FBSS_COMM_PREV~GET_BGWGT( importing ES_BGWGT = LS_BGWGT ).

*** Get Cutout Area & Location
    ME->GET_CUTOUT( importing ES_COARE = LS_COARE
                              EF_LCOUT = LF_LCOUT ).

    check LS_COARE is not initial
      and LF_LCOUT is not initial.

*** Get Bag Forward Property XML (Inbound)
    ME->ZIF_VCXI_FBSS_COMM_PREV~GET_BGFPX( importing ER_BL_BGFPROP = LR_BL_BGFPROP_PREV ).
    check LR_BL_BGFPROP_PREV is bound.

*** Get Bag Forward Property XML (Outbound)
    ME->ZIF_VCXI_FBSS_COMM_NEXT~GET_BGFPX( importing ER_BL_BGFPROP = LR_BL_BGFPROP_NEXT ).
    check LR_BL_BGFPROP_NEXT is bound.

*** Get Bag Part Info (Previous)
    read table LR_BL_BGFPROP_PREV->T_BPINFO into LS_BPINFO_PREV with key IBPAR = LF_LCOUT.
    if SY-SUBRC eq 0.

***   Calculate Bag Part Weight (Inbound)
      ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_BPINFO_PREV-BPARE
                                                          IF_CALCV_UOM_1 = LS_BPINFO_PREV-BPARE_UOM
                                                          IF_CALCV_2     = LS_BPINFO_PREV-SURWT
                                                          IF_CALCV_UOM_2 = LS_BPINFO_PREV-SURWT_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_MULTIPLY
                                                          IF_UOM         = LS_BGWGT-BGWGT_UOM
                                                importing EF_CALCV       = LS_BPWGT_PREV-BPWGT
                                                          EF_CALCV_UOM   = LS_BPWGT_PREV-BPWGT_UOM ).

    endif.

*** Get Bag Part Info (Next)
    read table LR_BL_BGFPROP_NEXT->T_BPINFO into LS_BPINFO_NEXT with key IBPAR = LF_LCOUT.
    if SY-SUBRC eq 0.

***   Calculate Bag Part Weight (Outbound)
      ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_BPINFO_NEXT-BPARE
                                                          IF_CALCV_UOM_1 = LS_BPINFO_NEXT-BPARE_UOM
                                                          IF_CALCV_2     = LS_BPINFO_NEXT-SURWT
                                                          IF_CALCV_UOM_2 = LS_BPINFO_NEXT-SURWT_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_MULTIPLY
                                                          IF_UOM         = LS_BGWGT-BGWGT_UOM
                                                importing EF_CALCV       = LS_BPWGT_NEXT-BPWGT
                                                          EF_CALCV_UOM   = LS_BPWGT_NEXT-BPWGT_UOM ).

    endif.

*** Calculate Bag Part Difference
    ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_BPWGT_PREV-BPWGT
                                                        IF_CALCV_UOM_1 = LS_BPWGT_PREV-BPWGT_UOM
                                                        IF_CALCV_2     = LS_BPWGT_NEXT-BPWGT
                                                        IF_CALCV_UOM_2 = LS_BPWGT_NEXT-BPWGT_UOM
                                                        IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_SUBTRACT
                                                        IF_UOM         = LS_BGWGT-BGWGT_UOM
                                              importing EF_CALCV       = LS_BPWGT_DIFF-BPWGT
                                                        EF_CALCV_UOM   = LS_BPWGT_DIFF-BPWGT_UOM ).

    check LS_BPWGT_DIFF-BPWGT gt 0.

    move LS_BPWGT_DIFF to ES_COWGT.


  endmethod.
  method GET_CUTOUT.

*** Get Cutout Area
    ME->GET_COARE( importing ES_COARE = ES_COARE ).

*** Get Cutout Location
    ME->GET_LCOUT( importing EF_LCOUT = EF_LCOUT ).

  endmethod.
  method GET_DCUTT.

    data: LF_DCONF type /VCXI/VKCR_DCONF.
    data: LS_PARAM type /VCXI/VKSR_S_PARAM,
          LS_MAKT  type MAKT.

***------------------------------------------------------------------------------------------------
*** Get Parameter
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_DCUTT ).
    move LS_PARAM-ATWRT to EF_DCUTT.
    move LS_PARAM-SETBY to EF_SETBY.

    check EF_DCUTT is not initial.

***------------------------------------------------------------------------------------------------
*** Get Instance of Material
    if ER_CUST_MATNR is requested.
      ME->R_GATE->GET_CONFIG_INFO( importing EF_DCONF = LF_DCONF ).
      ER_CUST_MATNR = ZCL_VCXI_XCS_CUST_MATNR=>GET_INSTANCE( IF_MATNR = EF_DCUTT
                                                             IF_WERKS = ME->GET_WERKS( )
                                                             IF_DATE  = LF_DCONF ).
    endif.

*** Get Material Description
    if EF_DESCR is requested.
      call function 'MAKT_SINGLE_READ'
        exporting
          MATNR      = EF_DCUTT
          SPRAS      = SY-LANGU
        importing
          WMAKT      = LS_MAKT
        exceptions
          WRONG_CALL = 1
          NOT_FOUND  = 2
          others     = 3.

      if SY-SUBRC ne 0.
        clear EF_DESCR.
      else.
        move LS_MAKT-MAKTX to EF_DESCR.
      endif.
    endif.

  endmethod.
  method GET_LCOUT.

    data: LS_PARAM  type  /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_LCOUT ).
    move LS_PARAM-ATWRT to EF_LCOUT.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_NOLAS.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_NOLAS ).
    move LS_PARAM-ATFLV to EF_NOLAS.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_STATUS.

    SUPER->GET_STATUS( receiving RF_ELMST = RF_ELMST ).

*** Check Cutout Area
    case ME->CHECK_COARE( ) .
      when 0.
        RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                       IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G ).
      when 1.
        RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                       IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R ).
    endcase.

*** Check Cutout Location
    case ME->CHECK_LCOUT( ) .
      when 0.
        RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                       IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G ).
      when 1.
        RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                       IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R ).
    endcase.

*** Check Perforation Type
    case ME->CHECK_TPERF( ) .
      when 0.
        RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                       IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G ).
      when 1.
        RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                       IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R ).
    endcase.

*** Check Number of Laser Lines
    case ME->CHECK_NOLAS( ) .
      when 0.
        RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                       IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G ).
      when 1.
        RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                       IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R ).
    endcase.

*** Check Die Cut Tool Number
    case ME->CHECK_DCUTT( ) .
      when 0.
      when 1.
        RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                       IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G ).
      when others.
        RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                       IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R ).
    endcase.

  endmethod.
  method GET_STATUS_MSG.

    SUPER->GET_STATUS_MSG( IR_MESSAGE = IR_MESSAGE ).

*** Check Cutout Area
    case ME->CHECK_COARE( ).
      when 0.
***     Cutout Area is maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FBSS'
                                 IF_MSGTY = 'S'
                                 IF_MSGNO = '451' ).

      when 1.
***     Cutout Area is not maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FBSS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '452' ).

    endcase.

*** Check Cutout Location
    case ME->CHECK_LCOUT( ).
      when 0.
***     Cutout Location is maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FBSS'
                                 IF_MSGTY = 'S'
                                 IF_MSGNO = '459' ).

      when 1.
***     Cutout Location is not maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FBSS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '460' ).

    endcase.

*** Check Perforation Type
    case ME->CHECK_TPERF( ).
      when 0.
***     Perforation Type is maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FBSS'
                                 IF_MSGTY = 'S'
                                 IF_MSGNO = '453' ).

      when 1.
***     Perforation Type is not maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FBSS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '454' ).

    endcase.

*** Check Number of Laser Lines
    case ME->CHECK_NOLAS( ).
      when 0.
***     Number of Laser Lines is maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FBSS'
                                 IF_MSGTY = 'S'
                                 IF_MSGNO = '455' ).

      when 1.
***     Number of Laser Lines is not maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FBSS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '456' ).

    endcase.


*** Check Die Cut Tool Number
    case ME->CHECK_DCUTT( ).
      when 0.
      when 1.
***     Die Cut Tool Number is maintained and valid.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FBSS'
                                 IF_MSGTY = 'S'
                                 IF_MSGNO = '457' ).

      when 2.
***     Die Cut Tool Number is not valid.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '458' ).
      when 3.
***     Die Cut Tool Number is not valid in Plant.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '484' ).

    endcase.

  endmethod.
  method GET_TPERF.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TPERF ).
    move LS_PARAM-ATWRT to EF_TPERF.
    move LS_PARAM-SETBY to EF_SETBY.

    check EF_TPERF is not initial.

    if EF_DESCR is requested.
***   Get Perforation Type Description
      select single DESCR
               from ZVCXI_F0S_TC030T
               into EF_DESCR
              where LANGU eq SY-LANGU
                and TPERF eq EF_TPERF.

      if SY-SUBRC ne 0.
        select single DESCR
                 from ZVCXI_F0S_TC030T
                 into EF_DESCR
                where TPERF eq EF_TPERF.
      endif.
    endif.

  endmethod.
  method GET_TPERF_FLASR.

    data: LF_TPERF  type  ZVCXI_F0S_TPERF.

*** Get Perforation Type
    ME->GET_TPERF( importing EF_TPERF = LF_TPERF ).
    check LF_TPERF is not initial.

*** Get Perforation Type  Flag - Laser
    RF_FLASR = ZCL_VCXI_F0S_CUST_PERF=>GET_FLASR( IF_TPERF = LF_TPERF ).

  endmethod.
  method HANDLE_CHECK_LCOUT.

    data: LF_LCOUT      type        ZVCXI_FBS_LCOUT.
    data: LR_BL_BGFPROP type ref to ZCL_VCXI_FBSS_BL_BGFPROP.

*** Get Cutout Location
    ME->GET_LCOUT( importing EF_LCOUT = LF_LCOUT ).

*** Get Bag Property Bag Parts
    ME->ZIF_VCXI_FBSS_COMM_NEXT~GET_BGFPX( importing ER_BL_BGFPROP = LR_BL_BGFPROP ).
    read table LR_BL_BGFPROP->T_BPINFO transporting no fields
                                       with key IBPAR = LF_LCOUT.
    check SY-SUBRC ne 0.

*** Reset Cutout Location
    clear LF_LCOUT.
    ME->SET_LCOUT( exporting IF_LCOUT = LF_LCOUT ).

  endmethod.
  method HANDLE_EVENT.

    SUPER->HANDLE_EVENT( IF_IEVNT         = IF_IEVNT
                         IT_GATE_PROVIDER = IT_GATE_PROVIDER ).

    case IF_IEVNT.

***   Check Cutout Location
      when C_IEVNT_I_CHECK_LCOUT.
        ME->HANDLE_CHECK_LCOUT( ).

    endcase.
  endmethod.
  method SET_COARE.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_COARE ).

    move IS_COARE-COARE     to LS_PARAM-ATFLV.
    move IS_COARE-COARE_UOM to LS_PARAM-UNIT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.

***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_COARE_CHANGED ).

    endif.

  endmethod.
  method SET_DCUTT.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_DCUTT ).

    move IF_DCUTT           to LS_PARAM-ATWRT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.

***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_DCUTT_CHANGED ).

    endif.

  endmethod.
  method SET_LCOUT.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_LCOUT ).

    move IF_LCOUT to LS_PARAM-ATWRT.
    move IF_SETBY to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.

***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_LCOUT_CHANGED ).

    endif.

  endmethod.
  method SET_NOLAS.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_NOLAS ).

    move IF_NOLAS           to LS_PARAM-ATFLV.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.

***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_NOLAS_CHANGED ).

    endif.

  endmethod.
  method SET_TPERF.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TPERF ).

    move IF_TPERF           to LS_PARAM-ATWRT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.

***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_TPERF_CHANGED ).

    endif.

  endmethod.
  method ZIF_VCXI_XCS_NGREP~FILL.

    data: LT_ATTR type CR_NETGRAPH_JSON->YT_ATTRIBUTE.


*** Get Attribute Data
    ME->GET_TPERF( importing EF_TPERF = data(LF_TPERF)                "Perforation Type
                             EF_DESCR = data(LF_DESCR) ).
    CR_NETGRAPH_JSON->ADD_ATTR( exporting IF_LABEL     = CR_NETGRAPH_JSON->GET_LABEL( IF_VALUE = LF_TPERF )
                                          IF_VALUE     = LF_DESCR
                                changing  CT_ATTR      = LT_ATTR ).

*** Add Node
    data(LF_NODE_KEY) = CR_NETGRAPH_JSON->ADD_NODE( IF_TITLE      = |{ ME->GET_DESCR( ) }|
                                                    IF_DESCR      = |{ ME->GET_DESCR( ) }|
                                                    IF_ICON       = 'sap-icon://horizontal-grip'
                                                    IF_STATUS     = CR_NETGRAPH_JSON->GET_STATUS_BY_GATE( ME->R_GATE )
                                                    IF_GROUP_KEY  = IF_GROUP_KEY
                                                    IT_ATTR       = LT_ATTR ).

*** Register Gate to Node
    CR_NETGRAPH_JSON->REGISTER_TO_NODE( IR_OBJECT   = ME->R_GATE
                                        IF_NODE_KEY = LF_NODE_KEY ).

  endmethod.
