
class ZCL_VCXI_P1SS_UI_CSTEP_ADSTP definition
  public
  inheriting from ZCL_VCXI_P1SS_UI_CSTEP
  create public .

public section.

  data S_CSTEP_ADSTP_VIS type ZVCXI_P1SS_S_CSTEP_ADSTP_VIS .
  constants C_UC_PSCALE type SYUCOMM value 'PSCALE' ##NO_TEXT.

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  data R_PR_CSTEP_ADSTP type ref to ZCL_VCXI_P1SS_PR_CSTEP_ADSTP .
  data R_GRID_MS_ADMAT type ref to ZCL_VCXI_PXSS_GRID_MS_ADMAT .

  methods HANDLE_UC_PSCALE
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_P1SS_UI_CSTEP_ADSTP definition
class ZCL_VCXI_P1SS_UI_CSTEP_ADSTP implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

***--------------------------------------------------------------------------------------
*** Bind UI to Dynpro
    call function 'ZVCXI_P1SS_UI_CSTEP_ADSTP_BIND'
      exporting
        IR_UI_CSTEP_ADSTP = ME
      importing
        ES_DYNP           = ME->S_DYNP.

***--------------------------------------------------------------------------------------
*** Create Grid for Additional Materials
    create object ME->R_GRID_MS_ADMAT
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_MS_ADMAT'.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_CSTEP_ADSTP.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

    if ME->R_GRID_MS_ADMAT is bound.
      ME->R_GRID_MS_ADMAT->DO_FREE( ).
      clear ME->R_GRID_MS_ADMAT.
    endif.

  endmethod.
  method DO_MODIFY_SCREEN.

    try.
        SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                 IF_DYNNR = IF_DYNNR ).

***----------------------------------------------------------------------------
***     Modify Screen
        loop at screen.

***       Fields based on Step Type
          case SCREEN-GROUP1.
            when 'MAN'.   "Manual
              if ME->S_CSTEP_ADSTP_VIS-TASTP ne 'M'.
                move 0 to SCREEN-INPUT.
                move 0 to SCREEN-ACTIVE.
              endif.
            when 'OUT'.    "Outsourcing
              if ME->S_CSTEP_ADSTP_VIS-TASTP ne 'O'.
                move 0 to SCREEN-INPUT.
                move 0 to SCREEN-ACTIVE.
              endif.
            when 'M/O'.    "Manual/Outsourcing
              if ME->S_CSTEP_ADSTP_VIS-TASTP ne 'M' and
                 ME->S_CSTEP_ADSTP_VIS-TASTP ne 'O'.
                move 0 to SCREEN-INPUT.
                move 0 to SCREEN-ACTIVE.
              endif.
          endcase.

***       Handling Dimension changeable if Override Flag is set
          if SCREEN-GROUP1 eq 'OHD'.
            if ME->S_CSTEP_ADSTP_VIS-FOVRR_HNDDIM eq ABAP_FALSE.
              move 0 to SCREEN-INPUT.
            endif.
          endif.

          modify screen.
        endloop.

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Super PBO
    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize Grid
    if ME->R_GRID_MS_ADMAT is bound.
      ME->R_GRID_MS_ADMAT->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_MS_ADMAT->REFRESH_GRID( ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_CSTEP_ADSTP_VIS type ZVCXI_P1SS_S_CSTEP_ADSTP_VIS.


*** Call SUPER
    SUPER->DO_UPDATE_VIS( IS_CSTEP_VIS = IS_CSTEP_VIS ).

    check ME->R_PR_CSTEP_ADSTP is bound.

*** "Cast" Vis Structure
    move-corresponding IS_CSTEP_VIS to LS_CSTEP_ADSTP_VIS.

***--------------------------------------------------------------------------------------
*** Set Additional Step Type
    if ME->S_CSTEP_ADSTP_VIS-TASTP ne LS_CSTEP_ADSTP_VIS-TASTP.
      ME->R_PR_CSTEP_ADSTP->SET_TASTP( IF_TASTP = LS_CSTEP_ADSTP_VIS-TASTP
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Sheet Direction
    if ME->S_CSTEP_ADSTP_VIS-SHTDI ne LS_CSTEP_ADSTP_VIS-SHTDI.
      ME->R_PR_CSTEP_ADSTP->SET_SHTDI( IF_SHTDI = LS_CSTEP_ADSTP_VIS-SHTDI
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Text
    if ME->S_CSTEP_ADSTP_VIS-TEXT ne LS_CSTEP_ADSTP_VIS-TEXT.
      ME->R_PR_CSTEP_ADSTP->SET_TEXT( IF_TEXT = LS_CSTEP_ADSTP_VIS-TEXT
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Machine Speed
    if ME->S_CSTEP_ADSTP_VIS-S_MSRS ne LS_CSTEP_ADSTP_VIS-S_MSRS.
      ME->R_PR_CSTEP_ADSTP->SET_MSRS( IS_MSRS = LS_CSTEP_ADSTP_VIS-S_MSRS
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Scrap
    if ME->S_CSTEP_ADSTP_VIS-S_SCRAP ne LS_CSTEP_ADSTP_VIS-S_SCRAP.
      ME->R_PR_CSTEP_ADSTP->SET_SCRAP( IS_SCRAP = LS_CSTEP_ADSTP_VIS-S_SCRAP
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Vendor
    if ME->S_CSTEP_ADSTP_VIS-LIFNR ne LS_CSTEP_ADSTP_VIS-LIFNR.
      ME->R_PR_CSTEP_ADSTP->SET_LIFNR( IF_LIFNR = LS_CSTEP_ADSTP_VIS-LIFNR
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Update Handling Dimension if it is "Overriden"
    if ME->S_CSTEP_ADSTP_VIS-FOVRR_HNDDIM eq ABAP_TRUE and
       ME->S_CSTEP_ADSTP_VIS-S_HNDDIM ne LS_CSTEP_ADSTP_VIS-S_HNDDIM.
      ME->R_PR_CSTEP_ADSTP->SET_HNDDIM( IS_HNDDIM = LS_CSTEP_ADSTP_VIS-S_HNDDIM
                                        IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Flag Override Handling Dimension
    if ME->S_CSTEP_ADSTP_VIS-FOVRR_HNDDIM ne LS_CSTEP_ADSTP_VIS-FOVRR_HNDDIM.
      ME->R_PR_CSTEP_ADSTP->SET_HNDDIM( IF_FOVRR = LS_CSTEP_ADSTP_VIS-FOVRR_HNDDIM ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

***--------------------------------------------------------------------------------------
*** Process Changes in Grid
    if ME->R_GRID_MS_ADMAT is bound.
      ME->R_GRID_MS_ADMAT->CHECK_CHANGED_DATA( ).
    endif.

***--------------------------------------------------------------------------------------
*** Call SUPER
    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    case IF_UCOMM.
      when C_UC_PSCALE.
        ME->HANDLE_UC_PSCALE( ).

    endcase.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_CSTEP_ADSTP.

  endmethod.
  method HANDLE_UC_PSCALE.

*** Call Popup with Price Scales
    ZCL_VCXI_XCS_POPUP_PSCALE=>START( IR_GATE   = ME->R_GATE
                                      IR_UIGATE = ME->R_UIGATE
                                      IF_LOCKED = ME->R_PR_CSTEP_ADSTP->ZIF_VCXI_XCS_PSCALE_MGR~IS_CHANGEABLE( ) ).

  endmethod.
  method INIT_VIS.

    SUPER->INIT_VIS( ).

    free: ME->S_CSTEP_ADSTP_VIS.
    ME->S_CSTEP_ADSTP_VIS-S_CSTEP_VIS = ME->S_CSTEP_VIS.

***--------------------------------------------------------------------------------------
*** Additional Step Type
    ME->S_CSTEP_ADSTP_VIS-TASTP   = ME->R_PR_CSTEP_ADSTP->GET_TASTP( ).

*** Sheet Direction
    ME->S_CSTEP_ADSTP_VIS-SHTDI   = ME->R_PR_CSTEP_ADSTP->GET_SHTDI( ).

*** Text
    ME->S_CSTEP_ADSTP_VIS-TEXT    = ME->R_PR_CSTEP_ADSTP->GET_TEXT( ).

*** Machine Speed
    ME->S_CSTEP_ADSTP_VIS-S_MSRS  = ME->R_PR_CSTEP_ADSTP->GET_MSRS( ).

*** Scrap
    ME->S_CSTEP_ADSTP_VIS-S_SCRAP = ME->R_PR_CSTEP_ADSTP->GET_SCRAP( ).

*** Vendor
    ME->S_CSTEP_ADSTP_VIS-LIFNR   = ME->R_PR_CSTEP_ADSTP->GET_LIFNR( importing EF_DESCR = ME->S_CSTEP_ADSTP_VIS-LIFNR_DESCR ).

*** Handling Dimension
    ME->S_CSTEP_ADSTP_VIS-S_HNDDIM = ME->R_PR_CSTEP_ADSTP->GET_HNDDIM( importing EF_FOVRR = ME->S_CSTEP_ADSTP_VIS-FOVRR_HNDDIM ).

  endmethod.
