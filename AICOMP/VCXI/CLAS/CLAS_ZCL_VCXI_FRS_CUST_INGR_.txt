
class ZCL_VCXI_FRS_CUST_INGR definition
  public
  inheriting from ZCL_VCXI_XCS_CUST_MATNR
  create protected .

public section.

  data S_INDEN type ZVCXI_FRS_S_INDEN .

  class-methods GET_INSTANCE_INGR
    importing
      !IF_MATNR type MATNR
      !IF_WERKS type WERKS_D optional
      !IF_DATE type DATS default SY-DATUM
    returning
      value(RR_INSTANCE) type ref to ZCL_VCXI_FRS_CUST_INGR
    raising
      /VCXI/CX_CKX .
protected section.

  constants C_SETID_INGR type /VCXI/CKXC_SETID value 'ZVCXI_FRS_INGR' ##NO_TEXT.

  methods CONSTRUCTOR
    importing
      !IF_DATE type DATS
      !IF_MATNR type MATNR
      !IF_WERKS type WERKS_D
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_FRS_CUST_INGR definition
class ZCL_VCXI_FRS_CUST_INGR implementation.
  method CONSTRUCTOR.

    data: LF_OBJNUM type        OBJNUM.
    data: LS_SETVA type        /VCXI/CKXC_S_SETVA,
          LS_MAPVN type        /VCXI/CKXC_S_MAPVN.
    data: LT_SETVA  type        /VCXI/CKXC_TT_SETVA.

    try.
***     Call Super Constructor
        SUPER->CONSTRUCTOR( IF_MATNR = IF_MATNR
                            IF_WERKS = IF_WERKS
                            IF_DATE  = IF_DATE ).

        move ME->F_MATNR to LF_OBJNUM.
        LT_SETVA = /VCXI/CL_CKXC_CCM=>GET_SETVA( IF_SETID  = C_SETID_INGR
                                                 IF_OBJNUM = LF_OBJNUM ).

        loop at LT_SETVA into LS_SETVA.
          clear: LS_MAPVN.

          case LS_SETVA-MAPID.
***         Get Ingredient Density
            when 'INDEN'.
              read table LS_SETVA-T_MAPVN into LS_MAPVN index 1.
              move LS_MAPVN-ATFLV     to ME->S_INDEN-INDEN.
              move LS_MAPVN-ATFLV_UOM to ME->S_INDEN-INDEN_UOM.

          endcase.
        endloop.

      cleanup.
        ME->DO_FREE( ).
    endtry.

  endmethod.
  method GET_INSTANCE_INGR.

    data:  LS_INSTANCE type YS_INSTANCE.


***--------------------------------------------------------------------------------------
*** Check for buffered instance and ensure correct Class Type
    read table ZCL_VCXI_XCS_CUST_MATNR=>T_INSTANCE
               into LS_INSTANCE
               with key MATNR = IF_MATNR
                        WERKS = IF_WERKS
                        DATE  = IF_DATE.

    if SY-SUBRC eq 0.
      try.
          move LS_INSTANCE-R_INSTANCE ?to RR_INSTANCE.
        catch CX_SY_MOVE_CAST_ERROR.
          delete ZCL_VCXI_XCS_CUST_MATNR=>T_INSTANCE where R_INSTANCE eq LS_INSTANCE-R_INSTANCE.
          clear RR_INSTANCE.
      endtry.
    endif.

***--------------------------------------------------------------------------------------
    if RR_INSTANCE is initial.
      try.
***       Constructor call of redefined Class
          create object RR_INSTANCE
            exporting
              IF_MATNR = IF_MATNR
              IF_WERKS = IF_WERKS
              IF_DATE  = IF_DATE.

        cleanup.
          clear RR_INSTANCE.
      endtry.
    endif.

  endmethod.
