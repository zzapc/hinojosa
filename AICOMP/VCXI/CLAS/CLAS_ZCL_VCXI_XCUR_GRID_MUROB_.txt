
class ZCL_VCXI_XCUR_GRID_MUROB definition
  public
  final
  create public .

public section.

  data T_MUROB type ZVCXI_XCUR_TT_MUROB read-only .
  data F_ERROR_PROTOCOL type FLAG read-only .
  constants C_TOOBJ_SDOC type /VCXI/CKX_TOOBJ value 'SDOC' ##NO_TEXT.
  constants C_TOOBJ_MAT type /VCXI/CKX_TOOBJ value 'MAT' ##NO_TEXT.

  methods CONSTRUCTOR
    importing
      !IF_TOOBJ type /VCXI/CKX_TOOBJ
      !IT_MUROB type ZVCXI_XCUR_TT_MUROB
      !IF_LOCKED type /VCXI/CKX_LOCKED
    raising
      /VCXI/CX_CKX .
  methods DO_FREE
    raising
      /VCXI/CX_CKX .
  methods REFRESH_GRID
    raising
      /VCXI/CX_CKX .
protected section.

  constants C_FUNC_CHECK type UI_FUNC value 'CHECK' ##NO_TEXT.
  constants C_BTYPE_BUTTON type TB_BTYPE value '0' ##NO_TEXT.
  data R_CONTAINER type ref to CL_GUI_CUSTOM_CONTAINER .
  data R_GRID type ref to CL_GUI_ALV_GRID .
  data T_FIELDCAT type LVC_T_FCAT .
  data F_TOOBJ type /VCXI/CKX_TOOBJ .
  data F_LOCKED type /VCXI/CKX_LOCKED .

  methods INIT_CONTAINER
    raising
      /VCXI/CX_CKX .
  methods INIT_GRID
    raising
      /VCXI/CX_CKX .
  methods INIT_DATA
    raising
      /VCXI/CX_CKX .
  methods GET_LAYOUT
    returning
      value(RS_LAYOUT) type LVC_S_LAYO
    raising
      /VCXI/CX_CKX .
  methods BUILD_FIELDCAT
    raising
      /VCXI/CX_CKX .
  methods HANDLE_DATA_CHANGED
    for event DATA_CHANGED of CL_GUI_ALV_GRID
    importing
      !ER_DATA_CHANGED
      !E_ONF4
      !E_ONF4_BEFORE
      !E_ONF4_AFTER
      !E_UCOMM .
  methods HANDLE_DATA_CHANGED_FINISHED
    for event DATA_CHANGED_FINISHED of CL_GUI_ALV_GRID
    importing
      !E_MODIFIED
      !ET_GOOD_CELLS .
private section.
endclass. "ZCL_VCXI_XCUR_GRID_MUROB definition
class ZCL_VCXI_XCUR_GRID_MUROB implementation.
  method BUILD_FIELDCAT.

    field-symbols: <S_FCAT> type LVC_S_FCAT.


*** Get Standard Field Catalog
    call function 'LVC_FIELDCATALOG_MERGE'
      exporting
        I_STRUCTURE_NAME       = 'ZVCXI_XCUR_S_MUROB'
      changing
        CT_FIELDCAT            = ME->T_FIELDCAT
      exceptions
        INCONSISTENT_INTERFACE = 1
        PROGRAM_ERROR          = 2
        others                 = 3.

    if SY-SUBRC ne 0.
      /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
    endif.

*** Modify Field Catalog
    loop at ME->T_FIELDCAT assigning <S_FCAT>.
      case <S_FCAT>-FIELDNAME.
***     Sales Document
        when 'VBELN' or 'POSNR'.
          if ME->F_TOOBJ eq ME->C_TOOBJ_MAT.
            <S_FCAT>-NO_OUT = ABAP_TRUE.
          endif.
          <S_FCAT>-EDIT     = switch #( ME->F_LOCKED when ABAP_TRUE then ABAP_TRUE ).
***     Material
        when 'MATNR' or 'WERKS'.
          if ME->F_TOOBJ eq ME->C_TOOBJ_SDOC.
            <S_FCAT>-NO_OUT = ABAP_TRUE.
          endif.
          <S_FCAT>-EDIT     = switch #( ME->F_LOCKED when ABAP_TRUE then ABAP_TRUE ).

        when others.
      endcase.
    endloop.

  endmethod.
  method CONSTRUCTOR.

    ME->F_TOOBJ  = IF_TOOBJ.
    ME->T_MUROB  = IT_MUROB.
    ME->F_LOCKED = IF_LOCKED.

  endmethod.
  method DO_FREE.

*** Check for Changes
    ME->R_GRID->CHECK_CHANGED_DATA( ).

*** Free Grid
    if ME->R_GRID is bound.
      ME->R_GRID->FREE( exceptions
                          CNTL_ERROR        = 1
                          CNTL_SYSTEM_ERROR = 2
                          others            = 3 ).
      if SY-SUBRC <> 0.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
      endif.
      clear ME->R_GRID.
    endif.

*** Free Container
    if ME->R_CONTAINER is bound.
      ME->R_CONTAINER->FREE( exceptions
                               CNTL_ERROR        = 1
                               CNTL_SYSTEM_ERROR = 2
                               others            = 3 ).
      if SY-SUBRC <> 0.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
      endif.
      clear ME->R_CONTAINER.
    endif.

  endmethod.
  method GET_LAYOUT.

    RS_LAYOUT-CWIDTH_OPT = 'X'.

  endmethod.
  method HANDLE_DATA_CHANGED.

  endmethod.
  method HANDLE_DATA_CHANGED_FINISHED.

    check E_MODIFIED eq ABAP_TRUE.

    try.

***     Refresh Grid
        ME->REFRESH_GRID( ).

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        LR_CX_CKX->SHOW_MESSAGE( ).
    endtry.

  endmethod.
  method INIT_CONTAINER.

*** Initialize Container
    if ME->R_CONTAINER is not bound.
      create object ME->R_CONTAINER
        exporting
          CONTAINER_NAME              = switch #( ME->F_TOOBJ when C_TOOBJ_SDOC  then '0100CC_MUROB_SDOC'
                                                              when C_TOOBJ_MAT   then '0100CC_MUROB_MAT' )
        exceptions
          CNTL_ERROR                  = 1
          CNTL_SYSTEM_ERROR           = 2
          CREATE_ERROR                = 3
          LIFETIME_ERROR              = 4
          LIFETIME_DYNPRO_DYNPRO_LINK = 5
          others                      = 6.
      if SY-SUBRC ne 0.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
      endif.
    endif.

  endmethod.
  method INIT_DATA.

*** Initialize Data
    delete ME->T_MUROB
      where VBELN is initial and
            MATNR is initial.

*** Add Additional Lines
    do 10 times.
      append initial line to ME->T_MUROB.
    enddo.

  endmethod.
method INIT_GRID.

*** Initialize Grid
  if ME->R_GRID is not bound.
    create object ME->R_GRID
      exporting
        I_PARENT          = ME->R_CONTAINER
        I_APPL_EVENTS     = 'X'
      exceptions
        ERROR_CNTL_CREATE = 1
        ERROR_CNTL_INIT   = 2
        ERROR_CNTL_LINK   = 3
        ERROR_DP_CREATE   = 4
        others            = 5.
    if SY-SUBRC ne 0.
      /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
    endif.

*** Set Enter as Event
    ME->R_GRID->REGISTER_EDIT_EVENT( I_EVENT_ID = CL_GUI_ALV_GRID=>MC_EVT_ENTER ).

*** Set Event Handler for Input
    set handler ME->HANDLE_DATA_CHANGED_FINISHED for ME->R_GRID.
    set handler ME->HANDLE_DATA_CHANGED          for ME->R_GRID.

*** Build Field Catalog
    ME->BUILD_FIELDCAT( ).

  endif.

endmethod.
  method REFRESH_GRID.

*** Initialized Data
    ME->INIT_DATA( ).

*** Initial Display
    if ME->R_GRID is not bound.
      ME->INIT_CONTAINER( ).
      ME->INIT_GRID( ).

      ME->R_GRID->SET_TABLE_FOR_FIRST_DISPLAY( exporting
                                                 IT_TOOLBAR_EXCLUDING          = value #( ( CL_GUI_ALV_GRID=>MC_FC_EXCL_ALL ) )
                                                 IS_LAYOUT                     = ME->GET_LAYOUT( )
                                               changing
                                                 IT_FIELDCATALOG               = ME->T_FIELDCAT
                                                 IT_OUTTAB                     = ME->T_MUROB
                                               exceptions
                                                 INVALID_PARAMETER_COMBINATION = 1
                                                 PROGRAM_ERROR                 = 2
                                                 TOO_MANY_LINES                = 3
                                                 others                        = 4 ).
      if SY-SUBRC ne 0.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
      endif.

*** Update of Data
    else.
      ME->R_GRID->CHECK_CHANGED_DATA( ).
      ME->R_GRID->REFRESH_TABLE_DISPLAY( exceptions FINISHED = 1 ).
      if SY-SUBRC ne 0.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
      endif.
    endif.

  endmethod.
