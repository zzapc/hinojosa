
class ZCL_VCXI_XCM_MBQ_STP_S407 definition
  public
  inheriting from ZCL_VCXI_XCM_MBQ_STP_S000
  create public .

public section.

  methods EXECUTE
    redefinition .
protected section.
private section.
endclass. "ZCL_VCXI_XCM_MBQ_STP_S407 definition
class ZCL_VCXI_XCM_MBQ_STP_S407 implementation.
  method EXECUTE.

    data: LS_MATLI    type ZVCXI_XCM_MBQ_S_MATLI.
    data: LR_BOB_PURSL type ref to ZCL_VCXI_XCI_BOB_PURSL,
          LR_ACTDAT    type ref to DATA.
    field-symbols: <S_ACTDAT_PURSL> type ZVCXI_XCM_MBQ_S_ACTDAT_PURSL.


    try.
***     Get Material List Entry of active Material List Entry
        LS_MATLI = ME->R_DCO_INPUT->T_MATLI[ MLCNT = ME->R_DCO_INPUT->F_MLCNT ].

***     Check Activity - Create Purchasing Source List
        if not LINE_EXISTS( LS_MATLI-T_ACTCON[ ACTID = ZCL_VCXI_XCM_MBQ_CUST=>C_ACTID_PURSL ] ).
***       Set Status to CANCELED
          ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_CANCELED ).
          return.
        endif.

***     Load Activity Control Data
        LR_ACTDAT = LS_MATLI-T_ACTCON[ ACTID = ZCL_VCXI_XCM_MBQ_CUST=>C_ACTID_PURSL ]-R_ACTDAT.
        assign LR_ACTDAT->* to <S_ACTDAT_PURSL>.


***--------------------------------------------------------------------------------------
***     Create Business Object for Purchase Source List
        LR_BOB_PURSL = new #( IF_MATNR = LS_MATLI-MATNR
                              IF_WERKS = <S_ACTDAT_PURSL>-WERKS ).

***     Generate and Maintain Source List
        LR_BOB_PURSL->MAINTAIN( IT_EORDU = LR_BOB_PURSL->GENERATE( ) ).

***     Set Status
        ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_COMPLETED ).

***--------------------------------------------------------------------------------------
      catch /VCXI/CX_CKX into data(LR_CX_CKX).
***     Set Status to Error
        ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_ERROR ).

***     Add Message to Message Log
        ME->R_RPSEG->R_MSG->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).

    endtry.

*** Perform Save and Commit
    ME->R_RPSEG->SAVE_AND_COMMIT( ).

  endmethod.
endclass. "ZCL_VCXI_XCM_MBQ_STP_S407 definition
endclass. "ZCL_VCXI_XCM_MBQ_STP_S407 implementation
