
class ZCL_VCXI_P1SS_UI_CRCONI definition
  public
  inheriting from ZCL_VCXI_PXSS_UI_XCONI
  create public .

public section.

  data S_CRCONI_VIS type ZVCXI_P1SS_S_CRCONI_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABPT type ZVCXI_XCS_TABPT default '0100S_IN_TAB-TAB'
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  constants C_UC_CPROP_IN type SYUCOMM value 'CPROP_IN' ##NO_TEXT.
  constants C_UC_SCSTR_IN type SYUCOMM value 'SCSTR_IN' ##NO_TEXT.
  data R_PR_CRCONI type ref to ZCL_VCXI_P1SS_PR_CRCONI .

  methods HANDLE_UC_CPROP
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_SCSTR
    raising
      /VCXI/CX_CKX .

  methods INIT_VIS
    redefinition .
  methods HANDLE_GATE_FREED
    redefinition .
private section.
endclass. "ZCL_VCXI_P1SS_UI_CRCONI definition
class ZCL_VCXI_P1SS_UI_CRCONI implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_P1SS_UI_CRCONI_BIND'
      exporting
        IR_UI_CRCONI = ME
      importing
        ES_DYNP      = ME->S_DYNP.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_CRCONI.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DO_USER_COMMAND.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    case IF_UCOMM.
***   Corrugated Property Popup
      when C_UC_CPROP_IN.
        ME->HANDLE_UC_CPROP( ).

***   Scores String Popup
      when C_UC_SCSTR_IN.
        ME->HANDLE_UC_SCSTR( ).

    endcase.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_CRCONI.

  endmethod.
  method HANDLE_UC_CPROP.

    data: LS_SURWT type ZVCXI_PXS_S_SURWT.
    data: LR_BL_CRBPROP type ref to ZCL_VCXI_P1SS_BL_CRBPROP,
          LR_BL_CRFPROP type ref to ZCL_VCXI_P1SS_BL_CRFPROP.

*** Get Corrugated Property (Inbound)
    LR_BL_CRBPROP = ME->R_PR_CRCONI->ZIF_VCXI_P1SS_COMM_NEXT~GET_CRBPX( ).
    LR_BL_CRFPROP = ME->R_PR_CRCONI->ZIF_VCXI_P1SS_COMM_NEXT~GET_CRFPX( ).
    LS_SURWT = ME->R_PR_CRCONI->ZIF_VCXI_P1SS_COMM_NEXT~GET_SURWT( ).

*** Start Popup
    ZCL_VCXI_P1SS_POPUP_CRPROP=>START( IR_BL_CRBPROP = LR_BL_CRBPROP
                                       IR_BL_CRFPROP = LR_BL_CRFPROP
                                       IF_WERKS      = ME->R_PR_CRCONI->GET_WERKS( )
                                       IS_SURWT      = LS_SURWT ).

  endmethod.
  method HANDLE_UC_SCSTR.

    data: LR_BL_CRBPROP type ref to ZCL_VCXI_P1SS_BL_CRBPROP.


*** Get Score String (Inbound)
    LR_BL_CRBPROP = ME->R_PR_CRCONI->ZIF_VCXI_P1SS_COMM_PREV~GET_CRBPX( ).
    check LR_BL_CRBPROP is bound.

*** Start Popup
    ZCL_VCXI_PXSS_POPUP_SCSTR=>START( IT_SCORE_COM = LR_BL_CRBPROP->T_SCORE
                                      IT_TAPE_COM  = LR_BL_CRBPROP->T_TAPE_COM ).

  endmethod.
  method INIT_VIS.

    data: LR_BL_CRFPROP type ref to ZCL_VCXI_P1SS_BL_CRFPROP.


    SUPER->INIT_VIS( ).

    free: ME->S_CRCONI_VIS.
    ME->S_CRCONI_VIS-S_XCONI_VIS = ME->S_XCONI_VIS.

***--------------------------------------------------------------------------------------
*** Get Sheet Dimension (Inbound)
    ME->R_PR_CRCONI->ZIF_VCXI_P1SS_COMM_PREV~GET_SHTDIM( importing ES_SHTLE = ME->S_CRCONI_VIS-S_IOSEC_IN-S_SHTLE
                                                                   ES_SHTWI = ME->S_CRCONI_VIS-S_IOSEC_IN-S_SHTWI ).
*** Number Ups (Inbound)
    ME->S_CRCONI_VIS-S_IOSEC_IN-NOUPS = ME->R_PR_CRCONI->ZIF_VCXI_P1SS_COMM_PREV~GET_NOUPS( ).

*** Get Flute & Caliper (Inbound)
    LR_BL_CRFPROP = ME->R_PR_CRCONI->ZIF_VCXI_P1SS_COMM_PREV~GET_CRFPX( ).
    if LR_BL_CRFPROP is bound.
      move LR_BL_CRFPROP->F_FLUTE to ME->S_CRCONI_VIS-S_IOSEC_IN-FLUTE.
      move LR_BL_CRFPROP->S_CALIP to ME->S_CRCONI_VIS-S_IOSEC_IN-S_CALIP.
    endif.

  endmethod.
