
class ZCL_VCXI_XCSR_TI_RTESPV definition
  public
  inheriting from ZCL_VCXI_XCS_TI
  create public .

public section.

  methods /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ
    redefinition .
  methods GET_INFO
    redefinition .
  methods IS_HIDDEN
    redefinition .
  methods /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL
    redefinition .
protected section.

  constants C_ATTID_HIDDEN type /VCXI/VKS_ATTID value 'TI_HIDDEN' ##NO_TEXT.

  methods HANDLE_UC_SHOW_RTE_INFO
    raising
      /VCXI/CX_CKX .
private section.

  constants C_UC_SHOW_RTE_INFO type UI_FUNC value 'SHOW_RTE_INFO' ##NO_TEXT.
endclass. "ZCL_VCXI_XCSR_TI_RTESPV definition
class ZCL_VCXI_XCSR_TI_RTESPV implementation.
  method /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ.

*** Super
    SUPER->/VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ( IR_MENU = IR_MENU ).
    IR_MENU->ADD_SEPARATOR( ).

***------------------------------------------------------------------------------------------------
*** Show Status Messages and Technical Info
    IR_MENU->ADD_FUNCTION( FCODE = C_UC_SHOW_RTE_INFO
                           TEXT  = 'Show Routing Information'(F01) ).

  endmethod.
  method /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL.

    case IF_FCODE.
      when C_UC_SHOW_RTE_INFO.
        ME->HANDLE_UC_SHOW_RTE_INFO( ).

      when others.
        SUPER->/VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL( IF_FCODE = IF_FCODE ).
    endcase.

  endmethod.
  method GET_INFO.

    data: LF_RETURN type I.
    data: LR_PR_RTESPV type ref to ZCL_VCXI_XCSR_PR_RTESPV.

    SUPER->GET_INFO( importing ES_NODE   = ES_NODE
                               ET_ITEM   = ET_ITEM
                               EF_EXPAND = EF_EXPAND ).

    LR_PR_RTESPV ?= ME->R_GATE->R_CLSPR.

***----------------------------------------------------------------------------
*** Check if Routing Supervisor is invalid itself or due to other routing supervisor
    if LR_PR_RTESPV->GET_EVQTY( )-EVQTY is not initial.
      try.
          LR_PR_RTESPV->CHECK_PROD_FILL( importing EF_RETURN = LF_RETURN ).
        catch /VCXI/CX_CKX.  "ignore at this point
      endtry.
      case LF_RETURN.
        when 0.     "Production is filled correctly

        when 1 or   "Not yet filled
             3.     "Refill needed

        when 2 or   "Fill is required, but not allowed
             4.     "Refill is required, but not allowed
          ES_NODE-STYLE = CL_COLUMN_TREE_MODEL=>STYLE_INTENSIFD_CRITICAL.

        when 5 or   "Related Supervisor requires fill
             6.     "Related Supervisor requires fill of Production, but not allowed.

      endcase.
    endif.

  endmethod.
  method HANDLE_UC_SHOW_RTE_INFO.

    ZCL_VCXI_XCSR_POPUP_RTEINFO=>START( IR_GATE   = ME->R_GATE
                                        IR_UIGATE = ME->R_UIGATE ).

  endmethod.
method IS_HIDDEN.

  data:  LS_ATTRI  type        /VCXI/VKS_S_ATTRI.

  RF_HIDDEN = SUPER->IS_HIDDEN( ).

***----------------------------------------------------------------------------
*** Get TreeItem Hidden Mode (by Attribute)
  try.
      LS_ATTRI = ME->R_GATE->GET_ATTRI( IF_ATTID = C_ATTID_HIDDEN ).
    catch /VCXI/CX_CKX.
      clear LS_ATTRI.
  endtry.

***----------------------------------------------------------------------------
*** Return Hidden Mode
  if LS_ATTRI-ATTVA is not initial.
    move LS_ATTRI-ATTVA to RF_HIDDEN.
  endif.

endmethod.
