
class ZCL_VCXI_XCDCU_CR_PART_SCRIM definition
  public
  inheriting from ZCL_VCXI_XCDCU_CR_PART
  create public .

public section.

  data S_GSCRIM_VIS type ZVCXI_XCDCU_S_CR_GSCRIM_VIS .

  methods CONSTRUCTOR
    importing
      !IR_CONDREC type ref to ZCL_VCXI_XCDC_CONDREC
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_GSCRIM_VIS
    importing
      !IS_GSCRIM_VIS type ZVCXI_XCDCU_S_CR_GSCRIM_VIS
    raising
      /VCXI/CX_CKX .

  methods DO_FREE
    redefinition .
  methods DO_PBO
    redefinition .
  methods GET_STRIPDESCR
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  types:
    begin of YS_OELIU,
                  OELIU       type ZVCXI_XCD_OELIU,
                  OELIU_DESCR type /VCXI/CKX_DESCR,
         end of YS_OELIU .
  types:
    YT_OELIU type standard table of YS_OELIU with default key .

  data R_GRID_SCRIM type ref to ZCL_VCXI_XCDCU_GRID_CR_SCRIM .
  data T_OELIU type YT_OELIU .

  methods MODIFY_SCREEN
    raising
      /VCXI/CX_CKX .
  methods INIT_VIS
    raising
      /VCXI/CX_CKX .
  methods INIT_OELIU_DESCR
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_XCDCU_CR_PART_SCRIM definition
class ZCL_VCXI_XCDCU_CR_PART_SCRIM implementation.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_CONDREC = IR_CONDREC ).

***----------------------------------------------------------------------------
  if ME->R_GRID_SCRIM is not bound.
*** Create Grid for DVC Condition Value
    create object ME->R_GRID_SCRIM
      exporting
        IR_CUSTOMIZING = ME->R_CONDREC.
  endif.

  call function 'ZVCXI_XCDCU_DUI_CRP_SCRIM'
    exporting
      IR_PART_SCRIM = ME.

  ME->INIT_OELIU_DESCR( ).

endmethod.
method DO_FREE.

  SUPER->DO_FREE( ).

***------------------------------------------------------------------
*** Free Screen Impact Grid
  if ME->R_GRID_SCRIM is bound.
    ME->R_GRID_SCRIM->DO_FREE( ).
    clear ME->R_GRID_SCRIM.
  endif.

endmethod.
method DO_PBO.

  ME->INIT_VIS( ).

*** Refresh Grid
  if ME->R_GRID_SCRIM is bound.
    move ABAP_TRUE to ME->R_GRID_SCRIM->F_REFRESH_REQ.
    if ME->F_REFRESH_REQUESTED = ABAP_TRUE.
      move ABAP_TRUE to ME->R_GRID_SCRIM->F_REFRESH_ALL_REQ.
    endif.
    ME->R_GRID_SCRIM->REFRESH_GRID( ).
  endif.

  ME->MODIFY_SCREEN( ).

endmethod.
method DO_UPDATE_GSCRIM_VIS.

  if ME->S_GSCRIM_VIS-OELIU ne IS_GSCRIM_VIS-OELIU.
    ME->R_CONDREC->SET_OELIU( IF_OELIU = IS_GSCRIM_VIS-OELIU ).
  endif.

endmethod.
method DO_USER_COMMAND.

  SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

  if ME->R_GRID_SCRIM is bound.
    ME->R_GRID_SCRIM->CHECK_CHANGED_DATA( ).
  endif.

endmethod.
method GET_STRIPDESCR.

  RF_DESCR = 'Screen Impact'(001).


  if ME->R_CONDREC->T_CR_SIMPACT is initial and
     ME->R_CONDREC->F_OELIU      is initial.
    concatenate ICON_WF_WORKITEM_READY 'Screen Impact'(001) into RF_DESCR.
  else.
    concatenate ICON_WF_WORKITEM_COMPLETED 'Screen Impact'(001) into RF_DESCR.
  endif.

endmethod.
method INIT_OELIU_DESCR.

  data:          LT_DD07V  type table of DD07V.
  data:          LS_DD07V  type          DD07V.
  field-symbols: <S_OELIU> type          YS_OELIU.

*** Get Domain Values
  call function 'DDIF_DOMA_GET'
    exporting
      NAME          = 'ZVCXI_XCD_OELIU'
      LANGU         = SY-LANGU
    tables
      DD07V_TAB     = LT_DD07V
    exceptions
      ILLEGAL_INPUT = 1
      others        = 2.
  if SY-SUBRC <> 0.
    /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
  endif.

  loop at LT_DD07V into LS_DD07V.
    append initial line to ME->T_OELIU assigning <S_OELIU>.
    move LS_DD07V-DOMVALUE_L to <S_OELIU>-OELIU.
    move LS_DD07V-DDTEXT     to <S_OELIU>-OELIU_DESCR.
  endloop.

endmethod.
method INIT_VIS.

  data: LS_OELIU type YS_OELIU.

  free: ME->S_GSCRIM_VIS.

*** Get Screen In Use Option
  move ME->R_CONDREC->F_OELIU to ME->S_GSCRIM_VIS-OELIU.
  read table ME->T_OELIU with key OELIU = ME->S_GSCRIM_VIS-OELIU into LS_OELIU.
  move LS_OELIU-OELIU_DESCR to ME->S_GSCRIM_VIS-OELIU_DESCR.

endmethod.
method MODIFY_SCREEN.

***------------------------------------------------------------------------------------------------
*** Modify Screen
  loop at screen.
    if ME->R_CONDREC->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ) ne ABAP_TRUE.
      move 0 to SCREEN-INPUT.
      move 0 to SCREEN-REQUIRED.
    endif.
    modify screen.
  endloop.

endmethod.
