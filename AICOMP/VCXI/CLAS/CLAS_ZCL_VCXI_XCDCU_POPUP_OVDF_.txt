
class ZCL_VCXI_XCDCU_POPUP_OVDF definition
  public
  final
  create protected .

public section.

  data R_UIFW type ref to ZCL_VCXI_XCDCU_UIFW .
  type-pools ABAP .
  data F_REFRESH_REQUESTED type ABAP_BOOL .

  methods ADD_DVCFILTER
    importing
      !IF_IDVCF type /VCXI/CKX_ID
    raising
      /VCXI/CX_CKX .
  methods DELETE_DVCFILTER
    importing
      !IF_GDVCF type ZVCXI_XCD_GDVCF
    exceptions
      /VCXI/CX_CKX .
  methods DO_USER_COMMAND
    changing
      !CF_UCOMM type SYUCOMM .
  class-methods CALL_POPUP
    importing
      !IR_UIFW type ref to ZCL_VCXI_XCDCU_UIFW
    raising
      /VCXI/CX_CKX .
  methods DO_PBO
    raising
      /VCXI/CX_CKX .
protected section.

  constants C_UC_OKAY type UI_FUNC value 'OKAY'. "#EC NOTEXT
  data R_GRID_OVDF type ref to ZCL_VCXI_XCDCU_GRID_OVDF .

  methods HANDLE_UC_OKAY
    raising
      /VCXI/CX_CKX .
  methods SET_PFSTATUS
    raising
      /VCXI/CX_CKX .
  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to ZCL_VCXI_XCDCU_UIFW
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_XCDCU_POPUP_OVDF definition
class ZCL_VCXI_XCDCU_POPUP_OVDF implementation.
method ADD_DVCFILTER.

  ME->R_UIFW->ADD_DVCFILTER( IF_IDVCF = IF_IDVCF ).

*** Refresh Grid
  move ABAP_TRUE to ME->F_REFRESH_REQUESTED.

endmethod.
method CALL_POPUP.

  data: LR_POPUP_OVDF  type ref to ZCL_VCXI_XCDCU_POPUP_OVDF.

  create object LR_POPUP_OVDF
    exporting
      IR_UIFW = IR_UIFW.

*** Display Overview Popup
  call function 'ZVCXI_XCDCU_POPUP_OVDF_START'
    exporting
      IR_POPUP_OVDF = LR_POPUP_OVDF.


endmethod.
method CONSTRUCTOR.

*** Save Guid's of DVC Filter
  move IR_UIFW to ME->R_UIFW.

***----------------------------------------------------------------------------
  if ME->R_GRID_OVDF is not bound.
*** Create Grid for DVC Filter
    create object ME->R_GRID_OVDF
      exporting
        IR_CUSTOMIZING = ME.
  endif.

*** Request Refresh
  move ABAP_TRUE to ME->F_REFRESH_REQUESTED.

endmethod.
method DELETE_DVCFILTER.

  ME->R_UIFW->DELETE_DVCFILTER( IF_GDVCF = IF_GDVCF ).

*** Refresh Grid
  move ABAP_TRUE to ME->F_REFRESH_REQUESTED.

endmethod.
method DO_PBO.

  data:  LR_CX_CKX type ref to /VCXI/CX_CKX.

  try.

      ME->SET_PFSTATUS( ).

*** Refresh Grid
  if ME->R_GRID_OVDF is bound.
    move ABAP_TRUE to ME->R_GRID_OVDF->F_REFRESH_REQ.
    if ME->F_REFRESH_REQUESTED = ABAP_TRUE.
      move ABAP_TRUE to ME->R_GRID_OVDF->F_REFRESH_ALL_REQ.
    endif.
    ME->R_GRID_OVDF->REFRESH_GRID( ).
  endif.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      LR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

endmethod.
method DO_USER_COMMAND.

  data:  LR_CX_CKX type ref to /VCXI/CX_CKX.

  try.
      case CF_UCOMM.
        when ZCL_VCXI_XCDCU_POPUP_OVDF=>C_UC_OKAY.
          ME->HANDLE_UC_OKAY( ).
      endcase.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      LR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

  clear CF_UCOMM.

endmethod.
method HANDLE_UC_OKAY.

  ME->R_GRID_OVDF->DO_FREE( ).
  free: ME->R_GRID_OVDF.

  leave to screen 0.

endmethod.
method SET_PFSTATUS.

*** ----------------------------------------------------------
*** Set Status
  set pf-status '0100' of program 'SAPLZVCXI_XCDCU_POPUP_OVDF'.

endmethod.
