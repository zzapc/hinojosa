
class ZCL_VCXI_P1SS_UI_CSTEP_LAMI definition
  public
  inheriting from ZCL_VCXI_P1SS_UI_CSTEP
  create public .

public section.

  data S_CSTEP_LAMI_VIS type ZVCXI_P1SS_S_CSTEP_LAMI_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
  methods DEACTIVATE
    redefinition .
protected section.

  data R_PR_CSTEP_LAMI type ref to ZCL_VCXI_P1SS_PR_CSTEP_LAMI .
  data R_TOOLBAR type ref to ZCL_VCXI_XCS_TOOLBAR_PRVDR .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_P1SS_UI_CSTEP_LAMI definition
class ZCL_VCXI_P1SS_UI_CSTEP_LAMI implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

***--------------------------------------------------------------------------------------
*** Bind UI to Dynpro
    call function 'ZVCXI_P1SS_UI_CSTEP_LAMI_BIND'
      exporting
        IR_UI_CSTEP_LAMI = ME
      importing
        ES_DYNP          = ME->S_DYNP
        EF_ACTIVE_TAB    = ME->F_ACTIVE_TAB.

*** Create Toolbar for Provider
    create object ME->R_TOOLBAR
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_TOOLBAR_LAMI'
        IF_IPRVT          = ZCL_VCXI_PXSS_PR_XSHT=>C_IPRVT_PX_XSHT.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_CSTEP_LAMI.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

*** Free Toolbar
    if ME->R_TOOLBAR is bound.
      ME->R_TOOLBAR->DO_FREE( ).
      clear ME->R_TOOLBAR.
    endif.

  endmethod.
  method DO_PBO.

    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize Toolbar and provide active tab as Selected Provider (for inserting/deletion)
    if ME->R_TOOLBAR is bound.
      ME->R_TOOLBAR->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_TOOLBAR->REFRESH_TOOLBAR( IT_PRVDR_SELECTED = value #( ( ME->R_OLD_GATE ) ) ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_CSTEP_LAMI_VIS type ZVCXI_P1SS_S_CSTEP_LAMI_VIS.


*** Call SUPER
    SUPER->DO_UPDATE_VIS( IS_CSTEP_VIS = IS_CSTEP_VIS ).

    check ME->R_PR_CSTEP_LAMI is bound.

*** "Cast" Vis Structure
    move-corresponding IS_CSTEP_VIS to LS_CSTEP_LAMI_VIS.

***--------------------------------------------------------------------------------------
*** Set Reduction on Width (Total)
    if ME->S_CSTEP_LAMI_VIS-S_RDWIT ne LS_CSTEP_LAMI_VIS-S_RDWIT.
      ME->R_PR_CSTEP_LAMI->ZIF_VCXI_PXSS_REDUC~SET_RDWIT( IS_RDWIT = LS_CSTEP_LAMI_VIS-S_RDWIT
                                                          IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Reduction on Length (Total)
    if ME->S_CSTEP_LAMI_VIS-S_RDLET ne LS_CSTEP_LAMI_VIS-S_RDLET.
      ME->R_PR_CSTEP_LAMI->ZIF_VCXI_PXSS_REDUC~SET_RDLET( IS_RDLET = LS_CSTEP_LAMI_VIS-S_RDLET
                                                          IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.


*** Set Reduction Left
    if ME->S_CSTEP_LAMI_VIS-S_RDLEF ne LS_CSTEP_LAMI_VIS-S_RDLEF.
      ME->R_PR_CSTEP_LAMI->ZIF_VCXI_PXSS_REDUC~SET_RDLEF( IS_RDLEF = LS_CSTEP_LAMI_VIS-S_RDLEF
                                                          IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Reduction Right
    if ME->S_CSTEP_LAMI_VIS-S_RDRIG ne LS_CSTEP_LAMI_VIS-S_RDRIG.
      ME->R_PR_CSTEP_LAMI->ZIF_VCXI_PXSS_REDUC~SET_RDRIG( IS_RDRIG = LS_CSTEP_LAMI_VIS-S_RDRIG
                                                          IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Reduction Top
    if ME->S_CSTEP_LAMI_VIS-S_RDTOP ne LS_CSTEP_LAMI_VIS-S_RDTOP.
      ME->R_PR_CSTEP_LAMI->ZIF_VCXI_PXSS_REDUC~SET_RDTOP( IS_RDTOP = LS_CSTEP_LAMI_VIS-S_RDTOP
                                                          IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Reduction Bottom
    if ME->S_CSTEP_LAMI_VIS-S_RDBOT ne LS_CSTEP_LAMI_VIS-S_RDBOT.
      ME->R_PR_CSTEP_LAMI->ZIF_VCXI_PXSS_REDUC~SET_RDBOT( IS_RDBOT = LS_CSTEP_LAMI_VIS-S_RDBOT
                                                          IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Sheet Direction
    if ME->S_CSTEP_LAMI_VIS-SHTDI ne LS_CSTEP_LAMI_VIS-SHTDI.
      ME->R_PR_CSTEP_LAMI->SET_SHTDI( IF_SHTDI = LS_CSTEP_LAMI_VIS-SHTDI
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_CSTEP_LAMI.

  endmethod.
  method INIT_VIS.

    SUPER->INIT_VIS( ).

    free: ME->S_CSTEP_LAMI_VIS.
    ME->S_CSTEP_LAMI_VIS-S_CSTEP_VIS = ME->S_CSTEP_VIS.

***--------------------------------------------------------------------------------------
*** Reduction
    ME->S_CSTEP_LAMI_VIS-S_RDWIT = ME->R_PR_CSTEP_LAMI->ZIF_VCXI_PXSS_REDUC~GET_RDWIT( ).
    ME->S_CSTEP_LAMI_VIS-S_RDLET = ME->R_PR_CSTEP_LAMI->ZIF_VCXI_PXSS_REDUC~GET_RDLET( ).
    ME->S_CSTEP_LAMI_VIS-S_RDLEF = ME->R_PR_CSTEP_LAMI->ZIF_VCXI_PXSS_REDUC~GET_RDLEF( ).
    ME->S_CSTEP_LAMI_VIS-S_RDRIG = ME->R_PR_CSTEP_LAMI->ZIF_VCXI_PXSS_REDUC~GET_RDRIG( ).
    ME->S_CSTEP_LAMI_VIS-S_RDTOP = ME->R_PR_CSTEP_LAMI->ZIF_VCXI_PXSS_REDUC~GET_RDTOP( ).
    ME->S_CSTEP_LAMI_VIS-S_RDBOT = ME->R_PR_CSTEP_LAMI->ZIF_VCXI_PXSS_REDUC~GET_RDBOT( ).

*** Sheet Direction
    ME->S_CSTEP_LAMI_VIS-SHTDI = ME->R_PR_CSTEP_LAMI->GET_SHTDI( ).

  endmethod.
