
class ZCL_VCXI_F0SS_GRID_ALTUOM definition
  public
  inheriting from ZCL_VCXI_XCS_GRID
  create public .

public section.

  data F_INIT_SEL_ROW type SYST_TABIX .

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABNAME type TABNAME default 'ZVCXI_F0SS_S_ALTUOM_GRID'
      !IF_CONTAINER_NAME type SCRFNAME
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_PR_ALTUOM type ref to ZCL_VCXI_F0SS_PR_ALTUOM
      !IF_APPL_EVENTS type ABAP_BOOL default ABAP_TRUE
    raising
      /VCXI/CX_CKX .

  methods REFRESH_GRID
    redefinition .
protected section.

  data R_PR_ALTUOM type ref to ZCL_VCXI_F0SS_PR_ALTUOM .

  methods CHANGE_COLOR
    importing
      !IF_FNAME type LVC_FNAME
    changing
      !CT_SCOL type LVC_T_SCOL
    exceptions
      /VCXI/CX_CKX .

  methods GET_FIELDCAT
    redefinition .
  methods GET_GRID_DATA
    redefinition .
  methods GET_LAYOUT
    redefinition .
private section.
endclass. "ZCL_VCXI_F0SS_GRID_ALTUOM definition
class ZCL_VCXI_F0SS_GRID_ALTUOM implementation.
  method CHANGE_COLOR.

    data: LS_SCOL type LVC_S_SCOL.

    LS_SCOL-FNAME     = IF_FNAME.
    LS_SCOL-COLOR-COL = 3.
    LS_SCOL-COLOR-INT = 1.
    LS_SCOL-COLOR-INV = 1.
    append LS_SCOL to CT_SCOL.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_GATE           = IR_GATE
                        IR_UIGATE         = IR_UIGATE
                        IF_TABNAME        = IF_TABNAME
                        IF_CONTAINER_NAME = IF_CONTAINER_NAME
                        IF_APPL_EVENTS    = IF_APPL_EVENTS ).

    move IR_PR_ALTUOM to ME->R_PR_ALTUOM.

  endmethod.
  method GET_FIELDCAT.

    field-symbols: <S_FCAT>     type        LVC_S_FCAT.

***--------------------------------------------------------------------------------------
*** Get Standard Field Catalog
    RT_FCAT = SUPER->GET_FIELDCAT( ).

***--------------------------------------------------------------------------------------
*** Adjust Field Catalog
    loop at RT_FCAT assigning <S_FCAT>.
      case <S_FCAT>-FIELDNAME.
***     Line Counter
        when 'CLINE'.
          move ABAP_TRUE to <S_FCAT>-NO_OUT.

***     Cell Color
        when 'T_SCOL'.
          move ABAP_TRUE to <S_FCAT>-TECH.
      endcase.
    endloop.

  endmethod.
  method GET_GRID_DATA.

    data: LS_ALTUOM_DEF  type ZVCXI_F0SS_S_ALTUOM_DEF,
          LS_ALTUOM_GRID type ZVCXI_F0SS_S_ALTUOM_GRID,
          LS_QITEM       type ZVCXI_XCSS_S_QITEM,
          LS_QITEM_BASE  type ZVCXI_XCSS_S_QITEM.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type ANY.


    check ME->R_PR_ALTUOM is bound and
          ME->R_PR_ALTUOM->R_BL_ALTUOM is bound.

***--------------------------------------------------------------------------------------
*** Get Base UoM
    ME->R_PR_ALTUOM->ZIF_VCXI_XCSS_OPQTY~GET_QITEM( importing ES_QITEM_BASE = LS_QITEM_BASE ).

***--------------------------------------------------------------------------------------
*** Dereferencing of Data Reference
    assign IR_DATA->* to <T_DATA>.

*** Get Grid Lines
    loop at ME->R_PR_ALTUOM->R_BL_ALTUOM->T_ALTUOM_DEF into LS_ALTUOM_DEF.

      clear LS_ALTUOM_GRID.

***   Set Line Counter
      move LS_ALTUOM_DEF-CLINE to LS_ALTUOM_GRID-CLINE.

***   Get Quantity Data
      clear LS_QITEM.
      try.
          LS_QITEM = ME->R_PR_ALTUOM->GET_QITEM_AS_ALTUOM( IF_UOM = LS_ALTUOM_DEF-UOM ).
        catch /VCXI/CX_CKX.
          clear LS_QITEM.
      endtry.

      check LS_QITEM-QITEM_UOM is not initial.

***   Fill Entry
      move LS_QITEM-QITEM     to LS_ALTUOM_GRID-OPQTY.
      move LS_QITEM-QITEM_UOM to LS_ALTUOM_GRID-OPQTY_UOM.

***   Add Line
      append initial line to <T_DATA> assigning <S_DATA>.

***   Highlight Base UoM
      if LS_QITEM_BASE-QITEM_UOM eq LS_QITEM-QITEM_UOM.
        move SY-TABIX to ME->F_INIT_SEL_ROW.

        ME->CHANGE_COLOR( exporting IF_FNAME = 'OPQTY'
                           changing CT_SCOL  = LS_ALTUOM_GRID-T_SCOL ).
        ME->CHANGE_COLOR( exporting IF_FNAME = 'OPQTY_UOM'
                           changing CT_SCOL  = LS_ALTUOM_GRID-T_SCOL ).
      endif.

      move-corresponding LS_ALTUOM_GRID to <S_DATA>.
    endloop.

  endmethod.
  method GET_LAYOUT.

    RS_LAYOUT = SUPER->GET_LAYOUT( ).

    move 'T_SCOL'  to RS_LAYOUT-CTAB_FNAME.

  endmethod.
  method REFRESH_GRID.

    data: LF_INITIAL_REFRESH   type ABAP_BOOL.
    data: LS_INDEX_ROWS        type LVC_S_ROW.
    data: LT_INDEX_ROWS        type LVC_T_ROW.

    check ME->F_FREED       ne ABAP_TRUE and
          ME->F_REFRESH_REQ eq ABAP_TRUE.

    if ME->R_GRID is not bound.
      move ABAP_TRUE to LF_INITIAL_REFRESH.
    endif.

*** Call Super Refresh
    SUPER->REFRESH_GRID( ).

    if LF_INITIAL_REFRESH eq ABAP_TRUE.
***   Set Initial Selected Row
      move ME->F_INIT_SEL_ROW to LS_INDEX_ROWS-INDEX.
      append LS_INDEX_ROWS to LT_INDEX_ROWS.
      ME->R_GRID->SET_SELECTED_ROWS( exporting IT_INDEX_ROWS = LT_INDEX_ROWS ).
    endif.

  endmethod.
