
class ZCL_VCXI_XCSR_UI_RTESPV definition
  public
  inheriting from /VCXI/CL_VKSRU_CLSUI
  create public .

public section.

  data F_ACTIVE_TAB type SCRFNAME read-only .
  data S_RTESPV_VIS type ZVCXI_XCSR_S_RTESPV_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_RTESPV_VIS
    importing
      !IS_RTESPV_VIS type ZVCXI_XCSR_S_RTESPV_VIS
    raising
      /VCXI/CX_CKX .
  methods POPUP_RTEEDI
    importing
      !IR_BL_PRTE type ref to ZCL_VCXI_XCSR_BL_PRTE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  constants C_ATTID_LLAYO type /VCXI/VKS_ATTID value 'DEFAULT_LLAYO' ##NO_TEXT.
  constants C_LLAYO_AW type ZVCXI_XCSR_LLAYO value 'AW' ##NO_TEXT.
  constants C_LLAYO_AWP type ZVCXI_XCSR_LLAYO value 'AWP' ##NO_TEXT.
  constants C_LLAYO_MAW type ZVCXI_XCSR_LLAYO value 'MAW' ##NO_TEXT.
  constants C_LLAYO_MAWP type ZVCXI_XCSR_LLAYO value 'MAWP' ##NO_TEXT.
  data C_TABNAME type STRING value '0100TAB_' ##NO_TEXT.
  constants C_UC_ADD type SYUCOMM value 'ADD' ##NO_TEXT.
  constants C_UC_EDIT type SYUCOMM value 'EDIT' ##NO_TEXT.
  constants C_UC_FILL type SYUCOMM value 'FILL' ##NO_TEXT.
  constants C_UC_INFO type SYUCOMM value 'INFO' ##NO_TEXT.
  data C_UC_NETGRAPH type SYUCOMM value 'NETGRAPH' ##NO_TEXT.
  data F_LLAYO type ZVCXI_XCSR_LLAYO .
  data F_REFRESH_REQ type ABAP_BOOL .
  data R_GRID_RTEALT type ref to ZCL_VCXI_XCSR_GRID_RTEALT .
  data R_GRID_RTEINV type ref to ZCL_VCXI_XCSR_GRID_RTEINV .
  data R_GRID_RTESEL type ref to ZCL_VCXI_XCSR_GRID_RTESEL .
  data R_PR_RTESPV type ref to ZCL_VCXI_XCSR_PR_RTESPV .
  data R_TREE_RTEALT type ref to ZCL_VCXI_XCSR_TREE_RTEALT .
  data R_TREE_RTEINV type ref to ZCL_VCXI_XCSR_TREE_RTEINV .

  methods HANDLE_UC_ADD
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_EDIT
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_FILL
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_INFO
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_NETGRAPH
    raising
      /VCXI/CX_CKX .
  methods INIT_RTEALT
    raising
      /VCXI/CX_CKX .
  methods INIT_RTEINV
    raising
      /VCXI/CX_CKX .
  methods INIT_RTESEL
    raising
      /VCXI/CX_CKX .
  methods INIT_VIS
    raising
      /VCXI/CX_CKX .
  methods SET_LLAYO
    importing
      !IF_LLAYO type ZVCXI_XCSR_LLAYO
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
private section.

  methods INIT_DEFAULT_LLAYO
    raising
      /VCXI/CX_CKX .
endclass. "ZCL_VCXI_XCSR_UI_RTESPV definition
class ZCL_VCXI_XCSR_UI_RTESPV implementation.
method ACTIVATE.

  SUPER->ACTIVATE( ).

***------------------------------------------------------------------
*** Bind UI to Dynpro
  call function 'ZVCXI_XCSR_UI_RTESPV_BIND'
    exporting
      IR_UI_RTESPV  = ME
    importing
      ES_DYNP       = ME->S_DYNP
      EF_ACTIVE_TAB = ME->F_ACTIVE_TAB.

***------------------------------------------------------------------
*** Initialize Selected Route
  ME->INIT_RTESEL( ).

*** Initialize Alternative Routes
  ME->INIT_RTEALT( ).

*** Initialize Invalid Routes
  ME->INIT_RTEINV( ).

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                      IR_UIGATE = IR_UIGATE
                      IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
  try.
      move IR_GATE->R_CLSPR ?to ME->R_PR_RTESPV.
    catch CX_SY_MOVE_CAST_ERROR.
***   UI ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '801'.
  endtry.

*** Initialize Default List Layout
  ME->INIT_DEFAULT_LLAYO( ).

endmethod.
method DEACTIVATE.

  SUPER->DEACTIVATE( ).

***------------------------------------------------------------------
  if ME->R_GRID_RTESEL is bound.
    ME->R_GRID_RTESEL->DO_FREE( ).
    clear ME->R_GRID_RTESEL.
  endif.

*** Free Alternative Routes
  if ME->R_GRID_RTEALT is bound.
    ME->R_GRID_RTEALT->DO_FREE( ).
    clear ME->R_GRID_RTEALT.
  endif.

  if ME->R_TREE_RTEALT is bound.
    ME->R_TREE_RTEALT->DO_FREE( ).
    clear ME->R_TREE_RTEALT.
  endif.

*** Free Invalid Routes
  if ME->R_GRID_RTEINV is bound.
    ME->R_GRID_RTEINV->DO_FREE( ).
    clear ME->R_GRID_RTEINV.
  endif.

  if ME->R_TREE_RTEINV is bound.
    ME->R_TREE_RTEINV->DO_FREE( ).
    clear ME->R_TREE_RTEINV.
  endif.

endmethod.
method DO_MODIFY_SCREEN.

  data:  LF_LOCKED             type        /VCXI/CKX_LOCKED,
         LF_INUSE              type        /VCXI/VKSR_INUSE,
         LF_FILL_PROD_ALLOWED  type        ABAP_BOOL.
  data:  LR_CX_CKX             type ref to /VCXI/CX_CKX.

  SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                           IF_DYNNR = IF_DYNNR ).

  try.
      LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
      LF_INUSE  = ME->R_GATE->IS_INUSE( ).
      ME->R_PR_RTESPV->IS_FILL_PROD_ALLOWED( importing EF_ALLOWED = LF_FILL_PROD_ALLOWED ).

***   Modify Scree using DynamicVC
      ZCL_VCXI_XCDR_SERVICE=>MODIFY_SCREEN( IR_GATE = ME->R_GATE ).

      loop at screen.
***     Disable Update if Selected Route is not a Alternative Route
        if SCREEN-NAME eq '0101BTN_FILL' and
           LF_FILL_PROD_ALLOWED ne ABAP_TRUE.
          move 0 to SCREEN-INPUT.
        endif.

***     Is BL Production Route changeable
        if ( SCREEN-NAME eq '0101BTN_EDIT' or
             SCREEN-NAME eq '0102BTN_ADD' ) and
           ME->R_PR_RTESPV->IS_BL_PRTE_CHANGEABLE( ) ne ABAP_TRUE.
          move 0 to SCREEN-INPUT.
        endif.

***     Lock everything if not in change-mode
        if LF_LOCKED ne ABAP_TRUE or
           LF_INUSE  ne ABAP_TRUE.
          move 0 to SCREEN-INPUT.
        endif.

***     Tabs always changable
        if SCREEN-GROUP1 eq 'TAB'.
          move 1 to SCREEN-INPUT.
        endif.

***     Info Elements always Ready
        if SCREEN-GROUP1 eq 'INF'.
          move 1 to SCREEN-INPUT.
        endif.

        modify screen.
      endloop.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

endmethod.
method DO_PBO.

  data: LR_CX_CKX  type ref to /VCXI/CX_CKX.

***------------------------------------------------------------------
*** Refresh Auto. Routes if needed
  try.
      ME->R_PR_RTESPV->REFRESH_AUTO( ).

    catch /VCXI/CX_CKX into LR_CX_CKX.
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

  ME->R_GATE->RAISE_EVENTS( ).

***------------------------------------------------------------------
*** Initialize Vis Structure
  ME->INIT_VIS( ).

***------------------------------------------------------------------
*** Selected Route
  if ME->R_GRID_RTESEL is bound.
    move ABAP_TRUE to ME->R_GRID_RTESEL->F_REFRESH_REQ.
    ME->R_GRID_RTESEL->REFRESH_GRID( ).
  endif.

***------------------------------------------------------------------
*** Alternative Routes
  if ME->R_GRID_RTEALT is bound.
    move ABAP_TRUE to ME->R_GRID_RTEALT->F_REFRESH_REQ.
    ME->R_GRID_RTEALT->REFRESH_GRID( ).
  endif.

  if ME->R_TREE_RTEALT is bound.
    move ABAP_TRUE to ME->R_TREE_RTEALT->F_REFRESH_REQ.
    ME->R_TREE_RTEALT->REFRESH_TREE( ).
  endif.

***------------------------------------------------------------------
*** Invalid Routes
  if ME->R_GRID_RTEINV is bound.
    move ABAP_TRUE to ME->R_GRID_RTEINV->F_REFRESH_REQ.
    ME->R_GRID_RTEINV->REFRESH_GRID( ).
  endif.

  if ME->R_TREE_RTEINV is bound.
    move ABAP_TRUE to ME->R_TREE_RTEINV->F_REFRESH_REQ.
    ME->R_TREE_RTEINV->REFRESH_TREE( ).
  endif.

endmethod.
method DO_UPDATE_RTESPV_VIS.

***----------------------------------------------------------------------------
*** Set Routing Option
  if ME->S_RTESPV_VIS-RTEOP ne IS_RTESPV_VIS-RTEOP.
    ME->R_PR_RTESPV->SET_RTEOP( IF_RTEOP = IS_RTESPV_VIS-RTEOP
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Set Relevance Code
  if ME->S_RTESPV_VIS-RELCO ne IS_RTESPV_VIS-RELCO.
    ME->R_PR_RTESPV->SET_RELCO( IF_RELCO = IS_RTESPV_VIS-RELCO
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
*** Refresh Lists
    move ABAP_TRUE to ME->F_REFRESH_REQ.
  endif.

*** Set Routing Redetermination Flag
  if ME->S_RTESPV_VIS-RTERD ne IS_RTESPV_VIS-RTERD.
    ME->R_PR_RTESPV->SET_RTERD( IF_RTERD = IS_RTESPV_VIS-RTERD
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.


***----------------------------------------------------------------------------
*** List Layout
  if ME->S_RTESPV_VIS-LLAYO ne IS_RTESPV_VIS-LLAYO.
    ME->SET_LLAYO( IF_LLAYO = IS_RTESPV_VIS-LLAYO ).
  endif.

endmethod.
  method DO_USER_COMMAND.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    if IF_UCOMM cs C_TABNAME.
      move IF_UCOMM to ME->F_ACTIVE_TAB.
    endif.

    case IF_UCOMM.
      when C_UC_FILL.
        ME->HANDLE_UC_FILL( ).

      when C_UC_EDIT.
        ME->HANDLE_UC_EDIT( ).

      when C_UC_INFO.
        ME->HANDLE_UC_INFO( ).

      when C_UC_ADD.
        ME->HANDLE_UC_ADD( ).

      when C_UC_NETGRAPH.
        ME->HANDLE_UC_NETGRAPH( ).

    endcase.

  endmethod.
method HANDLE_GATE_FREED.

  SUPER->HANDLE_GATE_FREED( ).

  free: ME->R_PR_RTESPV.

endmethod.
method HANDLE_UC_ADD.

  data:  LF_LCVAL      type        /VCXI/VSMC_LCVAL.
  data:  LS_RTE_DEF    type        ZVCXI_XCSR_S_RTE_DEF.
  data:  LR_RTE_PRTE   type ref to /VCXI/CL_VSMR_RTE_PRTE,
         LR_RTE_ARBPL  type ref to /VCXI/CL_VSMR_RTE_ARBPL,
         LR_ARBPL      type ref to /VCXI/CL_VSMC_ARBPL,
         LR_RTE_PSTEP  type ref to /VCXI/CL_VSMR_RTE_PSTEP,
         LR_BL_PRTE    type ref to ZCL_VCXI_XCSR_BL_PRTE.

  clear: LR_ARBPL.

***----------------------------------------------------------------------------
  if lines( ME->R_PR_RTESPV->T_RTE_DEF ) eq 1.
    read table ME->R_PR_RTESPV->T_RTE_DEF into LS_RTE_DEF index 1.
  elseif lines( ME->R_PR_RTESPV->T_RTE_DEF ) gt 1.
*** Popup to Pick Route
  else.
*** No Routing Definition found
    if ME->R_PR_RTESPV->T_BL_PSTEP is not initial.
      raise exception type /VCXI/CX_CKX.
    endif.
  endif.

***----------------------------------------------------------------------------
*** Create new Route
  create object LR_RTE_PRTE.

  loop at LS_RTE_DEF-T_RTE_PSTEP into LR_RTE_PSTEP.
*** Create Work Center
    create object LR_RTE_ARBPL
      exporting
        IR_ARBPL = LR_ARBPL
        IF_LCVAL = LR_RTE_PSTEP->F_LCVAL.
    append LR_RTE_ARBPL to LR_RTE_PRTE->T_RTE_ARBPL.
    append LR_RTE_PSTEP to LR_RTE_ARBPL->T_RTE_PSTEP.
  endloop.

*** Create BL Production Route
  LR_BL_PRTE = ME->R_PR_RTESPV->CREATE_BL_PRTE( IR_RTE_PRTE = LR_RTE_PRTE ).

***----------------------------------------------------------------------------
*** Start Popup
  ME->POPUP_RTEEDI( IR_BL_PRTE = LR_BL_PRTE ).

*** Refresh Lists
  move ABAP_TRUE to ME->F_REFRESH_REQ.

endmethod.
method HANDLE_UC_EDIT.

  check ME->R_PR_RTESPV->R_RTESEL is bound.

*** Start Popup
  ME->POPUP_RTEEDI( IR_BL_PRTE = ME->R_PR_RTESPV->R_RTESEL ).

endmethod.
method HANDLE_UC_FILL.

  ME->R_PR_RTESPV->FILL_PROD( ).

*** Production was updated.
  message id 'ZVCXI_XCSR' type 'S' number '371'.

endmethod.
method HANDLE_UC_INFO.

  ZCL_VCXI_XCSR_POPUP_RTEINFO=>START( IR_GATE   = ME->R_GATE
                                      IR_UIGATE = ME->R_UIGATE ).

endmethod.
  method HANDLE_UC_NETGRAPH.

*** Call Network Graphic
    ZCL_VCXI_XCS_POPUP_NETGRAPH=>START( IR_GATE = ME->R_GATE ).

  endmethod.
method INIT_DEFAULT_LLAYO.

  data:  LS_ATTRI type /VCXI/VKS_S_ATTRI.

  try.
      LS_ATTRI = ME->R_GATE->GET_ATTRI( IF_ATTID = C_ATTID_LLAYO ).
      move LS_ATTRI-ATTVA to ME->F_LLAYO.
    catch /VCXI/CX_CKX.
      clear ME->F_LLAYO.
  endtry.

  if ME->F_LLAYO is initial.
*** Default List Layout
    move C_LLAYO_AWP to ME->F_LLAYO.
  endif.

endmethod.
method INIT_RTEALT.

***------------------------------------------------------------------
*** Free existing Element
  if ME->R_GRID_RTEALT is bound.
    ME->R_GRID_RTEALT->DO_FREE( ).
    clear ME->R_GRID_RTEALT.
  endif.

  if ME->R_TREE_RTEALT is bound.
    ME->R_TREE_RTEALT->DO_FREE( ).
    clear ME->R_TREE_RTEALT.
  endif.

***------------------------------------------------------------------
  case ME->F_LLAYO.
*** Grid-based View
    when ZCL_VCXI_XCSR_GRID_RTEALT=>C_LLAYO_AW or
         ZCL_VCXI_XCSR_GRID_RTEALT=>C_LLAYO_AWP.
      create object ME->R_GRID_RTEALT
        exporting
          IR_GATE           = ME->R_GATE
          IR_UIGATE         = ME->R_UIGATE
          IF_CONTAINER_NAME = '0102CC_RTEALT'
          IF_LLAYO          = ME->F_LLAYO
          IR_UI_RTESPV      = ME.

*** Tree-based View
    when ZCL_VCXI_XCSR_TREE_RTEALT=>C_LLAYO_MAW or
         ZCL_VCXI_XCSR_TREE_RTEALT=>C_LLAYO_MAWP.
      create object ME->R_TREE_RTEALT
        exporting
          IR_GATE           = ME->R_GATE
          IR_UIGATE         = ME->R_UIGATE
          IF_CONTAINER_NAME = '0102CC_RTEALT'
          IF_LLAYO          = ME->F_LLAYO
          IR_UI_RTESPV      = ME.

  endcase.

endmethod.
method INIT_RTEINV.

***------------------------------------------------------------------
*** Free existing Element
  if ME->R_GRID_RTEINV is bound.
    ME->R_GRID_RTEINV->DO_FREE( ).
    clear ME->R_GRID_RTEINV.
  endif.

  if ME->R_TREE_RTEINV is bound.
    ME->R_TREE_RTEINV->DO_FREE( ).
    clear ME->R_TREE_RTEINV.
  endif.

***------------------------------------------------------------------
  case ME->F_LLAYO.
*** Grid-based View
    when ZCL_VCXI_XCSR_GRID_RTEINV=>C_LLAYO_AW or
         ZCL_VCXI_XCSR_GRID_RTEINV=>C_LLAYO_AWP.
      create object ME->R_GRID_RTEINV
        exporting
          IR_GATE           = ME->R_GATE
          IR_UIGATE         = ME->R_UIGATE
          IF_CONTAINER_NAME = '0103CC_RTEINV'
          IF_LLAYO          = ME->F_LLAYO
          IR_UI_RTESPV      = ME.

*** Tree-based View
    when ZCL_VCXI_XCSR_TREE_RTEINV=>C_LLAYO_MAW or
         ZCL_VCXI_XCSR_TREE_RTEINV=>C_LLAYO_MAWP.
      create object ME->R_TREE_RTEINV
        exporting
          IR_GATE           = ME->R_GATE
          IR_UIGATE         = ME->R_UIGATE
          IF_CONTAINER_NAME = '0103CC_RTEINV'
          IF_LLAYO          = ME->F_LLAYO
          IR_UI_RTESPV      = ME.

  endcase.

endmethod.
method INIT_RTESEL.

*** Create Grid for Selected Route
  create object ME->R_GRID_RTESEL
    exporting
      IR_GATE           = ME->R_GATE
      IR_UIGATE         = ME->R_UIGATE
      IF_CONTAINER_NAME = '0101CC_RTESEL'.

endmethod.
method INIT_VIS.

  data: LF_COUNT  type STRING.

  free: ME->S_RTESPV_VIS.

  move ME->R_PR_RTESPV->GET_RTEOP( )         to ME->S_RTESPV_VIS-RTEOP.

  if ME->R_PR_RTESPV->R_RTESEL is bound.
    move ME->R_PR_RTESPV->R_RTESEL->F_TECHC  to ME->S_RTESPV_VIS-TECHC.
  endif.

  move ME->R_PR_RTESPV->GET_RELCO( )         to ME->S_RTESPV_VIS-RELCO.
  move ME->R_PR_RTESPV->GET_RTERD( )         to ME->S_RTESPV_VIS-RTERD.

  move ME->F_LLAYO                           to ME->S_RTESPV_VIS-LLAYO.

*** Tab Description
  move LINES( ME->R_PR_RTESPV->T_RTEALT ) to LF_COUNT.
  concatenate '(' LF_COUNT ')' into LF_COUNT.
  concatenate 'Alternative Routes'(T01) LF_COUNT into ME->S_RTESPV_VIS-TAB_RTEALT separated by SPACE.

  move LINES( ME->R_PR_RTESPV->T_RTEINV ) to LF_COUNT.
  concatenate '(' LF_COUNT ')' into LF_COUNT.
  concatenate 'Invalid Routes'(T02) LF_COUNT into ME->S_RTESPV_VIS-TAB_RTEINV separated by SPACE.

endmethod.
method POPUP_RTEEDI.

  data:  LF_LOCKED      type        /VCXI/CKX_LOCKED.
  data:  LR_APPDATA     type ref to /VCXI/CL_VKSR_APPDATA.

*** Get Application Data
  move ME->R_GATE->GET_APPDATA( ) to LR_APPDATA.

*** Start Edit Popup
  move ME->R_PR_RTESPV->IS_BL_PRTE_CHANGEABLE( ) to LF_LOCKED.
  ZCL_VCXI_XCSR_POPUP_RTEEDI=>START( IR_BL_PRTE   = IR_BL_PRTE
                                     IR_PR_RTESPV = ME->R_PR_RTESPV
                                     IR_APPDATA   = LR_APPDATA
                                     IF_LOCKED    = LF_LOCKED
                                     IF_CLSNA     = 'ZCL_VCXI_XCSR_POPUP_RTEEDI' ).
  ME->R_GATE->RAISE_EVENTS( ).

endmethod.
method SET_LLAYO.

  move IF_LLAYO to ME->F_LLAYO.

*** Initialize Alternative Routes
  ME->INIT_RTEALT( ).

*** Initialize Invalid Routes
  ME->INIT_RTEINV( ).

endmethod.
