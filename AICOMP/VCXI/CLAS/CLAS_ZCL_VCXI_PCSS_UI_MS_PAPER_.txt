
class ZCL_VCXI_PCSS_UI_MS_PAPER definition
  public
  inheriting from /VCXI/CL_VKSRU_CLSUI
  create public .

public section.

  data S_MS_PAPER_VIS type ZVCXI_PCSS_S_MS_PAPER_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods UPDATE_BY_MS_PAPER_VIS
    importing
      !IS_MS_PAPER_VIS type ZVCXI_PCSS_S_MS_PAPER_VIS
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
protected section.

  data R_PR_MS_PAPER type ref to ZCL_VCXI_PCSS_PR_MS_PAPER .

  methods INIT_VIS
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
private section.
endclass. "ZCL_VCXI_PCSS_UI_MS_PAPER definition
class ZCL_VCXI_PCSS_UI_MS_PAPER implementation.
method ACTIVATE.

  SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
  call function 'ZVCXI_PCSS_UI_MS_PAPER_BIND'
    exporting
      IR_UI_MS_PAPER = ME
    importing
      ES_DYNP        = ME->S_DYNP.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                      IR_UIGATE = IR_UIGATE
                      IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
  try.
      move IR_GATE->R_CLSPR ?to ME->R_PR_MS_PAPER.
    catch CX_SY_MOVE_CAST_ERROR.
***   UI ABAP Class doesn't work together with PR ABAP Class
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '801'.
  endtry.

endmethod.
method DO_MODIFY_SCREEN.

  data:  LF_LOCKED type        /VCXI/CKX_LOCKED,
         LF_INUSE  type        /VCXI/VKSR_INUSE.
  data:  LR_CX_CKX type ref to /VCXI/CX_CKX.

  try.

      SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                               IF_DYNNR = IF_DYNNR ).

      LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
      LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***   Modify Screen using DynamicVC
      ZCL_VCXI_XCDR_SERVICE=>MODIFY_SCREEN( IR_GATE = ME->R_GATE ).

      loop at screen.
        if ME->R_PR_MS_PAPER->GET_SUBPA( ) eq ABAP_FALSE and
           SCREEN-GROUP1                   eq 'SUB'.
          SCREEN-INPUT = 0.
        endif.
        if LF_LOCKED ne ABAP_TRUE or
           LF_INUSE  ne ABAP_TRUE.
          move 0 to SCREEN-INPUT.
        endif.

        modify screen.
      endloop.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

endmethod.
method DO_PBO.

*** Initialize Visualization Structure
  ME->INIT_VIS( ).

endmethod.
method HANDLE_GATE_FREED.

  SUPER->HANDLE_GATE_FREED( ).

  free: ME->R_PR_MS_PAPER.

endmethod.
method INIT_VIS.

  data: LS_BAWGT type ZVCXI_PCS_S_BAWGT,
        LS_BAWGP type ZVCXI_PCS_S_BAWGP,
        LS_CALIP type ZVCXI_P0SS_S_CALIP.

  free: ME->S_MS_PAPER_VIS.
  check ME->R_PR_MS_PAPER is bound.

  move ME->R_PR_MS_PAPER->GET_MATNR( )        to ME->S_MS_PAPER_VIS-BPMAT.
  move ME->R_PR_MS_PAPER->GET_MATNR_DESCR( )  to ME->S_MS_PAPER_VIS-BPMAT_DESCR.

  move ME->R_PR_MS_PAPER->GET_BPAID( )        to ME->S_MS_PAPER_VIS-BPAID.
  move ME->R_PR_MS_PAPER->GET_BPAID_DESCR( )  to ME->S_MS_PAPER_VIS-BPAID_DESCR.

  move ME->R_PR_MS_PAPER->GET_BPFAC( )        to ME->S_MS_PAPER_VIS-BPFAC.

  move ME->R_PR_MS_PAPER->GET_PRICE( )        to ME->S_MS_PAPER_VIS-S_PRICE.
  move ME->R_PR_MS_PAPER->GET_PRICP( )        to ME->S_MS_PAPER_VIS-S_PRICP.

  move ME->R_PR_MS_PAPER->GET_BAWGT( )        to LS_BAWGT.
  move-corresponding LS_BAWGT                 to ME->S_MS_PAPER_VIS.

  move ME->R_PR_MS_PAPER->GET_BAWGP( )        to LS_BAWGP.
  move-corresponding LS_BAWGP                 to ME->S_MS_PAPER_VIS.

  move ME->R_PR_MS_PAPER->GET_CALIP( )        to LS_CALIP.
  move-corresponding LS_CALIP                 to ME->S_MS_PAPER_VIS.

  move ME->R_PR_MS_PAPER->GET_SUBPA( )        to ME->S_MS_PAPER_VIS-SUBPA.
  move ME->R_PR_MS_PAPER->GET_PURCH( )        to ME->S_MS_PAPER_VIS-PURCH.

endmethod.                    "INIT_VIS
method UPDATE_BY_MS_PAPER_VIS.

  data:  LS_PRICE          type ZVCXI_XCS_S_PRICE,
         LS_PRICP          type ZVCXI_XCS_S_PRICP,
         LS_BAWGT          type ZVCXI_PCS_S_BAWGT,
         LS_BAWGP          type ZVCXI_PCS_S_BAWGP,
         LS_CALIP          type ZVCXI_P0SS_S_CALIP.

  check ME->R_PR_MS_PAPER is bound.

  if ME->S_MS_PAPER_VIS-SUBPA ne IS_MS_PAPER_VIS-SUBPA.
    ME->R_PR_MS_PAPER->SET_SUBPA( IF_SUBPA = IS_MS_PAPER_VIS-SUBPA
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Only update if Substitute Falg is abap_true
  if ME->S_MS_PAPER_VIS-SUBPA eq ABAP_TRUE.

    if ME->S_MS_PAPER_VIS-PRICE ne IS_MS_PAPER_VIS-PRICE.
      move-corresponding IS_MS_PAPER_VIS to LS_PRICE.
      ME->R_PR_MS_PAPER->SET_PRICE( IS_PRICE = LS_PRICE
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

    if ME->S_MS_PAPER_VIS-PRICP ne IS_MS_PAPER_VIS-PRICP.
      move-corresponding IS_MS_PAPER_VIS to LS_PRICP.
      ME->R_PR_MS_PAPER->SET_PRICP( IS_PRICP = LS_PRICP
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

    if ME->S_MS_PAPER_VIS-BAWGT ne IS_MS_PAPER_VIS-BAWGT.
      move-corresponding IS_MS_PAPER_VIS to LS_BAWGT.
      ME->R_PR_MS_PAPER->SET_BAWGT( IS_BAWGT = LS_BAWGT
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

    if ME->S_MS_PAPER_VIS-BAWGP ne IS_MS_PAPER_VIS-BAWGP.
      move-corresponding IS_MS_PAPER_VIS to LS_BAWGP.
      ME->R_PR_MS_PAPER->SET_BAWGP( IS_BAWGP = LS_BAWGP
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

    if ME->S_MS_PAPER_VIS-CALIP ne IS_MS_PAPER_VIS-CALIP.
      move-corresponding IS_MS_PAPER_VIS to LS_CALIP.
      ME->R_PR_MS_PAPER->SET_CALIP( IS_CALIP = LS_CALIP
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.
  endif.

endmethod.
