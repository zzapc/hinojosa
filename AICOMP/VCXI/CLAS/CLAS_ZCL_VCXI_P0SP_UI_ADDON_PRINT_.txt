
class ZCL_VCXI_P0SP_UI_ADDON_PRINT definition
  public
  inheriting from ZCL_VCXI_XCSP_UI_ADDON
  create public .

public section.

  data S_ADDON_PRINT_VIS type ZVCXI_P0SP_S_ADDON_PRINT_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  data R_PR_ADDON_PRINT type ref to ZCL_VCXI_P0SP_PR_ADDON_PRINT .
  data R_GRID_MC_INK type ref to ZCL_VCXI_P0SP_GRID_MC_INK .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_P0SP_UI_ADDON_PRINT definition
class ZCL_VCXI_P0SP_UI_ADDON_PRINT implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_P0SP_UI_ADDON_PRINT_BIND'
      exporting
        IR_UI_ADDON_PRINT = ME
      importing
        ES_DYNP           = ME->S_DYNP.

*** Create Grid for Inks
    create object ME->R_GRID_MC_INK
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_MC_INK'.

  endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                      IR_UIGATE = IR_UIGATE
                      IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
  try.
      move IR_GATE->R_CLSPR ?to ME->R_PR_ADDON_PRINT.
    catch CX_SY_MOVE_CAST_ERROR.
***   UI ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '801'.
  endtry.

endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

    if ME->R_GRID_MC_INK is bound.
      ME->R_GRID_MC_INK->DO_FREE( ).
      clear ME->R_GRID_MC_INK.
    endif.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Super PBO
    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize Grid
    if ME->R_GRID_MC_INK is bound.
      ME->R_GRID_MC_INK->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_MC_INK->REFRESH_GRID( ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_ADDON_PRINT_VIS type ZVCXI_P0SP_S_ADDON_PRINT_VIS.


*** Super
    SUPER->DO_UPDATE_VIS( IS_VIS = IS_VIS ).
    check ME->R_PR_ADDON_PRINT is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_ADDON_PRINT_VIS.

***-------------------------------------------------------------------------------------
*** Set Printable Area
    if ME->S_ADDON_PRINT_VIS-PRARE ne LS_ADDON_PRINT_VIS-PRARE.
      ME->R_PR_ADDON_PRINT->SET_PRARE( IS_PRARE = LS_ADDON_PRINT_VIS-S_PRARE
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Print Type and Side
    if ME->S_ADDON_PRINT_VIS-PRTYP ne LS_ADDON_PRINT_VIS-PRTYP.
      ME->R_PR_ADDON_PRINT->SET_PRTYP( IF_PRTYP = LS_ADDON_PRINT_VIS-PRTYP
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

    if ME->S_ADDON_PRINT_VIS-PRSID ne LS_ADDON_PRINT_VIS-PRSID.
      ME->R_PR_ADDON_PRINT->SET_PRSID( IF_PRSID = LS_ADDON_PRINT_VIS-PRSID
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Surface Type and Fullsurface Print
    if ME->S_ADDON_PRINT_VIS-SUTYP ne LS_ADDON_PRINT_VIS-SUTYP.
      ME->R_PR_ADDON_PRINT->SET_SUTYP( IF_SUTYP = LS_ADDON_PRINT_VIS-SUTYP
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

    if ME->S_ADDON_PRINT_VIS-FPRFS ne LS_ADDON_PRINT_VIS-FPRFS.
      ME->R_PR_ADDON_PRINT->SET_FPRFS( IF_FPRFS = LS_ADDON_PRINT_VIS-FPRFS
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
method DO_USER_COMMAND.

  if ME->R_GRID_MC_INK is bound.
    ME->R_GRID_MC_INK->CHECK_CHANGED_DATA( ).
  endif.

  SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_ADDON_PRINT.

  endmethod.
  method INIT_VIS.

*** Super
    SUPER->INIT_VIS( ).

    free: ME->S_ADDON_PRINT_VIS.
    ME->S_ADDON_PRINT_VIS-S_ADDON_VIS = ME->S_ADDON_VIS.

***--------------------------------------------------------------------------------------
*** Get Printable Area
    ME->S_ADDON_PRINT_VIS-S_PRARE = ME->R_PR_ADDON_PRINT->GET_PRARE( ).

*** Get Print Type and Side
    ME->S_ADDON_PRINT_VIS-PRTYP       = ME->R_PR_ADDON_PRINT->GET_PRTYP( ).
    ME->S_ADDON_PRINT_VIS-PRTYP_DESCR = ME->R_PR_ADDON_PRINT->GET_PRTYP_DESCR( ).
    ME->S_ADDON_PRINT_VIS-PRSID       = ME->R_PR_ADDON_PRINT->GET_PRSID( ) .
    ME->S_ADDON_PRINT_VIS-PRSID_DESCR = ME->R_PR_ADDON_PRINT->GET_PRSID_DESCR( ).

*** Get No. Colors and Surface Type
    ME->S_ADDON_PRINT_VIS-SUTYP       = ME->R_PR_ADDON_PRINT->GET_SUTYP( ).
    ME->S_ADDON_PRINT_VIS-SUTYP_DESCR = ME->R_PR_ADDON_PRINT->GET_SUTYP_DESCR( ).
    ME->S_ADDON_PRINT_VIS-NOCOL       = ME->R_PR_ADDON_PRINT->GET_NOCOL( ).
    ME->S_ADDON_PRINT_VIS-FPRFS       = ME->R_PR_ADDON_PRINT->GET_FPRFS( ).

  endmethod.
