
class ZCL_VCXI_XCUR_POPUP_MUMAP definition
  public
  create protected .

public section.

  data S_MUMAP_VIS type ZVCXI_XCUR_S_MUMAP_VIS .

  methods DO_MODIFY_SCREEN
    importing
      !IF_REPID type SYREPID
      !IF_DYNNR type SYDYNNR
    raising
      /VCXI/CX_CKX .
  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_VIS
    importing
      !IS_MUMAP_VIS type ZVCXI_XCUR_S_MUMAP_VIS
    raising
      /VCXI/CX_CKX .
  methods DO_USER_COMMAND
    changing
      !CF_UCOMM type SYUCOMM
    raising
      /VCXI/CX_CKX .
  class-methods START
    importing
      !IS_MUCUP type ZVCXI_XCUC_S_MUCUP
      !IF_SYSOM type /VCXI/VKS_SYSOM
      !IF_LOCKED type /VCXI/CKX_LOCKED default ABAP_TRUE
    changing
      !CS_MURUP type ZVCXI_XCUR_S_MURUP
    raising
      /VCXI/CX_CKX .
protected section.

  constants C_UC_CANCEL type SYUCOMM value 'CANCEL' ##NO_TEXT.
  constants C_UC_OKAY type SYUCOMM value 'OKAY' ##NO_TEXT.
  data F_LOCKED type /VCXI/CKX_LOCKED .
  data F_SYSOM type /VCXI/VKS_SYSOM .
  data R_GRID_MUMAP type ref to ZCL_VCXI_XCUR_GRID_MUMAP .
  data S_MURUP type ZVCXI_XCUR_S_MURUP .

  methods CONSTRUCTOR
    importing
      !IS_MURUP type ZVCXI_XCUR_S_MURUP
      !IF_SYSOM type /VCXI/VKS_SYSOM
      !IS_MUCUP type ZVCXI_XCUC_S_MUCUP
      !IF_LOCKED type /VCXI/CKX_LOCKED
    raising
      /VCXI/CX_CKX .
  methods DO_FREE
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_CANCEL
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_OKAY
    raising
      /VCXI/CX_CKX .
  methods SET_PF_STATUS
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_XCUR_POPUP_MUMAP definition
class ZCL_VCXI_XCUR_POPUP_MUMAP implementation.
  method CONSTRUCTOR.

    ME->S_MURUP  = IS_MURUP.
    ME->F_LOCKED = IF_LOCKED.

***--------------------------------------------------------------------------------------
*** Initialize VIS Structure
    ZCL_VCXI_XCUC_MUCEL=>GET_PARAM_INFO( exporting IR_PARAM = ME->S_MURUP-R_PARAM
                                                   IF_SYSOM = IF_SYSOM
                                         importing EF_PARID = ME->S_MUMAP_VIS-PARID
                                                   EF_DESCR = ME->S_MUMAP_VIS-DESCR
                                                   EF_UNIT  = ME->S_MUMAP_VIS-UNIT ).

    ME->S_MUMAP_VIS-SETBY = ME->S_MURUP-SETBY.

***--------------------------------------------------------------------------------------
*** Initialize Grid
    create object ME->R_GRID_MUMAP
      exporting
        IF_DATEL  = IS_MUCUP-DATEL
        IS_MURUP  = ME->S_MURUP
        IF_LOCKED = ME->F_LOCKED
        IF_QUAN   = ME->S_MUMAP_VIS-UNIT.

  endmethod.
  method DO_FREE.

    free: ME->S_MUMAP_VIS,
          ME->S_MURUP.

    if ME->R_GRID_MUMAP is bound.
      ME->R_GRID_MUMAP->DO_FREE( ).
      clear ME->R_GRID_MUMAP.
    endif.

  endmethod.
  method DO_MODIFY_SCREEN.

    loop at screen.
***   Display Mode
      if ME->F_LOCKED  ne ABAP_TRUE.
        move 0 to SCREEN-INPUT.
        move 0 to SCREEN-REQUIRED.
      endif.

***   Hide Unit for Parameter with no unit
      if ( ME->S_MURUP-R_PARAM          is not bound or
           ME->S_MURUP-R_PARAM->F_TUORC ne /VCXI/CL_VKSC_PARAM=>C_TUORC_U ) and
         ( SCREEN-NAME                  eq 'ZVCXI_XCUR_S_MUMAP_VIS-UNIT' ).
        SCREEN-INVISIBLE = '1'.
      endif.

      modify screen.
    endloop.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Set GUI Status
    ME->SET_PF_STATUS( ).

***--------------------------------------------------------------------------------------
*** Refresh of Grids
    if ME->R_GRID_MUMAP is bound.
      ME->R_GRID_MUMAP->REFRESH_GRID( ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

*** Full Update
    ME->S_MUMAP_VIS = IS_MUMAP_VIS.

  endmethod.
  method DO_USER_COMMAND.

*** Process User Commands
    case CF_UCOMM.
      when C_UC_OKAY.
        ME->HANDLE_UC_OKAY( ).
      when C_UC_CANCEL.
        ME->HANDLE_UC_CANCEL( ).

    endcase.

*** User Command has been processed
    clear CF_UCOMM.

  endmethod.
  method HANDLE_UC_CANCEL.

*** Leave Popup
    leave to screen 0.

  endmethod.
  method HANDLE_UC_OKAY.

***--------------------------------------------------------------------------------------
*** Set Parameter-based Update based on VIS
    ME->S_MURUP-SETBY = ME->S_MUMAP_VIS-SETBY.

*** Get Mapping Table
    if ME->R_GRID_MUMAP is bound.
      ME->S_MURUP-T_MUMAP = ME->R_GRID_MUMAP->GET_MUMAP( ).
    endif.

***--------------------------------------------------------------------------------------
*** Leave Popup
    leave to screen 0.

  endmethod.
  method SET_PF_STATUS.

    data:  LT_EXCLUDE type standard table of SYUCOMM.


***----------------------------------------------------------------------------
*** Exclude Okay when not Locked
    if ME->F_LOCKED ne ABAP_TRUE.
      append C_UC_OKAY to LT_EXCLUDE.
    endif.

***----------------------------------------------------------------------------
*** Set PF-Status
    set pf-status '0100' of program 'SAPLZVCXI_XCUR_POPUP_MUMAP'
                         excluding LT_EXCLUDE.

  endmethod.
  method START.

    data: LR_POPUP_MUMAP type ref to ZCL_VCXI_XCUR_POPUP_MUMAP.


*** Create Instance
    create object LR_POPUP_MUMAP
      exporting
        IS_MURUP  = CS_MURUP
        IF_SYSOM  = IF_SYSOM
        IS_MUCUP  = IS_MUCUP
        IF_LOCKED = IF_LOCKED.

*** Call Popup
    call function 'ZVCXI_XCUR_POPUP_MUMAP_START'
      exporting
        IR_POPUP_MUMAP = LR_POPUP_MUMAP.

*** Change Mapping
    CS_MURUP = LR_POPUP_MUMAP->S_MURUP.

*** Free
    LR_POPUP_MUMAP->DO_FREE( ).

  endmethod.
