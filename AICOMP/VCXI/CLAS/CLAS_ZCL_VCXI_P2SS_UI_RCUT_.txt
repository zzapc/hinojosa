
class ZCL_VCXI_P2SS_UI_RCUT definition
  public
  inheriting from ZCL_VCXI_XCS_UI_TAB
  create public .

public section.

  data S_RCUT_VIS type ZVCXI_P2SS_S_RCUT_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABPT type ZVCXI_XCS_TABPT default '0100S_RCT_TAB-TAB'
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_VIS
    importing
      !IS_VIS type ANY
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  data R_PR_RCUT type ref to ZCL_VCXI_P2SS_PR_RCUT .
  constants C_UC_FCPROP_OUT type SYUCOMM value 'FCPROP_OUT' ##NO_TEXT.
  constants C_UC_SCSTR_OUT type SYUCOMM value 'SCSTR_OUT' ##NO_TEXT.
  data R_TEXTEDIT_LTEXT type ref to ZCL_VCXI_PXSS_TEXTEDIT_LTEXT .

  methods HANDLE_UC_FCPROP
    importing
      !IF_UCOMM type SYUCOMM
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_SCSTR
    importing
      !IF_UCOMM type SYUCOMM
    raising
      /VCXI/CX_CKX .
  methods INIT_VIS
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
private section.
endclass. "ZCL_VCXI_P2SS_UI_RCUT definition
class ZCL_VCXI_P2SS_UI_RCUT implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_P2SS_UI_RCUT_BIND'
      exporting
        IR_UI_RCUT    = ME
      importing
        ES_DYNP       = ME->S_DYNP
        EF_ACTIVE_TAB = ME->F_ACTIVE_TAB.

*** Create Text Editor for Long Text
    create object ME->R_TEXTEDIT_LTEXT
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_TEXTEDIT_LTEXT'.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE
                        IF_TABPT  = IF_TABPT ).

*** Assign th RP Class to the UI Class
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_RCUT.
      catch CX_SY_MOVE_CAST_ERROR.

***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

***--------------------------------------------------------------------------------------
*** Update the Long Text before leaving the screen
    if ME->R_TEXTEDIT_LTEXT is bound.
      ME->R_TEXTEDIT_LTEXT->UPDATE_TEXT( ).
      ME->R_TEXTEDIT_LTEXT->DO_FREE( ).
      clear ME->R_TEXTEDIT_LTEXT.
    endif.

  endmethod.
  method DO_MODIFY_SCREEN.

    data: LF_LOCKED type        /VCXI/CKX_LOCKED,           "#EC NEEDED
          LF_INUSE  type        /VCXI/VKSR_INUSE.
    data: LR_CX_CKX type ref to /VCXI/CX_CKX.

    try.
        SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                 IF_DYNNR = IF_DYNNR ).

        LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
        LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***     Modify Scree using DynamicVC
        ZCL_VCXI_XCDR_SERVICE=>MODIFY_SCREEN( IR_GATE = ME->R_GATE ).

        loop at screen.
***       Reopen Info Buttons
          if SCREEN-GROUP1 eq 'INF'.
            move 1 to SCREEN-INPUT.
          endif.

          modify screen.
        endloop.

      catch /VCXI/CX_CKX into LR_CX_CKX.
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_PBO.

    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize VIS
    ME->INIT_VIS( ).


***--------------------------------------------------------------------------------------
*** Get TextEdit Conversion Text
    if ME->R_TEXTEDIT_LTEXT is bound.
      move ABAP_TRUE to ME->R_TEXTEDIT_LTEXT->F_REFRESH_REQ.
      ME->R_TEXTEDIT_LTEXT->REFRESH_TEXTEDIT( ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_RCUT_VIS  type ZVCXI_P2SS_S_RCUT_VIS.


    check ME->R_PR_RCUT is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_RCUT_VIS.

***--------------------------------------------------------------------------------------
**** Set Work Center
*    if ME->S_CORR_VIS-ARBPL ne LS_CORR_VIS-ARBPL.
*      ME->R_PR_CORR->SET_ARBPL( IF_ARBPL = LS_CORR_VIS-ARBPL
*                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
*    endif.

  endmethod.
  method DO_USER_COMMAND.

***--------------------------------------------------------------------------------------
*** Update TextEdit Long Text
    if ME->R_TEXTEDIT_LTEXT is bound.
      ME->R_TEXTEDIT_LTEXT->UPDATE_TEXT( ).
    endif.

***--------------------------------------------------------------------------------------
*** Call Super
    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    case IF_UCOMM.
***   Folding Carton Property Pop-Up
      when C_UC_FCPROP_OUT.
        ME->HANDLE_UC_FCPROP( exporting IF_UCOMM = IF_UCOMM  ).

***   Scores String Pop-Up
      when C_UC_SCSTR_OUT.
        ME->HANDLE_UC_SCSTR( exporting IF_UCOMM = IF_UCOMM ).

    endcase.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_RCUT.

  endmethod.
  method HANDLE_UC_FCPROP.

*** Start Popup
    ZCL_VCXI_P2SS_POPUP_FCPROP=>START( IR_BL_FCBPROP = ME->R_PR_RCUT->ZIF_VCXI_P2SS_COMM_NEXT~GET_FCBPX( )
                                       IR_BL_FCFPROP = ME->R_PR_RCUT->ZIF_VCXI_P2SS_COMM_NEXT~GET_FCFPX( )
                                       IF_WERKS      = ME->R_PR_RCUT->GET_WERKS( )
                                       IS_SURWT      = ME->R_PR_RCUT->ZIF_VCXI_P2SS_COMM_NEXT~GET_SURWT( ) ).

  endmethod.
  method HANDLE_UC_SCSTR.

    data: LR_BL_FCBPROP type ref to ZCL_VCXI_P2SS_BL_FCBPROP.


*** Get Score String (Outbound)
    LR_BL_FCBPROP = ME->R_PR_RCUT->ZIF_VCXI_P2SS_COMM_NEXT~GET_FCBPX( ).
    check LR_BL_FCBPROP is bound.

*** Start Popup
    ZCL_VCXI_PXSS_POPUP_SCSTR=>START( IT_SCORE_COM = LR_BL_FCBPROP->T_SCORE ).

  endmethod.
  method INIT_VIS.

    data: LR_BL_FCFPROP type ref to ZCL_VCXI_P2SS_BL_FCFPROP.


    free: ME->S_RCUT_VIS.
    check ME->R_PR_RCUT is bound.

***--------------------------------------------------------------------------------------
**** Get Work Center
*    ME->S_RCUT_VIS-ARBPL = ME->R_PR_RCUT->GET_ARBPL( ).

***--------------------------------------------------------------------------------------
*** Get Sheet Dimension (Outbound)
    ME->R_PR_RCUT->ZIF_VCXI_P2SS_COMM_NEXT~GET_SHTDIM( importing ES_SHTLE = ME->S_RCUT_VIS-S_IOSEC_OUT-S_SHTLE
                                                                 ES_SHTWI = ME->S_RCUT_VIS-S_IOSEC_OUT-S_SHTWI
                                                                 EF_GRADI = ME->S_RCUT_VIS-S_IOSEC_OUT-GRADI ).

*** Number Ups (Outbound)
    ME->S_RCUT_VIS-S_IOSEC_OUT-NOUPS = ME->R_PR_RCUT->ZIF_VCXI_P2SS_COMM_NEXT~GET_NOUPS( ).

*** Get Caliper, Paper Grade and Grammage (Outbound)
    LR_BL_FCFPROP = ME->R_PR_RCUT->ZIF_VCXI_P2SS_COMM_NEXT~GET_FCFPX( ).
    if LR_BL_FCFPROP is bound.
      ME->S_RCUT_VIS-S_IOSEC_OUT-S_CALIP = LR_BL_FCFPROP->S_CALIP.
      ME->S_RCUT_VIS-S_IOSEC_OUT-PAPGR   = LR_BL_FCFPROP->F_PAPGR.
      ME->S_RCUT_VIS-S_IOSEC_OUT-S_GRAMM = LR_BL_FCFPROP->S_GRAMM.
    endif.

  endmethod.
