
class ZCL_VCXI_P5SS_UI_RLCONI definition
  public
  inheriting from ZCL_VCXI_PXSS_UI_XCONI
  create public .

public section.

  data S_RLCONI_VIS type ZVCXI_P5SS_S_RLCONI_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABPT type ZVCXI_XCS_TABPT default '0100S_IN_TAB-TAB'
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  constants C_UC_RLPROP_IN type SYUCOMM value 'RLPROP_IN' ##NO_TEXT.
  data R_PR_RLCONI type ref to ZCL_VCXI_P5SS_PR_RLCONI .

  methods HANDLE_UC_RLPROP
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_P5SS_UI_RLCONI definition
class ZCL_VCXI_P5SS_UI_RLCONI implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_P5SS_UI_RLCONI_BIND'
      exporting
        IR_UI_RLCONI = ME
      importing
        ES_DYNP      = ME->S_DYNP.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_RLCONI.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DO_USER_COMMAND.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    case IF_UCOMM.
***   Roll Property Popup
      when C_UC_RLPROP_IN.
        ME->HANDLE_UC_RLPROP( ).

    endcase.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_RLCONI.

  endmethod.
  method HANDLE_UC_RLPROP.

*** Start Popup
    ZCL_VCXI_P5SS_POPUP_RLPROP=>START( IR_BL_RLBPROP = ME->R_PR_RLCONI->ZIF_VCXI_P5SS_COMM_NEXT~GET_RLBPX( )
                                       IR_BL_RLFPROP = ME->R_PR_RLCONI->ZIF_VCXI_P5SS_COMM_NEXT~GET_RLFPX( )
                                       IF_WERKS      = ME->R_PR_RLCONI->GET_WERKS( )
                                       IS_WEBWI      = ME->R_PR_RLCONI->ZIF_VCXI_P5SS_COMM_NEXT~GET_WEBWI( )
                                       IS_SURWT      = ME->R_PR_RLCONI->ZIF_VCXI_P5SS_COMM_NEXT~GET_SURWT( ) ).

  endmethod.
  method INIT_VIS.

    data: LR_BL_RLFPROP type ref to ZCL_VCXI_P5SS_BL_RLFPROP.


    SUPER->INIT_VIS( ).

    free: ME->S_RLCONI_VIS.
    ME->S_RLCONI_VIS-S_XCONI_VIS = ME->S_XCONI_VIS.

***--------------------------------------------------------------------------------------
*** Get Web Width (Inbound)
    ME->S_RLCONI_VIS-S_IOSEC_IN-S_WEBWI = ME->R_PR_RLCONI->ZIF_VCXI_P5SS_COMM_PREV~GET_WEBWI( ).

*** Get Surface Weight (Inbound)
    ME->S_RLCONI_VIS-S_IOSEC_IN-S_SURWT = ME->R_PR_RLCONI->ZIF_VCXI_P5SS_COMM_PREV~GET_SURWT( ).

*** Number Ups (Inbound)
    ME->S_RLCONI_VIS-S_IOSEC_IN-NOUPS   = ME->R_PR_RLCONI->ZIF_VCXI_P5SS_COMM_PREV~GET_NOUPS( ).

*** Get Substrate and Caliper (Inbound)
    LR_BL_RLFPROP = ME->R_PR_RLCONI->ZIF_VCXI_P5SS_COMM_PREV~GET_RLFPX( ).
    if LR_BL_RLFPROP is bound.
      ME->S_RLCONI_VIS-S_IOSEC_IN-SBSTR   = LR_BL_RLFPROP->GET_SBSTR( ).
      ME->S_RLCONI_VIS-S_IOSEC_IN-S_CALIP = LR_BL_RLFPROP->S_CALIP.
    endif.

  endmethod.
