
class ZCL_VCXI_FFSS_ND_FCONO definition
  public
  inheriting from ZCL_VCXI_F0SS_ND
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods GET_FCODE
    redefinition .
protected section.

  methods INIT_NODE
    redefinition .
private section.
endclass. "ZCL_VCXI_FFSS_ND_FCONO definition
class ZCL_VCXI_FFSS_ND_FCONO implementation.
  method CONSTRUCTOR.

*** Call super constructor
    SUPER->CONSTRUCTOR( IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

*** Initialize with class type specific values
    ME->INIT_NODE( ).

  endmethod.
  method GET_FCODE.

    field-symbols:  <S_FCODE>     type      YS_FCODE.

***--------------------------------------------------------------------------------------
*** Create Popup Menu Entries for Node
    SUPER->GET_FCODE( importing ET_FCODE = ET_FCODE ).
    check ME->R_GATE->IS_LOCKED( ) eq ABAP_TRUE.

***--------------------------------------------------------------------------------------
*** Create Separation Line
    append initial line to ET_FCODE assigning <S_FCODE>.
    move C_UC_BLANK                      to <S_FCODE>-FCODE.
    move C_TEXT_SEP                      to <S_FCODE>-TEXT.

*** Create Merge
    append initial line to ET_FCODE assigning <S_FCODE>.
    move C_UC_MERGE                      to <S_FCODE>-FCODE.
    move text-T01                        to <S_FCODE>-TEXT.

  endmethod.
  method INIT_NODE.

    data:           LR_FCONO            type ref to ZCL_VCXI_FFSS_PR_FCONO.
    data:           LS_FIWID            type        ZVCXI_F0S_S_FIWID.
    field-symbols:  <S_NVAL>            type        YS_NVALS.

***----------------------------------------------------------------------------
*** Call Super
    SUPER->INIT_NODE( ).

***----------------------------------------------------------------------------
*** Check Input Gate is Film Connection Out
    try.
        move ME->R_GATE->R_CLSPR ?to LR_FCONO.
      catch CX_SY_MOVE_CAST_ERROR.
        clear LR_FCONO.
    endtry.

    check LR_FCONO is bound.

*** Initialize Node Values from Gate
    move 5 to ME->F_NETTYPE.

*** Set Node Description
    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = 0.
    <S_NVAL>-VAL  = LR_FCONO->GET_DESCR( ).

***----------------------------------------------------------------------------
*** Set Film Width and UOM
    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = 2.
    move text-D01 to <S_NVAL>-VAL.

    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = 3.
    LR_FCONO->ZIF_VCXI_FFSS_COMM_PREV~GET_FIWID( importing ES_FIWID = LS_FIWID ).
    <S_NVAL>-VAL = ME->CONV_ANY_TO_VAL( IF_VALUE = LS_FIWID-FIWID
                                        IF_UOM   = LS_FIWID-FIWID_UOM ).

*** Set Status Icon
    ME->SET_STATUS_ICON( ).

  endmethod.
endclass. "ZCL_VCXI_FFSS_ND_FCONO implementation
