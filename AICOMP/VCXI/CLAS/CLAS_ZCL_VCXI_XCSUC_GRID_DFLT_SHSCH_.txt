
class ZCL_VCXI_XCSUC_GRID_DFLT_SHSCH definition
  public
  inheriting from ZCL_VCXI_XCSUC_GRID
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IF_TABNAME type TABNAME optional
      !IF_CONTAINER_NAME type SCRFNAME optional
      !IR_CUSTOMIZING type ref to OBJECT
      !IR_S_CONTAINER type ref to CL_GUI_CONTAINER optional
    raising
      /VCXI/CX_CKX .
protected section.

  data R_DFLT type ref to ZCL_VCXI_XCSUC_DFLT .

  methods CHECK_SELECTION
    returning
      value(RF_OKAY) type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
  methods HANDLE_TOOLBAR
    for event TOOLBAR of CL_GUI_ALV_GRID
    importing
      !E_OBJECT
      !E_INTERACTIVE .
  methods HANDLE_USER_COMMAND
    for event USER_COMMAND of CL_GUI_ALV_GRID
    importing
      !E_UCOMM .

  methods GET_FIELDCAT
    redefinition .
  methods GET_GRID_DATA
    redefinition .
  methods GET_LAYOUT
    redefinition .
  methods SET_EVENT
    redefinition .
private section.

  constants C_FUNC_ADD type UI_FUNC value 'ADD'. "#EC NOTEXT
  constants C_FUNC_DEL type UI_FUNC value 'DELETE'. "#EC NOTEXT
endclass. "ZCL_VCXI_XCSUC_GRID_DFLT_SHSCH definition
class ZCL_VCXI_XCSUC_GRID_DFLT_SHSCH implementation.
method CHECK_SELECTION.

  data: LT_CELL type LVC_T_CELL,
        LT_ROW  type LVC_T_ROW.

  ME->R_GRID->GET_SELECTED_CELLS( importing ET_CELL = LT_CELL ).
  ME->R_GRID->GET_SELECTED_ROWS( importing ET_INDEX_ROWS = LT_ROW ).

*** Delete is only available if Cells are selected
  if lines( LT_CELL ) gt 0 or
     lines( LT_ROW )  gt 0.
    move ABAP_TRUE to RF_OKAY.
  else.
    move ABAP_FALSE to RF_OKAY.
  endif.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( exporting IF_TABNAME        = 'ZVCXI_XCSUC_S_DFLT_SHSCH_GRID'
                                IF_CONTAINER_NAME = '0100CC_DFLT_SHSCH_GRID'
                                IR_CUSTOMIZING    = IR_CUSTOMIZING
                                IR_S_CONTAINER    = IR_S_CONTAINER ).

*** Try to Assign Customizing Reference
  try.
      move IR_CUSTOMIZING ?to ME->R_DFLT.
    catch CX_SY_MOVE_CAST_ERROR.
***   Default Customizing Reference can't be assigned.
      /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE(
          IF_MSGTY = 'E'
          IF_MSGID = 'ZVCXI_XCSUC'
          IF_MSGNO = '203' ).
  endtry.

  check ME->R_DFLT is bound.
  ME->F_LOCKED = ME->R_DFLT->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ).

endmethod.
method GET_FIELDCAT.

  field-symbols: <S_FCAT>           type        LVC_S_FCAT.

***----------------------------------------------------------------------------
*** Get Standard Field Catalog
  RT_FCAT = SUPER->GET_FIELDCAT( ).

***----------------------------------------------------------------------------
*** Adjust Field Catalog
  loop at RT_FCAT assigning <S_FCAT>.
    case <S_FCAT>-FIELDNAME.
***   Set GUID to only technical Fields
      when 'GDFLT'.
        move ABAP_TRUE to <S_FCAT>-TECH.
    endcase.
  endloop.

endmethod.
method GET_GRID_DATA.

  data:          LS_DFLT_SHSCH_GRID   type          ZVCXI_XCSUC_S_DFLT_SHSCH_GRID,
                 LS_DFLT_SHSCH        type          ZVCXI_XCSUC_S_SHSCH_DFLT.
  field-symbols: <T_DATA>             type standard table,
                 <S_DATA>             type          any.

*** Dereferencing of Data Reference
  assign IR_DATA->* to <T_DATA>.

  free: <T_DATA>.

*** --------------------------------------------------------------------------------------------
*** Get all Shipping Scheme Entries
  loop at ME->R_DFLT->T_SHSCH_DFLT into LS_DFLT_SHSCH.

    clear: LS_DFLT_SHSCH_GRID.
    move-corresponding LS_DFLT_SHSCH to LS_DFLT_SHSCH_GRID.

*** --------------------------------------------------------------------------------------------
*** Add Entry
    append initial line to <T_DATA> assigning <S_DATA>.
    move-corresponding LS_DFLT_SHSCH_GRID to <S_DATA>.
  endloop.

endmethod.
method GET_LAYOUT.

  RS_LAYOUT = SUPER->GET_LAYOUT( ).

*** Set Toolbar to Activ
  move ABAP_FALSE  to RS_LAYOUT-NO_TOOLBAR.
  move 'A'         to RS_LAYOUT-SEL_MODE.

endmethod.
method HANDLE_TOOLBAR.

  data:  LS_TOOLBAR      type        STB_BUTTON.
  data:  LF_DISABLED     type        ABAP_BOOL.
  data:  LR_CX_CKX       type ref to /VCXI/CX_CKX.

  try.

      if ME->R_DFLT->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ) eq ABAP_FALSE.
        move ABAP_TRUE to LF_DISABLED.
      endif.

***----------------------------------------------------------------------------
***   Insert Toolbar elements Add and Delete
      clear LS_TOOLBAR.
      move C_FUNC_ADD                 to LS_TOOLBAR-FUNCTION.
      move 'Add Entry'(001)           to LS_TOOLBAR-QUICKINFO.
      move C_BTYPE_BUTTON             to LS_TOOLBAR-BUTN_TYPE.
      move ICON_INSERT_ROW            to LS_TOOLBAR-ICON.
      move LF_DISABLED                to LS_TOOLBAR-DISABLED.
      append LS_TOOLBAR               to E_OBJECT->MT_TOOLBAR.

      clear LS_TOOLBAR.
      move C_FUNC_DEL                 to LS_TOOLBAR-FUNCTION.
      move 'Delete Entry'(002)        to LS_TOOLBAR-QUICKINFO.
      move C_BTYPE_BUTTON             to LS_TOOLBAR-BUTN_TYPE.
      move ICON_DELETE_ROW            to LS_TOOLBAR-ICON.
      move LF_DISABLED                to LS_TOOLBAR-DISABLED.
      append LS_TOOLBAR               to E_OBJECT->MT_TOOLBAR.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      LR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

endmethod.
method HANDLE_USER_COMMAND.

  data:          LF_DISABLED         type               ABAP_BOOL.
  data: LS_CELL            type               LVC_S_CELL,
        LS_ROW             type               LVC_S_ROW,
        LS_DFLT_SHSCH      type               ZVCXI_XCSUC_S_SHSCH_DFLT,
        LS_DFLT_SHSCH_NEW  type               ZVCXI_XCSUC_S_DFLT_SHSCH_SHLP,
        LS_DFLT_SHSCH_OLD  type               ZVCXI_XCSUC_S_DFLT_SHSCH_SHLP,
        LS_DFLT_SHSCH_CURR type               ZVCXI_XCSUC_S_DFLT_SHSCH_SHLP.
  data: LT_CELL            type               LVC_T_CELL,
        LT_ROW             type               LVC_T_ROW,
        LT_DFLT_SHSCH      type               ZVCXI_XCSUC_TT_DFLT_SHSCH_SHLP,
        LT_DFLT_SHSCH_NEW  type               ZVCXI_XCSUC_TT_DFLT_SHSCH_SHLP,
        LT_DFLT_SHSCH_CURR type               ZVCXI_XCSUC_TT_DFLT_SHSCH_SHLP.
  data:          LR_CX_CKX           type ref to        /VCXI/CX_CKX.
  field-symbols: <T_DATA>       type standard      table,
                 <S_DFLT_SHSCH> type               ZVCXI_XCSUC_S_DFLT_SHSCH_SHLP,
                 <S_DATA>       type               ANY.

  try.

*** ---------------------------------------------------------------------------
***   Get Value Data
      assign ME->R_DATA->* to <T_DATA>.
      free: <T_DATA>.
      ME->GET_GRID_DATA( IR_DATA = ME->R_DATA ).

      ME->R_GRID->GET_SELECTED_CELLS( importing ET_CELL = LT_CELL ).
      ME->R_GRID->GET_SELECTED_ROWS( importing ET_INDEX_ROWS = LT_ROW ).

*** ---------------------------------------------------------------------------
***   Get Selected Shipping Scheme Assignment
      loop at LT_CELL into LS_CELL.
        read table <T_DATA> assigning <S_DATA> index LS_CELL-ROW_ID-INDEX.
        check SY-SUBRC = 0.

        append initial line to LT_DFLT_SHSCH assigning <S_DFLT_SHSCH>.
        move-corresponding <S_DATA> to <S_DFLT_SHSCH>.
      endloop.

      loop at LT_ROW into LS_ROW.
        read table <T_DATA> assigning <S_DATA> index LS_ROW-INDEX.
        check SY-SUBRC = 0.

        append initial line to LT_DFLT_SHSCH assigning <S_DFLT_SHSCH>.
        move-corresponding <S_DATA> to <S_DFLT_SHSCH>.
      endloop.

*** ---------------------------------------------------------------------------
***   handle UserCommand
      case E_UCOMM.

        when C_FUNC_ADD.
***       Add new Shipping Scheme Assignment
          if ME->R_DFLT->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ) eq ABAP_FALSE.
            move ABAP_TRUE to LF_DISABLED.
          endif.

***       Get all Current Shipping Schemes
          loop at ME->R_DFLT->T_SHSCH_DFLT into LS_DFLT_SHSCH.
            move-corresponding LS_DFLT_SHSCH to LS_DFLT_SHSCH_CURR.
            append LS_DFLT_SHSCH_CURR to LT_DFLT_SHSCH_CURR.
          endloop.
          clear: LS_DFLT_SHSCH.

          LT_DFLT_SHSCH_NEW = ZCL_VCXI_XCSUC_SHLP_DFLT_SHSCH=>CALL_SHLP( IF_DISPLAY         = LF_DISABLED
                                                                         IT_DFLT_SHSCH      = LT_DFLT_SHSCH_CURR
                                                                         IF_MULTIPLE_CHOICE = ABAP_TRUE ).
          loop at LT_DFLT_SHSCH_NEW into LS_DFLT_SHSCH_NEW.
            move-corresponding LS_DFLT_SHSCH_NEW to LS_DFLT_SHSCH.
            try.
                ME->R_DFLT->ADD_SHSCH_DFLT( IS_SHSCH_DFLT = LS_DFLT_SHSCH ).
              catch /VCXI/CX_CKX into LR_CX_CKX.
                LR_CX_CKX->SHOW_MESSAGE( ).
            endtry.
          endloop.

        when C_FUNC_DEL.
***       Delete Shipping Scheme Assignment
          if ME->CHECK_SELECTION( ) eq ABAP_FALSE.
***         No Row Selected. Please Select all Row's you wanna Delete.
            raise exception type /VCXI/CX_CKX
              exporting
                F_MSGTY = 'E'
                F_MSGID = 'ZVCXI_XCSUC'
                F_MSGNO = '003'.
          endif.

***       Delete all Shipping Schemes Assignments
          loop at LT_DFLT_SHSCH into LS_DFLT_SHSCH_OLD.
            move-corresponding LS_DFLT_SHSCH_OLD to LS_DFLT_SHSCH.
            ME->R_DFLT->DELETE_SHSCH_DFLT( IS_SHSCH_DFLT = LS_DFLT_SHSCH ).
          endloop.
      endcase.

      ME->F_REFRESH_REQ = ABAP_TRUE.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      LR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

endmethod.
method SET_EVENT.

  SUPER->SET_EVENT( ).

*** Add Event for F4Help, UserCommand and Toolbar
  set handler ME->HANDLE_USER_COMMAND for ME->R_GRID.
  set handler ME->HANDLE_TOOLBAR      for ME->R_GRID.

endmethod.
