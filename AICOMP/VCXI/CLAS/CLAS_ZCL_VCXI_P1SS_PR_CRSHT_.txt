
class ZCL_VCXI_P1SS_PR_CRSHT definition
  public
  inheriting from ZCL_VCXI_PXSS_PR_XSHT
  create public .

public section.

  interfaces ZIF_VCXI_P1SS_COMM_PREV .

  methods GET_CRBPX
    exporting
      !EF_CRBPX type ZVCXI_P1S_CRBPX
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RR_BL_CRBPROP) type ref to ZCL_VCXI_P1SS_BL_CRBPROP
    raising
      /VCXI/CX_CKX .
  methods GET_CRFPX
    exporting
      !EF_CRFPX type ZVCXI_P1S_CRFPX
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RR_BL_CRFPROP) type ref to ZCL_VCXI_P1SS_BL_CRFPROP
    raising
      /VCXI/CX_CKX .

  methods GET_CALIP
    redefinition .
  methods GET_HNDDIM
    redefinition .
  methods GET_PRODH
    redefinition .
  methods GET_SURWT
    redefinition .
  methods ZIF_VCXI_PXSS_COMM_PREV~DO_FORWARD
    redefinition .
  methods GET_ADDWT
    redefinition .
protected section.

  constants C_IEVNT_O_SURWT_CHANGED type /VCXI/VKSC_IEVNT value 'SURWT_CHANGED' ##NO_TEXT.
  constants C_PARID_CRFPX type /VCXI/VKS_PARID value 'ZVCXI_P1_CRFPX' ##NO_TEXT.
  constants C_PARID_SURWT type /VCXI/VKS_PARID value 'ZVCXI_PX_SURWT' ##NO_TEXT.
  constants C_IEVNT_O_CRFPX_CHANGED type /VCXI/VKSC_IEVNT value 'CRFPX_CHANGED' ##NO_TEXT.

  methods SET_CRFPX
    importing
      !IF_CRFPX type ZVCXI_P1S_CRFPX
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_SURWT
    importing
      !IS_SURWT type ZVCXI_PXS_S_SURWT
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_P1SS_PR_CRSHT definition
class ZCL_VCXI_P1SS_PR_CRSHT implementation.
  method GET_ADDWT.

    data: LR_BL_CRFPROP type ref to ZCL_VCXI_P1SS_BL_CRFPROP.


*** Get Corrugated Forward Property XML (Outbound)
    LR_BL_CRFPROP = ME->ZIF_VCXI_P1SS_COMM_PREV~GET_CRFPX( ).
    check LR_BL_CRFPROP is bound.

*** Get Additional Weight
    RS_ADDWT = LR_BL_CRFPROP->S_ADDWT.

  endmethod.
  method GET_CALIP.

    data: LR_BL_CRFPROP type ref to ZCL_VCXI_P1SS_BL_CRFPROP.


*** Get Corrugated Forward Property XML (Outbound)
    LR_BL_CRFPROP = ME->ZIF_VCXI_P1SS_COMM_PREV~GET_CRFPX( ).
    check LR_BL_CRFPROP is bound.

*** Get Caliper
    RS_CALIP = LR_BL_CRFPROP->S_CALIP.

  endmethod.
  method GET_CRBPX.

    data: LT_SCORE  type ZVCXI_PXSS_TT_SCORE_COM.


***--------------------------------------------------------------------------------------
*** Create fresh instance of Folding Carton Backward Property
    RR_BL_CRBPROP = ZCL_VCXI_P1SS_BL_CRBPROP=>FACTORY( ).

    if RR_BL_CRBPROP is bound.
***   Set Sheet Scores
      LT_SCORE = ZCL_VCXI_PXSS_BL_SCORE=>GET_SCORE_SHTN( IS_SHTNW = ME->GET_SHTWI( )
                                                         IS_SHTNL = ME->GET_SHTLE( ) ).
      RR_BL_CRBPROP->SET_SCORE( IT_SCORE = LT_SCORE ).

***   Sheet FEFCO
      RR_BL_CRBPROP->SET_FEFCO_SHEET( ).

***   Cost Split Distribution
      RR_BL_CRBPROP->SET_CSPLIT( IT_CSPLIT = ME->GET_CSPLIT( ) ).
    endif.

***--------------------------------------------------------------------------------------
*** Return Folding Carton Backward Property XML (Outbound)
    if EF_CRBPX is requested.
      move RR_BL_CRBPROP->GET_CRBPX( ) to EF_CRBPX.
    endif.

  endmethod.
  method GET_CRFPX.

***--------------------------------------------------------------------------------------
*** Get Corrugator Forward Property (Inbound)
    RR_BL_CRFPROP = ME->ZIF_VCXI_P1SS_COMM_PREV~GET_CRFPX( importing EF_SETBY = EF_SETBY ).

***--------------------------------------------------------------------------------------
*** Get new Corrugator Forward Property XML
    if EF_CRFPX is requested.
      EF_CRFPX = RR_BL_CRFPROP->GET_CRFPX( ).
    endif.

  endmethod.
  method GET_HNDDIM.

    data: LR_BL_CRFPROP type ref to ZCL_VCXI_P1SS_BL_CRFPROP.


*** Get Corrugated Forward Property XML (Outbound)
    LR_BL_CRFPROP = ME->ZIF_VCXI_P1SS_COMM_PREV~GET_CRFPX( ).
    check LR_BL_CRFPROP is bound.

*** Get Handling Dimension
    if ME->GET_FDIMT( ) ne ABAP_TRUE.
      RS_HNDDIM = LR_BL_CRFPROP->S_HNDDIM.
    else.
      RS_HNDDIM = value #( S_HNDLE = LR_BL_CRFPROP->S_HNDDIM-S_HNDWI
                           S_HNDWI = LR_BL_CRFPROP->S_HNDDIM-S_HNDLE
                           S_HNDHE = LR_BL_CRFPROP->S_HNDDIM-S_HNDHE ).
    endif.

  endmethod.
  method GET_PRODH.

    data: LR_BL_CRFPROP type ref to ZCL_VCXI_P1SS_BL_CRFPROP.


*** Get Corrugated Forward Property XML (Outbound)
    LR_BL_CRFPROP = ME->ZIF_VCXI_P1SS_COMM_PREV~GET_CRFPX( ).
    check LR_BL_CRFPROP is bound.

*** Get Product Hierarchy
    RF_PRODH = LR_BL_CRFPROP->F_PRODH.

    check RF_PRODH is not initial.

    if EF_DESCR is requested.
      EF_DESCR = ZCL_VCXI_XCS_SERVICE=>GET_PRODH_DESCR( IF_PRODH = RF_PRODH ).
    endif.

  endmethod.
  method GET_SURWT.

***--------------------------------------------------------------------------------------
*** Get Surface Weight (Inbound)
    RS_SURWT = ME->ZIF_VCXI_P1SS_COMM_PREV~GET_SURWT( ).

  endmethod.
  method SET_CRFPX.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_CRFPX ).

    move IF_CRFPX   to LS_PARAM-STRNG.
    move IF_SETBY   to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_CRFPX_CHANGED ).
    endif.

  endmethod.
  method SET_SURWT.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SURWT ).

    move IS_SURWT-SURWT     to LS_PARAM-ATFLV.
    move IS_SURWT-SURWT_UOM to LS_PARAM-UNIT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_SURWT_CHANGED ).
    endif.

  endmethod.
  method ZIF_VCXI_P1SS_COMM_PREV~GET_CRBPX.

***--------------------------------------------------------------------------------------
*** Get Corrugator Backward Property (Outbound)
    RR_BL_CRBPROP = ME->GET_CRBPX( ).

***--------------------------------------------------------------------------------------
*** Get Corrugator Backward Property Instance
    if EF_CRBPX is requested.
      move RR_BL_CRBPROP->GET_CRBPX( ) to EF_CRBPX.
    endif.

  endmethod.
  method ZIF_VCXI_P1SS_COMM_PREV~GET_CRFPX.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

***--------------------------------------------------------------------------------------
*** Get Parameter
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_CRFPX ).
    move LS_PARAM-STRNG to EF_CRFPX.
    move LS_PARAM-SETBY to EF_SETBY.

***--------------------------------------------------------------------------------------
*** Get Corrugator Forward Property Instance
    if RR_BL_CRFPROP is requested.
      RR_BL_CRFPROP = ZCL_VCXI_P1SS_BL_CRFPROP=>FACTORY( IF_CRFPX = EF_CRFPX ).
    endif.

  endmethod.
  method ZIF_VCXI_P1SS_COMM_PREV~GET_NOUPS.

*** Get Number of Ups (Outbound)
    RF_NOUPS = ME->GET_NOUPS( importing EF_SETBY = EF_SETBY ).

  endmethod.
  method ZIF_VCXI_P1SS_COMM_PREV~GET_SHTDIM.

    ME->ZIF_VCXI_P1SS_COMM_PREV~GET_CRBPX( )->GET_SHTDIM( importing ES_SHTLE = ES_SHTLE
                                                                    ES_SHTWI = ES_SHTWI ).

  endmethod.
  method ZIF_VCXI_P1SS_COMM_PREV~GET_SURWT.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SURWT ).
    move LS_PARAM-ATFLV to RS_SURWT-SURWT.
    move LS_PARAM-UNIT  to RS_SURWT-SURWT_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method ZIF_VCXI_PXSS_COMM_PREV~DO_FORWARD.

    data: LS_SURWT type ZVCXI_PXS_S_SURWT.
    data: LR_GATE            type ref to /VCXI/CL_VKSR_GATE,
          LR_COMM_NEXT       type ref to ZIF_VCXI_P1SS_COMM_NEXT,
          LR_BL_CRFPROP      type ref to ZCL_VCXI_P1SS_BL_CRFPROP,
          LR_BL_CRFPROP_NEXT type ref to ZCL_VCXI_P1SS_BL_CRFPROP.

***------------------------------------------------------------------------------------------------
*** Process all provided Gates
    loop at IT_GATE into LR_GATE.
      try.
          move LR_GATE->R_CLSPR ?to LR_COMM_NEXT.
        catch CX_SY_MOVE_CAST_ERROR.
          clear LR_COMM_NEXT.
      endtry.
      check LR_COMM_NEXT is bound.

***   Get Surface Weight
      LS_SURWT = LR_COMM_NEXT->GET_SURWT( ).

***   Combine Film Forward Property
      LR_BL_CRFPROP_NEXT = LR_COMM_NEXT->GET_CRFPX( ).
      if LR_BL_CRFPROP is not bound.
        move LR_BL_CRFPROP_NEXT to LR_BL_CRFPROP.
      else.
        LR_BL_CRFPROP->MERGE( IR_BL_CRFPROP = LR_BL_CRFPROP_NEXT ).
      endif.
    endloop.

*** Forward Surface Weight
    ME->SET_SURWT( IS_SURWT = LS_SURWT ).

*** Forward Film Forward Property XML
    if LR_BL_CRFPROP is bound.
      ME->SET_CRFPX( IF_CRFPX = LR_BL_CRFPROP->GET_CRFPX( ) ).
    else.
      ME->SET_CRFPX( IF_CRFPX = SPACE ).
    endif.

  endmethod.
