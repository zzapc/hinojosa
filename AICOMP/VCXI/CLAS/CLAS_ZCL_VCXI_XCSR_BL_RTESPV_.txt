
class ZCL_VCXI_XCSR_BL_RTESPV definition
  public
  create public .

public section.

  methods ADD_PSCALE_MGR
    importing
      !IT_PSCALE type ZVCXI_XCSR_TT_PSCALE
      !IR_SPVRE_PARENT type ref to /VCXI/CL_VKSR_SPVRE
    changing
      !CT_SPVRE type /VCXI/VKSR_TT_SPVRE
    raising
      /VCXI/CX_CKX .
  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods EVALUATE_PRTE
    importing
      !IS_EVQTY type ZVCXI_XCSR_S_EVQTY
      !IT_BL_PRTE type ZVCXI_XCSR_TT_BL_PRTE
    raising
      /VCXI/CX_CKX .
  methods EVALUATE_TIME
    changing
      !CT_EVAL_TIME type ZVCXI_XCSR_TT_EVAL_TIME
    raising
      /VCXI/CX_CKX .
  methods EVALUATE_WASTE
    changing
      !CT_EVAL_WASTE type ZVCXI_XCSR_TT_EVAL_WASTE
    raising
      /VCXI/CX_CKX .
  methods GET_SPVCE
    importing
      !IF_ID type /VCXI/CKX_ID
      !IR_SPVCE_PARENT type ref to /VCXI/CL_VKSC_SPVCE
    returning
      value(RR_SPVCE) type ref to /VCXI/CL_VKSC_SPVCE
    raising
      /VCXI/CX_CKX .
  methods SET_BL_PRTE
    importing
      !IR_BL_PRTE type ref to ZCL_VCXI_XCSR_BL_PRTE
      !IS_EXQTY type /VCXI/VKCR_S_EXQTY
    raising
      /VCXI/CX_CKX .
protected section.

  data T_SPVCE type /VCXI/CKX_TT_OBJECT .
  data R_GATE type ref to /VCXI/CL_VKSR_GATE .

  methods GET_DATE
    returning
      value(RF_DATE) type DATS .
  methods EVAL_WASTE_CMACH
    changing
      !CS_EVAL_WASTE type ZVCXI_XCSR_S_EVAL_WASTE
    raising
      /VCXI/CX_CKX .
  methods ADD_CSPLIT_MGR
    importing
      !IR_BL_PRTE type ref to ZCL_VCXI_XCSR_BL_PRTE
      !IR_SPVRE_ARBPL type ref to /VCXI/CL_VKSR_SPVRE
    changing
      !CT_SPVRE type /VCXI/VKSR_TT_SPVRE
    raising
      /VCXI/CX_CKX .
  methods ADD_HELPER
    importing
      !IR_RTE_ARBPL type ref to /VCXI/CL_VSMR_RTE_ARBPL
      !IR_SPVRE_ARBPL type ref to /VCXI/CL_VKSR_SPVRE
    changing
      !CT_SPVRE type /VCXI/VKSR_TT_SPVRE
    raising
      /VCXI/CX_CKX .
  methods ADD_MACHINE
    importing
      !IR_RTE_ARBPL type ref to /VCXI/CL_VSMR_RTE_ARBPL
      !IF_ID type /VCXI/CKX_ID
      !IR_SPVRE_GRP type ref to /VCXI/CL_VKSR_SPVRE
      !IR_BL_PRTE type ref to ZCL_VCXI_XCSR_BL_PRTE
    exporting
      !ER_SPVRE_MACHINE type ref to /VCXI/CL_VKSR_SPVRE
    changing
      !CT_SPVRE type /VCXI/VKSR_TT_SPVRE
    raising
      /VCXI/CX_CKX .
  methods ADD_MANU
    importing
      !IR_RTE_ARBPL type ref to /VCXI/CL_VSMR_RTE_ARBPL
      !IF_ID type /VCXI/CKX_ID
      !IR_SPVRE_GRP type ref to /VCXI/CL_VKSR_SPVRE
    exporting
      !ER_SPVRE_MANU type ref to /VCXI/CL_VKSR_SPVRE
    changing
      !CT_SPVRE type /VCXI/VKSR_TT_SPVRE
    raising
      /VCXI/CX_CKX .
  methods ADD_MFMGR
    importing
      !IR_RTE_ARBPL type ref to /VCXI/CL_VSMR_RTE_ARBPL
      !IR_SPVRE_ARBPL type ref to /VCXI/CL_VKSR_SPVRE
      !IR_BL_PRTE type ref to ZCL_VCXI_XCSR_BL_PRTE
      !IF_ID_MFSI type /VCXI/CKX_ID default 'MFSI_P'
    exporting
      !ER_SPVRE_MFMGR type ref to /VCXI/CL_VKSR_SPVRE
    changing
      !CT_SPVRE type /VCXI/VKSR_TT_SPVRE
    raising
      /VCXI/CX_CKX .
  methods ADD_OUTSO
    importing
      !IR_RTE_ARBPL type ref to /VCXI/CL_VSMR_RTE_ARBPL
      !IF_ID type /VCXI/CKX_ID
      !IR_SPVRE_GRP type ref to /VCXI/CL_VKSR_SPVRE
    exporting
      !ER_SPVRE_OUTSO type ref to /VCXI/CL_VKSR_SPVRE
    changing
      !CT_SPVRE type /VCXI/VKSR_TT_SPVRE
    raising
      /VCXI/CX_CKX .
  methods BUILD_STRUCT
    importing
      !IR_BL_PRTE type ref to ZCL_VCXI_XCSR_BL_PRTE
      !IS_EXQTY type /VCXI/VKCR_S_EXQTY
    exporting
      !ER_SPVRE_ROOT type ref to /VCXI/CL_VKSR_SPVRE
    changing
      !CT_SPVRE type /VCXI/VKSR_TT_SPVRE
    raising
      /VCXI/CX_CKX .
  methods EVAL_TIME_CMACH
    changing
      !CS_EVAL_TIME type ZVCXI_XCSR_S_EVAL_TIME
    raising
      /VCXI/CX_CKX .
  methods FIND_SPVRE
    importing
      !IR_SPVRE_PARENT type ref to /VCXI/CL_VKSR_SPVRE
      !IF_ID type /VCXI/CKX_ID
      !IT_SPVRE type /VCXI/VKSR_TT_SPVRE
    returning
      value(RR_SPVRE) type ref to /VCXI/CL_VKSR_SPVRE
    raising
      /VCXI/CX_CKX .
  methods GET_ARBPL_INFO
    importing
      !IR_SPVRE_ROOT type ref to /VCXI/CL_VKSR_SPVRE
      !IR_RTE_ARBPL type ref to /VCXI/CL_VSMR_RTE_ARBPL
      !IF_DATE type DATS
    exporting
      value(ER_SPVRE_GRP) type ref to /VCXI/CL_VKSR_SPVRE
      value(EF_ID_ARBPL) type /VCXI/CKX_ID
    changing
      !CT_SPVRE type /VCXI/VKSR_TT_SPVRE
    raising
      /VCXI/CX_CKX .
  methods GET_SPVRE
    importing
      !IR_SPVRE_PARENT type ref to /VCXI/CL_VKSR_SPVRE
      !IF_ID type /VCXI/CKX_ID
    exporting
      !ER_SPVRE type ref to /VCXI/CL_VKSR_SPVRE
    changing
      !CT_SPVRE type /VCXI/VKSR_TT_SPVRE
    raising
      /VCXI/CX_CKX .
  methods PROCESS_RTE_ARBPL
    importing
      !IR_SPVRE_ROOT type ref to /VCXI/CL_VKSR_SPVRE
      !IR_RTE_ARBPL type ref to /VCXI/CL_VSMR_RTE_ARBPL
      !IR_BL_PRTE type ref to ZCL_VCXI_XCSR_BL_PRTE
    exporting
      !ER_SPVRE_ARBPL type ref to /VCXI/CL_VKSR_SPVRE
    changing
      !CT_SPVRE type /VCXI/VKSR_TT_SPVRE
    raising
      /VCXI/CX_CKX .
  methods PROCESS_RTE_PSTEP
    importing
      !IR_RTE_PSTEP type ref to /VCXI/CL_VSMR_RTE_PSTEP
      !IR_SPVRE_ARBPL type ref to /VCXI/CL_VKSR_SPVRE
      !IR_ARBPL type ref to /VCXI/CL_VSMC_ARBPL
    changing
      !CT_SPVRE type /VCXI/VKSR_TT_SPVRE
    raising
      /VCXI/CX_CKX .
  methods SET_VALUE_PSCALE_A
    importing
      !IR_SPVRE_PSCALE type ref to /VCXI/CL_VKSR_SPVRE
      !IS_PSCALE type ZVCXI_XCSR_S_PSCALE
    raising
      /VCXI/CX_CKX .
  methods SET_VALUE_PSCALE_L
    importing
      !IR_SPVRE_PSCALE type ref to /VCXI/CL_VKSR_SPVRE
      !IS_PSCALE type ZVCXI_XCSR_S_PSCALE
    raising
      /VCXI/CX_CKX .
  methods SET_VALUE_PSCALE_P
    importing
      !IR_SPVRE_PSCALE type ref to /VCXI/CL_VKSR_SPVRE
      !IS_PSCALE type ZVCXI_XCSR_S_PSCALE
    raising
      /VCXI/CX_CKX .
  methods SET_VALUE_PSCALE_W
    importing
      !IR_SPVRE_PSCALE type ref to /VCXI/CL_VKSR_SPVRE
      !IS_PSCALE type ZVCXI_XCSR_S_PSCALE
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_XCSR_BL_RTESPV definition
class ZCL_VCXI_XCSR_BL_RTESPV implementation.
method ADD_CSPLIT_MGR.

  data:  LS_CSPLIT            type        ZVCXI_XCSR_S_CSPLIT,
         LS_LEOKY             type        /VCXI/VKSR_S_LEOKY,
         LS_PARAM             type        /VCXI/VKSR_S_PARAM.
  data:  LR_SPVCE_CSPLIT_MGR  type ref to /VCXI/CL_VKSC_SPVCE,
         LR_SPVRE_CSPLIT_MGR  type ref to /VCXI/CL_VKSR_SPVRE,
         LR_SPVCE_CSPLIT      type ref to /VCXI/CL_VKSC_SPVCE,
         LR_SPVRE_CSPLIT      type ref to /VCXI/CL_VKSR_SPVRE.

  check IR_BL_PRTE->T_CSPLIT is not initial.

***----------------------------------------------------------------------------
*** Get Customizing Element for Cost Split Manager
  LR_SPVCE_CSPLIT_MGR = ME->GET_SPVCE( IF_ID           = 'CSPLIT_MGR'
                                       IR_SPVCE_PARENT = IR_SPVRE_ARBPL->R_SPVCE ).

*** Create Supervisor Runtime Element
  create object LR_SPVRE_CSPLIT_MGR
    exporting
      IR_SPVCE  = LR_SPVCE_CSPLIT_MGR
      IR_PARENT = IR_SPVRE_ARBPL.
  append LR_SPVRE_CSPLIT_MGR to CT_SPVRE.

*** Set in Use
  LR_SPVRE_CSPLIT_MGR->SET_INUSE( IF_INUSE = ABAP_TRUE ).


***----------------------------------------------------------------------------
  loop at IR_BL_PRTE->T_CSPLIT into LS_CSPLIT.
    if LR_SPVCE_CSPLIT is not bound.
***   Get Customizing Element for Cost Split
      LR_SPVCE_CSPLIT = ME->GET_SPVCE( IF_ID           = 'CSPLIT'
                                       IR_SPVCE_PARENT = LR_SPVRE_CSPLIT_MGR->R_SPVCE ).
    endif.

*** Create Supervisor Runtime Element
    create object LR_SPVRE_CSPLIT
      exporting
        IR_SPVCE  = LR_SPVCE_CSPLIT
        IR_PARENT = LR_SPVRE_CSPLIT_MGR.
    append LR_SPVRE_CSPLIT to CT_SPVRE.

*** Set in Use
    LR_SPVRE_CSPLIT->SET_INUSE( IF_INUSE = ABAP_TRUE ).

*** Set ID of Cost Split
    move LS_CSPLIT-ICSPL                             to LS_LEOKY-LEOKY.
    move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_LEOKY-SETBY.
    LR_SPVRE_CSPLIT->SET_LEOKY( IS_LEOKY = LS_LEOKY ).

*** Set Distribution Key
    clear LS_PARAM.
    move ZCL_VCXI_XCSP_PR_CSPLIT=>C_PARID_DCSPL      to LS_PARAM-PARID.
    move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC           to LS_PARAM-PARTY.
    move LS_CSPLIT-DCSPL                             to LS_PARAM-ATFLV.
    move LS_CSPLIT-DCSPL_UOM                         to LS_PARAM-UNIT.
    move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
    LR_SPVRE_CSPLIT->SET_PARAM( IS_PARAM = LS_PARAM ).

*** Set Component Ratio
    clear LS_PARAM.
    move ZCL_VCXI_XCSP_PR_CSPLIT=>C_PARID_COMPR      to LS_PARAM-PARID.
    move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC           to LS_PARAM-PARTY.
    move LS_CSPLIT-COMPR                             to LS_PARAM-ATFLV.
    move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
    LR_SPVRE_CSPLIT->SET_PARAM( IS_PARAM = LS_PARAM ).

*** Set Quantity Factor
    clear LS_PARAM.
    move ZCL_VCXI_XCSP_PR_CSPLIT=>C_PARID_QFACT      to LS_PARAM-PARID.
    move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC           to LS_PARAM-PARTY.
    move LS_CSPLIT-QFACT                             to LS_PARAM-ATFLV.
    move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
    LR_SPVRE_CSPLIT->SET_PARAM( IS_PARAM = LS_PARAM ).

  endloop.

endmethod.
method ADD_HELPER.

  data:  LS_HELPER        type        ZVCXI_XCSR_S_HELPER,
         LS_PARAM         type        /VCXI/VKSR_S_PARAM.
  data:  LR_SPVCE         type ref to /VCXI/CL_VKSC_SPVCE,
         LR_RTE_PSTEP     type ref to /VCXI/CL_VSMR_RTE_PSTEP,
         LR_BL_PSTEP      type ref to ZCL_VCXI_XCSR_BL_PSTEP,
         LR_SPVRE_HELPER  type ref to /VCXI/CL_VKSR_SPVRE.

***----------------------------------------------------------------------------
  loop at IR_RTE_ARBPL->T_RTE_PSTEP into LR_RTE_PSTEP.
    move LR_RTE_PSTEP->R_BLOBJ ?to LR_BL_PSTEP.

    loop at LR_BL_PSTEP->T_HELPER into LS_HELPER
                                 where HELPN is not initial.

      if LR_SPVCE is not bound.
***     Get Customizing Element for Helper
        LR_SPVCE = ME->GET_SPVCE( IF_ID           = 'HELPER'
                                  IR_SPVCE_PARENT = IR_SPVRE_ARBPL->R_SPVCE ).
      endif.

***   Create Supervisor Runtime Element
      create object LR_SPVRE_HELPER
        exporting
          IR_SPVCE  = LR_SPVCE
          IR_PARENT = IR_SPVRE_ARBPL.
      append LR_SPVRE_HELPER to CT_SPVRE.

***   Set in Use
      LR_SPVRE_HELPER->SET_INUSE( IF_INUSE = ABAP_TRUE ).

***------------------------------------------------------------------------------------------------
***   Set Number of Helper
      clear LS_PARAM.
      move ZCL_VCXI_XCSP_PR_HELPER=>C_PARID_HELPN      to LS_PARAM-PARID.
      move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC           to LS_PARAM-PARTY.
      move LS_HELPER-HELPN                             to LS_PARAM-ATFLV.
      move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
      LR_SPVRE_HELPER->SET_PARAM( IS_PARAM = LS_PARAM ).

***   Set Text
      clear LS_PARAM.
      move ZCL_VCXI_XCSP_PR_HELPER=>C_PARID_TEXT       to LS_PARAM-PARID.
      move /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR           to LS_PARAM-PARTY.
      move LS_HELPER-HELPT                             to LS_PARAM-ATWRT.
      move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
      LR_SPVRE_HELPER->SET_PARAM( IS_PARAM = LS_PARAM ).

    endloop.
  endloop.

endmethod.
  method ADD_MACHINE.

    data: LF_STRNG   type /VCXI/CKX_STRING.
    data: LT_FEATURE type /VCXI/CKI_TT_FEATURE.
    data: LR_SPVCE   type ref to /VCXI/CL_VKSC_SPVCE.


*** Get Customizing Element for Work Center
    LR_SPVCE = ME->GET_SPVCE( IF_ID           = IF_ID
                              IR_SPVCE_PARENT = IR_SPVRE_GRP->R_SPVCE ).

*** Create Supervisor Runtime Element
    create object ER_SPVRE_MACHINE
      exporting
        IR_SPVCE  = LR_SPVCE
        IR_PARENT = IR_SPVRE_GRP.
    append ER_SPVRE_MACHINE to CT_SPVRE.

*** Set In Use
    ER_SPVRE_MACHINE->SET_INUSE( IF_INUSE = ABAP_TRUE ).

*** Set Leading Object
    ER_SPVRE_MACHINE->SET_LEOKY( IS_LEOKY = value #( LEOKY = IR_RTE_ARBPL->R_ARBPL->/VCXI/IF_CKX_OBJECT~GET_GUID( )
                                                     SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).

*** Get IQ.catalyst Feature stored on Machine Level
    LT_FEATURE = IR_BL_PRTE->GET_IQCFT( IR_RTE_ARBPL = IR_RTE_ARBPL ).
    if LT_FEATURE is not initial.
      call transformation ID
           source T_FEATURE = LT_FEATURE
           result xml LF_STRNG.
      ER_SPVRE_MACHINE->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_XCSP_PR_MACHINE=>C_PARID_IQCFT
                                                       PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_STRG
                                                       STRNG = LF_STRNG
                                                       SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).
    endif.

  endmethod.
method ADD_MANU.

  data:  LS_LEOKY          type        /VCXI/VKSR_S_LEOKY,
         LS_PARAM          type        /VCXI/VKSR_S_PARAM.
  data:  LR_SPVCE          type ref to /VCXI/CL_VKSC_SPVCE,
         LR_RTE_PSTEP      type ref to /VCXI/CL_VSMR_RTE_PSTEP,
         LR_BL_MANU        type ref to ZIF_VCXI_XCSR_BL_MANU.

***------------------------------------------------------------------------------------------------
*** Get Customizing Element for Work Center
  LR_SPVCE = ME->GET_SPVCE( IF_ID           = IF_ID
                            IR_SPVCE_PARENT = IR_SPVRE_GRP->R_SPVCE ).

*** Create Supervisor Runtime Element
  create object ER_SPVRE_MANU
    exporting
      IR_SPVCE  = LR_SPVCE
      IR_PARENT = IR_SPVRE_GRP.
  append ER_SPVRE_MANU to CT_SPVRE.

*** Set In Use
  ER_SPVRE_MANU->SET_INUSE( IF_INUSE = ABAP_TRUE ).

***------------------------------------------------------------------------------------------------
*** Set Leading Object
  move IR_RTE_ARBPL->R_ARBPL->/VCXI/IF_CKX_OBJECT~GET_GUID( ) to LS_LEOKY-LEOKY.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC                       to LS_LEOKY-SETBY.
  ER_SPVRE_MANU->SET_LEOKY( IS_LEOKY = LS_LEOKY ).

***------------------------------------------------------------------------------------------------
  read table IR_RTE_ARBPL->T_RTE_PSTEP into LR_RTE_PSTEP
                                       index 1.
  check SY-SUBRC eq 0.
  try.
      move LR_RTE_PSTEP->R_BLOBJ ?to LR_BL_MANU.
    catch CX_SY_MOVE_CAST_ERROR.
      clear LR_BL_MANU.
  endtry.
  check LR_BL_MANU is bound.

***------------------------------------------------------------------------------------------------
*** Set Text
  clear LS_PARAM.
  move ZCL_VCXI_XCSP_PR_MANU=>C_PARID_TEXT         to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR           to LS_PARAM-PARTY.
  move LR_BL_MANU->F_TEXT                          to LS_PARAM-ATWRT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
  ER_SPVRE_MANU->SET_PARAM( IS_PARAM = LS_PARAM ).

*** Set Run Speed Rate
  clear LS_PARAM.
  move LR_BL_MANU->F_PARID_MSRSR                   to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC           to LS_PARAM-PARTY.
  move LR_BL_MANU->S_MSRS-MSRSR                    to LS_PARAM-ATFLV.
  move LR_BL_MANU->S_MSRS-MSRSR_UOM                to LS_PARAM-UNIT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
  ER_SPVRE_MANU->SET_PARAM( IS_PARAM = LS_PARAM ).

*** Set Run Speed Time
  clear LS_PARAM.
  move ZCL_VCXI_XCSP_PR_MANU=>C_PARID_MSRST        to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC           to LS_PARAM-PARTY.
  move LR_BL_MANU->S_MSRS-MSRST                    to LS_PARAM-ATFLV.
  move LR_BL_MANU->S_MSRS-MSRST_UOM                to LS_PARAM-UNIT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
  ER_SPVRE_MANU->SET_PARAM( IS_PARAM = LS_PARAM ).

*** Scrap
  clear LS_PARAM.
  move ZCL_VCXI_XCSP_PR_MANU=>C_PARID_MSRWF        to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC           to LS_PARAM-PARTY.
  move LR_BL_MANU->S_SCRAP-SCRAP                   to LS_PARAM-ATFLV.
  move LR_BL_MANU->S_SCRAP-SCRAP_UOM               to LS_PARAM-UNIT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
  ER_SPVRE_MANU->SET_PARAM( IS_PARAM = LS_PARAM ).

endmethod.
method ADD_MFMGR.

  data:  LF_MFI           type        ABAP_BOOL,
         LF_MFO           type        ABAP_BOOL.
  data:  LT_SPVRE         type        /VCXI/VKSR_TT_SPVRE.
  data:  LR_SPVCE         type ref to /VCXI/CL_VKSC_SPVCE,
         LR_RTE_PSTEP     type ref to /VCXI/CL_VSMR_RTE_PSTEP,
         LR_BL_PSTEP      type ref to ZCL_VCXI_XCSR_BL_PSTEP,
         LR_SPVRE_MFSI    type ref to /VCXI/CL_VKSR_SPVRE.

***----------------------------------------------------------------------------
*** Get Customizing Element for Manager
  LR_SPVCE = ME->GET_SPVCE( IF_ID           = 'MFMGR'
                            IR_SPVCE_PARENT = IR_SPVRE_ARBPL->R_SPVCE ).

*** Create Supervisor Runtime Element
  create object ER_SPVRE_MFMGR
    exporting
      IR_SPVCE  = LR_SPVCE
      IR_PARENT = IR_SPVRE_ARBPL.
  append ER_SPVRE_MFMGR to CT_SPVRE.


***----------------------------------------------------------------------------
*** If first Operation
  read table IR_BL_PRTE->R_RTE_PRTE->T_RTE_ARBPL with key TABLE_LINE = IR_RTE_ARBPL
                                                 transporting no fields.
  if SY-TABIX eq 1.
    IR_BL_PRTE->GET_SPVRE_MFMI( exporting IR_BL_RTESPV   = ME
                                          IR_SPVRE_MFMGR = ER_SPVRE_MFMGR
                                changing  CT_SPVRE       = LT_SPVRE
                                          CF_MFI         = LF_MFI ).
  endif.

***----------------------------------------------------------------------------
  loop at IR_RTE_ARBPL->T_RTE_PSTEP into LR_RTE_PSTEP.
    move LR_RTE_PSTEP->R_BLOBJ ?to LR_BL_PSTEP.

    LR_BL_PSTEP->GET_SPVRE_MF( exporting IR_BL_RTESPV   = ME
                                         IR_SPVRE_MFMGR = ER_SPVRE_MFMGR
                               changing  CT_SPVRE       = LT_SPVRE
                                         CF_MFI         = LF_MFI
                                         CF_MFO         = LF_MFO ).
  endloop.
  append lines of LT_SPVRE to CT_SPVRE.

***----------------------------------------------------------------------------
  if LT_SPVRE is initial.
*** Get Customizing Element for Manager
    LR_SPVCE = ME->GET_SPVCE( IF_ID           = IF_ID_MFSI
                              IR_SPVCE_PARENT = ER_SPVRE_MFMGR->R_SPVCE ).

*** Create Supervisor Runtime Element
    create object LR_SPVRE_MFSI
      exporting
        IR_SPVCE  = LR_SPVCE
        IR_PARENT = ER_SPVRE_MFMGR.
    append LR_SPVRE_MFSI to CT_SPVRE.
    LR_SPVRE_MFSI->SET_INUSE( IF_INUSE = ABAP_TRUE ).
  endif.

endmethod.
method ADD_OUTSO.

  data:  LS_LEOKY          type        /VCXI/VKSR_S_LEOKY,
         LS_PARAM          type        /VCXI/VKSR_S_PARAM.
  data:  LR_SPVCE          type ref to /VCXI/CL_VKSC_SPVCE,
         LR_RTE_PSTEP      type ref to /VCXI/CL_VSMR_RTE_PSTEP,
         LR_BL_OUTSO       type ref to ZIF_VCXI_XCSR_BL_OUTSO.

***------------------------------------------------------------------------------------------------
*** Get Customizing Element for Work Center
  LR_SPVCE = ME->GET_SPVCE( IF_ID           = IF_ID
                            IR_SPVCE_PARENT = IR_SPVRE_GRP->R_SPVCE ).

*** Create Supervisor Runtime Element
  create object ER_SPVRE_OUTSO
    exporting
      IR_SPVCE  = LR_SPVCE
      IR_PARENT = IR_SPVRE_GRP.
  append ER_SPVRE_OUTSO to CT_SPVRE.

*** Set In Use
  ER_SPVRE_OUTSO->SET_INUSE( IF_INUSE = ABAP_TRUE ).

***------------------------------------------------------------------------------------------------
*** Set Leading Object
  move IR_RTE_ARBPL->R_ARBPL->/VCXI/IF_CKX_OBJECT~GET_GUID( ) to LS_LEOKY-LEOKY.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC                       to LS_LEOKY-SETBY.
  ER_SPVRE_OUTSO->SET_LEOKY( IS_LEOKY = LS_LEOKY ).

***------------------------------------------------------------------------------------------------
  read table IR_RTE_ARBPL->T_RTE_PSTEP into LR_RTE_PSTEP
                                       index 1.
  check SY-SUBRC eq 0.
  try.
      move LR_RTE_PSTEP->R_BLOBJ ?to LR_BL_OUTSO.
    catch CX_SY_MOVE_CAST_ERROR.
      clear LR_BL_OUTSO.
  endtry.
  check LR_BL_OUTSO is bound.

***------------------------------------------------------------------------------------------------
*** Set Text
  clear LS_PARAM.
  move ZCL_VCXI_XCSP_PR_OUTSO=>C_PARID_TEXT        to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR           to LS_PARAM-PARTY.
  move LR_BL_OUTSO->F_TEXT                         to LS_PARAM-ATWRT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
  ER_SPVRE_OUTSO->SET_PARAM( IS_PARAM = LS_PARAM ).

*** Vendor
  clear LS_PARAM.
  move ZCL_VCXI_XCSP_PR_OUTSO=>C_PARID_LIFNR       to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR           to LS_PARAM-PARTY.
  move LR_BL_OUTSO->F_LIFNR                        to LS_PARAM-ATWRT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
  ER_SPVRE_OUTSO->SET_PARAM( IS_PARAM = LS_PARAM ).

*** Set Price
  clear LS_PARAM.
  move ZCL_VCXI_XCSP_PR_OUTSO=>C_PARID_PRICE       to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC           to LS_PARAM-PARTY.
  move LR_BL_OUTSO->S_PRICE-PRICE                  to LS_PARAM-ATFLV.
  move LR_BL_OUTSO->S_PRICE-PRICE_CURR             to LS_PARAM-CURKY.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
  ER_SPVRE_OUTSO->SET_PARAM( IS_PARAM = LS_PARAM ).

*** Set Price Per
  clear LS_PARAM.
  move LR_BL_OUTSO->F_PARID_PRICP                  to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC           to LS_PARAM-PARTY.
  move LR_BL_OUTSO->S_PRICP-PRICP                  to LS_PARAM-ATFLV.
  move LR_BL_OUTSO->S_PRICP-PRICP_UOM              to LS_PARAM-UNIT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
  ER_SPVRE_OUTSO->SET_PARAM( IS_PARAM = LS_PARAM ).

*** Scrap
  clear LS_PARAM.
  move ZCL_VCXI_XCSP_PR_OUTSO=>C_PARID_SCRAP       to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC           to LS_PARAM-PARTY.
  move LR_BL_OUTSO->S_SCRAP-SCRAP                  to LS_PARAM-ATFLV.
  move LR_BL_OUTSO->S_SCRAP-SCRAP_UOM              to LS_PARAM-UNIT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
  ER_SPVRE_OUTSO->SET_PARAM( IS_PARAM = LS_PARAM ).

***------------------------------------------------------------------------------------------------
*** Add Price Scale
  if LR_BL_OUTSO->T_PSCALE is not initial.
    ME->ADD_PSCALE_MGR( exporting IR_SPVRE_PARENT = ER_SPVRE_OUTSO
                                  IT_PSCALE       = LR_BL_OUTSO->T_PSCALE
                        changing  CT_SPVRE        = CT_SPVRE ).
  endif.

endmethod.
method ADD_PSCALE_MGR.

  data:  LS_PSCALE           type        ZVCXI_XCSR_S_PSCALE.
  data:  LR_SPVCE_PSCALE_MGR type ref to /VCXI/CL_VKSC_SPVCE,
         LR_SPVCE_PSCALE     type ref to /VCXI/CL_VKSC_SPVCE,
         LR_SPVRE_PSCALE_MGR type ref to /VCXI/CL_VKSR_SPVRE,
         LR_SPVRE_PSCALE     type ref to /VCXI/CL_VKSR_SPVRE.

  check IT_PSCALE is not initial.

***------------------------------------------------------------------------------------------------
*** Get Customizing Element
  LR_SPVCE_PSCALE_MGR = ME->GET_SPVCE( IF_ID           = 'PSCALE_MGR'
                                       IR_SPVCE_PARENT = IR_SPVRE_PARENT->R_SPVCE ).

*** Create Supervisor Runtime Element
  create object LR_SPVRE_PSCALE_MGR
    exporting
      IR_SPVCE  = LR_SPVCE_PSCALE_MGR
      IR_PARENT = IR_SPVRE_PARENT.
  append LR_SPVRE_PSCALE_MGR to CT_SPVRE.

*** Set In Use
  LR_SPVRE_PSCALE_MGR->SET_INUSE( IF_INUSE = ABAP_TRUE ).

***------------------------------------------------------------------------------------------------
*** Add Scales
  loop at IT_PSCALE into LS_PSCALE.

***------------------------------------------------------------------------------------------------
*** Get Customizing Element
    LR_SPVCE_PSCALE = ME->GET_SPVCE( IF_ID           = LS_PSCALE-ISVCE
                                     IR_SPVCE_PARENT = LR_SPVRE_PSCALE_MGR->R_SPVCE ).

*** Create Supervisor Runtime Element
    create object LR_SPVRE_PSCALE
      exporting
        IR_SPVCE  = LR_SPVCE_PSCALE
        IR_PARENT = LR_SPVRE_PSCALE_MGR.
    append LR_SPVRE_PSCALE to CT_SPVRE.

*** Set In Use
    LR_SPVRE_PSCALE->SET_INUSE( IF_INUSE = ABAP_TRUE ).

***------------------------------------------------------------------------------------------------
*** Set specific Price Scale Data
    case LS_PSCALE-ISVCE.
      when 'PSCALE_A'.
        ME->SET_VALUE_PSCALE_A( IR_SPVRE_PSCALE = LR_SPVRE_PSCALE
                                IS_PSCALE       = LS_PSCALE ).

      when 'PSCALE_L'.
        ME->SET_VALUE_PSCALE_L( IR_SPVRE_PSCALE = LR_SPVRE_PSCALE
                                IS_PSCALE       = LS_PSCALE ).

      when 'PSCALE_P'.
        ME->SET_VALUE_PSCALE_P( IR_SPVRE_PSCALE = LR_SPVRE_PSCALE
                                IS_PSCALE       = LS_PSCALE ).

      when 'PSCALE_W'.
        ME->SET_VALUE_PSCALE_W( IR_SPVRE_PSCALE = LR_SPVRE_PSCALE
                                IS_PSCALE       = LS_PSCALE ).

    endcase.

  endloop.

endmethod.
method BUILD_STRUCT.

  data:  LS_PARAM          type        /VCXI/VKSR_S_PARAM.
  data:  LT_SPVRE_CURR     type        /VCXI/VKSR_TT_SPVRE.

***----------------------------------------------------------------------------
*** Get Supervised Runtime Elements
  LT_SPVRE_CURR = ME->R_GATE->GET_SPVRE( ).

*** Find Root
  loop at LT_SPVRE_CURR into ER_SPVRE_ROOT
                       where TABLE_LINE->R_PARENT is initial.
    append ER_SPVRE_ROOT to CT_SPVRE.

*** Update Explosion Quantity
    clear LS_PARAM.
    move ZCL_VCXI_XCSP_PR_ORGLC=>C_PARID_EXQTY_P     to LS_PARAM-PARID.
    move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC           to LS_PARAM-PARTY.
    move IS_EXQTY-EXQTY                              to LS_PARAM-ATFLV.
    move IS_EXQTY-EXQTY_UOM                          to LS_PARAM-UNIT.
    move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
    ER_SPVRE_ROOT->SET_PARAM( IS_PARAM = LS_PARAM ).
  endloop.

endmethod.
method CONSTRUCTOR.

  move IR_GATE to ME->R_GATE.

*** Get Supervised Customizing Elements
  ME->T_SPVCE = ME->R_GATE->GET_SPVCE( ).

endmethod.
  method EVAL_TIME_CMACH.

    data: LT_ADDON_MSSTI type /VCXI/VSMR_TT_ADDON_MSSTI,
          LT_ADDON_MSRSF type /VCXI/VSMR_TT_ADDON_MSRSF,
          LT_ADDON_MSTTI type /VCXI/VSMR_TT_ADDON_MSTTI.
    data: LR_RTE_CADDON type ref to /VCXI/CL_VSMR_RTE_CADDON.


    check CS_EVAL_TIME-R_RTE_CMACH is bound.

***----------------------------------------------------------------------------
*** Get Values by Add-on
    loop at CS_EVAL_TIME-T_RTE_ATYPE into data(LS_RTE_ATYPE).
***   Get Add-on Customizing
      LR_RTE_CADDON = /VCXI/CL_VSMR_RTE_CADDON=>GET_INSTANCE( IR_RTE_CMACH = CS_EVAL_TIME-R_RTE_CMACH
                                                              IR_ATYPE     = LS_RTE_ATYPE-R_ATYPE ).

***   Get Add-on Setup Time
      append value #( IATYP   = LS_RTE_ATYPE-R_ATYPE->/VCXI/IF_CKX_OBJECT~GET_ID( )
                      S_MSSTI = LR_RTE_CADDON->GET_MSSTI( IT_RTE_SAREC = LS_RTE_ATYPE-T_RTE_SAREC
                                                          IT_RTE_SAVAL = LS_RTE_ATYPE-T_RTE_SAVAL )
                    ) to LT_ADDON_MSSTI.

***   Get Add-on Slowdown Factor
      append value #( IATYP   = LS_RTE_ATYPE-R_ATYPE->/VCXI/IF_CKX_OBJECT~GET_ID( )
                      S_MSRSF = LR_RTE_CADDON->GET_MSRSF( IS_OPQTY     = value #( OPQTY     = CS_EVAL_TIME-S_QTY-QTY
                                                                                  OPQTY_UOM = CS_EVAL_TIME-S_QTY-QTY_UOM )
                                                          IT_RTE_SAREC = LS_RTE_ATYPE-T_RTE_SAREC
                                                          IT_RTE_SAVAL = LS_RTE_ATYPE-T_RTE_SAVAL )
                    ) to LT_ADDON_MSRSF.

***   Get Add-on Teardown Time
      append value #( IATYP   = LS_RTE_ATYPE-R_ATYPE->/VCXI/IF_CKX_OBJECT~GET_ID( )
                      S_MSTTI = LR_RTE_CADDON->GET_MSTTI( IT_RTE_SAREC = LS_RTE_ATYPE-T_RTE_SAREC
                                                          IT_RTE_SAVAL = LS_RTE_ATYPE-T_RTE_SAVAL )
                    ) to LT_ADDON_MSTTI.
    endloop.

***----------------------------------------------------------------------------
*** Get Machine Setup Time
    CS_EVAL_TIME-S_MSSTI = CS_EVAL_TIME-R_RTE_CMACH->GET_MSSTI( IT_RTE_SMVAL   = CS_EVAL_TIME-T_RTE_SMVAL
                                                                IT_ADDON_MSSTI = LT_ADDON_MSSTI
                                                                IT_FEATURE     = CS_EVAL_TIME-T_FEATURE ).

*** Get Slowdown Factor
    CS_EVAL_TIME-S_MSRSF = CS_EVAL_TIME-R_RTE_CMACH->GET_MSRSF( IS_OPQTY       = value #( OPQTY     = CS_EVAL_TIME-S_QTY-QTY
                                                                                          OPQTY_UOM = CS_EVAL_TIME-S_QTY-QTY_UOM )
                                                                IT_RTE_SMVAL   = CS_EVAL_TIME-T_RTE_SMVAL
                                                                IT_ADDON_MSRSF = LT_ADDON_MSRSF ).

*** Get Machine Speed (incl. Slowdown Factor)
    CS_EVAL_TIME-S_MSRS = CS_EVAL_TIME-R_RTE_CMACH->GET_MSRS( IS_OPQTY   = value #( OPQTY     = CS_EVAL_TIME-S_QTY-QTY
                                                                                    OPQTY_UOM = CS_EVAL_TIME-S_QTY-QTY_UOM )
                                                              IS_MSRSF   = CS_EVAL_TIME-S_MSRSF
                                                              IF_RRVAL   = CS_EVAL_TIME-RRVAL
                                                              IT_FEATURE = CS_EVAL_TIME-T_FEATURE ).

*** Get Machine Teardown Time
    CS_EVAL_TIME-S_MSTTI = CS_EVAL_TIME-R_RTE_CMACH->GET_MSTTI( IT_RTE_SMVAL   = CS_EVAL_TIME-T_RTE_SMVAL
                                                                IT_ADDON_MSTTI = LT_ADDON_MSTTI
                                                                IT_FEATURE     = CS_EVAL_TIME-T_FEATURE ).

  endmethod.
  method EVAL_WASTE_CMACH.

    data: LT_ADDON_MSSWA type /VCXI/VSMR_TT_ADDON_MSSWA,
          LT_ADDON_MSRWF type /VCXI/VSMR_TT_ADDON_MSRWF.
    data: LR_RTE_CADDON type ref to /VCXI/CL_VSMR_RTE_CADDON.


***----------------------------------------------------------------------------
*** Get Values by Add-on
    loop at CS_EVAL_WASTE-T_RTE_ATYPE into data(LS_RTE_ATYPE).
***   Get Add-on Customizing
      LR_RTE_CADDON = /VCXI/CL_VSMR_RTE_CADDON=>GET_INSTANCE( IR_RTE_CMACH = CS_EVAL_WASTE-R_RTE_CMACH
                                                              IR_ATYPE     = LS_RTE_ATYPE-R_ATYPE ).

***   Get Add-on Setup Waste
      append value #( IATYP   = LS_RTE_ATYPE-R_ATYPE->/VCXI/IF_CKX_OBJECT~GET_ID( )
                      S_MSSWA = LR_RTE_CADDON->GET_MSSWA( IT_RTE_SAREC = LS_RTE_ATYPE-T_RTE_SAREC
                                                          IT_RTE_SAVAL = LS_RTE_ATYPE-T_RTE_SAVAL )
                    ) to LT_ADDON_MSSWA.

***   Get Add-on Waste Factor
      append value #( IATYP   = LS_RTE_ATYPE-R_ATYPE->/VCXI/IF_CKX_OBJECT~GET_ID( )
                      S_MSRWF = LR_RTE_CADDON->GET_MSRWF( IS_OPQTY     = value #( OPQTY     = CS_EVAL_WASTE-S_QTY-QTY
                                                                                  OPQTY_UOM = CS_EVAL_WASTE-S_QTY-QTY_UOM )
                                                          IT_RTE_SAREC = LS_RTE_ATYPE-T_RTE_SAREC
                                                          IT_RTE_SAVAL = LS_RTE_ATYPE-T_RTE_SAVAL )
                    ) to LT_ADDON_MSRWF.
    endloop.

***----------------------------------------------------------------------------
*** Get Machine Setup Waste
    CS_EVAL_WASTE-S_MSSWA = CS_EVAL_WASTE-R_RTE_CMACH->GET_MSSWA( IT_RTE_SMVAL   = CS_EVAL_WASTE-T_RTE_SMVAL
                                                                  IT_ADDON_MSSWA = LT_ADDON_MSSWA ).

*** Get Waste Factor
    CS_EVAL_WASTE-S_MSRWF = CS_EVAL_WASTE-R_RTE_CMACH->GET_MSRWF( IS_OPQTY       = value #( OPQTY     = CS_EVAL_WASTE-S_QTY-QTY
                                                                                            OPQTY_UOM = CS_EVAL_WASTE-S_QTY-QTY_UOM )
                                                                  IT_RTE_SMVAL   = CS_EVAL_WASTE-T_RTE_SMVAL
                                                                  IT_ADDON_MSRWF = LT_ADDON_MSRWF ).

*** Get Run Waste
    CS_EVAL_WASTE-S_MSRWA = CS_EVAL_WASTE-R_RTE_CMACH->GET_MSRWA( IS_OPQTY   = value #( OPQTY     = CS_EVAL_WASTE-S_QTY-QTY
                                                                                        OPQTY_UOM = CS_EVAL_WASTE-S_QTY-QTY_UOM )
                                                                  IS_MSRWF   = CS_EVAL_WASTE-S_MSRWF
                                                                  IT_FEATURE = CS_EVAL_WASTE-T_FEATURE ).

  endmethod.
  method EVALUATE_PRTE.

    data: LF_LINES     type I,
          LF_MAX_ARBPL type I.
    data: LS_CALCV type ZVCXI_XCS_S_CALCV.
    data: LT_EVAL_WASTE type ZVCXI_XCSR_TT_EVAL_WASTE,
          LT_EVAL_TIME  type ZVCXI_XCSR_TT_EVAL_TIME.
    data: LR_BL_PRTE   type ref to ZCL_VCXI_XCSR_BL_PRTE,
          LR_RTE_ARBPL type ref to /VCXI/CL_VSMR_RTE_ARBPL.


*** Get maximum number of Work Centers per Route
    loop at IT_BL_PRTE into LR_BL_PRTE.
      LF_LINES = LINES( LR_BL_PRTE->R_RTE_PRTE->T_RTE_ARBPL ).
      LF_MAX_ARBPL = cond #( when LF_LINES gt LF_MAX_ARBPL then LF_LINES else LF_MAX_ARBPL ).

***   Reset Evaluation Values
      LR_BL_PRTE->INIT_EVALUATION( ).
    endloop.

***--------------------------------------------------------------------------------------
*** Waste Calculation
    do LF_MAX_ARBPL times.
      data(LF_INDEX) = SY-INDEX - 1. "Offset from last Operation

      free: LT_EVAL_WASTE.  "Refresh Waste Buffer, but keep Time as it's processed outside the DO-loop.

      loop at IT_BL_PRTE into LR_BL_PRTE.
***     Get Work Center based on offset from last operation (backward processing)
        LF_LINES = LINES( LR_BL_PRTE->R_RTE_PRTE->T_RTE_ARBPL ).
        LR_RTE_ARBPL = value #( LR_BL_PRTE->R_RTE_PRTE->T_RTE_ARBPL[ LF_LINES - LF_INDEX ] optional ).
        check LR_RTE_ARBPL is bound.

***     Convert to for last Operation
        if LF_INDEX = 0.
          LS_CALCV = value #( CALCV     = IS_EVQTY-EVQTY
                              CALCV_UOM = IS_EVQTY-EVQTY_UOM ).
          LR_BL_PRTE->CONVERT_TO_MQTYO( exporting IR_RTE_ARBPL = LR_RTE_ARBPL
                                        changing  CS_CALCV     = LS_CALCV ).
        else.
***       Get Quantity of previous Operation
          LS_CALCV = corresponding #( LR_BL_PRTE->T_EVTRC[ 1 ]-S_MQTYI mapping CALCV     = MQTYI
                                                                               CALCV_UOM = MQTYI_UOM ).
        endif.

***     Prepare Waste Determination (Add entry to Evaluation Trace)
        LR_BL_PRTE->PREP_EVAL_WASTE( exporting IR_RTE_ARBPL  = LR_RTE_ARBPL
                                               IS_MQTYO      = value #( MQTYO     = LS_CALCV-CALCV
                                                                        MQTYO_UOM = LS_CALCV-CALCV_UOM )
                                      changing CT_EVAL_WASTE = LT_EVAL_WASTE ).
      endloop.

***   Evaluate/Determine Waste
      ME->EVALUATE_WASTE( changing CT_EVAL_WASTE = LT_EVAL_WASTE ).

***   Update Evaluation Trace
      loop at IT_BL_PRTE into LR_BL_PRTE.
***     Get Work Center based on offset from last operation (backward processing)
        LF_LINES = LINES( LR_BL_PRTE->R_RTE_PRTE->T_RTE_ARBPL ).
        LR_RTE_ARBPL = value #( LR_BL_PRTE->R_RTE_PRTE->T_RTE_ARBPL[ LF_LINES - LF_INDEX ] optional ).
        check LR_RTE_ARBPL is bound.

***     Update Trace Info based on Work Center and prepare Time Calculation
        LR_BL_PRTE->UPDATE_EVTRC_BY_WASTE( exporting IR_RTE_ARBPL  = LR_RTE_ARBPL
                                                     IT_EVAL_WASTE = LT_EVAL_WASTE
                                           changing  CT_EVAL_TIME  = LT_EVAL_TIME ).
      endloop.
    enddo.

***--------------------------------------------------------------------------------------
*** Evaluate/Determine Time
    ME->EVALUATE_TIME( changing CT_EVAL_TIME = LT_EVAL_TIME ).

*** Update Evaluation Trace
    loop at IT_BL_PRTE into LR_BL_PRTE.
      loop at LR_BL_PRTE->R_RTE_PRTE->T_RTE_ARBPL into LR_RTE_ARBPL.
        LR_BL_PRTE->UPDATE_EVTRC_BY_TIME( IR_RTE_ARBPL = LR_RTE_ARBPL
                                          IT_EVAL_TIME = LT_EVAL_TIME ).
      endloop.
    endloop.

  endmethod.
  method EVALUATE_TIME.

    data: LT_FEATURE_ST type /VCXI/CKI_TT_FEATURE,
          LT_FEATURE_RS type /VCXI/CKI_TT_FEATURE,
          LT_FEATURE_TT type /VCXI/CKI_TT_FEATURE.
    field-symbols: <S_EVAL_TIME>  type ZVCXI_XCSR_S_EVAL_TIME.


*** IQ.catalyst
    data(LR_IQC) = /VCXI/CL_CKI_IQC=>GET_INSTANCE( IF_DATE = ME->GET_DATE( ) ).

***------------------------------------------------------------------------------------------------
*** Prepare IQ.catalyst Bulk Call
    loop at CT_EVAL_TIME assigning <S_EVAL_TIME>
                         where R_RTE_CMACH is bound.

***   Filter provided Features to match requested
      <S_EVAL_TIME>-R_RTE_CMACH->FILTER_FEATURE( exporting IT_FEATURE    = <S_EVAL_TIME>-T_FEATURE
                                                 importing ET_FEATURE_ST = LT_FEATURE_ST
                                                           ET_FEATURE_RS = LT_FEATURE_RS
                                                           ET_FEATURE_TT = LT_FEATURE_TT ).

***   Add Request if Data Source is IQ.catalyst
      if <S_EVAL_TIME>-R_RTE_CMACH->R_MGRPV_SPEED->S_MDS-MDSST eq /VCXI/CL_VSMC_MGRPV_SPEED=>C_MDS_IQC.
        LR_IQC->ADD_QUERY_REQUEST( IS_CNTXT       = <S_EVAL_TIME>-R_RTE_CMACH->R_ARBPL->GET_IQC_CNTXT( )
                                   IF_IQCMT       = <S_EVAL_TIME>-R_RTE_CMACH->R_MTYPE->S_IMT-IMTST
                                   IT_FEATURE_INP = LT_FEATURE_ST ).
      endif.

      if <S_EVAL_TIME>-R_RTE_CMACH->R_MGRPV_SPEED->S_MDS-MDSRS eq /VCXI/CL_VSMC_MGRPV_SPEED=>C_MDS_IQC.
        LR_IQC->ADD_QUERY_REQUEST( IS_CNTXT       = <S_EVAL_TIME>-R_RTE_CMACH->R_ARBPL->GET_IQC_CNTXT( )
                                   IF_IQCMT       = <S_EVAL_TIME>-R_RTE_CMACH->R_MTYPE->S_IMT-IMTRS
                                   IT_FEATURE_INP = LT_FEATURE_RS ).
      endif.

      if <S_EVAL_TIME>-R_RTE_CMACH->R_MGRPV_SPEED->S_MDS-MDSTT eq /VCXI/CL_VSMC_MGRPV_SPEED=>C_MDS_IQC.
        LR_IQC->ADD_QUERY_REQUEST( IS_CNTXT       = <S_EVAL_TIME>-R_RTE_CMACH->R_ARBPL->GET_IQC_CNTXT( )
                                   IF_IQCMT       = <S_EVAL_TIME>-R_RTE_CMACH->R_MTYPE->S_IMT-IMTTT
                                   IT_FEATURE_INP = LT_FEATURE_TT ).
      endif.
    endloop.

***------------------------------------------------------------------------------------------------
*** Process requested Queries
    LR_IQC->PROCESS_QUERY( ).

***------------------------------------------------------------------------------------------------
    loop at CT_EVAL_TIME assigning <S_EVAL_TIME>.
***   Determine Time for customized Work Center
      if <S_EVAL_TIME>-R_RTE_CMACH is bound.
        ME->EVAL_TIME_CMACH( changing CS_EVAL_TIME = <S_EVAL_TIME> ).
      endif.

***   Calculate Machine Run Time
      if <S_EVAL_TIME>-S_MSRS-MSRSR is not initial.
        <S_EVAL_TIME>-S_MSRTI-MSRTI     = <S_EVAL_TIME>-S_MSRS-MSRST * <S_EVAL_TIME>-S_QTY-QTY / <S_EVAL_TIME>-S_MSRS-MSRSR.
        <S_EVAL_TIME>-S_MSRTI-MSRTI_UOM = <S_EVAL_TIME>-S_MSRS-MSRST_UOM.
      endif.

    endloop.

  endmethod.
  method EVALUATE_WASTE.

    data: LT_FEATURE_WA type /VCXI/CKI_TT_FEATURE.
    field-symbols: <S_EVAL_WASTE>  type ZVCXI_XCSR_S_EVAL_WASTE.


*** IQ.catalyst
    data(LR_IQC) = /VCXI/CL_CKI_IQC=>GET_INSTANCE( IF_DATE = ME->GET_DATE( ) ).

***------------------------------------------------------------------------------------------------
*** Prepare IQ.catalyst Bulk Call
    loop at CT_EVAL_WASTE assigning <S_EVAL_WASTE>
                         where R_RTE_CMACH is bound.

***   Filter provided Features to match requested
      <S_EVAL_WASTE>-R_RTE_CMACH->FILTER_FEATURE( exporting IT_FEATURE    = <S_EVAL_WASTE>-T_FEATURE
                                                 importing ET_FEATURE_WA = LT_FEATURE_WA ).

***   Add Request if Data Source is IQ.catalyst
      if <S_EVAL_WASTE>-R_RTE_CMACH->R_MGRPV_SPEED->S_MDS-MDSWA eq /VCXI/CL_VSMC_MGRPV_SPEED=>C_MDS_IQC.
        LR_IQC->ADD_QUERY_REQUEST( IS_CNTXT       = <S_EVAL_WASTE>-R_RTE_CMACH->R_ARBPL->GET_IQC_CNTXT( )
                                   IF_IQCMT       = <S_EVAL_WASTE>-R_RTE_CMACH->R_MTYPE->S_IMT-IMTWA
                                   IT_FEATURE_INP = LT_FEATURE_WA ).
      endif.
    endloop.

***------------------------------------------------------------------------------------------------
*** Process requested Queries
    LR_IQC->PROCESS_QUERY( ).

***------------------------------------------------------------------------------------------------
    loop at CT_EVAL_WASTE assigning <S_EVAL_WASTE>.
***   Determine Waste for customized Work Center
      if <S_EVAL_WASTE>-R_RTE_CMACH is bound.
        ME->EVAL_WASTE_CMACH( changing CS_EVAL_WASTE = <S_EVAL_WASTE> ).

      else.
***     Calculate Machine Run Waste (Outsourcing/Manual)
        if <S_EVAL_WASTE>-S_MSRWF-MSRWF is not initial.
          <S_EVAL_WASTE>-S_MSRWA-MSRWA     = <S_EVAL_WASTE>-S_QTY-QTY * ( ( <S_EVAL_WASTE>-S_MSRWF-MSRWF / 100 ) / ( 1 - ( <S_EVAL_WASTE>-S_MSRWF-MSRWF / 100 ) ) ).
          <S_EVAL_WASTE>-S_MSRWA-MSRWA_UOM = <S_EVAL_WASTE>-S_QTY-QTY_UOM.

***       Round PC up to next full PC
          if <S_EVAL_WASTE>-S_MSRWA-MSRWA_UOM eq 'ST'.
            <S_EVAL_WASTE>-S_MSRWA-MSRWA = CEIL( <S_EVAL_WASTE>-S_MSRWA-MSRWA ).
          endif.
        endif.
      endif.
    endloop.

  endmethod.
method FIND_SPVRE.

  data:  LF_ID_PARENT type /VCXI/CKX_ID.

*** Find Element
  loop at IT_SPVRE into RR_SPVRE.
    if RR_SPVRE->R_PARENT  eq IR_SPVRE_PARENT and
       RR_SPVRE->GET_ID( ) eq IF_ID.
      exit.
    else.
      clear RR_SPVRE.
    endif.
  endloop.

  if RR_SPVRE is not bound.
*** Supervised Runt.Element &1 can't be found as child of &2.
    if IR_SPVRE_PARENT is bound.
      move IR_SPVRE_PARENT->GET_ID( ) to LF_ID_PARENT.
    endif.
    /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( IF_MSGTY = 'E'
                                          IF_MSGID = 'ZVCXI_XCSR'
                                          IF_MSGNO = '404'
                                          IF_MSGV1 = IF_ID
                                          IF_MSGV2 = LF_ID_PARENT ).
  endif.

endmethod.
method GET_ARBPL_INFO.

  data:  LF_ID_GRP         type        /VCXI/CKX_ID.
  data:  LR_RTE_PSTEP      type ref to /VCXI/CL_VSMR_RTE_PSTEP,
         LR_BL_PSTEP       type ref to ZCL_VCXI_XCSR_BL_PSTEP.

***-------------------------------------------------------------------------------------–----------
*** Get ID of Group and Work Center where to be supervised to
  loop at IR_RTE_ARBPL->T_RTE_PSTEP into LR_RTE_PSTEP.
    try.
        move LR_RTE_PSTEP->R_BLOBJ ?to LR_BL_PSTEP.
        LR_BL_PSTEP->GET_ARBPL_INFO( changing CF_ID_GRP   = LF_ID_GRP
                                              CF_ID_ARBPL = EF_ID_ARBPL ).
      catch CX_SY_MOVE_CAST_ERROR.
    endtry.
  endloop.

***-------------------------------------------------------------------------------------–----------
*** Find/Create Group
  ME->GET_SPVRE( exporting IR_SPVRE_PARENT = IR_SPVRE_ROOT
                           IF_ID           = LF_ID_GRP
                 importing ER_SPVRE        = ER_SPVRE_GRP
                 changing  CT_SPVRE        = CT_SPVRE ).
endmethod.
  method GET_DATE.

    RF_DATE = cast ZCL_VCXI_XCSR_PR_RTESPV( ME->R_GATE->R_CLSPR )->F_DATE.

  endmethod.
method GET_SPVCE.

  data:  LF_ID       type        /VCXI/CKX_ID.
  data:  LR_OBJECT   type ref to /VCXI/IF_CKX_OBJECT,
         LR_SPVCE    type ref to /VCXI/CL_VKSC_SPVCE.

  loop at ME->T_SPVCE into LR_OBJECT.
    move LR_OBJECT ?to LR_SPVCE.

    if LR_SPVCE->/VCXI/IF_CKX_OBJECT~GET_ID( )       eq IF_ID and
       LR_SPVCE->/VCXI/IF_CKX_OBJECT_H~GET_PARENT( ) eq IR_SPVCE_PARENT.
      move LR_SPVCE to RR_SPVCE.
      exit.
    endif.
  endloop.

  if RR_SPVCE is not bound.
    if IR_SPVCE_PARENT is not bound.
***   Supervised Cust.Element &1 can't be found.
      /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( IF_MSGTY = 'E'
                                            IF_MSGID = 'ZVCXI_XCSR'
                                            IF_MSGNO = '401'
                                            IF_MSGV1 = IF_ID ).
    else.
***   Supervised Cust.Element &1 can't be found as child of &2.
      move IR_SPVCE_PARENT->/VCXI/IF_CKX_OBJECT~GET_ID( ) to LF_ID.
      /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( IF_MSGTY = 'E'
                                            IF_MSGID = 'ZVCXI_XCSR'
                                            IF_MSGNO = '402'
                                            IF_MSGV1 = IF_ID
                                            IF_MSGV2 = LF_ID ).
    endif.
  endif.

endmethod.
method GET_SPVRE.

  data:  LF_ID_PARENT type        /VCXI/CKX_ID.
  data:  LR_SPVCE     type ref to /VCXI/CL_VKSC_SPVCE.

  clear ER_SPVRE.

*** Get Customizing Element
  LR_SPVCE = ME->GET_SPVCE( IF_ID           = IF_ID
                            IR_SPVCE_PARENT = IR_SPVRE_PARENT->R_SPVCE ).

*** Find Element
  loop at CT_SPVRE into ER_SPVRE.
    if ER_SPVRE->R_PARENT  eq IR_SPVRE_PARENT and
       ER_SPVRE->R_SPVCE   eq LR_SPVCE.
      exit.
    else.
      clear ER_SPVRE.
    endif.
  endloop.

*** Create if needed
  if ER_SPVRE is not bound.
    create object ER_SPVRE
      exporting
        IR_SPVCE  = LR_SPVCE
        IR_PARENT = IR_SPVRE_PARENT.
    append ER_SPVRE to CT_SPVRE.
  endif.

endmethod.
method PROCESS_RTE_ARBPL.

  data:  LF_ID_ARBPL       type        /VCXI/CKX_ID,
         LF_OUTSO          type        ZVCXI_XCSR_OUTSO,
         LF_MANWC          type        ZVCXI_XCSR_MANWC.
  data:  LR_SPVRE_GRP      type ref to /VCXI/CL_VKSR_SPVRE,
         LR_RTE_PSTEP      type ref to /VCXI/CL_VSMR_RTE_PSTEP,
         LR_BL_OUTSO       type ref to ZIF_VCXI_XCSR_BL_OUTSO,
         LR_BL_MANU        type ref to ZIF_VCXI_XCSR_BL_MANU.


  ME->GET_ARBPL_INFO( exporting IR_SPVRE_ROOT = IR_SPVRE_ROOT
                                IR_RTE_ARBPL  = IR_RTE_ARBPL
                                IF_DATE       = IR_BL_PRTE->F_DATE
                      importing ER_SPVRE_GRP  = LR_SPVRE_GRP
                                EF_ID_ARBPL   = LF_ID_ARBPL
                      changing  CT_SPVRE      = CT_SPVRE ).
  check LR_SPVRE_GRP is bound.

***-------------------------------------------------------------------------------------–----------
*** Check is Outsourcing or Manual
  read table IR_RTE_ARBPL->T_RTE_PSTEP into LR_RTE_PSTEP
                                       index 1.
  if SY-SUBRC eq 0.
*** Check for Manual Work Center
    try.
        move LR_RTE_PSTEP->R_BLOBJ ?to LR_BL_MANU.
        if LR_BL_MANU is bound.
          move LR_BL_MANU->F_MANWC to LF_MANWC.
        endif.
      catch CX_SY_MOVE_CAST_ERROR.
        clear LR_BL_MANU.
    endtry.

*** Check for Outsourcing
    try.
        move LR_RTE_PSTEP->R_BLOBJ ?to LR_BL_OUTSO.
        if LR_BL_OUTSO is bound.
          move LR_BL_OUTSO->F_OUTSO to LF_OUTSO.
        endif.
      catch CX_SY_MOVE_CAST_ERROR.
        clear LR_BL_OUTSO.
    endtry.
  endif.

***-------------------------------------------------------------------------------------–----------
*** Add Operation
  case ABAP_TRUE.
    when LF_MANWC.
***   Add Manual Work Center
      ME->ADD_MANU( exporting IR_RTE_ARBPL  = IR_RTE_ARBPL
                              IR_SPVRE_GRP  = LR_SPVRE_GRP
                              IF_ID         = LF_ID_ARBPL
                    importing ER_SPVRE_MANU = ER_SPVRE_ARBPL
                    changing  CT_SPVRE      = CT_SPVRE ).

    when LF_OUTSO.
***   Add Outsourcing
      ME->ADD_OUTSO( exporting IR_RTE_ARBPL   = IR_RTE_ARBPL
                               IR_SPVRE_GRP   = LR_SPVRE_GRP
                               IF_ID          = LF_ID_ARBPL
                     importing ER_SPVRE_OUTSO = ER_SPVRE_ARBPL
                     changing  CT_SPVRE       = CT_SPVRE ).

    when others.
***   Add Machine
      ME->ADD_MACHINE( exporting IR_RTE_ARBPL     = IR_RTE_ARBPL
                                 IR_SPVRE_GRP     = LR_SPVRE_GRP
                                 IF_ID            = LF_ID_ARBPL
                                 IR_BL_PRTE       = IR_BL_PRTE
                       importing ER_SPVRE_MACHINE = ER_SPVRE_ARBPL
                       changing  CT_SPVRE         = CT_SPVRE ).
  endcase.


***-------------------------------------------------------------------------------------–----------
*** Add Material Flow Manager
  ME->ADD_MFMGR( exporting IR_RTE_ARBPL   = IR_RTE_ARBPL
                           IR_SPVRE_ARBPL = ER_SPVRE_ARBPL
                           IR_BL_PRTE     = IR_BL_PRTE
                 changing  CT_SPVRE       = CT_SPVRE ).

***-------------------------------------------------------------------------------------–----------
*** Process Production Steps
  loop at IR_RTE_ARBPL->T_RTE_PSTEP into LR_RTE_PSTEP.
    ME->PROCESS_RTE_PSTEP( exporting IR_RTE_PSTEP   = LR_RTE_PSTEP
                                     IR_SPVRE_ARBPL = ER_SPVRE_ARBPL
                                     IR_ARBPL       = IR_RTE_ARBPL->R_ARBPL
                           changing  CT_SPVRE       = CT_SPVRE ).
  endloop.

***-------------------------------------------------------------------------------------–----------
*** Add Helper
  ME->ADD_HELPER( exporting IR_RTE_ARBPL     = IR_RTE_ARBPL
                            IR_SPVRE_ARBPL   = ER_SPVRE_ARBPL
                  changing  CT_SPVRE         = CT_SPVRE ).

***-------------------------------------------------------------------------------------–----------
*** Add Cost Split Manager
  if IR_BL_PRTE->T_CSPLIT is not initial.
    ME->ADD_CSPLIT_MGR( exporting IR_BL_PRTE       = IR_BL_PRTE
                                  IR_SPVRE_ARBPL   = ER_SPVRE_ARBPL
                        changing  CT_SPVRE         = CT_SPVRE ).
  endif.

endmethod.
method PROCESS_RTE_PSTEP.

  data:  LF_OUTSO          type        ZVCXI_XCSR_OUTSO,
         LF_MANWC          type        ZVCXI_XCSR_MANWC.
  data:  LT_SPVRE          type        /VCXI/VKSR_TT_SPVRE.
  data:  LR_BL_PSTEP       type ref to ZCL_VCXI_XCSR_BL_PSTEP,
         LR_BL_OUTSO       type ref to ZIF_VCXI_XCSR_BL_OUTSO,
         LR_BL_MANU        type ref to ZIF_VCXI_XCSR_BL_MANU.

***-------------------------------------------------------------------------------------–----------
*** Check for Manual Work Center
  try.
      move IR_RTE_PSTEP->R_BLOBJ ?to LR_BL_MANU.
      if LR_BL_MANU is bound.
        move LR_BL_MANU->F_MANWC to LF_MANWC.
      endif.
    catch CX_SY_MOVE_CAST_ERROR.
      clear LR_BL_MANU.
  endtry.

*** Check for Outsourcing
  try.
      move IR_RTE_PSTEP->R_BLOBJ ?to LR_BL_OUTSO.
      if LR_BL_OUTSO is bound.
        move LR_BL_OUTSO->F_OUTSO to LF_OUTSO.
      endif.
    catch CX_SY_MOVE_CAST_ERROR.
      clear LR_BL_OUTSO.
  endtry.

***-------------------------------------------------------------------------------------–----------
*** Get Addon/Activity
  move IR_RTE_PSTEP->R_BLOBJ ?to LR_BL_PSTEP.

  case ABAP_TRUE.
    when LF_OUTSO or LF_MANWC.
***   Get Activity
      LT_SPVRE = LR_BL_PSTEP->GET_SPVRE_ACTVY( IR_BL_RTESPV   = ME
                                               IR_SPVRE_ARBPL = IR_SPVRE_ARBPL
                                               IR_ARBPL       = IR_ARBPL ).

    when others.
***   Get Addon
      LT_SPVRE = LR_BL_PSTEP->GET_SPVRE_ADDON( IR_BL_RTESPV   = ME
                                               IR_SPVRE_ARBPL = IR_SPVRE_ARBPL
                                               IR_ARBPL       = IR_ARBPL ).

  endcase.

  append lines of LT_SPVRE to CT_SPVRE.

endmethod.
method SET_BL_PRTE.

  data:  LT_SPVRE_NEW      type        /VCXI/VKSR_TT_SPVRE.
  data:  LR_BL_PRTE        type ref to ZCL_VCXI_XCSR_BL_PRTE,
         LR_SPVRE_ROOT     type ref to /VCXI/CL_VKSR_SPVRE,
         LR_RTE_ARBPL      type ref to /VCXI/CL_VSMR_RTE_ARBPL.

  check IR_BL_PRTE is bound and
        IR_BL_PRTE->R_RTE_PRTE is bound.

***----------------------------------------------------------------------------
*** Copy incoming BL Production Route to decouple it
  move IR_BL_PRTE->COPY( ) to LR_BL_PRTE.

***----------------------------------------------------------------------------
*** Build Structure
  ME->BUILD_STRUCT( exporting IR_BL_PRTE    = LR_BL_PRTE
                              IS_EXQTY      = IS_EXQTY
                    importing ER_SPVRE_ROOT = LR_SPVRE_ROOT
                    changing  CT_SPVRE      = LT_SPVRE_NEW ).

***----------------------------------------------------------------------------
*** Process Work Center
  loop at LR_BL_PRTE->R_RTE_PRTE->T_RTE_ARBPL into LR_RTE_ARBPL.
    ME->PROCESS_RTE_ARBPL( exporting IR_SPVRE_ROOT = LR_SPVRE_ROOT
                                     IR_RTE_ARBPL  = LR_RTE_ARBPL
                                     IR_BL_PRTE    = LR_BL_PRTE
                           changing  CT_SPVRE      = LT_SPVRE_NEW ).
  endloop.

***----------------------------------------------------------------------------
*** Set Supervised Runtime Elements
  ME->R_GATE->SET_SPVRE( IT_SPVRE = LT_SPVRE_NEW ).

endmethod.
method SET_VALUE_PSCALE_A.

  data:  LS_PARAM            type        /VCXI/VKSR_S_PARAM.

***------------------------------------------------------------------------------------------------
*** Set Price Scale Quantity
  clear LS_PARAM.
  move ZCL_VCXI_XCS_PR_PSCALE=>C_PARID_PSQTY_A       to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC             to LS_PARAM-PARTY.
  move IS_PSCALE-PSQTY                               to LS_PARAM-ATFLV.
  move IS_PSCALE-PSQTY_UOM                           to LS_PARAM-UNIT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC              to LS_PARAM-SETBY.
  IR_SPVRE_PSCALE->SET_PARAM( IS_PARAM = LS_PARAM ).

***------------------------------------------------------------------------------------------------
*** Set Price
  clear LS_PARAM.
  move ZCL_VCXI_XCS_PR_PSCALE=>C_PARID_PRICE         to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC             to LS_PARAM-PARTY.
  move IS_PSCALE-PRICE                               to LS_PARAM-ATFLV.
  move IS_PSCALE-PRICE_CURR                          to LS_PARAM-CURKY.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC              to LS_PARAM-SETBY.
  IR_SPVRE_PSCALE->SET_PARAM( IS_PARAM = LS_PARAM ).

***------------------------------------------------------------------------------------------------
*** Set Price Unit
  clear LS_PARAM.
  move ZCL_VCXI_XCS_PR_PSCALE=>C_PARID_PRICP_A       to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC             to LS_PARAM-PARTY.
  move IS_PSCALE-PRICP                               to LS_PARAM-ATFLV.
  move IS_PSCALE-PRICP_UOM                           to LS_PARAM-UNIT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC              to LS_PARAM-SETBY.
  IR_SPVRE_PSCALE->SET_PARAM( IS_PARAM = LS_PARAM ).


endmethod.
method SET_VALUE_PSCALE_L.

  data:  LS_PARAM            type        /VCXI/VKSR_S_PARAM.

***------------------------------------------------------------------------------------------------
*** Set Price Scale Quantity
  clear LS_PARAM.
  move ZCL_VCXI_XCS_PR_PSCALE=>C_PARID_PSQTY_L       to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC             to LS_PARAM-PARTY.
  move IS_PSCALE-PSQTY                               to LS_PARAM-ATFLV.
  move IS_PSCALE-PSQTY_UOM                           to LS_PARAM-UNIT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC              to LS_PARAM-SETBY.
  IR_SPVRE_PSCALE->SET_PARAM( IS_PARAM = LS_PARAM ).

***------------------------------------------------------------------------------------------------
*** Set Price
  clear LS_PARAM.
  move ZCL_VCXI_XCS_PR_PSCALE=>C_PARID_PRICE         to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC             to LS_PARAM-PARTY.
  move IS_PSCALE-PRICE                               to LS_PARAM-ATFLV.
  move IS_PSCALE-PRICE_CURR                          to LS_PARAM-CURKY.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC              to LS_PARAM-SETBY.
  IR_SPVRE_PSCALE->SET_PARAM( IS_PARAM = LS_PARAM ).

***------------------------------------------------------------------------------------------------
*** Set Price Unit
  clear LS_PARAM.
  move ZCL_VCXI_XCS_PR_PSCALE=>C_PARID_PRICP_L       to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC             to LS_PARAM-PARTY.
  move IS_PSCALE-PRICP                               to LS_PARAM-ATFLV.
  move IS_PSCALE-PRICP_UOM                           to LS_PARAM-UNIT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC              to LS_PARAM-SETBY.
  IR_SPVRE_PSCALE->SET_PARAM( IS_PARAM = LS_PARAM ).


endmethod.
method SET_VALUE_PSCALE_P.

  data:  LS_PARAM            type        /VCXI/VKSR_S_PARAM.

***------------------------------------------------------------------------------------------------
*** Set Price Scale Quantity
  clear LS_PARAM.
  move ZCL_VCXI_XCS_PR_PSCALE=>C_PARID_PSQTY_P       to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC             to LS_PARAM-PARTY.
  move IS_PSCALE-PSQTY                               to LS_PARAM-ATFLV.
  move IS_PSCALE-PSQTY_UOM                           to LS_PARAM-UNIT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC              to LS_PARAM-SETBY.
  IR_SPVRE_PSCALE->SET_PARAM( IS_PARAM = LS_PARAM ).

***------------------------------------------------------------------------------------------------
*** Set Price
  clear LS_PARAM.
  move ZCL_VCXI_XCS_PR_PSCALE=>C_PARID_PRICE         to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC             to LS_PARAM-PARTY.
  move IS_PSCALE-PRICE                               to LS_PARAM-ATFLV.
  move IS_PSCALE-PRICE_CURR                          to LS_PARAM-CURKY.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC              to LS_PARAM-SETBY.
  IR_SPVRE_PSCALE->SET_PARAM( IS_PARAM = LS_PARAM ).

***------------------------------------------------------------------------------------------------
*** Set Price Unit
  clear LS_PARAM.
  move ZCL_VCXI_XCS_PR_PSCALE=>C_PARID_PRICP_P       to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC             to LS_PARAM-PARTY.
  move IS_PSCALE-PRICP                               to LS_PARAM-ATFLV.
  move IS_PSCALE-PRICP_UOM                           to LS_PARAM-UNIT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC              to LS_PARAM-SETBY.
  IR_SPVRE_PSCALE->SET_PARAM( IS_PARAM = LS_PARAM ).


endmethod.
method SET_VALUE_PSCALE_W.

  data:  LS_PARAM            type        /VCXI/VKSR_S_PARAM.

***------------------------------------------------------------------------------------------------
*** Set Price Scale Quantity
  clear LS_PARAM.
  move ZCL_VCXI_XCS_PR_PSCALE=>C_PARID_PSQTY_W       to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC             to LS_PARAM-PARTY.
  move IS_PSCALE-PSQTY                               to LS_PARAM-ATFLV.
  move IS_PSCALE-PSQTY_UOM                           to LS_PARAM-UNIT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC              to LS_PARAM-SETBY.
  IR_SPVRE_PSCALE->SET_PARAM( IS_PARAM = LS_PARAM ).

***------------------------------------------------------------------------------------------------
*** Set Price
  clear LS_PARAM.
  move ZCL_VCXI_XCS_PR_PSCALE=>C_PARID_PRICE         to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC             to LS_PARAM-PARTY.
  move IS_PSCALE-PRICE                               to LS_PARAM-ATFLV.
  move IS_PSCALE-PRICE_CURR                          to LS_PARAM-CURKY.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC              to LS_PARAM-SETBY.
  IR_SPVRE_PSCALE->SET_PARAM( IS_PARAM = LS_PARAM ).

***------------------------------------------------------------------------------------------------
*** Set Price Unit
  clear LS_PARAM.
  move ZCL_VCXI_XCS_PR_PSCALE=>C_PARID_PRICP_W       to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC             to LS_PARAM-PARTY.
  move IS_PSCALE-PRICP                               to LS_PARAM-ATFLV.
  move IS_PSCALE-PRICP_UOM                           to LS_PARAM-UNIT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC              to LS_PARAM-SETBY.
  IR_SPVRE_PSCALE->SET_PARAM( IS_PARAM = LS_PARAM ).


endmethod.
