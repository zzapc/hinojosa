
class ZCL_VCXI_FFE_MM_MMEXT definition
  public
  inheriting from ZCL_VCXI_F0E_MM_MMEXT
  create public .

public section.

  constants C_CNTXT_FF type ZVCXI_XCE_MM_CNTXT value 'FF' ##NO_TEXT.
  data S_MMEXT type ZVCXI_FFE_MM_S_MMEXT .

  methods SET_MMEXT
    importing
      !IS_MMEXT type ZVCXI_FFE_MM_S_MMEXT
    raising
      /VCXI/CX_CKX .

  methods SAVE
    redefinition .
  methods SET_EXTVAL
    redefinition .
  methods UPDATE_BY_FG
    redefinition .
  methods COPY
    redefinition .
protected section.

  methods LOAD_FROM_DB
    redefinition .
private section.
endclass. "ZCL_VCXI_FFE_MM_MMEXT definition
class ZCL_VCXI_FFE_MM_MMEXT implementation.
  method COPY.

    RR_MMEXT = SUPER->COPY( IF_MATNR = IF_MATNR
                            IF_WERKS = IF_WERKS ).

    check RR_MMEXT is bound.

*** Copy Values
    cast ZCL_VCXI_FFE_MM_MMEXT( RR_MMEXT )->S_MMEXT = ME->S_MMEXT.

  endmethod.
  method LOAD_FROM_DB.

    data: LS_TD100 type ZVCXI_FFE_TD100.


***----------------------------------------------------------------------------
*** Load from SUPER
    SUPER->LOAD_FROM_DB( ).

***----------------------------------------------------------------------------
*** Load Material Master Extension
    select single *
           into LS_TD100
           from ZVCXI_FFE_TD100
          where MATNR eq ME->F_MATNR
           and  WERKS eq ME->F_WERKS.
    if SY-SUBRC eq 0.
***   Load current values
      ME->S_MMEXT = LS_TD100-S_MMEXT.
    endif.

  endmethod.
  method SAVE.

    data: LS_TD100 type ZVCXI_FFE_TD100.


*** Save SUPER
    SUPER->SAVE( ).

***----------------------------------------------------------------------------
*** Save basic Material Master Extension
    LS_TD100 = value #( MATNR   = ME->F_MATNR
                        WERKS   = ME->F_WERKS
                        S_MMEXT = ME->S_MMEXT ).
    modify ZVCXI_FFE_TD100 from LS_TD100.

  endmethod.
  method SET_EXTVAL.

    data: LS_EXTVAL type ZVCXI_FFE_MM_S_EXTVAL.


    SUPER->SET_EXTVAL( IS_EXTVAL = IS_EXTVAL ).

***----------------------------------------------------------------------------
*** "Cast" Extension Values
    LS_EXTVAL = corresponding #( IS_EXTVAL ).

*** Extension Header
    if ME->S_MMEXT ne LS_EXTVAL-S_MMEXT.
      ME->S_MMEXT = LS_EXTVAL-S_MMEXT.
      raise event /VCXI/IF_CKX_OBJECT~CHANGED.
    endif.

  endmethod.
  method SET_MMEXT.

    check ME->S_MMEXT ne IS_MMEXT.
    ME->S_MMEXT = IS_MMEXT.
    raise event /VCXI/IF_CKX_OBJECT~CHANGED.

  endmethod.
  method UPDATE_BY_FG.

    data: LR_EA_FFG     type ref to ZCL_VCXI_FFSS_EA_FFG.


***----------------------------------------------------------------------------
*** Get Element API Reference
    try.
        LR_EA_FFG ?= IR_SVCEL->GET_CLSEA( ).
      catch CX_SY_MOVE_CAST_ERROR into data(LR_CX_SY_MOVE_CAST_ERROR).
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_SY_MOVE_CAST_ERROR ).
    endtry.
    check LR_EA_FFG is bound.

***----------------------------------------------------------------------------
*** Set Extension Values
    ME->SET_EXTVAL( IS_EXTVAL = LR_EA_FFG->GET_EXTVAL( ) ).

  endmethod.
