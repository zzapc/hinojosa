
class ZCL_VCXI_PCSP_UI_ACTVY_SLSC definition
  public
  inheriting from /VCXI/CL_VKSRU_CLSUI
  create public .

public section.

  data S_ACTVY_SLSC_VIS type ZVCXI_PCSP_S_ACTVY_SLSC_VIS .
  data F_CURSOR type SCRFNAME .

  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_ACTVY_SLSC_VIS
    importing
      !IS_ACTVY_SLSC_VIS type ZVCXI_PCSP_S_ACTVY_SLSC_VIS
    raising
      /VCXI/CX_CKX .
  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods SET_CURSOR
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
protected section.

  data R_PR_ACTVY_SLSC type ref to ZCL_VCXI_PCSP_PR_ACTVY_SLSC .

  methods INIT_VIS_ACTVY_SLSC
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
private section.
endclass. "ZCL_VCXI_PCSP_UI_ACTVY_SLSC definition
class ZCL_VCXI_PCSP_UI_ACTVY_SLSC implementation.
method ACTIVATE.

  SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
  call function 'ZVCXI_PCSP_UI_ACTVY_SLSC_BIND'
    exporting
      IR_UI_ACTVY_SLSC = ME
    importing
      ES_DYNP          = ME->S_DYNP.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                      IR_UIGATE = IR_UIGATE
                      IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
  try.
      move IR_GATE->R_CLSPR ?to ME->R_PR_ACTVY_SLSC.
    catch CX_SY_MOVE_CAST_ERROR.
***   UI ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '801'.
  endtry.

endmethod.
method DO_MODIFY_SCREEN.

  data:  LF_LOCKED type        /VCXI/CKX_LOCKED,
         LF_INUSE  type        /VCXI/VKSR_INUSE.
  data:  LR_CX_CKX type ref to /VCXI/CX_CKX.

  SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                           IF_DYNNR = IF_DYNNR ).

  try.

      if IF_REPID eq ME->S_DYNP-REPID and
         IF_DYNNR eq ME->S_DYNP-DYNNR.

        LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
        LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***     Modify Scree using DynamicVC
        ZCL_VCXI_XCDR_SERVICE=>MODIFY_SCREEN( IR_GATE = ME->R_GATE ).

        loop at screen.
          if LF_LOCKED ne ABAP_TRUE or
             LF_INUSE  ne ABAP_TRUE.
            move 0 to SCREEN-INPUT.
          endif.
          modify screen.
        endloop.
      endif.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

endmethod.
method DO_PBO.

  ME->INIT_VIS_ACTVY_SLSC( ).

  set cursor field ME->F_CURSOR.
  clear ME->F_CURSOR.

endmethod.
method DO_UPDATE_ACTVY_SLSC_VIS.

  data:          LS_PCWID             type          ZVCXI_P0SP_S_PCWID.

  move-corresponding IS_ACTVY_SLSC_VIS to LS_PCWID.

*** Set PC Width
  if ME->S_ACTVY_SLSC_VIS-PCWID ne IS_ACTVY_SLSC_VIS-PCWID.
    ME->R_PR_ACTVY_SLSC->SET_PCWID( IS_PCWID = LS_PCWID
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Set PC Type
  if ME->S_ACTVY_SLSC_VIS-PCTYP ne IS_ACTVY_SLSC_VIS-PCTYP.
    ME->R_PR_ACTVY_SLSC->SET_PCTYP( IF_PCTYP = IS_ACTVY_SLSC_VIS-PCTYP
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

endmethod.
method HANDLE_GATE_FREED.

  SUPER->HANDLE_GATE_FREED( ).

  free: ME->R_PR_ACTVY_SLSC.

endmethod.
method INIT_VIS_ACTVY_SLSC.

  data:          LS_PCWID             type          ZVCXI_P0SP_S_PCWID.

  free: ME->S_ACTVY_SLSC_VIS.

*** Get PC Width
  move ME->R_PR_ACTVY_SLSC->GET_PCWID( ) to LS_PCWID.
  move-corresponding LS_PCWID to ME->S_ACTVY_SLSC_VIS.

*** Get PC Type and Description
  move ME->R_PR_ACTVY_SLSC->GET_PCTYP( )       to ME->S_ACTVY_SLSC_VIS-PCTYP.
  move ME->R_PR_ACTVY_SLSC->GET_PCTYP_DESCR( ) to ME->S_ACTVY_SLSC_VIS-PCTYP_DESCR.

endmethod.
method SET_CURSOR.

  data:  LF_CURSOR type SCRFNAME.

  get cursor field LF_CURSOR.
  if LF_CURSOR is not initial.
    move LF_CURSOR to ME->F_CURSOR.
  endif.

endmethod.
