
class ZCL_VCXI_PFSS_CUST_SBOARD definition
  public
  create protected .

public section.

  constants C_GRDIR_L type ZVCXI_PFSS_GRDIR value 'LG'. "#EC NOTEXT
  constants C_GRDIR_S type ZVCXI_PFSS_GRDIR value 'SG'. "#EC NOTEXT

  class-methods CLASS_CONSTRUCTOR .
  class-methods GET_GRDIR
    importing
      !IS_AGGRA type ZVCXI_PFSS_S_AGGRA
      !IS_INGRA type ZVCXI_PFSS_S_INGRA
    returning
      value(RF_GRDIR) type ZVCXI_PFSS_GRDIR
    raising
      /VCXI/CX_CKX .
  class-methods GET_GRDIR_DESCR
    importing
      !IF_GRDIR type ZVCXI_PFSS_GRDIR
    returning
      value(RF_GRDIR_DESCR) type /VCXI/CKX_DESCR
    raising
      /VCXI/CX_CKX .
  class-methods GET_PAPGR_DESCR
    importing
      !IF_PAPGR type ZVCXI_PFSS_PAPGR
    returning
      value(RF_PAPGR_DESCR) type /VCXI/CKX_DESCR
    raising
      /VCXI/CX_CKX .
  class-methods GET_TCUTT_DESCR
    importing
      !IF_TCUTT type ZVCXI_PFSS_TCUTT
    returning
      value(RF_PAPGR_DESCR) type /VCXI/CKX_DESCR
    raising
      /VCXI/CX_CKX .
  type-pools ABAP .
  class-methods CHECK_PAPGR
    importing
      !IF_PAPGR type ZVCXI_PFSS_PAPGR
    returning
      value(RF_VALID) type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
  class-methods CHECK_TCUTT
    importing
      !IF_TCUTT type ZVCXI_PFSS_TCUTT
    returning
      value(RF_VALID) type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
protected section.

  types:
    YT_TC110T type standard table of ZVCXI_PFS_TC110T with default key .
  types:
    YT_TC110 type standard table of ZVCXI_PFS_TC110 with default key .
  types:
    YT_TC120T type standard table of ZVCXI_PFS_TC120T with default key .
  types:
    YT_TC120 type standard table of ZVCXI_PFS_TC120 with default key .

  class-data T_TC110T type YT_TC110T .
  class-data T_TC110 type YT_TC110 .
  class-data T_TC120T type YT_TC120T .
  class-data T_TC120 type YT_TC120 .
private section.
endclass. "ZCL_VCXI_PFSS_CUST_SBOARD definition
class ZCL_VCXI_PFSS_CUST_SBOARD implementation.
method CHECK_PAPGR.

  read table T_TC110 with key PAPGR = IF_PAPGR
                     transporting no fields.
  check SY-SUBRC = 0.
  RF_VALID = ABAP_TRUE.

endmethod.
method CHECK_TCUTT.

  read table T_TC120 with key TCUTT = IF_TCUTT
                     transporting no fields.
  check SY-SUBRC = 0.
  RF_VALID = ABAP_TRUE.

endmethod.
method CLASS_CONSTRUCTOR.

*** Get all Paper Grade
  select *
    from ZVCXI_PFS_TC110
    into table T_TC110.

*** Get Paper Grade Description
  select *
    from ZVCXI_PFS_TC110T
    into table T_TC110T.

*** Get all Cuttint Types
  select *
    from ZVCXI_PFS_TC120
    into table T_TC120.

*** Get Cuttint Type Description
  select *
    from ZVCXI_PFS_TC120T
    into table T_TC120T.

endmethod.
method GET_GRDIR.

*** Get Grain Direction
  if IS_AGGRA-AGGRA > IS_INGRA-INGRA.
    move C_GRDIR_S to RF_GRDIR.
  else.
    move C_GRDIR_L to RF_GRDIR.
  endif.

endmethod.
method GET_GRDIR_DESCR.

  constants: LC_DOMNAME type                   DOMNAME value 'ZVCXI_PFSS_GRDIR'.
  data:      LT_D07V    type standard table of DD07V.
  data:      LS_D07V    type                   DD07V.

*** Return Sheet Direction Description
  call function 'DD_DOMVALUES_GET'
    exporting
      DOMNAME   = LC_DOMNAME
      TEXT      = 'X'
      LANGU     = SY-LANGU
    tables
      DD07V_TAB = LT_D07V.

  check LT_D07V is not initial.
  read table LT_D07V with key DOMVALUE_L = IF_GRDIR into LS_D07V.
  check SY-SUBRC = 0.

  RF_GRDIR_DESCR = LS_D07V-DDTEXT.

endmethod.
method GET_PAPGR_DESCR.

  data: LS_TC110T type ZVCXI_PFS_TC110T.

  read table T_TC110T into LS_TC110T with key PAPGR = IF_PAPGR
                                              LANGU = SY-LANGU.
  if SY-SUBRC ne 0.
    read table T_TC110T into LS_TC110T with key PAPGR = IF_PAPGR
                                                LANGU = 'E'.
  endif.
  check SY-SUBRC = 0.

  move LS_TC110T-DESCR to RF_PAPGR_DESCR.

endmethod.
method GET_TCUTT_DESCR.

  data: LS_TC120T type ZVCXI_PFS_TC120T.

  read table T_TC120T into LS_TC120T with key TCUTT = IF_TCUTT
                                              LANGU = SY-LANGU.
  if SY-SUBRC ne 0.
    read table T_TC120T into LS_TC120T with key TCUTT = IF_TCUTT
                                                LANGU = 'E'.
  endif.

  check SY-SUBRC = 0.
  move LS_TC120T-DESCR to RF_PAPGR_DESCR.

endmethod.
