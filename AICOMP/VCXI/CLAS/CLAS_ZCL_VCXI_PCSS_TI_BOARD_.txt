
class ZCL_VCXI_PCSS_TI_BOARD definition
  public
  inheriting from ZCL_VCXI_XCS_TI
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ
    redefinition .
  methods /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL
    redefinition .
  methods /VCXI/IF_VKSU_TI_EVENT~HANDLE_DOUBLE_CLICK
    redefinition .
protected section.

  constants C_OPEN_MODE_SPEC type /VCXI/CKX_PRPVA value 'SPEC'. "#EC NOTEXT
  constants C_UC_SHOW_BOARD type UI_FUNC value 'SHOW_BOARD'. "#EC NOTEXT
  data R_PR_BOARD type ref to ZCL_VCXI_PCSS_PR_BOARD .

  methods OPEN_UI_SPEC
    raising
      /VCXI/CX_CKX .
  methods SHOW_BOARD
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_PCSS_TI_BOARD definition
class ZCL_VCXI_PCSS_TI_BOARD implementation.
method /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ.

  data: LF_TEXT         type GUI_TEXT,
        LF_BOARD        type ZVCXI_PCS_BOARD,
        LF_MBOAR        type ZVCXI_PCSS_MBOAR,
        LF_DCONF        type DATS.

  SUPER->/VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ( IR_MENU = IR_MENU ).

***------------------------------------------------------------------------------------------------
*** Show Board Customizing
  LF_BOARD = ME->R_PR_BOARD->GET_BOARD( ).
  LF_MBOAR = ME->R_PR_BOARD->GET_MBOAR( ).
  LF_DCONF = ME->R_PR_BOARD->GET_DCONF( ).
  if LF_BOARD is not initial and
     LF_MBOAR is not initial and
     LF_DCONF is not initial.

    concatenate 'Show BoardID'(F10) LF_BOARD into LF_TEXT separated by SPACE.

*** Set Seperator
    IR_MENU->ADD_SEPARATOR( ).

*** Set Menu
    IR_MENU->ADD_FUNCTION( FCODE = C_UC_SHOW_BOARD
                           TEXT  = LF_TEXT ).
  endif.


endmethod.
method /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL.

  case IF_FCODE.
    when C_UC_SHOW_BOARD.
      ME->SHOW_BOARD( ).

    when others.
      SUPER->/VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL( IF_FCODE = IF_FCODE ).
  endcase.

endmethod.
method /VCXI/IF_VKSU_TI_EVENT~HANDLE_DOUBLE_CLICK.

*** If it is TI_OPEN_MODE with Attribute MACHINE
  if IF_ITMNA ne /VCXI/CL_VKSRU_TREE=>C_COLUMN_STATUS and
     ME->GET_OPEN_MODE( ) eq C_OPEN_MODE_SPEC.
    ME->OPEN_UI_SPEC( ).

*** All other do Super
  else.
    SUPER->/VCXI/IF_VKSU_TI_EVENT~HANDLE_DOUBLE_CLICK( IF_ITMNA = IF_ITMNA ).
  endif.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_UIGATE = IR_UIGATE
                      IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
  try.
      move IR_GATE->R_CLSPR ?to ME->R_PR_BOARD.
    catch CX_SY_MOVE_CAST_ERROR.
***   TI ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '802'.
  endtry.

endmethod.
method OPEN_UI_SPEC.

  data:  LR_GATE_SPEC   type ref to /VCXI/CL_VKSR_GATE,
         LR_UI          type ref to /VCXI/CL_VKSRU_CLSUI,
         LR_UI_SPEC     type ref to ZCL_VCXI_PCSS_UI_SPEC,
         LR_UI_SPEC_SE  type ref to ZCL_VCXI_PCSS_UI_SPEC_SE.

  LR_GATE_SPEC = ME->R_GATE->GET_PARENT( ).
  check LR_GATE_SPEC is bound.

*** Open SPEC UI
  LR_UI = ME->R_UIGATE->OPEN_SUBSCREEN( IR_GATE = LR_GATE_SPEC ).

*** Set Default Gate for Activ Tab - Spec
  try.
      move LR_UI ?to LR_UI_SPEC.
      if LR_UI_SPEC is bound.
        LR_UI_SPEC->R_DEFAULT_GATE = ME->R_GATE.
      endif.
    catch CX_SY_MOVE_CAST_ERROR.
      clear: LR_UI_SPEC.
  endtry.

*** Set Default Gate for Activ Tab - Spec SE
  try.
      move LR_UI ?to LR_UI_SPEC_SE.
      if LR_UI_SPEC_SE is bound.
        LR_UI_SPEC_SE->R_DEFAULT_GATE = ME->R_GATE.
      endif.
    catch CX_SY_MOVE_CAST_ERROR.
      clear: LR_UI_SPEC_SE.
  endtry.

endmethod.
method SHOW_BOARD.

  data: LF_BOARD        type ZVCXI_PCS_BOARD,
        LF_MBOAR        type ZVCXI_PCSS_MBOAR,
        LF_DCONF        type DATS,
        LF_MESSAGE      type CHAR100.

*** Get Board ID Definition.
  LF_BOARD = ME->R_PR_BOARD->GET_BOARD( ).
  LF_MBOAR = ME->R_PR_BOARD->GET_MBOAR( ).
  LF_DCONF = ME->R_PR_BOARD->GET_DCONF( ).

*** Open Board ID in new Task
  call function 'ZVCXI_PCSSC_BOARD_START'
    starting new task 'NEW'
    exporting
      IF_BOARD              = LF_BOARD
      IF_MBOAR              = LF_MBOAR
      IF_DATE               = LF_DCONF
    exceptions
      SYSTEM_FAILURE        = 97  message LF_MESSAGE
      COMMUNICATION_FAILURE = 98  message LF_MESSAGE
      others                = 99.
  if SY-SUBRC   ne 0 and
     LF_MESSAGE is not initial.
    message LF_MESSAGE type 'W'.
  endif.

endmethod.
