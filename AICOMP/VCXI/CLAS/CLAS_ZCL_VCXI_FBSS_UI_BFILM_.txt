
class ZCL_VCXI_FBSS_UI_BFILM definition
  public
  inheriting from ZCL_VCXI_F0S_UI_TAB
  create public .

public section.

  data S_BFILM_VIS type ZVCXI_FBSS_S_BFILM_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABPT type ZVCXI_F0S_TABPT default '0100S_BF_TAB-TAB'
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_BFILM_VIS
    importing
      !IS_BFILM_VIS type ZVCXI_FBSS_S_BFILM_VIS
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_PBO
    redefinition .
protected section.

  data R_PR_BFILM type ref to ZCL_VCXI_FBSS_PR_BFILM .
  data R_TOOLBAR type ref to ZCL_VCXI_F0S_TOOLBAR_PRVDR .
  data R_PICTURE_RHNDL type ref to ZCL_VCXI_FFS_PICTURE_RHNDL .

  methods INIT_VIS
    raising
      /VCXI/CX_CKX .
  methods DISPLAY_PICTURE
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
private section.
endclass. "ZCL_VCXI_FBSS_UI_BFILM definition
class ZCL_VCXI_FBSS_UI_BFILM implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_FBSS_UI_BFILM_BIND'
      exporting
        IR_UI_BFILM   = ME
      importing
        ES_DYNP       = ME->S_DYNP
        EF_ACTIVE_TAB = ME->F_ACTIVE_TAB.

*** Create Roll Winding Picture
    if ME->R_PICTURE_RHNDL is not bound.
      create object ME->R_PICTURE_RHNDL
        exporting
          IR_GATE           = ME->R_GATE
          IR_UIGATE         = ME->R_UIGATE
          IF_CONTAINER_NAME = '0100CC_PICTURE'.
    endif.

*** Create Toolbar for Provider
    create object ME->R_TOOLBAR
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_BF_TOOLBAR'.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE
                        IF_TABPT  = IF_TABPT ).
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_BFILM.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

*** Free Toolbar
    if ME->R_TOOLBAR is bound.
      ME->R_TOOLBAR->DO_FREE( ).
      clear ME->R_TOOLBAR.
    endif.

*** Free Roll Winding Picture
    if ME->R_PICTURE_RHNDL is bound.
      ME->R_PICTURE_RHNDL->DO_FREE( ).
      clear ME->R_PICTURE_RHNDL.
    endif.

  endmethod.
  method DISPLAY_PICTURE.

    data: LF_RWSID type ZVCXI_FFS_RWSID,
          LF_RWDIR type ZVCXI_FFS_RWDIR,
          LF_EYEMP type ZVCXI_FFS_EYEMP.

***--------------------------------------------------------------------------------------
*** Get Roll Winding Information
    ME->R_PR_BFILM->GET_RWSID( importing EF_RWSID = LF_RWSID ).
    ME->R_PR_BFILM->GET_RWDIR( importing EF_RWDIR = LF_RWDIR ).
    ME->R_PR_BFILM->GET_EYEMP( importing EF_EYEMP = LF_EYEMP ).

*** Refresh Winding Picture
    ME->R_PICTURE_RHNDL->REFRESH_PICTURE( IF_EYEMP = LF_EYEMP
                                          IF_RWDIR = LF_RWDIR
                                          IF_RWSID = LF_RWSID ).

  endmethod.
  method DO_MODIFY_SCREEN.

    data: LR_CX_CKX type ref to /VCXI/CX_CKX.

    try.
        SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                 IF_DYNNR = IF_DYNNR ).

        loop at screen.
***       Reopen Info Buttons
          if SCREEN-GROUP1 eq 'INF'.
            move 1 to SCREEN-INPUT.
          endif.
          modify screen.
        endloop.

      catch /VCXI/CX_CKX into LR_CX_CKX.
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_PBO.

    data: LT_PRVDR_SELECTED type /VCXI/VKSR_TT_GATE.

    SUPER->DO_PBO( ).

*** Initialize Visualization Structure
    ME->INIT_VIS( ).

*** Display Picture of Winding Direction
    ME->DISPLAY_PICTURE( ).

*** Initialize Toolbar and provide active tab as Selected Provider (for inserting/deletion)
    append ME->R_OLD_GATE to LT_PRVDR_SELECTED.
    ME->R_TOOLBAR->REFRESH_TOOLBAR( IT_PRVDR_SELECTED = LT_PRVDR_SELECTED ).

  endmethod.
  method DO_UPDATE_BFILM_VIS.

    check ME->R_PR_BFILM is bound.

***--------------------------------------------------------------------------------------
*** Set Number of Film
    if ME->S_BFILM_VIS-NOFIL ne IS_BFILM_VIS-NOFIL.
      ME->R_PR_BFILM->SET_NOFIL( IF_NOFIL = IS_BFILM_VIS-NOFIL
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Number of Ups
    if ME->S_BFILM_VIS-NOUPS ne IS_BFILM_VIS-NOUPS.
      ME->R_PR_BFILM->SET_NOUPS( IF_NOUPS = IS_BFILM_VIS-NOUPS
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Trim on Width
    if ME->S_BFILM_VIS-S_TRWID ne IS_BFILM_VIS-S_TRWID.
      ME->R_PR_BFILM->SET_TRWID( IS_TRWID = IS_BFILM_VIS-S_TRWID
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Trim on Length
    if ME->S_BFILM_VIS-S_TRLEN ne IS_BFILM_VIS-S_TRLEN.
      ME->R_PR_BFILM->SET_TRLEN( IS_TRLEN = IS_BFILM_VIS-S_TRLEN
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Winding Side
    if ME->S_BFILM_VIS-RWSID ne IS_BFILM_VIS-RWSID.
      ME->R_PR_BFILM->SET_RWSID( exporting IF_RWSID = IS_BFILM_VIS-RWSID
                                           IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Winding Direction
    if ME->S_BFILM_VIS-RWDIR ne IS_BFILM_VIS-RWDIR.
      ME->R_PR_BFILM->SET_RWDIR( exporting IF_RWDIR = IS_BFILM_VIS-RWDIR
                                           IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Eyemark Position
    if ME->S_BFILM_VIS-EYEMP ne IS_BFILM_VIS-EYEMP.
      ME->R_PR_BFILM->SET_EYEMP( exporting IF_EYEMP = IS_BFILM_VIS-EYEMP
                                           IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_BFILM.

  endmethod.
  method INIT_VIS.

    free: ME->S_BFILM_VIS.
    check ME->R_PR_BFILM is bound.

***--------------------------------------------------------------------------------------
*** Number of Film
    ME->R_PR_BFILM->GET_NOFIL( importing EF_NOFIL = ME->S_BFILM_VIS-NOFIL ).

***--------------------------------------------------------------------------------------
*** Number of Ups
    ME->R_PR_BFILM->GET_NOUPS( importing EF_NOUPS = ME->S_BFILM_VIS-NOUPS ).

*** Trim on Width
    ME->R_PR_BFILM->GET_TRWID( importing ES_TRWID = ME->S_BFILM_VIS-S_TRWID ).

*** Film Width (Outbound)
    ME->R_PR_BFILM->GET_FIWID( importing ES_FIWID = ME->S_BFILM_VIS-S_FIWID ).

***--------------------------------------------------------------------------------------
*** Trim on Length
    ME->R_PR_BFILM->GET_TRLEN( importing ES_TRLEN = ME->S_BFILM_VIS-S_TRLEN ).

*** Film Length (Outbound)
    ME->R_PR_BFILM->GET_FILEN( importing ES_FILEN = ME->S_BFILM_VIS-S_FILEN ).

***--------------------------------------------------------------------------------------
*** Surface Weight (Outbound)
    ME->R_PR_BFILM->GET_SURWT( importing ES_SURWT = ME->S_BFILM_VIS-S_SURWT ).
    move ME->S_BFILM_VIS-SURWT_UOM to ME->S_BFILM_VIS-SURWT_UOMC6.

*** Internal Film Thickness (Outbound)
    ME->R_PR_BFILM->GET_FITIN( importing ES_FITIN = ME->S_BFILM_VIS-S_FITIN ).

*** External Film Thickness (Outbound)
    ME->R_PR_BFILM->GET_FITEX( importing ES_FITEX = ME->S_BFILM_VIS-S_FITEX ).

***--------------------------------------------------------------------------------------
*** Get Winding Side
    ME->R_PR_BFILM->GET_RWSID( importing EF_RWSID = ME->S_BFILM_VIS-RWSID ).

*** Get Winding Direction
    ME->R_PR_BFILM->GET_RWDIR( importing EF_RWDIR = ME->S_BFILM_VIS-RWDIR ).

*** Get Eyemark Position
    ME->R_PR_BFILM->GET_EYEMP( importing EF_EYEMP = ME->S_BFILM_VIS-EYEMP ).

  endmethod.
