
class ZCL_VCXI_PCSS_UI_SPEC_CR definition
  public
  inheriting from /VCXI/CL_VKSRU_CLSUI
  create public .

public section.

  interfaces /VCXI/IF_VKSU_UIFW .

  data F_ACTIVE_TAB type STRING .
  data S_SPEC_TAB type ZVCXI_PCSS_S_TAB_DESCR .
  data S_SPEC_CR_VIS type ZVCXI_PCSS_S_SPEC_CR_VIS .
  data F_ACTIVE_TAB_OLD type STRING .
  data R_DEFAULT_GATE type ref to /VCXI/CL_VKSR_GATE .
  data F_CURSOR type SCRFNAME .

  type-pools ABAP .
  methods SHLP_BOARW
    importing
      !IF_DISPLAY type ABAP_BOOL
    changing
      !CS_BOARW type ZVCXI_PCSS_S_BOARW
    raising
      /VCXI/CX_CKX .
  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_SPEC_CR_VIS
    importing
      !IS_SPEC_CR_VIS type ZVCXI_PCSS_S_SPEC_CR_VIS
    raising
      /VCXI/CX_CKX .
  methods GET_REGISTERED_UI_INFO
    returning
      value(RS_DYNP) type /VCXI/CKX_S_DYNP_KEY
    exceptions
      /VCXI/CX_CKX .
  methods SET_CURSOR
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  constants C_TAB type STRING value 'TAB'. "#EC NOTEXT
  constants C_TABNAME type STRING value '0100S_TAB-TAB'. "#EC NOTEXT
  data F_TAB_LIST_REFRESH_REQ type ABAP_BOOL .
  data R_PR_SPEC_CR type ref to ZCL_VCXI_PCSS_PR_SPEC_CR .
  data R_REGISTERED_UI type ref to /VCXI/CL_VKSU_CLSUI .
  data S_DYNP_BLANK type /VCXI/CKX_S_DYNP_KEY .
  data T_TAB_LIST type ZVCXI_PCSS_TT_TAB_LIST .
  data F_ALREADY_OPEN type ABAP_BOOL .
  data R_OLD_GATE type ref to /VCXI/CL_VKSR_GATE .

  methods DISPLAY_ACTIVE_TAB
    raising
      /VCXI/CX_CKX .
  methods INIT_TAB_LIST
    raising
      /VCXI/CX_CKX .
  methods INIT_VIS
    raising
      /VCXI/CX_CKX .
  methods OPEN_SUBSCREEN
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    returning
      value(RR_CLSUI) type ref to /VCXI/CL_VKSRU_CLSUI
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
private section.

  methods INIT_BLANK_UI
    raising
      /VCXI/CX_CKX .
endclass. "ZCL_VCXI_PCSS_UI_SPEC_CR definition
class ZCL_VCXI_PCSS_UI_SPEC_CR implementation.
method /VCXI/IF_VKSU_UIFW~DO_MODIFY_SCREEN.

  if ME->R_REGISTERED_UI is bound.
    ME->R_REGISTERED_UI->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                           IF_DYNNR = IF_DYNNR ).
  endif.


endmethod.
method /VCXI/IF_VKSU_UIFW~DO_USER_COMMAND.

  data:  LR_CX_CKX type ref to /VCXI/CX_CKX.

  try.
      if ME->R_REGISTERED_UI is bound.
        ME->R_REGISTERED_UI->DO_USER_COMMAND( IF_UCOMM = CF_UCOMM ).
      endif.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      LR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

endmethod.
method /VCXI/IF_VKSU_UIFW~REFRESH_SCREEN.

  ME->R_UIFW->REFRESH_SCREEN( ).

endmethod.
method /VCXI/IF_VKSU_UIFW~REGISTER_UI.

  do.
    if ME->R_REGISTERED_UI is not bound.
      exit.
    endif.
    ME->R_REGISTERED_UI->DEACTIVATE( ).
  enddo.

  move IR_UI to ME->R_REGISTERED_UI.

endmethod.
method /VCXI/IF_VKSU_UIFW~UNREGISTER_UI.

  if IR_UI eq ME->R_REGISTERED_UI.
    clear ME->R_REGISTERED_UI.
  endif.

endmethod.
method ACTIVATE.

  SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
  call function 'ZVCXI_PCSS_UI_SPEC_CR_BIND'
    exporting
      IR_UI_SPEC_CR = ME
    importing
      ES_DYNP       = ME->S_DYNP
      EF_ACTIVE_TAB = ME->F_ACTIVE_TAB.

  if ME->R_REGISTERED_UI is bound.
    ME->R_REGISTERED_UI->ACTIVATE( ).
  endif.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                      IR_UIGATE = IR_UIGATE
                      IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
  try.
      move IR_GATE->R_CLSPR ?to ME->R_PR_SPEC_CR.
    catch CX_SY_MOVE_CAST_ERROR.
***   UI ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '801'.
  endtry.


*** Request Init List for Tabs
  move ABAP_TRUE to ME->F_TAB_LIST_REFRESH_REQ.

*** Initialize Blank UI
  ME->INIT_BLANK_UI( ).

endmethod.
method DEACTIVATE.

  SUPER->DEACTIVATE( ).

  if ME->R_REGISTERED_UI is bound.
    ME->R_REGISTERED_UI->DEACTIVATE( ).
  endif.

  clear: ME->F_ALREADY_OPEN.

endmethod.
method DISPLAY_ACTIVE_TAB.

  data:  LS_TAB_LIST    type        ZVCXI_XCSP_S_TAB_LIST.
  data:  LR_GATE        type ref to /VCXI/CL_VKSR_GATE.

  check ME->F_ACTIVE_TAB cs C_TABNAME.

  check ME->F_ACTIVE_TAB   ne ME->F_ACTIVE_TAB_OLD
     or ME->F_ALREADY_OPEN eq ABAP_FALSE.

  read table ME->T_TAB_LIST into LS_TAB_LIST with key TABNAME = ME->F_ACTIVE_TAB.
  if SY-SUBRC eq 0.
    move LS_TAB_LIST-R_GATE to LR_GATE.
  endif.

  R_OLD_GATE = LR_GATE.

  ME->OPEN_SUBSCREEN( IR_GATE = LR_GATE ).

endmethod.
method DO_MODIFY_SCREEN.

  data:  LF_LOCKED type        /VCXI/CKX_LOCKED,
         LF_INUSE  type        /VCXI/VKSR_INUSE.
  data:  LR_CX_CKX type ref to /VCXI/CX_CKX.

  try.
      SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                               IF_DYNNR = IF_DYNNR ).

      LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
      LF_INUSE  = ME->R_GATE->IS_INUSE( ).

      loop at screen.
***     Set the Tabs which aren't in use as invisible.
        if SCREEN-NAME cs C_TABNAME.
          read table ME->T_TAB_LIST with key TABNAME = SCREEN-NAME transporting no fields.
          if SY-SUBRC ne 0.
            move 1 to SCREEN-INVISIBLE.
          endif.
        endif.

        modify screen.
      endloop.

***     Modify Screen using DynamicVC
      ZCL_VCXI_XCDR_SERVICE=>MODIFY_SCREEN( IR_GATE = ME->R_GATE ).


***   Lock everything if not in change mode
      loop at screen.

***     Get Override Roll Diameter to make it editable or not
        if SCREEN-GROUP1 = 'ORD'.
          if ME->R_PR_SPEC_CR->GET_FOVRR_RODIA( ) eq ABAP_FALSE.
            move 0 to SCREEN-INPUT.
          endif.
        endif.

***     Self Deckling Flag is not editable if we have it mandatory for the BoardID
***     or if we have Preprint selected!
        if SCREEN-GROUP1                             eq 'FSD' and
         ( ME->R_PR_SPEC_CR->GET_BOARD_CUST_NSTBG( ) eq ABAP_TRUE or
           ME->R_PR_SPEC_CR->GET_FPRPR( )            eq ABAP_TRUE ).
          SCREEN-INPUT = 0.
        endif.

        if LF_LOCKED ne ABAP_TRUE or
           LF_INUSE  ne ABAP_TRUE.
          move 0 to SCREEN-INPUT.
        endif.
        modify screen.
      endloop.

      loop at screen.
***     Set tabs back to input
        if SCREEN-NAME cs C_TABNAME.
          move 1 to SCREEN-INPUT.
        endif.
        modify screen.
      endloop.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

endmethod.
method DO_PBO.

*** Check if the PR Class is bound
  check ME->R_PR_SPEC_CR is bound.

  if ME->F_ACTIVE_TAB ne ME->F_ACTIVE_TAB_OLD.
    move ME->F_ACTIVE_TAB to ME->F_ACTIVE_TAB_OLD.
    clear: ME->F_ALREADY_OPEN.
  endif.

  ME->INIT_VIS( ).
  ME->INIT_TAB_LIST( ).
  ME->DISPLAY_ACTIVE_TAB( ).

  set cursor field ME->F_CURSOR.
  clear ME->F_CURSOR.

endmethod.
method DO_UPDATE_SPEC_CR_VIS.

  data: LS_ROWID      type        ZVCXI_PCS_S_ROWID,
        LS_ROLEN      type        ZVCXI_PCS_S_ROLEN,
        LS_RODIA      type        ZVCXI_PCS_S_RODIA,
        LS_CODIA      type        ZVCXI_PCS_S_CODIA,
        LS_BOARW      type        ZVCXI_PCSS_S_BOARW.

  move-corresponding IS_SPEC_CR_VIS to:
                     LS_ROWID, LS_ROLEN,
                     LS_RODIA, LS_CODIA.

*** Set Material Description and Customer Material Number
  if ME->S_SPEC_CR_VIS-MATDE ne IS_SPEC_CR_VIS-MATDE.
    ME->R_PR_SPEC_CR->SET_MATDE( IF_MATDE = IS_SPEC_CR_VIS-MATDE
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

  if ME->S_SPEC_CR_VIS-CMATN ne IS_SPEC_CR_VIS-CMATN.
    ME->R_PR_SPEC_CR->SET_CMATN( IF_CMATN = IS_SPEC_CR_VIS-CMATN
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Set Product Type
  if ME->S_SPEC_CR_VIS-PTYPE ne IS_SPEC_CR_VIS-PTYPE.
    ME->R_PR_SPEC_CR->SET_PTYPE( IF_PTYPE = IS_SPEC_CR_VIS-PTYPE
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Set Roll Wdith
  if ME->S_SPEC_CR_VIS-ROWID ne LS_ROWID-ROWID.
    ME->R_PR_SPEC_CR->SET_ROWID( IS_ROWID = LS_ROWID
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Set Roll Length
  if ME->S_SPEC_CR_VIS-ROLEN ne LS_ROLEN-ROLEN.
    ME->R_PR_SPEC_CR->SET_ROLEN( IS_ROLEN = LS_ROLEN
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Set Roll Diameter
  if ME->R_PR_SPEC_CR->GET_FOVRR_RODIA( ) eq ABAP_TRUE.
    if ME->S_SPEC_CR_VIS-RODIA ne LS_RODIA-RODIA.
      ME->R_PR_SPEC_CR->SET_RODIA( IS_RODIA = LS_RODIA
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.
  endif.

*** Set Roll Diameter override Flag
  if ME->S_SPEC_CR_VIS-FOVRR_RODIA ne IS_SPEC_CR_VIS-FOVRR_RODIA.
    ME->R_PR_SPEC_CR->SET_FOVRR_RODIA( IF_FOVRR_RODIA = IS_SPEC_CR_VIS-FOVRR_RODIA ).
  endif.

*** Set Core Diameter
  if ME->S_SPEC_CR_VIS-CODIA ne LS_CODIA-CODIA.
    ME->R_PR_SPEC_CR->SET_CODIA( IS_CODIA = LS_CODIA
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Set Board Information
  if ME->S_SPEC_CR_VIS-SABOA ne IS_SPEC_CR_VIS-SABOA.
    ME->R_PR_SPEC_CR->SET_SABOA( IF_SABOA = IS_SPEC_CR_VIS-SABOA
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

  if ME->S_SPEC_CR_VIS-PRBOA ne IS_SPEC_CR_VIS-PRBOA.
    ME->R_PR_SPEC_CR->SET_PRBOA( IF_PRBOA = IS_SPEC_CR_VIS-PRBOA
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

  if ME->S_SPEC_CR_VIS-SELFD ne IS_SPEC_CR_VIS-SELFD.
    ME->R_PR_SPEC_CR->SET_SELFD( IF_SELFD = IS_SPEC_CR_VIS-SELFD
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Set Preprint
  if ME->S_SPEC_CR_VIS-FPRPR ne IS_SPEC_CR_VIS-FPRPR.
    ME->R_PR_SPEC_CR->SET_FPRPR( IF_FPRPR = IS_SPEC_CR_VIS-FPRPR ).
  endif.

*** Update Context
  if ME->S_SPEC_CR_VIS-ICONT ne IS_SPEC_CR_VIS-ICONT.
    ME->R_PR_SPEC_CR->SET_ICONT( IF_ICONT = IS_SPEC_CR_VIS-ICONT
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

***--------------------------------------------------------------------------------------
*** Update Self Deckeling Information - Board Grade Width if needed
  if ME->R_PR_SPEC_CR->GET_FOVRS( ) eq ABAP_TRUE.
    if ME->S_SPEC_CR_VIS-BOARW ne IS_SPEC_CR_VIS-BOARW.
      move-corresponding IS_SPEC_CR_VIS to LS_BOARW.
      ME->R_PR_SPEC_CR->SET_BOARW( IS_BOARW = LS_BOARW
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.
  endif.

*** Set Selfdeckling override Flag
  if ME->S_SPEC_CR_VIS-FOVRS ne IS_SPEC_CR_VIS-FOVRS.
    ME->R_PR_SPEC_CR->SET_FOVRS( IF_FOVRS = IS_SPEC_CR_VIS-FOVRS ).
  endif.

endmethod.
method DO_USER_COMMAND.

  data:  LF_UCOMM      type SYUCOMM.

  SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

  if IF_UCOMM cs C_TABNAME.
    move IF_UCOMM to ME->F_ACTIVE_TAB.
  endif.

  move IF_UCOMM to LF_UCOMM.
  ME->/VCXI/IF_VKSU_UIFW~DO_USER_COMMAND( changing CF_UCOMM = LF_UCOMM ).

endmethod.
method GET_REGISTERED_UI_INFO.

  try.
      if ME->R_REGISTERED_UI is bound.
        move ME->R_REGISTERED_UI->S_DYNP to RS_DYNP.
      else.
        move ME->S_DYNP_BLANK-REPID    to RS_DYNP-REPID.
        move ME->S_DYNP_BLANK-DYNNR    to RS_DYNP-DYNNR.
      endif.

    catch /VCXI/CX_CKX.
      move ME->S_DYNP_BLANK-REPID    to RS_DYNP-REPID.
      move ME->S_DYNP_BLANK-DYNNR    to RS_DYNP-DYNNR.
  endtry.

endmethod.
method HANDLE_GATE_FREED.

  SUPER->HANDLE_GATE_FREED( ).

  free: ME->R_PR_SPEC_CR.

endmethod.
method INIT_BLANK_UI.

  data:  LR_CLSUI type ref to /VCXI/CL_VKSU_CLSUI.

  create object LR_CLSUI
    exporting
      IR_UIFW = ME.

  LR_CLSUI->ACTIVATE( ).
  move LR_CLSUI->S_DYNP to ME->S_DYNP_BLANK.

endmethod.
method INIT_TAB_LIST.

  data:           LF_TAB             type         NUMC3,
                  LF_FNAME           type         STRING.
  data:           LS_PRVDR_DEF       type         /VCXI/VKSR_S_PRVDR_DEF,
                  LS_TAB_LIST        type         ZVCXI_PCSS_S_TAB_LIST.
  data:           LT_GATE            type         /VCXI/VKSR_TT_GATE,
                  LT_GATE_CHILD      type         /VCXI/VKSR_TT_GATE,
                  LT_PRVDR_DEF       type         /VCXI/VKSR_TT_PRVDR_DEF.
  data:           LR_GATE            type ref to  /VCXI/CL_VKSR_GATE.
  field-symbols:  <S_TAB_LIST>       type         ZVCXI_PCSS_S_TAB_LIST,
                  <F_DESCR>          type         /VCXI/CKX_DESCR.

*** Save "Gate" of selected tab befor refresh
  if ME->R_DEFAULT_GATE is not bound and
     ME->F_ACTIVE_TAB   is not initial.
    read table ME->T_TAB_LIST into LS_TAB_LIST with key TABNAME = ME->F_ACTIVE_TAB.
    if LS_TAB_LIST-R_GATE          is bound and
       LS_TAB_LIST-R_GATE->F_FREED eq ABAP_FALSE.
      move LS_TAB_LIST-R_GATE to ME->R_DEFAULT_GATE.
    endif.
  endif.

  free: ME->T_TAB_LIST,
        ME->S_SPEC_TAB,
        LF_TAB.

***------------------------------------------------------------------
*** Get all Children
  LT_PRVDR_DEF = ME->R_GATE->GET_PRVDR_DEF( IF_TPRVR = /VCXI/IF_VKSC_REGELMNT_PRVTY=>C_TPRVR_CHILD ).

  loop at LT_PRVDR_DEF into LS_PRVDR_DEF.
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = LS_PRVDR_DEF-IPRVT
                                     IF_INUSE = ABAP_TRUE ).

    append lines of LT_GATE to LT_GATE_CHILD.
  endloop.
  sort LT_GATE_CHILD.
  delete adjacent duplicates from LT_GATE_CHILD.

***------------------------------------------------------------------
*** Sort Children
  ME->R_GATE->SORT_GATE( changing CT_GATE = LT_GATE_CHILD ).

  loop at LT_GATE_CHILD into LR_GATE.

    check LR_GATE->GET_CLSUI( ) is not initial.

    append initial line to ME->T_TAB_LIST assigning <S_TAB_LIST>.
    add 1 to LF_TAB.
    concatenate C_TABNAME LF_TAB into <S_TAB_LIST>-TABNAME.
    move LR_GATE                   to <S_TAB_LIST>-R_GATE.

*** Set Activ Tab if it was set by default Gate
    if ME->R_DEFAULT_GATE is bound and
       LR_GATE eq ME->R_DEFAULT_GATE.
      move <S_TAB_LIST>-TABNAME to ME->F_ACTIVE_TAB.
      move <S_TAB_LIST>-TABNAME to ME->F_ACTIVE_TAB_OLD.
      if LR_GATE ne ME->R_OLD_GATE.
        move ABAP_FALSE to ME->F_ALREADY_OPEN.
      endif.
*** Default gate is not bound but Gate of Activ Tab do not match
    elseif ME->R_DEFAULT_GATE is not bound.
      if ME->F_ACTIVE_TAB eq <S_TAB_LIST>-TABNAME and
         ME->R_OLD_GATE   ne LR_GATE.
        move ABAP_FALSE to ME->F_ALREADY_OPEN.
      endif.
    endif.

    concatenate C_TAB LF_TAB into LF_FNAME.
    assign component LF_FNAME of structure ME->S_SPEC_TAB to <F_DESCR>.
    if SY-SUBRC eq 0.
      if <S_TAB_LIST>-R_GATE->R_CLSPR is bound.
        move <S_TAB_LIST>-R_GATE->R_CLSPR->GET_DESCR( ) to <F_DESCR>.
      else.
        move <S_TAB_LIST>-R_GATE->GET_DESCR( ) to <F_DESCR>.
      endif.
    endif.
  endloop.

***------------------------------------------------------------------
*** Clear Default gate
  clear: ME->R_DEFAULT_GATE.

*** Check Active Tab exists
  if ME->F_ACTIVE_TAB cs C_TABNAME.
    read table ME->T_TAB_LIST with key TABNAME = ME->F_ACTIVE_TAB
                              transporting no fields.
    if SY-SUBRC ne 0.
      read table ME->T_TAB_LIST assigning <S_TAB_LIST> index 1.
      if SY-SUBRC eq 0.
        move <S_TAB_LIST>-TABNAME to ME->F_ACTIVE_TAB.
      else.
        clear ME->F_ACTIVE_TAB.
      endif.
    endif.
  endif.

endmethod.
method INIT_VIS.

  data: LF_VALID_TBOAR  type        CHAR100,
        LF_DCONF        type        DATS,
        LF_TPRST        type        ZVCXI_P0SS_TPRST,
        LF_PRSTY        type        ZVCXI_P0SS_PRSTY.
  data: LS_ROWID        type        ZVCXI_PCS_S_ROWID,
        LS_ROLEN        type        ZVCXI_PCS_S_ROLEN,
        LS_RODIA        type        ZVCXI_PCS_S_RODIA,
        LS_CODIA        type        ZVCXI_PCS_S_CODIA,
        LS_SIDRU        type        ZVCXI_P0SS_S_SIDRU,
        LS_BOARW        type        ZVCXI_PCSS_S_BOARW.

  free: ME->S_SPEC_CR_VIS.

*** Material Description and Customer Material number
  ME->S_SPEC_CR_VIS-MATDE         = ME->R_PR_SPEC_CR->GET_MATDE( ).
  ME->S_SPEC_CR_VIS-CMATN         = ME->R_PR_SPEC_CR->GET_CMATN( ).

*** Get Product Type
  ME->S_SPEC_CR_VIS-PTYPE         = ME->R_PR_SPEC_CR->GET_PTYPE( ).
  ME->S_SPEC_CR_VIS-PTYPE_DESCR   = ME->R_PR_SPEC_CR->GET_PTYPE_DESCR( ).

*** Get Roll Width
  LS_ROWID                     = ME->R_PR_SPEC_CR->GET_ROWID( ).
  move-corresponding LS_ROWID to ME->S_SPEC_CR_VIS.

*** Get Roll Length
  LS_ROLEN                     = ME->R_PR_SPEC_CR->GET_ROLEN( ).
  move-corresponding LS_ROLEN to ME->S_SPEC_CR_VIS.

*** Get Roll Diametr and Override Flag
  ME->R_PR_SPEC_CR->GET_RODIA( importing ES_RODIA = LS_RODIA ).
  move-corresponding LS_RODIA to ME->S_SPEC_CR_VIS.
  ME->S_SPEC_CR_VIS-FOVRR_RODIA  = ME->R_PR_SPEC_CR->GET_FOVRR_RODIA( ).

*** Get Core Diameter
  LS_CODIA                     = ME->R_PR_SPEC_CR->GET_CODIA( ).
  move-corresponding LS_CODIA to ME->S_SPEC_CR_VIS.

*** Get Board Information
  ME->S_SPEC_CR_VIS-SABOA         = ME->R_PR_SPEC_CR->GET_SABOA( ).
  ME->S_SPEC_CR_VIS-SABOA_DESCR   = ME->R_PR_SPEC_CR->GET_SABOA_DESCR( ).

  ME->S_SPEC_CR_VIS-PRBOA         = ME->R_PR_SPEC_CR->GET_PRBOA( ).
  ME->S_SPEC_CR_VIS-PRBOA_DESCR   = ME->R_PR_SPEC_CR->GET_PRBOA_DESCR( ).

  ME->S_SPEC_CR_VIS-FLUTE         = ME->R_PR_SPEC_CR->GET_FLUTE( ).
  ME->S_SPEC_CR_VIS-FLUTE_DESCR   = ME->R_PR_SPEC_CR->GET_FLUTE_DESCR( ).

  ME->S_SPEC_CR_VIS-SELFD         = ME->R_PR_SPEC_CR->GET_SELFD( ).

*** Get Flag of Preprint
  ME->S_SPEC_CR_VIS-FPRPR         = ME->R_PR_SPEC_CR->GET_FPRPR( ).

*** Get Context
  ME->S_SPEC_CR_VIS-ICONT         = ME->R_PR_SPEC_CR->GET_ICONT( ).
  ME->S_SPEC_CR_VIS-ICONT_DESCR   = ME->R_PR_SPEC_CR->GET_ICONT_DESCR( ).

***--------------------------------------------------------------------------------------
*** Get Board width and Side Run
  ME->R_PR_SPEC_CR->GET_BOARW( importing ES_BOARW = LS_BOARW ).
  ME->R_PR_SPEC_CR->GET_SIDRU( importing ES_SIDRU = LS_SIDRU ).
  move-corresponding LS_BOARW               to ME->S_SPEC_CR_VIS.
  move-corresponding LS_SIDRU               to ME->S_SPEC_CR_VIS.
  move ME->R_PR_SPEC_CR->GET_FOVRS( )       to ME->S_SPEC_CR_VIS-FOVRS.

*** Set Parameter Valid Board Types
  LF_VALID_TBOAR                  = ME->R_PR_SPEC_CR->GET_VALID_TBOAR( ).
  set parameter id 'ZVCXI_PCS_TBOAR' field LF_VALID_TBOAR.

*** Set Parameter Date Config
  LF_DCONF                        = ME->R_PR_SPEC_CR->GET_DCONF( ).
  set parameter id 'ZVCXI_XCS_DCONF' field LF_DCONF.

*** Clear Fefco Parameters
  LF_TPRST                        = ME->R_PR_SPEC_CR->GET_TPRST( ).
  LF_PRSTY                        = ME->R_PR_SPEC_CR->GET_PRSTY( ).
  set parameter id 'ZVCXI_P0SS_TPRST' field LF_TPRST.
  set parameter id 'ZVCXI_P0SS_PRSTY' field LF_PRSTY.

endmethod.
method OPEN_SUBSCREEN.

  data:     LF_CLSUI  type        /VCXI/VKSC_CLSUI.

  if IR_GATE is bound.
    LF_CLSUI = IR_GATE->GET_CLSUI( ).
  endif.

  if LF_CLSUI is not initial.
*** Create Subscreen
    create object RR_CLSUI
      type
        (LF_CLSUI)
      exporting
        IR_UIFW    = ME
        IR_UIGATE  = ME->R_UIGATE
        IR_GATE    = IR_GATE.
    RR_CLSUI->ACTIVATE( ).
    ME->F_ALREADY_OPEN = ABAP_TRUE.
  else.
*** Clear Subscreen
    if ME->R_REGISTERED_UI is bound.
      ME->R_REGISTERED_UI->DEACTIVATE( ).
    endif.
  endif.

endmethod.
method SET_CURSOR.

  data:  LF_CURSOR type SCRFNAME.

  get cursor field LF_CURSOR.
  if LF_CURSOR is not initial.
    move LF_CURSOR to ME->F_CURSOR.
  endif.

endmethod.
method SHLP_BOARW.

  data:  LS_RETURN type DDSHRETVAL.
  data:  LT_COWID  type ZVCXI_PCS_TT_COWID,
         LT_RETURN type table of DDSHRETVAL.

*** Get all allowed Corrugation Width
  LT_COWID = ME->R_PR_SPEC_CR->GET_COWID_ALL( ).

*** Call Search Help
  call function 'F4IF_INT_TABLE_VALUE_REQUEST'
    exporting
      RETFIELD        = 'COWID'
      VALUE_ORG       = 'S'
      DISPLAY         = IF_DISPLAY
    tables
      VALUE_TAB       = LT_COWID
      RETURN_TAB      = LT_RETURN
    exceptions
      PARAMETER_ERROR = 1
      NO_VALUES_FOUND = 2
      others          = 3.

  if SY-SUBRC eq 0 and
     IF_DISPLAY ne ABAP_TRUE.
    read table LT_RETURN index 1 into LS_RETURN.
    if SY-SUBRC eq 0.
      try.
          call function 'RSDS_CONVERT_NUMBER'
            exporting
              INPUT  = LS_RETURN-FIELDVAL
            importing
              OUTPUT = CS_BOARW-BOARW.
        catch CX_ROOT.
      endtry.
    endif.
  endif.

endmethod.
