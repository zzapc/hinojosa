
class ZCL_VCXI_P3SS_UI_ASPART definition
  public
  inheriting from ZCL_VCXI_PXSS_UI_XPART
  create public .

public section.

  data S_ASPART_VIS type ZVCXI_P3SS_S_ASPART_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABPT type ZVCXI_XCS_TABPT default '0100S_ASP_TAB-TAB'
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
  methods DO_PBO
    redefinition .
protected section.

  constants C_UC_ASPROP type SYUCOMM value 'AFG_ASPROP' ##NO_TEXT.
  data R_PR_ASPART type ref to ZCL_VCXI_P3SS_PR_ASPART .
  data R_TOOLBAR type ref to ZCL_VCXI_XCS_TOOLBAR_PRVDR .

  methods GET_PRODIM_DESCR
    returning
      value(RF_DESCR) type /VCXI/CKX_DESCR
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_ASPROP
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_P3SS_UI_ASPART definition
class ZCL_VCXI_P3SS_UI_ASPART implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

***--------------------------------------------------------------------------------------
*** Bind UI to Dynpro
    call function 'ZVCXI_P3SS_UI_ASPART_BIND'
      exporting
        IR_UI_ASPART  = ME
      importing
        ES_DYNP       = ME->S_DYNP
        EF_ACTIVE_TAB = ME->F_ACTIVE_TAB.

***--------------------------------------------------------------------------------------
*** Create Toolbar to add Connection
    create object ME->R_TOOLBAR
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_TOOLBAR'.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE
                        IF_TABPT  = IF_TABPT ).

*** Assign th RP Class to the UI Class
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_ASPART.
      catch CX_SY_MOVE_CAST_ERROR.

***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

    if ME->R_TOOLBAR is bound.
      ME->R_TOOLBAR->DO_FREE( ).
      clear ME->R_TOOLBAR.
    endif.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Super PBO
    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize Toolbar
    if ME->R_TOOLBAR is bound.
      ME->R_TOOLBAR->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_TOOLBAR->REFRESH_TOOLBAR( IT_PRVDR_SELECTED = value #( ( ME->R_OLD_GATE ) ) ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_ASPART_VIS  type ZVCXI_P3SS_S_ASPART_VIS.


*** Super
    SUPER->DO_UPDATE_VIS( IS_VIS = IS_VIS ).
    check ME->R_PR_ASPART is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_ASPART_VIS.

***--------------------------------------------------------------------------------------
*** Set Product Length
    if ME->S_ASPART_VIS-S_PROLE ne LS_ASPART_VIS-S_PROLE.
      ME->R_PR_ASPART->SET_PROLE( IS_PROLE = LS_ASPART_VIS-S_PROLE
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Product Width
    if ME->S_ASPART_VIS-S_PROWI ne LS_ASPART_VIS-S_PROWI.
      ME->R_PR_ASPART->SET_PROWI( IS_PROWI = LS_ASPART_VIS-S_PROWI
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Product Height
    if ME->S_ASPART_VIS-S_PROHE ne LS_ASPART_VIS-S_PROHE.
      ME->R_PR_ASPART->SET_PROHE( IS_PROHE = LS_ASPART_VIS-S_PROHE
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    case IF_UCOMM.
      when C_UC_ASPROP.
        ME->HANDLE_UC_ASPROP( ).

    endcase.

  endmethod.
  method GET_PRODIM_DESCR.

*** Define Dimensions
    RF_DESCR = 'L/W/H'.

*** Full Description String
    RF_DESCR = 'Product Dim. ('(T01) && RF_DESCR && ')'.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_ASPART.

  endmethod.
  method HANDLE_UC_ASPROP.

*** Start Popup of Properties (Outbound)
    ZCL_VCXI_P3SS_POPUP_ASPROP=>START( IR_BL_ASBPROP = ME->R_PR_ASPART->GET_ASBPX( )
                                       IR_BL_ASFPROP = ME->R_PR_ASPART->GET_ASFPX( )
                                       IF_WERKS      = ME->R_PR_ASPART->GET_WERKS( ) ).

  endmethod.
  method INIT_VIS.

    data: LR_BL_ASFPROP type ref to ZCL_VCXI_P3SS_BL_ASFPROP.

    free: ME->S_ASPART_VIS.

    SUPER->INIT_VIS( ).
    ME->S_ASPART_VIS-S_XPART_VIS = ME->S_XPART_VIS.
    check ME->R_PR_ASPART is bound.

***------------------------------------------------------------------------------------------------
*** Get Product Dimension
    ME->S_ASPART_VIS-PRODIM_DESCR = ME->GET_PRODIM_DESCR( ).
    ME->S_ASPART_VIS-S_PROWI = ME->R_PR_ASPART->GET_PROWI( ).
    ME->S_ASPART_VIS-S_PROLE = ME->R_PR_ASPART->GET_PROLE( ).
    ME->S_ASPART_VIS-S_PROHE = ME->R_PR_ASPART->GET_PROHE( ).


***------------------------------------------------------------------------------------------------
*** Get Handling Dimension (Outbound)
    LR_BL_ASFPROP = ME->R_PR_ASPART->GET_ASFPX( ).
    if LR_BL_ASFPROP is bound.
      ME->S_ASPART_VIS-S_HNDDIM = LR_BL_ASFPROP->S_HNDDIM.
    endif.

  endmethod.
