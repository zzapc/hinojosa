
class ZCL_VCXI_XCSS_TI_DOC_MGR definition
  public
  inheriting from ZCL_VCXI_XCS_TI
  create public .

public section.

  methods /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ
    redefinition .
  methods /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL
    redefinition .
protected section.

  constants C_UC_LINK_DOC type UI_FUNC value 'LINK_DOC@' ##NO_TEXT.
  constants C_UC_CREATE_DOC type UI_FUNC value 'CREATE_DOC@' ##NO_TEXT.

  methods ADD_MENU_DOC
    importing
      !IR_MENU type ref to CL_CTMENU
      !IF_FCODE type UI_FUNC
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_CREATE_DOC
    importing
      !IF_FCODE type SYUCOMM
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_LINK_DOC
    importing
      !IF_FCODE type SYUCOMM
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_XCSS_TI_DOC_MGR definition
class ZCL_VCXI_XCSS_TI_DOC_MGR implementation.
  method /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ.

    data: LR_MENU_LINK   type ref to CL_CTMENU,
          LR_MENU_CREATE type ref to CL_CTMENU.


    SUPER->/VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ( IR_MENU = IR_MENU ).

    IR_MENU->ADD_SEPARATOR( ).

*** Add Menu for linking existing DMS Documents
    LR_MENU_LINK = new #( ).
    ME->ADD_MENU_DOC( IR_MENU  = LR_MENU_LINK
                      IF_FCODE = C_UC_LINK_DOC ).
    IR_MENU->ADD_SUBMENU( MENU = LR_MENU_LINK
                          TEXT = 'Link Document'(T01) ).

*** Add Menu for creating new DMS Documents
    LR_MENU_CREATE = new #( ).
    ME->ADD_MENU_DOC( IR_MENU  = LR_MENU_CREATE
                      IF_FCODE = C_UC_CREATE_DOC ).
    IR_MENU->ADD_SUBMENU( MENU = LR_MENU_CREATE
                          TEXT = 'Create Document'(T02) ).

  endmethod.
  method /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL.

    SUPER->/VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL( IF_FCODE = IF_FCODE ).

*** Link existing Document
    if IF_FCODE cs C_UC_LINK_DOC.
      ME->HANDLE_UC_LINK_DOC( IF_FCODE = IF_FCODE ).
    endif.

*** Create new Document
    if IF_FCODE cs C_UC_CREATE_DOC.
      ME->HANDLE_UC_CREATE_DOC( IF_FCODE = IF_FCODE ).
    endif.

  endmethod.
  method ADD_MENU_DOC .

    data: LT_PRVDR_DEF type /VCXI/VKSR_TT_PRVDR_DEF,
          LT_GATE      type /VCXI/VKSR_TT_GATE.
    field-symbols: <S_PRVDR_DEF> type /VCXI/VKSR_S_PRVDR_DEF.


    clear: LT_PRVDR_DEF.

*** Get possible Provider based on thier ID
    LT_PRVDR_DEF = ME->R_GATE->GET_PRVDR_DEF( IF_TPRVR = /VCXI/IF_VKSC_REGELMNT_PRVTY=>C_TPRVR_CHILD ).
    sort LT_PRVDR_DEF by ID.
    delete adjacent duplicates from LT_PRVDR_DEF comparing ID.
    sort LT_PRVDR_DEF by DESCR.

    loop at LT_PRVDR_DEF assigning <S_PRVDR_DEF>.
***   Get Gates of Provider
      LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = <S_PRVDR_DEF>-IPRVT
                                       IF_INUSE = ABAP_TRUE ).

***   Reduece possible Provider based on aready active Provider with searched ID
      loop at LT_GATE transporting no fields
                      where TABLE_LINE->F_ID eq <S_PRVDR_DEF>-ID.
        <S_PRVDR_DEF>-QTYMA = <S_PRVDR_DEF>-QTYMA - 1.
        if <S_PRVDR_DEF>-QTYMA lt 1.
          exit.
        endif.
      endloop.

***   Add Function
      IR_MENU->ADD_FUNCTION( FCODE    = IF_FCODE && <S_PRVDR_DEF>-ID
                             TEXT     = conv GUI_TEXT( <S_PRVDR_DEF>-DESCR )
                             DISABLED = switch #( <S_PRVDR_DEF>-QTYMA when 0 then ABAP_TRUE ) ).

    endloop.

  endmethod.
  method HANDLE_UC_CREATE_DOC .

    data: LT_GATE type /VCXI/VKSR_TT_GATE.
    data: LR_BL_DOC type ref to ZCL_VCXI_XCSS_BL_DOC.


***--------------------------------------------------------------------------------------
*** Call Pop-Up and Request Document Number, Part, Version
    LR_BL_DOC = ZCL_VCXI_XCSS_POPUP_DOC=>START( IF_DOKAR = SUBSTRING_AFTER( VAL = IF_FCODE SUB = C_UC_CREATE_DOC )  ).
    check LR_BL_DOC is bound.

*** Add Document using the DMS Document Manager
    LT_GATE = cast ZCL_VCXI_XCSS_PR_DOC_MGR( ME->R_GATE->R_CLSPR )->ADD_DOCS( IT_DOC_KEY = value #( ( LR_BL_DOC->GET_DOC_KEY( ) ) ) ).
    if LT_GATE is not initial.
***   Open UI
      ME->R_UIGATE->OPEN_SUBSCREEN( IR_GATE = LT_GATE[ 1 ] ).
    endif.
    ME->R_GATE->RAISE_EVENTS( ).

*** Request the Refresh
    ME->R_UIGATE->REQUEST_TREE_REFRESH( ).
    ME->R_UIGATE->REFRESH_SCREEN( ).

  endmethod.
  method HANDLE_UC_LINK_DOC .

    data: LT_GATE type /VCXI/VKSR_TT_GATE.
    data: LR_BL_DOC type ref to ZCL_VCXI_XCSS_BL_DOC.


***--------------------------------------------------------------------------------------
*** Call Pop-Up and Request Document Number, Part, Version
    LR_BL_DOC = ZCL_VCXI_XCSS_POPUP_DOC=>REQUEST_DOC_KEY( IF_DOKAR = SUBSTRING_AFTER( VAL = IF_FCODE SUB = C_UC_LINK_DOC )  ).
    check LR_BL_DOC is bound.

*** Add Document using the DMS Document Manager
    LT_GATE = cast ZCL_VCXI_XCSS_PR_DOC_MGR( ME->R_GATE->R_CLSPR )->ADD_DOCS( IT_DOC_KEY = value #( ( LR_BL_DOC->GET_DOC_KEY( ) ) ) ).
    if LT_GATE is not initial.
***   Open UI
      ME->R_UIGATE->OPEN_SUBSCREEN( IR_GATE = LT_GATE[ 1 ] ).
    endif.
    ME->R_GATE->RAISE_EVENTS( ).

*** Request the Refresh
    ME->R_UIGATE->REQUEST_TREE_REFRESH( ).
    ME->R_UIGATE->REFRESH_SCREEN( ).

  endmethod.
