
class ZCL_VCXI_XCSUC_SHLP_DFLT_PMATY definition
  public
  create public .

public section.

  type-pools ABAP .
  class-methods CALL_SHLP
    importing
      !IF_DISPLAY type ABAP_BOOL default ABAP_FALSE
      !IT_DFLT_PMATY type ZVCXI_XCSUC_TT_DFLT_PMATY_SHLP
      !IF_SYSOM type /VCXI/VKS_SYSOM default 'M'
      !IT_WERKS type T_WERKS optional
    returning
      value(RT_DFLT_PMATY) type ZVCXI_XCSUC_TT_DFLT_PMATY_SHLP
    raising
      /VCXI/CX_CKX .
protected section.
private section.
endclass. "ZCL_VCXI_XCSUC_SHLP_DFLT_PMATY definition
class ZCL_VCXI_XCSUC_SHLP_DFLT_PMATY implementation.
method CALL_SHLP.

  data: LF_WERKS type                   WERKS_D,
        LF_PMATY type                   ZVCXI_XCSU_PMATY.
  data: LS_PMATY_COMBI type                   ZVCXI_XCSU_S_PMATY_COMBI,
        LS_RETURN      type                   DDSHRETVAL,
        LS_MAP         type                   DSELC.
  data: LT_DFLT_PMATY_SHLP type                   ZVCXI_XCSUC_TT_DFLT_PMATY_SHLP,
        LT_PMATY_COMBI     type                   ZVCXI_XCSU_TT_PMATY_COMBI,
        LT_PMATY           type table of          ZVCXI_XCSU_PMATY,
        LT_CONSC           type                   ZVCXI_XCSU_TT_CONSC,
        LT_CONSC_ALL       type                   ZVCXI_XCSU_TT_CONSC,
        LT_RETURN          type table of          DDSHRETVAL,
        LT_MAP             type standard table of DSELC.
  data:          LR_CUST_SHIPU       type ref to            ZCL_VCXI_XCSU_CUST_SHIPU.
  field-symbols: <F_FIELD>           type                   ANY,
                 <S_DFLT_PMATY_SHLP> type                   ZVCXI_XCSUC_S_DFLT_PMATY_SHLP.

  LR_CUST_SHIPU = ZCL_VCXI_XCSU_CUST_SHIPU=>GET_INSTANCE( ).

  check LR_CUST_SHIPU is bound.
  LT_PMATY_COMBI = LR_CUST_SHIPU->GET_PMATY_COMBI( IF_SYSOM = IF_SYSOM ).

  loop at LT_PMATY_COMBI into LS_PMATY_COMBI.
    read table IT_DFLT_PMATY with key PMATY = LS_PMATY_COMBI-PMATY
                             transporting no fields.
    check SY-SUBRC ne 0.
    append initial line to LT_DFLT_PMATY_SHLP assigning <S_DFLT_PMATY_SHLP>.
    move-corresponding LS_PMATY_COMBI to <S_DFLT_PMATY_SHLP>.

*** Collect all Packaging Material Types
    collect LS_PMATY_COMBI-PMATY into LT_PMATY.
  endloop.

*** Get all Consumption Schemes valid for Plants and Packaging Material Type
  loop at LT_PMATY into LF_PMATY.
    loop at IT_WERKS into LF_WERKS.
      LT_CONSC = LR_CUST_SHIPU->GET_CONSC_FOR_PMATY( IF_PMATY = LF_PMATY
                                                     IF_WERKS = LF_WERKS ).

      append lines of LT_CONSC to LT_CONSC_ALL.
    endloop.
  endloop.

*** Reduce restricted Conscumption Schemes
  if IT_WERKS is not initial.
    loop at LT_DFLT_PMATY_SHLP assigning <S_DFLT_PMATY_SHLP>.
      read table LT_CONSC_ALL with key CONSC = <S_DFLT_PMATY_SHLP>-CONSC
                              transporting no fields.
      check SY-SUBRC ne 0.
      delete LT_DFLT_PMATY_SHLP.
    endloop.
  endif.

*** Start Searchhelp
  sort LT_DFLT_PMATY_SHLP by PMATY PMATN CONSC.

  LS_MAP-FLDNAME   = 'F0001'. "Field for Position 1
  LS_MAP-DYFLDNAME = 'PMATY'.
  append LS_MAP to LT_MAP.

  LS_MAP-FLDNAME   = 'F0002'. "Field for Position 2
  LS_MAP-DYFLDNAME = 'PMATN'.
  append LS_MAP to LT_MAP.

  LS_MAP-FLDNAME   = 'F0003'. "Field for Position 3
  LS_MAP-DYFLDNAME = 'CONSC'.
  append LS_MAP to LT_MAP.

  LS_MAP-FLDNAME   = 'F0004'. "Field for Position 4
  LS_MAP-DYFLDNAME = 'PMQTY_UOM'.
  append LS_MAP to LT_MAP.

  call function 'F4IF_INT_TABLE_VALUE_REQUEST'
    exporting
      RETFIELD        = 'PMATY'
      STEPL           = 1
      VALUE_ORG       = 'S'
      MULTIPLE_CHOICE = ABAP_FALSE
      DISPLAY         = IF_DISPLAY
    tables
      VALUE_TAB       = LT_DFLT_PMATY_SHLP
      DYNPFLD_MAPPING = LT_MAP
      RETURN_TAB      = LT_RETURN
    exceptions
      PARAMETER_ERROR = 1
      NO_VALUES_FOUND = 2
      others          = 3.

  if SY-SUBRC eq 0.
    sort LT_RETURN by RECORDPOS.
    loop at LT_RETURN into LS_RETURN.
      read table RT_DFLT_PMATY assigning <S_DFLT_PMATY_SHLP> index LS_RETURN-RECORDPOS.
      if SY-SUBRC ne 0.
        append initial line to RT_DFLT_PMATY assigning <S_DFLT_PMATY_SHLP>.
      endif.
      assign component LS_RETURN-RETFIELD of structure <S_DFLT_PMATY_SHLP> to <F_FIELD>.
      check SY-SUBRC = 0.
      move LS_RETURN-FIELDVAL to <F_FIELD>.
      case LS_RETURN-RETFIELD.
        when 'PMATN'.
***       Convert Material to Input
          call function 'CONVERSION_EXIT_MATN1_INPUT'
            exporting
              INPUT  = <F_FIELD>
            importing
              OUTPUT = <F_FIELD>.
        when 'PMQTY_UOM'.
***       Convert Unit to Input
          call function 'CONVERSION_EXIT_CUNIT_INPUT'
            exporting
              INPUT  = <F_FIELD>
            importing
              OUTPUT = <F_FIELD>.
      endcase.
      unassign <F_FIELD>.
    endloop.
  endif.

endmethod.
endclass. "ZCL_VCXI_XCSUC_SHLP_DFLT_PMATY implementation
