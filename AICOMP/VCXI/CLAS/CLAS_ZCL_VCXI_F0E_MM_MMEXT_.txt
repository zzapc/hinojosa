
class ZCL_VCXI_F0E_MM_MMEXT definition
  public
  inheriting from ZCL_VCXI_XCE_MM_MMEXT
  create public .

public section.
protected section.

  methods GET_ALTUOM
    importing
      !IR_SVCEL type ref to /VCXI/CL_VKSRA_SVC_ELEMENT
    returning
      value(RR_EA_ALTUOM) type ref to ZCL_VCXI_F0SS_EA_ALTUOM
    raising
      /VCXI/CX_CKX .
  methods GET_XFG
    importing
      !IR_SVCEL type ref to /VCXI/CL_VKSRA_SVC_ELEMENT
    returning
      value(RR_EA_XFG) type ref to ZCL_VCXI_F0SS_EA_XFG
    raising
      /VCXI/CX_CKX .

  methods UPDATE_KTEXT
    redefinition .
  methods UPDATE_MARA
    redefinition .
  methods UPDATE_MEINH
    redefinition .
private section.
endclass. "ZCL_VCXI_F0E_MM_MMEXT definition
class ZCL_VCXI_F0E_MM_MMEXT implementation.
  method GET_ALTUOM.

    data: LT_SVCEL type /VCXI/CKX_TT_OBJECT.
    data: LR_CLSEA  type ref to /VCXI/CL_VKSR_CLSEA,
          LR_OBJECT type ref to /VCXI/IF_CKX_OBJECT.

***--------------------------------------------------------------------------------------
*** Get Alternative UoM Element
    loop at IR_SVCEL->/VCXI/IF_CKX_OBJECT_H~GET_CHILDREN( ) into LR_OBJECT.
      LR_CLSEA = cast /VCXI/CL_VKSRA_SVC_ELEMENT( LR_OBJECT )->GET_CLSEA( ).
      if LR_CLSEA is bound and
         LR_CLSEA is instance of ZCL_VCXI_F0SS_EA_ALTUOM.
        RR_EA_ALTUOM ?= LR_CLSEA.
        exit.
      endif.
    endloop.

  endmethod.
  method GET_XFG.

*** Get Element API Reference
    try.
        RR_EA_XFG ?= IR_SVCEL->GET_CLSEA( ).
      catch CX_SY_MOVE_CAST_ERROR into data(LR_CX_SY_MOVE_CAST_ERROR).
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_SY_MOVE_CAST_ERROR ).
    endtry.

  endmethod.
  method UPDATE_KTEXT.

    data: LR_EA_XFG     type ref to ZCL_VCXI_F0SS_EA_XFG.


    SUPER->UPDATE_KTEXT( exporting IS_MARA  = IS_MARA
                                   IR_SVC   = IR_SVC
                                   IR_SVCEL = IR_SVCEL
                         changing  CT_KTEXT = CT_KTEXT ).

***--------------------------------------------------------------------------------------
*** Get Element API Reference
    LR_EA_XFG = ME->GET_XFG( IR_SVCEL = IR_SVCEL ).
    check LR_EA_XFG is bound.

    data(LF_MAKTX) = LR_EA_XFG->GET_MAKTX( ).

*** Update all Description
    loop at CT_KTEXT assigning field-symbol(<S_KTEXT>).
      <S_KTEXT>-MAKTX = LF_MAKTX.
    endloop.

  endmethod.
  method UPDATE_MARA.

    data: LF_UMREN type UMREN,
          LF_UMREZ type UMREZ.
    data: LR_EA_ALTUOM type ref to ZCL_VCXI_F0SS_EA_ALTUOM.


    SUPER->UPDATE_MARA( exporting IR_SVC   = IR_SVC
                                  IR_SVCEL = IR_SVCEL
                        changing  CS_MARA  = CS_MARA ).

***--------------------------------------------------------------------------------------
*** Get Alternative UoM Element
    LR_EA_ALTUOM = ME->GET_ALTUOM( IR_SVCEL = IR_SVCEL ).
    check LR_EA_ALTUOM is bound.

*** Calculate UMREZ/UMREN for Weight Unit
    LR_EA_ALTUOM->GET_UMREZ_UMREN( exporting IF_UOM   = CS_MARA-GEWEI
                                   importing EF_UMREN = LF_UMREN
                                             EF_UMREZ = LF_UMREZ ).

*** Calculate Net Weigth based on Conversion Factor
    if LF_UMREZ ne 0.
      CS_MARA-NTGEW = LF_UMREN / LF_UMREZ.
    else.
      clear CS_MARA-NTGEW.
    endif.

*** Gross equals Net Weight
    CS_MARA-BRGEW = CS_MARA-NTGEW.

  endmethod.
  method UPDATE_MEINH.

    data: LS_ALTUOM_DEF type ZVCXI_F0SS_S_ALTUOM_DEF,
          LS_MEINH_KG   type SMEINH.
    data: LR_EA_ALTUOM type ref to ZCL_VCXI_F0SS_EA_ALTUOM.
    field-symbols: <S_MEINH> type SMEINH.


***--------------------------------------------------------------------------------------
*** Get Alternative UoM Element
    LR_EA_ALTUOM = ME->GET_ALTUOM( IR_SVCEL = IR_SVCEL ).
    check LR_EA_ALTUOM is bound.

*** Process all customized Alternate Unit Definition
    loop at LR_EA_ALTUOM->GET_ALTUOM_DEF( ) into LS_ALTUOM_DEF.
      if LINE_EXISTS( CT_MEINH[ MEINH = LS_ALTUOM_DEF-UOM ] ).
        assign CT_MEINH[ MEINH = LS_ALTUOM_DEF-UOM ] to <S_MEINH>.
      else.
***     Add customized Alt.UoM before otherwise defined UoMs
        insert value #( MEINH = LS_ALTUOM_DEF-UOM ) into table CT_MEINH assigning <S_MEINH>.
      endif.

***   Get Denominator/Numerator for conversion to base units of measure
      LR_EA_ALTUOM->GET_UMREZ_UMREN( exporting IF_UOM   = <S_MEINH>-MEINH
                                     importing EF_UMREZ = <S_MEINH>-UMREZ
                                               EF_UMREN = <S_MEINH>-UMREN ).
    endloop.

***--------------------------------------------------------------------------------------
*** Verify/Update all Conversions
    SUPER->UPDATE_MEINH( exporting IS_MARA  = IS_MARA
                                   IR_SVC   = IR_SVC
                                   IR_SVCEL = IR_SVCEL
                         changing  CT_MEINH = CT_MEINH ).

  endmethod.
