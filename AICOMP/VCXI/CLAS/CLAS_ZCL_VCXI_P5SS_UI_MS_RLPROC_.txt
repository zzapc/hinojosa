
class ZCL_VCXI_P5SS_UI_MS_RLPROC definition
  public
  inheriting from ZCL_VCXI_PXSS_UI_MS_XPROC
  create public .

public section.

  data S_MS_RLPROC_VIS type ZVCXI_P5SS_S_MS_RLPROC_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABPT type ZVCXI_XCS_TABPT default '0101S_TAB-TAB'
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  constants C_UC_RLPROP_OUT type SYUCOMM value 'RLPROP_OUT' ##NO_TEXT.
  data R_GRID_PRINT_COM type ref to ZCL_VCXI_PXSS_GRID_PRINT_COM .
  data R_PR_MS_RLPROC type ref to ZCL_VCXI_P5SS_PR_MS_RLPROC .

  methods HANDLE_UC_RLPROP
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_P5SS_UI_MS_RLPROC definition
class ZCL_VCXI_P5SS_UI_MS_RLPROC implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_P5SS_UI_MS_RLPROC_BIND'
      exporting
        IR_UI_MS_RLPROC = ME
      importing
        ES_DYNP         = ME->S_DYNP.

***--------------------------------------------------------------------------------------
*** Create Grid for Print
    create object ME->R_GRID_PRINT_COM
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_PRINT_COM'
        IR_PRINT_COM      = ME->R_PR_MS_RLPROC.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE
                        IF_TABPT  = IF_TABPT ).
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_MS_RLPROC.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

***--------------------------------------------------------------------------------------
*** Free Price Scale Grid
    if ME->R_GRID_PRINT_COM is bound.
      ME->R_GRID_PRINT_COM->DO_FREE( ).
      clear ME->R_GRID_PRINT_COM.
    endif.

  endmethod.
  method DO_MODIFY_SCREEN.

    try.
        SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                 IF_DYNNR = IF_DYNNR ).

        loop at screen.



          modify screen.
        endloop.

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Super PBO
    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize Grid
    if ME->R_GRID_PRINT_COM is bound.
      ME->R_GRID_PRINT_COM->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_PRINT_COM->REFRESH_GRID( ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_MS_RLPROC_VIS type ZVCXI_P5SS_S_MS_RLPROC_VIS.


*** Call SUPER
    SUPER->DO_UPDATE_VIS( IS_VIS = IS_VIS ).
    check ME->R_PR_MS_RLPROC is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_MS_RLPROC_VIS.

***--------------------------------------------------------------------------------------
*** Set CCB Grade Number
    if ME->S_MS_RLPROC_VIS-CCBGN ne LS_MS_RLPROC_VIS-CCBGN.
      ME->R_PR_MS_RLPROC->SET_CCBGN( IF_CCBGN = LS_MS_RLPROC_VIS-CCBGN
                                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Grammage
    if ME->S_MS_RLPROC_VIS-S_GRAMM ne LS_MS_RLPROC_VIS-S_GRAMM.
      ME->R_PR_MS_RLPROC->SET_GRAMM( IS_GRAMM = LS_MS_RLPROC_VIS-S_GRAMM
                                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Caliper
    if ME->S_MS_RLPROC_VIS-S_CALIP ne LS_MS_RLPROC_VIS-S_CALIP.
      ME->R_PR_MS_RLPROC->SET_CALIP( IS_CALIP = LS_MS_RLPROC_VIS-S_CALIP
                                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Surface Weight
    if ME->S_MS_RLPROC_VIS-S_SURWT ne LS_MS_RLPROC_VIS-S_SURWT.
      ME->R_PR_MS_RLPROC->SET_SURWT( IS_SURWT = LS_MS_RLPROC_VIS-S_SURWT
                                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Procurement Web Width
    if ME->S_MS_RLPROC_VIS-S_PRWWI ne LS_MS_RLPROC_VIS-S_PRWWI.
      ME->R_PR_MS_RLPROC->SET_PRWWI( IS_PRWWI = LS_MS_RLPROC_VIS-S_PRWWI
                                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    if ME->R_GRID_PRINT_COM is bound.
      ME->R_GRID_PRINT_COM->CHECK_CHANGED_DATA( ).
    endif.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    case IF_UCOMM.
***   Roll Property Popup
      when C_UC_RLPROP_OUT.
        ME->HANDLE_UC_RLPROP( ).
    endcase.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_MS_RLPROC.

  endmethod.
  method HANDLE_UC_RLPROP.

*** Start Popup
    ZCL_VCXI_P5SS_POPUP_RLPROP=>START( IR_BL_RLBPROP = ME->R_PR_MS_RLPROC->ZIF_VCXI_P5SS_COMM_NEXT~GET_RLBPX( )
                                       IR_BL_RLFPROP = ME->R_PR_MS_RLPROC->ZIF_VCXI_P5SS_COMM_NEXT~GET_RLFPX( )
                                       IF_WERKS      = ME->R_PR_MS_RLPROC->GET_WERKS( )
                                       IS_WEBWI      = ME->R_PR_MS_RLPROC->ZIF_VCXI_P5SS_COMM_NEXT~GET_WEBWI( )
                                       IS_SURWT      = ME->R_PR_MS_RLPROC->ZIF_VCXI_P5SS_COMM_NEXT~GET_SURWT( ) ).

  endmethod.
  method INIT_VIS.

    data: LR_BL_RLFPROP type ref to ZCL_VCXI_P5SS_BL_RLFPROP.


    SUPER->INIT_VIS( ).

    free: ME->S_MS_RLPROC_VIS.
    ME->S_MS_RLPROC_VIS-S_MS_XPROC_VIS = ME->S_MS_XPROC_VIS.

***--------------------------------------------------------------------------------------
*** CCB Grade Number
    ME->S_MS_RLPROC_VIS-CCBGN = ME->R_PR_MS_RLPROC->GET_CCBGN( ).

*** Grammage
    ME->S_MS_RLPROC_VIS-S_GRAMM = ME->R_PR_MS_RLPROC->GET_GRAMM( ).

*** Caliper
    ME->S_MS_RLPROC_VIS-S_CALIP = ME->R_PR_MS_RLPROC->GET_CALIP( ).

*** Surface Weight
    ME->S_MS_RLPROC_VIS-S_SURWT = ME->R_PR_MS_RLPROC->GET_SURWT( ).

*** Procurement Web Width
    ME->S_MS_RLPROC_VIS-S_PRWWI = ME->R_PR_MS_RLPROC->GET_PRWWI( ).

***--------------------------------------------------------------------------------------
*** Get Web Width (Outbound)
    ME->S_MS_RLPROC_VIS-S_IOSEC_OUT-S_WEBWI = ME->R_PR_MS_RLPROC->ZIF_VCXI_P5SS_COMM_NEXT~GET_WEBWI( ).

*** Get Surface Weight (Outbound)
    ME->S_MS_RLPROC_VIS-S_IOSEC_OUT-S_SURWT = ME->R_PR_MS_RLPROC->ZIF_VCXI_P5SS_COMM_NEXT~GET_SURWT( ).

*** Number Ups (Outbound)
    ME->S_MS_RLPROC_VIS-S_IOSEC_OUT-NOUPS   = ME->R_PR_MS_RLPROC->ZIF_VCXI_P5SS_COMM_NEXT~GET_NOUPS( ).

*** Get Substrate and Caliper (Outbound)
    LR_BL_RLFPROP = ME->R_PR_MS_RLPROC->ZIF_VCXI_P5SS_COMM_NEXT~GET_RLFPX( ).
    if LR_BL_RLFPROP is bound.
      ME->S_MS_RLPROC_VIS-S_IOSEC_OUT-SBSTR   = LR_BL_RLFPROP->GET_SBSTR( ).
      ME->S_MS_RLPROC_VIS-S_IOSEC_OUT-S_CALIP = LR_BL_RLFPROP->S_CALIP.
    endif.

  endmethod.
