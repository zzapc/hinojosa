
class ZCL_VCXI_FBSS_PR_BCOMP definition
  public
  inheriting from ZCL_VCXI_F0SS_PR_XCOMP
  create public .

public section.

  types:
    YT_TC210T type standard table of ZVCXI_FBS_TC210T .

  constants C_IPRVT_FB_BCOMP type /VCXI/VKSC_IPRVT value 'ZVCXI_FBSS_BCOMP' ##NO_TEXT.
  class-data T_TC210T type YT_TC210T .

  methods DISPLAY_POPUP_RECLO_STEPS
    importing
      !IT_RECLO type ZVCXI_FBSSC_TT_BAGTY_RECLO
    exporting
      !EF_TRECL type ZVCXI_FBS_TRECL
    raising
      /VCXI/CX_CKX .
  methods SET_PRVDR_PARAM
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_PARAM_VAL type /VCXI/CKX_ID
    raising
      /VCXI/CX_CKX .
protected section.

  data R_SALV_TABLE type ref to CL_SALV_TABLE .
  data T_RECLO_TYPE type ZVCXI_FBSS_TT_RECLO_TYPE .

  methods HANDLE_POPUP_DOUBLE_CLICK
    for event DOUBLE_CLICK of CL_SALV_EVENTS_TABLE
    importing
      !ROW
      !COLUMN .
private section.
endclass. "ZCL_VCXI_FBSS_PR_BCOMP definition
class ZCL_VCXI_FBSS_PR_BCOMP implementation.
  method DISPLAY_POPUP_RECLO_STEPS.

    data: LF_INDEX      type        I.
    data: LS_RECLO_TYPE type        ZVCXI_FBSS_S_RECLO_TYPE,
          LS_RECLO      type        ZVCXI_FBSSC_S_BAGTY_RECLO,
          LS_TC210T     type        ZVCXI_FBS_TC210T.
    data: LT_ROWS       type        SALV_T_ROW.
    data: LR_COLUMNS    type ref to CL_SALV_COLUMNS,
          LR_COLUMN     type ref to CL_SALV_COLUMN,
          LR_SETTINGS   type ref to CL_SALV_DISPLAY_SETTINGS,
          LR_SELECTIONS type ref to CL_SALV_SELECTIONS,
          LR_EVENT      type ref to CL_SALV_EVENTS_TABLE.
    field-symbols: <S_RECLO_TYPE> type ZVCXI_FBSS_S_RECLO_TYPE.

*** Update buffer
    if T_TC210T is initial.
      select * from ZVCXI_FBS_TC210T
        into table T_TC210T
        where LANGU eq SY-LANGU.
    endif.

*** Update Table
    refresh ME->T_RECLO_TYPE.
    loop at IT_RECLO into LS_RECLO.
      insert initial line into table ME->T_RECLO_TYPE assigning <S_RECLO_TYPE>.
      move LS_RECLO-TRECL to <S_RECLO_TYPE>-TRECL.

      read table T_TC210T into LS_TC210T with key TRECL = <S_RECLO_TYPE>-TRECL.
      check SY-SUBRC is initial.
      move LS_TC210T-DESCR to <S_RECLO_TYPE>-DESCR.
    endloop.

*** Create SALV
    try.
        CL_SALV_TABLE=>FACTORY( importing R_SALV_TABLE = ME->R_SALV_TABLE
                                changing  T_TABLE      = ME->T_RECLO_TYPE ).

      catch CX_SALV_MSG .
        raise exception type /VCXI/CX_CKX.
    endtry.

    check ME->R_SALV_TABLE is bound.

*** Get Events
    LR_EVENT = ME->R_SALV_TABLE->GET_EVENT( ).
    set handler ME->HANDLE_POPUP_DOUBLE_CLICK for LR_EVENT.

*** Set Columns Technical
    LR_COLUMNS = ME->R_SALV_TABLE->GET_COLUMNS( ).

    try.
        clear LR_COLUMN.
        LR_COLUMN = LR_COLUMNS->GET_COLUMN( 'TRECL' ).
        LR_COLUMN->SET_TECHNICAL( IF_SALV_C_BOOL_SAP=>TRUE ).

        clear LR_COLUMN.
        LR_COLUMN = LR_COLUMNS->GET_COLUMN( 'DESCR' ).
        LR_COLUMN->SET_LONG_TEXT( VALUE = text-T02 ).

      catch CX_SALV_NOT_FOUND.
        raise exception type /VCXI/CX_CKX.
    endtry.

*** Set Popup Title
    LR_SETTINGS = ME->R_SALV_TABLE->GET_DISPLAY_SETTINGS( ).
    LR_SETTINGS->SET_LIST_HEADER( text-T01 ).

*** Set Selection Mode Single
    LR_SELECTIONS = ME->R_SALV_TABLE->GET_SELECTIONS( ).
    LR_SELECTIONS->SET_SELECTION_MODE( IF_SALV_C_SELECTION_MODE=>SINGLE ).

*** Set 1-st Row Selected
    append 1 to LT_ROWS.
    LR_SELECTIONS->SET_SELECTED_ROWS( LT_ROWS ).

*** Set Popup Screen
    ME->R_SALV_TABLE->SET_SCREEN_POPUP( START_COLUMN = 120
                                        END_COLUMN   = 170
                                        START_LINE   = 1
                                        END_LINE     = 10 ).

*** Display Popup
    ME->R_SALV_TABLE->DISPLAY( ).

*** Check OK Button Pressed
    check SY-UCOMM eq '&ONT'.

*** Get Selected Row
    clear LT_ROWS.
    LT_ROWS = LR_SELECTIONS->GET_SELECTED_ROWS( ).

    clear: LF_INDEX, LS_RECLO_TYPE.
    read table LT_ROWS into LF_INDEX index 1.
    check SY-SUBRC eq 0.

    read table ME->T_RECLO_TYPE into LS_RECLO_TYPE index LF_INDEX.
    if SY-SUBRC eq 0.
      move LS_RECLO_TYPE-TRECL to EF_TRECL.
    endif.

  endmethod.
  method HANDLE_POPUP_DOUBLE_CLICK.

    data: LT_ROWS       type        SALV_T_ROW,
          LR_SELECTIONS type ref to CL_SALV_SELECTIONS.

*** Set Double Click Row to Selected Rows
    LR_SELECTIONS = ME->R_SALV_TABLE->GET_SELECTIONS( ).
    append ROW to LT_ROWS.
    LR_SELECTIONS->SET_SELECTED_ROWS( LT_ROWS ).

*** Pass OK code
    move '&ONT' to SY-UCOMM.
    ME->R_SALV_TABLE->CLOSE_SCREEN( ).

  endmethod.
  method SET_PRVDR_PARAM.

    data: LS_PARAM  type  /VCXI/VKSR_S_PARAM.

    LS_PARAM = IR_GATE->GET_PARAM( IF_PARID = ZCL_VCXI_FBSS_PR_CSTEP_RECLO=>C_PARID_TRECL ).
    move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC   to LS_PARAM-SETBY.
    move IF_PARAM_VAL                       to LS_PARAM-ATWRT.

    IR_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

*** Send Event
    IR_GATE->SET_EVENT( IF_IEVNT = ZCL_VCXI_FBSS_PR_CSTEP_RECLO=>C_IEVNT_O_TRECL_CHANGED ).
    IR_GATE->RAISE_EVENTS( ).

  endmethod.
