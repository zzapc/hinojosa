
class ZCL_VCXI_FFSP_PR_ADDON_SLIT definition
  public
  inheriting from ZCL_VCXI_XCSP_PR_ADDON
  create public .

public section.

  interfaces ZIF_VCXI_FFSP_ADJ_FIDIM .

  constants C_IAATY_FITIN type /VCXI/VSMC_IAATY value 'FF_AA_FITIN' ##NO_TEXT.
  constants C_IAATY_NOOUT type /VCXI/VSMC_IAATY value 'F0_AA_NOOUT' ##NO_TEXT.

  methods GET_FITIN
    exporting
      !ES_FITIN type ZVCXI_FFS_S_FITIN
      !EF_SETBY type /VCXI/VKSR_SETBY
    raising
      /VCXI/CX_CKX .
  methods GET_NOOUT
    returning
      value(RF_NOOUT) type ZVCXI_XCS_NOOUT
    raising
      /VCXI/CX_CKX .

  methods HANDLE_EVENT
    redefinition .
protected section.

  constants C_IEVNT_I_RECALC_FIDIM type /VCXI/VKSC_IEVNT value 'RECALC_FIDIM' ##NO_TEXT.
  constants C_IEVNT_O_FITIN_CHANGED type /VCXI/VKSC_IEVNT value 'FITIN_CHANGED' ##NO_TEXT.
  constants C_PARID_FITIN type /VCXI/VKS_PARID value 'ZVCXI_FF_FITIN' ##NO_TEXT.

  methods SET_FITIN
    importing
      !IS_FITIN type ZVCXI_FFS_S_FITIN
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .

  methods GET_RTE_SAREC
    redefinition .
private section.
endclass. "ZCL_VCXI_FFSP_PR_ADDON_SLIT definition
class ZCL_VCXI_FFSP_PR_ADDON_SLIT implementation.
  method GET_FITIN.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_FITIN ).
    move LS_PARAM-ATFLV to ES_FITIN-FITIN.
    move LS_PARAM-UNIT  to ES_FITIN-FITIN_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_NOOUT.

    data: LF_NOOUT    type        ZVCXI_XCS_NOOUT.
    data: LT_GATE     type        /VCXI/VKSR_TT_GATE.
    data: LR_GATE     type ref to /VCXI/CL_VKSR_GATE,
          LR_PR_FIOUT type ref to ZCL_VCXI_FFSP_PR_FIOUT.

***--------------------------------------------------------------------------------------
*** Get all Film Dimensions
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_FFSP_PR_FIOUT=>C_IPRVT_FIOUT
                                     IF_INUSE  = ABAP_TRUE
                                     IF_SORTED = ABAP_TRUE ).
    check LT_GATE is not initial.

***--------------------------------------------------------------------------------------
*** Receive defined Dimensions
    loop at LT_GATE into LR_GATE.
      move LR_GATE->R_CLSPR ?to LR_PR_FIOUT.
      check LR_PR_FIOUT is bound.

      LR_PR_FIOUT->GET_NOOUT( importing EF_NOOUT = LF_NOOUT ).
      add LF_NOOUT to RF_NOOUT.
    endloop.

  endmethod.
  method GET_RTE_SAREC.

    data:          LF_GAATY      type /VCXI/VSMC_GAATY.
    data:          LS_FITIN      type ZVCXI_FFS_S_FITIN.
    field-symbols: <S_RTE_SAREC> type /VCXI/VSMR_S_RTE_SAREC,
                   <S_ARECN>     type /VCXI/VSMR_S_RTE_RANGE_ARECN.

***--------------------------------------------------------------------------------------
*** Get Super
    RT_RTE_SAREC = SUPER->GET_RTE_SAREC( ).

    check ME->R_RTE_CADDON          is bound and
          ME->R_RTE_CADDON->R_ATYPE is bound.

***--------------------------------------------------------------------------------------
*** Aspect - Number of Outs
    try.
        LF_GAATY = ME->R_RTE_CADDON->R_ATYPE->GET_GAATY_BY_IAATY( IF_IAATY = C_IAATY_NOOUT
                                                                  IF_MAATY = /VCXI/CL_VSMC_ATYPE=>C_MAATY_REC ).
        append initial line to RT_RTE_SAREC assigning <S_RTE_SAREC>.
        move LF_GAATY                                     to <S_RTE_SAREC>-GAATY.
        move 1                                            to <S_RTE_SAREC>-MAXSEL.
        move /VCXI/CL_VSMR_RTE_CADDON=>C_ASORT_DESCENDING to <S_RTE_SAREC>-ASORT.

        append initial line      to <S_RTE_SAREC>-I_ARECN assigning <S_ARECN>.
        move 'I'                 to <S_ARECN>-SIGN.
        move 'LE'                to <S_ARECN>-OPTION.
        move ME->GET_NOOUT( )    to <S_ARECN>-LOW.

      catch /VCXI/CX_CKX.
    endtry.

***--------------------------------------------------------------------------------------
*** Aspect - Internal Film Thickness
    try.
        ME->GET_FITIN( importing ES_FITIN = LS_FITIN ).

        LF_GAATY = ME->R_RTE_CADDON->R_ATYPE->GET_GAATY_BY_IAATY( IF_IAATY = C_IAATY_FITIN
                                                                  IF_MAATY = /VCXI/CL_VSMC_ATYPE=>C_MAATY_REC ).
        append initial line to RT_RTE_SAREC assigning <S_RTE_SAREC>.
        move LF_GAATY                                     to <S_RTE_SAREC>-GAATY.
        move 1                                            to <S_RTE_SAREC>-MAXSEL.
        move /VCXI/CL_VSMR_RTE_CADDON=>C_ASORT_DESCENDING to <S_RTE_SAREC>-ASORT.

        append initial line      to <S_RTE_SAREC>-I_ARECN assigning <S_ARECN>.
        move 'I'                 to <S_ARECN>-SIGN.
        move 'LE'                to <S_ARECN>-OPTION.
        move LS_FITIN-FITIN      to <S_ARECN>-LOW.

      catch /VCXI/CX_CKX.
    endtry.

  endmethod.
  method HANDLE_EVENT.

    SUPER->HANDLE_EVENT( IF_IEVNT         = IF_IEVNT
                         IT_GATE_PROVIDER = IT_GATE_PROVIDER ).

    case IF_IEVNT.
***   Film Dimensions
      when C_IEVNT_I_RECALC_FIDIM.
        ME->R_GATE->SET_EVENT( IF_IEVNT = ZIF_VCXI_FFSP_ADJ_FIDIM~C_IEVNT_O_FIDIM_CHANGED ).

    endcase.

  endmethod.
  method SET_FITIN.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_FITIN ).

    move IS_FITIN-FITIN                      to LS_PARAM-ATFLV.
    move IS_FITIN-FITIN_UOM                  to LS_PARAM-UNIT.
    move IF_SETBY                            to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_FITIN_CHANGED ).
    endif.

  endmethod.
  method ZIF_VCXI_FFSP_ADJ_FIDIM~CALC_FIDIM_OUT.

    data: LS_FIDIM    type        ZVCXI_FFSP_S_FIDIM.
    data: LT_GATE     type        /VCXI/VKSR_TT_GATE.
    data: LR_GATE     type ref to /VCXI/CL_VKSR_GATE,
          LR_PR_FIOUT type ref to ZCL_VCXI_FFSP_PR_FIOUT.
    field-symbols: <S_FIDIM>     type  ZVCXI_FFSP_S_FIDIM.

***--------------------------------------------------------------------------------------
*** Get first inbound Dimensions as base
    read table CT_FIDIM into LS_FIDIM index 1.

*** Buffer Film Thickness for Aspect
    ME->SET_FITIN( IS_FITIN = LS_FIDIM-S_FITIN ).

***--------------------------------------------------------------------------------------
*** Define all new Dimensions
    free CT_FIDIM.

*** Get all Film Dimensions (sorted)
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_FFSP_PR_FIOUT=>C_IPRVT_FIOUT
                                     IF_INUSE  = ABAP_TRUE
                                     IF_SORTED = ABAP_TRUE ).
    loop at LT_GATE into LR_GATE.
      move LR_GATE->R_CLSPR ?to LR_PR_FIOUT.
      check LR_PR_FIOUT is bound.

      append LS_FIDIM to CT_FIDIM assigning <S_FIDIM>.
      LR_PR_FIOUT->GET_FIWID( importing ES_FIWID = <S_FIDIM>-S_FIWID ).
      LR_PR_FIOUT->GET_RELEN( importing ES_RELEN = <S_FIDIM>-S_RELEN ).
      LR_PR_FIOUT->GET_NOUPS( importing EF_NOUPS = <S_FIDIM>-NOUPS ).
      LR_PR_FIOUT->GET_NOOUT( importing EF_NOOUT = <S_FIDIM>-NOOUT ).
    endloop.

  endmethod.
