
class ZCL_VCXI_P1SS_GRID_PRPLIN definition
  public
  inheriting from ZCL_VCXI_XCS_GRID
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABNAME type TABNAME default 'ZVCXI_P1SS_S_PRPLIN_GRID'
      !IF_CONTAINER_NAME type SCRFNAME
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL default ABAP_TRUE
    raising
      /VCXI/CX_CKX .
protected section.

  data R_PR_PREPR type ref to ZCL_VCXI_P1SS_PR_PREPR .

  methods CHANGE_STYLE_PRPLIN
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IS_DATA type ANY
    changing
      !CT_STYL type LVC_T_STYL
    raising
      /VCXI/CX_CKX .
  methods GET_PRPLIN_VIS
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    changing
      !CS_DATA type ANY
    raising
      /VCXI/CX_CKX .
  methods HANDLE_DOUBLE_CLICK
    for event DOUBLE_CLICK of CL_GUI_ALV_GRID
    importing
      !E_ROW
      !E_COLUMN
      !ES_ROW_NO .
  methods HANDLE_HOTSPOT_CLICK
    for event HOTSPOT_CLICK of CL_GUI_ALV_GRID
    importing
      !E_ROW_ID
      !E_COLUMN_ID
      !ES_ROW_NO .
  methods SET_PRPLIN_VIS
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IS_DATA type ANY
    raising
      /VCXI/CX_CKX .

  methods GET_FIELDCAT
    redefinition .
  methods GET_GRID_DATA
    redefinition .
  methods HANDLE_DATA_CHANGED_FINISHED
    redefinition .
  methods SET_EVENT
    redefinition .
private section.
endclass. "ZCL_VCXI_P1SS_GRID_PRPLIN definition
class ZCL_VCXI_P1SS_GRID_PRPLIN implementation.
  method CHANGE_STYLE_PRPLIN.

    data: LF_STYLE_ENABLED type LVC_STYLE.
    data: LS_PRPLIN_GRID type ZVCXI_P1SS_S_PRPLIN_GRID.
    data: LR_PR_PRPLIN  type ref to ZCL_VCXI_P1SS_PR_PRPLIN.


    free CT_STYL.

***--------------------------------------------------------------------------------------
*** Get PR Class
    if IR_GATE is bound.
      try.
          move IR_GATE->R_CLSPR ?to LR_PR_PRPLIN.
        catch CX_SY_MOVE_CAST_ERROR.
      endtry.
      check LR_PR_PRPLIN is bound.

      LF_STYLE_ENABLED = ME->F_STYLE_ENABLED.
    else.
      LF_STYLE_ENABLED = ME->F_STYLE_DISABLED.
    endif.

*** "Cast" generic structure to Vis Structure
    move-corresponding IS_DATA to LS_PRPLIN_GRID.

***--------------------------------------------------------------------------------------
*** Preprint Flag
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'FPRPR'
                                IF_STYLE     = switch #( LS_PRPLIN_GRID-FPRPR when ABAP_UNDEFINED then ME->F_STYLE_DISABLED
                                                                                                  else ME->F_STYLE_ENABLED )
                      changing  CT_STYL      = CT_STYL ).

*** Set Web Width = Enable
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'WEBWI'
                                IF_STYLE     = LF_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_GATE           = IR_GATE
                        IF_TABNAME        = IF_TABNAME
                        IF_CONTAINER_NAME = IF_CONTAINER_NAME
                        IR_UIGATE         = IR_UIGATE
                        IF_APPL_EVENTS    = IF_APPL_EVENTS ).

    ME->R_PR_PREPR ?= ME->R_GATE->R_CLSPR.

  endmethod.
  method GET_FIELDCAT.

    data: LF_LOCKED type /VCXI/CKX_LOCKED,
          LF_INUSE  type /VCXI/VKSR_INUSE,
          LF_COLPOS type I.


***----------------------------------------------------------------------------
*** Get Standard Field Catalog
    RT_FCAT = SUPER->GET_FIELDCAT( ).

*** Get Lock and In-Use Status
    LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
    LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***----------------------------------------------------------------------------
*** Adjust Field Catalog
    loop at RT_FCAT assigning field-symbol(<S_FCAT>).
      case <S_FCAT>-FIELDNAME.
        when 'POSID' or 'GATKY' or 'R_GATE' or 'T_STYL'.
          move ABAP_TRUE    to <S_FCAT>-TECH.
          move 0            to <S_FCAT>-COL_POS.

        when 'BPFAC'.
          <S_FCAT>-NO_OUT = ABAP_TRUE.

        when 'FPRPR'.
          <S_FCAT>-CHECKBOX = ABAP_TRUE.
          <S_FCAT>-HOTSPOT  = ABAP_TRUE.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            <S_FCAT>-EDIT = ABAP_TRUE.
          endif.

        when 'WEBWI'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            <S_FCAT>-EDIT = ABAP_TRUE.
          endif.
        when 'WEBWIOUT' or 'WEBWIOUT_UOM'.
          <S_FCAT>-NO_OUT = ABAP_TRUE.
      endcase.

***----------------------------------------------------------------------------
***   Set Position for each Field
*      move 1 to LF_COLPOS.
*      if <S_FCAT>-FIELDNAME eq 'MATNR'.           <S_FCAT>-COL_POS = LF_COLPOS.  endif.
*      add 1 to LF_COLPOS.
*      if <S_FCAT>-FIELDNAME eq 'MATNR_DESCR'.     <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    endloop.

    sort RT_FCAT by COL_POS.

  endmethod.
  method GET_GRID_DATA.

    data: LS_PRPLIN_GRID type ZVCXI_P1SS_S_PRPLIN_GRID.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type          ANY,
                   <T_STYL> type          LVC_T_STYL.


***--------------------------------------------------------------------------------------
*** Dereferencing of Data Reference
    assign IR_DATA->* to <T_DATA>.

***--------------------------------------------------------------------------------------
*** Add Lines based on Flute Structure
    ME->R_PR_PREPR->GET_FLUTE( importing ET_FSTRUCT = data(LT_FSTRUCT) ).
    loop at LT_FSTRUCT into data(LS_FSTRUCT).
      append initial line to <T_DATA> assigning <S_DATA>.
      move-corresponding value ZVCXI_P1SS_S_PRPLIN_GRID( POSID = LS_FSTRUCT-SEQNR
                                                         BPAID = LS_FSTRUCT-BPAID
                                                         FPRPR = switch #( LS_FSTRUCT-OBLIG when ABAP_FALSE then ABAP_UNDEFINED )
                                                       ) to <S_DATA>.
    endloop.

*** Enrich with defined Preprint Liners
    loop at ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_P1SS_PR_PRPLIN=>C_IPRVT_P1_PRPLIN
                                   IF_INUSE  = ABAP_TRUE ) into data(LR_GATE).
      loop at <T_DATA> assigning <S_DATA>.
        LS_PRPLIN_GRID = corresponding #( <S_DATA> ).
        check LS_PRPLIN_GRID-BPAID = cast ZCL_VCXI_P1SS_PR_PRPLIN( LR_GATE->R_CLSPR )->GET_BPAID( ).

        LS_PRPLIN_GRID-GATKY  = LR_GATE->F_GATKY.
        LS_PRPLIN_GRID-R_GATE = LR_GATE.
        LS_PRPLIN_GRID-FPRPR  = ABAP_TRUE.
        <S_DATA> = corresponding #( base ( <S_DATA> ) LS_PRPLIN_GRID ).

***     Get Visualization Data
        ME->GET_PRPLIN_VIS( exporting IR_GATE = LR_GATE
                            changing  CS_DATA = <S_DATA> ).
      endloop.
    endloop.

*** Update Style
    data(LS_LAYOUT) = ME->GET_LAYOUT( ).
    if LS_LAYOUT-STYLEFNAME is not initial.
      loop at <T_DATA> assigning <S_DATA>.
        LS_PRPLIN_GRID = corresponding #( <S_DATA> ).
***     Set Style Details
        assign component LS_LAYOUT-STYLEFNAME of structure <S_DATA> to <T_STYL>.
        if SY-SUBRC eq 0.
          ME->CHANGE_STYLE_PRPLIN( exporting IR_GATE = LS_PRPLIN_GRID-R_GATE
                                             IS_DATA = <S_DATA>
                                    changing CT_STYL = <T_STYL> ).
***       Call DVC Servcie - Modify Style
          ZCL_VCXI_XCDR_SERVICE=>MODIFY_STYLE( exporting IR_GATE = LS_PRPLIN_GRID-R_GATE
                                               changing  CT_STYL = <T_STYL> ).
        endif.
      endloop.
    endif.

  endmethod.
  method GET_PRPLIN_VIS.

    data: LS_PRPLIN_VIS type ZVCXI_P1SS_S_PRPLIN_GRID.
    data: LR_PR_PRPLIN  type ref to ZCL_VCXI_P1SS_PR_PRPLIN.


***--------------------------------------------------------------------------------------
*** Get PR Class
    try.
        move IR_GATE->R_CLSPR ?to LR_PR_PRPLIN.
      catch CX_SY_MOVE_CAST_ERROR.
    endtry.
    check LR_PR_PRPLIN is bound.

*** "Cast" generic structure to Vis Structure
    move-corresponding CS_DATA to LS_PRPLIN_VIS.

***--------------------------------------------------------------------------------------
*** Web Width (Outbound)
    LS_PRPLIN_VIS-S_WEBWI = LR_PR_PRPLIN->GET_WEBWI( ).

*** Repeat Length (Outbound)
    LS_PRPLIN_VIS-S_REPLE = LR_PR_PRPLIN->GET_REPLE( ).

***--------------------------------------------------------------------------------------
*** Web Width (Outbound)
    LS_PRPLIN_VIS-S_IOSEC-S_WEBWI = LR_PR_PRPLIN->GET_WEBWI( ).

*** Surface Weight (Outbound)
    LS_PRPLIN_VIS-S_IOSEC-S_SURWT = LR_PR_PRPLIN->GET_SURWT( ).

*** Number Ups (Outbound)
    LS_PRPLIN_VIS-S_IOSEC-NOUPS   = LR_PR_PRPLIN->GET_NOUPS( ).

*** Substrate and Caliper (Outbound)
    data(LR_BL_RLFPROP) = LR_PR_PRPLIN->GET_RLFPX( ).
    if LR_BL_RLFPROP is bound.
      LS_PRPLIN_VIS-S_IOSEC-SBSTR   = LR_BL_RLFPROP->GET_SBSTR( ).
      LS_PRPLIN_VIS-S_IOSEC-S_CALIP = LR_BL_RLFPROP->S_CALIP.
    endif.

***--------------------------------------------------------------------------------------
*** "Cast" Vis Structure back to generic structure
    move-corresponding LS_PRPLIN_VIS to CS_DATA.

  endmethod.
  method HANDLE_DATA_CHANGED_FINISHED.

    data: LS_PRPLIN_GRID type ZVCXI_P1SS_S_PRPLIN_GRID.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type          ANY.

    check E_MODIFIED eq ABAP_TRUE.

    try.
        SUPER->HANDLE_DATA_CHANGED_FINISHED( E_MODIFIED    = E_MODIFIED
                                             ET_GOOD_CELLS = ET_GOOD_CELLS ).

***     Dereferencing of Data Reference
        assign ME->R_DATA->* to <T_DATA>.

        loop at <T_DATA> assigning <S_DATA>.
          move-corresponding <S_DATA> to LS_PRPLIN_GRID.

          check LS_PRPLIN_GRID-R_GATE is bound and
                LS_PRPLIN_GRID-R_GATE->F_FREED is initial.

***       Set Values to PR Class
          ME->SET_PRPLIN_VIS( IR_GATE = LS_PRPLIN_GRID-R_GATE
                              IS_DATA = <S_DATA> ).
        endloop.

        ME->R_GATE->RAISE_EVENTS( ).

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

*** Request Refresh for ALV
    move ABAP_TRUE to ME->F_REFRESH_REQ.

  endmethod.
  method HANDLE_DOUBLE_CLICK.

    data: LS_PRPLIN_GRID type ZVCXI_P1SS_S_PRPLIN_GRID.
    field-symbols: <T_DATA>  type standard table.


    try.
***     Dereferencing of Data Reference
        assign ME->R_DATA->* to <T_DATA>.

        check LINE_EXISTS( <T_DATA>[ E_ROW-INDEX ] ).
        LS_PRPLIN_GRID = corresponding #( <T_DATA>[ E_ROW-INDEX ] ).

***     Check Gate is bound and Provider has an own UI
        check LS_PRPLIN_GRID-R_GATE is bound and
              LS_PRPLIN_GRID-R_GATE->GET_CLSUI( ) is not initial.
        ME->R_UIGATE->OPEN_SUBSCREEN( IR_GATE = LS_PRPLIN_GRID-R_GATE ).

***     Expand and select in Tree
        ME->R_UIGATE->EXPAND_TREE_NODE( IF_GATKY = LS_PRPLIN_GRID-R_GATE->F_GATKY ).
        ME->R_UIGATE->SELECT_TREE_NODE( IF_GATKY = LS_PRPLIN_GRID-R_GATE->F_GATKY ).

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method HANDLE_HOTSPOT_CLICK.

    data: LS_PRPLIN_GRID type ZVCXI_P1SS_S_PRPLIN_GRID.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type          ANY.

    try.

***     Dereferencing of Data Reference
        assign ME->R_DATA->* to <T_DATA>.

        loop at <T_DATA> assigning <S_DATA>.
          check SY-TABIX  eq E_ROW_ID.

          LS_PRPLIN_GRID = corresponding #( <S_DATA> ).

          case E_COLUMN_ID-FIELDNAME.
            when 'FPRPR'.   "Preprint Flag
              if LS_PRPLIN_GRID-R_GATE is bound and
                 LS_PRPLIN_GRID-R_GATE->F_FREED is initial.
                LS_PRPLIN_GRID-R_GATE->DO_FREE( ).
              else.
                ME->R_PR_PREPR->ADD_PRVDR_PRPLIN( IF_BPAID = LS_PRPLIN_GRID-BPAID ).
              endif.

          endcase.

        endloop.

        ME->R_GATE->RAISE_EVENTS( ).

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

*** Request Refresh for ALV
    move ABAP_TRUE to ME->F_REFRESH_REQ.

  endmethod.
  method SET_EVENT.

    SUPER->SET_EVENT( ).

*** Set Hotspot
    set handler ME->HANDLE_HOTSPOT_CLICK       for ME->R_GRID.

*** Set Double Click
    set handler ME->HANDLE_DOUBLE_CLICK        for ME->R_GRID.

  endmethod.
  method SET_PRPLIN_VIS.

    data: LS_PRPLIN_VIS_OLD type ZVCXI_P1SS_S_PRPLIN_VIS,
          LS_PRPLIN_VIS_NEW type ZVCXI_P1SS_S_PRPLIN_VIS.
    data: LR_PR_PRPLIN  type ref to ZCL_VCXI_P1SS_PR_PRPLIN.


***--------------------------------------------------------------------------------------
*** Get PR Class
    check IR_GATE->R_CLSPR is instance of ZCL_VCXI_P1SS_PR_PRPLIN.
    LR_PR_PRPLIN ?= IR_GATE->R_CLSPR.
    check LR_PR_PRPLIN is bound.

*** Get old Values
    ME->GET_PRPLIN_VIS( exporting IR_GATE = IR_GATE
                        changing  CS_DATA = LS_PRPLIN_VIS_OLD ).
*** Get new Values
    move-corresponding IS_DATA to LS_PRPLIN_VIS_NEW.

***--------------------------------------------------------------------------------------
*** Set Web Width
    if LS_PRPLIN_VIS_NEW-S_WEBWI ne LS_PRPLIN_VIS_OLD-S_WEBWI.
      LR_PR_PRPLIN->SET_WEBWI( IS_WEBWI = LS_PRPLIN_VIS_NEW-S_WEBWI
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
