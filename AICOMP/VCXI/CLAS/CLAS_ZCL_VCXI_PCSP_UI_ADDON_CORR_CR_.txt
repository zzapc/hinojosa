
class ZCL_VCXI_PCSP_UI_ADDON_CORR_CR definition
  public
  inheriting from ZCL_VCXI_XCSP_UI_ADDON
  create public .

public section.

  data S_ADDON_CORR_CR_VIS type ZVCXI_PCSP_S_ADDON_CORR_CR_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
protected section.

  data R_PR_ADDON_CORR_CR type ref to ZCL_VCXI_PCSP_PR_ADDON_CORR_CR .

  methods DO_UPDATE_MC_STARCH_VIS
    importing
      !IS_ADDON_CORR_CR_VIS type ZVCXI_PCSP_S_ADDON_CORR_CR_VIS
    raising
      /VCXI/CX_CKX .
  methods INIT_VIS_MC_STARCH
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_PCSP_UI_ADDON_CORR_CR definition
class ZCL_VCXI_PCSP_UI_ADDON_CORR_CR implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_PCSP_UI_ADDON_CCR_BIND'
      exporting
        IR_UI_ADDON_CORR_CR = ME
      importing
        ES_DYNP             = ME->S_DYNP.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_ADDON_CORR_CR.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
method DO_UPDATE_MC_STARCH_VIS.

  data:          LS_AVGCO             type          ZVCXI_XCS_S_AVGCO,
                 LS_AVGCP             type          ZVCXI_XCS_S_AVGCP.
  data:          LT_GATE              type          /VCXI/VKSR_TT_GATE.
  data:          LR_GATE              type ref to   /VCXI/CL_VKSR_GATE,
                 LR_PR_MC_STARCH      type ref to   ZCL_VCXI_PCSP_PR_MC_STARCH.

*** Set Starch Consumption Data
*** Get Gates of Material Consumption
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCSP_PR_MC=>C_IPRVT_XCS_MC
                                   IF_INUSE = ABAP_TRUE ).

  read table LT_GATE into LR_GATE index 1.

  try.
      move LR_GATE->R_CLSPR ?to LR_PR_MC_STARCH.
      check LR_PR_MC_STARCH is bound.

      move-corresponding IS_ADDON_CORR_CR_VIS to:
                         LS_AVGCO, LS_AVGCP.

***   Set Material Number
      if ME->S_ADDON_CORR_CR_VIS-MATNR ne IS_ADDON_CORR_CR_VIS-MATNR.
        LR_PR_MC_STARCH->SET_MATNR( IF_MATNR = IS_ADDON_CORR_CR_VIS-MATNR
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
      endif.

***   Set Average Consumption and Consumption per
      if ME->S_ADDON_CORR_CR_VIS-AVGCO ne IS_ADDON_CORR_CR_VIS-AVGCO.
        LR_PR_MC_STARCH->SET_AVGCO( IS_AVGCO = LS_AVGCO
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
      endif.

      if ME->S_ADDON_CORR_CR_VIS-AVGCP ne IS_ADDON_CORR_CR_VIS-AVGCP.
        LR_PR_MC_STARCH->SET_AVGCP( IS_AVGCP = LS_AVGCP
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
      endif.

***   Set Starch Factor
      if ME->S_ADDON_CORR_CR_VIS-STARF ne IS_ADDON_CORR_CR_VIS-STARF.
        LR_PR_MC_STARCH->SET_STARF( IF_STARF = IS_ADDON_CORR_CR_VIS-STARF
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
      endif.

    catch CX_SY_MOVE_CAST_ERROR.
      clear: LR_PR_MC_STARCH.
  endtry.

endmethod.
  method DO_UPDATE_VIS.

    data: LS_ADDON_CORR_CR_VIS type ZVCXI_PCSP_S_ADDON_CORR_CR_VIS.


*** Super
    SUPER->DO_UPDATE_VIS( IS_VIS = IS_VIS ).
    check ME->R_PR_ADDON_CORR_CR is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_ADDON_CORR_CR_VIS.

***-------------------------------------------------------------------------------------
*** Set Roll Length, Width and Diameter
    if ME->S_ADDON_CORR_CR_VIS-ROLEN ne LS_ADDON_CORR_CR_VIS-ROLEN.
      ME->R_PR_ADDON_CORR_CR->SET_ROLEN( IS_ROLEN = LS_ADDON_CORR_CR_VIS-S_ROLEN
                                         IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

    if ME->S_ADDON_CORR_CR_VIS-ROWID ne LS_ADDON_CORR_CR_VIS-ROWID.
      ME->R_PR_ADDON_CORR_CR->SET_ROWID( IS_ROWID = LS_ADDON_CORR_CR_VIS-S_ROWID
                                         IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

    if ME->S_ADDON_CORR_CR_VIS-RODIA ne LS_ADDON_CORR_CR_VIS-RODIA.
      ME->R_PR_ADDON_CORR_CR->SET_RODIA( IS_RODIA = LS_ADDON_CORR_CR_VIS-S_RODIA
                                         IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Number Ups
    if ME->S_ADDON_CORR_CR_VIS-PCUPS ne LS_ADDON_CORR_CR_VIS-PCUPS.
      ME->R_PR_ADDON_CORR_CR->SET_PCUPS( IF_PCUPS = LS_ADDON_CORR_CR_VIS-PCUPS
                                         IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set PC Direction
    if ME->S_ADDON_CORR_CR_VIS-PCDIR ne LS_ADDON_CORR_CR_VIS-PCDIR.
      ME->R_PR_ADDON_CORR_CR->SET_PCDIR( IF_PCDIR = LS_ADDON_CORR_CR_VIS-PCDIR
                                         IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set PC Type
    if ME->S_ADDON_CORR_CR_VIS-PCTYP ne LS_ADDON_CORR_CR_VIS-PCTYP.
      ME->R_PR_ADDON_CORR_CR->SET_PCTYP( IF_PCTYP = LS_ADDON_CORR_CR_VIS-PCTYP
                                         IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Board ID
    if ME->S_ADDON_CORR_CR_VIS-BOARD ne LS_ADDON_CORR_CR_VIS-BOARD.
      ME->R_PR_ADDON_CORR_CR->SET_BOARD( IF_BOARD = LS_ADDON_CORR_CR_VIS-BOARD
                                         IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Flute Type
    if ME->S_ADDON_CORR_CR_VIS-FLUTE ne LS_ADDON_CORR_CR_VIS-FLUTE.
      ME->R_PR_ADDON_CORR_CR->SET_FLUTE( IF_FLUTE = LS_ADDON_CORR_CR_VIS-FLUTE
                                         IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Corrugation Width
    if ME->S_ADDON_CORR_CR_VIS-COWID ne LS_ADDON_CORR_CR_VIS-COWID.
      ME->R_PR_ADDON_CORR_CR->SET_COWID( IS_COWID = LS_ADDON_CORR_CR_VIS-S_COWID
                                         IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***-------------------------------------------------------------------------------------
*** Update Material Consumption Starch
    ME->DO_UPDATE_MC_STARCH_VIS( IS_ADDON_CORR_CR_VIS = LS_ADDON_CORR_CR_VIS ).

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_ADDON_CORR_CR.

  endmethod.
  method INIT_VIS.

*** Super
    SUPER->INIT_VIS( ).

    free: ME->S_ADDON_CORR_CR_VIS.
    ME->S_ADDON_CORR_CR_VIS-S_ADDON_VIS = ME->S_ADDON_VIS.

***--------------------------------------------------------------------------------------
*** Get Roll Dimension
    ME->S_ADDON_CORR_CR_VIS-S_ROLEN = ME->R_PR_ADDON_CORR_CR->GET_ROLEN( ).
    ME->S_ADDON_CORR_CR_VIS-S_ROWID = ME->R_PR_ADDON_CORR_CR->GET_ROWID( ).
    ME->S_ADDON_CORR_CR_VIS-S_RODIA = ME->R_PR_ADDON_CORR_CR->GET_RODIA( ).

*** Get Number Ups
    move ME->R_PR_ADDON_CORR_CR->GET_PCUPS( ) to ME->S_ADDON_CORR_CR_VIS-PCUPS.

*** Get PC Direction
    move ME->R_PR_ADDON_CORR_CR->GET_PCDIR( ) to ME->S_ADDON_CORR_CR_VIS-PCDIR.

*** Get PC Type and Description
    move ME->R_PR_ADDON_CORR_CR->GET_PCTYP( )       to ME->S_ADDON_CORR_CR_VIS-PCTYP.
    move ME->R_PR_ADDON_CORR_CR->GET_PCTYP_DESCR( ) to ME->S_ADDON_CORR_CR_VIS-PCTYP_DESCR.

*** Get Board ID
    move ME->R_PR_ADDON_CORR_CR->GET_BOARD( ) to ME->S_ADDON_CORR_CR_VIS-BOARD.

*** Get Flute Type
    move ME->R_PR_ADDON_CORR_CR->GET_FLUTE( ) to ME->S_ADDON_CORR_CR_VIS-FLUTE.

*** Get Corrugation Width
    ME->S_ADDON_CORR_CR_VIS-S_COWID = ME->R_PR_ADDON_CORR_CR->GET_COWID( ).

***--------------------------------------------------------------------------------------
*** Initialize Material Consumption Vis
    ME->INIT_VIS_MC_STARCH( ).

  endmethod.
method INIT_VIS_MC_STARCH.

  data:          LS_AVGCO             type          ZVCXI_XCS_S_AVGCO,
                 LS_AVGCP             type          ZVCXI_XCS_S_AVGCP,
                 LS_CONSU             type          ZVCXI_XCSP_S_CONSU.
  data:          LT_GATE              type          /VCXI/VKSR_TT_GATE.
  data:          LR_GATE              type ref to   /VCXI/CL_VKSR_GATE,
                 LR_PR_MC_STARCH      type ref to   ZCL_VCXI_PCSP_PR_MC_STARCH.

***--------------------------------------------------------------------------------------
*** Get Gates of Material Consumption
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCSP_PR_MC=>C_IPRVT_XCS_MC
                                   IF_INUSE = ABAP_TRUE ).

  read table LT_GATE into LR_GATE index 1.
  check SY-SUBRC eq 0.

  try.
      move LR_GATE->R_CLSPR ?to LR_PR_MC_STARCH.
      check LR_PR_MC_STARCH is bound.

***   Get Material Number
      move LR_PR_MC_STARCH->GET_MATNR( )       to ME->S_ADDON_CORR_CR_VIS-MATNR.
      move LR_PR_MC_STARCH->GET_MATNR_DESCR( ) to ME->S_ADDON_CORR_CR_VIS-MATNR_DESCR.

***   Get Average Consumption and Consumption per
      move LR_PR_MC_STARCH->GET_AVGCO( ) to LS_AVGCO.
      move-corresponding LS_AVGCO        to ME->S_ADDON_CORR_CR_VIS.

      move LR_PR_MC_STARCH->GET_AVGCP( ) to LS_AVGCP.
      move-corresponding LS_AVGCP        to ME->S_ADDON_CORR_CR_VIS.

***   Get Starch Factor
      move LR_PR_MC_STARCH->GET_STARF( )       to ME->S_ADDON_CORR_CR_VIS-STARF.

***   Get Starch Consumption
      move LR_PR_MC_STARCH->GET_CONSU( ) to LS_CONSU.
      move-corresponding LS_CONSU        to ME->S_ADDON_CORR_CR_VIS.

    catch CX_SY_MOVE_CAST_ERROR.
      clear: LR_PR_MC_STARCH.
  endtry.

endmethod.
