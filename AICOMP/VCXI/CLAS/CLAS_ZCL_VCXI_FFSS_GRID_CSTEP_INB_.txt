
class ZCL_VCXI_FFSS_GRID_CSTEP_INB definition
  public
  inheriting from ZCL_VCXI_XCS_GRID_PRVDR
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABNAME type TABNAME default 'ZVCXI_FFSS_S_CSTEP_INB_GRID'
      !IF_CONTAINER_NAME type SCRFNAME
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL default ABAP_TRUE
    raising
      /VCXI/CX_CKX .
  methods IS_ENABLED
    returning
      value(RF_RETURN) type ABAP_BOOL .
protected section.

  methods GET_FIELDCAT
    redefinition .
  methods GET_GRID_DATA
    redefinition .
  methods GET_LAYOUT
    redefinition .
private section.
endclass. "ZCL_VCXI_FFSS_GRID_CSTEP_INB definition
class ZCL_VCXI_FFSS_GRID_CSTEP_INB implementation.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_GATE           = IR_GATE
                        IR_UIGATE         = IR_UIGATE
                        IF_TABNAME        = IF_TABNAME
                        IF_CONTAINER_NAME = IF_CONTAINER_NAME
                        IF_APPL_EVENTS    = IF_APPL_EVENTS ).

*** Define Provider Type for Button
    ME->F_IPRVT = ZCL_VCXI_F0SS_PR_CSTEP=>C_IPRVT_F0_CSTEP. "ZCL_VCXI_FFSS_PR_CSTEP=>C_IPRVT_CSTEP.

  endmethod.
  method GET_FIELDCAT.

    field-symbols: <T_DATA> type standard table,
                   <S_FCAT> type LVC_S_FCAT.

***--------------------------------------------------------------------------------------
*** Get Standard Field Catalog
    RT_FCAT = SUPER->GET_FIELDCAT( ).

*** Dereferencing of Data Reference
    assign ME->R_DATA->* to <T_DATA>.

***--------------------------------------------------------------------------------------
*** Adjust Field Catalog
    loop at RT_FCAT assigning <S_FCAT>.
      case <S_FCAT>-FIELDNAME.
        when  'POSID' or 'GATKY' or 'R_GATE' or 'T_STYL'.
          move ABAP_TRUE    to <S_FCAT>-TECH.

        when 'CSTEP_DESCR'.
          move 1        to <S_FCAT>-COL_POS.
          move 40       to <S_FCAT>-OUTPUTLEN.
          move text-CST to <S_FCAT>-SCRTEXT_L.
          move text-CST to <S_FCAT>-SCRTEXT_M.
          move text-CST to <S_FCAT>-REPTEXT.
          move text-CSS to <S_FCAT>-SCRTEXT_S.

      endcase.
    endloop.

    sort RT_FCAT by COL_POS.

  endmethod.
  method GET_GRID_DATA.

    data:          LS_CSTEP_INB_GRID    type          ZVCXI_FFSS_S_CSTEP_INB_GRID.
    data:          LT_GATE              type          /VCXI/VKSR_TT_GATE.
    data:          LR_GATE              type ref to   /VCXI/CL_VKSR_GATE.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type          ANY.

***--------------------------------------------------------------------------------------
*** Dereferencing of Data Reference
    assign IR_DATA->* to <T_DATA>.

*** Get Gates of CSteps
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ME->F_IPRVT
                                     IF_INUSE  = ABAP_TRUE
                                     IF_SORTED = ABAP_TRUE ).

    loop at LT_GATE into LR_GATE.
      clear: LS_CSTEP_INB_GRID.

***--------------------------------------------------------------------------------------
***   Get Gate Data
      move LR_GATE->GET_POSID( ) to LS_CSTEP_INB_GRID-POSID.
      move LR_GATE->F_GATKY      to LS_CSTEP_INB_GRID-GATKY.
      move LR_GATE               to LS_CSTEP_INB_GRID-R_GATE.

      move LR_GATE->GET_DESCR( ) to LS_CSTEP_INB_GRID-CSTEP_DESCR.

***   Call DVC Servcie - Modify Style
      ZCL_VCXI_XCDR_SERVICE=>MODIFY_STYLE( exporting IR_GATE = LR_GATE
                                           changing  CT_STYL = LS_CSTEP_INB_GRID-T_STYL ).

***--------------------------------------------------------------------------------------
***   Add Entry
      append initial line to <T_DATA> assigning <S_DATA>.
      move-corresponding LS_CSTEP_INB_GRID to <S_DATA>.

    endloop.

  endmethod.
  method GET_LAYOUT.

    SUPER->GET_LAYOUT( receiving RS_LAYOUT = RS_LAYOUT ).

    move ABAP_FALSE to RS_LAYOUT-CWIDTH_OPT.

  endmethod.
  method IS_ENABLED.

    move ABAP_FALSE to RF_RETURN.

    check LINES( ME->T_PRVDR_DEF ) is not initial.

    move ABAP_TRUE to RF_RETURN.

  endmethod.
