
class ZCL_VCXI_FFSS_CUST_RHNDL definition
  public
  create protected .

public section.

  constants C_MAX type CHAR3 value 'MAX' ##NO_TEXT.
  constants C_MIN type CHAR3 value 'MIN' ##NO_TEXT.

  class-methods CALC_RLEOF_BY_ROLEN
    importing
      !IF_ROLEN type ZVCXI_FFS_ROLEN
      !IF_ROLEN_UOM type /VCXI/CKX_UOM
      !IS_RLEOP type ZVCXI_FFS_S_RLEOP
    returning
      value(RF_RLEOF) type ZVCXI_FFS_RLEOF
    raising
      /VCXI/CX_CKX .
  class-methods CALC_BY_RDIMTA
    importing
      !IS_RDIMTA type ZVCXI_FFS_S_RDIMTA
      !IS_WIDIM type ZVCXI_FFS_S_WIDIM
      !IS_CRDIO type ZVCXI_FFS_S_CRDIO
      !IS_CRWGT type ZVCXI_FFS_S_CRWGT
      !IS_RLEOP type ZVCXI_FFS_S_RLEOP
    exporting
      !ES_RDITA type ZVCXI_FFS_S_RDITA
      !ES_RLETA type ZVCXI_FFS_S_RLETA
      !ES_RWGTA type ZVCXI_FFS_S_RWGTA
      !EF_RLEOF type ZVCXI_FFS_RLEOF
    raising
      /VCXI/CX_CKX .
  class-methods CALC_CLETA
    importing
      !IS_WIWID type ZVCXI_FFS_S_WIWID
      !IS_COMAR type ZVCXI_FFS_S_COMAR
      !IF_ROPCO type ZVCXI_FFS_ROPCO
    returning
      value(RS_CLETA) type ZVCXI_FFS_S_CLETA
    raising
      /VCXI/CX_CKX .
  class-methods CALC_CRWGT_PER_ROLL
    importing
      !IS_CRLEN type ZVCXI_FFS_S_CRLEN
      !IS_CRWGT type ZVCXI_FFS_S_CRWGT
      !IS_WIWID type ZVCXI_FFS_S_WIWID
      !IS_COMAR type ZVCXI_FFS_S_COMAR
      !IF_CRCUT type ZVCXI_FFS_CRCUT
    exporting
      !ES_CRWGT type ZVCXI_FFS_S_CRWGT
    raising
      /VCXI/CX_CKX .
  class-methods CALC_DIMEN_BY_PERC
    importing
      !IF_PERC type ZVCXI_FFS_PERC
      !IF_PERC_UOM type /VCXI/CKX_UOM
      !IF_DIMTA type ZVCXI_FFS_DIMEN
      !IF_DIMTA_UOM type /VCXI/CKX_UOM
      !IF_MINMAX type CHAR3
    exporting
      !EF_DIMEN type ZVCXI_FFS_DIMEN
    raising
      /VCXI/CX_CKX .
  class-methods CALC_PERC_BY_DIMEN
    importing
      !IF_DIMEN type ZVCXI_FFS_DIMEN
      !IF_DIMEN_UOM type /VCXI/CKX_UOM
      !IF_DIMTA type ZVCXI_FFS_DIMEN
      !IF_DIMTA_UOM type /VCXI/CKX_UOM
      !IF_MINMAX type CHAR3
    exporting
      !EF_PERC type ZVCXI_FFS_PERC
    raising
      /VCXI/CX_CKX .
  class-methods CALC_RODIA_BY_ROLEN
    importing
      !IF_ROLEN type ZVCXI_FFS_ROLEN
      !IF_ROLEN_UOM type /VCXI/CKX_UOM
      !IS_WITHI type ZVCXI_FFS_S_WITHI
      !IS_CRDIO type ZVCXI_FFS_S_CRDIO
      !IF_RODIA_UOM type /VCXI/CKX_UOM
    exporting
      !EF_RODIA type ZVCXI_FFS_RODIA
    raising
      /VCXI/CX_CKX .
  class-methods CALC_ROLEN_BY_RODIA
    importing
      !IF_RODIA type ZVCXI_FFS_RODIA
      !IF_RODIA_UOM type /VCXI/CKX_UOM
      !IS_WITHI type ZVCXI_FFS_S_WITHI
      !IS_CRDIO type ZVCXI_FFS_S_CRDIO
      !IF_ROLEN_UOM type /VCXI/CKX_UOM
    exporting
      !EF_ROLEN type ZVCXI_FFS_ROLEN
    raising
      /VCXI/CX_CKX .
  class-methods CALC_ROLEN_BY_ROWGT
    importing
      !IF_ROWGT type ZVCXI_FFS_ROWGT
      !IF_ROWGT_UOM type /VCXI/CKX_UOM
      !IS_CRWGT type ZVCXI_FFS_S_CRWGT
      !IS_WIWID type ZVCXI_FFS_S_WIWID
      !IS_WISWG type ZVCXI_FFS_S_WISWG
    exporting
      !EF_ROLEN type ZVCXI_FFS_ROLEN
      !EF_ROLEN_UOM type /VCXI/CKX_UOM
    raising
      /VCXI/CX_CKX .
  class-methods CALC_ROPCO
    importing
      !IF_MATNR type MATNR
      !IS_WIWID type ZVCXI_FFS_S_WIWID
      !IS_COMAR type ZVCXI_FFS_S_COMAR
      !IF_FSHCO type ZVCXI_FFS_FSHCO
      !IF_WERKS type WERKS_D
    returning
      value(RF_ROPCO) type ZVCXI_FFS_ROPCO
    raising
      /VCXI/CX_CKX .
  class-methods CALC_ROWGT_BY_ROLEN
    importing
      !IF_ROLEN type ZVCXI_FFS_ROLEN
      !IF_ROLEN_UOM type /VCXI/CKX_UOM
      !IS_CRWGT type ZVCXI_FFS_S_CRWGT
      !IS_WIWID type ZVCXI_FFS_S_WIWID
      !IS_WISWG type ZVCXI_FFS_S_WISWG
    exporting
      !EF_ROWGT type ZVCXI_FFS_ROWGT
      !EF_ROWGT_UOM type /VCXI/CKX_UOM
    raising
      /VCXI/CX_CKX .
  class-methods CHECK_RHNDL
    importing
      !IT_RHNDL type ZVCXI_FFSR_TT_RHNDL
      !IF_WERKS type WERKS_D
      !IF_DATE type DATS
    exporting
      !ET_MSG type /VCXI/CKXM_TT_MSG
    returning
      value(RF_RETURN) type I
    raising
      /VCXI/CX_CKX .
  class-methods CLASS_CONSTRUCTOR .
  class-methods GET_CORE
    importing
      !IF_WERKS type WERKS_D
      !IF_TCORE type ZVCXI_FFS_TCORE
      !IS_CRDII type ZVCXI_FFS_S_CRDII
      !IS_CRWMI type ZVCXI_FFS_S_CRWMI
      !IS_CRWMA type ZVCXI_FFS_S_CRWMA
      !IS_WIWID type ZVCXI_FFS_S_WIWID
      !IS_COMAR type ZVCXI_FFS_S_COMAR
      !IF_ROPCO type ZVCXI_FFS_ROPCO optional
    returning
      value(RF_MATNR) type MATNR
    raising
      /VCXI/CX_CKX .
  class-methods GET_DFLT_FG
    importing
      !IF_WERKS type WERKS_D
      !IF_KUNAG type KUNAG optional
      !IF_KUNWE type KUNWE
    returning
      value(RS_RHNDL_DFLT) type ZVCXI_FFSS_S_RHNDL_DFLT
    raising
      /VCXI/CX_CKX .
  class-methods GET_DFLT_WIP
    importing
      !IF_WERKS type WERKS_D
      !IF_ARBPL type ARBPL
    returning
      value(RS_RHNDL_DFLT) type ZVCXI_FFSS_S_RHNDL_DFLT
    raising
      /VCXI/CX_CKX .
  class-methods GET_RWGNA
    importing
      !IF_RWSID type ZVCXI_FFS_RWSID
      !IF_RWDIR type ZVCXI_FFS_RWDIR
      !IF_EYEMP type ZVCXI_FFS_EYEMP
    returning
      value(RF_RWGNA) type ZVCXI_FFS_RWGNA .
protected section.

  types:
    YT_TC410  type standard table of ZVCXI_FFS_TC410 .
  types:
    YT_TC440  type standard table of ZVCXI_FFS_TC440 .
  types:
    YT_TC450  type standard table of ZVCXI_FFS_TC450 .
  types:
    YT_TC470  type standard table of ZVCXI_FFS_TC470 .

  constants C_PI type F value '3.141592653589793' ##NO_TEXT.
  class-data T_TC410 type YT_TC410 .
  class-data T_TC440 type YT_TC440 .
  class-data T_TC450 type YT_TC450 .
  class-data T_TC470 type YT_TC470 .

  class-methods GET_CORE_MATERIALS_LIST
    importing
      !IF_WERKS type WERKS_D
    exporting
      !ET_CORE type ZVCXI_FFS_TT_CORE
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_FFSS_CUST_RHNDL definition
class ZCL_VCXI_FFSS_CUST_RHNDL implementation.
  method CALC_BY_RDIMTA.

    case IS_RDIMTA-RDMTA.
***--------------------------------------------------------------------------------------
      when 'D'.   "Diameter
***     Use as Roll Diameter
        ES_RDITA = value #( RDITA     = IS_RDIMTA-S_RDVTA-RDVTA
                            RDITA_UOM = IS_RDIMTA-S_RDVTA-RDVTA_UOM ).
***     Calculate Roll Length
        ZCL_VCXI_FFSS_CUST_RHNDL=>CALC_ROLEN_BY_RODIA( exporting IF_RODIA     = ES_RDITA-RDITA
                                                                 IF_RODIA_UOM = ES_RDITA-RDITA_UOM
                                                                 IS_CRDIO     = IS_CRDIO
                                                                 IS_WITHI     = IS_WIDIM-S_WITHI
                                                                 IF_ROLEN_UOM = ES_RLETA-RLETA_UOM
                                                       importing EF_ROLEN     = ES_RLETA-RLETA ).
***     Calculate Roll Weight
        ZCL_VCXI_FFSS_CUST_RHNDL=>CALC_ROWGT_BY_ROLEN( exporting IF_ROLEN     = ES_RLETA-RLETA
                                                                 IF_ROLEN_UOM = ES_RLETA-RLETA_UOM
                                                                 IS_CRWGT     = IS_CRWGT
                                                                 IS_WIWID     = IS_WIDIM-S_WIWID
                                                                 IS_WISWG     = IS_WIDIM-S_WISWG
                                                       importing EF_ROWGT     = ES_RWGTA-RWGTA
                                                                 EF_ROWGT_UOM = ES_RWGTA-RWGTA_UOM ).
***     Calculate Roll Length Optimization Factor
        EF_RLEOF = ZCL_VCXI_FFSS_CUST_RHNDL=>CALC_RLEOF_BY_ROLEN( IF_ROLEN     = ES_RLETA-RLETA
                                                                  IF_ROLEN_UOM = ES_RLETA-RLETA_UOM
                                                                  IS_RLEOP     = IS_RLEOP ).

***--------------------------------------------------------------------------------------
      when 'L'.    "Length
***     Use as Roll Length
        ES_RLETA = value #( RLETA     = IS_RDIMTA-S_RDVTA-RDVTA
                            RLETA_UOM = IS_RDIMTA-S_RDVTA-RDVTA_UOM ).
***     Calculate Roll Diameter
        ZCL_VCXI_FFSS_CUST_RHNDL=>CALC_RODIA_BY_ROLEN( exporting IF_ROLEN     = ES_RLETA-RLETA
                                                                 IF_ROLEN_UOM = ES_RLETA-RLETA_UOM
                                                                 IS_CRDIO     = IS_CRDIO
                                                                 IS_WITHI     = IS_WIDIM-S_WITHI
                                                                 IF_RODIA_UOM = ES_RDITA-RDITA_UOM
                                                       importing EF_RODIA     = ES_RDITA-RDITA ).
***     Calculate Roll Weight
        ZCL_VCXI_FFSS_CUST_RHNDL=>CALC_ROWGT_BY_ROLEN( exporting IF_ROLEN     = ES_RLETA-RLETA
                                                                 IF_ROLEN_UOM = ES_RLETA-RLETA_UOM
                                                                 IS_CRWGT     = IS_CRWGT
                                                                 IS_WIWID     = IS_WIDIM-S_WIWID
                                                                 IS_WISWG     = IS_WIDIM-S_WISWG
                                                       importing EF_ROWGT     = ES_RWGTA-RWGTA
                                                                 EF_ROWGT_UOM = ES_RWGTA-RWGTA_UOM ).
***     Calculate Roll Length Optimization Factor
        EF_RLEOF = ZCL_VCXI_FFSS_CUST_RHNDL=>CALC_RLEOF_BY_ROLEN( IF_ROLEN     = ES_RLETA-RLETA
                                                                  IF_ROLEN_UOM = ES_RLETA-RLETA_UOM
                                                                  IS_RLEOP     = IS_RLEOP ).

***--------------------------------------------------------------------------------------
      when 'W'.    "Weight
***     Use as Roll Weight
        ES_RWGTA = value #( RWGTA     = IS_RDIMTA-S_RDVTA-RDVTA
                            RWGTA_UOM = IS_RDIMTA-S_RDVTA-RDVTA_UOM ).
***     Calculate Roll Length
        ZCL_VCXI_FFSS_CUST_RHNDL=>CALC_ROLEN_BY_ROWGT( exporting IF_ROWGT     = ES_RWGTA-RWGTA
                                                                 IF_ROWGT_UOM = ES_RWGTA-RWGTA_UOM
                                                                 IS_CRWGT     = IS_CRWGT
                                                                 IS_WIWID     = IS_WIDIM-S_WIWID
                                                                 IS_WISWG     = IS_WIDIM-S_WISWG
                                                       importing EF_ROLEN     = ES_RLETA-RLETA
                                                                 EF_ROLEN_UOM = ES_RLETA-RLETA_UOM ).
***     Calculate Roll Diameter
        ZCL_VCXI_FFSS_CUST_RHNDL=>CALC_RODIA_BY_ROLEN( exporting IF_ROLEN     = ES_RLETA-RLETA
                                                                 IF_ROLEN_UOM = ES_RLETA-RLETA_UOM
                                                                 IS_CRDIO     = IS_CRDIO
                                                                 IS_WITHI     = IS_WIDIM-S_WITHI
                                                                 IF_RODIA_UOM = ES_RDITA-RDITA_UOM
                                                       importing EF_RODIA     = ES_RDITA-RDITA ).
***     Calculate Roll Length Optimization Factor
        EF_RLEOF = ZCL_VCXI_FFSS_CUST_RHNDL=>CALC_RLEOF_BY_ROLEN( IF_ROLEN     = ES_RLETA-RLETA
                                                                  IF_ROLEN_UOM = ES_RLETA-RLETA_UOM
                                                                  IS_RLEOP     = IS_RLEOP ).

***--------------------------------------------------------------------------------------
      when 'O'.    "Roll Length Optimization
***     Use as Roll Length Optimization Factor
        EF_RLEOF = IS_RDIMTA-S_RDVTA-RDVTA.
***     Calculate Roll Length
        ES_RLETA = value #( RLETA     = IS_RLEOP-RLEOP * EF_RLEOF
                            RLETA_UOM = IS_RLEOP-RLEOP_UOM ).
***     Calculate Roll Diameter
        ZCL_VCXI_FFSS_CUST_RHNDL=>CALC_RODIA_BY_ROLEN( exporting IF_ROLEN     = ES_RLETA-RLETA
                                                                 IF_ROLEN_UOM = ES_RLETA-RLETA_UOM
                                                                 IS_CRDIO     = IS_CRDIO
                                                                 IS_WITHI     = IS_WIDIM-S_WITHI
                                                                 IF_RODIA_UOM = ES_RDITA-RDITA_UOM
                                                       importing EF_RODIA     = ES_RDITA-RDITA ).
***     Calculate Roll Weight
        ZCL_VCXI_FFSS_CUST_RHNDL=>CALC_ROWGT_BY_ROLEN( exporting IF_ROLEN     = ES_RLETA-RLETA
                                                                 IF_ROLEN_UOM = ES_RLETA-RLETA_UOM
                                                                 IS_CRWGT     = IS_CRWGT
                                                                 IS_WIWID     = IS_WIDIM-S_WIWID
                                                                 IS_WISWG     = IS_WIDIM-S_WISWG
                                                       importing EF_ROWGT     = ES_RWGTA-RWGTA
                                                                 EF_ROWGT_UOM = ES_RWGTA-RWGTA_UOM ).
    endcase.

  endmethod.
  method CALC_CLETA.

    data: LS_CALCV type  ZVCXI_XCS_S_CALCV.


    check IS_WIWID is not initial.

*** Start with Winding Width
    move IS_WIWID-WIWID     to LS_CALCV-CALCV.
    move IS_WIWID-WIWID_UOM to LS_CALCV-CALCV_UOM.

*** Add Core Margin
    if IS_COMAR-COMAR_UOM is not initial.
      ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_CALCV-CALCV
                                                          IF_CALCV_UOM_1 = LS_CALCV-CALCV_UOM
                                                          IF_CALCV_2     = IS_COMAR-COMAR
                                                          IF_CALCV_UOM_2 = IS_COMAR-COMAR_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_ADD
                                                          IF_UOM         = LS_CALCV-CALCV_UOM
                                                importing EF_CALCV       = LS_CALCV-CALCV
                                                          EF_CALCV_UOM   = LS_CALCV-CALCV_UOM ).
***   No negative Value
      LS_CALCV-CALCV = cond #( when LS_CALCV-CALCV lt 0 then 0 else LS_CALCV-CALCV ).
    endif.

*** Multiply by Rolls/Core
    RS_CLETA-CLETA     = LS_CALCV-CALCV * IF_ROPCO.
    RS_CLETA-CLETA_UOM = LS_CALCV-CALCV_UOM.

  endmethod.
  method CALC_CRWGT_PER_ROLL.

    data: LF_CALCV                   type        F.
    data: LS_CALCV_WIWID             type        ZVCXI_XCS_S_CALCV.
    data: LR_CX_CONVERSION_OVERFLOW  type ref to CX_SY_CONVERSION_OVERFLOW.


*** Only calculated proportion per Roll if Core Cutting is allowed by Core
    if iF_CRCUT eq ABAP_TRUE.
      check IS_CRLEN-CRLEN is not initial.

***   Calculate Winding Width including Margin
      ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = IS_WIWID-WIWID
                                                          IF_CALCV_UOM_1 = IS_WIWID-WIWID_UOM
                                                          IF_CALCV_2     = IS_COMAR-COMAR
                                                          IF_CALCV_UOM_2 = IS_COMAR-COMAR_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_ADD
                                                importing EF_CALCV       = LS_CALCV_WIWID-CALCV
                                                          EF_CALCV_UOM   = LS_CALCV_WIWID-CALCV_UOM ).

***   Calculate propotional Factor of Roll on Core
      ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_CALCV_WIWID-CALCV
                                                          IF_CALCV_UOM_1 = LS_CALCV_WIWID-CALCV_UOM
                                                          IF_CALCV_2     = IS_CRLEN-CRLEN
                                                          IF_CALCV_UOM_2 = IS_CRLEN-CRLEN_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_DIVIDE
                                                importing EF_CALCV       = LF_CALCV ).
    else.
***   Use full Core Weight
      LF_CALCV = 1.
    endif.

*** Export Core Weight per Roll
    try .
        ES_CRWGT-CRWGT     = IS_CRWGT-CRWGT * LF_CALCV.
        ES_CRWGT-CRWGT_UOM = IS_CRWGT-CRWGT_UOM.
      catch CX_SY_CONVERSION_OVERFLOW into LR_CX_CONVERSION_OVERFLOW.
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_CONVERSION_OVERFLOW ).
    endtry.

  endmethod.
  method CALC_DIMEN_BY_PERC.

    data: LS_CALCV_DIMEN            type        ZVCXI_XCS_S_CALCV.
    data: LR_CX_CONVERSION_OVERFLOW type ref to CX_SY_CONVERSION_OVERFLOW.

    check IF_PERC_UOM  is not initial and
          IF_DIMTA     is not initial and
          IF_DIMTA_UOM is not initial.

    case IF_MINMAX.

***   ( 1 – Min % / 100 )
      when C_MIN.
        LS_CALCV_DIMEN-CALCV = 1 - ( IF_PERC / 100 ).

***   ( 1 + Max % / 100 )
      when C_MAX.
        LS_CALCV_DIMEN-CALCV = 1 + ( IF_PERC / 100 ).

    endcase.

*** Target Value * [Min,Max] Value
    LS_CALCV_DIMEN-CALCV = IF_DIMTA * LS_CALCV_DIMEN-CALCV.
    move IF_DIMTA_UOM to LS_CALCV_DIMEN-CALCV_UOM.

    if LS_CALCV_DIMEN-CALCV lt 0.
      move 0 to LS_CALCV_DIMEN-CALCV.
    endif.

*** Export Dimension
    try .
        move LS_CALCV_DIMEN-CALCV to EF_DIMEN.
      catch CX_SY_CONVERSION_OVERFLOW into LR_CX_CONVERSION_OVERFLOW.
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_CONVERSION_OVERFLOW ).
    endtry.

  endmethod.
  method CALC_PERC_BY_DIMEN.

    data: LS_CALCV_PERC             type        ZVCXI_XCS_S_CALCV.
    data: LR_CX_CONVERSION_OVERFLOW type ref to CX_SY_CONVERSION_OVERFLOW.

    check IF_DIMEN     is not initial and
          IF_DIMEN_UOM is not initial and
          IF_DIMTA     is not initial and
          IF_DIMTA_UOM is not initial.

*** Min Max Value / Target Value
    ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = IF_DIMEN
                                                        IF_CALCV_UOM_1 = IF_DIMEN_UOM
                                                        IF_CALCV_2     = IF_DIMTA
                                                        IF_CALCV_UOM_2 = IF_DIMTA_UOM
                                                        IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_DIVIDE
                                              importing EF_CALCV       = LS_CALCV_PERC-CALCV
                                                        EF_CALCV_UOM   = LS_CALCV_PERC-CALCV_UOM ).
    case IF_MINMAX.

***   Min Percentage = ( 1 – Min Value /Target Value) * 100
      when C_MIN.
***     Minimum must be lower equal than Target
        check IF_DIMEN le IF_DIMTA.
        LS_CALCV_PERC-CALCV = ( 1 - LS_CALCV_PERC-CALCV ) * 100.

***   Max Percentage = ( Max Value / Target Value – 1 ) * 100
      when C_MAX.
***     Target needs to be lower-equal than Maximum
        check IF_DIMTA le IF_DIMEN.
****    Maximum value / Target Value needs to be less than 10.9999 because the displayed value can be '999,99'
        check LS_CALCV_PERC-CALCV le '10.9999'.
        break HARDCODED_LIMIT.
        LS_CALCV_PERC-CALCV = ( LS_CALCV_PERC-CALCV - 1 ) * 100.

    endcase.

    if LS_CALCV_PERC-CALCV lt 0.
      move 0 to LS_CALCV_PERC-CALCV.
    endif.

*** Export Percentage
    try.
        move LS_CALCV_PERC-CALCV to EF_PERC.
      catch CX_SY_CONVERSION_OVERFLOW into LR_CX_CONVERSION_OVERFLOW.
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_CONVERSION_OVERFLOW ).
    endtry.

  endmethod.
  method CALC_RLEOF_BY_ROLEN.

    if IS_RLEOP-RLEOP ne 0.
      ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = IF_ROLEN
                                                          IF_CALCV_UOM_1 = IF_ROLEN_UOM
                                                          IF_CALCV_2     = IS_RLEOP-RLEOP
                                                          IF_CALCV_UOM_2 = IS_RLEOP-RLEOP_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_DIVIDE
                                                importing EF_CALCV       = RF_RLEOF ).
    else.
      RF_RLEOF = 0.
    endif.

  endmethod.
  method CALC_RODIA_BY_ROLEN.

    data: LS_CALCV_ROLEN            type        ZVCXI_XCS_S_CALCV,
          LS_CALCV_RODIA            type        ZVCXI_XCS_S_CALCV,
          LS_CALCV_CRDIO            type        ZVCXI_XCS_S_CALCV,
          LS_CALCV_WITHI            type        ZVCXI_XCS_S_CALCV.
    data: LR_CX_CONVERSION_OVERFLOW type ref to CX_SY_CONVERSION_OVERFLOW.

    check IF_ROLEN_UOM       is not initial and
          IS_CRDIO-CRDIO_UOM is not initial and
          IS_WITHI-WITHI_UOM is not initial.

*** Roll Length             Convert Unit to RDIXX
    move IF_ROLEN           to LS_CALCV_ROLEN-CALCV.
    move IF_ROLEN_UOM       to LS_CALCV_ROLEN-CALCV_UOM.
    ZCL_VCXI_XCS_SERVICE_CALC=>CONVERT_UNIT( exporting IF_UOM    = IF_RODIA_UOM
                                             changing  CS_CALCV  = LS_CALCV_ROLEN ).

*** Film Thickness          Convert Unit to RODIA
    move IS_WITHI-WITHI     to LS_CALCV_WITHI-CALCV.
    move IS_WITHI-WITHI_UOM to LS_CALCV_WITHI-CALCV_UOM.
    ZCL_VCXI_XCS_SERVICE_CALC=>CONVERT_UNIT( exporting IF_UOM    = IF_RODIA_UOM
                                             changing  CS_CALCV  = LS_CALCV_WITHI ).

*** Core Outer Diameter     Convert Unit to RODIA
    move IS_CRDIO-CRDIO     to LS_CALCV_CRDIO-CALCV.
    move IS_CRDIO-CRDIO_UOM to LS_CALCV_CRDIO-CALCV_UOM.
    ZCL_VCXI_XCS_SERVICE_CALC=>CONVERT_UNIT( exporting IF_UOM    = IF_RODIA_UOM
                                             changing  CS_CALCV  = LS_CALCV_CRDIO ).

*** Roll Length * Film Thickness * 4 / PI
    LS_CALCV_ROLEN-CALCV = LS_CALCV_ROLEN-CALCV * LS_CALCV_WITHI-CALCV * 4 / C_PI.

*** Core Outer Diameter ^ 2
    LS_CALCV_CRDIO-CALCV = LS_CALCV_CRDIO-CALCV * LS_CALCV_CRDIO-CALCV.

*** Roll Diameter = Square root ( (Roll Length * Film Thickness * 4 / Pi ) + Core Outer Diameter^2 )
    LS_CALCV_RODIA-CALCV = SQRT( LS_CALCV_ROLEN-CALCV + LS_CALCV_CRDIO-CALCV  ).

*** Export Roll Diameter
    try .
        move LS_CALCV_RODIA-CALCV to EF_RODIA.
      catch CX_SY_CONVERSION_OVERFLOW into LR_CX_CONVERSION_OVERFLOW.
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_CONVERSION_OVERFLOW ).
    endtry.

  endmethod.
  method CALC_ROLEN_BY_RODIA.

    data: LS_CALCV_RODIA            type        ZVCXI_XCS_S_CALCV,
          LS_CALCV_ROLEN            type        ZVCXI_XCS_S_CALCV,
          LS_CALCV_CRDIO            type        ZVCXI_XCS_S_CALCV,
          LS_CALCV_WITHI            type        ZVCXI_XCS_S_CALCV.
    data: LR_CX_CONVERSION_OVERFLOW type ref to CX_SY_CONVERSION_OVERFLOW.

    check IF_RODIA_UOM       is not initial and
          IS_CRDIO-CRDIO_UOM is not initial and
          IS_WITHI-WITHI_UOM is not initial.

*** Roll Diameter           Convert Unit to ROLEN
    move IF_RODIA           to LS_CALCV_RODIA-CALCV.
    move IF_RODIA_UOM       to LS_CALCV_RODIA-CALCV_UOM.
    ZCL_VCXI_XCS_SERVICE_CALC=>CONVERT_UNIT( exporting IF_UOM    = IF_ROLEN_UOM
                                             changing  CS_CALCV  = LS_CALCV_RODIA ).

*** Winding Thickness       Convert Unit to ROLEN
    move IS_WITHI-WITHI     to LS_CALCV_WITHI-CALCV.
    move IS_WITHI-WITHI_UOM to LS_CALCV_WITHI-CALCV_UOM.
    ZCL_VCXI_XCS_SERVICE_CALC=>CONVERT_UNIT( exporting IF_UOM    = IF_ROLEN_UOM
                                             changing  CS_CALCV  = LS_CALCV_WITHI ).

*** Core Outer Diameter     Convert Unit to ROLEN
    move IS_CRDIO-CRDIO     to LS_CALCV_CRDIO-CALCV.
    move IS_CRDIO-CRDIO_UOM to LS_CALCV_CRDIO-CALCV_UOM.
    ZCL_VCXI_XCS_SERVICE_CALC=>CONVERT_UNIT( exporting IF_UOM    = IF_ROLEN_UOM
                                             changing  CS_CALCV  = LS_CALCV_CRDIO ).

*** Roll Diameter^2 - Core Outer Diameter^2
    LS_CALCV_RODIA-CALCV = LS_CALCV_RODIA-CALCV ** 2 - LS_CALCV_CRDIO-CALCV ** 2 .

*** Roll Length = ( ( Roll Diameter^2 - Core Outer Diameter^2 ) * Pi ) / Winding Thickness * 4
    if LS_CALCV_WITHI-CALCV is not initial.
      LS_CALCV_ROLEN-CALCV = ( LS_CALCV_RODIA-CALCV * C_PI ) / ( LS_CALCV_WITHI-CALCV * 4 ).
    endif.
    if LS_CALCV_ROLEN-CALCV lt 0.
      move 0 to LS_CALCV_ROLEN-CALCV.
    endif.

*** Export Roll Length
    try .
        move LS_CALCV_ROLEN-CALCV to EF_ROLEN.
      catch CX_SY_CONVERSION_OVERFLOW into LR_CX_CONVERSION_OVERFLOW.
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_CONVERSION_OVERFLOW ).
    endtry.

  endmethod.
  method CALC_ROLEN_BY_ROWGT.

    data: LS_CALCV_ROLEN            type        ZVCXI_XCS_S_CALCV,
          LS_CALCV_CRWGT            type        ZVCXI_XCS_S_CALCV,
          LS_CALCV_WISWG            type        ZVCXI_XCS_S_CALCV.
    data: LR_CX_CONVERSION_OVERFLOW type ref to CX_SY_CONVERSION_OVERFLOW.


    check IF_ROWGT_UOM       is not initial and
          IS_CRWGT-CRWGT_UOM is not initial and
          IS_WIWID-WIWID_UOM is not initial and
          IS_WISWG-WISWG_UOM is not initial.

    check IS_WISWG-WISWG     gt 0 and
          IS_WIWID-WIWID     gt 0.

*** Roll Weight Netto = Roll Weight – Core Weight
    ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = IF_ROWGT
                                                        IF_CALCV_UOM_1 = IF_ROWGT_UOM
                                                        IF_CALCV_2     = IS_CRWGT-CRWGT
                                                        IF_CALCV_UOM_2 = IS_CRWGT-CRWGT_UOM
                                                        IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_SUBTRACT
                                              importing EF_CALCV       = LS_CALCV_CRWGT-CALCV
                                                        EF_CALCV_UOM   = LS_CALCV_CRWGT-CALCV_UOM ).

    check LS_CALCV_CRWGT-CALCV gt 0.

*** Winding Area = Roll Weight Netto / Winding Surface Weight
    ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_CALCV_CRWGT-CALCV
                                                        IF_CALCV_UOM_1 = LS_CALCV_CRWGT-CALCV_UOM
                                                        IF_CALCV_2     = IS_WISWG-WISWG
                                                        IF_CALCV_UOM_2 = IS_WISWG-WISWG_UOM
                                                        IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_DIVIDE
                                              importing EF_CALCV       = LS_CALCV_WISWG-CALCV
                                                        EF_CALCV_UOM   = LS_CALCV_WISWG-CALCV_UOM ).

*** Roll Length = Winding Area / Winding Width
    ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_CALCV_WISWG-CALCV
                                                        IF_CALCV_UOM_1 = LS_CALCV_WISWG-CALCV_UOM
                                                        IF_CALCV_2     = IS_WIWID-WIWID
                                                        IF_CALCV_UOM_2 = IS_WIWID-WIWID_UOM
                                                        IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_DIVIDE
                                              importing EF_CALCV       = LS_CALCV_ROLEN-CALCV
                                                        EF_CALCV_UOM   = LS_CALCV_ROLEN-CALCV_UOM ).

*** Export Roll Length
    try .
        move LS_CALCV_ROLEN-CALCV     to EF_ROLEN.
        move LS_CALCV_ROLEN-CALCV_UOM to EF_ROLEN_UOM.
      catch CX_SY_CONVERSION_OVERFLOW into LR_CX_CONVERSION_OVERFLOW.
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_CONVERSION_OVERFLOW ).
    endtry.

  endmethod.
  method CALC_ROPCO.

    data: LS_CALCV      type        ZVCXI_XCS_S_CALCV.
    data: LS_CRLEN_MIN  type        ZVCXI_FFS_S_CRLEN.
    data: LR_CUST_CORE  type ref to ZCL_VCXI_FFSS_CUST_CORE.

*** Get Characteristics Info
    try.
        LR_CUST_CORE = ZCL_VCXI_FFSS_CUST_CORE=>GET_INSTANCE_CORE( IF_MATNR = IF_MATNR
                                                                   IF_WERKS = IF_WERKS
                                                                   IF_DATE  = SY-DATUM ).
      catch /VCXI/CX_CKX.
        clear: LR_CUST_CORE.
    endtry.

    check LR_CUST_CORE is bound.

***--------------------------------------------------------------------------------------
*** Start with 1 Rolls/Core
    move 1 to RF_ROPCO.

*** Check Cutting Allowed
    check LR_CUST_CORE->F_CRCUT eq ABAP_TRUE.

*** Calculate only for non-shared cores
    check IF_FSHCO ne ABAP_TRUE.

***--------------------------------------------------------------------------------------
*** Get Winding Width from FFG
    check IS_WIWID is not initial.
    move IS_WIWID-WIWID     to LS_CRLEN_MIN-CRLEN.
    move IS_WIWID-WIWID_UOM to LS_CRLEN_MIN-CRLEN_UOM.

*** Add Margin per Core
    if IS_COMAR-COMAR is not initial.
      ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_CRLEN_MIN-CRLEN
                                                          IF_CALCV_UOM_1 = LS_CRLEN_MIN-CRLEN_UOM
                                                          IF_CALCV_2     = IS_COMAR-COMAR
                                                          IF_CALCV_UOM_2 = IS_COMAR-COMAR_UOM
                                                          IF_UOM         = LS_CRLEN_MIN-CRLEN_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_ADD
                                                importing EF_CALCV       = LS_CRLEN_MIN-CRLEN
                                                          EF_CALCV_UOM   = LS_CRLEN_MIN-CRLEN_UOM ).
    endif.

*** Roles/Core = floor( Core Length / ( Winding Width + Margin )  )
    ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LR_CUST_CORE->S_CRLEN-CRLEN
                                                        IF_CALCV_UOM_1 = LR_CUST_CORE->S_CRLEN-CRLEN_UOM
                                                        IF_CALCV_2     = LS_CRLEN_MIN-CRLEN
                                                        IF_CALCV_UOM_2 = LS_CRLEN_MIN-CRLEN_UOM
                                                        IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_DIVIDE
                                              importing EF_CALCV       = LS_CALCV-CALCV ).
    RF_ROPCO = FLOOR( LS_CALCV-CALCV ).

*** Set Fallback for Rolls/Core
    if RF_ROPCO eq 0.
      move 1 to RF_ROPCO.
    endif.

  endmethod.
  method CALC_ROWGT_BY_ROLEN.

    data: LS_CALCV_ROLEN            type        ZVCXI_XCS_S_CALCV,
          LS_CALCV_ROWGT            type        ZVCXI_XCS_S_CALCV.
    data: LR_CX_CONVERSION_OVERFLOW type ref to CX_SY_CONVERSION_OVERFLOW.


    check IF_ROLEN_UOM       is not initial and
          IS_CRWGT-CRWGT_UOM is not initial and
          IS_WIWID-WIWID_UOM is not initial and
          IS_WISWG-WISWG_UOM is not initial.

    check IS_WISWG-WISWG     gt 0 and
          IS_WIWID-WIWID     gt 0.

*** Winding Area = Roll Length * Winding Width
    ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = IF_ROLEN
                                                        IF_CALCV_UOM_1 = IF_ROLEN_UOM
                                                        IF_CALCV_2     = IS_WIWID-WIWID
                                                        IF_CALCV_UOM_2 = IS_WIWID-WIWID_UOM
                                                        IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_MULTIPLY
                                              importing EF_CALCV       = LS_CALCV_ROLEN-CALCV
                                                        EF_CALCV_UOM   = LS_CALCV_ROLEN-CALCV_UOM ).

*** Netto Roll Weight = Winding Area * Winding Surface Weight
    ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_CALCV_ROLEN-CALCV
                                                        IF_CALCV_UOM_1 = LS_CALCV_ROLEN-CALCV_UOM
                                                        IF_CALCV_2     = IS_WISWG-WISWG
                                                        IF_CALCV_UOM_2 = IS_WISWG-WISWG_UOM
                                                        IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_MULTIPLY
                                              importing EF_CALCV       = LS_CALCV_ROLEN-CALCV
                                                        EF_CALCV_UOM   = LS_CALCV_ROLEN-CALCV_UOM ).

*** Roll Weight = Netto Roll Weight + Core Weight
    ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_CALCV_ROLEN-CALCV
                                                        IF_CALCV_UOM_1 = LS_CALCV_ROLEN-CALCV_UOM
                                                        IF_CALCV_2     = IS_CRWGT-CRWGT
                                                        IF_CALCV_UOM_2 = IS_CRWGT-CRWGT_UOM
                                                        IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_ADD
                                              importing EF_CALCV       = LS_CALCV_ROWGT-CALCV
                                                        EF_CALCV_UOM   = LS_CALCV_ROWGT-CALCV_UOM ).

*** Export Roll Weight
    try .
        move LS_CALCV_ROWGT-CALCV     to EF_ROWGT.
        move LS_CALCV_ROWGT-CALCV_UOM to EF_ROWGT_UOM.
      catch CX_SY_CONVERSION_OVERFLOW into LR_CX_CONVERSION_OVERFLOW.
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_CONVERSION_OVERFLOW ).
    endtry.

  endmethod.
  method CHECK_RHNDL.
*** 0 - Roll Handlings are valid.
*** 1 - Roll Handlings are not valid.

    data: LS_RHNDL     type        ZVCXI_FFSR_S_RHNDL,
          LS_RHNDL_SUM type        ZVCXI_FFSR_S_RHNDL,
          LS_CLETA_SUM type        ZVCXI_FFS_S_CLETA,
          LS_CRLEN     type        ZVCXI_FFS_S_CRLEN.
    data: LT_RHNDL     type        ZVCXI_FFSR_TT_RHNDL,
          LR_CUST_CORE type ref to ZCL_VCXI_FFSS_CUST_CORE.
    data: LR_MESSAGE type ref to /VCXI/CL_CKXM_MESSAGE,
          LR_CX_CKX  type ref to /VCXI/CX_CKX.

    move 0 to RF_RETURN.

    create object LR_MESSAGE.

    move IT_RHNDL to LT_RHNDL.

    loop at IT_RHNDL into LS_RHNDL.

***--------------------------------------------------------------------------------------
***   Check Shared Core ID
      if  LS_RHNDL-FSHCO is not initial and
          LS_RHNDL-ISHCO is initial.

***     Shared Core ID is mandatory.
        LR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '167' ).
        move 1 to RF_RETURN.
        continue.

      endif.

***   Check Rolls/Core
      if LS_RHNDL-ROPCO is initial.

***     Rolls/Core is not maintained.
        LR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '169' ).
        move 1 to RF_RETURN.
        continue.

      endif.

***--------------------------------------------------------------------------------------
***   Check Target Core Length
      if LS_RHNDL-S_CLETA is initial.

***     Target Core Length is not maintained.
        LR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '163' ).
        move 1 to RF_RETURN.
        continue.

      endif.

***   Get Core Length
      if LS_RHNDL-MATNR is not initial.
        try.
            LR_CUST_CORE = ZCL_VCXI_FFSS_CUST_CORE=>GET_INSTANCE_CORE( IF_MATNR = LS_RHNDL-S_MS_CORE-MATNR
                                                                       IF_WERKS = IF_WERKS
                                                                       IF_DATE  = IF_DATE ).
          catch /VCXI/CX_CKX into LR_CX_CKX.
            clear: LR_CUST_CORE.
***         Core Material cannot be instantiated.
            LR_MESSAGE->ADD_MESSAGE_BY_CX_CKX( LR_CX_CKX ).
        endtry.

        if LR_CUST_CORE is bound.
          move LR_CUST_CORE->S_CRLEN to LS_CRLEN.
        endif.
      endif.

      move LS_RHNDL-S_CLETA to LS_CLETA_SUM.
      loop at LT_RHNDL into LS_RHNDL_SUM where ISHCO eq LS_RHNDL-ICORE.
        check not LS_RHNDL_SUM-ISHCO is initial.

***     Sum all Target CORE Length
        ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_CLETA_SUM-CLETA
                                                            IF_CALCV_UOM_1 = LS_CLETA_SUM-CLETA_UOM
                                                            IF_CALCV_2     = LS_RHNDL_SUM-CLETA
                                                            IF_CALCV_UOM_2 = LS_RHNDL_SUM-CLETA_UOM
                                                            IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_ADD
                                                            IF_UOM         = LS_CLETA_SUM-CLETA_UOM
                                                  importing EF_CALCV       = LS_CLETA_SUM-CLETA
                                                            EF_CALCV_UOM   = LS_CLETA_SUM-CLETA_UOM ).

      endloop.

***   Shared cores are not activated
      if LS_RHNDL-CLETA eq LS_CLETA_SUM-CLETA.
        if LS_RHNDL-CLETA gt LS_CRLEN-CRLEN.

***       Target Core Length exceed Core Length
          LR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                                   IF_MSGTY = 'E'
                                   IF_MSGNO = '164' ).
          move 1 to RF_RETURN.

        endif.
      else.

***     Shared cores are activated
        if LS_CLETA_SUM-CLETA gt LS_CRLEN-CRLEN.

***       Target Core Length (Sum) exceed Core Length
          LR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                                   IF_MSGTY = 'E'
                                   IF_MSGNO = '165' ).
          move 1 to RF_RETURN.

        endif.

      endif.

    endloop.

***--------------------------------------------------------------------------------------
*** Show messages
    if LR_MESSAGE->T_MSG is not initial.
      if ET_MSG is requested.
        move LR_MESSAGE->T_MSG to ET_MSG.
      else.
        LR_MESSAGE->DISPLAY_MSG( ).
      endif.
    endif.

  endmethod.
  method CLASS_CONSTRUCTOR.

***--------------------------------------------------------------------------------------
*** Get Winding Information
    select * from ZVCXI_FFS_TC410
      into table T_TC410
      where LANGU eq SY-LANGU
       or   LANGU eq SPACE.

***--------------------------------------------------------------------------------------
*** Get Roll Handling Default FG
    select * from ZVCXI_FFS_TC440
      into table T_TC440.

***--------------------------------------------------------------------------------------
*** Get Roll Handling Default SFG
    select * from ZVCXI_FFS_TC450
      into table T_TC450.

***--------------------------------------------------------------------------------------
*** Get Core Material Identification
    select * from ZVCXI_FFS_TC470
      into table T_TC470.

  endmethod.
  method GET_CORE.

    data: LS_CLETA      type        ZVCXI_FFS_S_CLETA,
          LS_CORE       type        ZVCXI_FFS_S_CORE,
          LS_CALCV      type        ZVCXI_XCS_S_CALCV.
    data: LT_CORE       type        ZVCXI_FFS_TT_CORE,
          LT_CORE_VALID type        ZVCXI_FFS_TT_CORE.

*** Check Prerequisites for Defaulting
    check IF_WERKS       is not initial and
          IF_TCORE       is not initial and
          IS_WIWID-WIWID is not initial.

***--------------------------------------------------------------------------------------
*** Get List of possible Cores
    GET_CORE_MATERIALS_LIST( exporting IF_WERKS = IF_WERKS
                             importing ET_CORE  = LT_CORE ).
    check LT_CORE is not initial.

*** Calculate minimum Core Target Length = Winding Width + Margin
    if IF_ROPCO is supplied.
      LS_CLETA = ZCL_VCXI_FFSS_CUST_CORE=>CALC_CLETA( IS_WIWID = IS_WIWID
                                                      IS_COMAR = IS_COMAR
                                                      IF_ROPCO = IF_ROPCO ).
    else.
      LS_CLETA = ZCL_VCXI_FFSS_CUST_CORE=>CALC_CLETA( IS_WIWID = IS_WIWID
                                                      IS_COMAR = IS_COMAR
                                                      IF_ROPCO = 1 ).
    endif.

***--------------------------------------------------------------------------------------
*** Filter List Result
    loop at LT_CORE into LS_CORE.

***   Core Material Type
      check LS_CORE-TCORE eq IF_TCORE.

***   Core Inner Diameter
      check ZCL_VCXI_XCS_SERVICE_CALC=>COMPARE_WITH_ANY( IF_CALCV_1     = LS_CORE-CRDII
                                                         IF_CALCV_UOM_1 = LS_CORE-CRDII_UOM
                                                         IF_CALCV_2     = IS_CRDII-CRDII
                                                         IF_CALCV_UOM_2 = IS_CRDII-CRDII_UOM
                                                         IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_EQ ) eq ABAP_TRUE.

***   Check Core Wall Thickness >= Core Wall Thickness Minimum
      check ZCL_VCXI_XCS_SERVICE_CALC=>COMPARE_WITH_ANY( IF_CALCV_1     = LS_CORE-CRWTH
                                                         IF_CALCV_UOM_1 = LS_CORE-CRWTH_UOM
                                                         IF_CALCV_2     = IS_CRWMI-CRWMI
                                                         IF_CALCV_UOM_2 = IS_CRWMI-CRWMI_UOM
                                                         IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_GE ) eq ABAP_TRUE.

***   Check Core Wall Thickness <= Core Wall Thickness Maximum
      check ZCL_VCXI_XCS_SERVICE_CALC=>COMPARE_WITH_ANY( IF_CALCV_1     = LS_CORE-CRWTH
                                                         IF_CALCV_UOM_1 = LS_CORE-CRWTH_UOM
                                                         IF_CALCV_2     = IS_CRWMA-CRWMA
                                                         IF_CALCV_UOM_2 = IS_CRWMA-CRWMA_UOM
                                                         IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_LE ) eq ABAP_TRUE.

***   Check Core Length >= Minimum Core Target Length
      check ZCL_VCXI_XCS_SERVICE_CALC=>COMPARE_WITH_ANY( IF_CALCV_1     = LS_CORE-CRLEN
                                                         IF_CALCV_UOM_1 = LS_CORE-CRLEN_UOM
                                                         IF_CALCV_2     = LS_CLETA-CLETA
                                                         IF_CALCV_UOM_2 = LS_CLETA-CLETA_UOM
                                                         IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_GE ) eq ABAP_TRUE.

***   Check Roll Cutting
      check LS_CORE-CRCUT eq ABAP_TRUE or
            ( LS_CORE-CRCUT eq ABAP_FALSE and
              IF_ROPCO le 1 ).

***--------------------------------------------------------------------------------------
***   Determine Rolls/Core
      if IF_ROPCO is supplied.
        move IF_ROPCO to LS_CORE-ROPCO.
        move LS_CLETA to LS_CORE-S_CLETA.
      elseif LS_CORE-CRCUT eq ABAP_FALSE.
        move 1        to LS_CORE-ROPCO.
        move LS_CLETA to LS_CORE-S_CLETA.
      else.
***     Calculate & Write Roles/Core to the corresponding field
***     Roles/Core = floor( Core Length / ( Winding Width + Margin )  )
        ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_CORE-CRLEN
                                                            IF_CALCV_UOM_1 = LS_CORE-CRLEN_UOM
                                                            IF_CALCV_2     = LS_CLETA-CLETA
                                                            IF_CALCV_UOM_2 = LS_CLETA-CLETA_UOM
                                                            IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_DIVIDE
                                                  importing EF_CALCV       = LS_CALCV-CALCV ).
        LS_CORE-ROPCO = FLOOR( LS_CALCV-CALCV ).

***     Determine Target Core Length
        LS_CORE-S_CLETA = ZCL_VCXI_FFSS_CUST_CORE=>CALC_CLETA( IS_WIWID = IS_WIWID
                                                               IS_COMAR = IS_COMAR
                                                               IF_ROPCO = LS_CORE-ROPCO ).
      endif.

***   Waste/Role = ( Core Length - Target Core Length ) / Roles/Core
      if LS_CORE-ROPCO is not initial.
        LS_CORE-WAPRO = ( LS_CORE-CRLEN - LS_CORE-CLETA ) / LS_CORE-ROPCO.
      endif.
      move LS_CORE-CRLEN_UOM to LS_CORE-WAPRO_UOM.

      append LS_CORE to LT_CORE_VALID.
    endloop.
    check LT_CORE_VALID is not initial.

***--------------------------------------------------------------------------------------
*** Sort possible Core Materials by [ Waste/Role && Roles/Core && material number itself ].
    sort LT_CORE_VALID by WAPRO ascending ROPCO descending MATNR ascending.

*** Return the best Core Material
    read table LT_CORE_VALID into LS_CORE index 1.
    if SY-SUBRC eq 0.
      move LS_CORE-MATNR to RF_MATNR.
    endif.

  endmethod.
  method GET_CORE_MATERIALS_LIST.

    types: begin of YS_CORE_WERKS,
             WERKS  type WERKS_D,
             T_CORE  type ZVCXI_FFS_TT_CORE,
           end   of YS_CORE_WERKS.

    data: LF_MTART_NR type        I,
          LF_MATKL_NR type        I,
          LF_MATNR    type        MATNR.
    data: LS_TC470                     type        ZVCXI_FFS_TC470.
    data: LT_TC470 type table of ZVCXI_FFS_TC470,
          LT_WHERE type table of EDPLINE,
          LT_MATNR type standard table of MATNR.
    data: LR_CX_SY_DYN_CALL_ERROR      type ref to CX_SY_DYN_CALL_ERROR,
          LR_CX_SY_DYNAMIC_OSQL_SYNTAX type ref to CX_SY_DYNAMIC_OSQL_SYNTAX,
          LR_CUST_CORE                 type ref to ZCL_VCXI_FFSS_CUST_CORE.
    field-symbols: <S_WHERE> type EDPLINE,
                   <S_TC470> type ZVCXI_FFS_TC470,
                   <S_CORE>  type ZVCXI_FFS_S_CORE.
    statics: LT_CORE_WERKS type table of YS_CORE_WERKS.


***--------------------------------------------------------------------------------------
*** Check Buffer
    if LINE_EXISTS( LT_CORE_WERKS[ WERKS = IF_WERKS ] ).
      ET_CORE = LT_CORE_WERKS[ WERKS = IF_WERKS ]-T_CORE.
      exit.
    endif.

***--------------------------------------------------------------------------------------
    move T_TC470 to LT_TC470.

*** Get all Plant or Global related entries from the customizing
    read table LT_TC470 with key WERKS = IF_WERKS transporting no fields.
    if SY-SUBRC eq 0.
      delete LT_TC470 where WERKS ne IF_WERKS.
    else.
      delete LT_TC470 where WERKS ne ''.
    endif.

*** Entries with no material type or exit function won't be considered.
*** It is necessary to specify at least material type or function module.
    delete LT_TC470 where MTART is initial and FNAME is initial.

*** Check Roll Defaulting is Cusomized
    check LT_TC470 is not initial.

***--------------------------------------------------------------------------------------
*** Use Exit function if it was specified
    loop at LT_TC470 into LS_TC470 where FNAME is not initial.

***   Dynamic Call of Function Module
      try.
          call function LS_TC470-FNAME
            exporting
              IF_WERKS = IF_WERKS
            importing
              ET_CORE  = ET_CORE.
        catch CX_SY_DYN_CALL_ERROR into LR_CX_SY_DYN_CALL_ERROR.
          /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_SY_DYN_CALL_ERROR ).
      endtry.

      exit.
    endloop.

    if SY-SUBRC ne 0.

***--------------------------------------------------------------------------------------
***   Select from Material Master

***   The entry with empty material group overrules the entry with material group
      loop at LT_TC470 into LS_TC470 where MATKL is initial.
        delete LT_TC470 where MTART = LS_TC470-MTART and MATKL is not initial.
      endloop.

      sort LT_TC470 ascending by MTART MATKL.

***--------------------------------------------------------------------------------------
***   Create Where Conditions table
      loop at LT_TC470 assigning <S_TC470>.

        at first.
          clear: LF_MTART_NR.
          append initial line to LT_WHERE assigning <S_WHERE>.

          if <S_TC470>-WERKS is not initial.
            concatenate 'MARC~WERKS eq ' ##NO_TEXT
                        ''''  <S_TC470>-WERKS '''' into <S_WHERE> respecting blanks.
          else.
            concatenate 'MARC~WERKS eq ' ##NO_TEXT
                        ''''  IF_WERKS        '''' into <S_WHERE> respecting blanks.
          endif.

          append initial line to LT_WHERE assigning <S_WHERE>.
          move ' AND ( '  to <S_WHERE>.
        endat.

        at new MTART.
          add 1 to LF_MTART_NR.
          clear LF_MATKL_NR.

          append initial line to LT_WHERE assigning <S_WHERE>.
          if LF_MTART_NR gt 1.
            move '      OR' to <S_WHERE>.
            append initial line to LT_WHERE assigning <S_WHERE>.
          endif.
          concatenate  '      ( MARA~MTART eq ' ##NO_TEXT
                       '''' <S_TC470>-MTART '''' into <S_WHERE> respecting blanks.
        endat.

        if <S_TC470>-MATKL is not initial.
          add 1 to LF_MATKL_NR.

          append initial line to LT_WHERE assigning <S_WHERE>.
          if LF_MATKL_NR gt 1.
            move '                  ) ' to <S_WHERE>.
            append initial line to LT_WHERE assigning <S_WHERE>.
            move '                  OR ' to <S_WHERE>.
            append initial line to LT_WHERE assigning <S_WHERE>.
          else.
            move '            AND ( ' to <S_WHERE>.
            append initial line to LT_WHERE assigning <S_WHERE>.
          endif.
          concatenate  '                  ( MARA~MATKL eq ' ##NO_TEXT
                       '''' <S_TC470>-MATKL '''' into <S_WHERE> respecting blanks.
        endif.

        at end of MTART.
          if LF_MATKL_NR is not initial.
            append initial line to LT_WHERE assigning <S_WHERE>.
            move '                  ) ' to <S_WHERE>.
            append initial line to LT_WHERE assigning <S_WHERE>.
            move '            ) ' to <S_WHERE>.
          endif.
          append initial line to LT_WHERE assigning <S_WHERE>.
          move '      ) ' to <S_WHERE>.
        endat.

        at last.
          append initial line to LT_WHERE assigning <S_WHERE>.
***       Ignore deleted materials
          move ') AND MARA~LVORM ne ''X'' AND MARC~LVORM ne ''X'' ' to <S_WHERE>.
        endat.
      endloop.

      if <S_TC470> is assigned.
        try .

***--------------------------------------------------------------------------------------
***         Material Master
            select MARC~MATNR
              into table LT_MATNR
              from MARC
              join MARA on MARA~MATNR eq MARC~MATNR
                where (LT_WHERE).

          catch CX_SY_DYNAMIC_OSQL_SYNTAX into LR_CX_SY_DYNAMIC_OSQL_SYNTAX.
            /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_SY_DYNAMIC_OSQL_SYNTAX ).
        endtry.
      endif.

    endif.

***--------------------------------------------------------------------------------------
*** Return Core Materials with characteristics
    loop at LT_MATNR into LF_MATNR.

***   Get Characteristics Info
      clear LR_CUST_CORE.
      try.
          LR_CUST_CORE = ZCL_VCXI_FFSS_CUST_CORE=>GET_INSTANCE_CORE( IF_MATNR = LF_MATNR
                                                                     IF_WERKS = IF_WERKS
                                                                     IF_DATE  = SY-DATUM ).
        catch /VCXI/CX_CKX.
***       Do not stop when one material could not be instanciated, but continue.
          continue.
      endtry.

      if LR_CUST_CORE is bound.

***     Entries with no material type won't be considered.
***     It is necessary to specify a material type.
        check LR_CUST_CORE->F_TCORE is not initial.

        append initial line to ET_CORE assigning <S_CORE>.
        move LF_MATNR                 to <S_CORE>-MATNR.
        move LR_CUST_CORE->S_CRDII    to <S_CORE>-S_CRDII.
        move LR_CUST_CORE->S_CRWTH    to <S_CORE>-S_CRWTH.
        move LR_CUST_CORE->S_CRLEN    to <S_CORE>-S_CRLEN.
        move LR_CUST_CORE->S_CRWGT    to <S_CORE>-S_CRWGT.
        move LR_CUST_CORE->F_CRCUT    to <S_CORE>-CRCUT.
        move LR_CUST_CORE->F_TCORE    to <S_CORE>-TCORE.
      endif.
    endloop.

*** Add to Buffer
    append value #( WERKS = IF_WERKS
                    T_CORE = ET_CORE ) to LT_CORE_WERKS.

  endmethod.
  method GET_DFLT_FG.

    types:
      begin of YS_BUFFER,
        WERKS        type WERKS_D,
        KUNAG        type KUNAG,
        KUNWE        type KUNWE,
        S_RHNDL_DFLT type ZVCXI_FFSS_S_RHNDL_DFLT,
      end   of YS_BUFFER.

    statics: LT_BUFFER type sorted table of YS_BUFFER with unique key WERKS KUNAG KUNWE.
    data: LS_RHNDL_DFLT type ZVCXI_FFSS_S_RHNDL_DFLT.

*** Check request is completely specified
    check IF_WERKS is not initial and
          IF_KUNWE is not initial.

    if not LINE_EXISTS( LT_BUFFER[ WERKS = IF_WERKS
                                   KUNAG = IF_KUNAG
                                   KUNWE = IF_KUNWE ] ).
***   Get Customizing by Access Sequence
      select single *
             into corresponding fields of @LS_RHNDL_DFLT
             from ZVCXI_FFS_TC440
            where WERKS eq @IF_WERKS
             and  KUNNR eq @IF_KUNWE.
      if SY-SUBRC ne 0.
        select single WERKS          "Consider Sold-to only if Ship-To is not a Plant
               into @data(LF_WERKS)
               from T001W
              where KUNNR = @IF_KUNWE.
        if SY-SUBRC ne 0.
          select single *
                 into corresponding fields of @LS_RHNDL_DFLT
                 from ZVCXI_FFS_TC440
                where WERKS eq @IF_WERKS
                 and  KUNNR eq @IF_KUNAG.
        else.
          SY-SUBRC = 9. "Force next Access Sequence Step
        endif.
        if SY-SUBRC ne 0.
          select single *
                 into corresponding fields of @LS_RHNDL_DFLT
                 from ZVCXI_FFS_TC440
                where WERKS eq @IF_WERKS
                 and  KUNNR eq @SPACE.
          if SY-SUBRC ne 0.
            select single *
                   into corresponding fields of @LS_RHNDL_DFLT
                   from ZVCXI_FFS_TC440
                  where WERKS eq @SPACE
                   and  KUNNR eq @SPACE.
            if SY-SUBRC ne 0.
              clear LS_RHNDL_DFLT.
            endif.
          endif.
        endif.
      endif.

***   Add to Buffer
      insert value #( WERKS        = IF_WERKS
                      KUNAG        = IF_KUNAG
                      KUNWE        = IF_KUNWE
                      S_RHNDL_DFLT = LS_RHNDL_DFLT ) into table LT_BUFFER.
    endif.

*** Get buffered Value
    RS_RHNDL_DFLT = LT_BUFFER[ WERKS = IF_WERKS
                               KUNAG = IF_KUNAG
                               KUNWE = IF_KUNWE ]-S_RHNDL_DFLT.

  endmethod.
  method GET_DFLT_WIP.

    types:
      begin of YS_BUFFER,
        WERKS        type WERKS_D,
        ARBPL        type ARBPL,
        S_RHNDL_DFLT type ZVCXI_FFSS_S_RHNDL_DFLT,
      end   of YS_BUFFER.

    statics: LT_BUFFER type sorted table of YS_BUFFER with unique key WERKS ARBPL.
    data: LS_RHNDL_DFLT type ZVCXI_FFSS_S_RHNDL_DFLT.


    if not LINE_EXISTS( LT_BUFFER[ WERKS = IF_WERKS
                                   ARBPL = IF_ARBPL ] ).
***   Get Customizing by Access Sequence
      select single *
             into corresponding fields of LS_RHNDL_DFLT
             from ZVCXI_FFS_TC450
            where WERKS eq IF_WERKS
             and  ARBPL eq IF_ARBPL.
      if SY-SUBRC ne 0.
        select single *
               into corresponding fields of LS_RHNDL_DFLT
               from ZVCXI_FFS_TC450
              where WERKS eq IF_WERKS
               and  ARBPL eq SPACE.
        if SY-SUBRC ne 0.
          select single *
                 into corresponding fields of LS_RHNDL_DFLT
                 from ZVCXI_FFS_TC450
                where WERKS eq SPACE
                 and  ARBPL eq SPACE.
          if SY-SUBRC ne 0.
            clear LS_RHNDL_DFLT.
          endif.
        endif.
      endif.

***   Add to Buffer
      insert value #( WERKS        = IF_WERKS
                      ARBPL        = IF_ARBPL
                      S_RHNDL_DFLT = LS_RHNDL_DFLT ) into table LT_BUFFER.
    endif.

*** Get buffered Value
    RS_RHNDL_DFLT = LT_BUFFER[ WERKS = IF_WERKS
                               ARBPL = IF_ARBPL ]-S_RHNDL_DFLT.

  endmethod.
  method GET_RWGNA.

    data:  LS_TC410  type ZVCXI_FFS_TC410.

*** Language-dependent Entry
    read table T_TC410 into LS_TC410 with key RWSID = IF_RWSID
                                              RWDIR = IF_RWDIR
                                              EYEMP = IF_EYEMP
                                              LANGU = SY-LANGU.
    if SY-SUBRC ne 0.
***   Language-independent Entry
      read table T_TC410 into LS_TC410 with key RWSID = IF_RWSID
                                                RWDIR = IF_RWDIR
                                                EYEMP = IF_EYEMP
                                                LANGU = SPACE.
    endif.

    check SY-SUBRC eq 0.
    move LS_TC410-RWGNA to RF_RWGNA.

  endmethod.
