
class ZCL_VCXI_XCDCU_GRID_CR_EXIT definition
  public
  inheriting from ZCL_VCXI_XCDCU_GRID
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IF_TABNAME type TABNAME optional
      !IF_CONTAINER_NAME type SCRFNAME optional
      !IR_CUSTOMIZING type ref to OBJECT
      !IR_DUI_CONDREC type ref to ZCL_VCXI_XCDCU_DUI_CONDREC
    raising
      /VCXI/CX_CKX .
protected section.

  data R_CONDREC type ref to ZCL_VCXI_XCDC_CONDREC .
  constants C_FUNC_ADD type UI_FUNC value 'ADD'. "#EC NOTEXT
  constants C_FUNC_DEL type UI_FUNC value 'DELETE'. "#EC NOTEXT
  data R_DUI_CONDREC type ref to ZCL_VCXI_XCDCU_DUI_CONDREC .

  methods HANDLE_DOUBLE_CLICK
    for event DOUBLE_CLICK of CL_GUI_ALV_GRID
    importing
      !E_ROW
      !E_COLUMN
      !ES_ROW_NO .
  methods ADD_EXIT
    raising
      /VCXI/CX_CKX .
  methods DELETE_EXIT
    importing
      !IT_CR_EXIT type ZVCXI_XCDC_TT_CR_EXIT
    raising
      /VCXI/CX_CKX .
  methods CHECK_SEL_DELETE
    returning
      value(RF_OKAY) type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
  methods HANDLE_TOOLBAR
    for event TOOLBAR of CL_GUI_ALV_GRID
    importing
      !E_OBJECT
      !E_INTERACTIVE .
  methods HANDLE_USER_COMMAND
    for event USER_COMMAND of CL_GUI_ALV_GRID
    importing
      !E_UCOMM .

  methods GET_GRID_DATA
    redefinition .
  methods GET_LAYOUT
    redefinition .
  methods INIT_LOCKED
    redefinition .
  methods SET_EVENT
    redefinition .
private section.
endclass. "ZCL_VCXI_XCDCU_GRID_CR_EXIT definition
class ZCL_VCXI_XCDCU_GRID_CR_EXIT implementation.
method ADD_EXIT.

  data:          LS_RETURN         type                   DDSHRETVAL,
                 LS_CD_EXIT        type                   ZVCXI_XCDC_S_CD_EXIT,
                 LS_CR_EXIT        type                   ZVCXI_XCDC_S_CR_EXIT.
  data:          LT_RETURN         type table of          DDSHRETVAL,
                 LT_CD_EXIT_SHLP   type standard table of ZVCXI_XCDCU_S_CD_EXIT_SHLP.
  field-symbols: <S_CD_EXIT_SHLP>  type                   ZVCXI_XCDCU_S_CD_EXIT_SHLP.


  check ME->R_CONDREC                      is bound and
        ME->R_CONDREC->R_CONDDEF           is bound.
  loop at ME->R_CONDREC->R_CONDDEF->T_CD_EXIT into LS_CD_EXIT.
*** Show only Exits which are not already used.
    read table ME->R_CONDREC->T_CR_EXIT
    with key IEXIT = LS_CD_EXIT-IEXIT
    transporting no fields.
    check SY-SUBRC ne 0.

    append initial line to LT_CD_EXIT_SHLP assigning <S_CD_EXIT_SHLP>.
    move LS_CD_EXIT-IEXIT to <S_CD_EXIT_SHLP>-IEXIT.
    move LS_CD_EXIT-FNAME to <S_CD_EXIT_SHLP>-FNAME.

  endloop.

  call function 'F4IF_INT_TABLE_VALUE_REQUEST'
    exporting
      DDIC_STRUCTURE  = 'ZVCXI_XCDCU_S_CD_EXIT_SHLP'
      RETFIELD        = 'IEXIT'
      VALUE_ORG       = 'S'
      MULTIPLE_CHOICE = 'X'
    tables
      VALUE_TAB       = LT_CD_EXIT_SHLP
      RETURN_TAB      = LT_RETURN
    exceptions
      PARAMETER_ERROR = 1
      NO_VALUES_FOUND = 2
      others          = 3.
  if SY-SUBRC ne 0.
    /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
  endif.

*** Process Result
  loop at LT_RETURN into LS_RETURN.
    clear: LS_CR_EXIT-IEXIT.
    move  LS_RETURN-FIELDVAL to LS_CR_EXIT-IEXIT.

*** Add Default Element
    ME->R_CONDREC->ADD_CR_EXIT( IS_CR_EXIT = LS_CR_EXIT ).

  endloop.

endmethod.
method CHECK_SEL_DELETE.

  data: LT_CELL type LVC_T_CELL,
        LT_ROW  type LVC_T_ROW.

  ME->R_GRID->GET_SELECTED_CELLS( importing ET_CELL = LT_CELL ).
  ME->R_GRID->GET_SELECTED_ROWS( importing ET_INDEX_ROWS = LT_ROW ).

*** Delete is only available if Cells are selected
  if lines( LT_CELL ) gt 0 or
     lines( LT_ROW )  gt 0.
    move ABAP_TRUE to RF_OKAY.
  else.
    move ABAP_FALSE to RF_OKAY.
  endif.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IF_TABNAME        = 'ZVCXI_XCDCU_S_CR_EXIT_GRID'
                      IF_CONTAINER_NAME = '0100CC_GRID_EXIT'
                      IR_CUSTOMIZING    = IR_CUSTOMIZING ).

  move IR_DUI_CONDREC to ME->R_DUI_CONDREC.

*** Try to Assign Customizing Reference
  try.
      move IR_CUSTOMIZING ?to ME->R_CONDREC.
    catch CX_SY_MOVE_CAST_ERROR.
***   Condition Record Customizing Reference can't be assigned.
      /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE(
          IF_MSGTY = 'E'
          IF_MSGID = 'ZVCXI_XCDC'
          IF_MSGNO = '252' ).
  endtry.

  check ME->R_CONDREC is bound.
  ME->F_LOCKED = ME->R_CONDREC->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ).

endmethod.
method DELETE_EXIT.

  data: LS_CR_EXIT type ZVCXI_XCDC_S_CR_EXIT.

  loop at IT_CR_EXIT into LS_CR_EXIT.
*** Delete by ID
    ME->R_CONDREC->DELETE_CR_EXIT( IS_CR_EXIT = LS_CR_EXIT ).

  endloop.

endmethod.
method GET_GRID_DATA.

  data:          LS_CR_EXIT_GRID     type          ZVCXI_XCDCU_S_CR_EXIT_GRID,
                 LS_CR_EXIT          type          ZVCXI_XCDC_S_CR_EXIT.
  field-symbols: <T_DATA>            type standard table,
                 <S_DATA>            type          any.

*** Dereferencing of Data Reference
  assign IR_DATA->* to <T_DATA>.

  free: <T_DATA>.

*** --------------------------------------------------------------------------------------------
*** Get all Condition Values
  loop at ME->R_CONDREC->T_CR_EXIT into LS_CR_EXIT.
    clear: LS_CR_EXIT_GRID.

    move-corresponding LS_CR_EXIT to LS_CR_EXIT_GRID.

*** --------------------------------------------------------------------------------------------
*** Add Entry
    append initial line to <T_DATA> assigning <S_DATA>.
    move-corresponding LS_CR_EXIT_GRID to <S_DATA>.

  endloop.

endmethod.
method GET_LAYOUT.

  RS_LAYOUT = SUPER->GET_LAYOUT( ).

*** Set Toolbar to Activ
  move ABAP_FALSE to RS_LAYOUT-NO_TOOLBAR.
  move 'A'        to RS_LAYOUT-SEL_MODE.

endmethod.
method HANDLE_DOUBLE_CLICK.

  data:          LF_GREGE             type          /VCXI/CKR_GREGE.
  data:          LR_CX_CKX            type ref to   /VCXI/CX_CKX.

  try.

***   Handle Double Click
      case E_COLUMN-FIELDNAME.
        when 'IEXIT'.

          check ME->R_DUI_CONDREC is bound.

          ME->R_DUI_CONDREC->OPEN_CONDDEF( ).

      endcase.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      LR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

endmethod.
method HANDLE_TOOLBAR.

  data:  LS_TOOLBAR      type        STB_BUTTON.
  data:  LF_DISABLED     type        ABAP_BOOL.
  data:  LR_CX_CKX       type ref to /VCXI/CX_CKX.

  try.

      if ME->R_CONDREC->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ) eq ABAP_FALSE.
        move ABAP_TRUE to LF_DISABLED.
      endif.

***----------------------------------------------------------------------------
***   Insert Toolbar elements Add and Delete
      clear LS_TOOLBAR.
      move C_FUNC_ADD                 to LS_TOOLBAR-FUNCTION.
      move 'Add'(002)                 to LS_TOOLBAR-TEXT.
      move C_BTYPE_BUTTON             to LS_TOOLBAR-BUTN_TYPE.
      move ICON_INSERT_ROW            to LS_TOOLBAR-ICON.
      move LF_DISABLED                to LS_TOOLBAR-DISABLED.
      append LS_TOOLBAR               to E_OBJECT->MT_TOOLBAR.

      clear LS_TOOLBAR.
      move C_FUNC_DEL                 to LS_TOOLBAR-FUNCTION.
      move 'Delete'(003)              to LS_TOOLBAR-TEXT.
      move C_BTYPE_BUTTON             to LS_TOOLBAR-BUTN_TYPE.
      move ICON_DELETE_ROW            to LS_TOOLBAR-ICON.
      move LF_DISABLED                to LS_TOOLBAR-DISABLED.
      append LS_TOOLBAR               to E_OBJECT->MT_TOOLBAR.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      LR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

endmethod.
method HANDLE_USER_COMMAND.

  data:          LS_CELL         type          LVC_S_CELL,
                 LS_ROW          type          LVC_S_ROW.
  data:          LT_CELL         type          LVC_T_CELL,
                 LT_ROW          type          LVC_T_ROW,
                 LT_CR_EXIT      type          ZVCXI_XCDC_TT_CR_EXIT.
  data:          LR_CX_CKX       type ref to   /VCXI/CX_CKX.
  field-symbols: <T_DATA>        type standard table,
                 <S_CR_EXIT>     type          ZVCXI_XCDC_S_CR_EXIT,
                 <S_DATA>        type          any.

  try.

***   Get Value Data
      assign ME->R_DATA->* to <T_DATA>.
      free: <T_DATA>.
      ME->GET_GRID_DATA( IR_DATA = ME->R_DATA ).

      ME->R_GRID->GET_SELECTED_CELLS( importing ET_CELL = LT_CELL ).
      ME->R_GRID->GET_SELECTED_ROWS( importing ET_INDEX_ROWS = LT_ROW ).

      case E_UCOMM.

        when C_FUNC_ADD.
***       Add new Condition Exit
          ME->ADD_EXIT( ).

        when C_FUNC_DEL.
***       Delete Condition Exit
          if ME->CHECK_SEL_DELETE( ) eq ABAP_FALSE.
***         No Row Selected. Please Select all Row's you wanna Delete.
            raise exception type /VCXI/CX_CKX
             exporting
                F_MSGTY = 'E'
                F_MSGID = 'ZVCXI_XCDCU'
                F_MSGNO = '301'.
          endif.

***       Get Selected Condition Exit's
          loop at LT_CELL into LS_CELL.
            read table <T_DATA> assigning <S_DATA> index LS_CELL-ROW_ID-INDEX.
            check SY-SUBRC = 0.

            append initial line to LT_CR_EXIT assigning <S_CR_EXIT>.
            move-corresponding <S_DATA> to <S_CR_EXIT>.
          endloop.

          loop at LT_ROW into LS_ROW.
            read table <T_DATA> assigning <S_DATA> index LS_ROW-INDEX.
            check SY-SUBRC = 0.

            append initial line to LT_CR_EXIT assigning <S_CR_EXIT>.
            move-corresponding <S_DATA> to <S_CR_EXIT>.
          endloop.

***       Delete Condition Exit's
          ME->DELETE_EXIT( IT_CR_EXIT = LT_CR_EXIT ).

      endcase.

      ME->F_REFRESH_REQ = ABAP_TRUE.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      LR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

endmethod.
method INIT_LOCKED.

  ME->F_LOCKED = ME->R_CONDREC->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ).

endmethod.
method SET_EVENT.

  SUPER->SET_EVENT( ).

*** Add Event for Double Click, UserCommand and Toolbar
  set handler ME->HANDLE_USER_COMMAND for ME->R_GRID.
  set handler ME->HANDLE_TOOLBAR      for ME->R_GRID.
  set handler ME->HANDLE_DOUBLE_CLICK for ME->R_GRID.

endmethod.
