
class ZCL_VCXI_P3SS_UI_ASCPMCNF definition
  public
  inheriting from ZCL_VCXI_PXSS_UI_XCPMCNF
  create public .

public section.

  data S_ASCPMCNF_VIS type ZVCXI_P3SS_S_ASCPMCNF_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABPT type ZVCXI_XCS_TABPT default '0100S_CPM_TAB-TAB'
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
  methods DO_PBO
    redefinition .
protected section.

  constants C_UC_ASPROP type SYUCOMM value 'AFG_ASPROP' ##NO_TEXT.
  data R_PR_ASCPMCNF type ref to ZCL_VCXI_P3SS_PR_ASCPMCNF .
  data R_TOOLBAR type ref to ZCL_VCXI_XCS_TOOLBAR_PRVDR .

  methods HANDLE_UC_ASPROP
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_P3SS_UI_ASCPMCNF definition
class ZCL_VCXI_P3SS_UI_ASCPMCNF implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

***--------------------------------------------------------------------------------------
*** Bind UI to Dynpro
    call function 'ZVCXI_P3SS_UI_ASCPMCNF_BIND'
      exporting
        IR_UI_ASCPMCNF = ME
      importing
        ES_DYNP        = ME->S_DYNP
        EF_ACTIVE_TAB  = ME->F_ACTIVE_TAB.

***--------------------------------------------------------------------------------------
*** Create Toolbar to add Connection
    create object ME->R_TOOLBAR
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_TOOLBAR'.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE
                        IF_TABPT  = IF_TABPT ).

*** Assign th RP Class to the UI Class
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_ASCPMCNF.
      catch CX_SY_MOVE_CAST_ERROR.

***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

    if ME->R_TOOLBAR is bound.
      ME->R_TOOLBAR->DO_FREE( ).
      clear ME->R_TOOLBAR.
    endif.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Super PBO
    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize Toolbar
    if ME->R_TOOLBAR is bound.
      ME->R_TOOLBAR->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_TOOLBAR->REFRESH_TOOLBAR( IT_PRVDR_SELECTED = value #( ( ME->R_OLD_GATE ) ) ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_ASCPMCNF_VIS  type ZVCXI_P3SS_S_ASCPMCNF_VIS.


*** Super
    SUPER->DO_UPDATE_VIS( IS_VIS = IS_VIS ).
    check ME->R_PR_ASCPMCNF is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_ASCPMCNF_VIS.

***--------------------------------------------------------------------------------------
*** Set Product Length
    if ME->S_ASCPMCNF_VIS-S_PROLE ne LS_ASCPMCNF_VIS-S_PROLE.
      ME->R_PR_ASCPMCNF->SET_PROLE( IS_PROLE = LS_ASCPMCNF_VIS-S_PROLE
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Product Width
    if ME->S_ASCPMCNF_VIS-S_PROWI ne LS_ASCPMCNF_VIS-S_PROWI.
      ME->R_PR_ASCPMCNF->SET_PROWI( IS_PROWI = LS_ASCPMCNF_VIS-S_PROWI
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Product Height
    if ME->S_ASCPMCNF_VIS-S_PROHE ne LS_ASCPMCNF_VIS-S_PROHE.
      ME->R_PR_ASCPMCNF->SET_PROHE( IS_PROHE = LS_ASCPMCNF_VIS-S_PROHE
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    case IF_UCOMM.
      when C_UC_ASPROP.
        ME->HANDLE_UC_ASPROP( ).

    endcase.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_ASCPMCNF.

  endmethod.
  method HANDLE_UC_ASPROP.

*** Start Popup of Properties (Outbound)
    ZCL_VCXI_P3SS_POPUP_ASPROP=>START( IR_BL_ASBPROP = ME->R_PR_ASCPMCNF->GET_ASBPX( )
                                       IR_BL_ASFPROP = ME->R_PR_ASCPMCNF->GET_ASFPX( )
                                       IF_WERKS      = ME->R_PR_ASCPMCNF->GET_WERKS( ) ).


  endmethod.
  method INIT_VIS.

    data: LR_BL_ASFPROP type ref to ZCL_VCXI_P3SS_BL_ASFPROP.


    free: ME->S_ASCPMCNF_VIS.

    SUPER->INIT_VIS( ).
    ME->S_ASCPMCNF_VIS-S_XCPMCNF_VIS = ME->S_XCPMCNF_VIS.
    check ME->R_PR_ASCPMCNF is bound.

***------------------------------------------------------------------------------------------------
*** Get Product Dimension
    ME->S_ASCPMCNF_VIS-PRODIM_DESCR = ME->R_PR_ASCPMCNF->GET_PRODIM_DESCR( ).
    ME->S_ASCPMCNF_VIS-S_PROWI = ME->R_PR_ASCPMCNF->GET_PROWI( ).
    ME->S_ASCPMCNF_VIS-S_PROLE = ME->R_PR_ASCPMCNF->GET_PROLE( ).
    ME->S_ASCPMCNF_VIS-S_PROHE = ME->R_PR_ASCPMCNF->GET_PROHE( ).


***------------------------------------------------------------------------------------------------
*** Get Handling Dimension (Outbound)
    LR_BL_ASFPROP = ME->R_PR_ASCPMCNF->GET_ASFPX( ).
    if LR_BL_ASFPROP is bound.
      ME->S_ASCPMCNF_VIS-S_HNDDIM = LR_BL_ASFPROP->S_HNDDIM.
    endif.

  endmethod.
