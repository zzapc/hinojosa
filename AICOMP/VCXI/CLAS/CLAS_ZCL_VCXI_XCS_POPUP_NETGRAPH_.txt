
class ZCL_VCXI_XCS_POPUP_NETGRAPH definition
  public
  create protected .

public section.

  constants C_UC_CANCEL type SYUCOMM value 'CANCEL' ##NO_TEXT.
  constants C_UC_OKAY type SYUCOMM value 'OKAY' ##NO_TEXT.

  methods DO_USER_COMMAND
    importing
      !IF_UCOMM type SYUCOMM
    raising
      /VCXI/CX_CKX .
  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  class-methods START
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
protected section.

  data R_CONTAINER type ref to CL_GUI_CUSTOM_CONTAINER .
  data R_NETGRAPH type ref to ZCL_VCXI_XCS_NETGRAPH .

  methods HANDLE_UC_CANCEL
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_OKAY
    raising
      /VCXI/CX_CKX .
  methods SET_PF_STATUS
    raising
      /VCXI/CX_CKX .
  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
private section.

  methods DO_FREE
    raising
      /VCXI/CX_CKX .
endclass. "ZCL_VCXI_XCS_POPUP_NETGRAPH definition
class ZCL_VCXI_XCS_POPUP_NETGRAPH implementation.
  method CONSTRUCTOR.

    ME->R_NETGRAPH = new #( IR_GATE           = IR_GATE
                            IF_CONTAINER_NAME = '0100CC_NETGRAPH' ).

  endmethod.
  method DO_FREE.

*** Free Network Graph
    if ME->R_NETGRAPH is bound.
      ME->R_NETGRAPH->DO_FREE( ).
      clear ME->R_NETGRAPH.
    endif.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Set PF-Status
    ME->SET_PF_STATUS( ).

*** Refresh Network Graph
    if ME->R_NETGRAPH is bound.
      ME->R_NETGRAPH->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_NETGRAPH->REFRESH_NETGRAPH( ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    case IF_UCOMM.
      when C_UC_OKAY.
        ME->HANDLE_UC_OKAY( ).

      when C_UC_CANCEL.
        ME->HANDLE_UC_CANCEL( ).

    endcase.

  endmethod.
  method HANDLE_UC_CANCEL.

*** Leave Popup
    leave to screen 0.

  endmethod.
  method HANDLE_UC_OKAY.

*** Leave Popup
    leave to screen 0.

  endmethod.
  method SET_PF_STATUS.

    set pf-status '0100' of program 'SAPLZVCXI_XCS_POPUP_NETGRAPH'.

  endmethod.
  method START.

    data: LR_POPUP_NETGRAPH type ref to ZCL_VCXI_XCS_POPUP_NETGRAPH.

*** Create Instance
    create object LR_POPUP_NETGRAPH
      exporting
        IR_GATE = IR_GATE.

*** Call Popup
    call function 'ZVCXI_XCS_POPUP_NETGRAPH_START'
      exporting
        IR_POPUP_NETGRAPH = LR_POPUP_NETGRAPH.

*** Free the Popup
    LR_POPUP_NETGRAPH->DO_FREE( ).

  endmethod.
