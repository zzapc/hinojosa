
class ZCL_VCXI_XCSP_GRID_MC_PACKMAT definition
  public
  inheriting from ZCL_VCXI_XCS_GRID_PRVDR
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_CONTAINER_NAME type SCRFNAME
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL default ABAP_TRUE
    raising
      /VCXI/CX_CKX .
protected section.

  data F_PURCHASED type ABAP_BOOL .

  methods CHANGE_STYLE_MC_PACKMAT
    importing
      !IF_PURCH type ZVCXI_XCS_PURCH
      !IF_DUMMY type ABAP_BOOL
    changing
      !CT_STYL type LVC_T_STYL
    raising
      /VCXI/CX_CKX .
  methods GET_MC_PACKMAT_VIS
    importing
      !IR_PR_MC_PACKMAT type ref to ZCL_VCXI_XCSP_PR_MC_PACKMAT
    returning
      value(RS_MC_PACKMAT_VIS) type ZVCXI_XCSP_S_MC_PACKMAT_VIS
    raising
      /VCXI/CX_CKX .
  methods SET_MC_PACKMAT_VIS
    importing
      !IR_PR_MC_PACKMAT type ref to ZCL_VCXI_XCSP_PR_MC_PACKMAT
      !IS_MC_PACKMAT_VIS type ZVCXI_XCSP_S_MC_PACKMAT_VIS
    raising
      /VCXI/CX_CKX .

  methods GET_FIELDCAT
    redefinition .
  methods GET_GRID_DATA
    redefinition .
  methods HANDLE_DATA_CHANGED_FINISHED
    redefinition .
private section.
endclass. "ZCL_VCXI_XCSP_GRID_MC_PACKMAT definition
class ZCL_VCXI_XCSP_GRID_MC_PACKMAT implementation.
method CHANGE_STYLE_MC_PACKMAT.

  free CT_STYL.

***--------------------------------------------------------------------------------------------
*** Set Style Details
*** Set Packaging Matnr = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MATNR'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Average Consumption = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'AVGCO'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Average Consumption Per = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'AVGCP'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Text = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'TEXT'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

***--------------------------------------------------------------------------------------------
*** Set Vendor, Price and Price Unit based on Procurement Option
  case IF_PURCH.
    when ZCL_VCXI_XCSP_PR_MC=>C_PURCH_DIRECT_PRICE.
***   Set Vendor = Enable
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR'
                                  IF_STYLE     = ME->F_STYLE_ENABLED
                        changing  CT_STYL      = CT_STYL ).
***   Set Price = Enable
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                                  IF_STYLE     = ME->F_STYLE_ENABLED
                        changing  CT_STYL      = CT_STYL ).
***   Set Price Per = Enable
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                                  IF_STYLE     = ME->F_STYLE_ENABLED
                        changing  CT_STYL      = CT_STYL ).

    when ZCL_VCXI_XCSP_PR_MC=>C_PURCH_DIRECT.
***   Set Vendor = Enable
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR'
                                  IF_STYLE     = ME->F_STYLE_ENABLED
                        changing  CT_STYL      = CT_STYL ).
***   Set Price = Disable
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                                  IF_STYLE     = ME->F_STYLE_DISABLED
                        changing  CT_STYL      = CT_STYL ).
***   Set Price Per = Disable
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                                  IF_STYLE     = ME->F_STYLE_DISABLED
                        changing  CT_STYL      = CT_STYL ).

    when ZCL_VCXI_XCSP_PR_MC=>C_PURCH_STOCK.
***   Set Vendor = Disable
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR'
                                  IF_STYLE     = ME->F_STYLE_DISABLED
                        changing  CT_STYL      = CT_STYL ).
***   Set Price = Disable
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                                  IF_STYLE     = ME->F_STYLE_DISABLED
                        changing  CT_STYL      = CT_STYL ).
***   Set Price Per = Disable
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                                  IF_STYLE     = ME->F_STYLE_DISABLED
                        changing  CT_STYL      = CT_STYL ).
  endcase.

***--------------------------------------------------------------------------------------------
*** Set Purchase Flag not editable in case of Dummy
  if IF_DUMMY eq ABAP_TRUE.
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PURCH'
                                IF_STYLE     = ME->F_STYLE_DISABLED
                      changing  CT_STYL      = CT_STYL ).
  else.
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PURCH'
                                IF_STYLE     = ME->F_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).
  endif.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_GATE           = IR_GATE
                      IR_UIGATE         = IR_UIGATE
                      IF_TABNAME        = 'ZVCXI_XCSP_S_MC_PACKMAT_GRID'
                      IF_CONTAINER_NAME = IF_CONTAINER_NAME
                      IF_APPL_EVENTS    = IF_APPL_EVENTS ).

*** Define Provider Type for Button
  ME->F_IPRVT = ZCL_VCXI_XCSP_PR_MC=>C_IPRVT_XCS_MC.

endmethod.
method GET_FIELDCAT.

  data: LF_LOCKED type        /VCXI/CKX_LOCKED,
        LF_INUSE  type        /VCXI/VKSR_INUSE,
        LF_COLPOS type        I                 value 4.
  field-symbols: <S_FCAT>  type        LVC_S_FCAT.

***----------------------------------------------------------------------------
*** Get Standard Field Catalog
  RT_FCAT = SUPER->GET_FIELDCAT( ).

*** Get Lock and In-Use Status
  LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
  LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***----------------------------------------------------------------------------
*** Adjust Field Catalog
  loop at RT_FCAT assigning <S_FCAT>.
    case <S_FCAT>-FIELDNAME.
      when 'POSID' or 'GATKY' or 'R_GATE' or 'T_STYL'.
        move ABAP_TRUE    to <S_FCAT>-TECH.

*** Set Edit for all fields which should Editable at all
*** -> Don't forget to set also a Style for this Field
      when 'MATNR' or 'TEXT' or 'AVGCO' or 'AVGCP'.
        if LF_LOCKED eq ABAP_TRUE and
           LF_INUSE  eq ABAP_TRUE.
          move ABAP_TRUE    to <S_FCAT>-EDIT.
        endif.

*** Set Edit for Price fields if it is purchased
*** And Hide it if not needed
*** -> Don't forget to set also a Style for this Field
      when 'LIFNR' or 'PRICE' or 'PRICP'.
        if ME->F_PURCHASED eq ABAP_TRUE.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE    to <S_FCAT>-EDIT.
          endif.
        else.
          move ABAP_TRUE    to <S_FCAT>-NO_OUT.
        endif.

***   Purchase Flag always editable
      when 'PURCH'.
        if LF_LOCKED eq ABAP_TRUE and
           LF_INUSE  eq ABAP_TRUE.
          move ABAP_TRUE    to <S_FCAT>-EDIT.
        endif.

      when 'LIFNR_DESCR' or 'PRICE_CURR' or 'PRICP_UOM'.
        if ME->F_PURCHASED eq ABAP_FALSE.
          move ABAP_TRUE    to <S_FCAT>-NO_OUT.
        endif.

    endcase.

*** Set Position for each Field
    if <S_FCAT>-FIELDNAME eq 'MATNR'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'MATNR_DESCR'. <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'CONSU'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'CONSU_UOM'.   <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'AVGCO'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'AVGCO_UOM'.   <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'AVGCP'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'AVGCP_UOM'.   <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'TEXT'.        <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'PURCH'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'LIFNR'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'LIFNR_DESCR'. <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'PRICE'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'PRICE_CURR'.  <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'PRICP'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'PRICP_UOM'.   <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.

  endloop.

endmethod.
  method GET_GRID_DATA.

    data: LS_MC_PACKMAT_VIS  type ZVCXI_XCSP_S_MC_PACKMAT_VIS,
          LS_MC_PACKMAT_GRID type ZVCXI_XCSP_S_MC_PACKMAT_GRID.
    data: LT_GATE            type /VCXI/VKSR_TT_GATE.
    data: LR_GATE          type ref to   /VCXI/CL_VKSR_GATE,
          LR_PR_MC_PACKMAT type ref to   ZCL_VCXI_XCSP_PR_MC_PACKMAT.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type          ANY.


    clear: ME->F_PURCHASED.

*** Dereferencing of Data Reference
    assign IR_DATA->* to <T_DATA>.

*** Get Gates of Material Consumption
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_XCSP_PR_MC=>C_IPRVT_XCS_MC
                                     IF_INUSE  = ABAP_TRUE
                                     IF_SORTED = ABAP_TRUE ).

    loop at LT_GATE into LR_GATE.

      clear: LS_MC_PACKMAT_GRID, LR_PR_MC_PACKMAT.

      check LR_GATE->IS_INUSE( ) eq ABAP_TRUE
        and LR_GATE->R_CLSPR     is bound.

***   Work with Packaging Material
      try.
          move LR_GATE->R_CLSPR ?to LR_PR_MC_PACKMAT.
        catch CX_SY_MOVE_CAST_ERROR.
          clear: LR_PR_MC_PACKMAT.
      endtry.

      check LR_PR_MC_PACKMAT is bound.

***--------------------------------------------------------------------------------------------
***   Get Data from PR Class
      move LR_GATE->GET_POSID( ) to LS_MC_PACKMAT_GRID-POSID.
      move LR_GATE->F_GATKY      to LS_MC_PACKMAT_GRID-GATKY.
      move LR_GATE               to LS_MC_PACKMAT_GRID-R_GATE.

      LS_MC_PACKMAT_VIS = ME->GET_MC_PACKMAT_VIS( IR_PR_MC_PACKMAT = LR_PR_MC_PACKMAT ).
      move-corresponding LS_MC_PACKMAT_VIS to LS_MC_PACKMAT_GRID.

***   Get Flags for Fieldcat
      case LS_MC_PACKMAT_VIS-PURCH.
        when ZCL_VCXI_XCSS_PR_MS=>C_PURCH_DIRECT_PRICE.
          ME->F_PURCHASED =  LS_MC_PACKMAT_VIS-PURCH.
        when ZCL_VCXI_XCSS_PR_MS=>C_PURCH_DIRECT.
          if ME->F_PURCHASED ne ZCL_VCXI_XCSS_PR_MS=>C_PURCH_DIRECT_PRICE.
            ME->F_PURCHASED =  LS_MC_PACKMAT_VIS-PURCH.
          endif.
      endcase.

***--------------------------------------------------------------------------------------------
***   Set Style Details
      ME->CHANGE_STYLE_MC_PACKMAT( exporting IF_PURCH = LS_MC_PACKMAT_GRID-PURCH
                                             IF_DUMMY = LR_PR_MC_PACKMAT->IS_DUMMY( )
                                   changing  CT_STYL  = LS_MC_PACKMAT_GRID-T_STYL ).

***   Call DVC Service - Modify Style
      ZCL_VCXI_XCDR_SERVICE=>MODIFY_STYLE( exporting IR_GATE = LR_GATE
                                           changing  CT_STYL = LS_MC_PACKMAT_GRID-T_STYL ).

***--------------------------------------------------------------------------------------------
***   Add Entry
      append initial line to <T_DATA> assigning <S_DATA>.
      move-corresponding LS_MC_PACKMAT_GRID to <S_DATA>.

    endloop.

  endmethod.
  method GET_MC_PACKMAT_VIS.

*** Get MaterialNumber and Description
    move IR_PR_MC_PACKMAT->GET_MATNR( )       to RS_MC_PACKMAT_VIS-MATNR.
    move IR_PR_MC_PACKMAT->GET_MATNR_DESCR( ) to RS_MC_PACKMAT_VIS-MATNR_DESCR.

*** Get Consumption
    RS_MC_PACKMAT_VIS-S_CONSU = IR_PR_MC_PACKMAT->GET_CONSU( ).

*** Get Average Consumption and Consumption per
    RS_MC_PACKMAT_VIS-S_AVGCO = IR_PR_MC_PACKMAT->GET_AVGCO( ).
    RS_MC_PACKMAT_VIS-S_AVGCP = IR_PR_MC_PACKMAT->GET_AVGCP( ).

*** Get Vendor, Description and Purchase Flag and Text
    move IR_PR_MC_PACKMAT->GET_LIFNR( )       to RS_MC_PACKMAT_VIS-LIFNR.
    move IR_PR_MC_PACKMAT->GET_LIFNR_DESCR( ) to RS_MC_PACKMAT_VIS-LIFNR_DESCR.
    move IR_PR_MC_PACKMAT->GET_PURCH( )       to RS_MC_PACKMAT_VIS-PURCH.
    move IR_PR_MC_PACKMAT->GET_TEXT( )        to RS_MC_PACKMAT_VIS-TEXT.

*** Get Price and Price Unit
    RS_MC_PACKMAT_VIS-S_PRICE = IR_PR_MC_PACKMAT->GET_PRICE( ).
    RS_MC_PACKMAT_VIS-S_PRICP = IR_PR_MC_PACKMAT->GET_PRICP( ).

  endmethod.
  method HANDLE_DATA_CHANGED_FINISHED.

    data: LS_MC_PACKMAT_GRID type ZVCXI_XCSP_S_MC_PACKMAT_GRID.
    data: LR_PR_MC_PACKMAT type ref to ZCL_VCXI_XCSP_PR_MC_PACKMAT.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type          ANY.


    check E_MODIFIED eq ABAP_TRUE.

    try.

        SUPER->HANDLE_DATA_CHANGED_FINISHED( E_MODIFIED    = E_MODIFIED
                                             ET_GOOD_CELLS = ET_GOOD_CELLS ).

***     Dereferencing of Data Reference
        assign ME->R_DATA->* to <T_DATA>.

        loop at <T_DATA> assigning <S_DATA>.
          move-corresponding <S_DATA> to LS_MC_PACKMAT_GRID.
          check LS_MC_PACKMAT_GRID-R_GATE is bound.

          clear: LR_PR_MC_PACKMAT.

***       Work with Packaging Material
          try.
              move LS_MC_PACKMAT_GRID-R_GATE->R_CLSPR ?to LR_PR_MC_PACKMAT.
            catch CX_SY_MOVE_CAST_ERROR.
              clear: LR_PR_MC_PACKMAT.
          endtry.

          check LR_PR_MC_PACKMAT is bound.
***       Set Values to PR Class
          ME->SET_MC_PACKMAT_VIS( IR_PR_MC_PACKMAT  = LR_PR_MC_PACKMAT
                                  IS_MC_PACKMAT_VIS = LS_MC_PACKMAT_GRID-S_MC_PACKMAT_VIS ).

        endloop.

        ME->R_GATE->RAISE_EVENTS( ).

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

*** Request Refresh for ALV
    move ABAP_TRUE to ME->F_REFRESH_REQ.

  endmethod.
  method SET_MC_PACKMAT_VIS.

    data:  LS_MC_PACKMAT_VIS type ZVCXI_XCSP_S_MC_PACKMAT_VIS.


*** Get Current data to do Update only if Value changed.
    LS_MC_PACKMAT_VIS = ME->GET_MC_PACKMAT_VIS( IR_PR_MC_PACKMAT = IR_PR_MC_PACKMAT ).

*** Material
    if LS_MC_PACKMAT_VIS-MATNR ne IS_MC_PACKMAT_VIS-MATNR.
      IR_PR_MC_PACKMAT->SET_MATNR( IF_MATNR = IS_MC_PACKMAT_VIS-MATNR
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Average Consumption  & Consumption per
    if LS_MC_PACKMAT_VIS-AVGCO ne IS_MC_PACKMAT_VIS-AVGCO.
      IR_PR_MC_PACKMAT->SET_AVGCO( IS_AVGCO = IS_MC_PACKMAT_VIS-S_AVGCO
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

    if LS_MC_PACKMAT_VIS-AVGCP ne IS_MC_PACKMAT_VIS-AVGCP.
      IR_PR_MC_PACKMAT->SET_AVGCP( IS_AVGCP = IS_MC_PACKMAT_VIS-S_AVGCP
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Text
    if LS_MC_PACKMAT_VIS-TEXT ne IS_MC_PACKMAT_VIS-TEXT.
      IR_PR_MC_PACKMAT->SET_TEXT( IF_TEXT = IS_MC_PACKMAT_VIS-TEXT
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Purchase Flag
    if LS_MC_PACKMAT_VIS-PURCH ne IS_MC_PACKMAT_VIS-PURCH.
      IR_PR_MC_PACKMAT->SET_PURCH( IF_PURCH = IS_MC_PACKMAT_VIS-PURCH
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Vendor
    if LS_MC_PACKMAT_VIS-LIFNR ne IS_MC_PACKMAT_VIS-LIFNR.
      IR_PR_MC_PACKMAT->SET_LIFNR( IF_LIFNR = IS_MC_PACKMAT_VIS-LIFNR
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Price
    if LS_MC_PACKMAT_VIS-PRICE ne IS_MC_PACKMAT_VIS-PRICE.
      IR_PR_MC_PACKMAT->SET_PRICE( IS_PRICE = IS_MC_PACKMAT_VIS-S_PRICE
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

    if LS_MC_PACKMAT_VIS-PRICP ne IS_MC_PACKMAT_VIS-PRICP.
      IR_PR_MC_PACKMAT->SET_PRICP( IS_PRICP = IS_MC_PACKMAT_VIS-S_PRICP
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
