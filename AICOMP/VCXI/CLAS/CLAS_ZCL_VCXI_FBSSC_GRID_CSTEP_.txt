
class ZCL_VCXI_FBSSC_GRID_CSTEP definition
  public
  inheriting from ZCL_VCXI_FBSSC_GRID
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IF_TABNAME type TABNAME
      !IF_CONTAINER_NAME type SCRFNAME
      !IR_CUSTOMIZING type ref to OBJECT
    raising
      /VCXI/CX_CKX .
protected section.

  data R_BAGTY type ref to ZCL_VCXI_FBSSC_BAGTY .

  methods CHECK_CSTEP_ADD
    importing
      !IF_ICSTP type ZVCXI_FBS_ICSTP
    returning
      value(RF_DISABLE) type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
  methods ADD_CSTEP
    importing
      !IF_ICSTP type ZVCXI_FBS_ICSTP
    raising
      /VCXI/CX_CKX .
  methods CHECK_SEL_DELETE
    returning
      value(RF_OKAY) type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
  methods CHANGE_STYLE_CSTEP
    changing
      !CT_STYL type LVC_T_STYL
    raising
      /VCXI/CX_CKX .
  methods DELETE_CSTEP
    importing
      !IT_BAGTY_CSTEP type ZVCXI_FBSSC_TT_BAGTY_CSTEP
    raising
      /VCXI/CX_CKX .
  methods HANDLE_TOOLBAR
    for event TOOLBAR of CL_GUI_ALV_GRID
    importing
      !E_OBJECT
      !E_INTERACTIVE .
  methods HANDLE_USER_COMMAND
    for event USER_COMMAND of CL_GUI_ALV_GRID
    importing
      !E_UCOMM .
  methods HANDLE_MENU_BUTTON
    for event MENU_BUTTON of CL_GUI_ALV_GRID
    importing
      !E_OBJECT
      !E_UCOMM
      !SENDER .

  methods GET_FIELDCAT
    redefinition .
  methods GET_GRID_DATA
    redefinition .
  methods GET_LAYOUT
    redefinition .
  methods HANDLE_DATA_CHANGED_FINISHED
    redefinition .
  methods SET_EVENT
    redefinition .
private section.
endclass. "ZCL_VCXI_FBSSC_GRID_CSTEP definition
class ZCL_VCXI_FBSSC_GRID_CSTEP implementation.
  method ADD_CSTEP.

    data: LS_BAGTY_CSTEP type ZVCXI_FBSSC_S_BAGTY_CSTEP.

    move ME->R_BAGTY->F_IBAGT to LS_BAGTY_CSTEP-IBAGT.
    move IF_ICSTP to LS_BAGTY_CSTEP-ICSTP.
    move ME->R_BAGTY->C_COUNT_MIN1 to : LS_BAGTY_CSTEP-QTYMI, LS_BAGTY_CSTEP-QTYMA.

*** Add Conv. Step
    ME->R_BAGTY->ADD_CSTEP( IS_BAGTY_CSTEP = LS_BAGTY_CSTEP ).

  endmethod.
  method CHANGE_STYLE_CSTEP.

*** Set Conversion Step = Not Editable
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'ICSTP'
                                IF_STYLE     = ME->F_STYLE_DISABLED
                      changing  CT_STYL      = CT_STYL ).

*** Set Min. = Editable
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'QTYMI'
                                IF_STYLE     = ME->F_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).

*** Set Max. = Editable
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'QTYMA'
                                IF_STYLE     = ME->F_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).

  endmethod.
  method CHECK_CSTEP_ADD.

    move ABAP_FALSE to RF_DISABLE.
    read table ME->R_BAGTY->T_BAGTY_CSTEP with key ICSTP = IF_ICSTP
                                          transporting no fields.
    if SY-SUBRC is initial.
      move ABAP_TRUE to RF_DISABLE.
    endif.

  endmethod.
  method CHECK_SEL_DELETE.

    data: LT_CELL type LVC_T_CELL,
          LT_ROW  type LVC_T_ROW.

    ME->R_GRID->GET_SELECTED_CELLS( importing ET_CELL = LT_CELL ).
    ME->R_GRID->GET_SELECTED_ROWS( importing ET_INDEX_ROWS = LT_ROW ).

*** Delete is only available if Cells are selected
    if LINES( LT_CELL ) gt 0 or
       LINES( LT_ROW )  gt 0.
      move ABAP_TRUE to RF_OKAY.
    else.
      move ABAP_FALSE to RF_OKAY.
    endif.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IF_TABNAME        = IF_TABNAME
                        IF_CONTAINER_NAME = IF_CONTAINER_NAME ).

*** Try to Assign Customizing Reference
    try.
        move IR_CUSTOMIZING ?to ME->R_BAGTY.
      catch CX_SY_MOVE_CAST_ERROR.
***     Bag Type Customizing Reference can't be assigned.
        /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE(
            IF_MSGTY = 'E'
            IF_MSGID = 'ZVCXI_FBSSC'
            IF_MSGNO = '101' ).
    endtry.

    check ME->R_BAGTY is bound.
    ME->F_LOCKED = ME->R_BAGTY->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ).

  endmethod.
  method DELETE_CSTEP.

    data:  LS_BAGTY_CSTEP  type ZVCXI_FBSSC_S_BAGTY_CSTEP.

*** Process Result
    loop at IT_BAGTY_CSTEP into LS_BAGTY_CSTEP.
***   Delete Conv. Step
      ME->R_BAGTY->DELETE_CSTEP( IS_BAGTY_CSTEP = LS_BAGTY_CSTEP ).
    endloop.

  endmethod.
  method GET_FIELDCAT.

    data: LF_LOCKED   type /VCXI/CKX_LOCKED.
    field-symbols: <S_FCAT> type LVC_S_FCAT.

***--------------------------------------------------------------------------------------
*** Get Standard Field Catalog
    RT_FCAT = SUPER->GET_FIELDCAT( ).

*** Get Locked Status
    LF_LOCKED = ME->R_BAGTY->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ).

***--------------------------------------------------------------------------------------
*** Adjust Field Catalog
    loop at RT_FCAT assigning <S_FCAT>.
      case <S_FCAT>-FIELDNAME.
        when 'T_STYL' or 'IBAGT' or 'ICSTP'.
          move ABAP_TRUE    to <S_FCAT>-TECH.

        when 'ICSTP_DESCR'.
          move text-D02 to : <S_FCAT>-SCRTEXT_L, <S_FCAT>-SCRTEXT_M,
                             <S_FCAT>-SCRTEXT_S, <S_FCAT>-REPTEXT.
          move 'A'      to   <S_FCAT>-COL_OPT.
          move 1        to   <S_FCAT>-COL_POS.

        when 'QTYMI'.
          move text-D03 to : <S_FCAT>-SCRTEXT_L, <S_FCAT>-SCRTEXT_M,
                             <S_FCAT>-SCRTEXT_S, <S_FCAT>-REPTEXT.
          move 'A'      to   <S_FCAT>-COL_OPT.

          if LF_LOCKED eq ABAP_TRUE.
            move ABAP_TRUE    to <S_FCAT>-EDIT.
          endif.

        when 'QTYMA'.
          move text-D04 to : <S_FCAT>-SCRTEXT_L, <S_FCAT>-SCRTEXT_M,
                             <S_FCAT>-SCRTEXT_S, <S_FCAT>-REPTEXT.
          move 'A'      to   <S_FCAT>-COL_OPT.

          if LF_LOCKED eq ABAP_TRUE.
            move ABAP_TRUE    to <S_FCAT>-EDIT.
          endif.

      endcase.
    endloop.

  endmethod.
  method GET_GRID_DATA.

    data: LS_CSTEP      type ZVCXI_FBSSC_S_BAGTY_CSTEP,
          LS_TC030T     type ZVCXI_FBS_TC030T,
          LS_CSTEP_GRID type ZVCXI_FBSSC_S_BAGTY_CSTEP_GRID.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type          ANY.

*** Dereferencing of Data Reference
    assign IR_DATA->* to <T_DATA>.

***--------------------------------------------------------------------------------------
*** Get Bag Type - Conversion Steps
    loop at ME->R_BAGTY->T_BAGTY_CSTEP into LS_CSTEP.
      clear: LS_CSTEP_GRID.

      move: LS_CSTEP-IBAGT to LS_CSTEP_GRID-IBAGT,
            LS_CSTEP-ICSTP to LS_CSTEP_GRID-ICSTP,
            LS_CSTEP-QTYMI to LS_CSTEP_GRID-QTYMI,
            LS_CSTEP-QTYMA to LS_CSTEP_GRID-QTYMA.

      read table ME->R_BAGTY->T_TC030T into LS_TC030T with key ICSTP = LS_CSTEP-ICSTP.
      if SY-SUBRC is initial.
        move LS_TC030T-DESCR to LS_CSTEP_GRID-ICSTP_DESCR.
      else.
        move LS_CSTEP-ICSTP to LS_CSTEP_GRID-ICSTP_DESCR.
      endif.

***--------------------------------------------------------------------------------------
***   Set Style Details
      ME->CHANGE_STYLE_CSTEP( changing CT_STYL = LS_CSTEP_GRID-T_STYL ).

***--------------------------------------------------------------------------------------
***   Add Entry
      append initial line to <T_DATA> assigning <S_DATA>.
      move-corresponding LS_CSTEP_GRID to <S_DATA>.

    endloop.

  endmethod.
  method GET_LAYOUT.

    RS_LAYOUT = SUPER->GET_LAYOUT( ).

*** Set Toolbar to Activ
    move ABAP_FALSE to RS_LAYOUT-NO_TOOLBAR.
    move 'A'        to RS_LAYOUT-SEL_MODE.

  endmethod.
  method HANDLE_DATA_CHANGED_FINISHED.

    data: LS_BAGTY_CSTEP      type          ZVCXI_FBSSC_S_BAGTY_CSTEP,
          LS_BAGTY_CSTEP_GRID type          ZVCXI_FBSSC_S_BAGTY_CSTEP_GRID.
    data: LR_CX_CKX           type ref to   /VCXI/CX_CKX.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type          ANY.

    check E_MODIFIED eq ABAP_TRUE.

    try.

        SUPER->HANDLE_DATA_CHANGED_FINISHED( exporting E_MODIFIED    = E_MODIFIED
                                                       ET_GOOD_CELLS = ET_GOOD_CELLS ).

***     Dereferencing of Data Reference
        assign ME->R_DATA->* to <T_DATA>.

        loop at <T_DATA> assigning <S_DATA>.

          read table ET_GOOD_CELLS with key ROW_ID = SY-TABIX transporting no fields.
          check SY-SUBRC eq 0.

          move-corresponding <S_DATA> to LS_BAGTY_CSTEP_GRID.
          move-corresponding LS_BAGTY_CSTEP_GRID to LS_BAGTY_CSTEP.
          move ME->R_BAGTY->F_IBAGT to LS_BAGTY_CSTEP-IBAGT.

***       Update
          ME->R_BAGTY->UPDATE_CSTEP( IS_BAGTY_CSTEP = LS_BAGTY_CSTEP ).

        endloop.

      catch /VCXI/CX_CKX into LR_CX_CKX.
        LR_CX_CKX->SHOW_MESSAGE( ).
    endtry.

*** Request Refresh for ALV
    move ABAP_TRUE to ME->F_REFRESH_REQ.

  endmethod.
  method HANDLE_MENU_BUTTON.

    data: LF_DISABLED type        ABAP_BOOL,
          LF_FCODE    type        UI_FUNC,
          LF_TEXT     type        GUI_TEXT.
    data: LS_TC030T       type ZVCXI_FBS_TC030T.
    data: LR_CX_CKX type ref to /VCXI/CX_CKX.

    check E_OBJECT is bound.
    check E_UCOMM eq C_FCODE_ADD.

    try.

        if ME->R_BAGTY->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ) eq ABAP_FALSE.
          move ABAP_TRUE to LF_DISABLED.
        endif.

        loop at ME->R_BAGTY->T_TC030T into LS_TC030T.
          clear: LF_FCODE, LF_TEXT.

***       Get Function Code
          concatenate C_FCODE_ADD LS_TC030T-ICSTP into LF_FCODE separated by '@'.

***       Get Description
          move LS_TC030T-DESCR to LF_TEXT.

          search LF_TEXT for '&'.
          if SY-FDPOS is not initial.
            concatenate LF_TEXT+0(SY-FDPOS) ' &' LF_TEXT+SY-FDPOS into LF_TEXT.
          endif.

***       Check if Entry can be added
          LF_DISABLED = ME->CHECK_CSTEP_ADD( IF_ICSTP = LS_TC030T-ICSTP ).

***       Add Function
          E_OBJECT->ADD_FUNCTION( FCODE    = LF_FCODE
                                  TEXT     = LF_TEXT
                                  DISABLED = LF_DISABLED ).
        endloop.

      catch /VCXI/CX_CKX into LR_CX_CKX.
        LR_CX_CKX->SHOW_MESSAGE( ).
    endtry.

  endmethod.
  method HANDLE_TOOLBAR.

    data: LF_DISABLED type        ABAP_BOOL.
    data: LR_CX_CKX type ref to /VCXI/CX_CKX.
    field-symbols: <S_TOOLBAR> type STB_BUTTON.

    try.

        if ME->R_BAGTY->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ) eq ABAP_FALSE.
          move ABAP_TRUE to LF_DISABLED.
        endif.

***--------------------------------------------------------------------------------------
***     Insert Toolbar elements Add, Delete
        insert initial line into table E_OBJECT->MT_TOOLBAR assigning <S_TOOLBAR>.
        move: C_FCODE_ADD               to <S_TOOLBAR>-FUNCTION,
              text-T02                  to <S_TOOLBAR>-TEXT,
              C_BTYPE_MENU              to <S_TOOLBAR>-BUTN_TYPE,
              ICON_INSERT_ROW           to <S_TOOLBAR>-ICON,
              LF_DISABLED               to <S_TOOLBAR>-DISABLED.

        insert initial line into table E_OBJECT->MT_TOOLBAR assigning <S_TOOLBAR>.
        move: C_FCODE_DELETE            to <S_TOOLBAR>-FUNCTION,
              text-T03                  to <S_TOOLBAR>-TEXT,
              C_BTYPE_BUTTON            to <S_TOOLBAR>-BUTN_TYPE,
              ICON_DELETE_ROW           to <S_TOOLBAR>-ICON,
              LF_DISABLED               to <S_TOOLBAR>-DISABLED.

      catch /VCXI/CX_CKX into LR_CX_CKX.
        LR_CX_CKX->SHOW_MESSAGE( ).
    endtry.

  endmethod.
  method HANDLE_USER_COMMAND.

    data: LF_UCOMM       type SYUCOMM,
          LF_ICSTP       type ZVCXI_FBS_ICSTP.
    data: LS_CELL        type LVC_S_CELL,
          LS_ROW         type LVC_S_ROW.
    data: LT_CELL        type LVC_T_CELL,
          LT_ROW         type LVC_T_ROW,
          LT_BAGTY_CSTEP type          ZVCXI_FBSSC_TT_BAGTY_CSTEP.
    data:          LR_CX_CKX       type ref to   /VCXI/CX_CKX.
    field-symbols: <T_DATA>        type standard table,
                   <S_BAGTY_CSTEP> type          ZVCXI_FBSSC_S_BAGTY_CSTEP,
                   <S_DATA>        type          ANY.

    try.
***     Get Handles
        assign ME->R_DATA->* to <T_DATA>.
        free: <T_DATA>.
        ME->GET_GRID_DATA( IR_DATA = ME->R_DATA ).

        ME->R_GRID->GET_SELECTED_CELLS( importing ET_CELL = LT_CELL ).
        ME->R_GRID->GET_SELECTED_ROWS( importing ET_INDEX_ROWS = LT_ROW ).

***     Pre-check User command
        split E_UCOMM at '@' into LF_UCOMM LF_ICSTP.

        case LF_UCOMM.
          when C_FCODE_ADD.
***         Add new Conv. Step
            ME->ADD_CSTEP( IF_ICSTP = LF_ICSTP ).

          when C_FCODE_DELETE.
***         Delete Conv. Step
            if ME->CHECK_SEL_DELETE( ) eq ABAP_FALSE.
***           No rows selected. Please select all rows you want to delete.
              raise exception type /VCXI/CX_CKX
                exporting
                  F_MSGTY = 'E'
                  F_MSGID = 'ZVCXI_FBSSC'
                  F_MSGNO = '056'.
            endif.

***         Get Selected Conv. Step
            loop at LT_CELL into LS_CELL.
              read table <T_DATA> assigning <S_DATA> index LS_CELL-ROW_ID-INDEX.
              check SY-SUBRC = 0.

              insert initial line into table LT_BAGTY_CSTEP assigning <S_BAGTY_CSTEP>.
              move-corresponding <S_DATA> to <S_BAGTY_CSTEP>.
            endloop.

            loop at LT_ROW into LS_ROW.
              read table <T_DATA> assigning <S_DATA> index LS_ROW-INDEX.
              check SY-SUBRC = 0.

              insert initial line into table LT_BAGTY_CSTEP assigning <S_BAGTY_CSTEP>.
              move-corresponding <S_DATA> to <S_BAGTY_CSTEP>.
            endloop.

***         Delete Selected Conv. Step
            ME->DELETE_CSTEP( IT_BAGTY_CSTEP = LT_BAGTY_CSTEP ).

        endcase.

      catch /VCXI/CX_CKX into LR_CX_CKX.
        LR_CX_CKX->SHOW_MESSAGE( ).
    endtry.

    ME->F_REFRESH_REQ = ABAP_TRUE.

  endmethod.
  method SET_EVENT.

    SUPER->SET_EVENT( ).

*** Add Event for UserCommand and Toolbar
    set handler ME->HANDLE_USER_COMMAND for ME->R_GRID.
    set handler ME->HANDLE_TOOLBAR      for ME->R_GRID.
    set handler ME->HANDLE_MENU_BUTTON for ME->R_GRID.

  endmethod.
