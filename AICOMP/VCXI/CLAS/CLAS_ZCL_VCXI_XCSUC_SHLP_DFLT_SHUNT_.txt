
class ZCL_VCXI_XCSUC_SHLP_DFLT_SHUNT definition
  public
  create public .

public section.

  type-pools ABAP .
  class-methods CALL_SHLP
    importing
      !IF_DISPLAY type ABAP_BOOL default ABAP_FALSE
      !IT_DFLT_SHUNT type ZVCXI_XCSUC_TT_DFLT_SHUNT_SHLP
      !IT_WERKS type T_WERKS optional
    returning
      value(RT_DFLT_SHUNT) type ZVCXI_XCSUC_TT_DFLT_SHUNT_SHLP
    raising
      /VCXI/CX_CKX .
protected section.
private section.
endclass. "ZCL_VCXI_XCSUC_SHLP_DFLT_SHUNT definition
class ZCL_VCXI_XCSUC_SHLP_DFLT_SHUNT implementation.
method CALL_SHLP.

  data:          LF_WERKS            type                   WERKS_D,
                 LF_SHUNT            type                   ZVCXI_XCSU_SHUNT.
  data:          LS_SHUNT_COMBI      type                   ZVCXI_XCSU_S_SHUNT_COMBI,
                 LS_RETURN           type                   DDSHRETVAL,
                 LS_MAP              type                   DSELC.
  data:          LT_DFLT_SHUNT_SHLP  type                   ZVCXI_XCSUC_TT_DFLT_SHUNT_SHLP,
                 LT_SHUNT_COMBI      type                   ZVCXI_XCSU_TT_SHUNT_COMBI,
                 LT_SHUNT            type table of          ZVCXI_XCSU_SHUNT,
                 LT_PACKP            type                   ZVCXI_XCSU_TT_PACKP,
                 LT_PACKP_ALL        type                   ZVCXI_XCSU_TT_PACKP,
                 LT_RETURN           type table of          DDSHRETVAL,
                 LT_MAP              type standard table of DSELC.
  data:          LR_CUST_SHIPU       type ref to            ZCL_VCXI_XCSU_CUST_SHIPU.
  field-symbols: <F_FIELD>           type                   any,
                 <S_DFLT_SHUNT_SHLP> type                   ZVCXI_XCSUC_S_DFLT_SHUNT_SHLP.

  LR_CUST_SHIPU = ZCL_VCXI_XCSU_CUST_SHIPU=>GET_INSTANCE( ).

  check LR_CUST_SHIPU is bound.
*** Get all Shipping Unit and Packaging Patter Combinations
  LT_SHUNT_COMBI = LR_CUST_SHIPU->GET_SHUNT_COMBI( ).

  loop at LT_SHUNT_COMBI into LS_SHUNT_COMBI.
    read table IT_DFLT_SHUNT with key SHUNT = LS_SHUNT_COMBI-SHUNT
                             transporting no fields.
    check SY-SUBRC ne 0.
    append initial line to LT_DFLT_SHUNT_SHLP assigning <S_DFLT_SHUNT_SHLP>.
    move-corresponding LS_SHUNT_COMBI to <S_DFLT_SHUNT_SHLP>.

*** Collect all Shipping Units
    collect LS_SHUNT_COMBI-SHUNT into LT_SHUNT.
  endloop.

*** Get all Packaging Pattern valid for Plants and Shippint Units
  if IT_WERKS is not initial.
    loop at LT_SHUNT into LF_SHUNT.
      loop at IT_WERKS into LF_WERKS.
        LT_PACKP = LR_CUST_SHIPU->GET_PACKP_FOR_SHUNT( IF_SHUNT = LF_SHUNT
                                                       IF_WERKS = LF_WERKS ).

        append lines of LT_PACKP to LT_PACKP_ALL.
      endloop.
    endloop.
  endif.

*** Reduce restricted Packing Pattern
  loop at LT_DFLT_SHUNT_SHLP assigning <S_DFLT_SHUNT_SHLP>.
    read table LT_PACKP_ALL with key PACKP = <S_DFLT_SHUNT_SHLP>-PACKP
                            transporting no fields.
    check SY-SUBRC ne 0.
    delete LT_DFLT_SHUNT_SHLP.
  endloop.

*** Start Searchhelp
  sort LT_DFLT_SHUNT_SHLP by SHUNT PACKP.

  LS_MAP-FLDNAME   = 'F0001'. "Field for Position 1
  LS_MAP-DYFLDNAME = 'SHUNT'.
  append LS_MAP to LT_MAP.

  LS_MAP-FLDNAME   = 'F0002'. "Field for Position 2
  LS_MAP-DYFLDNAME = 'PACKP'.
  append LS_MAP to LT_MAP.

  call function 'F4IF_INT_TABLE_VALUE_REQUEST'
    exporting
      RETFIELD        = 'SHUNT'
      VALUE_ORG       = 'S'
      MULTIPLE_CHOICE = ABAP_FALSE
      DISPLAY         = IF_DISPLAY
    tables
      VALUE_TAB       = LT_DFLT_SHUNT_SHLP
      DYNPFLD_MAPPING = LT_MAP
      RETURN_TAB      = LT_RETURN
    exceptions
      PARAMETER_ERROR = 1
      NO_VALUES_FOUND = 2
      others          = 3.

  if SY-SUBRC eq 0.
    sort LT_RETURN by RECORDPOS.
    loop at LT_RETURN into LS_RETURN.
      read table RT_DFLT_SHUNT assigning <S_DFLT_SHUNT_SHLP> index LS_RETURN-RECORDPOS.
      if SY-SUBRC ne 0.
        append initial line to RT_DFLT_SHUNT assigning <S_DFLT_SHUNT_SHLP>.
      endif.
      assign component LS_RETURN-RETFIELD of structure <S_DFLT_SHUNT_SHLP> to <F_FIELD>.
      check SY-SUBRC = 0.
      move LS_RETURN-FIELDVAL to <F_FIELD>.
      unassign <F_FIELD>.
    endloop.
  endif.

endmethod.
endclass. "ZCL_VCXI_XCSUC_SHLP_DFLT_SHUNT implementation
