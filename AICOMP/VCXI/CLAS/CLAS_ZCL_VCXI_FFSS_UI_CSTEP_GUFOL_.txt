
class ZCL_VCXI_FFSS_UI_CSTEP_GUFOL definition
  public
  inheriting from ZCL_VCXI_FFSS_UI_CSTEP
  create public .

public section.

  data S_CSTEP_GUFOL_VIS type ZVCXI_FFSS_S_CSTEP_GUFOL_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
    raising
      /VCXI/CX_CKX .
  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_CSTEP_GUFOL_VIS
    importing
      !IS_CSTEP_GUFOL_VIS type ZVCXI_FFSS_S_CSTEP_GUFOL_VIS
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
protected section.

  data R_PR_CSTEP_GUFOL type ref to ZCL_VCXI_FFSS_PR_CSTEP_GUFOL .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_FFSS_UI_CSTEP_GUFOL definition
class ZCL_VCXI_FFSS_UI_CSTEP_GUFOL implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_FFSS_UI_CSTEP_GUFOL_BIND'
      exporting
        IR_UI_CSTEP_GUFOL  = ME
      importing
        ES_DYNP            = ME->S_DYNP.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_CSTEP_GUFOL.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Initialize VIS
    ME->INIT_VIS( ).

***--------------------------------------------------------------------------------------
*** Get TextEdit Conversion Text
    if ME->R_TEXTEDIT_CSTEP is bound.
      move ABAP_TRUE to ME->R_TEXTEDIT_CSTEP->F_REFRESH_REQ.
      ME->R_TEXTEDIT_CSTEP->REFRESH_TEXTEDIT( ).
    endif.

  endmethod.
  method DO_UPDATE_CSTEP_GUFOL_VIS.

    check ME->R_PR_CSTEP_GUFOL is bound.

***--------------------------------------------------------------------------------------
*** Set Left Gusset Width
    if ME->S_CSTEP_GUFOL_VIS-S_GULEF ne IS_CSTEP_GUFOL_VIS-S_GULEF.
      ME->R_PR_CSTEP_GUFOL->SET_GULEF( IS_GULEF = IS_CSTEP_GUFOL_VIS-S_GULEF
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Right Gusset Width
    if ME->S_CSTEP_GUFOL_VIS-S_GURIG ne IS_CSTEP_GUFOL_VIS-S_GURIG.
      ME->R_PR_CSTEP_GUFOL->SET_GURIG( IS_GURIG = IS_CSTEP_GUFOL_VIS-S_GURIG
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Update TextEdit Long Text
    if ME->R_TEXTEDIT_CSTEP is bound.
      ME->R_TEXTEDIT_CSTEP->UPDATE_TEXT( ).
    endif.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_CSTEP_GUFOL.

  endmethod.
  method INIT_VIS.

    SUPER->INIT_VIS( ).

    move ME->S_CSTEP_VIS to S_CSTEP_GUFOL_VIS-S_CSTEP_VIS.

***--------------------------------------------------------------------------------------
*** Left Gusset Width
    ME->R_PR_CSTEP_GUFOL->GET_GULEF( importing ES_GULEF = ME->S_CSTEP_GUFOL_VIS-S_GULEF ).

*** Right Gusset Width
    ME->R_PR_CSTEP_GUFOL->GET_GURIG( importing ES_GURIG = ME->S_CSTEP_GUFOL_VIS-S_GURIG ).

  endmethod.
