
class ZCL_VCXI_P2SS_UI_FCSHT definition
  public
  inheriting from ZCL_VCXI_PXSS_UI_XSHT
  create public .

public section.

  data S_FCSHT_VIS type ZVCXI_P2SS_S_FCSHT_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABPT type ZVCXI_XCS_TABPT default '0100S_SHT_TAB-TAB'
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  constants C_UC_FCPROP type SYUCOMM value 'FCSHT_FCPROP' ##NO_TEXT.
  data R_PR_FCSHT type ref to ZCL_VCXI_P2SS_PR_FCSHT .
  data R_TOOLBAR type ref to ZCL_VCXI_XCS_TOOLBAR_PRVDR .

  methods HANDLE_UC_FCPROP
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_P2SS_UI_FCSHT definition
class ZCL_VCXI_P2SS_UI_FCSHT implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

***--------------------------------------------------------------------------------------
*** Bind UI to Dynpro
    call function 'ZVCXI_P2SS_UI_FCSHT_BIND'
      exporting
        IR_UI_FCSHT   = ME
      importing
        ES_DYNP       = ME->S_DYNP
        EF_ACTIVE_TAB = ME->F_ACTIVE_TAB.

***--------------------------------------------------------------------------------------
*** Create Toolbar to add Connection
    create object ME->R_TOOLBAR
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_TOOLBAR'.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE
                        IF_TABPT  = IF_TABPT ).

*** Assign th RP Class to the UI Class
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_FCSHT.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

    if ME->R_TOOLBAR is bound.
      ME->R_TOOLBAR->DO_FREE( ).
      clear ME->R_TOOLBAR.
    endif.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Super PBO
    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize Toolbar
    if ME->R_TOOLBAR is bound.
      ME->R_TOOLBAR->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_TOOLBAR->REFRESH_TOOLBAR( IT_PRVDR_SELECTED = value #( ( ME->R_OLD_GATE ) ) ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_FCSHT_VIS  type ZVCXI_P2SS_S_FCSHT_VIS.


*** Super
    SUPER->DO_UPDATE_VIS( IS_VIS = IS_VIS ).
    check ME->R_PR_FCSHT is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_FCSHT_VIS.

****--------------------------------------------------------------------------------------
**** Set Grain Orientation
*    if ME->S_FCSHT_VIS-GRAIO ne LS_FCSHT_VIS-GRAIO.
*      ME->R_PR_FCSHT->SET_GRAIO( IF_GRAIO = LS_FCSHT_VIS-GRAIO
*                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
*    endif.

  endmethod.
  method DO_USER_COMMAND.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    case IF_UCOMM.
      when C_UC_FCPROP.
        ME->HANDLE_UC_FCPROP( ).

    endcase.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_FCSHT.

  endmethod.
  method HANDLE_UC_FCPROP.

*** Start Popup
    ZCL_VCXI_P2SS_POPUP_FCPROP=>START( IR_BL_FCBPROP = ME->R_PR_FCSHT->GET_FCBPX( )
                                       IR_BL_FCFPROP = ME->R_PR_FCSHT->GET_FCFPX( )
                                       IF_WERKS      = ME->R_PR_FCSHT->GET_WERKS( )
                                       IS_SURWT      = ME->R_PR_FCSHT->GET_SURWT( ) ).

  endmethod.
  method INIT_VIS.

    data: LR_BL_FCFPROP type ref to ZCL_VCXI_P2SS_BL_FCFPROP.


    free: ME->S_FCSHT_VIS.

    SUPER->INIT_VIS( ).
    ME->S_FCSHT_VIS-S_XSHT_VIS = ME->S_XSHT_VIS.
    check ME->R_PR_FCSHT is bound.

***--------------------------------------------------------------------------------------
*** Get Grain Orientation
*    ME->S_FCSHT_VIS-GRAIO = ME->R_PR_FCSHT->GET_GRAIO( ).

***------------------------------------------------------------------------------------------------
*** Get Sheet Dimension (Outbound)
    ME->S_FCSHT_VIS-S_IOSEC-S_SHTLE = ME->R_PR_FCSHT->GET_SHTLE( ).
    ME->S_FCSHT_VIS-S_IOSEC-S_SHTWI = ME->R_PR_FCSHT->GET_SHTWI( ).
    ME->S_FCSHT_VIS-S_IOSEC-GRADI   = ZCL_VCXI_P2SS_BL_FCFPROP=>DET_GRADI( IS_SHTWI = ME->S_FCSHT_VIS-S_IOSEC-S_SHTWI
                                                                           IS_SHTLE = ME->S_FCSHT_VIS-S_IOSEC-S_SHTLE ).

*** Number Ups (Outbound)
    ME->S_FCSHT_VIS-S_IOSEC-NOUPS = ME->R_PR_FCSHT->GET_NOUPS( ).

*** Get Caliper, Paper Grade and Grammage (Outbound)
    LR_BL_FCFPROP = ME->R_PR_FCSHT->GET_FCFPX( ).
    if LR_BL_FCFPROP is bound.
      ME->S_FCSHT_VIS-S_IOSEC-S_CALIP = LR_BL_FCFPROP->S_CALIP.
      ME->S_FCSHT_VIS-S_IOSEC-PAPGR   = LR_BL_FCFPROP->F_PAPGR.
      ME->S_FCSHT_VIS-S_IOSEC-S_GRAMM = LR_BL_FCFPROP->S_GRAMM.
    endif.

  endmethod.
