
class ZCL_VCXI_P5SS_UI_INPUT definition
  public
  inheriting from ZCL_VCXI_XCS_UI_TAB
  create public .

public section.

  data S_INPUT_VIS type ZVCXI_P5SS_S_INPUT_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABPT type ZVCXI_XCS_TABPT default '0100S_IN_TAB-TAB'
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  constants C_UC_RLPROP type SYUCOMM value 'INPUT_RLPROP' ##NO_TEXT.
  data R_PR_INPUT type ref to ZCL_VCXI_P5SS_PR_INPUT .
  data R_TOOLBAR type ref to ZCL_VCXI_XCS_TOOLBAR_PRVDR .

  methods HANDLE_UC_RLPROP
    raising
      /VCXI/CX_CKX .
  methods INIT_VIS
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
private section.
endclass. "ZCL_VCXI_P5SS_UI_INPUT definition
class ZCL_VCXI_P5SS_UI_INPUT implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_P5SS_UI_INPUT_BIND'
      exporting
        IR_UI_INPUT   = ME
      importing
        ES_DYNP       = ME->S_DYNP
        EF_ACTIVE_TAB = ME->F_ACTIVE_TAB.


*** Create Toolbar for Provider
    create object ME->R_TOOLBAR
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_TOOLBAR'.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE
                        IF_TABPT  = IF_TABPT ).

    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_INPUT.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

*** Free Toolbar
    if ME->R_TOOLBAR is bound.
      ME->R_TOOLBAR->DO_FREE( ).
      clear ME->R_TOOLBAR.
    endif.

  endmethod.
  method DO_MODIFY_SCREEN.

    try.
        SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                 IF_DYNNR = IF_DYNNR ).

***     Modify Scree using DynamicVC
        ZCL_VCXI_XCDR_SERVICE=>MODIFY_SCREEN( IR_GATE = ME->R_GATE ).

        loop at screen.
***       Reopen Info Buttons
          if SCREEN-GROUP1 eq 'INF'.
            move 1 to SCREEN-INPUT.
          endif.

          modify screen.
        endloop.

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_PBO.

    data: LT_PRVDR_SELECTED type /VCXI/VKSR_TT_GATE.

    SUPER->DO_PBO( ).

*** Initialize Visualization Structure
    ME->INIT_VIS( ).

***--------------------------------------------------------------------------------------
*** Initialize Toolbar and provide active tab as Selected Provider (for inserting/deletion)
    append ME->R_OLD_GATE to LT_PRVDR_SELECTED.
    ME->R_TOOLBAR->REFRESH_TOOLBAR( IT_PRVDR_SELECTED = LT_PRVDR_SELECTED ).

  endmethod.
  method DO_USER_COMMAND.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    case IF_UCOMM.
      when C_UC_RLPROP.
        ME->HANDLE_UC_RLPROP( ).

    endcase.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_INPUT.

  endmethod.
  method HANDLE_UC_RLPROP.

*** Start Popup
    ZCL_VCXI_P5SS_POPUP_RLPROP=>START( IR_BL_RLBPROP = ME->R_PR_INPUT->ZIF_VCXI_P5SS_COMM_NEXT~GET_RLBPX( )
                                       IR_BL_RLFPROP = ME->R_PR_INPUT->ZIF_VCXI_P5SS_COMM_NEXT~GET_RLFPX( )
                                       IF_WERKS      = ME->R_PR_INPUT->GET_WERKS( )
                                       IS_WEBWI      = ME->R_PR_INPUT->ZIF_VCXI_P5SS_COMM_NEXT~GET_WEBWI( )
                                       IS_SURWT      = ME->R_PR_INPUT->ZIF_VCXI_P5SS_COMM_NEXT~GET_SURWT( ) ).

  endmethod.
  method INIT_VIS.

    data: LR_BL_RLFPROP type ref to ZCL_VCXI_P5SS_BL_RLFPROP.


    free: ME->S_INPUT_VIS.
    check ME->R_PR_INPUT is bound.

***--------------------------------------------------------------------------------------
*** Web Width (Outbound)
    ME->S_INPUT_VIS-S_IOSEC-S_WEBWI = ME->R_PR_INPUT->ZIF_VCXI_P5SS_COMM_NEXT~GET_WEBWI( ).

*** Get Surface Weight (Outbound)
    ME->S_INPUT_VIS-S_IOSEC-S_SURWT = ME->R_PR_INPUT->ZIF_VCXI_P5SS_COMM_NEXT~GET_SURWT( ).

*** Number Ups (Outbound)
    ME->S_INPUT_VIS-S_IOSEC-NOUPS   = ME->R_PR_INPUT->ZIF_VCXI_P5SS_COMM_NEXT~GET_NOUPS( ).

*** Get Substrate and Caliper (Outbound)
    LR_BL_RLFPROP = ME->R_PR_INPUT->ZIF_VCXI_P5SS_COMM_NEXT~GET_RLFPX( ).
    if LR_BL_RLFPROP is bound.
      ME->S_INPUT_VIS-S_IOSEC-SBSTR   = LR_BL_RLFPROP->GET_SBSTR( ).
      ME->S_INPUT_VIS-S_IOSEC-S_CALIP = LR_BL_RLFPROP->S_CALIP.
    endif.

  endmethod.
