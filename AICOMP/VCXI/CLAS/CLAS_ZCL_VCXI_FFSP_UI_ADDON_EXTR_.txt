
class ZCL_VCXI_FFSP_UI_ADDON_EXTR definition
  public
  inheriting from ZCL_VCXI_XCSP_UI_ADDON
  create public .

public section.

  data S_ADDON_EXTR_VIS type ZVCXI_FFSP_S_ADDON_EXTR_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_ADDON_EXTR_VIS
    importing
      !IS_ADDON_EXTR_VIS type ZVCXI_FFSP_S_ADDON_EXTR_VIS
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
protected section.

  data R_PR_ADDON_EXTR type ref to ZCL_VCXI_FFSP_PR_ADDON_EXTR .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_FFSP_UI_ADDON_EXTR definition
class ZCL_VCXI_FFSP_UI_ADDON_EXTR implementation.
method ACTIVATE.

  SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
  call function 'ZVCXI_FFSP_UI_ADDON_EXTR_BIND'
    exporting
      IR_UI_ADDON_EXTR = ME
    importing
      ES_DYNP          = ME->S_DYNP.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                      IR_UIGATE = IR_UIGATE
                      IR_GATE   = IR_GATE ).

*** Assign the PR Class to the UI Class
  try.
      move IR_GATE->R_CLSPR ?to ME->R_PR_ADDON_EXTR.
    catch CX_SY_MOVE_CAST_ERROR.
***   UI ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '801'.
  endtry.

endmethod.
method DO_MODIFY_SCREEN.

  data:  LF_LOCKED type        /VCXI/CKX_LOCKED,
         LF_INUSE  type        /VCXI/VKSR_INUSE.
  data:  LR_CX_CKX type ref to /VCXI/CX_CKX.

  SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                           IF_DYNNR = IF_DYNNR ).

  try.

      if IF_REPID eq ME->S_DYNP-REPID and
         IF_DYNNR eq ME->S_DYNP-DYNNR.

        LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
        LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***     Modify Scree using DynamicVC
        ZCL_VCXI_XCDR_SERVICE=>MODIFY_SCREEN( IR_GATE = ME->R_GATE ).

        loop at screen.
          if LF_LOCKED ne ABAP_TRUE or
             LF_INUSE  ne ABAP_TRUE.
            move 0 to SCREEN-INPUT.
          endif.
          modify screen.
        endloop.
      endif.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

endmethod.
  method DO_UPDATE_ADDON_EXTR_VIS.


  endmethod.
  method DO_UPDATE_VIS.

    data: LS_ADDON_EXTR_VIS type ZVCXI_FFSP_S_ADDON_EXTR_VIS.


*** Super
    SUPER->DO_UPDATE_VIS( IS_VIS = IS_VIS ).
    check ME->R_PR_ADDON_EXTR is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_ADDON_EXTR_VIS.

***--------------------------------------------------------------------------------------
*** Set Average Consumption
    if ME->S_ADDON_EXTR_VIS-S_AVGCO ne LS_ADDON_EXTR_VIS-S_AVGCO.
      ME->R_PR_ADDON_EXTR->SET_AVGCO( IS_AVGCO = LS_ADDON_EXTR_VIS-S_AVGCO
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Average Consumption Per
    if ME->S_ADDON_EXTR_VIS-S_AVGCP ne LS_ADDON_EXTR_VIS-S_AVGCP.
      ME->R_PR_ADDON_EXTR->SET_AVGCP( IS_AVGCP = LS_ADDON_EXTR_VIS-S_AVGCP
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Extrusion Width
    if ME->S_ADDON_EXTR_VIS-S_EXWID ne LS_ADDON_EXTR_VIS-S_EXWID.
      ME->R_PR_ADDON_EXTR->SET_EXWID( IS_EXWID = LS_ADDON_EXTR_VIS-S_EXWID
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Internal Film Thickness
    if ME->S_ADDON_EXTR_VIS-S_FITIN ne LS_ADDON_EXTR_VIS-S_FITIN.
      ME->R_PR_ADDON_EXTR->SET_FITIN( IS_FITIN = LS_ADDON_EXTR_VIS-S_FITIN
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set External Film Thickness
    if ME->S_ADDON_EXTR_VIS-S_FITEX ne LS_ADDON_EXTR_VIS-S_FITEX.
      ME->R_PR_ADDON_EXTR->SET_FITEX( IS_FITEX = LS_ADDON_EXTR_VIS-S_FITEX
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Blocked Film
    if ME->S_ADDON_EXTR_VIS-FBLOF ne LS_ADDON_EXTR_VIS-FBLOF.
      ME->R_PR_ADDON_EXTR->SET_FBLOF( IF_FBLOF = LS_ADDON_EXTR_VIS-FBLOF
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.


***--------------------------------------------------------------------------------------
*** Set Film Width
    if ME->S_ADDON_EXTR_VIS-S_FIDIM-S_FIWID ne LS_ADDON_EXTR_VIS-S_FIDIM-S_FIWID.
      ME->R_PR_ADDON_EXTR->SET_FIWID( IS_FIWID = LS_ADDON_EXTR_VIS-S_FIDIM-S_FIWID
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Repeat Length
    if ME->S_ADDON_EXTR_VIS-S_FIDIM-S_RELEN ne LS_ADDON_EXTR_VIS-S_FIDIM-S_RELEN.
      ME->R_PR_ADDON_EXTR->SET_RELEN( IS_RELEN = LS_ADDON_EXTR_VIS-S_FIDIM-S_RELEN
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Number of Ups
    if ME->S_ADDON_EXTR_VIS-S_FIDIM-NOUPS ne LS_ADDON_EXTR_VIS-S_FIDIM-NOUPS.
      ME->R_PR_ADDON_EXTR->SET_NOUPS( IF_NOUPS = LS_ADDON_EXTR_VIS-S_FIDIM-NOUPS
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Number of Outs
    if ME->S_ADDON_EXTR_VIS-S_FIDIM-NOOUT ne LS_ADDON_EXTR_VIS-S_FIDIM-NOOUT.
      ME->R_PR_ADDON_EXTR->SET_NOOUT( IF_NOOUT = LS_ADDON_EXTR_VIS-S_FIDIM-NOOUT
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
method HANDLE_GATE_FREED.

  SUPER->HANDLE_GATE_FREED( ).

  free: ME->R_PR_ADDON_EXTR.

endmethod.
  method INIT_VIS.

*** Super
    SUPER->INIT_VIS( ).

    free: ME->S_ADDON_EXTR_VIS.
    ME->S_ADDON_EXTR_VIS-S_ADDON_VIS = ME->S_ADDON_VIS.

***--------------------------------------------------------------------------------------
*** Get Average Consumption
    ME->R_PR_ADDON_EXTR->GET_AVGCO( importing ES_AVGCO = ME->S_ADDON_EXTR_VIS-S_AVGCO ).

*** Get Average Consumption Per
    ME->R_PR_ADDON_EXTR->GET_AVGCP( importing ES_AVGCP = ME->S_ADDON_EXTR_VIS-S_AVGCP ).

*** Get Extrusion Width
    ME->R_PR_ADDON_EXTR->GET_EXWID( importing ES_EXWID = ME->S_ADDON_EXTR_VIS-S_EXWID ).

*** Get Internal Film Thickness
    ME->R_PR_ADDON_EXTR->GET_FITIN( importing ES_FITIN = ME->S_ADDON_EXTR_VIS-S_FITIN ).

*** Get External Film Thickness
    ME->R_PR_ADDON_EXTR->GET_FITEX( importing ES_FITEX = ME->S_ADDON_EXTR_VIS-S_FITEX ).

*** Get Blocked Film
    ME->R_PR_ADDON_EXTR->GET_FBLOF( importing EF_FBLOF = ME->S_ADDON_EXTR_VIS-FBLOF ).


***--------------------------------------------------------------------------------------
*** Get Film Dimensions
    move ME->R_PR_ADDON_EXTR->GET_FIDIM( ) to ME->S_ADDON_EXTR_VIS-S_FIDIM.
    move ME->S_ADDON_EXTR_VIS-S_FIDIM-SURWT_UOM to ME->S_ADDON_EXTR_VIS-SURWT_UOMC6.

  endmethod.
