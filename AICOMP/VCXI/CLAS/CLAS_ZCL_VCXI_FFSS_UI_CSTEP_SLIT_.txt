
class ZCL_VCXI_FFSS_UI_CSTEP_SLIT definition
  public
  inheriting from ZCL_VCXI_FFSS_UI_CSTEP
  create public .

public section.

  data S_CSTEP_SLIT_VIS type ZVCXI_FFSS_S_CSTEP_SLIT_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_CSTEP_SLIT_VIS
    importing
      !IS_CSTEP_SLIT_VIS type ZVCXI_FFSS_S_CSTEP_SLIT_VIS
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
protected section.

  data R_PR_CSTEP_SLIT type ref to ZCL_VCXI_FFSS_PR_CSTEP_SLIT .

  methods INIT_VIS
    redefinition .
  methods HANDLE_GATE_FREED
    redefinition .
private section.
endclass. "ZCL_VCXI_FFSS_UI_CSTEP_SLIT definition
class ZCL_VCXI_FFSS_UI_CSTEP_SLIT implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_FFSS_UI_CSTEP_SLIT_BIND'
      exporting
        IR_UI_CSTEP_SLIT = ME
      importing
        ES_DYNP          = ME->S_DYNP.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_CSTEP_SLIT.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DO_MODIFY_SCREEN.

    data: LR_CX_CKX type ref to /VCXI/CX_CKX.

    try.
        SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                 IF_DYNNR = IF_DYNNR ).

        loop at screen.
          case SCREEN-GROUP1.
***         Disable Trim on Width if not allowed
            when 'TRW'.
              if R_PR_CSTEP_SLIT->IS_FTRAL( ) ne ABAP_TRUE.
                SCREEN-INPUT = 0.
              endif.
          endcase.
          modify screen.
        endloop.

      catch /VCXI/CX_CKX into LR_CX_CKX.
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Initialize VIS
    ME->INIT_VIS( ).

***--------------------------------------------------------------------------------------
*** Get TextEdit Conversion Text
    if ME->R_TEXTEDIT_CSTEP is bound.
      move ABAP_TRUE to ME->R_TEXTEDIT_CSTEP->F_REFRESH_REQ.
      ME->R_TEXTEDIT_CSTEP->REFRESH_TEXTEDIT( ).
    endif.

  endmethod.
  method DO_UPDATE_CSTEP_SLIT_VIS.

    check ME->R_PR_CSTEP_SLIT is bound.

***--------------------------------------------------------------------------------------
*** Set Trim on the Width
    if ME->S_CSTEP_SLIT_VIS-S_TRWID ne IS_CSTEP_SLIT_VIS-S_TRWID.
      ME->R_PR_CSTEP_SLIT->SET_TRWID( IS_TRWID = IS_CSTEP_SLIT_VIS-S_TRWID
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Number of Outs
    if ME->S_CSTEP_SLIT_VIS-NOOUT ne IS_CSTEP_SLIT_VIS-NOOUT.
      ME->R_PR_CSTEP_SLIT->SET_NOOUT( IF_NOOUT = IS_CSTEP_SLIT_VIS-NOOUT
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Update TextEdit Long Text
    if ME->R_TEXTEDIT_CSTEP is bound.
      ME->R_TEXTEDIT_CSTEP->UPDATE_TEXT( ).
    endif.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_CSTEP_SLIT.

  endmethod.
  method INIT_VIS.

    SUPER->INIT_VIS( ).

    move ME->S_CSTEP_VIS to S_CSTEP_SLIT_VIS-S_CSTEP_VIS.

***--------------------------------------------------------------------------------------
*** Number of Outs
    ME->R_PR_CSTEP_SLIT->GET_NOOUT( importing EF_NOOUT = ME->S_CSTEP_SLIT_VIS-NOOUT ).

*** Trim on the Width
    ME->R_PR_CSTEP_SLIT->GET_TRWID( importing ES_TRWID = ME->S_CSTEP_SLIT_VIS-S_TRWID ).


  endmethod.
