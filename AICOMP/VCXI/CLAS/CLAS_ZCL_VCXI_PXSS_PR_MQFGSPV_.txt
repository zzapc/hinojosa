
class ZCL_VCXI_PXSS_PR_MQFGSPV definition
  public
  inheriting from /VCXI/CL_VKSR_CLSPR
  create public .

public section.

  methods INIT_MQFG_BY_SVC
    importing
      !IR_SVC type ref to /VCXI/CL_VKSRA_SVC
    raising
      /VCXI/CX_CKX .
protected section.
private section.
endclass. "ZCL_VCXI_PXSS_PR_MQFGSPV definition
class ZCL_VCXI_PXSS_PR_MQFGSPV implementation.
  method INIT_MQFG_BY_SVC.

    data: LS_PARAM type /VCXI/VKSR_S_PARAM.
    data: LT_SVCEL type /VCXI/CKX_TT_OBJECT,
          LT_SPVRE type /VCXI/VKSR_TT_SPVRE.
    data: LR_SPVRE        type ref to /VCXI/CL_VKSR_SPVRE,
          LR_SPVCE        type ref to /VCXI/CL_VKSC_SPVCE,
          LR_SPVRE_PARENT type ref to /VCXI/CL_VKSR_SPVRE,
          LR_OBJECT       type ref to /VCXI/IF_CKX_OBJECT,
          LR_EA_FG        type ref to ZCL_VCXI_PXSS_EA_FG.


***----------------------------------------------------------------------------
*** Get Component SE and Component FG Elements and sort them
    LT_SVCEL = IR_SVC->GET_SVC_ELEMENTS( IF_ATTID  = 'MQ_COMP'
                                         IF_ATTVA  = 'X'
                                         IF_INUSE  = ABAP_TRUE
                                         IF_SORTED = ABAP_TRUE ).

*** Get current Supervised Runtime Elements
    LT_SPVRE = ME->R_GATE->GET_SPVRE( ).

***----------------------------------------------------------------------------
*** Delete old Finished Goods and keep parent
    delete LT_SPVRE where TABLE_LINE->R_PARENT is not initial.
    check LT_SPVRE is not initial.
    LR_SPVRE_PARENT ?= LT_SPVRE[ 1 ].

*** Find Customizing Element for Component
    loop at ME->R_GATE->GET_SPVCE( ) into LR_OBJECT.
      if LR_OBJECT->GET_ID( ) eq 'MQFG'.
        LR_SPVCE ?= LR_OBJECT.
        exit.
      endif.
    endloop.

*** Add new Finished Good Items
    loop at LT_SVCEL into LR_OBJECT.
      LR_EA_FG ?= cast /VCXI/CL_VKSRA_SVC_ELEMENT( LR_OBJECT )->GET_CLSEA( ).

      create object LR_SPVRE
        exporting
          IR_SPVCE  = LR_SPVCE
          IR_PARENT = LR_SPVRE_PARENT.
      LR_SPVRE->SET_INUSE( IF_INUSE = ABAP_TRUE ).
      append LR_SPVRE to LT_SPVRE.

***   Copy Quantity Structure
      LR_SPVRE->SET_PARAM( IS_PARAM = value #( PARID = ZCL_VCXI_PXSS_PR_MQFG=>C_PARID_QFACT
                                               PARTY = /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC
                                               ATFLV = LR_EA_FG->GET_QFACT( )
                                               SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC ) ).
    endloop.

    ME->R_GATE->SET_SPVRE( IT_SPVRE = LT_SPVRE ).

  endmethod.
endclass. "ZCL_VCXI_PXSS_PR_MQFGSPV implementation
