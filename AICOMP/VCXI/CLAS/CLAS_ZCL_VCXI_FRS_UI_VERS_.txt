
class ZCL_VCXI_FRS_UI_VERS definition
  public
  inheriting from ZCL_VCXI_F0S_UI_TAB
  create public .

public section.

  data S_VERS_VIS type ZVCXI_FRS_S_VERS_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABPT type ZVCXI_F0S_TABPT default '0100S_VERS_T-TAB'
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_VERS_VIS
    importing
      !IS_VERS_VIS type ZVCXI_FRS_S_VERS_VIS
    raising
      /VCXI/CX_CKX .

  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  constants C_UC_CHANGE_VRSTA type SYUCOMM value 'CHANGE_VRSTA' ##NO_TEXT.
  data R_PR_VERS type ref to ZCL_VCXI_FRS_PR_VERS .

  methods HANDLE_UC_CHANGE_VRSTA
    raising
      /VCXI/CX_CKX .
  methods INIT_VIS
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
private section.
endclass. "ZCL_VCXI_FRS_UI_VERS definition
class ZCL_VCXI_FRS_UI_VERS implementation.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE
                        IF_TABPT  = IF_TABPT ).

    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_VERS.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DO_MODIFY_SCREEN.

    data:  LR_CX_CKX type ref to /VCXI/CX_CKX.

    try.
        SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                 IF_DYNNR = IF_DYNNR ).

        loop at screen.

***       Button to Change Version Status
          if SCREEN-NAME cs 'BTN_VRSTA'.
***         Handling for released Versions
            if ME->R_PR_VERS->IS_RELEASED( ) eq ABAP_TRUE.
***           Hide Button if Undo is not allowed
              if ME->R_PR_VERS->F_FUNDO ne ABAP_TRUE.
                move 1 to SCREEN-INVISIBLE.
              else.
                move 1 to SCREEN-INPUT.
              endif.
            else.
***           Disable Button if Status is not green
              if ME->R_GATE->GET_STATUS( ) ne /VCXI/CL_VKSR_SERVICE=>C_ELMST_G.
                move 0 to SCREEN-INPUT.
              endif.
            endif.
          endif.

***       Set tabs back to input
          if SCREEN-NAME cs ME->F_TABPT.
            move 1 to SCREEN-INPUT.
          endif.

          modify screen.
        endloop.

      catch /VCXI/CX_CKX into LR_CX_CKX.
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_PBO.

    SUPER->DO_PBO( ).

*** Initialize Visualization Structure
    ME->INIT_VIS( ).

  endmethod.
  method DO_UPDATE_VERS_VIS.

***--------------------------------------------------------------------------------------
*** Set Valid From
    if ME->S_VERS_VIS-VALFR ne IS_VERS_VIS-VALFR.
      ME->R_PR_VERS->SET_VALFR( exporting IF_VALFR = IS_VERS_VIS-VALFR
                                          IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    case IF_UCOMM.
      when C_UC_CHANGE_VRSTA .
        ME->HANDLE_UC_CHANGE_VRSTA( ).
    endcase.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_VERS.

  endmethod.
  method HANDLE_UC_CHANGE_VRSTA.

*** If Version is not yet released
    if ME->R_PR_VERS->IS_RELEASED( ) ne ABAP_TRUE.
      ME->R_PR_VERS->RELEASE( ).
    else.
      ME->R_PR_VERS->UNDO_RELEASE( ).
    endif.

  endmethod.
  method INIT_VIS.

    free: ME->S_VERS_VIS.

***--------------------------------------------------------------------------------------
*** Get Valid From
    ME->R_PR_VERS->GET_VALFR( importing EF_VALFR = ME->S_VERS_VIS-VALFR ).

***--------------------------------------------------------------------------------------
*** Get Version Status
    ME->R_PR_VERS->GET_VRSTA( importing EF_VRSTA = ME->S_VERS_VIS-VRSTA ).

*** Button to change Version Status
    if ME->R_PR_VERS->IS_RELEASED( ) ne ABAP_TRUE.
      concatenate ICON_RELEASE 'Release'(001) into ME->S_VERS_VIS-BTN_VRSTA separated by SPACE.
    else.
      if ME->R_PR_VERS->F_FUNDO eq ABAP_TRUE.
        concatenate ICON_SYSTEM_UNDO 'Undo'(002) into ME->S_VERS_VIS-BTN_VRSTA separated by SPACE.
      endif.
    endif.

  endmethod.
