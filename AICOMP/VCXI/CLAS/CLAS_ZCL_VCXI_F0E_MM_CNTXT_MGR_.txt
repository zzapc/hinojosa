
class ZCL_VCXI_F0E_MM_CNTXT_MGR definition
  public
  inheriting from ZCL_VCXI_XCE_MM_CNTXT_MGR
  create public .

public section.

  class-methods CLASS_CONSTRUCTOR .

  methods GET_FG
    redefinition .
  methods GET_FG_INFO
    redefinition .
protected section.
private section.
endclass. "ZCL_VCXI_F0E_MM_CNTXT_MGR definition
class ZCL_VCXI_F0E_MM_CNTXT_MGR implementation.
  method CLASS_CONSTRUCTOR.

*** Film
    REGISTER_CNTXT( IF_CNTXT = ZCL_VCXI_FFE_MM_MMEXT=>C_CNTXT_FF
                    IF_CLSNA = 'ZCL_VCXI_FFE_MM_MMEXT' ).

*** Bag
    REGISTER_CNTXT( IF_CNTXT = ZCL_VCXI_FBE_MM_MMEXT=>C_CNTXT_FB
                    IF_CLSNA = 'ZCL_VCXI_FBE_MM_MMEXT' ).

  endmethod.
  method GET_FG.

*** Get Film Finished Goods
    append lines of IR_SVC->GET_SVC_ELEMENTS( IF_ID    = 'FFG'
                                              IF_INUSE = ABAP_TRUE ) to RT_SVCEL.

*** Get Bag Finished Goods
    append lines of IR_SVC->GET_SVC_ELEMENTS( IF_ID    = 'BFG'
                                              IF_INUSE = ABAP_TRUE ) to RT_SVCEL.

  endmethod.
  method GET_FG_INFO.

    data: LR_EA_XFG  type ref to ZCL_VCXI_F0SS_EA_XFG.


*** Get Reference to Finished Good
    try.
        LR_EA_XFG ?= IR_SVCEL_FG->GET_CLSEA( ).
      catch CX_SY_MOVE_CAST_ERROR.
        clear LR_EA_XFG.
    endtry.
    check LR_EA_XFG is bound.

*** Get Material Number
    EF_MATNR = LR_EA_XFG->GET_MATNR( ).

*** Get Context
    EF_CNTXT = switch #( IR_SVCEL_FG->/VCXI/IF_CKX_OBJECT~GET_ID( ) when 'FFG'   then ZCL_VCXI_FFE_MM_MMEXT=>C_CNTXT_FF    "Film Finished Good
                                                                    when 'BFG'   then ZCL_VCXI_FBE_MM_MMEXT=>C_CNTXT_FB    "Bag Finished Good
                                                                    else throw /VCXI/CX_CKX( ) ).

  endmethod.
endclass. "ZCL_VCXI_F0E_MM_CNTXT_MGR implementation
