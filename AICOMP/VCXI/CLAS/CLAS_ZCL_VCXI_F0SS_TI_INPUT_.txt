
class ZCL_VCXI_F0SS_TI_INPUT definition
  public
  inheriting from /VCXI/CL_VKSRU_CLSTI
  create public .

public section.

  methods /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ
    redefinition .
  methods /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL
    redefinition .
protected section.

  methods ADD_MENU_CHILDREN
    importing
      !IR_MENU type ref to CL_CTMENU
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_ADD_CHILD
    importing
      !IF_FCODE type SYUCOMM
    raising
      /VCXI/CX_CKX .
private section.

  constants C_UC_ADD_CHILD type UI_FUNC value 'ADD_CHILD@' ##NO_TEXT.
endclass. "ZCL_VCXI_F0SS_TI_INPUT definition
class ZCL_VCXI_F0SS_TI_INPUT implementation.
  method /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ.

    SUPER->/VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ( IR_MENU = IR_MENU ).

*** Add Menu for Children
    IR_MENU->ADD_SEPARATOR( ).
    ME->ADD_MENU_CHILDREN( IR_MENU = IR_MENU ).

  endmethod.
  method /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL.

    SUPER->/VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL( IF_FCODE = IF_FCODE ).

*** Add Children
    if IF_FCODE cs C_UC_ADD_CHILD.
      ME->HANDLE_UC_ADD_CHILD( IF_FCODE = IF_FCODE ).
    endif.

  endmethod.
  method ADD_MENU_CHILDREN .

    data: LF_DISABLED type ABAP_BOOL.
    data: LT_PRVDR_DEF type /VCXI/VKSR_TT_PRVDR_DEF,
          LT_GATE      type        /VCXI/VKSR_TT_GATE.
    field-symbols: <S_PRVDR_DEF> type /VCXI/VKSR_S_PRVDR_DEF.


*** Get possible Provider based on thier ID
    LT_PRVDR_DEF = ME->R_GATE->GET_PRVDR_DEF( IF_TPRVR = /VCXI/IF_VKSC_REGELMNT_PRVTY=>C_TPRVR_CHILD ).
    sort LT_PRVDR_DEF.
    delete adjacent duplicates from LT_PRVDR_DEF comparing ID.
    sort LT_PRVDR_DEF by DESCR.

*** Get Disable-Status
    if ME->R_GATE->IS_LOCKED( ) ne ABAP_TRUE or
       ME->R_GATE->IS_INUSE( )  ne ABAP_TRUE.
      move ABAP_TRUE to LF_DISABLED.
    endif.

    loop at LT_PRVDR_DEF assigning <S_PRVDR_DEF>.
      if LF_DISABLED ne ABAP_TRUE.
***     Get Gates of Provider
        LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = <S_PRVDR_DEF>-IPRVT
                                         IF_INUSE = ABAP_TRUE ).

***     Reduece possible Provider based on aready active Provider with searched ID
        loop at LT_GATE transporting no fields
                        where TABLE_LINE->F_ID eq <S_PRVDR_DEF>-ID.
          <S_PRVDR_DEF>-QTYMA = <S_PRVDR_DEF>-QTYMA - 1.
          if <S_PRVDR_DEF>-QTYMA lt 1.
            exit.
          endif.
        endloop.
      else.
        <S_PRVDR_DEF>-QTYMA = 0. " Dont allow adding in Display Mode
      endif.

***   Add Function
      IR_MENU->ADD_FUNCTION( FCODE    = C_UC_ADD_CHILD && <S_PRVDR_DEF>-ID
                             TEXT     = conv GUI_TEXT( <S_PRVDR_DEF>-DESCR )
                             DISABLED = switch #( <S_PRVDR_DEF>-QTYMA when 0 then ABAP_TRUE ) ).

    endloop.

  endmethod.
  method HANDLE_UC_ADD_CHILD .

    data: LR_GATE type ref to /VCXI/CL_VKSR_GATE.


*** Create Child based on ID provided after generic User Command
    LR_GATE = ME->R_GATE->CREATE_ELEMENT( IF_ID = SUBSTRING_AFTER( VAL = IF_FCODE SUB = C_UC_ADD_CHILD ) ).
    if LR_GATE is bound.
***   Set In Use
      if LR_GATE->IS_INUSE( ) eq ABAP_FALSE.
        LR_GATE->SET_INUSE( IF_INUSE = ABAP_TRUE ).
      endif.

***   Open UI
      ME->R_UIGATE->OPEN_SUBSCREEN( IR_GATE = LR_GATE ).
    endif.
    ME->R_GATE->RAISE_EVENTS( ).

*** Request the Refresh
    ME->R_UIGATE->REQUEST_TREE_REFRESH( ).
    ME->R_UIGATE->REFRESH_SCREEN( ).

  endmethod.
