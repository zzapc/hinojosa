
class ZCL_VCXI_FFSS_ND_CSTEP_PERF definition
  public
  inheriting from ZCL_VCXI_F0SS_ND_CSTEP
  create public .

public section.

  types:
    YT_TC030T type standard table of ZVCXI_F0S_TC030T .

  class-data T_TC030T type YT_TC030T read-only .

  class-methods CLASS_CONSTRUCTOR .
protected section.

  methods INIT_NODE
    redefinition .
private section.
endclass. "ZCL_VCXI_FFSS_ND_CSTEP_PERF definition
class ZCL_VCXI_FFSS_ND_CSTEP_PERF implementation.
  method CLASS_CONSTRUCTOR.

*** Load from DB
    select * from ZVCXI_F0S_TC030T
      into table T_TC030T
      where LANGU eq SY-LANGU.

  endmethod.
  method INIT_NODE.

    data: LF_TPERF type        ZVCXI_F0S_TPERF,
          LF_DCUTT type        ZVCXI_F0S_DCUTT,
          LF_NOLAS type        ZVCXI_F0S_NOLAS.
    data:           LS_TC030T     type        ZVCXI_F0S_TC030T.
    data:           LR_CSTEP_PERF type ref to ZCL_VCXI_FFSS_PR_CSTEP_PERF.
    field-symbols:  <S_NVAL>      type        YS_NVALS.

***--------------------------------------------------------------------------------------
*** Call Super
    SUPER->INIT_NODE( ).

***--------------------------------------------------------------------------------------
*** Check Input Gate is Perforation
    try.
        move ME->R_GATE->R_CLSPR ?to LR_CSTEP_PERF.
      catch CX_SY_MOVE_CAST_ERROR.
        clear LR_CSTEP_PERF.
    endtry.

    check LR_CSTEP_PERF is bound.

*** Set Perforation Type
    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = 2.
    move text-D01 to <S_NVAL>-VAL.

    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = 3.
    LR_CSTEP_PERF->GET_TPERF( importing EF_TPERF = LF_TPERF ).

    read table T_TC030T into LS_TC030T with key TPERF = LF_TPERF.
    if SY-SUBRC is initial.
      move LS_TC030T-DESCR to <S_NVAL>-VAL.
    endif.

    if LR_CSTEP_PERF->GET_TPERF_FLASR( ) ne ABAP_TRUE.
***   Set Die Cut Tool Number
      append initial line to ME->T_NVALS assigning <S_NVAL>.
      <S_NVAL>-FL   = 4.
      move text-D02 to <S_NVAL>-VAL.

      append initial line to ME->T_NVALS assigning <S_NVAL>.
      <S_NVAL>-FL   = 5.
      LR_CSTEP_PERF->GET_DCUTT( importing EF_DCUTT = LF_DCUTT ).

      call function 'CONVERSION_EXIT_MATN1_OUTPUT'
        exporting
          INPUT  = LF_DCUTT
        importing
          OUTPUT = <S_NVAL>-VAL.
    else.
***   Set Number of Laser Lines
      append initial line to ME->T_NVALS assigning <S_NVAL>.
      <S_NVAL>-FL   = 4.
      move text-D03 to <S_NVAL>-VAL.

      append initial line to ME->T_NVALS assigning <S_NVAL>.
      <S_NVAL>-FL   = 5.
      LR_CSTEP_PERF->GET_NOLAS( importing EF_NOLAS = LF_NOLAS ).
      write LF_NOLAS to <S_NVAL>-VAL no-zero.
    endif.

  endmethod.
endclass. "ZCL_VCXI_FFSS_ND_CSTEP_PERF implementation
