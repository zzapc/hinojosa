
class ZCL_VCXI_PXSP_GRID_MC_CORE definition
  public
  inheriting from ZCL_VCXI_XCS_GRID_PRVDR
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_CONTAINER_NAME type SCRFNAME optional
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL default ABAP_TRUE
      !IF_TABNAME type TABNAME default 'ZVCXI_PXSP_S_MC_CORE_GRID'
    raising
      /VCXI/CX_CKX .
protected section.

  data F_PURCHASED type ABAP_BOOL .

  methods CHANGE_STYLE_MC_CORE
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IS_DATA type ANY
    changing
      !CT_STYL type LVC_T_STYL
    raising
      /VCXI/CX_CKX .
  methods GET_MC_CORE_VIS
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    changing
      value(CS_DATA) type ANY
    raising
      /VCXI/CX_CKX .
  methods SET_MC_CORE_VIS
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IS_DATA type ANY
    raising
      /VCXI/CX_CKX .

  methods GET_FIELDCAT
    redefinition .
  methods GET_GRID_DATA
    redefinition .
  methods HANDLE_DATA_CHANGED_FINISHED
    redefinition .
private section.
endclass. "ZCL_VCXI_PXSP_GRID_MC_CORE definition
class ZCL_VCXI_PXSP_GRID_MC_CORE implementation.
  method CHANGE_STYLE_MC_CORE.

    data: LS_MC_CORE_VIS type ZVCXI_PXSP_S_MC_CORE_VIS.
    data: LR_PR_MC_CORE type ref to ZCL_VCXI_PXSP_PR_MC_CORE.

    free CT_STYL.

***--------------------------------------------------------------------------------------
*** Get PR Class
    try.
        move IR_GATE->R_CLSPR ?to LR_PR_MC_CORE.
      catch CX_SY_MOVE_CAST_ERROR.
    endtry.

*** "Cast" generic structure to Vis Structure
    move-corresponding IS_DATA to LS_MC_CORE_VIS.

    check LR_PR_MC_CORE is bound.

***--------------------------------------------------------------------------------------
*** Set Ink Matnr = Editable
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MATNR'
                                IF_STYLE     = ME->F_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).

*** Set Text = Editable
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'TEXT'
                                IF_STYLE     = ME->F_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).

*** Set Target Roll Length = Editable
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'RLETA'
                                IF_STYLE     = ME->F_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).

*** Set Number of Outs = Editable
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'NOOUT'
                                IF_STYLE     = ME->F_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).

*** Set Rolls per Core = Editable
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'RLPCR'
                                IF_STYLE     = ME->F_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).

***--------------------------------------------------------------------------------------
*** Set Procurement Option = editable
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PURCH'
                                IF_STYLE     = ME->F_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).

*** Set Vendor, Price and Price Unit based on Procurement Option
    case LS_MC_CORE_VIS-PURCH.
      when ZCL_VCXI_XCSP_PR_MC=>C_PURCH_DIRECT_PRICE.
***     Set Vendor = Enable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR'
                                    IF_STYLE     = ME->F_STYLE_ENABLED
                          changing  CT_STYL      = CT_STYL ).
***     Set Price = Enable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                                    IF_STYLE     = ME->F_STYLE_ENABLED
                          changing  CT_STYL      = CT_STYL ).
***     Set Price Per = Enable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                                    IF_STYLE     = ME->F_STYLE_ENABLED
                          changing  CT_STYL      = CT_STYL ).

      when ZCL_VCXI_XCSP_PR_MC=>C_PURCH_DIRECT.
***     Set Vendor = Enable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR'
                                    IF_STYLE     = ME->F_STYLE_ENABLED
                          changing  CT_STYL      = CT_STYL ).
***     Set Price = Disable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                                    IF_STYLE     = ME->F_STYLE_DISABLED
                          changing  CT_STYL      = CT_STYL ).
***     Set Price Per = Disable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                                    IF_STYLE     = ME->F_STYLE_DISABLED
                          changing  CT_STYL      = CT_STYL ).

      when ZCL_VCXI_XCSP_PR_MC=>C_PURCH_STOCK.
***     Set Vendor = Disable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR'
                                    IF_STYLE     = ME->F_STYLE_DISABLED
                          changing  CT_STYL      = CT_STYL ).
***     Set Price = Disable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                                    IF_STYLE     = ME->F_STYLE_DISABLED
                          changing  CT_STYL      = CT_STYL ).
***     Set Price Per = Disable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                                    IF_STYLE     = ME->F_STYLE_DISABLED
                          changing  CT_STYL      = CT_STYL ).
    endcase.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_GATE           = IR_GATE
                        IR_UIGATE         = IR_UIGATE
                        IF_TABNAME        = IF_TABNAME
                        IF_CONTAINER_NAME = IF_CONTAINER_NAME
                        IF_APPL_EVENTS    = IF_APPL_EVENTS ).

*** Define Provider Type for Button
    ME->F_IPRVT = ZCL_VCXI_PXSP_PR_MC_CORE=>C_IPRVT_PX_MC_CORE.

  endmethod.
  method GET_FIELDCAT.

    data: LF_LOCKED type /VCXI/CKX_LOCKED,
          LF_INUSE  type /VCXI/VKSR_INUSE,
          LF_COLPOS type I,
          LF_LIFNR  type ABAP_BOOL,
          LF_PRICE  type ABAP_BOOL.
    data: LS_MC_ADMAT_GRID type ZVCXI_PXSP_S_MC_ADMAT_GRID.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type          ANY,
                   <S_FCAT> type          LVC_S_FCAT.

***----------------------------------------------------------------------------
*** Get Standard Field Catalog
    RT_FCAT = SUPER->GET_FIELDCAT( ).

*** Get Lock and In-Use Status
    LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
    LF_INUSE  = ME->R_GATE->IS_INUSE( ).

*** Dereferencing of Data Reference
    assign ME->R_DATA->* to <T_DATA>.

*** Find Purchased
    loop at <T_DATA> assigning <S_DATA>.
      move-corresponding <S_DATA> to LS_MC_ADMAT_GRID.
      case LS_MC_ADMAT_GRID-PURCH.
        when ZCL_VCXI_XCSS_PR_MS=>C_PURCH_DIRECT_PRICE.
          LF_LIFNR = ABAP_TRUE.
          LF_PRICE = ABAP_TRUE.
        when ZCL_VCXI_XCSS_PR_MS=>C_PURCH_DIRECT.
          LF_LIFNR = ABAP_TRUE.
      endcase.
    endloop.

***----------------------------------------------------------------------------
*** Adjust Field Catalog
    loop at RT_FCAT assigning <S_FCAT>.
      case <S_FCAT>-FIELDNAME.
        when 'POSID' or 'GATKY' or 'R_GATE' or 'T_STYL'.
          move ABAP_TRUE    to <S_FCAT>-TECH.
          move 0            to <S_FCAT>-COL_POS.

***     Set Edit for all fields which should Editable at all
        when 'MATNR' or 'TEXT' or 'RLETA' or 'NOOUT' or 'RLPCR'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE    to <S_FCAT>-EDIT.
          endif.

        when 'PURCH'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE  to <S_FCAT>-EDIT.
          endif.

        when 'LIFNR'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE  to <S_FCAT>-EDIT.
          endif.

        when 'PRICE' or 'PRICP'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE  to <S_FCAT>-EDIT.
          endif.
      endcase.


***----------------------------------------------------------------------------
***   Hide Field if not purchase
      if LF_LIFNR ne ABAP_TRUE.
        if <S_FCAT>-FIELDNAME eq 'LIFNR' or
           <S_FCAT>-FIELDNAME eq 'LIFNR_DESCR'.
          move ABAP_TRUE to <S_FCAT>-NO_OUT.
        endif.
      endif.
      if LF_PRICE ne ABAP_TRUE.
        if <S_FCAT>-FIELDNAME eq 'PRICE' or
           <S_FCAT>-FIELDNAME eq 'PRICE_CURR' or
           <S_FCAT>-FIELDNAME eq 'PRICP' or
           <S_FCAT>-FIELDNAME eq 'PRICP_UOM'.
          move ABAP_TRUE to <S_FCAT>-NO_OUT.
        endif.
      endif.

***----------------------------------------------------------------------------
***   Set Position for each Field
      move 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'MATNR'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'MATNR_DESCR'. <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'CONSU'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'CONSU_UOM'.   <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'TEXT'.        <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'RLETA'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'RLETA_UOM'.   <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'NOOUT'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'RLPCR'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'PURCH'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'LIFNR'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'LIFNR_DESCR'. <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'PRICE'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'PRICE_CURR'.  <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'PRICP'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'PRICP_UOM'.   <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.

    endloop.

    sort RT_FCAT by COL_POS.

  endmethod.
  method GET_GRID_DATA.

    data: LS_LAYOUT type LVC_S_LAYO.
    data: LT_GATE type /VCXI/VKSR_TT_GATE.
    data: LR_GATE type ref to /VCXI/CL_VKSR_GATE.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type          ANY,
                   <T_STYL> type          LVC_T_STYL.

***--------------------------------------------------------------------------------------
*** Dereferencing of Data Reference
    assign IR_DATA->* to <T_DATA>.

*** Get Layout
    LS_LAYOUT = ME->GET_LAYOUT( ).

*** Get Gates of Provider
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ME->F_IPRVT
                                     IF_INUSE  = ABAP_TRUE
                                     IF_SORTED = ABAP_TRUE ).

***--------------------------------------------------------------------------------------
    loop at LT_GATE into LR_GATE.
***   Add Line with Gate Key Data
      append initial line to <T_DATA> assigning <S_DATA>.
      move-corresponding value ZVCXI_PXSP_S_MC_CORE_GRID( POSID  = LR_GATE->GET_POSID( )
                                                          GATKY  = LR_GATE->F_GATKY
                                                          R_GATE = LR_GATE
                                                        ) to <S_DATA>.

***   Get Visualization Data
      ME->GET_MC_CORE_VIS( exporting IR_GATE = LR_GATE
                           changing  CS_DATA = <S_DATA> ).

***   Set Style Details
      if LS_LAYOUT-STYLEFNAME is not initial.
        assign component LS_LAYOUT-STYLEFNAME of structure <S_DATA> to <T_STYL>.
        if SY-SUBRC eq 0.
          ME->CHANGE_STYLE_MC_CORE( exporting IR_GATE = LR_GATE
                                              IS_DATA = <S_DATA>
                                     changing CT_STYL = <T_STYL> ).
***       Call DVC Servcie - Modify Style
          ZCL_VCXI_XCDR_SERVICE=>MODIFY_STYLE( exporting IR_GATE = LR_GATE
                                               changing  CT_STYL = <T_STYL> ).
        endif.
      endif.
    endloop.

  endmethod.
  method GET_MC_CORE_VIS.

    data: LS_MC_CORE_VIS type ZVCXI_PXSP_S_MC_CORE_VIS.
    data: LR_PR_MC_CORE  type ref to ZCL_VCXI_PXSP_PR_MC_CORE.


***--------------------------------------------------------------------------------------
*** Get PR Class
    try.
        move IR_GATE->R_CLSPR ?to LR_PR_MC_CORE.
      catch CX_SY_MOVE_CAST_ERROR.
    endtry.
    check LR_PR_MC_CORE is bound.

*** "Cast" generic structure to Vis Structure
    move-corresponding CS_DATA to LS_MC_CORE_VIS.

***--------------------------------------------------------------------------------------
*** Get Material Number and Description
    LS_MC_CORE_VIS-MATNR = LR_PR_MC_CORE->GET_MATNR( ).
    LS_MC_CORE_VIS-MATNR_DESCR = LR_PR_MC_CORE->GET_MATNR_DESCR( ).

*** Get Consumption
    LS_MC_CORE_VIS-S_CONSU = LR_PR_MC_CORE->GET_CONSU( ).

*** Get Target Roll Length, Number of Outs and Rolls/Core
    LS_MC_CORE_VIS-S_RLETA = LR_PR_MC_CORE->GET_RLETA( ).
    LS_MC_CORE_VIS-NOOUT   = LR_PR_MC_CORE->GET_NOOUT( ).
    LS_MC_CORE_VIS-RLPCR   = LR_PR_MC_CORE->GET_RLPCR( ).

***--------------------------------------------------------------------------------------
*** Get Vendor, Description and Procurement Option and Text
    LS_MC_CORE_VIS-LIFNR       = LR_PR_MC_CORE->GET_LIFNR( ).
    LS_MC_CORE_VIS-LIFNR_DESCR = LR_PR_MC_CORE->GET_LIFNR_DESCR( ).
    LS_MC_CORE_VIS-PURCH       = LR_PR_MC_CORE->GET_PURCH( ).
    LS_MC_CORE_VIS-TEXT        = LR_PR_MC_CORE->GET_TEXT( ).

*** Get Price and Price Unit
    LS_MC_CORE_VIS-S_PRICE = LR_PR_MC_CORE->GET_PRICE( ).
    LS_MC_CORE_VIS-S_PRICP = LR_PR_MC_CORE->GET_PRICP( ).

***--------------------------------------------------------------------------------------
*** "Cast" Vis Structure back to generic structure
    move-corresponding LS_MC_CORE_VIS to CS_DATA.

  endmethod.
  method HANDLE_DATA_CHANGED_FINISHED.

    data: LS_MC_CORE_GRID type ZVCXI_PXSP_S_MC_CORE_GRID.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type          ANY.

    check E_MODIFIED eq ABAP_TRUE.

    try.
        SUPER->HANDLE_DATA_CHANGED_FINISHED( E_MODIFIED    = E_MODIFIED
                                             ET_GOOD_CELLS = ET_GOOD_CELLS ).

***     Dereferencing of Data Reference
        assign ME->R_DATA->* to <T_DATA>.

        loop at <T_DATA> assigning <S_DATA>.
          move-corresponding <S_DATA> to LS_MC_CORE_GRID.

          check LS_MC_CORE_GRID-R_GATE is bound and
                LS_MC_CORE_GRID-R_GATE->F_FREED is initial.

***       Set Values to PR Class
          ME->SET_MC_CORE_VIS( IR_GATE = LS_MC_CORE_GRID-R_GATE
                               IS_DATA = <S_DATA> ).

        endloop.

        ME->R_GATE->RAISE_EVENTS( ).

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

*** Request Refresh for ALV
    move ABAP_TRUE to ME->F_REFRESH_REQ.

  endmethod.
  method SET_MC_CORE_VIS.

    data: LS_MC_CORE_VIS_OLD type        ZVCXI_PXSP_S_MC_CORE_VIS,
          LS_MC_CORE_VIS_NEW type        ZVCXI_PXSP_S_MC_CORE_VIS.
    data: LR_PR_MC_CORE  type ref to ZCL_VCXI_PXSP_PR_MC_CORE.


***--------------------------------------------------------------------------------------
*** Get PR Class
*{   REPLACE        HEDK927906                                        1
*\    check IR_GATE->R_CLSPR is instance of ZCL_VCXI_PXSP_PR_MC_CORE.
*\    LR_PR_MC_CORE ?= IR_GATE->R_CLSPR.
      try.
        LR_PR_MC_CORE = cast #( IR_GATE->R_CLSPR ).
     catch CX_SY_MOVE_CAST_ERROR.
    endtry.
*}   REPLACE
    check LR_PR_MC_CORE is bound.

*** Get old Values
    ME->GET_MC_CORE_VIS( exporting IR_GATE = IR_GATE
                         changing  CS_DATA = LS_MC_CORE_VIS_OLD ).
*** Get new Values
    move-corresponding IS_DATA to LS_MC_CORE_VIS_NEW.

***----------------------------------------------------------------------------
*** Material
    if LS_MC_CORE_VIS_NEW-MATNR ne LS_MC_CORE_VIS_OLD-MATNR.
      LR_PR_MC_CORE->SET_MATNR( IF_MATNR = LS_MC_CORE_VIS_NEW-MATNR
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Target Roll Length
    if LS_MC_CORE_VIS_NEW-S_RLETA ne LS_MC_CORE_VIS_OLD-S_RLETA.
      LR_PR_MC_CORE->SET_RLETA( IS_RLETA = LS_MC_CORE_VIS_NEW-S_RLETA
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Number of Outs
    if LS_MC_CORE_VIS_NEW-NOOUT ne LS_MC_CORE_VIS_OLD-NOOUT.
      LR_PR_MC_CORE->SET_NOOUT( IF_NOOUT = LS_MC_CORE_VIS_NEW-NOOUT
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Rolls/Core
    if LS_MC_CORE_VIS_NEW-RLPCR ne LS_MC_CORE_VIS_OLD-RLPCR.
      LR_PR_MC_CORE->SET_RLPCR( IF_RLPCR = LS_MC_CORE_VIS_NEW-RLPCR
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Text
    if LS_MC_CORE_VIS_NEW-TEXT ne LS_MC_CORE_VIS_OLD-TEXT.
      LR_PR_MC_CORE->SET_TEXT( IF_TEXT  = LS_MC_CORE_VIS_NEW-TEXT
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***----------------------------------------------------------------------------
*** Procurement Option
    if LS_MC_CORE_VIS_NEW-PURCH ne LS_MC_CORE_VIS_OLD-PURCH.
      LR_PR_MC_CORE->SET_PURCH( IF_PURCH = LS_MC_CORE_VIS_NEW-PURCH
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Vendor
    if LS_MC_CORE_VIS_NEW-LIFNR ne LS_MC_CORE_VIS_OLD-LIFNR.
      LR_PR_MC_CORE->SET_LIFNR( IF_LIFNR = LS_MC_CORE_VIS_NEW-LIFNR
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Price
    if LS_MC_CORE_VIS_NEW-S_PRICE ne LS_MC_CORE_VIS_OLD-S_PRICE.
      LR_PR_MC_CORE->SET_PRICE( IS_PRICE = LS_MC_CORE_VIS_NEW-S_PRICE
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Price per
    if LS_MC_CORE_VIS_NEW-S_PRICP ne LS_MC_CORE_VIS_OLD-S_PRICP.
      LR_PR_MC_CORE->SET_PRICP( IS_PRICP = LS_MC_CORE_VIS_NEW-S_PRICP
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
