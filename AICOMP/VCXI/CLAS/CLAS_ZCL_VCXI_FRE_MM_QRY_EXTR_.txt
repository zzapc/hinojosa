
class ZCL_VCXI_FRE_MM_QRY_EXTR definition
  public
  inheriting from ZCL_VCXI_XCA_MWB_CLQRY
  create public .

public section.

  data:
    I_MATNR type range of MATNR .
  data:
    I_MAKTX type range of MAKTX .
  data:
    I_MSTAE type range of MSTAE .
  data:
    I_MMSTA type range of MMSTA .

  methods CONSTRUCTOR
    raising
      /VCXI/CX_CKX .

  methods EXECUTE
    redefinition .
protected section.
private section.
endclass. "ZCL_VCXI_FRE_MM_QRY_EXTR definition
class ZCL_VCXI_FRE_MM_QRY_EXTR implementation.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( ).

    ME->F_MARC = ABAP_TRUE.

  endmethod.
  method EXECUTE.

    data: LF_MATNR   type MATNR.
    data: LS_ORGDATA type ZVCXI_XCA_MWB_S_ORGDATA.
    data: LT_MATNR   type table of MATNR,
          LT_ORGDATA type ZVCXI_XCA_MWB_TT_ORGDATA.
    data: LI_CUOBJ type range of CUOBJ.
    data: LR_CLMAT type ref to ZCL_VCXI_XCA_MWB_CLMAT.


***------------------------------------------------------------------
*** Get Configurable Material of Template Materials by Model ID
    data(LF_IREGE) = ME->R_REGMDL->/VCXI/IF_CKX_OBJECT~GET_ID( ).
    select STDPD
           into table @data(LT_STDPD)
           from ZVCXI_XCA_TD110
           inner join MARC
            on MARC~MATNR eq ZVCXI_XCA_TD110~MATNR
          where IREGE eq @LF_IREGE.

    check LT_STDPD is not initial.

***------------------------------------------------------------------
*** Select Data from Database
    select MARA~MATNR
           from MARA
          inner join MARC
             on MARC~MATNR eq MARA~MATNR
          inner join @LT_STDPD as TEMPLATE
             on TEMPLATE~STDPD eq MARC~STDPD
          inner join MAKT
             on MAKT~MATNR eq MARA~MATNR and
                MAKT~SPRAS eq @SY-LANGU
          where MARA~MATNR in @ME->I_MATNR
            and MARC~WERKS in @ME->I_WERKS
            and MARA~MSTAE in @ME->I_MSTAE
            and MAKT~MAKTX in @ME->I_MAKTX
            and MARC~MMSTA in @ME->I_MMSTA
            and MARC~CUOBJ in @LI_CUOBJ
            and MARC~LVORM ne 'X'
           group by MARA~MATNR
           order by MARA~MATNR
           into table @LT_MATNR
           up to @ME->F_MAXSEL rows.

***------------------------------------------------------------------
*** Create Material Object List based on Selection Result
    sort LT_MATNR.
    delete adjacent duplicates from LT_MATNR.
    loop at LT_MATNR into LF_MATNR.
***   Get all Organisational Data for a Material
      LT_ORGDATA = ME->GET_ORGDATA( IF_MATNR = LF_MATNR ).

***   Create Material instance for each Org Data
      loop at LT_ORGDATA into LS_ORGDATA.
        append ZCL_VCXI_XCA_MWB_CLMAT=>FACTORY( IR_REGMDL  = ME->R_REGMDL
                                                IF_MATNR   = LF_MATNR
                                                IS_ORGDATA = LS_ORGDATA ) to RT_RESULT.
      endloop.
    endloop.

  endmethod.
endclass. "ZCL_VCXI_FRE_MM_QRY_EXTR implementation
