
class ZCL_VCXI_FFSS_UI_OUTPUT definition
  public
  inheriting from ZCL_VCXI_F0S_UI_TAB
  create public .

public section.

  data S_OUTPUT_VIS type ZVCXI_FFSS_S_OUTPUT_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABPT type ZVCXI_F0S_TABPT default '0100S_OUT_TAB-TAB'
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_OUTPUT_VIS
    importing
      !IS_OUTPUT_VIS type ZVCXI_FFSS_S_OUTPUT_VIS
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  data R_TOOLBAR type ref to ZCL_VCXI_F0S_TOOLBAR_PRVDR .
  data R_PR_OUTPUT type ref to ZCL_VCXI_FFSS_PR_OUTPUT .
  constants C_UC_FSTRC type SYUCOMM value 'OUTPUT_FSTRC' ##NO_TEXT.
  constants C_UC_FPROP type SYUCOMM value 'OUTPUT_FPROP' ##NO_TEXT.

  methods HANDLE_UC_FPROP
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_FSTRC
    raising
      /VCXI/CX_CKX .
  methods INIT_VIS
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
private section.
endclass. "ZCL_VCXI_FFSS_UI_OUTPUT definition
class ZCL_VCXI_FFSS_UI_OUTPUT implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_FFSS_UI_OUTPUT_BIND'
      exporting
        IR_UI_OUTPUT  = ME
      importing
        ES_DYNP       = ME->S_DYNP
        EF_ACTIVE_TAB = ME->F_ACTIVE_TAB.


*** Create Toolbar for Provider
    create object ME->R_TOOLBAR
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_TOOLBAR'.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE
                        IF_TABPT  = IF_TABPT ).

    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_OUTPUT.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

*** Free Toolbar
    if ME->R_TOOLBAR is bound.
      ME->R_TOOLBAR->DO_FREE( ).
      clear ME->R_TOOLBAR.
    endif.

  endmethod.
  method DO_MODIFY_SCREEN.

    data: LF_LOCKED type        /VCXI/CKX_LOCKED,
          LF_INUSE  type        /VCXI/VKSR_INUSE.
    data: LR_CX_CKX type ref to /VCXI/CX_CKX.

    try.
        SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                 IF_DYNNR = IF_DYNNR ).

        LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
        LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***     Modify Screen using DynamicVC
        ZCL_VCXI_XCDR_SERVICE=>MODIFY_SCREEN( IR_GATE = ME->R_GATE ).

        loop at screen.
          if SCREEN-GROUP1 = 'TRW'.
***         Disable Trim on Width if not allowed
            if ME->R_PR_OUTPUT->IS_FTRAL( ) ne ABAP_TRUE.
              SCREEN-INPUT = 0.
            endif.
***         Disable Trim on Width if Slitting is Optimized
            if ME->S_OUTPUT_VIS-FOPSL eq ABAP_TRUE.
              SCREEN-INPUT = 0.
            endif.
          endif.

***       Hide Optimization of Slitting if not available
          if SCREEN-NAME eq 'ZVCXI_FFSS_S_OUTPUT_VIS-FOPSL' and
             ME->S_OUTPUT_VIS-FOPSL eq ABAP_UNDEFINED.
            SCREEN-INVISIBLE = 1.
            SCREEN-INPUT     = 0.
          endif.

***       Reopen Info Buttons
          if SCREEN-GROUP1 eq 'INF'.
            move 1 to SCREEN-INPUT.
          endif.

          modify screen.
        endloop.

      catch /VCXI/CX_CKX into LR_CX_CKX.
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_PBO.

    data: LT_PRVDR_SELECTED type /VCXI/VKSR_TT_GATE.

    SUPER->DO_PBO( ).

*** Initialize Visualization Structure
    ME->INIT_VIS( ).

***--------------------------------------------------------------------------------------
    append ME->R_OLD_GATE to LT_PRVDR_SELECTED.
*** Initialize Toolbar
    ME->R_TOOLBAR->REFRESH_TOOLBAR( IT_PRVDR_SELECTED = LT_PRVDR_SELECTED ).

  endmethod.
  method DO_UPDATE_OUTPUT_VIS.

    check ME->R_PR_OUTPUT is bound.

***--------------------------------------------------------------------------------------
*** Set Trim Width
    if ME->S_OUTPUT_VIS-S_TRWID ne IS_OUTPUT_VIS-S_TRWID.
      ME->R_PR_OUTPUT->SET_TRWID( IS_TRWID = IS_OUTPUT_VIS-S_TRWID
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Flag Optimized Slitting
    if ME->S_OUTPUT_VIS-FOPSL ne IS_OUTPUT_VIS-FOPSL.
      ME->R_PR_OUTPUT->SET_FOPSL( IF_FOPSL = IS_OUTPUT_VIS-FOPSL
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    data:  LF_UCOMM type SYUCOMM.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    case IF_UCOMM.
      when C_UC_FSTRC.
        ME->HANDLE_UC_FSTRC( ).

      when C_UC_FPROP.
        ME->HANDLE_UC_FPROP( ).

    endcase.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_OUTPUT.

  endmethod.
  method HANDLE_UC_FPROP.

    data: LR_BL_FIBPROP    type ref to ZCL_VCXI_FFSS_BL_FIBPROP,
          LR_BL_FIFPROP    type ref to ZCL_VCXI_FFSS_BL_FIFPROP.

*** Get Film Property
    ME->R_PR_OUTPUT->ZIF_VCXI_FFSS_COMM_PREV~GET_FIBPX( importing ER_BL_FIBPROP = LR_BL_FIBPROP ).
    ME->R_PR_OUTPUT->ZIF_VCXI_FFSS_COMM_PREV~GET_FIFPX( importing ER_BL_FIFPROP = LR_BL_FIFPROP ).

*** Start Popup
    ZCL_VCXI_FFSS_POPUP_FIPROP=>START( IR_BL_FIBPROP = LR_BL_FIBPROP
                                       IR_BL_FIFPROP = LR_BL_FIFPROP
                                       IS_FIWID      = ME->S_OUTPUT_VIS-S_FIWID
                                       IS_FITIN      = ME->S_OUTPUT_VIS-S_FITIN  ).

  endmethod.
  method HANDLE_UC_FSTRC.

    data: LR_BL_FISTRUCT     type ref to ZCL_VCXI_FFSS_BL_FISTRUCT.


*** Get Film Structure XML
    ME->R_PR_OUTPUT->ZIF_VCXI_FFSS_COMM_PREV~GET_FISTX( importing ER_BL_FISTRUCT = LR_BL_FISTRUCT ).

*** Start Popup
    check LR_BL_FISTRUCT is bound.
    ZCL_VCXI_FFSS_POPUP_FISTRUCT=>START( IR_BL_FISTRUCT = LR_BL_FISTRUCT ).

  endmethod.
  method INIT_VIS.

    free: ME->S_OUTPUT_VIS.
    check ME->R_PR_OUTPUT is bound.

***--------------------------------------------------------------------------------------
*** Film Width (Inbound)
    ME->R_PR_OUTPUT->ZIF_VCXI_FFSS_COMM_PREV~GET_FIWID( importing ES_FIWID = ME->S_OUTPUT_VIS-S_FIWID ).

*** Trim Requirement on Width (Inbound)
    ME->R_PR_OUTPUT->ZIF_VCXI_FFSS_COMM_PREV~GET_TREQW( importing ES_TREQW = ME->S_OUTPUT_VIS-S_TREQW ).

*** Number of Ups (Inbound)
    ME->R_PR_OUTPUT->ZIF_VCXI_FFSS_COMM_PREV~GET_NOUPS( importing EF_NOUPS = ME->S_OUTPUT_VIS-NOUPS ).

*** Internal Film Thickness (Inbound)
    ME->R_PR_OUTPUT->ZIF_VCXI_FFSS_COMM_PREV~GET_FITIN( importing ES_FITIN = ME->S_OUTPUT_VIS-S_FITIN ).

*** Film Thickness External (Inbound)
    ME->R_PR_OUTPUT->ZIF_VCXI_FFSS_COMM_PREV~GET_FITEX( importing ES_FITEX = ME->S_OUTPUT_VIS-S_FITEX ).

*** Surface Weight (Inbound)
    ME->R_PR_OUTPUT->ZIF_VCXI_FFSS_COMM_PREV~GET_SURWT( importing ES_SURWT = ME->S_OUTPUT_VIS-S_SURWT ).
    move ME->S_OUTPUT_VIS-S_SURWT-SURWT_UOM to ME->S_OUTPUT_VIS-SURWT_UOMC6.

***--------------------------------------------------------------------------------------
*** Trim on the Width
    ME->R_PR_OUTPUT->GET_TRWID( importing ES_TRWID = ME->S_OUTPUT_VIS-S_TRWID ).

*** Flag Optimized Slitting
    ME->R_PR_OUTPUT->GET_FOPSL( importing EF_FOPSL = ME->S_OUTPUT_VIS-FOPSL ).

  endmethod.
