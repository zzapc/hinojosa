
class ZCL_VCXI_PCSU_PR_SHIPMGR_CR definition
  public
  inheriting from ZCL_VCXI_XCSU_PR_SHIPMGR
  create public .

public section.

  constants C_PMATY_PALLET type ZVCXI_XCSU_PMATY value 'ZVCXI_P0_PALLET'. "#EC NOTEXT

  methods GET_BASE_DIMEN
    redefinition .
  methods GET_BASE_PRQTY
    redefinition .
protected section.

  data R_PR_SPEC_CR type ref to ZCL_VCXI_PCSS_PR_SPEC_CR .
  data R_PR_AREA_WGT_CR type ref to ZCL_VCXI_PCSS_PR_AREA_WGT_CR .

  methods HANDLE_INIT_REFERENCES
    redefinition .
private section.
endclass. "ZCL_VCXI_PCSU_PR_SHIPMGR_CR definition
class ZCL_VCXI_PCSU_PR_SHIPMGR_CR implementation.
method GET_BASE_DIMEN.

  data:          LS_RODIA  type ZVCXI_PCS_S_RODIA,
                 LS_ROWID  type ZVCXI_PCS_S_ROWID,
                 LS_WDRYR  type ZVCXI_PCSS_S_WDRYR.
  field-symbols: <S_DIMEN> type ZVCXI_XCSU_S_DIMEN.

*** First check if all References are there
  if ME->R_PR_SPEC_CR     is not bound or
     ME->R_PR_AREA_WGT_CR is not bound.
    ME->HANDLE_INIT_REFERENCES( ).
  endif.


  if ME->R_PR_SPEC_CR is bound.
    ME->R_PR_SPEC_CR->GET_RODIA( importing ES_RODIA = LS_RODIA ).
    LS_ROWID = ME->R_PR_SPEC_CR->GET_ROWID( ).
  endif.

  if ME->R_PR_AREA_WGT_CR is bound.
    LS_WDRYR = ME->R_PR_AREA_WGT_CR->GET_WDRYR( ).
  endif.

  append initial line to RT_DIMEN assigning <S_DIMEN>.
  <S_DIMEN>-DIMEN      = ZCL_VCXI_XCSU_CUST_SHIPU=>C_DIMEN_DIAMETER.
  <S_DIMEN>-DIMVA      = LS_RODIA-RODIA.
  <S_DIMEN>-DIMVA_UOM  = LS_RODIA-RODIA_UOM.

  append initial line to RT_DIMEN assigning <S_DIMEN>.
  <S_DIMEN>-DIMEN     = ZCL_VCXI_XCSU_CUST_SHIPU=>C_DIMEN_HEIGHT.
  <S_DIMEN>-DIMVA     = LS_ROWID-ROWID.
  <S_DIMEN>-DIMVA_UOM = LS_ROWID-ROWID_UOM.

  append initial line to RT_DIMEN assigning <S_DIMEN>.
  <S_DIMEN>-DIMEN     = ZCL_VCXI_XCSU_CUST_SHIPU=>C_DIMEN_WEIGHT.
  <S_DIMEN>-DIMVA     = LS_WDRYR-WDRYR.
  <S_DIMEN>-DIMVA_UOM = LS_WDRYR-WDRYR_UOM.

endmethod.
method GET_BASE_PRQTY.

  RS_PRQTY-PRQTY     = 1.
  RS_PRQTY-PRQTY_UOM = 'ST'.

endmethod.
method HANDLE_INIT_REFERENCES.

  data:  LT_GATE            type        /VCXI/VKSR_TT_GATE.
  data:  LR_GATE            type ref to /VCXI/CL_VKSR_GATE.

  SUPER->HANDLE_INIT_REFERENCES( ).

***------------------------------------------------------------------------------------------------
*** Get Sheet Reference
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_PCSS_PR_SPEC_CR=>C_IPRVT_PC_SPEC_CR
                                   IF_INUSE = ABAP_TRUE ).

  read table LT_GATE into LR_GATE index 1.
  try.
      if LR_GATE          is bound and
         LR_GATE->R_CLSPR is bound.
        move LR_GATE->R_CLSPR ?to ME->R_PR_SPEC_CR.
      endif.
    catch CX_SY_MOVE_CAST_ERROR.
      clear: ME->R_PR_SPEC_CR.
  endtry.

  if ME->R_PR_SPEC_CR is not bound.
*** Provider &1 is not available for &2.
    /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE(
        IF_MSGTY = 'E'
        IF_MSGID = 'ZVCXI_XCS'
        IF_MSGNO = '010'
        IF_MSGV1 = ZCL_VCXI_PCSS_PR_SPEC_CR=>C_IPRVT_PC_SPEC_CR
        IF_MSGV2 = ME->R_GATE->F_ID ).
  endif.

***------------------------------------------------------------------------------------------------
*** Get Area and Weight Reference
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_PCSS_PR_AREA_WGT_CR=>C_IPRVT_PC_AREA_WGT_CR
                                   IF_INUSE = ABAP_TRUE ).

  read table LT_GATE into LR_GATE index 1.
  try.
      if LR_GATE          is bound and
         LR_GATE->R_CLSPR is bound.
        move LR_GATE->R_CLSPR ?to ME->R_PR_AREA_WGT_CR.
      endif.
    catch CX_SY_MOVE_CAST_ERROR.
      clear: ME->R_PR_AREA_WGT_CR.
  endtry.

  if ME->R_PR_AREA_WGT_CR is not bound.
*** Provider &1 is not available for &2.
    /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE(
        IF_MSGTY = 'E'
        IF_MSGID = 'ZVCXI_XCS'
        IF_MSGNO = '010'
        IF_MSGV1 = ZCL_VCXI_PCSS_PR_AREA_WGT_CR=>C_IPRVT_PC_AREA_WGT_CR
        IF_MSGV2 = ME->R_GATE->F_ID ).
  endif.

endmethod.
endclass. "ZCL_VCXI_PCSU_PR_SHIPMGR_CR implementation
