
class ZCL_VCXI_FBSS_UI_BCONI definition
  public
  inheriting from /VCXI/CL_VKSRU_CLSUI
  create public .

public section.

  data S_BCONI_VIS type ZVCXI_FBSS_S_BCONI_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABPT type ZVCXI_F0S_TABPT default '0100S_IN_TAB-TAB'
    raising
      /VCXI/CX_CKX .
  methods DO_PBO
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  data R_PR_BCONI type ref to ZCL_VCXI_FBSS_PR_BCONI .
  constants C_UC_BGPROP type SYUCOMM value 'BCONI_BGPROP' ##NO_TEXT.
  constants C_UC_CONOUT type SYUCOMM value 'BCONI_CONOUT' ##NO_TEXT.

  methods HANDLE_UC_BGPROP
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_CONOUT
    raising
      /VCXI/CX_CKX .
  methods INIT_VIS
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_FBSS_UI_BCONI definition
class ZCL_VCXI_FBSS_UI_BCONI implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_FBSS_UI_BCONI_BIND'
      exporting
        IR_UI_BCONI = ME
      importing
        ES_DYNP     = ME->S_DYNP.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_BCONI.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DO_MODIFY_SCREEN.

    data: LF_LOCKED type        /VCXI/CKX_LOCKED,
          LF_INUSE  type        /VCXI/VKSR_INUSE.
    data: LR_CX_CKX type ref to /VCXI/CX_CKX.

    try.
        SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                 IF_DYNNR = IF_DYNNR ).

        LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
        LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***     Modify Scree using DynamicVC
        ZCL_VCXI_XCDR_SERVICE=>MODIFY_SCREEN( IR_GATE = ME->R_GATE ).

        loop at screen.
***       Lock everything if not in change mode
          if LF_LOCKED ne ABAP_TRUE or
             LF_INUSE  ne ABAP_TRUE.
            move 0 to SCREEN-INPUT.
          endif.
***       Reopen Info Buttons
          if SCREEN-GROUP1 eq 'INF'.
            move 1 to SCREEN-INPUT.
          endif.

*          if SCREEN-NAME eq '0100BT_CONOUT'.
*            if ME->R_PR_BCONI->GET_CONNECTED_GATE( ) is not bound.
*              move 0 to SCREEN-INPUT.
*            endif.
*          endif.

          modify screen.
        endloop.

      catch /VCXI/CX_CKX into LR_CX_CKX.
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_PBO.

*** Initialize Visualization Structure
    ME->INIT_VIS( ).

  endmethod.
  method DO_USER_COMMAND.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    case IF_UCOMM.
      when C_UC_BGPROP.
        ME->HANDLE_UC_BGPROP( ).

      when C_UC_CONOUT.
        ME->HANDLE_UC_CONOUT( ).

    endcase.

  endmethod.
  method HANDLE_UC_BGPROP.

    data: LS_BGDATA type ZVCXI_FBSS_S_BGDATA.
    data: LR_BL_BGBPROP type ref to ZCL_VCXI_FBSS_BL_BGBPROP,
          LR_BL_BGFPROP type ref to ZCL_VCXI_FBSS_BL_BGFPROP.

***--------------------------------------------------------------------------------------
*** Get Bag Connection In Backward Property XML (Outbound)
    ME->R_PR_BCONI->ZIF_VCXI_FBSS_COMM_PREV~GET_BGBPX( importing ER_BL_BGBPROP = LR_BL_BGBPROP ).
    check LR_BL_BGBPROP is bound.

***--------------------------------------------------------------------------------------
*** Get Bag Connection In Forward Property XML (Outbound)
    ME->R_PR_BCONI->ZIF_VCXI_FBSS_COMM_PREV~GET_BGFPX( importing ER_BL_BGFPROP = LR_BL_BGFPROP ).
    check LR_BL_BGFPROP is bound.

***--------------------------------------------------------------------------------------
*** Get Bag Basic data information
    ME->R_PR_BCONI->ZIF_VCXI_FBSS_COMM_PREV~GET_IBAGT( importing EF_IBAGT = LS_BGDATA-IBAGT ).

    ME->R_PR_BCONI->ZIF_VCXI_FBSS_COMM_PREV~GET_BGHEI( importing ES_BGHEI = LS_BGDATA-S_BGHEI ).

    ME->R_PR_BCONI->ZIF_VCXI_FBSS_COMM_PREV~GET_BGWID( importing ES_BGWID = LS_BGDATA-S_BGWID ).

    ME->R_PR_BCONI->ZIF_VCXI_FBSS_COMM_PREV~GET_BGWGT( importing ES_BGWGT = LS_BGDATA-S_BGWGT ).

*** Start Popup
    ZCL_VCXI_FBSS_POPUP_BGPROP=>START( IR_GATE       = ME->R_GATE
                                       IR_UIGATE     = ME->R_UIGATE
                                       IR_BL_BGBPROP = LR_BL_BGBPROP
                                       IR_BL_BGFPROP = LR_BL_BGFPROP
                                       IS_BGDATA     = LS_BGDATA  ).

  endmethod.
  method HANDLE_UC_CONOUT.

    data: LT_GATE  type /VCXI/VKSR_TT_GATE.
    data: LR_PR_SOV     type ref to ZCL_VCXI_F0SS_PR_SOV,
          LR_BCONO_GATE type ref to /VCXI/CL_VKSR_GATE.

***--------------------------------------------------------------------------------------
*** Get Connected Gate
    check ME->R_PR_BCONI is bound.
    move ME->R_PR_BCONI->GET_CONNECTED_GATE( ) to LR_BCONO_GATE.

***--------------------------------------------------------------------------------------
*** Connection Options
    if LR_BCONO_GATE is not bound.
      LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_F0SS_PR_SOV=>C_IPRVT_F0_SOV ).
      check LT_GATE is not initial.
      LR_PR_SOV ?= LT_GATE[ 1 ]->R_CLSPR.
      check LR_PR_SOV is bound.
      LT_GATE = LR_PR_SOV->GET_PRVDR_XCONO( IF_CONNECTED = ABAP_FALSE ).
      if LINES( LT_GATE ) eq 1.
        LR_BCONO_GATE = LT_GATE[ 1 ].
        LR_PR_SOV->CONNECT( IR_GATE_IN  = ME->R_GATE
                            IR_GATE_OUT = LR_BCONO_GATE ).
      endif.
    endif.

***--------------------------------------------------------------------------------------
*** Navigate to Connection
    check LR_BCONO_GATE is bound and
          LR_BCONO_GATE->GET_CLSUI( ) is not initial.
    ME->R_UIGATE->OPEN_SUBSCREEN( IR_GATE = LR_BCONO_GATE ).

*** Open Tree Node
    ME->R_UIGATE->EXPAND_TREE_NODE( IF_GATKY = LR_BCONO_GATE->F_GATKY ).

*** Highlight Tree Node
    ME->R_UIGATE->SELECT_TREE_NODE( IF_GATKY = LR_BCONO_GATE->F_GATKY ).

  endmethod.
  method INIT_VIS.

    free: ME->S_BCONI_VIS.
    check ME->R_PR_BCONI is bound.

***--------------------------------------------------------------------------------------
*** Material Description
    ME->R_PR_BCONI->GET_MAKTX( importing EF_MAKTX = ME->S_BCONI_VIS-MAKTX ).

***--------------------------------------------------------------------------------------
*** Bag Type (Inbound)
    ME->R_PR_BCONI->ZIF_VCXI_FBSS_COMM_PREV~GET_IBAGT( importing EF_IBAGT = ME->S_BCONI_VIS-IBAGT
                                                                 EF_DESCR = ME->S_BCONI_VIS-IBAGT_DESCR ).

*** Bag Width (Inbound)
    ME->R_PR_BCONI->ZIF_VCXI_FBSS_COMM_PREV~GET_BGWID( importing ES_BGWID = ME->S_BCONI_VIS-S_BGWID ).

*** Bag Height (Inbound)
    ME->R_PR_BCONI->ZIF_VCXI_FBSS_COMM_PREV~GET_BGHEI( importing ES_BGHEI = ME->S_BCONI_VIS-S_BGHEI ).

*** Bag Weight (Inbound)
    ME->R_PR_BCONI->ZIF_VCXI_FBSS_COMM_PREV~GET_BGWGT( importing ES_BGWGT = ME->S_BCONI_VIS-S_BGWGT ).

  endmethod.
