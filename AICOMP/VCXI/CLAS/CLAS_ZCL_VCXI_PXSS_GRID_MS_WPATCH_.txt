
class ZCL_VCXI_PXSS_GRID_MS_WPATCH definition
  public
  inheriting from ZCL_VCXI_XCS_GRID_PRVDR
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABNAME type TABNAME default 'ZVCXI_PXSS_S_MS_WPATCH_GRID'
      !IF_CONTAINER_NAME type SCRFNAME
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL default ABAP_TRUE
    raising
      /VCXI/CX_CKX .
protected section.

  methods CHANGE_STYLE_MS_WPATCH
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IS_DATA type ANY
    changing
      !CT_STYL type LVC_T_STYL
    raising
      /VCXI/CX_CKX .
  methods GET_MS_WPATCH_VIS
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    changing
      !CS_DATA type ANY
    raising
      /VCXI/CX_CKX .
  methods SET_MS_WPATCH_VIS
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IS_DATA type DATA
    raising
      /VCXI/CX_CKX .

  methods GET_FIELDCAT
    redefinition .
  methods GET_GRID_DATA
    redefinition .
  methods HANDLE_DATA_CHANGED_FINISHED
    redefinition .
private section.
endclass. "ZCL_VCXI_PXSS_GRID_MS_WPATCH definition
class ZCL_VCXI_PXSS_GRID_MS_WPATCH implementation.
  method CHANGE_STYLE_MS_WPATCH.

    data: LS_MS_WPATCH_VIS type ZVCXI_PXSS_S_MS_WPATCH_VIS.
    data: LR_PR_MS_WPATCH  type ref to ZCL_VCXI_PXSS_PR_MS_WPATCH.


    free CT_STYL.

***--------------------------------------------------------------------------------------
*** Get PR Class
    try.
        move IR_GATE->R_CLSPR ?to LR_PR_MS_WPATCH.
      catch CX_SY_MOVE_CAST_ERROR.
    endtry.
    check LR_PR_MS_WPATCH is bound.

*** "Cast" generic structure to Vis Structure
    move-corresponding IS_DATA to LS_MS_WPATCH_VIS.

***------------------------------------------------------------------
*** Set Material Number = Enable
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MATNR'
                                IF_STYLE     = ME->F_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).

***------------------------------------------------------------------
*** Purchase
    case LS_MS_WPATCH_VIS-PURCH.
      when ZCL_VCXI_XCSS_PR_MS=>C_PURCH_DIRECT_PRICE.
***     Set Vendor = Enable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR'
                                    IF_STYLE     = ME->F_STYLE_ENABLED
                          changing  CT_STYL      = CT_STYL ).
***     Set Price = Enable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                                    IF_STYLE     = ME->F_STYLE_ENABLED
                          changing  CT_STYL      = CT_STYL ).
***     Set Price Per = Enable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                                    IF_STYLE     = ME->F_STYLE_ENABLED
                          changing  CT_STYL      = CT_STYL ).

      when ZCL_VCXI_XCSS_PR_MS=>C_PURCH_DIRECT.
***     Set Vendor = Enable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR'
                                    IF_STYLE     = ME->F_STYLE_ENABLED
                          changing  CT_STYL      = CT_STYL ).
***     Set Price = Disable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                                    IF_STYLE     = ME->F_STYLE_DISABLED
                          changing  CT_STYL      = CT_STYL ).
***     Set Price Per = Disable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                                    IF_STYLE     = ME->F_STYLE_DISABLED
                          changing  CT_STYL      = CT_STYL ).

      when ZCL_VCXI_XCSS_PR_MS=>C_PURCH_STOCK.
***     Set Vendor = Disable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR'
                                    IF_STYLE     = ME->F_STYLE_DISABLED
                          changing  CT_STYL      = CT_STYL ).
***     Set Price = Disable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                                    IF_STYLE     = ME->F_STYLE_DISABLED
                          changing  CT_STYL      = CT_STYL ).
***     Set Price Per = Disable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                                    IF_STYLE     = ME->F_STYLE_DISABLED
                          changing  CT_STYL      = CT_STYL ).
    endcase.

***------------------------------------------------------------------
*** Dummy Flag
    if LR_PR_MS_WPATCH->IS_DUMMY( ) eq ABAP_TRUE.
***   Window Patch Dimensions = Enable
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'WPLEN'
                                  IF_STYLE     = ME->F_STYLE_ENABLED
                        changing  CT_STYL      = CT_STYL ).
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'WPWID'
                                  IF_STYLE     = ME->F_STYLE_ENABLED
                        changing  CT_STYL      = CT_STYL ).
***   Window Patch Area = Enable
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'WPWID'
                                  IF_STYLE     = ME->F_STYLE_ENABLED
                        changing  CT_STYL      = CT_STYL ).
***   Surface Weight = Enable
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'SURWT'
                                  IF_STYLE     = ME->F_STYLE_ENABLED
                        changing  CT_STYL      = CT_STYL ).
***   Purchase = Disable
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PURCH'
                                  IF_STYLE     = ME->F_STYLE_DISABLED
                        changing  CT_STYL      = CT_STYL ).
***   Material Description = Enable
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MATNR_DESCR'
                                  IF_STYLE     = ME->F_STYLE_ENABLED
                        changing  CT_STYL      = CT_STYL ).
***   Procurement to Stock = Enable
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'FPTOS'
                                  IF_STYLE     = ME->F_STYLE_ENABLED
                        changing  CT_STYL      = CT_STYL ).
    else.
      if LS_MS_WPATCH_VIS-FOVRR_WPLEN ne ABAP_UNDEFINED.
***     Window Patch Length = Enable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'WPLEN'
                                    IF_STYLE     = ME->F_STYLE_ENABLED
                          changing  CT_STYL      = CT_STYL ).
      else.
***     Window Patch Length = Disable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'WPLEN'
                                    IF_STYLE     = ME->F_STYLE_DISABLED
                          changing  CT_STYL      = CT_STYL ).
      endif.
      if LS_MS_WPATCH_VIS-FOVRR_WPWID ne ABAP_UNDEFINED.
***     Window Patch Length = Enable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'WPWID'
                                    IF_STYLE     = ME->F_STYLE_ENABLED
                          changing  CT_STYL      = CT_STYL ).
      else.
***     Window Patch Length = Disable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'WPWID'
                                    IF_STYLE     = ME->F_STYLE_DISABLED
                          changing  CT_STYL      = CT_STYL ).
      endif.
      if LS_MS_WPATCH_VIS-FOVRR_WPARE ne ABAP_UNDEFINED.
***     Window Patch Area = Enable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'WPARE'
                                    IF_STYLE     = ME->F_STYLE_ENABLED
                          changing  CT_STYL      = CT_STYL ).
      else.
***     Window Patch Area = Disable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'WPARE'
                                    IF_STYLE     = ME->F_STYLE_DISABLED
                          changing  CT_STYL      = CT_STYL ).
      endif.
***   Surface Weight = Disable
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'SURWT'
                                  IF_STYLE     = ME->F_STYLE_DISABLED
                        changing  CT_STYL      = CT_STYL ).
***   Purchase = Enable
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PURCH'
                                  IF_STYLE     = ME->F_STYLE_ENABLED
                        changing  CT_STYL      = CT_STYL ).
***   Material Description = Disable
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MATNR_DESCR'
                                  IF_STYLE     = ME->F_STYLE_DISABLED
                        changing  CT_STYL      = CT_STYL ).
***   Procurement to Stock = Disable
      ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'FPTOS'
                                  IF_STYLE     = ME->F_STYLE_DISABLED
                        changing  CT_STYL      = CT_STYL ).
    endif.
  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_GATE           = IR_GATE
                        IR_UIGATE         = IR_UIGATE
                        IF_TABNAME        = IF_TABNAME
                        IF_CONTAINER_NAME = IF_CONTAINER_NAME
                        IF_APPL_EVENTS    = IF_APPL_EVENTS ).

*** Define Provider Type for Button
    ME->F_IPRVT = ZCL_VCXI_PXSS_PR_MS_WPATCH=>C_IPRVT_PX_MS_WPATCH.

  endmethod.
  method GET_FIELDCAT.

    data: LF_LOCKED type /VCXI/CKX_LOCKED,
          LF_INUSE  type /VCXI/VKSR_INUSE,
          LF_COLPOS type I,
          LF_LIFNR  type ABAP_BOOL,
          LF_PRICE  type ABAP_BOOL.
    data: LS_MS_WPATCH_GRID type ZVCXI_PXSS_S_MS_WPATCH_GRID.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type          ANY,
                   <S_FCAT> type          LVC_S_FCAT.

***----------------------------------------------------------------------------
*** Get Standard Field Catalog
    RT_FCAT = SUPER->GET_FIELDCAT( ).

*** Get Lock and In-Use Status
    LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
    LF_INUSE  = ME->R_GATE->IS_INUSE( ).

*** Dereferencing of Data Reference
    assign ME->R_DATA->* to <T_DATA>.

    loop at <T_DATA> assigning <S_DATA>.
      move-corresponding <S_DATA> to LS_MS_WPATCH_GRID.
***   Find Purchased
      case LS_MS_WPATCH_GRID-PURCH.
        when ZCL_VCXI_XCSS_PR_MS=>C_PURCH_DIRECT_PRICE.
          LF_LIFNR = ABAP_TRUE.
          LF_PRICE = ABAP_TRUE.
        when ZCL_VCXI_XCSS_PR_MS=>C_PURCH_DIRECT.
          LF_LIFNR = ABAP_TRUE.
      endcase.
    endloop.

***----------------------------------------------------------------------------
*** Adjust Field Catalog
    loop at RT_FCAT assigning <S_FCAT>.
      case <S_FCAT>-FIELDNAME.
        when 'POSID' or 'GATKY' or 'R_GATE' or 'T_STYL'.
          move ABAP_TRUE    to <S_FCAT>-TECH.
          move 0            to <S_FCAT>-COL_POS.
        when 'WERKS'.
          move ABAP_TRUE    to <S_FCAT>-NO_OUT.

        when 'MATNR'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE  to <S_FCAT>-EDIT.
          endif.

        when 'PURCH'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE  to <S_FCAT>-EDIT.
          endif.

        when 'FPTOS'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE  to <S_FCAT>-NO_OUT.
          endif.

        when 'WPLEN' or 'WPWID' or 'WPARE'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE  to <S_FCAT>-EDIT.
          endif.

        when 'FOVRR_WPLEN' or 'FOVRR_WPWID' or 'FOVRR_WPARE'.
          move ABAP_TRUE  to <S_FCAT>-NO_OUT.

        when 'SURWT'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE  to <S_FCAT>-EDIT.
          endif.

        when 'NOSBN'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE  to <S_FCAT>-EDIT.
          endif.

        when 'GLUTN'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE  to <S_FCAT>-EDIT.
            move ABAP_TRUE  to <S_FCAT>-F4AVAILABL.
          endif.

        when 'FOVRR_GLUTD'.
          move ABAP_TRUE  to <S_FCAT>-NO_OUT.

        when 'LIFNR'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE  to <S_FCAT>-EDIT.
          endif.

        when 'PRICE' or 'PRICP'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE  to <S_FCAT>-EDIT.
          endif.

      endcase.

***----------------------------------------------------------------------------
***   Hide Field if not purchase
      if LF_LIFNR ne ABAP_TRUE.
        if <S_FCAT>-FIELDNAME eq 'LIFNR' or
           <S_FCAT>-FIELDNAME eq 'LIFNR_DESCR'.
          move ABAP_TRUE to <S_FCAT>-NO_OUT.
        endif.
      endif.
      if LF_PRICE ne ABAP_TRUE.
        if <S_FCAT>-FIELDNAME eq 'PRICE' or
           <S_FCAT>-FIELDNAME eq 'PRICE_CURR' or
           <S_FCAT>-FIELDNAME eq 'PRICP' or
           <S_FCAT>-FIELDNAME eq 'PRICP_UOM'.
          move ABAP_TRUE to <S_FCAT>-NO_OUT.
        endif.
      endif.

***----------------------------------------------------------------------------
***   Set Position for each Field
      move 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'MATNR'.           <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'MATNR_DESCR'.     <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'PURCH'.           <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'FPTOS'.           <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'WPLEN'.           <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'WPLEN_UOM'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'FOVRR_WPLEN'.     <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'WPWID'.           <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'WPWID_UOM'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'FOVRR_WPWID'.     <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'WPARE'.           <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'WPARE_UOM'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'SURWT'.           <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'SURWT_UOM'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'FOVRR_WPARE'.     <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'NOSBN'.           <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'GLUTN'.           <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'GLUTD'.           <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'FOVRR_GLUTD'.     <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'LIFNR'.           <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'LIFNR_DESCR'.     <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'PRICE'.           <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'PRICE_CURR'.      <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'PRICP'.           <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
      if <S_FCAT>-FIELDNAME eq 'PRICP_UOM'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
      add 1 to LF_COLPOS.
    endloop.

    sort RT_FCAT by COL_POS.

  endmethod.
  method GET_GRID_DATA.

    data: LS_LAYOUT type LVC_S_LAYO.
    data: LT_GATE type /VCXI/VKSR_TT_GATE.
    data: LR_GATE type ref to /VCXI/CL_VKSR_GATE.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type          ANY,
                   <T_STYL> type          LVC_T_STYL.

***--------------------------------------------------------------------------------------
*** Dereferencing of Data Reference
    assign IR_DATA->* to <T_DATA>.

*** Get Layout
    LS_LAYOUT = ME->GET_LAYOUT( ).

*** Get Gates of Provider
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ME->F_IPRVT
                                     IF_INUSE  = ABAP_TRUE
                                     IF_SORTED = ABAP_TRUE ).

***--------------------------------------------------------------------------------------
    loop at LT_GATE into LR_GATE.
***   Add Line with Gate Key Data
      append initial line to <T_DATA> assigning <S_DATA>.
      move-corresponding value ZVCXI_PXSS_S_MS_WPATCH_GRID( POSID  = LR_GATE->GET_POSID( )
                                                            GATKY  = LR_GATE->F_GATKY
                                                            R_GATE = LR_GATE
                                                          ) to <S_DATA>.

***   Get Visualization Data
      ME->GET_MS_WPATCH_VIS( exporting IR_GATE = LR_GATE
                             changing  CS_DATA = <S_DATA> ).

***   Set Style Details
      if LS_LAYOUT-STYLEFNAME is not initial.
        assign component LS_LAYOUT-STYLEFNAME of structure <S_DATA> to <T_STYL>.
        if SY-SUBRC eq 0.
          ME->CHANGE_STYLE_MS_WPATCH( exporting IR_GATE = LR_GATE
                                                IS_DATA = <S_DATA>
                                      changing  CT_STYL = <T_STYL> ).
***       Call DVC Servcie - Modify Style
          ZCL_VCXI_XCDR_SERVICE=>MODIFY_STYLE( exporting IR_GATE = LR_GATE
                                               changing  CT_STYL = <T_STYL> ).
        endif.
      endif.
    endloop.

  endmethod.
  method GET_MS_WPATCH_VIS.

    data: LS_MS_WPATCH_VIS type ZVCXI_PXSS_S_MS_WPATCH_GRID.
    data: LR_PR_MS_WPATCH  type ref to ZCL_VCXI_PXSS_PR_MS_WPATCH.


***--------------------------------------------------------------------------------------
*** Get PR Class
    try.
        move IR_GATE->R_CLSPR ?to LR_PR_MS_WPATCH.
      catch CX_SY_MOVE_CAST_ERROR.
    endtry.
    check LR_PR_MS_WPATCH is bound.

*** "Cast" generic structure to Vis Structure
    move-corresponding CS_DATA to LS_MS_WPATCH_VIS.

***--------------------------------------------------------------------------------------
*** Material
    LS_MS_WPATCH_VIS-MATNR       = LR_PR_MS_WPATCH->GET_MATNR( ).
    LS_MS_WPATCH_VIS-MATNR_DESCR = LR_PR_MS_WPATCH->GET_MATNR_DESCR( ).

*** Window Patch Dimensions
    LS_MS_WPATCH_VIS-S_WPLEN = LR_PR_MS_WPATCH->GET_WPLEN( importing EF_FOVRR = LS_MS_WPATCH_VIS-FOVRR_WPLEN ).
    LS_MS_WPATCH_VIS-S_WPWID = LR_PR_MS_WPATCH->GET_WPWID( importing EF_FOVRR = LS_MS_WPATCH_VIS-FOVRR_WPWID ).

*** Window Patch Area
    LS_MS_WPATCH_VIS-S_WPARE = LR_PR_MS_WPATCH->GET_WPARE( importing EF_FOVRR = LS_MS_WPATCH_VIS-FOVRR_WPARE ).

*** Surface Weight
    LS_MS_WPATCH_VIS-S_SURWT = LR_PR_MS_WPATCH->GET_SURWT( ).

*** Number of Soft Bends
    LS_MS_WPATCH_VIS-NOSBN = LR_PR_MS_WPATCH->GET_NOSBN( ).

*** Glue Tool
    LS_MS_WPATCH_VIS-GLUTN = LR_PR_MS_WPATCH->GET_GLUTN( ).
    LS_MS_WPATCH_VIS-GLUTD = LR_PR_MS_WPATCH->GET_GLUTD( ).

***--------------------------------------------------------------------------------------
*** Procurement Option
    LS_MS_WPATCH_VIS-PURCH = LR_PR_MS_WPATCH->GET_PURCH( ).

*** Procurement To Stock
    LS_MS_WPATCH_VIS-FPTOS = LR_PR_MS_WPATCH->GET_FPTOS( ).

*** Vendor
    LS_MS_WPATCH_VIS-LIFNR       = LR_PR_MS_WPATCH->GET_LIFNR( ).
    LS_MS_WPATCH_VIS-LIFNR_DESCR = LR_PR_MS_WPATCH->GET_LIFNR_DESCR( ).

*** Price and Price Per
    LS_MS_WPATCH_VIS-S_PRICE = LR_PR_MS_WPATCH->GET_PRICE( ).
    LS_MS_WPATCH_VIS-S_PRICP = LR_PR_MS_WPATCH->GET_PRICP( ).

***--------------------------------------------------------------------------------------
*** "Cast" Vis Structure back to generic structure
    move-corresponding LS_MS_WPATCH_VIS to CS_DATA.

  endmethod.
  method HANDLE_DATA_CHANGED_FINISHED.

    data: LS_MS_WPATCH_GRID type ZVCXI_PXSS_S_MS_WPATCH_GRID.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type          ANY.

    check E_MODIFIED eq ABAP_TRUE.

    try.
        SUPER->HANDLE_DATA_CHANGED_FINISHED( E_MODIFIED    = E_MODIFIED
                                             ET_GOOD_CELLS = ET_GOOD_CELLS ).

***     Dereferencing of Data Reference
        assign ME->R_DATA->* to <T_DATA>.

        loop at <T_DATA> assigning <S_DATA>.
          move-corresponding <S_DATA> to LS_MS_WPATCH_GRID.

          check LS_MS_WPATCH_GRID-R_GATE is bound and
                LS_MS_WPATCH_GRID-R_GATE->F_FREED is initial.

***       Set Values to PR Class
          ME->SET_MS_WPATCH_VIS( IR_GATE = LS_MS_WPATCH_GRID-R_GATE
                                 IS_DATA = <S_DATA> ).

        endloop.

        ME->R_GATE->RAISE_EVENTS( ).

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

*** Request Refresh for ALV
    move ABAP_TRUE to ME->F_REFRESH_REQ.

  endmethod.
  method SET_MS_WPATCH_VIS.

    data: LS_MS_WPATCH_VIS_OLD type ZVCXI_PXSS_S_MS_WPATCH_VIS,
          LS_MS_WPATCH_VIS_NEW type ZVCXI_PXSS_S_MS_WPATCH_VIS.
    data: LR_PR_MS_WPATCH  type ref to ZCL_VCXI_PXSS_PR_MS_WPATCH.


***--------------------------------------------------------------------------------------
*** Get PR Class
    check IR_GATE->R_CLSPR is instance of ZCL_VCXI_PXSS_PR_MS_WPATCH.
    LR_PR_MS_WPATCH ?= IR_GATE->R_CLSPR.
    check LR_PR_MS_WPATCH is bound.

*** Get old Values
    ME->GET_MS_WPATCH_VIS( exporting IR_GATE = IR_GATE
                          changing  CS_DATA = LS_MS_WPATCH_VIS_OLD ).
*** Get new Values
    move-corresponding IS_DATA to LS_MS_WPATCH_VIS_NEW.

***--------------------------------------------------------------------------------------
*** Material
    if LS_MS_WPATCH_VIS_NEW-MATNR ne LS_MS_WPATCH_VIS_OLD-MATNR.
      LR_PR_MS_WPATCH->SET_MATNR( IF_MATNR = LS_MS_WPATCH_VIS_NEW-MATNR
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Material Description
    if LS_MS_WPATCH_VIS_NEW-MATNR_DESCR ne LS_MS_WPATCH_VIS_OLD-MATNR_DESCR.
      LR_PR_MS_WPATCH->SET_MATNR_DESCR( IF_DESCR = LS_MS_WPATCH_VIS_NEW-MATNR_DESCR
                                        IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Window Patch Dimensions
    if LS_MS_WPATCH_VIS_NEW-S_WPLEN ne LS_MS_WPATCH_VIS_OLD-S_WPLEN.
      LR_PR_MS_WPATCH->SET_WPLEN( IS_WPLEN = LS_MS_WPATCH_VIS_NEW-S_WPLEN
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

    if LS_MS_WPATCH_VIS_NEW-S_WPWID ne LS_MS_WPATCH_VIS_OLD-S_WPWID.
      LR_PR_MS_WPATCH->SET_WPWID( IS_WPWID = LS_MS_WPATCH_VIS_NEW-S_WPWID
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Window Patch Area
    if LS_MS_WPATCH_VIS_NEW-S_WPARE ne LS_MS_WPATCH_VIS_OLD-S_WPARE.
      LR_PR_MS_WPATCH->SET_WPARE( IS_WPARE = LS_MS_WPATCH_VIS_NEW-S_WPARE
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Surface Weight
    if LS_MS_WPATCH_VIS_NEW-S_SURWT ne LS_MS_WPATCH_VIS_OLD-S_SURWT.
      LR_PR_MS_WPATCH->SET_SURWT( IS_SURWT = LS_MS_WPATCH_VIS_NEW-S_SURWT
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Number of Soft Bends
    if LS_MS_WPATCH_VIS_NEW-NOSBN ne LS_MS_WPATCH_VIS_OLD-NOSBN.
      LR_PR_MS_WPATCH->SET_NOSBN( IF_NOSBN = LS_MS_WPATCH_VIS_NEW-NOSBN
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Glue Tool
    if LS_MS_WPATCH_VIS_NEW-GLUTN ne LS_MS_WPATCH_VIS_OLD-GLUTN.
      LR_PR_MS_WPATCH->SET_GLUTN( IF_GLUTN = LS_MS_WPATCH_VIS_NEW-GLUTN
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Procurement Option
    if LS_MS_WPATCH_VIS_NEW-PURCH ne LS_MS_WPATCH_VIS_OLD-PURCH.
      LR_PR_MS_WPATCH->SET_PURCH( IF_PURCH = LS_MS_WPATCH_VIS_NEW-PURCH
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Procurement To Stock
    if LS_MS_WPATCH_VIS_NEW-FPTOS ne LS_MS_WPATCH_VIS_OLD-FPTOS.
      LR_PR_MS_WPATCH->SET_FPTOS( IF_FPTOS = LS_MS_WPATCH_VIS_NEW-FPTOS
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Vendor
    if LS_MS_WPATCH_VIS_NEW-LIFNR ne LS_MS_WPATCH_VIS_OLD-LIFNR.
      LR_PR_MS_WPATCH->SET_LIFNR( IF_LIFNR = LS_MS_WPATCH_VIS_NEW-LIFNR
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Price
    if LS_MS_WPATCH_VIS_NEW-PRICE ne LS_MS_WPATCH_VIS_OLD-PRICE.
      LR_PR_MS_WPATCH->SET_PRICE( IS_PRICE = LS_MS_WPATCH_VIS_NEW-S_PRICE
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Price Per
    if LS_MS_WPATCH_VIS_NEW-PRICP ne LS_MS_WPATCH_VIS_OLD-PRICP.
      LR_PR_MS_WPATCH->SET_PRICP( IS_PRICP = LS_MS_WPATCH_VIS_NEW-S_PRICP
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
