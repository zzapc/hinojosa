
class ZCL_VCXI_P1SS_PR_PREPR definition
  public
  inheriting from /VCXI/CL_VKSR_CLSPR
  create public .

public section.

  interfaces ZIF_VCXI_XCS_NGREP .

  constants C_IPRVT_P1_PREPR type /VCXI/VKSC_IPRVT value 'ZVCXI_P1SS_PREPR' ##NO_TEXT.

  methods ADD_PRVDR_PRPLIN
    importing
      !IF_BPAID type ZVCXI_PCS_BPAID
    exporting
      !ER_GATE type ref to /VCXI/CL_VKSR_GATE
    returning
      value(RR_PR_PRPLIN) type ref to ZCL_VCXI_P1SS_PR_PRPLIN
    raising
      /VCXI/CX_CKX .
  methods GET_FLUTE
    exporting
      !ET_FSTRUCT type ZVCXI_PCSSC_TT_FSTRUCT
    returning
      value(RF_FLUTE) type ZVCXI_PCS_FLUTE
    raising
      /VCXI/CX_CKX .
protected section.

  methods GET_PRVDR_BOARD
    importing
      !IF_MBOAR type ZVCXI_PCSS_MBOAR
    exporting
      !ER_GATE type ref to /VCXI/CL_VKSR_GATE
    returning
      value(RR_PR_BOARD) type ref to ZCL_VCXI_P1SS_PR_BOARD
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_P1SS_PR_PREPR definition
class ZCL_VCXI_P1SS_PR_PREPR implementation.
  method ADD_PRVDR_PRPLIN.

*** Add Preprint Liner Element
    ER_GATE = ME->R_GATE->CREATE_ELEMENT( IF_IPRVT = ZCL_VCXI_P1SS_PR_PRPLIN=>C_IPRVT_P1_PRPLIN ).
    ER_GATE->SET_INUSE( IF_INUSE = ABAP_TRUE ).
    RR_PR_PRPLIN ?= ER_GATE->R_CLSPR.

*** Set Board Paper ID
    RR_PR_PRPLIN->SET_BPAID( IF_BPAID = IF_BPAID ).

*** Default Web Width by Corrugation Width
    data(LS_COWID) = RR_PR_PRPLIN->GET_COWID( ).
    RR_PR_PRPLIN->SET_WEBWI( IS_WEBWI = value #( WEBWI     = LS_COWID-COWID
                                                 WEBWI_UOM = LS_COWID-COWID_UOM ) ).

  endmethod.
  method GET_FLUTE.

    data: LR_GATE_BOARD type ref to /VCXI/CL_VKSR_GATE.


*** Always use Production Board values
    data(LR_PR_BOARD) = ME->GET_PRVDR_BOARD( exporting IF_MBOAR = ZCL_VCXI_P1SS_PR_BOARD=>F_MBOAR_P
                                             importing ER_GATE  = LR_GATE_BOARD ).
    check LR_PR_BOARD is bound.

*** Get Flute
    if RF_FLUTE is requested.
      RF_FLUTE = ME->GET_FLUTE( ).
    endif.

*** Get Flute Structure
    if ET_FSTRUCT is requested.
      LR_PR_BOARD->GET_BOARD( importing ER_CUST_BOARD = data(LR_CUST_BOARD) ).
      if LR_CUST_BOARD is bound.
        ET_FSTRUCT = LR_CUST_BOARD->GET_FSTRUCT( ).

***     Turn existing Papers into "obligatory" to as optional as well as substitued papers can't be preprinted
        loop at LR_GATE_BOARD->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_P1SS_PR_MS_PAPER=>C_IPRVT_P1_MS_PAPER
                                          IF_INUSE  = ABAP_TRUE ) into data(LR_GATE_PAPER).
          data(LF_BPAID) = cast ZCL_VCXI_P1SS_PR_MS_PAPER( LR_GATE_PAPER->R_CLSPR )->GET_BPAID( ).
          check LINE_EXISTS( ET_FSTRUCT[ BPAID = LF_BPAID ] ).

          if cast ZCL_VCXI_P1SS_PR_MS_PAPER( LR_GATE_PAPER->R_CLSPR )->GET_SUBPA( ) eq ABAP_TRUE.
***         Turn substitued papers into optional as they can't be preprinted
            ET_FSTRUCT[ BPAID = LF_BPAID ]-OBLIG = ABAP_FALSE.
          else.
***         Turn existing Papers into "obligatory" to as optional papers can't be preprinted
            ET_FSTRUCT[ BPAID = LF_BPAID ]-OBLIG = ABAP_TRUE.
          endif.
        endloop.
      endif.
    endif.

  endmethod.
  method GET_PRVDR_BOARD.

    data: LT_GATE type /VCXI/VKSR_TT_GATE.
    data: LR_GATE     type ref to /VCXI/CL_VKSR_GATE,
          LR_PR_BOARD type ref to ZCL_VCXI_P1SS_PR_BOARD.


    clear: RR_PR_BOARD, ER_GATE.

***--------------------------------------------------------------------------------------
*** Get all Boards
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_P1SS_PR_BOARD=>C_IPRVT_BOARD
                                     IF_INUSE = ABAP_TRUE ).

***--------------------------------------------------------------------------------------
*** Get Boards References
    loop at LT_GATE into LR_GATE.
      try.
          move LR_GATE->R_CLSPR ?to LR_PR_BOARD.
          check LR_PR_BOARD is bound.

          check LR_PR_BOARD->GET_MBOAR( ) eq IF_MBOAR.
          move LR_PR_BOARD to RR_PR_BOARD.
          move LR_GATE     to ER_GATE.
          exit.

        catch CX_SY_MOVE_CAST_ERROR.
          clear: LR_PR_BOARD.
      endtry.
    endloop.

  endmethod.
  method ZIF_VCXI_XCS_NGREP~FILL.

***------------------------------------------------------------------------------------------------
*** Find Provider for Preprint Liner
    loop at ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_P1SS_PR_PRPLIN=>C_IPRVT_P1_PRPLIN
                                   IF_INUSE = ABAP_TRUE ) into data(LR_GATE).
      if LR_GATE->R_CLSPR is instance of ZIF_VCXI_XCS_NGREP.
        cast ZIF_VCXI_XCS_NGREP( LR_GATE->R_CLSPR )->FILL( exporting IF_GROUP_KEY     = IF_GROUP_KEY
                                                           changing  CR_NETGRAPH_JSON = CR_NETGRAPH_JSON ).
      endif.
    endloop.

  endmethod.
