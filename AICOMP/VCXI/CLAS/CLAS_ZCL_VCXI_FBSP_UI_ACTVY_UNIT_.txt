
class ZCL_VCXI_FBSP_UI_ACTVY_UNIT definition
  public
  inheriting from /VCXI/CL_VKSRU_CLSUI
  create public .

public section.

  data F_CURSOR type SCRFNAME read-only .
  data S_ACTVY_UNIT_VIS type ZVCXI_FBSP_S_ACTVY_UNIT_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_VIS
    importing
      !IS_VIS type ANY
    raising
      /VCXI/CX_CKX .
  methods SET_CURSOR
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  data R_GRID_MC_PACKMAT type ref to ZCL_VCXI_XCSP_GRID_MC_PACKMAT .
  data R_PR_ACTVY_UNIT type ref to ZCL_VCXI_FBSP_PR_ACTVY_UNIT .

  methods INIT_VIS
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
private section.
endclass. "ZCL_VCXI_FBSP_UI_ACTVY_UNIT definition
class ZCL_VCXI_FBSP_UI_ACTVY_UNIT implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_FBSP_UI_ACTVY_UNIT_BIND'
      exporting
        IR_UI_ACTVY_UNIT = ME
      importing
        ES_DYNP          = ME->S_DYNP.

*** Create Grid for MC Packaging Material
    create object ME->R_GRID_MC_PACKMAT
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_MC_PACKMAT'.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

*** Assign the PR Class to the UI Class
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_ACTVY_UNIT.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

    if ME->R_GRID_MC_PACKMAT is bound.
      ME->R_GRID_MC_PACKMAT->DO_FREE( ).
      clear ME->R_GRID_MC_PACKMAT.
    endif.

  endmethod.
  method DO_MODIFY_SCREEN.

    data: LF_LOCKED type        /VCXI/CKX_LOCKED,
          LF_INUSE  type        /VCXI/VKSR_INUSE.
    data: LR_CX_CKX type ref to /VCXI/CX_CKX.

    SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                             IF_DYNNR = IF_DYNNR ).

    try.

        if IF_REPID eq ME->S_DYNP-REPID and
           IF_DYNNR eq ME->S_DYNP-DYNNR.

          LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
          LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***       Modify Scree using DynamicVC
          ZCL_VCXI_XCDR_SERVICE=>MODIFY_SCREEN( IR_GATE = ME->R_GATE ).

          loop at screen.
            if LF_LOCKED ne ABAP_TRUE or
               LF_INUSE  ne ABAP_TRUE.
              move 0 to SCREEN-INPUT.
            endif.
            modify screen.

***         Set Cursor
            if SCREEN-NAME eq ME->F_CURSOR.
              set cursor field ME->F_CURSOR.
            endif.
          endloop.
        endif.

      catch /VCXI/CX_CKX into LR_CX_CKX.
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_PBO.

    ME->INIT_VIS( ).

    if ME->R_GRID_MC_PACKMAT is bound.
      ME->R_GRID_MC_PACKMAT->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_MC_PACKMAT->REFRESH_GRID( ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_ACTVY_UNIT_VIS type ZVCXI_FBSP_S_ACTVY_UNIT_VIS.


    check ME->R_PR_ACTVY_UNIT is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_ACTVY_UNIT_VIS.

***-------------------------------------------------------------------------------------
*** Set Number of Ups
    if ME->S_ACTVY_UNIT_VIS-NOUPS ne LS_ACTVY_UNIT_VIS-NOUPS.
      ME->R_PR_ACTVY_UNIT->SET_NOUPS( IF_NOUPS = LS_ACTVY_UNIT_VIS-NOUPS
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Shipping Unit
    if ME->S_ACTVY_UNIT_VIS-SHUNT ne LS_ACTVY_UNIT_VIS-SHUNT.
      ME->R_PR_ACTVY_UNIT->SET_SHUNT( IF_SHUNT = LS_ACTVY_UNIT_VIS-SHUNT
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Previous Quantity
    if ME->S_ACTVY_UNIT_VIS-PRQTY ne LS_ACTVY_UNIT_VIS-PRQTY.
      ME->R_PR_ACTVY_UNIT->SET_PRQTY( IS_PRQTY = LS_ACTVY_UNIT_VIS-S_PRQTY
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    if ME->R_GRID_MC_PACKMAT is bound.
      ME->R_GRID_MC_PACKMAT->CHECK_CHANGED_DATA( ).
    endif.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_ACTVY_UNIT.

  endmethod.
  method INIT_VIS.

    free: ME->S_ACTVY_UNIT_VIS.

*** Get Number of Ups
    ME->S_ACTVY_UNIT_VIS-NOUPS = ME->R_PR_ACTVY_UNIT->GET_NOUPS( ).

*** Get Shipping Unit and Description
    ME->S_ACTVY_UNIT_VIS-SHUNT = ME->R_PR_ACTVY_UNIT->GET_SHUNT( importing EF_DESCR = ME->S_ACTVY_UNIT_VIS-SHUNT_DESCR ).

*** Get Previous Quantity
    ME->S_ACTVY_UNIT_VIS-S_PRQTY = ME->R_PR_ACTVY_UNIT->GET_PRQTY( ).

  endmethod.
  method SET_CURSOR.

    data:  LF_CURSOR type SCRFNAME.

    get cursor field LF_CURSOR.
    if LF_CURSOR is not initial.
      move LF_CURSOR to ME->F_CURSOR.
    endif.

  endmethod.
