
class ZCL_VCXI_FFSP_PR_ADDON_TREAT definition
  public
  inheriting from ZCL_VCXI_XCSP_PR_ADDON
  create public .

public section.

  constants C_IAATY_TTREA type /VCXI/VSMC_IAATY value 'FF_AA_TTREA' ##NO_TEXT.
  constants C_PARID_TTREA type /VCXI/VKS_PARID value 'ZVCXI_FF_TTREA' ##NO_TEXT.

  methods GET_TTREA
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
      !EF_TTREA type ZVCXI_FFS_TTREA
    raising
      /VCXI/CX_CKX .
  methods SET_TTREA
    importing
      !IF_TTREA type ZVCXI_FFS_TTREA
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
protected section.

  constants C_IEVNT_O_TTREA_CHANGED type /VCXI/VKSC_IEVNT value 'TTREA_CHANGED' ##NO_TEXT.

  methods GET_RTE_SAVAL
    redefinition .
private section.
endclass. "ZCL_VCXI_FFSP_PR_ADDON_TREAT definition
class ZCL_VCXI_FFSP_PR_ADDON_TREAT implementation.
  method GET_RTE_SAVAL.


    data: LF_GAATY type        /VCXI/VSMC_GAATY,
          LF_TTREA type        ZVCXI_FFS_TTREA.
    field-symbols: <S_RTE_SAVAL> type        /VCXI/VSMR_S_RTE_SAVAL,
                   <S_AVVAL>     type        /VCXI/VSMR_S_RTE_RANGE_AVVAL.

*** Get Super
    RT_RTE_SAVAL = SUPER->GET_RTE_SAVAL( ).

    check ME->R_RTE_CADDON          is bound and
          ME->R_RTE_CADDON->R_ATYPE is bound.

***--------------------------------------------------------------------------------------
*** Aspect - Treatment Type
    try.
        ME->GET_TTREA( importing EF_TTREA = LF_TTREA ).

        LF_GAATY = ME->R_RTE_CADDON->R_ATYPE->GET_GAATY_BY_IAATY( IF_IAATY = C_IAATY_TTREA
                                                                  IF_MAATY = /VCXI/CL_VSMC_ATYPE=>C_MAATY_VALUE ).
        append initial line to RT_RTE_SAVAL assigning <S_RTE_SAVAL>.
        move LF_GAATY to <S_RTE_SAVAL>-GAATY.

        append initial line to <S_RTE_SAVAL>-I_AVVAL assigning <S_AVVAL>.
        move 'I'              to <S_AVVAL>-SIGN.
        move 'EQ'             to <S_AVVAL>-OPTION.
        move LF_TTREA         to <S_AVVAL>-LOW.

      catch /VCXI/CX_CKX.
        return.
    endtry.

  endmethod.
  method GET_TTREA.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TTREA ).
    move LS_PARAM-ATWRT to EF_TTREA.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method SET_TTREA.

    data: LF_CHANGED type ABAP_BOOL.
    data: LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TTREA ).

    move IF_TTREA to LS_PARAM-ATWRT.
    move IF_SETBY to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_TTREA_CHANGED ).
    endif.

  endmethod.
