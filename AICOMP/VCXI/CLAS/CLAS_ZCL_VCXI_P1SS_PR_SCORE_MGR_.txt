
class ZCL_VCXI_P1SS_PR_SCORE_MGR definition
  public
  inheriting from ZCL_VCXI_PXSS_PR_SCORE_MGR
  create public .

public section.

  methods GET_FLUTE
    returning
      value(RF_FLUTE) type ZVCXI_PCS_FLUTE
    raising
      /VCXI/CX_CKX .
  methods GET_FLUTO
    returning
      value(RF_FLUTO) type ZVCXI_PCSS_FLUTO
    raising
      /VCXI/CX_CKX .

  methods GET_PRSTY
    redefinition .
  methods GET_PRSTY_ELEMENTS
    redefinition .
protected section.

  data R_CUST_FEFCO type ref to ZCL_VCXI_PCSS_CUST_FEFCO .

  methods GET_BOXPA
    redefinition .
  methods GET_CUST_SCORES
    redefinition .
  methods GET_JOIPO
    redefinition .
  methods HANDLE_INIT_CUST_PRSTY
    redefinition .
private section.
endclass. "ZCL_VCXI_P1SS_PR_SCORE_MGR definition
class ZCL_VCXI_P1SS_PR_SCORE_MGR implementation.
  method GET_BOXPA.

    case type of ME->R_GATE->GET_PARENT( )->R_CLSPR.
***   Corrugated FG
      when type ZCL_VCXI_P1SS_PR_CFG into data(LR_PR_CFG).
        RF_BOXPA = LR_PR_CFG->GET_BOXPA( ).

***   Corrugated Part
      when type ZCL_VCXI_P1SS_PR_CRPART into data(LR_PR_CRPART).
        RF_BOXPA = LR_PR_CRPART->GET_BOXPA( ).

***   Corrugated Procurement Details
      when type ZCL_VCXI_P1SS_PR_CRPRDTL into data(LR_PR_CRPRDTL).
        RF_BOXPA = LR_PR_CRPRDTL->GET_BOXPA( ).

***   Corrugated Configured Packaging Material
      when type ZCL_VCXI_P1SS_PR_CRCPMCNF into data(LR_PR_CRCPMCNF).
        RF_BOXPA = LR_PR_CRCPMCNF->GET_BOXPA( ).

    endcase.

  endmethod.
  method GET_CUST_SCORES.

    data: LF_PANOR type ZVCXI_P0SS_PANOR,
          LF_WERKS type WERKS_D,
          LF_PTYPE type ZVCXI_P0S_PTYPE,
          LF_JOIPO type ZVCXI_P0S_JOIPO,
          LF_BOXPA type ZVCXI_P0SS_BOXPA,
          LF_FLUTE type ZVCXI_PCS_FLUTE,
          LF_FLUTO type ZVCXI_PCSS_FLUTO,
          LF_DCONF type DATS.
    data: LT_ELEMENT type ZVCXI_P0SS_TT_ELEMENT,
          LT_SCORE   type ZVCXI_P0SS_TT_SCORE.
    field-symbols: <S_SCORE> type ZVCXI_PXSS_S_SCORE.


    if ME->R_CUST_PRSTY is not bound.
      move ABAP_TRUE to EF_CHANGED.
      exit.
    endif.

***----------------------------------------------------------------------------
*** Collect generall Data
    LF_BOXPA = ME->GET_BOXPA( ).
    LF_PANOR = ME->GET_PANOR( ).
    LF_WERKS = ME->GET_WERKS( ).

*** Joint Position and Product Type
    LF_JOIPO = ME->GET_JOIPO( ).
    LF_PTYPE = ME->GET_PTYPE( ).
    LF_DCONF = ME->GET_DCONF( ).

*** Collect Elements
    LT_ELEMENT = ME->GET_PRSTY_ELEMENTS( ).

*** Get Flute
    move ME->GET_FLUTE( ) to LF_FLUTE.
    move ME->GET_FLUTO( ) to LF_FLUTO.

***----------------------------------------------------------------------------
*** Request Scores
    ME->R_CUST_FEFCO->GET_FEFCO_SCORES( exporting IF_BOXPA   = LF_BOXPA
                                                  IF_PANOR   = LF_PANOR
                                                  IF_WERKS   = LF_WERKS
                                                  IF_PTYPE   = LF_PTYPE
                                                  IF_JOIPO   = LF_JOIPO
                                                  IF_FLUTE   = LF_FLUTE
                                                  IF_FLUTO   = LF_FLUTO
                                                  IF_DATE    = LF_DCONF
                                                  IT_ELEMENT = LT_ELEMENT
                                        importing ET_SCORE   = LT_SCORE
                                                  EF_CHANGED = EF_CHANGED ).

    move-corresponding LT_SCORE to ET_SCORE.

*** Get Slot Indicators of each Score
    loop at ET_SCORE assigning <S_SCORE>.
      <S_SCORE>-FSLIN = ME->R_CUST_PRSTY->GET_FSLIN( IF_CSCOR = <S_SCORE>-CSCOR
                                                     IF_NSCOR = <S_SCORE>-NSCOR ).
    endloop.

***----------------------------------------------------------------------------
*** Identify change
    if ET_SCORE ne ME->T_SCORE.
      EF_CHANGED = ABAP_TRUE.
    endif.

  endmethod.
  method GET_FLUTE.

    case type of ME->R_GATE->GET_PARENT( )->R_CLSPR.
***   Corrugated FG
      when type ZCL_VCXI_P1SS_PR_CFG into data(LR_PR_CFG).
        RF_FLUTE = LR_PR_CFG->GET_FLUTE( ).

***   Corrugated Part
      when type ZCL_VCXI_P1SS_PR_CRPART into data(LR_PR_CRPART).
        RF_FLUTE = LR_PR_CRPART->GET_FLUTE( ).

***   Corrugated Procurement Details
      when type ZCL_VCXI_P1SS_PR_CRPRDTL into data(LR_PR_CRPRDTL).
        RF_FLUTE = LR_PR_CRPRDTL->GET_FLUTE( ).

***   Corrugated Configured Packaging Material
      when type ZCL_VCXI_P1SS_PR_CRCPMCNF into data(LR_PR_CRCPMCNF).
        RF_FLUTE = LR_PR_CRCPMCNF->GET_FLUTE( ).

    endcase.

  endmethod.
  method GET_FLUTO.

    case type of ME->R_GATE->GET_PARENT( )->R_CLSPR.
***   Corrugated FG
      when type ZCL_VCXI_P1SS_PR_CFG into data(LR_PR_CFG).
        RF_FLUTO = LR_PR_CFG->GET_FLUTO( ).

***   Corrugated Part
      when type ZCL_VCXI_P1SS_PR_CRPART into data(LR_PR_CRPART).
        RF_FLUTO = LR_PR_CRPART->GET_FLUTO( ).

***   Corrugated Procurement Details
      when type ZCL_VCXI_P1SS_PR_CRPRDTL into data(LR_PR_CRPRDTL).
        RF_FLUTO = LR_PR_CRPRDTL->GET_FLUTO( ).

***   Corrugated Configured Packaging Material
      when type ZCL_VCXI_P1SS_PR_CRCPMCNF into data(LR_PR_CRCPMCNF).
        RF_FLUTO = LR_PR_CRCPMCNF->GET_FLUTO( ).

    endcase.

  endmethod.
  method GET_JOIPO.

    case type of ME->R_GATE->GET_PARENT( )->R_CLSPR.
***   Corrugated FG
      when type ZCL_VCXI_P1SS_PR_CFG into data(LR_PR_CFG).
        RF_JOIPO = LR_PR_CFG->GET_JOIPO( ).

***   Corrugated Part
      when type ZCL_VCXI_P1SS_PR_CRPART into data(LR_PR_CRPART).
        RF_JOIPO = LR_PR_CRPART->GET_JOIPO( ).

***   Corrugated Procurement Details
      when type ZCL_VCXI_P1SS_PR_CRPRDTL into data(LR_PR_CRPRDTL).
        RF_JOIPO = LR_PR_CRPRDTL->GET_JOIPO( ).

***   Corrugated Configured Packaging Material
      when type ZCL_VCXI_P1SS_PR_CRCPMCNF into data(LR_PR_CRCPMCNF).
        RF_JOIPO = LR_PR_CRCPMCNF->GET_JOIPO( ).

    endcase.

  endmethod.
  method GET_PRSTY.

    case type of ME->R_GATE->GET_PARENT( )->R_CLSPR.
***   Corrugated FG
      when type ZCL_VCXI_P1SS_PR_CFG into data(LR_PR_CFG).
        RF_PRSTY = LR_PR_CFG->GET_FEFCO( importing ER_CUST_PRSTY = ER_CUST_PRSTY ).

***   Corrugated Part
      when type ZCL_VCXI_P1SS_PR_CRPART into data(LR_PR_CRPART).
        RF_PRSTY = LR_PR_CRPART->GET_FEFCO( importing ER_CUST_PRSTY = ER_CUST_PRSTY ).

***   Corrugated Procurement Details
      when type ZCL_VCXI_P1SS_PR_CRPRDTL into data(LR_PR_CRPRDTL).
        RF_PRSTY = LR_PR_CRPRDTL->GET_FEFCO( importing ER_CUST_PRSTY = ER_CUST_PRSTY ).

***   Corrugated Configured Packaging Material
      when type ZCL_VCXI_P1SS_PR_CRCPMCNF into data(LR_PR_CRCPMCNF).
        RF_PRSTY = LR_PR_CRCPMCNF->GET_FEFCO( importing ER_CUST_PRSTY = ER_CUST_PRSTY ).

    endcase.

  endmethod.
  method GET_PRSTY_ELEMENTS.

    case type of ME->R_GATE->GET_PARENT( )->R_CLSPR.
***   Corrugated FG
      when type ZCL_VCXI_P1SS_PR_CFG into data(LR_PR_CFG).
        RT_ELEMENT = LR_PR_CFG->GET_PRSTY_ELEMENTS( ).

***   Corrugated Part
      when type ZCL_VCXI_P1SS_PR_CRPART into data(LR_PR_CRPART).
        RT_ELEMENT = LR_PR_CRPART->GET_PRSTY_ELEMENTS( ).

***   Corrugated Procurement Details
      when type ZCL_VCXI_P1SS_PR_CRPRDTL into data(LR_PR_CRPRDTL).
        RT_ELEMENT = LR_PR_CRPRDTL->GET_PRSTY_ELEMENTS( ).

***   Corrugated Configured Packaging Material
      when type ZCL_VCXI_P1SS_PR_CRCPMCNF into data(LR_PR_CRCPMCNF).
        RT_ELEMENT = LR_PR_CRCPMCNF->GET_PRSTY_ELEMENTS( ).

    endcase.

  endmethod.
  method HANDLE_INIT_CUST_PRSTY.

    data: LF_FEFCO type        ZVCXI_PCS_FEFCO,
          LF_OWNER type        /VCXI/CKX_STRING,
          LF_SYSOM type        /VCXI/VKS_SYSOM.

    LF_FEFCO = ME->GET_PRSTY( ).

    move ME->R_GATE->F_GATKY to LF_OWNER.
    ME->R_GATE->GET_CONFIG_INFO( importing EF_SYSOM = LF_SYSOM ).

*** Reload Scores from scratch on change of Product Style with automatic calculation logic (reset defaults)
    if ME->R_CUST_PRSTY is bound and
       ME->R_CUST_PRSTY->F_PRSTY ne LF_FEFCO and
       ZCL_VCXI_P0SS_CUST_PRSTY=>GET_SSCOR_CALC( IF_SSCOR = ME->GET_SSCOR( ) ) eq '1'.  "Automatic
      ME->DEL_ALL_SCORES( ).
      clear: ME->T_SCORE.
    endif.

*** Check if Customizing is the right one
    if ME->R_CUST_PRSTY is not bound or
       ME->R_CUST_PRSTY->F_PRSTY ne LF_FEFCO or
       ME->R_CUST_PRSTY->F_OWNER ne LF_OWNER or
       ME->R_CUST_PRSTY->F_SYSOM ne LF_SYSOM.

      free: ME->R_CUST_PRSTY, ME->R_CUST_FEFCO.

      if LF_FEFCO is not initial.

        try.

            ME->R_CUST_FEFCO = ZCL_VCXI_PCSS_CUST_FEFCO=>GET_FEFCO_INSTANCE( IF_FEFCO = LF_FEFCO
                                                                             IF_OWNER = LF_OWNER
                                                                             IF_SYSOM = LF_SYSOM ).

            move ME->R_CUST_FEFCO to ME->R_CUST_PRSTY.

          catch /VCXI/CX_CKX.
            free: ME->R_CUST_PRSTY, ME->R_CUST_FEFCO.
        endtry.
      endif.
    endif.

  endmethod.
