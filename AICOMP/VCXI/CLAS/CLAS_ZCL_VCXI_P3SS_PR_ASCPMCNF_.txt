
class ZCL_VCXI_P3SS_PR_ASCPMCNF definition
  public
  inheriting from ZCL_VCXI_PXSS_PR_XCPMCNF
  create public .

public section.

  interfaces ZIF_VCXI_P3SS_COMM_PREV .

  constants C_IPRVT_AFG type /VCXI/VKSC_IPRVT value 'ZVCXI_P3SS_AFG' ##NO_TEXT.
  constants C_PARID_PROHE type /VCXI/VKS_PARID value 'ZVCXI_P0_PROHE' ##NO_TEXT.
  constants C_PARID_PROLE type /VCXI/VKS_PARID value 'ZVCXI_P0_PROLE' ##NO_TEXT.
  constants C_PARID_PROWI type /VCXI/VKS_PARID value 'ZVCXI_P0_PROWI' ##NO_TEXT.

  methods GET_ASBPX
    exporting
      !EF_ASBPX type ZVCXI_P3S_ASBPX
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RR_BL_ASBPROP) type ref to ZCL_VCXI_P3SS_BL_ASBPROP
    raising
      /VCXI/CX_CKX .
  methods GET_PRODIM_DESCR
    returning
      value(RF_DESCR) type /VCXI/CKX_DESCR
    raising
      /VCXI/CX_CKX .
  methods GET_ASFPX
    exporting
      !EF_ASFPX type ZVCXI_P3S_ASFPX
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RR_BL_ASFPROP) type ref to ZCL_VCXI_P3SS_BL_ASFPROP
    raising
      /VCXI/CX_CKX .
  methods GET_PROHE
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_PROHE) type ZVCXI_P0S_S_PROHE
    raising
      /VCXI/CX_CKX .
  methods GET_PROLE
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_PROLE) type ZVCXI_P0S_S_PROLE
    raising
      /VCXI/CX_CKX .
  methods GET_PROWI
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_PROWI) type ZVCXI_P0S_S_PROWI
    raising
      /VCXI/CX_CKX .
  methods SET_PROHE
    importing
      !IS_PROHE type ZVCXI_P0S_S_PROHE
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_PROLE
    importing
      !IS_PROLE type ZVCXI_P0S_S_PROLE
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_PROWI
    importing
      !IS_PROWI type ZVCXI_P0S_S_PROWI
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .

  methods GET_HNDDIM
    redefinition .
  methods GET_NBLWT
    redefinition .
  methods GET_STATUS
    redefinition .
  methods GET_STATUS_MSG
    redefinition .
  methods ZIF_VCXI_PXSS_COMM_PREV~DO_FORWARD
    redefinition .
  methods GET_PACKMAT_DIM
    redefinition .
protected section.

  constants C_IEVNT_O_ASFPX_CHANGED type /VCXI/VKSC_IEVNT value 'ASFPX_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_PROHE_CHANGED type /VCXI/VKSC_IEVNT value 'PROHE_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_PROLE_CHANGED type /VCXI/VKSC_IEVNT value 'PROLE_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_PROWI_CHANGED type /VCXI/VKSC_IEVNT value 'PROWI_CHANGED' ##NO_TEXT.
  constants C_PARID_ASFPX type /VCXI/VKS_PARID value 'ZVCXI_P3_ASFPX' ##NO_TEXT.

  methods SET_ASFPX
    importing
      !IF_ASFPX type ZVCXI_P3S_ASFPX
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods CHECK_PRODIM
    importing
      !IR_MESSAGE type ref to /VCXI/CL_CKXM_MESSAGE optional
    returning
      value(RF_ELMST) type /VCXI/VKSR_ELMST
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_P3SS_PR_ASCPMCNF definition
class ZCL_VCXI_P3SS_PR_ASCPMCNF implementation.
  method CHECK_PRODIM.

*** Start positive...
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G.

***--------------------------------------------------------------------------------------
*** Check Product Length
    if ME->GET_PROLE( )-PROLE is not initial.
      if IR_MESSAGE is bound.
***     Product Length is maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P3SS'
                                 IF_MSGTY = 'S'
                                 IF_MSGNO = '151' ).
      endif.

    else.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
      if IR_MESSAGE is bound.
***     Product Length is not maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P3SS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '152' ).
      endif.
    endif.

***--------------------------------------------------------------------------------------
*** Check Product Width
    if ME->GET_PROWI( )-PROWI is not initial.
      if IR_MESSAGE is bound.
***     Product Width is maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P3SS'
                                 IF_MSGTY = 'S'
                                 IF_MSGNO = '153' ).
      endif.

    else.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
      if IR_MESSAGE is bound.
***     Product Width is not maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P3SS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '154' ).
      endif.
    endif.

***--------------------------------------------------------------------------------------
*** Check Product Height
    if ME->GET_PROHE( )-PROHE is not initial.
      if IR_MESSAGE is bound.
***     Product Height is maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P3SS'
                                 IF_MSGTY = 'S'
                                 IF_MSGNO = '155' ).
      endif.

    else.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
      if IR_MESSAGE is bound.
***     Product Height is not maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_P3SS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '156' ).
      endif.
    endif.

  endmethod.
  method GET_ASBPX.

***--------------------------------------------------------------------------------------
*** Create fresh instance of Assembly Backward Property
    RR_BL_ASBPROP = ZCL_VCXI_P3SS_BL_ASBPROP=>FACTORY( ).

    if RR_BL_ASBPROP is bound.
***   Cost Split Distribution
      RR_BL_ASBPROP->SET_CSPLIT( IT_CSPLIT = ME->GET_CSPLIT( ) ).

***   Product Dimensions
      RR_BL_ASBPROP->SET_PRODIM( IS_PRODIM = value #( S_PROLE = ME->GET_PROLE( )
                                                      S_PROWI = ME->GET_PROWI( )
                                                      S_PROHE = ME->GET_PROHE( ) ) ).
    endif.

***--------------------------------------------------------------------------------------
*** Return Corrugator Backward Property XML (Outbound)
    if EF_ASBPX is requested.
      move RR_BL_ASBPROP->GET_ASBPX( ) to EF_ASBPX.
    endif.

  endmethod.
  method GET_ASFPX.

***--------------------------------------------------------------------------------------
*** Get Assembly Forward Property (Inbound)
    ME->ZIF_VCXI_P3SS_COMM_PREV~GET_ASFPX( importing EF_ASFPX = EF_ASFPX
                                                     EF_SETBY = EF_SETBY ).

***--------------------------------------------------------------------------------------
*** Get Assembly Forward Property Instance
    if RR_BL_ASFPROP is requested.
      RR_BL_ASFPROP = ZCL_VCXI_P3SS_BL_ASFPROP=>FACTORY( IF_ASFPX = EF_ASFPX ).
    endif.

  endmethod.
  method GET_HNDDIM.

    data: LR_BL_ASFPROP type ref to ZCL_VCXI_P3SS_BL_ASFPROP.


*** Get Assembly Forward Property XML (Outbound)
    LR_BL_ASFPROP = ME->GET_ASFPX( ).
    check LR_BL_ASFPROP is bound.

*** Get Handling Dimension
    RS_HNDDIM = LR_BL_ASFPROP->S_HNDDIM.

  endmethod.
  method GET_NBLWT.

***------------------------------------------------------------------------------------------------
*** Get Assembly Forward Property XML (Inbound)
    data(LR_BL_ASFPROP) = ME->GET_ASFPX( ).
    check LR_BL_ASFPROP is bound.

*** Get Sold Blank Weight and Cut Out Weight
    data(LS_SBLWT) = LR_BL_ASFPROP->GET_SBLWT( ).
    data(LS_COUWT) = LR_BL_ASFPROP->GET_COUWT( ).

***------------------------------------------------------------------------------------------------
*** Use Sold Blank Weight as basic Net Blank Weight
    check LS_SBLWT-SBLWT_UOM is not initial.
    RS_NBLWT = value #( NBLWT     = LS_SBLWT-SBLWT
                        NBLWT_UOM = LS_SBLWT-SBLWT_UOM ).

*** Reduce by Cut Out Area
    if LS_COUWT-COUWT_UOM is not initial.
      ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = RS_NBLWT-NBLWT
                                                          IF_CALCV_UOM_1 = RS_NBLWT-NBLWT_UOM
                                                          IF_CALCV_2     = LS_COUWT-COUWT
                                                          IF_CALCV_UOM_2 = LS_COUWT-COUWT_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_SUBTRACT
                                                          IF_UOM         = RS_NBLWT-NBLWT_UOM
                                                importing EF_CALCV       = RS_NBLWT-NBLWT
                                                          EF_CALCV_UOM   = RS_NBLWT-NBLWT_UOM ).
      if RS_NBLWT-NBLWT lt 0.
        RS_NBLWT-NBLWT = 0.
      endif.
    endif.

  endmethod.
  method GET_PACKMAT_DIM.

***------------------------------------------------------------------------------------------------
*** Length
    data(LS_PROLE) = ME->GET_PROLE( ).
    RS_PACKMAT_DIM-LAENG = LS_PROLE-PROLE.
    RS_PACKMAT_DIM-MEABM = LS_PROLE-PROLE_UOM.

*** Width
    data(LS_PROWI) = ME->GET_PROWI( ).
    ZCL_VCXI_XCS_SERVICE_UNIT=>CONVERT_UNIT( exporting IF_VALUE_IN  = LS_PROWI-PROWI
                                                       IF_UOM_IN    = LS_PROWI-PROWI_UOM
                                                       IF_UOM_OUT   = RS_PACKMAT_DIM-MEABM
                                             importing EF_VALUE_OUT = RS_PACKMAT_DIM-BREIT
                                                       EF_UOM_OUT   = RS_PACKMAT_DIM-MEABM ).
*** Height
    data(LS_PROHE) = ME->GET_PROHE( ).
    ZCL_VCXI_XCS_SERVICE_UNIT=>CONVERT_UNIT( exporting IF_VALUE_IN  = LS_PROHE-PROHE
                                                       IF_UOM_IN    = LS_PROHE-PROHE_UOM
                                                       IF_UOM_OUT   = RS_PACKMAT_DIM-MEABM
                                             importing EF_VALUE_OUT = RS_PACKMAT_DIM-HOEHE
                                                       EF_UOM_OUT   = RS_PACKMAT_DIM-MEABM ).

***------------------------------------------------------------------------------------------------
*** Weight
    data(LS_NBLWT) = ME->GET_NBLWT( ).
    RS_PACKMAT_DIM-BRGEW = LS_NBLWT-NBLWT.
    RS_PACKMAT_DIM-GEWEI = LS_NBLWT-NBLWT_UOM.

  endmethod.
  method GET_PRODIM_DESCR.

*** Define Dimensions
    RF_DESCR = 'L/W/H'.

*** Full Description String
    RF_DESCR = 'Product Dim. ('(T01) && RF_DESCR && ')'.

  endmethod.
  method GET_PROHE.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PROHE ).
    move LS_PARAM-ATFLV to RS_PROHE-PROHE.
    move LS_PARAM-UNIT  to RS_PROHE-PROHE_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_PROLE.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PROLE ).
    move LS_PARAM-ATFLV to RS_PROLE-PROLE.
    move LS_PARAM-UNIT  to RS_PROLE-PROLE_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_PROWI.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PROWI ).
    move LS_PARAM-ATFLV to RS_PROWI-PROWI.
    move LS_PARAM-UNIT  to RS_PROWI-PROWI_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_STATUS.

    RF_ELMST = SUPER->GET_STATUS( ).

***--------------------------------------------------------------------------------------
*** Check Product Dimensions
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = ME->CHECK_PRODIM( ) ).

  endmethod.
  method GET_STATUS_MSG.

    SUPER->GET_STATUS_MSG( IR_MESSAGE = IR_MESSAGE ).

***--------------------------------------------------------------------------------------
*** Check Product Dimensions
    ME->CHECK_PRODIM( IR_MESSAGE = IR_MESSAGE ).

  endmethod.
  method SET_ASFPX.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_ASFPX ).

    move IF_ASFPX   to LS_PARAM-STRNG.
    move IF_SETBY   to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_ASFPX_CHANGED ).
    endif.

  endmethod.
  method SET_PROHE.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PROHE ).

    move IS_PROHE-PROHE     to LS_PARAM-ATFLV.
    move IS_PROHE-PROHE_UOM to LS_PARAM-UNIT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_PROHE_CHANGED ).
    endif.

  endmethod.
  method SET_PROLE.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PROLE ).

    move IS_PROLE-PROLE     to LS_PARAM-ATFLV.
    move IS_PROLE-PROLE_UOM to LS_PARAM-UNIT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_PROLE_CHANGED ).
    endif.

  endmethod.
  method SET_PROWI.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PROWI ).

    move IS_PROWI-PROWI     to LS_PARAM-ATFLV.
    move IS_PROWI-PROWI_UOM to LS_PARAM-UNIT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_PROWI_CHANGED ).
    endif.

  endmethod.
  method ZIF_VCXI_P3SS_COMM_PREV~GET_ASBPX.

***--------------------------------------------------------------------------------------
*** Get Assembly Backward Property (Outbound)
    RR_BL_ASBPROP = ME->GET_ASBPX( importing EF_SETBY = EF_SETBY ).

***--------------------------------------------------------------------------------------
*** Get Assembly Backward Property Instance
    if EF_ASBPX is requested.
      EF_ASBPX = RR_BL_ASBPROP->GET_ASBPX( ).
    endif.

  endmethod.
  method ZIF_VCXI_P3SS_COMM_PREV~GET_ASFPX.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

***--------------------------------------------------------------------------------------
*** Get Parameter
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_ASFPX ).
    move LS_PARAM-STRNG to EF_ASFPX.
    move LS_PARAM-SETBY to EF_SETBY.

***--------------------------------------------------------------------------------------
*** Get Assembly Forward Property Instance
    if RR_BL_ASFPROP is requested.
      RR_BL_ASFPROP = ZCL_VCXI_P3SS_BL_ASFPROP=>FACTORY( IF_ASFPX = EF_ASFPX ).
    endif.

  endmethod.
  method ZIF_VCXI_PXSS_COMM_PREV~DO_FORWARD.

    data: LR_GATE            type ref to /VCXI/CL_VKSR_GATE,
          LR_COMM_NEXT       type ref to ZIF_VCXI_P3SS_COMM_NEXT,
          LR_BL_ASFPROP      type ref to ZCL_VCXI_P3SS_BL_ASFPROP,
          LR_BL_ASFPROP_NEXT type ref to ZCL_VCXI_P3SS_BL_ASFPROP.

***------------------------------------------------------------------------------------------------
*** Process all provided Gates
    loop at IT_GATE into LR_GATE.
      try.
          move LR_GATE->R_CLSPR ?to LR_COMM_NEXT.
        catch CX_SY_MOVE_CAST_ERROR.
          clear LR_COMM_NEXT.
      endtry.
      check LR_COMM_NEXT is bound.

***   Combine Assembly Forward Property
      LR_BL_ASFPROP_NEXT = LR_COMM_NEXT->GET_ASFPX( ).
      if LR_BL_ASFPROP is not bound.
        move LR_BL_ASFPROP_NEXT to LR_BL_ASFPROP.
      else.
        LR_BL_ASFPROP->MERGE( IR_BL_ASFPROP = LR_BL_ASFPROP_NEXT ).
      endif.

    endloop.

*** Forward Assembly Forward Property XML
    if LR_BL_ASFPROP is bound.
      ME->SET_ASFPX( IF_ASFPX = LR_BL_ASFPROP->GET_ASFPX( ) ).
    else.
      ME->SET_ASFPX( IF_ASFPX = SPACE ).
    endif.

  endmethod.
