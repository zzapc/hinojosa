
class ZCL_VCXI_PXSS_POPUP_SCORE_MGR definition
  public
  create protected .

public section.

  interfaces /VCXI/IF_VKSU_UIFW .

  constants C_UC_CLOSE type SYUCOMM value 'CLOSE' ##NO_TEXT.

  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods DO_USER_COMMAND
    importing
      !IF_UCOMM type SYUCOMM
    raising
      /VCXI/CX_CKX .
  methods GET_REGISTERED_UI_INFO
    returning
      value(RS_DYNP) type /VCXI/CKX_S_DYNP_KEY
    raising
      /VCXI/CX_CKX .
  class-methods START
    importing
      !IF_LOCKED type /VCXI/CKX_LOCKED optional
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
protected section.

  data F_LOCKED type /VCXI/CKX_LOCKED .
  data F_REFRESH_REQ type ABAP_BOOL .
  data R_CONTAINER type ref to CL_GUI_CUSTOM_CONTAINER .
  data R_GATE type ref to /VCXI/CL_VKSR_GATE .
  data R_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE .
  data R_REGISTERED_UI type ref to /VCXI/CL_VKSU_CLSUI .

  methods CONSTRUCTOR
    importing
      !IF_LOCKED type /VCXI/CKX_LOCKED
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
  methods DO_FREE
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_CLOSE
    raising
      /VCXI/CX_CKX .
  methods SET_PF_STATUS
    raising
      /VCXI/CX_CKX .
  methods OPEN_SUBSCREEN
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    returning
      value(RR_CLSUI) type ref to /VCXI/CL_VKSRU_CLSUI
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_PXSS_POPUP_SCORE_MGR definition
class ZCL_VCXI_PXSS_POPUP_SCORE_MGR implementation.
  method /VCXI/IF_VKSU_UIFW~DO_MODIFY_SCREEN.

    if ME->R_REGISTERED_UI is bound.
      ME->R_REGISTERED_UI->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                             IF_DYNNR = IF_DYNNR ).
    endif.

  endmethod.
  method /VCXI/IF_VKSU_UIFW~DO_USER_COMMAND.

    data:  LR_CX_CKX type ref to /VCXI/CX_CKX.

    try.
        if ME->R_REGISTERED_UI is bound.
          ME->R_REGISTERED_UI->DO_USER_COMMAND( IF_UCOMM = CF_UCOMM ).
        endif.

      catch /VCXI/CX_CKX into LR_CX_CKX.
        LR_CX_CKX->SHOW_MESSAGE( ).
    endtry.

  endmethod.
  method /VCXI/IF_VKSU_UIFW~REFRESH_SCREEN.

    ME->R_UIGATE->REFRESH_SCREEN( ).

  endmethod.
  method /VCXI/IF_VKSU_UIFW~REGISTER_UI.

    do.
      if ME->R_REGISTERED_UI is not bound.
        exit.
      endif.
      ME->R_REGISTERED_UI->DEACTIVATE( ).
    enddo.

    move IR_UI to ME->R_REGISTERED_UI.

  endmethod.
  method /VCXI/IF_VKSU_UIFW~UNREGISTER_UI.

    if IR_UI eq ME->R_REGISTERED_UI.
      clear ME->R_REGISTERED_UI.
    endif.

  endmethod.
  method CONSTRUCTOR.

    move IR_GATE        to ME->R_GATE.
    move IR_UIGATE      to ME->R_UIGATE.
    move IF_LOCKED      to ME->F_LOCKED.

    ME->OPEN_SUBSCREEN( IR_GATE = ME->R_GATE ).

  endmethod.
  method DO_FREE.

    if ME->R_REGISTERED_UI is bound.
      ME->R_REGISTERED_UI->DEACTIVATE( ).
    endif.

  endmethod.
  method DO_PBO.

*** Set PF-Status
    ME->SET_PF_STATUS( ).

  endmethod.
  method DO_USER_COMMAND.

    case IF_UCOMM.
      when C_UC_CLOSE.
        ME->HANDLE_UC_CLOSE( ).

    endcase.

    data(LF_UCOMM) = IF_UCOMM.
    ME->/VCXI/IF_VKSU_UIFW~DO_USER_COMMAND( changing CF_UCOMM = LF_UCOMM ).

    ME->R_GATE->RAISE_EVENTS( ).

  endmethod.
  method GET_REGISTERED_UI_INFO.

    if ME->R_REGISTERED_UI is bound.
      RS_DYNP = ME->R_REGISTERED_UI->S_DYNP.
    endif.

  endmethod.
  method HANDLE_UC_CLOSE.

*** Raise Event
    ME->R_GATE->RAISE_EVENTS( ).

*** Leave Popup
    leave to screen 0.

  endmethod.
  method OPEN_SUBSCREEN.

    data: LF_CLSUI  type  /VCXI/VKSC_CLSUI.


    if IR_GATE is bound.
      LF_CLSUI = IR_GATE->GET_CLSUI( ).
    endif.

    if LF_CLSUI is not initial.
***   Create Subscreen
      create object RR_CLSUI
        type
          (LF_CLSUI)
        exporting
          IR_UIFW    = ME
          IR_UIGATE  = ME->R_UIGATE
          IR_GATE    = IR_GATE.
      RR_CLSUI->ACTIVATE( ).
    else.
***   Clear Subscreen
      if ME->R_REGISTERED_UI is bound.
        ME->R_REGISTERED_UI->DEACTIVATE( ).
      endif.
    endif.

  endmethod.
  method SET_PF_STATUS.

    set pf-status '0100' of program 'SAPLZVCXI_PXSS_POPUP_SCORE_MGR'.

  endmethod.
  method START.

    data:  LR_POPUP_SCORE_MGR type ref to ZCL_VCXI_PXSS_POPUP_SCORE_MGR.


*** Create Instance
    create object LR_POPUP_SCORE_MGR
      exporting
        IR_GATE        = IR_GATE
        IR_UIGATE      = IR_UIGATE
        IF_LOCKED      = IF_LOCKED
        IF_APPL_EVENTS = IF_APPL_EVENTS.

*** Call Popup
    call function 'ZVCXI_PXSS_POPUP_SCR_MGR_START'
      exporting
        IR_POPUP_SCORE_MGR = LR_POPUP_SCORE_MGR.

*** Free the Popup
    LR_POPUP_SCORE_MGR->DO_FREE( ).

  endmethod.
