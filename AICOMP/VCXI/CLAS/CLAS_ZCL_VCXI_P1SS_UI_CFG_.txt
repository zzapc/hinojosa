
class ZCL_VCXI_P1SS_UI_CFG definition
  public
  inheriting from ZCL_VCXI_PXSS_UI_XFG
  create public .

public section.

  data S_CFG_VIS type ZVCXI_P1SS_S_CFG_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABPT type ZVCXI_XCS_TABPT default '0100S_CFG_TAB-TAB'
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
  methods DO_PBO
    redefinition .
protected section.

  constants C_UC_CPROP type SYUCOMM value 'CFG_CPROP' ##NO_TEXT.
  data R_PR_CFG type ref to ZCL_VCXI_P1SS_PR_CFG .
  data R_GRID_PRINT_COM type ref to ZCL_VCXI_PXSS_GRID_PRINT_COM .

  methods GET_IELEM_BY_SCREEN
    importing
      !IF_GROUP2 type SCREEN-GROUP2
    returning
      value(RF_IELEM) type ZVCXI_P0SS_IELEM
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_CPROP
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_P1SS_UI_CFG definition
class ZCL_VCXI_P1SS_UI_CFG implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_P1SS_UI_CFG_BIND'
      exporting
        IR_UI_CFG     = ME
      importing
        ES_DYNP       = ME->S_DYNP
        EF_ACTIVE_TAB = ME->F_ACTIVE_TAB.

***--------------------------------------------------------------------------------------
*** Create Grid for Print
    create object ME->R_GRID_PRINT_COM
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_PRINT_COM'
        IR_PRINT_COM      = ME->R_PR_CFG.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE
                        IF_TABPT  = IF_TABPT ).

*** Assign th RP Class to the UI Class
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_CFG.
      catch CX_SY_MOVE_CAST_ERROR.

***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

***--------------------------------------------------------------------------------------
*** Free Printing Grid
    if ME->R_GRID_PRINT_COM is bound.
      ME->R_GRID_PRINT_COM->DO_FREE( ).
      clear ME->R_GRID_PRINT_COM.
    endif.

  endmethod.
  method DO_MODIFY_SCREEN.

    data: LF_BOXPA type ZVCXI_P0SS_BOXPA,
          LF_CADLS type ZVCXI_PXS_CADLS.
    data: LR_CUST_PRSTY type ref to ZCL_VCXI_PXSS_CUST_PRSTY.


    try.
        SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                 IF_DYNNR = IF_DYNNR ).

***     Get FEFCO Customizing and Part of Box
        ME->R_PR_CFG->GET_FEFCO( importing ER_CUST_PRSTY = LR_CUST_PRSTY ).
        LF_BOXPA = ME->R_PR_CFG->GET_BOXPA( ).

***     Get CAD Lock Status
        LF_CADLS = ME->R_PR_CFG->GET_CADLS( ).

***----------------------------------------------------------------------------
***     Modify Screen
        loop at screen.
***       Hide Custom Product Sheet W/L
          if SCREEN-GROUP1 eq 'CST'.
            if LR_CUST_PRSTY is not bound or
               LR_CUST_PRSTY->GET_FCUSD( ) ne ABAP_TRUE.
              move 0 to SCREEN-ACTIVE.
            endif.
          endif.

***       Hide Joint Dimension if no Joint Flap is defined
          if SCREEN-GROUP1 eq 'NJO' and
             ME->S_CFG_VIS-JOIPO eq ZCL_VCXI_PXSS_PR_SCORE_MGR=>C_JOIPO_NONE.
            move 0 to SCREEN-ACTIVE.
          endif.

***       Hide Part of Box if not defined by Product Style
          if SCREEN-GROUP1 eq 'BXP'.
            if LR_CUST_PRSTY is not bound or
               LR_CUST_PRSTY->GET_BOXPA( ) is initial.
              move 0 to SCREEN-ACTIVE.
            endif.
          endif.

***       Disable Fanfold Dimensions
          case ABAP_TRUE.
            when ME->S_CFG_VIS-FANDM_FANHE.
              if    SCREEN-GROUP3 eq 'FLE'
                 or SCREEN-GROUP3 eq 'FSH' .
                move 0 to SCREEN-INPUT.
              endif.
            when ME->S_CFG_VIS-FANDM_FANLE.
              if    SCREEN-GROUP3 eq 'FHE'
                 or SCREEN-GROUP3 eq 'FSH' .
                move 0 to SCREEN-INPUT.
              endif.
            when ME->S_CFG_VIS-FANDM_FANSH.
              if    SCREEN-GROUP3 eq 'FHE'
                 or SCREEN-GROUP3 eq 'FLE' .
                move 0 to SCREEN-INPUT.
              endif.
          endcase.

***       Adjust based on Product Style Element
          if SCREEN-GROUP1 eq 'PSE'.
            if LR_CUST_PRSTY is bound.
              if LR_CUST_PRSTY->IS_IELEM_USED( IF_IELEM = ME->GET_IELEM_BY_SCREEN( IF_GROUP2 = SCREEN-GROUP2 )
                                               IF_BOXPA = LF_BOXPA ) eq ABAP_FALSE.
                move 0 to SCREEN-ACTIVE.
              endif.
            else.
              SCREEN-ACTIVE = switch #( ME->GET_IELEM_BY_SCREEN( IF_GROUP2 = SCREEN-GROUP2 )
                                            when ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_LENGTH then 1
                                            when ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_WIDTH  then 1
                                            when ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_HEIGHT then 1
                                                                                          else 0 ).
            endif.
          endif.

***       Hide Print Override Flag if undefined
          if SCREEN-NAME cp '*-FOVRR_PRINT' and
             ME->S_CFG_VIS-FOVRR_PRINT eq ABAP_UNDEFINED.
            move 0 to SCREEN-ACTIVE.
          endif.

***       Adjust based on CAD Lock Status
          if SCREEN-GROUP4 eq 'CAD' and
             ( LF_CADLS eq ZCL_VCXI_PXSS_PR_CADIF_PRJ=>C_CADLS_REVISION or
               LF_CADLS eq ZCL_VCXI_PXSS_PR_CADIF_PRJ=>C_CADLS_LOCKED ).
            move 0 to SCREEN-INPUT.
          endif.

***       Reopen Info Buttons
          if SCREEN-GROUP1 eq 'INF'.
            move 1 to SCREEN-INPUT.
          endif.

          modify screen.
        endloop.

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Super PBO
    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize Grid
    if ME->R_GRID_PRINT_COM is bound.
      ME->R_GRID_PRINT_COM->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_PRINT_COM->REFRESH_GRID( ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_CFG_VIS  type ZVCXI_P1SS_S_CFG_VIS.


*** Super
    SUPER->DO_UPDATE_VIS( IS_VIS = IS_VIS ).
    check ME->R_PR_CFG is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_CFG_VIS.

***--------------------------------------------------------------------------------------
*** Set Fefco
    if ME->S_CFG_VIS-FEFCO ne LS_CFG_VIS-FEFCO.
      ME->R_PR_CFG->SET_FEFCO( IF_FEFCO = LS_CFG_VIS-FEFCO
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Part of Box
    if ME->S_CFG_VIS-BOXPA ne LS_CFG_VIS-BOXPA.
      ME->R_PR_CFG->SET_BOXPA( IF_BOXPA = LS_CFG_VIS-BOXPA
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Product Length
    if ME->S_CFG_VIS-S_PROLE ne LS_CFG_VIS-S_PROLE.
      ME->R_PR_CFG->SET_PROLE( IS_PROLE = LS_CFG_VIS-S_PROLE
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Product Width
    if ME->S_CFG_VIS-S_PROWI ne LS_CFG_VIS-S_PROWI.
      ME->R_PR_CFG->SET_PROWI( IS_PROWI = LS_CFG_VIS-S_PROWI
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Product Height
    if ME->S_CFG_VIS-S_PROHE ne LS_CFG_VIS-S_PROHE.
      ME->R_PR_CFG->SET_PROHE( IS_PROHE = LS_CFG_VIS-S_PROHE
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Product Height2
    if ME->S_CFG_VIS-S_PROH2 ne LS_CFG_VIS-S_PROH2.
      ME->R_PR_CFG->SET_PROH2( IS_PROH2 = LS_CFG_VIS-S_PROH2
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Product Sheet Length
    if ME->S_CFG_VIS-S_SHTPL ne LS_CFG_VIS-S_SHTPL.
      ME->R_PR_CFG->SET_SHTPL( IS_SHTPL = LS_CFG_VIS-S_SHTPL
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Product Sheet Width
    if ME->S_CFG_VIS-S_SHTPW ne LS_CFG_VIS-S_SHTPW.
      ME->R_PR_CFG->SET_SHTPW( IS_SHTPW = LS_CFG_VIS-S_SHTPW
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Overlap
    if ME->S_CFG_VIS-S_OVLAP ne LS_CFG_VIS-S_OVLAP.
      ME->R_PR_CFG->SET_OVLAP( IS_OVLAP = LS_CFG_VIS-S_OVLAP
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Tuck in Flap
    if ME->S_CFG_VIS-S_FTUCK ne LS_CFG_VIS-S_FTUCK.
      ME->R_PR_CFG->SET_FTUCK( IS_FTUCK = LS_CFG_VIS-S_FTUCK
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Dust Flap
    if ME->S_CFG_VIS-S_FDUST ne LS_CFG_VIS-S_FDUST.
      ME->R_PR_CFG->SET_FDUST( IS_FDUST = LS_CFG_VIS-S_FDUST
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Gap in Flap Top
    if ME->S_CFG_VIS-S_FGAPT ne LS_CFG_VIS-S_FGAPT.
      ME->R_PR_CFG->SET_FGAPT( IS_FGAPT = LS_CFG_VIS-S_FGAPT
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Gap in Flap Botton
    if ME->S_CFG_VIS-S_FGAPB ne LS_CFG_VIS-S_FGAPB.
      ME->R_PR_CFG->SET_FGAPB( IS_FGAPB = LS_CFG_VIS-S_FGAPB
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Fanfold Dimension
    if ME->S_CFG_VIS-FANDM_FANHE ne LS_CFG_VIS-FANDM_FANHE or
       ME->S_CFG_VIS-FANDM_FANLE ne LS_CFG_VIS-FANDM_FANLE or
       ME->S_CFG_VIS-FANDM_FANSH ne LS_CFG_VIS-FANDM_FANSH.
      ME->R_PR_CFG->SET_FANDM( IF_FANDM = cond #( when LS_CFG_VIS-FANDM_FANHE eq ABAP_TRUE then ZCL_VCXI_P1SS_PR_CFG=>C_FANDM_FANHE
                                                  when LS_CFG_VIS-FANDM_FANLE eq ABAP_TRUE then ZCL_VCXI_P1SS_PR_CFG=>C_FANDM_FANLE
                                                  when LS_CFG_VIS-FANDM_FANSH eq ABAP_TRUE then ZCL_VCXI_P1SS_PR_CFG=>C_FANDM_FANSH )
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Fanfold Height
    if ME->S_CFG_VIS-S_FANHE ne LS_CFG_VIS-S_FANHE.
      ME->R_PR_CFG->SET_FANHE( IS_FANHE = LS_CFG_VIS-S_FANHE
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Fanfold Length
    if ME->S_CFG_VIS-S_FANLE ne LS_CFG_VIS-S_FANLE.
      ME->R_PR_CFG->SET_FANLE( IS_FANLE = LS_CFG_VIS-S_FANLE
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Fanfold Sheets
    if ME->S_CFG_VIS-S_FANSH ne LS_CFG_VIS-S_FANSH.
      ME->R_PR_CFG->SET_FANSH( IS_FANSH = LS_CFG_VIS-S_FANSH
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Roll Length
    if ME->S_CFG_VIS-S_ROLEN ne LS_CFG_VIS-S_ROLEN.
      ME->R_PR_CFG->SET_ROLEN( IS_ROLEN = LS_CFG_VIS-S_ROLEN
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Flute Orientation
    if ME->S_CFG_VIS-FLUTO ne LS_CFG_VIS-FLUTO.
      ME->R_PR_CFG->SET_FLUTO( IF_FLUTO = LS_CFG_VIS-FLUTO
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Cut Out Area
    if ME->S_CFG_VIS-S_COUAR ne LS_CFG_VIS-S_COUAR.
      ME->R_PR_CFG->SET_COUAR( IS_COUAR = LS_CFG_VIS-S_COUAR
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.


***--------------------------------------------------------------------------------------
*** Set Joint Position
    if ME->S_CFG_VIS-JOIPO ne LS_CFG_VIS-JOIPO.
      ME->R_PR_CFG->SET_JOIPO( IF_JOIPO = LS_CFG_VIS-JOIPO
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Joint Width
    if ME->S_CFG_VIS-S_JOIWI ne LS_CFG_VIS-S_JOIWI.
      ME->R_PR_CFG->SET_JOIWI( IS_JOIWI = LS_CFG_VIS-S_JOIWI
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Joint Length
    if ME->S_CFG_VIS-S_JOILE ne LS_CFG_VIS-S_JOILE.
      ME->R_PR_CFG->SET_JOILE( IS_JOILE = LS_CFG_VIS-S_JOILE
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Joint Extension Top
    if ME->S_CFG_VIS-S_JEXTT ne LS_CFG_VIS-S_JEXTT.
      ME->R_PR_CFG->SET_JEXTT( IS_JEXTT = LS_CFG_VIS-S_JEXTT
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Joint Extension Bottom
    if ME->S_CFG_VIS-S_JEXTB ne LS_CFG_VIS-S_JEXTB.
      ME->R_PR_CFG->SET_JEXTB( IS_JEXTB = LS_CFG_VIS-S_JEXTB
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Board ID Suggestion
    if ME->S_CFG_VIS-SUBOA ne LS_CFG_VIS-SUBOA.
      ME->R_PR_CFG->SET_SUBOA( IF_SUBOA = LS_CFG_VIS-SUBOA
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Override for Print
    if ME->S_CFG_VIS-FOVRR_PRINT ne LS_CFG_VIS-FOVRR_PRINT.
      ME->R_PR_CFG->SET_FOVRR_PRINT( IF_FOVRR = LS_CFG_VIS-FOVRR_PRINT ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    if ME->R_GRID_PRINT_COM is bound.
      ME->R_GRID_PRINT_COM->CHECK_CHANGED_DATA( ).
    endif.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    case IF_UCOMM.
      when C_UC_CPROP.
        ME->HANDLE_UC_CPROP( ).

    endcase.

  endmethod.
  method GET_IELEM_BY_SCREEN.

    RF_IELEM = switch #( IF_GROUP2 when 'PLE' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_LENGTH
                                   when 'PWI' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_WIDTH
                                   when 'PHE' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_HEIGHT
                                   when 'PH2' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_HEIGHT2
                                   when 'OLP' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_OVLAP
                                   when 'FDU' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_FDUST
                                   when 'FTU' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_FTUCK
                                   when 'FGT' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_GAP_FLAP_T
                                   when 'FGB' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_GAP_FLAP_B
                                   when 'FAN' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_FANLE
                                   when 'RLE' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_ROLEN
                       ).

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_CFG.

  endmethod.
  method HANDLE_UC_CPROP.

    data: LF_WERKS type WERKS_D.
    data: LS_SURWT type ZVCXI_PXS_S_SURWT.
    data: LR_BL_CRBPROP type ref to ZCL_VCXI_P1SS_BL_CRBPROP,
          LR_BL_CRFPROP type ref to ZCL_VCXI_P1SS_BL_CRFPROP.

*** Get Corrugator Property (Outbound)
    LR_BL_CRBPROP = ME->R_PR_CFG->GET_CRBPX( ).
    LR_BL_CRFPROP = ME->R_PR_CFG->GET_CRFPX( ).

*** Get Plant
    LF_WERKS = ME->R_PR_CFG->GET_WERKS( ).

*** Get Surface Weight
    LS_SURWT = ME->R_PR_CFG->GET_SURWT( ).

*** Start Popup
    ZCL_VCXI_P1SS_POPUP_CRPROP=>START( IR_BL_CRBPROP = LR_BL_CRBPROP
                                       IR_BL_CRFPROP = LR_BL_CRFPROP
                                       IF_WERKS      = LF_WERKS
                                       IS_SURWT      = LS_SURWT ).

  endmethod.
  method INIT_VIS.

    data: LR_BL_CRFPROP type ref to ZCL_VCXI_P1SS_BL_CRFPROP.


    free: ME->S_CFG_VIS.

    SUPER->INIT_VIS( ).
    ME->S_CFG_VIS-S_XFG_VIS = ME->S_XFG_VIS.
    check ME->R_PR_CFG is bound.

***------------------------------------------------------------------------------------------------
*** Get Fefco
    ME->S_CFG_VIS-FEFCO = ME->R_PR_CFG->GET_FEFCO( importing EF_DESCR = ME->S_CFG_VIS-FEFCO_DESCR ).

*** Get Part of Box
    ME->S_CFG_VIS-BOXPA = ME->R_PR_CFG->GET_BOXPA( ).

*** Get Product Dimension
    ME->S_CFG_VIS-PRODIM_DESCR = ME->R_PR_CFG->GET_PRODIM_DESCR( ).
    ME->S_CFG_VIS-S_PROWI = ME->R_PR_CFG->GET_PROWI( ).
    ME->S_CFG_VIS-S_PROLE = ME->R_PR_CFG->GET_PROLE( ).
    ME->S_CFG_VIS-S_PROHE = ME->R_PR_CFG->GET_PROHE( ).
    ME->S_CFG_VIS-S_PROH2 = ME->R_PR_CFG->GET_PROH2( ).

*** Get Product Sheet Dimension
    ME->S_CFG_VIS-S_SHTPW = ME->R_PR_CFG->GET_SHTPW( ).
    ME->S_CFG_VIS-S_SHTPL = ME->R_PR_CFG->GET_SHTPL( ).

*** Get Overlap/Tuck in/Dust Flap
    ME->S_CFG_VIS-S_OVLAP = ME->R_PR_CFG->GET_OVLAP( ).
    ME->S_CFG_VIS-S_FTUCK = ME->R_PR_CFG->GET_FTUCK( ).
    ME->S_CFG_VIS-S_FDUST = ME->R_PR_CFG->GET_FDUST( ).

*** Gap in Flap Top/Botton
    ME->S_CFG_VIS-S_FGAPT = ME->R_PR_CFG->GET_FGAPT( ).
    ME->S_CFG_VIS-S_FGAPB = ME->R_PR_CFG->GET_FGAPB( ).

*** Fanfold
    case ME->R_PR_CFG->GET_FANDM( ).
      when ZCL_VCXI_P1SS_PR_CFG=>C_FANDM_FANHE.
        ME->S_CFG_VIS-FANDM_FANHE = ABAP_TRUE.
      when ZCL_VCXI_P1SS_PR_CFG=>C_FANDM_FANLE.
        ME->S_CFG_VIS-FANDM_FANLE = ABAP_TRUE.
      when ZCL_VCXI_P1SS_PR_CFG=>C_FANDM_FANSH.
        ME->S_CFG_VIS-FANDM_FANSH = ABAP_TRUE.
    endcase.
    ME->S_CFG_VIS-S_FANHE = ME->R_PR_CFG->GET_FANHE( ).
    ME->S_CFG_VIS-S_FANLE = ME->R_PR_CFG->GET_FANLE( ).
    ME->S_CFG_VIS-S_FANSH = ME->R_PR_CFG->GET_FANSH( ).

*** Get Roll Length
    ME->S_CFG_VIS-S_ROLEN = ME->R_PR_CFG->GET_ROLEN( ).

*** Get Flute Orientation
    ME->S_CFG_VIS-FLUTO = ME->R_PR_CFG->GET_FLUTO( ).

*** Get Cut Out Area
    ME->S_CFG_VIS-S_COUAR = ME->R_PR_CFG->GET_COUAR( ).

***------------------------------------------------------------------------------------------------
*** Get Sheet Dimension (Outbound)
    ME->S_CFG_VIS-S_SHTLE = ME->R_PR_CFG->GET_SHTLE( ).
    ME->S_CFG_VIS-S_SHTWI = ME->R_PR_CFG->GET_SHTWI( ).

*** Number Ups (Outbound)
    ME->S_CFG_VIS-NOUPS = ME->R_PR_CFG->GET_NOUPS( ).

*** Get Flute & Caliper (Inbound)
    LR_BL_CRFPROP = ME->R_PR_CFG->GET_CRFPX( ).
    if LR_BL_CRFPROP is bound.
      ME->S_CFG_VIS-FLUTE   = LR_BL_CRFPROP->GET_FLUTE( importing EF_DESCR = ME->S_CFG_VIS-FLUTE_DESCR ).
      ME->S_CFG_VIS-S_CALIP = LR_BL_CRFPROP->S_CALIP.
    endif.

*** Get Board ID (Inbound)
    ME->R_PR_CFG->GET_BOARD( importing EF_BOARD = ME->S_CFG_VIS-BOARD
                                       EF_DESCR = ME->S_CFG_VIS-BOARD_DESCR ).

***------------------------------------------------------------------------------------------------
*** Joint Dimensions
    ME->S_CFG_VIS-JOIPO = ME->R_PR_CFG->GET_JOIPO( ).
    ME->S_CFG_VIS-S_JOIWI = ME->R_PR_CFG->GET_JOIWI( ).
    ME->S_CFG_VIS-S_JOILE = ME->R_PR_CFG->GET_JOILE( ).
    ME->S_CFG_VIS-S_JEXTT = ME->R_PR_CFG->GET_JEXTT( ).
    ME->S_CFG_VIS-S_JEXTB = ME->R_PR_CFG->GET_JEXTB( ).

***------------------------------------------------------------------------------------------------
*** Board ID Suggestion
    ME->S_CFG_VIS-SUBOA = ME->R_PR_CFG->GET_SUBOA( importing EF_DESCR = ME->S_CFG_VIS-SUBOA_DESCR ).

***------------------------------------------------------------------------------------------------
*** Override for Print
    ME->S_CFG_VIS-FOVRR_PRINT = ME->R_PR_CFG->GET_FOVRR_PRINT( ).

  endmethod.
