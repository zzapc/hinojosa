
class ZCL_VCXI_P0SP_PR_ADDON_TAPE definition
  public
  inheriting from ZCL_VCXI_XCSP_PR_ADDON
  create public .

public section.

  constants C_IAATY_NOTAP type /VCXI/VSMC_IAATY value 'P0_AA_NOTAP'. "#EC NOTEXT

  methods HANDLE_EVENT
    redefinition .
protected section.

  constants C_IEVNT_I_RECALC_MC_CONSU type /VCXI/VKSC_IEVNT value 'RECALC_MC_CONSU'. "#EC NOTEXT

  methods GET_NOTAP
    returning
      value(RF_NOTAP) type I
    raising
      /VCXI/CX_CKX .
  methods HANDLE_RECALC_MC_CONSU
    importing
      !IT_GATE type /VCXI/VKSR_TT_GATE
    raising
      /VCXI/CX_CKX .

  methods GET_RTE_SAREC
    redefinition .
private section.
endclass. "ZCL_VCXI_P0SP_PR_ADDON_TAPE definition
class ZCL_VCXI_P0SP_PR_ADDON_TAPE implementation.
method GET_NOTAP.

  data:  LT_GATE            type        /VCXI/VKSR_TT_GATE.

***------------------------------------------------------------------
*** Get all Material Consumptions
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCSP_PR_MC=>C_IPRVT_XCS_MC
                                   IF_INUSE = ABAP_TRUE ).

  RF_NOTAP = lines( LT_GATE ).

endmethod.
method GET_RTE_SAREC.

  data:          LF_GAATY      type /VCXI/VSMC_GAATY.
  field-symbols: <S_RTE_SAREC> type /VCXI/VSMR_S_RTE_SAREC,
                 <S_ARECN>     type /VCXI/VSMR_S_RTE_RANGE_ARECN.

*** Get Super
  RT_RTE_SAREC = SUPER->GET_RTE_SAREC( ).

  check ME->R_RTE_CADDON          is bound and
        ME->R_RTE_CADDON->R_ATYPE is bound.

*** Aspect - P0_AA_NOTAP
  try.
      LF_GAATY = ME->R_RTE_CADDON->R_ATYPE->GET_GAATY_BY_IAATY( IF_IAATY = C_IAATY_NOTAP
                                                                IF_MAATY = /VCXI/CL_VSMC_ATYPE=>C_MAATY_REC ).
      append initial line to RT_RTE_SAREC assigning <S_RTE_SAREC>.
      move LF_GAATY                                     to <S_RTE_SAREC>-GAATY.
      move 1                                            to <S_RTE_SAREC>-MAXSEL.
      move /VCXI/CL_VSMR_RTE_CADDON=>C_ASORT_DESCENDING to <S_RTE_SAREC>-ASORT.

      append initial line      to <S_RTE_SAREC>-I_ARECN assigning <S_ARECN>.
      move 'I'                 to <S_ARECN>-SIGN.
      move 'LE'                to <S_ARECN>-OPTION.
      move ME->GET_NOTAP( )    to <S_ARECN>-LOW.
    catch /VCXI/CX_CKX.
  endtry.

endmethod.
method HANDLE_EVENT.

  SUPER->HANDLE_EVENT( IF_IEVNT         = IF_IEVNT
                       IT_GATE_PROVIDER = IT_GATE_PROVIDER ).

  case IF_IEVNT.
    when C_IEVNT_I_RECALC_MC_CONSU.
      ME->HANDLE_RECALC_MC_CONSU( IT_GATE = IT_GATE_PROVIDER ).
  endcase.

endmethod.
method HANDLE_RECALC_MC_CONSU.

  data:  LF_UPDATE_ALL      type        ABAP_BOOL.
  data:  LT_GATE            type        /VCXI/VKSR_TT_GATE.
  data:  LS_MQTYI           type        ZVCXI_XCSP_S_MQTYI,
         LS_QTY             type        ZVCXI_XCSP_S_QTY,
         LS_MQTYO           type        ZVCXI_XCSP_S_MQTYO.
  data:  LR_GATE            type ref to /VCXI/CL_VKSR_GATE,
         LR_GATE_PARENT     type ref to /VCXI/CL_VKSR_GATE,
         LR_PR_MACHINE      type ref to ZCL_VCXI_XCSP_PR_MACHINE,
         LR_PR_MC           type ref to ZCL_VCXI_XCSP_PR_MC.

***------------------------------------------------------------------
*** Should all MC Tapes updated or just the requester
  read table IT_GATE with key TABLE_LINE = ME->R_GATE
  transporting no fields.
  if SY-SUBRC = 0.
    LF_UPDATE_ALL = ABAP_TRUE.
  else.
    LR_GATE_PARENT = ME->R_GATE->GET_PARENT( ).
    read table IT_GATE with key TABLE_LINE = LR_GATE_PARENT
                       transporting no fields.
    if SY-SUBRC = 0.
      LF_UPDATE_ALL = ABAP_TRUE.
    endif.
  endif.

***------------------------------------------------------------------
*** Get all Machine Quantity and Setup Waste
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCSP_PR_MACHINE=>C_IPRVT_XCS_MACHINE
                                   IF_INUSE = ABAP_TRUE ).

  read table LT_GATE into LR_GATE index 1.
  if SY-SUBRC = 0.

    clear: LR_PR_MACHINE.
    try.
        move LR_GATE->R_CLSPR ?to LR_PR_MACHINE.
      catch CX_SY_MOVE_CAST_ERROR.
        clear: LR_PR_MACHINE.
    endtry.
    if LR_PR_MACHINE is bound.
      move LR_PR_MACHINE->GET_MQTYI( ) to LS_MQTYI.

*** Get Machine Quantity In in PC
      move LS_MQTYI-MQTYI     to LS_QTY-QTY.
      move LS_MQTYI-MQTYI_UOM to LS_QTY-QTY_UOM.
      LS_MQTYO = LR_PR_MACHINE->CONV_TO_MQTYO( IS_QTY = LS_QTY ).
      move LS_MQTYO-MQTYO     to LS_MQTYI-MQTYI.
      move LS_MQTYO-MQTYO_UOM to LS_MQTYI-MQTYI_UOM.

    endif.

  endif.

***------------------------------------------------------------------
*** Get all Material Consumptions
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCSP_PR_MC=>C_IPRVT_XCS_MC
                                   IF_INUSE = ABAP_TRUE ).

***----------------------------------------------------------------------------
*** Start Recalc Consumption of all Material Consumptions or just the requester
  loop at LT_GATE into LR_GATE.
    if LF_UPDATE_ALL eq ABAP_FALSE.
      read table IT_GATE with key TABLE_LINE = LR_GATE transporting no fields.
      if SY-SUBRC ne 0.
        continue.
      endif.
    endif.

    try.

        clear: LR_PR_MC.
        move LR_GATE->R_CLSPR ?to LR_PR_MC.
        check LR_PR_MC is bound.
        LR_PR_MC->CALC_CONSU( IS_MQTYI = LS_MQTYI ).

      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.

  endloop.

endmethod.
