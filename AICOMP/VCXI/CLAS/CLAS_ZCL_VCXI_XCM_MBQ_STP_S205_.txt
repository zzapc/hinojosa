
class ZCL_VCXI_XCM_MBQ_STP_S205 definition
  public
  inheriting from ZCL_VCXI_XCM_MBQ_STP_S000
  create public .

public section.

  methods EXECUTE
    redefinition .
protected section.
private section.
endclass. "ZCL_VCXI_XCM_MBQ_STP_S205 definition
class ZCL_VCXI_XCM_MBQ_STP_S205 implementation.
  method EXECUTE.

    data: LS_MATLI    type ZVCXI_XCM_MBQ_S_MATLI,
          LS_CLASSI   type ZVCXI_XCM_MBQ_S_CLASSI,
          LS_BAPIRET2 type BAPIRET2.
    data: LR_ACTDAT  type ref to DATA,
          LR_BOB_MAT type ref to ZCL_VCXI_XCI_BOB_MAT.
    data: LT_BAPIRET2 type BAPIRETTAB.
    field-symbols: <S_ACTDAT_MCL> type ZVCXI_XCM_MBQ_S_ACTDAT_MCL.


    try.
***     Get Material List Entry of active Material List Entry
        LS_MATLI = ME->R_DCO_INPUT->T_MATLI[ MLCNT = ME->R_DCO_INPUT->F_MLCNT ].

***     Check Activity - Create Material Classification
        if not LINE_EXISTS( LS_MATLI-T_ACTCON[ ACTID = ZCL_VCXI_XCM_MBQ_CUST=>C_ACTID_MCL ] ).
***       Set Status to CANCELED
          ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_CANCELED ).
          return.
        endif.

***     Load Activity Control Data
        LR_ACTDAT = LS_MATLI-T_ACTCON[ ACTID = ZCL_VCXI_XCM_MBQ_CUST=>C_ACTID_MCL ]-R_ACTDAT.
        assign LR_ACTDAT->* to <S_ACTDAT_MCL>.


***--------------------------------------------------------------------------------------
***     Load Material Business Object
        create object LR_BOB_MAT.
        LR_BOB_MAT->INIT_FROM_DB( exporting IF_MATNR    = LS_MATLI-MATNR
                                  importing ET_BAPIRET2 = LT_BAPIRET2 ).

        loop at <S_ACTDAT_MCL>-T_CLASSI into LS_CLASSI.
          LR_BOB_MAT->CREATE_CLASSI( exporting IF_CLNAM      = LS_CLASSI-CLNAM
                                               IF_CLTYP      = LS_CLASSI-CLTYP
                                               IT_ALLOC_NUM  = LS_CLASSI-T_ALLOC_NUM
                                               IT_ALLOC_CHAR = LS_CLASSI-T_ALLOC_CHAR
                                               IT_ALLOC_CURR = LS_CLASSI-T_ALLOC_CURR
                                     importing ET_BAPIRET2   = LT_BAPIRET2 ).

***       Add Message to Message Log
          loop at LT_BAPIRET2 into LS_BAPIRET2.
            ME->R_RPSEG->R_MSG->ADD_MESSAGE_BY_BAPIRET2( IS_BAPIRET2 = LS_BAPIRET2 ).
          endloop.
        endloop.

***     Set Status to Complete
        ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_COMPLETED ).

***--------------------------------------------------------------------------------------
      catch /VCXI/CX_CKX into data(LR_CX_CKX).
***     Set Status to Error
        ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_ERROR ).

***     Add Message to Message Log
        ME->R_RPSEG->R_MSG->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).

    endtry.

*** Perform Save and Commit
    ME->R_RPSEG->SAVE_AND_COMMIT( ).

  endmethod.
endclass. "ZCL_VCXI_XCM_MBQ_STP_S205 definition
endclass. "ZCL_VCXI_XCM_MBQ_STP_S205 implementation
