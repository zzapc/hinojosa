
class ZCL_VCXI_P2SS_UI_FCCONI definition
  public
  inheriting from ZCL_VCXI_PXSS_UI_XCONI
  create public .

public section.

  data S_FCCONI_VIS type ZVCXI_P2SS_S_FCCONI_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABPT type ZVCXI_XCS_TABPT default '0100S_IN_TAB-TAB'
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  constants C_UC_FCPROP_IN type SYUCOMM value 'FCPROP_IN' ##NO_TEXT.
  constants C_UC_SCSTR_IN type SYUCOMM value 'SCSTR_IN' ##NO_TEXT.
  data R_PR_FCCONI type ref to ZCL_VCXI_P2SS_PR_FCCONI .

  methods HANDLE_UC_FCPROP
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_SCSTR
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_P2SS_UI_FCCONI definition
class ZCL_VCXI_P2SS_UI_FCCONI implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_P2SS_UI_FCCONI_BIND'
      exporting
        IR_UI_FCCONI = ME
      importing
        ES_DYNP      = ME->S_DYNP.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_FCCONI.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DO_USER_COMMAND.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    case IF_UCOMM.
***   Folding Carton Property Popup
      when C_UC_FCPROP_IN.
        ME->HANDLE_UC_FCPROP( ).

***   Scores String Popup
      when C_UC_SCSTR_IN.
        ME->HANDLE_UC_SCSTR( ).

    endcase.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_FCCONI.

  endmethod.
  method HANDLE_UC_FCPROP.

    data: LS_SURWT type ZVCXI_PXS_S_SURWT.
    data: LR_BL_FCBPROP type ref to ZCL_VCXI_P2SS_BL_FCBPROP,
          LR_BL_FCFPROP type ref to ZCL_VCXI_P2SS_BL_FCFPROP.


*** Get Folding Carton Property (Inbound)
    LR_BL_FCBPROP = ME->R_PR_FCCONI->ZIF_VCXI_P2SS_COMM_NEXT~GET_FCBPX( ).
    LR_BL_FCFPROP = ME->R_PR_FCCONI->ZIF_VCXI_P2SS_COMM_NEXT~GET_FCFPX( ).
    LS_SURWT = ME->R_PR_FCCONI->ZIF_VCXI_P2SS_COMM_NEXT~GET_SURWT( ).

*** Start Popup
    ZCL_VCXI_P2SS_POPUP_FCPROP=>START( IR_BL_FCBPROP = LR_BL_FCBPROP
                                       IR_BL_FCFPROP = LR_BL_FCFPROP
                                       IF_WERKS      = ME->R_PR_FCCONI->GET_WERKS( )
                                       IS_SURWT      = LS_SURWT ).

  endmethod.
  method HANDLE_UC_SCSTR.

    data: LR_BL_FCBPROP type ref to ZCL_VCXI_P2SS_BL_FCBPROP.


*** Get Score String (Inbound)
    LR_BL_FCBPROP = ME->R_PR_FCCONI->ZIF_VCXI_P2SS_COMM_PREV~GET_FCBPX( ).
    check LR_BL_FCBPROP is bound.

*** Start Popup
    ZCL_VCXI_PXSS_POPUP_SCSTR=>START( IT_SCORE_COM = LR_BL_FCBPROP->T_SCORE ).
*                                      IT_TAPE_COM  = LR_BL_FCBPROP->T_TAPE_COM ).

  endmethod.
  method INIT_VIS.

    data: LR_BL_FCFPROP type ref to ZCL_VCXI_P2SS_BL_FCFPROP.


    SUPER->INIT_VIS( ).

    free: ME->S_FCCONI_VIS.
    ME->S_FCCONI_VIS-S_XCONI_VIS = ME->S_XCONI_VIS.

***--------------------------------------------------------------------------------------
*** Get Sheet Dimension (Inbound)
    ME->R_PR_FCCONI->ZIF_VCXI_P2SS_COMM_PREV~GET_SHTDIM( importing ES_SHTLE = ME->S_FCCONI_VIS-S_IOSEC_IN-S_SHTLE
                                                                   ES_SHTWI = ME->S_FCCONI_VIS-S_IOSEC_IN-S_SHTWI
                                                                   EF_GRADI = ME->S_FCCONI_VIS-S_IOSEC_IN-GRADI ).
*** Number Ups (Inbound)
    ME->S_FCCONI_VIS-S_IOSEC_IN-NOUPS = ME->R_PR_FCCONI->ZIF_VCXI_P2SS_COMM_PREV~GET_NOUPS( ).

*** Get Caliper, Paper Grade and Grammage (Inbound)
    LR_BL_FCFPROP = ME->R_PR_FCCONI->ZIF_VCXI_P2SS_COMM_PREV~GET_FCFPX( ).
    if LR_BL_FCFPROP is bound.
      ME->S_FCCONI_VIS-S_IOSEC_IN-S_CALIP = LR_BL_FCFPROP->S_CALIP.
      ME->S_FCCONI_VIS-S_IOSEC_IN-PAPGR   = LR_BL_FCFPROP->F_PAPGR.
      ME->S_FCCONI_VIS-S_IOSEC_IN-S_GRAMM = LR_BL_FCFPROP->S_GRAMM.
    endif.

  endmethod.
