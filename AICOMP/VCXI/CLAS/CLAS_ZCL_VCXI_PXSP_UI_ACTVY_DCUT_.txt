
class ZCL_VCXI_PXSP_UI_ACTVY_DCUT definition
  public
  inheriting from /VCXI/CL_VKSRU_CLSUI
  create public .

public section.

  data F_CURSOR type SCRFNAME .
  data S_ACTVY_DCUT_VIS type ZVCXI_PXSP_S_ACTVY_DCUT_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods SET_CURSOR
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  data R_GRID_MC_EMBMAT type ref to ZCL_VCXI_PXSP_GRID_MC_EMBMAT .
  data R_GRID_PCOUT type ref to ZCL_VCXI_P0SP_GRID_PCOUT .
  data R_PR_ACTVY_DCUT type ref to ZCL_VCXI_PXSP_PR_ACTVY_DCUT .

  methods INIT_VIS
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
private section.
endclass. "ZCL_VCXI_PXSP_UI_ACTVY_DCUT definition
class ZCL_VCXI_PXSP_UI_ACTVY_DCUT implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_PXSP_UI_ACTVY_DCUT_BIND'
      exporting
        IR_UI_ACTVY_DCUT = ME
      importing
        ES_DYNP          = ME->S_DYNP.

*** Create Grid for PC Outs
    create object ME->R_GRID_PCOUT
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_PCOUT'.

*** Create Grid for Embossing Material
    create object ME->R_GRID_MC_EMBMAT
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_MC_EMBMAT'.

  endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                      IR_UIGATE = IR_UIGATE
                      IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
  try.
      move IR_GATE->R_CLSPR ?to ME->R_PR_ACTVY_DCUT.
    catch CX_SY_MOVE_CAST_ERROR.
***   UI ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '801'.
  endtry.

endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

    if ME->R_GRID_PCOUT is bound.
      ME->R_GRID_PCOUT->DO_FREE( ).
      clear ME->R_GRID_PCOUT.
    endif.

    if ME->R_GRID_MC_EMBMAT is bound.
      ME->R_GRID_MC_EMBMAT->DO_FREE( ).
      clear ME->R_GRID_MC_EMBMAT.
    endif.

  endmethod.
  method DO_MODIFY_SCREEN.

    data: LF_LOCKED type        /VCXI/CKX_LOCKED,
          LF_INUSE  type        /VCXI/VKSR_INUSE.
    data:  LR_CX_CKX type ref to /VCXI/CX_CKX.

    SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                             IF_DYNNR = IF_DYNNR ).

    try.

        if IF_REPID eq ME->S_DYNP-REPID and
           IF_DYNNR eq ME->S_DYNP-DYNNR.

          LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
          LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***       Modify Scree using DynamicVC
          ZCL_VCXI_XCDR_SERVICE=>MODIFY_SCREEN( IR_GATE = ME->R_GATE ).

          loop at screen.
            if LF_LOCKED ne ABAP_TRUE or
               LF_INUSE  ne ABAP_TRUE.
              move 0 to SCREEN-INPUT.
            endif.
            modify screen.
          endloop.
        endif.

      catch /VCXI/CX_CKX into LR_CX_CKX.
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_PBO.

    ME->INIT_VIS( ).

    if ME->R_GRID_PCOUT is bound.
      ME->R_GRID_PCOUT->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_PCOUT->REFRESH_GRID( ).
    endif.

    if ME->R_GRID_MC_EMBMAT is bound.
      ME->R_GRID_MC_EMBMAT->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_MC_EMBMAT->REFRESH_GRID( ).
    endif.

    set cursor field ME->F_CURSOR.
    clear ME->F_CURSOR.

  endmethod.
  method DO_USER_COMMAND.

    if ME->R_GRID_PCOUT is bound.
      ME->R_GRID_PCOUT->CHECK_CHANGED_DATA( ).
    endif.

    if ME->R_GRID_MC_EMBMAT is bound.
      ME->R_GRID_MC_EMBMAT->CHECK_CHANGED_DATA( ).
    endif.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_ACTVY_DCUT.

  endmethod.
  method INIT_VIS.

    free: ME->S_ACTVY_DCUT_VIS.

***----------------------------------------------------------------------------
*** Get Number of Outs
    ME->S_ACTVY_DCUT_VIS-NOOUT = ME->R_PR_ACTVY_DCUT->GET_NOOUT( ).

  endmethod.
  method SET_CURSOR.

    data:  LF_CURSOR type SCRFNAME.

    get cursor field LF_CURSOR.
    if LF_CURSOR is not initial.
      move LF_CURSOR to ME->F_CURSOR.
    endif.

  endmethod.
