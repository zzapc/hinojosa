
class ZCL_VCXI_P4SS_PR_PRCOMP definition
  public
  inheriting from ZCL_VCXI_PXSS_PR_XCOMP
  create public .

public section.

  methods GET_PRVDR_PRFG
    exporting
      !ER_GATE type ref to /VCXI/CL_VKSR_GATE
    returning
      value(RR_PR_PRFG) type ref to ZCL_VCXI_P4SS_PR_PRFG
    raising
      /VCXI/CX_CKX .

  methods ZIF_VCXI_XCS_NGREP~FILL
    redefinition .
  methods GET_DESCR
    redefinition .
protected section.
private section.
endclass. "ZCL_VCXI_P4SS_PR_PRCOMP definition
class ZCL_VCXI_P4SS_PR_PRCOMP implementation.
  method GET_DESCR.

    data(LR_PR_PRFG) = ME->GET_PRVDR_PRFG( ).
    if LR_PR_PRFG is bound.
      RF_DESCR = LR_PR_PRFG->ZIF_VCXI_PXSS_FG~GET_MAKTX( ).
    else.
      RF_DESCR = ME->R_GATE->GET_DESCR( ).
    endif.

  endmethod.
  method GET_PRVDR_PRFG.

    data:  LT_GATE  type /VCXI/VKSR_TT_GATE.


    clear: ER_GATE,
           RR_PR_PRFG.

***------------------------------------------------------------------------------------------------
*** Find Provider for Specification
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_P4SS_PR_PRFG=>C_IPRVT_P4_PRFG
                                     IF_INUSE = ABAP_TRUE ).
    read table LT_GATE into ER_GATE index 1.
    check SY-SUBRC is initial.

    move ER_GATE->R_CLSPR ?to RR_PR_PRFG.

  endmethod.
  method ZIF_VCXI_XCS_NGREP~FILL.

    data: LF_GROUP_KEY type I,
          LF_NODE_KEY  type I.
    data: LS_XCONR     type ZVCXI_PXSS_S_XCONR.
    data: LT_GATE type /VCXI/VKSR_TT_GATE,
          LT_ATTR type CR_NETGRAPH_JSON->YT_ATTRIBUTE.
    data: LR_PR_PRFG type ref to ZCL_VCXI_P4SS_PR_PRFG,
          LR_GATE    type ref to /VCXI/CL_VKSR_GATE.


    check CR_NETGRAPH_JSON is bound.

*** Determine the Group Key for all contained Nodes
    LF_GROUP_KEY = CR_NETGRAPH_JSON->GET_NODE_KEY_BY_OBJECT( ME->R_GATE ).

***--------------------------------------------------------------------------------------
*** Get Procurement Finished Good
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_P4SS_PR_PRFG=>C_IPRVT_P4_PRFG
                                     IF_INUSE = ABAP_TRUE ).
    check LT_GATE is not initial.
    LR_GATE = LT_GATE[ 1 ].

*** Get Attribute Data
    CR_NETGRAPH_JSON->ADD_ATTR( exporting IF_VALUE = cast ZCL_VCXI_P4SS_PR_PRFG( LR_GATE->R_CLSPR )->ZIF_VCXI_PXSS_FG~GET_MAKTX( )
                                changing  CT_ATTR  = LT_ATTR ).

*** Add Node
    LF_NODE_KEY = CR_NETGRAPH_JSON->ADD_NODE( IF_TITLE     = LR_GATE->GET_DESCR( )
                                              IF_DESCR     = LR_GATE->GET_DESCR( )
                                              IF_ICON      = 'sap-icon://product'
                                              IF_STATUS    = CR_NETGRAPH_JSON->GET_STATUS_BY_GATE( ME->R_GATE )
                                              IF_GROUP_KEY = LF_GROUP_KEY
                                              IT_ATTR      = LT_ATTR ).

*** Register Gate to Node
    CR_NETGRAPH_JSON->REGISTER_TO_NODE( IR_OBJECT   = LR_GATE
                                        IF_NODE_KEY = LF_NODE_KEY ).

  endmethod.
endclass. "ZCL_VCXI_P4SS_PR_PRCOMP implementation
