
class ZCL_VCXI_FFSS_PR_CSTEP_PRINT definition
  public
  inheriting from ZCL_VCXI_FFSS_PR_CSTEP
  create public .

public section.

  interfaces ZIF_VCXI_XCSS_DOC_USER .
  interfaces ZIF_VCXI_XCSS_TUSER .

  constants C_IEVNT_I_INIT_INK type /VCXI/VKSC_IEVNT value 'INIT_INK' ##NO_TEXT.
  constants C_IEVNT_O_CTRQW_CHANGED type /VCXI/VKSC_IEVNT value 'CTRQW_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_DESDD_CHANGED type /VCXI/VKSC_IEVNT value 'DESDD_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_DESDO_CHANGED type /VCXI/VKSC_IEVNT value 'DESDO_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_NOACC_CHANGED type /VCXI/VKSC_IEVNT value 'NOACC_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_NOATC_CHANGED type /VCXI/VKSC_IEVNT value 'NOATC_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_NODDG_CHANGED type /VCXI/VKSC_IEVNT value 'NODDG_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_PRCAT_CHANGED type /VCXI/VKSC_IEVNT value 'PRCAT_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_PRRLE_CHANGED type /VCXI/VKSC_IEVNT value 'PRRLE_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_PRRWI_CHANGED type /VCXI/VKSC_IEVNT value 'PRRWI_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_PRTIN_CHANGED type /VCXI/VKSC_IEVNT value 'PRTIN_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_REBEH_CHANGED type /VCXI/VKSC_IEVNT value 'REBEH_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_RENEX_CHANGED type /VCXI/VKSC_IEVNT value 'RENEX_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_SETTN_CHANGED type /VCXI/VKSC_IEVNT value 'SETTN_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_TPRIN_CHANGED type /VCXI/VKSC_IEVNT value 'TPRIN_CHANGED' ##NO_TEXT.
  constants C_IPRVT_CSTEP_PRINT type /VCXI/VKSC_IPRVT value 'ZVCXI_FFSS_CSTEP_PRINT' ##NO_TEXT.
  constants C_LOAD_CONTEXT_DESDO type STRING value 'DESDO' ##NO_TEXT.
  constants C_PARID_CTRQW type /VCXI/VKS_PARID value 'ZVCXI_FF_CTRQW' ##NO_TEXT.
  constants C_PARID_DESDD type /VCXI/VKS_PARID value 'ZVCXI_FF_DESDD' ##NO_TEXT.
  constants C_PARID_DESDO type /VCXI/VKS_PARID value 'ZVCXI_FF_DESDO' ##NO_TEXT.
  constants C_PARID_NOACC type /VCXI/VKS_PARID value 'ZVCXI_FF_NOACC' ##NO_TEXT.
  constants C_PARID_NOATC type /VCXI/VKS_PARID value 'ZVCXI_FF_NOATC' ##NO_TEXT.
  constants C_PARID_NODDG type /VCXI/VKS_PARID value 'ZVCXI_FF_NODDG' ##NO_TEXT.
  constants C_PARID_PRCAT type /VCXI/VKS_PARID value 'ZVCXI_FF_PRCAT' ##NO_TEXT.
  constants C_PARID_PRRLE type /VCXI/VKS_PARID value 'ZVCXI_FF_PRRLE' ##NO_TEXT.
  constants C_PARID_PRRWI type /VCXI/VKS_PARID value 'ZVCXI_FF_PRRWI' ##NO_TEXT.
  constants C_PARID_PRTIN type /VCXI/VKS_PARID value 'ZVCXI_FF_PRTIN' ##NO_TEXT.
  constants C_PARID_REBEH type /VCXI/VKS_PARID value 'ZVCXI_FF_REBEH' ##NO_TEXT.
  constants C_PARID_RENEX type /VCXI/VKS_PARID value 'ZVCXI_FF_RENEX' ##NO_TEXT.
  constants C_PARID_SETTN type /VCXI/VKS_PARID value 'ZVCXI_FF_SETTN' ##NO_TEXT.
  constants C_PARID_TPRIN type /VCXI/VKS_PARID value 'ZVCXI_FF_TPRIN' ##NO_TEXT.

  methods CREATE_DESDO
    importing
      !IR_BL_DOC type ref to ZCL_VCXI_XCSS_BL_DOC
    raising
      /VCXI/CX_CKX .
  methods GET_CTRQW
    exporting
      !ES_CTRQW type ZVCXI_FFS_S_CTRQW
      !EF_SETBY type /VCXI/VKSR_SETBY
    raising
      /VCXI/CX_CKX .
  methods GET_DESDD
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
      !EF_FOVRR type ZVCXI_XCS_FOVRR
      !ET_DKTXT type ZVCXI_XCS_TT_DKTXT
    returning
      value(RF_DESDD) type ZVCXI_FFS_DESDD
    raising
      /VCXI/CX_CKX .
  methods GET_DESDO
    exporting
      !EF_DESDO type ZVCXI_FFS_DESDO
      !EF_SETBY type /VCXI/VKSR_SETBY
      !EF_RELEA type ABAP_BOOL
      !EF_LINK_ICON type ICON_D
      !EF_LINK_DESCR type /VCXI/CKX_DESCR
      !ER_BL_DOC type ref to ZCL_VCXI_XCSS_BL_DOC
    returning
      value(RF_DESDO) type ZVCXI_FFS_DESDO
    raising
      /VCXI/CX_CKX .
  methods GET_FOVRR_PRTIN
    returning
      value(RF_FOVRR) type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
  methods GET_NOACC
    exporting
      !EF_NOACC type ZVCXI_FFS_NOACC
      !EF_SETBY type /VCXI/VKSR_SETBY
    raising
      /VCXI/CX_CKX .
  methods GET_NOATC
    exporting
      !EF_NOATC type ZVCXI_FFS_NOATC
      !EF_SETBY type /VCXI/VKSR_SETBY
    raising
      /VCXI/CX_CKX .
  methods GET_NOCOL
    importing
      !IF_TINK type ZVCXI_FFS_TINK optional
    exporting
      !EF_DESCR type /VCXI/CKX_DESCR
      !ET_GATE type /VCXI/VKSR_TT_GATE
    returning
      value(RF_NOCOL) type ZVCXI_FFS_NOCOL
    raising
      /VCXI/CX_CKX .
  methods GET_NODDG
    exporting
      !EF_NODDG type ZVCXI_FFS_NODDG
      !EF_SETBY type /VCXI/VKSR_SETBY
    raising
      /VCXI/CX_CKX .
  methods GET_PD
    returning
      value(RS_PD) type ZVCXI_FFE_DMS_S_PD
    raising
      /VCXI/CX_CKX .
  methods GET_PRCAT
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RF_PRCAT) type ZVCXI_FFS_PRCAT
    raising
      /VCXI/CX_CKX .
  methods GET_PRRLE
    exporting
      !ES_PRRLE type ZVCXI_FFS_S_PRRLE
      !EF_SETBY type /VCXI/VKSR_SETBY
    raising
      /VCXI/CX_CKX .
  methods GET_PRRWI
    exporting
      !ES_PRRWI type ZVCXI_FFS_S_PRRWI
      !EF_SETBY type /VCXI/VKSR_SETBY
    raising
      /VCXI/CX_CKX .
  methods GET_PRTIN
    exporting
      !ES_PRTIN type ZVCXI_FFS_S_PRTIN
      !EF_SETBY type /VCXI/VKSR_SETBY
    raising
      /VCXI/CX_CKX .
  methods GET_PRVDR_DOC_MGR
    exporting
      !ER_GATE type ref to /VCXI/CL_VKSR_GATE
    returning
      value(RR_PR_DOC_MGR) type ref to ZCL_VCXI_XCSS_PR_DOC_MGR
    raising
      /VCXI/CX_CKX .
  methods GET_PRVDR_TINFO
    exporting
      !ER_GATE_TINFO type ref to /VCXI/CL_VKSR_GATE
      !EF_ID type /VCXI/CKX_ID
    returning
      value(RR_PR_TINFO) type ref to ZCL_VCXI_XCSS_PR_TINFO
    raising
      /VCXI/CX_CKX .
  methods GET_PRWID
    returning
      value(RS_PRWID) type ZVCXI_FFS_S_PRWID
    raising
      /VCXI/CX_CKX .
  methods GET_REBEH
    exporting
      !EF_REBEH type ZVCXI_FFS_REBEH
      !EF_SETBY type /VCXI/VKSR_SETBY
    raising
      /VCXI/CX_CKX .
  methods GET_RENEX
    exporting
      !EF_RENEX type ZVCXI_FFS_RENEX
      !EF_SETBY type /VCXI/VKSR_SETBY
    raising
      /VCXI/CX_CKX .
  methods GET_SETTN
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
      !EF_DESCR type /VCXI/CKX_DESCR
      !EF_FOTUT type ZVCXI_XCS_FOTUT
    returning
      value(RF_SETTN) type ZVCXI_FFS_SETTN
    raising
      /VCXI/CX_CKX .
  methods GET_TPRIN
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
      !EF_DESCR type /VCXI/CKX_DESCR
      !EF_PRTEC type ZVCXI_FFS_PRTEC
      !EF_FAPCA type ZVCXI_FFS_FAPCA
    returning
      value(RF_TPRIN) type ZVCXI_FFS_TPRIN
    raising
      /VCXI/CX_CKX .
  methods SET_CTRQW
    importing
      !IS_CTRQW type ZVCXI_FFS_S_CTRQW
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_DESDD
    importing
      !IF_DESDD type ZVCXI_FFS_DESDD optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_DESDO
    importing
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
      !IF_DESDO type ZVCXI_FFS_DESDO optional
    raising
      /VCXI/CX_CKX .
  methods SET_DESDO_DATA
    raising
      /VCXI/CX_CKX .
  methods SET_FOVRR_PRTIN
    importing
      !IF_FOVRR type ABAP_BOOL optional
    raising
      /VCXI/CX_CKX .
  methods SET_NOACC
    importing
      !IF_NOACC type ZVCXI_FFS_NOACC optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_NOATC
    importing
      !IF_NOATC type ZVCXI_FFS_NOATC optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_NOCOL
    importing
      !IF_NOCOL type ZVCXI_FFS_NOCOL
      !IF_TINK type ZVCXI_FFS_TINK optional
    returning
      value(RT_GATE) type /VCXI/VKSR_TT_GATE
    raising
      /VCXI/CX_CKX .
  methods SET_NODDG
    importing
      !IF_NODDG type ZVCXI_FFS_NODDG optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_PRCAT
    importing
      !IF_PRCAT type ZVCXI_FFS_PRCAT
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_PRRLE
    importing
      !IS_PRRLE type ZVCXI_FFS_S_PRRLE
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_PRRWI
    importing
      !IS_PRRWI type ZVCXI_FFS_S_PRRWI
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_PRTIN
    importing
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
      !IS_PRTIN type ZVCXI_FFS_S_PRTIN optional
    raising
      /VCXI/CX_CKX .
  methods SET_REBEH
    importing
      !IF_REBEH type ZVCXI_FFS_REBEH
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_RENEX
    importing
      !IF_RENEX type ZVCXI_FFS_RENEX
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_SETTN
    importing
      !IF_SETTN type ZVCXI_FFS_SETTN optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_TPRIN
    importing
      !IF_TPRIN type ZVCXI_FFS_TPRIN
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .

  methods GET_STATUS
    redefinition .
  methods GET_STATUS_MSG
    redefinition .
  methods HANDLE_EVENT
    redefinition .
  methods ZIF_VCXI_FFSS_COMM_NEXT~GET_FISTX
    redefinition .
  methods ZIF_VCXI_FFSS_COMM_NEXT~GET_FITIN
    redefinition .
  methods ZIF_VCXI_FFSS_COMM_NEXT~GET_RELEN
    redefinition .
  methods ZIF_VCXI_FFSS_COMM_NEXT~GET_SURWT
    redefinition .
  methods ZIF_VCXI_FFSS_COMM_NEXT~GET_TREQW
    redefinition .
  methods ZIF_VCXI_XCS_NGREP~FILL
    redefinition .
protected section.

  constants C_IEVNT_I_INIT_TINFO type /VCXI/VKSC_IEVNT value 'INIT_TINFO' ##NO_TEXT.
  constants C_IEVNT_I_LOAD_DATA_DESDO type /VCXI/VKSC_IEVNT value 'LOAD_DATA_DESDO' ##NO_TEXT.
  constants C_IEVNT_I_RECALC_DESDD type /VCXI/VKSC_IEVNT value 'RECALC_DESDD' ##NO_TEXT.
  constants C_IEVNT_I_RECALC_PRTIN type /VCXI/VKSC_IEVNT value 'RECALC_PRTIN' ##NO_TEXT.
  constants C_IEVNT_I_REQ_INK_DEFAULT type /VCXI/VKSC_IEVNT value 'REQ_INK_DEFAULT' ##NO_TEXT.
  constants C_IEVNT_O_INK_DEFAULT_REQ type /VCXI/VKSC_IEVNT value 'INK_DEFAULT_REQ' ##NO_TEXT.

  methods CHECK_DESDO_DATA
    importing
      !IR_MESSAGE type ref to /VCXI/CL_CKXM_MESSAGE optional
    returning
      value(RF_ELMST) type /VCXI/VKSR_ELMST
    raising
      /VCXI/CX_CKX .
  methods CHECK_REBEH
    returning
      value(RF_RETURN) type I
    raising
      /VCXI/CX_CKX .
  methods CHECK_RENEX
    returning
      value(RF_RETURN) type I
    raising
      /VCXI/CX_CKX .
  methods CHECK_TPRIN
    importing
      !IR_MESSAGE type ref to /VCXI/CL_CKXM_MESSAGE optional
    returning
      value(RF_ELMST) type /VCXI/VKSR_ELMST
    raising
      /VCXI/CX_CKX .
  methods GET_MS_INK_SURWT
    exporting
      !ES_SURWT type ZVCXI_F0S_S_SURWT
    raising
      /VCXI/CX_CKX .
  methods HANDLE_INIT_TINFO
    raising
      /VCXI/CX_CKX .
  methods HANDLE_LOAD_DATA_DESDO
    raising
      /VCXI/CX_CKX .
  methods HANDLE_RECALC_DESDD
    raising
      /VCXI/CX_CKX .
  methods HANDLE_RECALC_PRTIN
    raising
      /VCXI/CX_CKX .
  methods IS_TINFO_NEEDED
    returning
      value(RF_NEEDED) type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
  methods CHECK_INK
    importing
      !IR_MESSAGE type ref to /VCXI/CL_CKXM_MESSAGE optional
    returning
      value(RF_ELMST) type /VCXI/VKSR_ELMST
    raising
      /VCXI/CX_CKX .
  methods HANDLE_INIT_INK
    raising
      /VCXI/CX_CKX .
  methods CHECK_PRWID
    importing
      !IR_MESSAGE type ref to /VCXI/CL_CKXM_MESSAGE optional
    returning
      value(RF_ELMST) type /VCXI/VKSR_ELMST
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_FFSS_PR_CSTEP_PRINT definition
class ZCL_VCXI_FFSS_PR_CSTEP_PRINT implementation.
  method CHECK_DESDO_DATA.

    data: LF_DESDO type ZVCXI_FFS_DESDO,
          LF_RELEA type ABAP_BOOL.
    data: LT_PDINK     type ZVCXI_FFE_DMS_TT_PDINK.
    data: LR_DMSEXT_PD type ref to ZCL_VCXI_FFE_DMS_DMSEXT_PD,
          LR_GATE      type ref to /VCXI/CL_VKSR_GATE,
          LR_PR_MS_INK type ref to ZCL_VCXI_FFSS_PR_MS_INK.


*** Ensure a Design Document has been specified and is released
    LF_DESDO = ME->GET_DESDO( importing EF_RELEA = LF_RELEA ).
    check LF_DESDO is not initial.

***--------------------------------------------------------------------------------------
*** Get instance of DMS Data Extension for Print Design
    try.
        LR_DMSEXT_PD ?= ZCL_VCXI_FFE_DMS_DMSEXT_PD=>/VCXI/IF_CKX_OBJECT~GET_INSTANCE( IF_GUID = conv #( LF_DESDO ) ).
      catch CX_SY_MOVE_CAST_ERROR into data(LR_CX_SY_MOVE_CAST_ERROR).
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_SY_MOVE_CAST_ERROR ).
    endtry.

***--------------------------------------------------------------------------------------
*** Check Plant of Design Document matches Configuration
    if ME->GET_WERKS( ) ne LR_DMSEXT_PD->S_PD-WERKS.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
      if IR_MESSAGE is bound.
***     Plant of Print Design doesn't match Printing Step.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '361' ).
      endif.
      return.
    endif.

***--------------------------------------------------------------------------------------
*** Further Check only for released Design Documents
    if LF_RELEA eq ABAP_TRUE.
***   Get Inks Information
      loop at ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_FFSS_PR_MS_INK=>C_IPRVT_MS_INK
                                     IF_INUSE  = ABAP_TRUE
                                     IF_SORTED = ABAP_TRUE ) into LR_GATE.
        append cast ZCL_VCXI_FFSS_PR_MS_INK( LR_GATE->R_CLSPR )->GET_PDINK( ) to LT_PDINK.
      endloop.

***   Adjust Units
      loop at LT_PDINK assigning field-symbol(<S_PDINK>).
        check LINE_EXISTS( LR_DMSEXT_PD->T_PDINK[ SY-TABIX ] ).
        ZCL_VCXI_XCS_SERVICE_UNIT=>CONVERT_UNIT( exporting IF_VALUE_IN  = <S_PDINK>-APWGT
                                                           IF_UOM_IN    = <S_PDINK>-APWGT_UOM
                                                           IF_UOM_OUT   = LR_DMSEXT_PD->T_PDINK[ SY-TABIX ]-APWGT_UOM
                                                 importing EF_VALUE_OUT = <S_PDINK>-APWGT
                                                           EF_UOM_OUT   = <S_PDINK>-APWGT_UOM ).
      endloop.

***   Check Print Design Data
      if LR_DMSEXT_PD->S_PD    eq ME->GET_PD( ) and
         LR_DMSEXT_PD->T_PDINK eq LT_PDINK.
        RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G.
        if IR_MESSAGE is bound.
***       Print Design Information match.
          IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                                   IF_MSGTY = 'S'
                                   IF_MSGNO = '359' ).
        endif.

      else.
        RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
        if IR_MESSAGE is bound.
***       Print Design Information don't match.
          IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                                   IF_MSGTY = 'E'
                                   IF_MSGNO = '360' ).
        endif.
      endif.
    endif.

  endmethod.
  method CHECK_INK.

    data: LF_MAINTAINED type ABAP_BOOL.
    data: LR_GATE type ref to /VCXI/CL_VKSR_GATE.


***--------------------------------------------------------------------------------------
*** Ignore Check if Number of Colors is maitained
    if ME->GET_NOCOL( ) eq 0.
      loop at ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_FFSS_PR_MS_DINK=>C_IPRVT_MS_DINK
                                     IF_INUSE = ABAP_TRUE ) into LR_GATE.
        if cast ZCL_VCXI_FFSS_PR_MS_DINK( LR_GATE->R_CLSPR )->GET_COVRG( )-COVRG ne 0.
          LF_MAINTAINED = ABAP_TRUE.
        endif.
      endloop.

      if LF_MAINTAINED ne ABAP_TRUE.
        RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
        if IR_MESSAGE is bound.
***       Ink Information not maintained.
          IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                                   IF_MSGTY = 'E'
                                   IF_MSGNO = '362' ).
        endif.
      endif.
    endif.

***--------------------------------------------------------------------------------------
*** Check Min/Max requirement per Ink Types of Print Type
    loop at ZCL_VCXI_FFSS_CUST_PRINT=>GET_TPRIN_TINK( IF_TPRIN = ME->GET_TPRIN( ) ) into data(LS_TINK_INFO)
                                                                                   where QTYMI ne 0
                                                                                    or   QTYMA ne 0.
      data(LF_NOCOL) = ME->GET_NOCOL( IF_TINK = LS_TINK_INFO-TINK ).

***   Check Minimum
      if LS_TINK_INFO-QTYMI ne 0 and
         LS_TINK_INFO-QTYMI gt LF_NOCOL.
        RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
        if IR_MESSAGE is bound.
***       Required minimum for &1 is &2.
          IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                                   IF_MSGTY = 'E'
                                   IF_MSGNO = '363'
                                   IF_MSGV1 = LS_TINK_INFO-TINK
                                   IF_MSGV2 = LS_TINK_INFO-QTYMI ).
        endif.
      endif.

***   Check Maximum
      if LS_TINK_INFO-QTYMA ne 0 and
         LS_TINK_INFO-QTYMA lt LF_NOCOL.
        RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
        if IR_MESSAGE is bound.
***       Allowed maximum for &1 is &2.
          IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                                   IF_MSGTY = 'E'
                                   IF_MSGNO = '364'
                                   IF_MSGV1 = LS_TINK_INFO-TINK
                                   IF_MSGV2 = LS_TINK_INFO-QTYMA ).
        endif.
      endif.
    endloop.

  endmethod.
  method CHECK_PRWID.

    data(LS_PRWID) = ME->GET_PRWID( ).
    ME->ZIF_VCXI_FFSS_COMM_NEXT~GET_FIWID( importing ES_FIWID = data(LS_FIWID) ).

    if ZCL_VCXI_XCS_SERVICE_CALC=>COMPARE_WITH_ANY( IF_CALCV_1     = LS_PRWID-PRWID
                                                    IF_CALCV_UOM_1 = LS_PRWID-PRWID_UOM
                                                    IF_CALCV_2     = LS_FIWID-FIWID
                                                    IF_CALCV_UOM_2 = LS_FIWID-FIWID_UOM
                                                    IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_LE ).
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G.
      if IR_MESSAGE is bound.
***     Repeat Width * Repeat Next is smaller than Outbound Film Width.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                                 IF_MSGTY = 'S'
                                 IF_MSGNO = '357' ).
      endif.
    else.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
      if IR_MESSAGE is bound.
***     Repeat Width * Repeat Next is not smaller than Outbound Film Width.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '358' ).
      endif.
    endif.

  endmethod.
  method CHECK_REBEH.

*** 0 Repeat Behind is maintained.
*** 1 Repeat Behind is not maintained.

    data: LF_REBEH type ZVCXI_FFS_REBEH.

    move 0 to RF_RETURN.

    ME->GET_REBEH( importing EF_REBEH = LF_REBEH ).

    if LF_REBEH is initial.
      move 1 to RF_RETURN.
    endif.

  endmethod.
  method CHECK_RENEX.

*** 0 Repeat Next is maintained.
*** 1 Repeat Next is not maintained.

    data: LF_RENEX type ZVCXI_FFS_RENEX.

    move 0 to RF_RETURN.

    ME->GET_RENEX( importing EF_RENEX = LF_RENEX ).

    if LF_RENEX is initial.
      move 1 to RF_RETURN.
    endif.

  endmethod.
  method CHECK_TPRIN.

*** Check Print Type
    if ME->GET_TPRIN( ) is not initial.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G.
      if IR_MESSAGE is bound.
***     Print Type is maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                                 IF_MSGTY = 'S'
                                 IF_MSGNO = '351' ).
      endif.

    else.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
      if IR_MESSAGE is bound.
***     Print Type is not maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '352' ).
      endif.
    endif.

  endmethod.
  method CREATE_DESDO.

    data: LT_PDINK type ZVCXI_FFE_DMS_TT_PDINK.
    data: LR_PR_DOC_MGR type ref to ZCL_VCXI_XCSS_PR_DOC_MGR,
          LR_DMSEXT_PD  type ref to ZCL_VCXI_FFE_DMS_DMSEXT_PD,
          LR_GATE       type ref to /VCXI/CL_VKSR_GATE,
          LR_PR_MS_INK  type ref to ZCL_VCXI_FFSS_PR_MS_INK.


***--------------------------------------------------------------------------------------
*** Get Document Manager Provider
    LR_PR_DOC_MGR = ME->GET_PRVDR_DOC_MGR( ).
    check LR_PR_DOC_MGR is bound.

*** Add Document Instance
    LR_PR_DOC_MGR->ADD_DOCS( IT_DOC_KEY = value #( ( IR_BL_DOC->GET_DOC_KEY( ) ) ) ).

***--------------------------------------------------------------------------------------
*** Get instance of DMS Data Extension for Print Design
    try.
        LR_DMSEXT_PD ?= ZCL_VCXI_FFE_DMS_DMSEXT_PD=>/VCXI/IF_CKX_OBJECT~GET_INSTANCE( IF_GUID = ZCL_VCXI_XCSS_BL_DOC=>ENCRYPT_GUID( IF_DOKAR = IR_BL_DOC->F_DOKAR
                                                                                                                                    IF_DOKNR = IR_BL_DOC->F_DOKNR
                                                                                                                                    IF_DOKTL = IR_BL_DOC->F_DOKTL
                                                                                                                                    IF_DOKVR = IR_BL_DOC->F_DOKVR ) ).
      catch CX_SY_MOVE_CAST_ERROR into data(LR_CX_SY_MOVE_CAST_ERROR).
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_SY_MOVE_CAST_ERROR ).
    endtry.

*** Collect Inks Information
    loop at ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_FFSS_PR_MS_DINK=>C_IPRVT_MS_DINK
                                   IF_INUSE  = ABAP_TRUE
                                   IF_SORTED = ABAP_TRUE ) into LR_GATE.
      append cast ZCL_VCXI_FFSS_PR_MS_DINK( LR_GATE->R_CLSPR )->GET_PDINK( ) to LT_PDINK.
    endloop.
    loop at ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_FFSS_PR_MS_INK=>C_IPRVT_MS_INK
                                   IF_INUSE  = ABAP_TRUE
                                   IF_SORTED = ABAP_TRUE ) into LR_GATE.
      append cast ZCL_VCXI_FFSS_PR_MS_INK( LR_GATE->R_CLSPR )->GET_PDINK( ) to LT_PDINK.
    endloop.

*** Update Values
    LR_DMSEXT_PD->SET_PD( IS_PD = ME->GET_PD( ) ).
    LR_DMSEXT_PD->SET_PDINK( IT_PDINK = LT_PDINK ).

*** Call Save in order to set the values during saving
    LR_DMSEXT_PD->SAVE( IF_UPDATE_TASK = ABAP_TRUE ).

***--------------------------------------------------------------------------------------
*** Set Design Document
    ME->SET_DESDO( IF_DESDO = conv #( ZCL_VCXI_XCSS_BL_DOC=>ENCRYPT_GUID( IF_DOKAR = IR_BL_DOC->F_DOKAR
                                                                          IF_DOKNR = IR_BL_DOC->F_DOKNR
                                                                          IF_DOKTL = IR_BL_DOC->F_DOKTL
                                                                          IF_DOKVR = IR_BL_DOC->F_DOKVR ) ) ).

  endmethod.
  method GET_CTRQW.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_CTRQW ).
    move LS_PARAM-ATFLV to ES_CTRQW-CTRQW.
    move LS_PARAM-UNIT  to ES_CTRQW-CTRQW_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_DESDD.

    data: LS_PARAM   type /VCXI/VKSR_S_PARAM.
    data: LR_BL_DOC  type ref to ZCL_VCXI_XCSS_BL_DOC.


    ME->GET_DESDO( importing ER_BL_DOC = LR_BL_DOC ).
    if LR_BL_DOC is not bound.
      LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_DESDD ).
      RF_DESDD = LS_PARAM-STRNG.
      EF_SETBY = LS_PARAM-SETBY.

      if EF_FOVRR is requested.
        EF_FOVRR = cond #( when ZCL_VCXI_F0S_BL_MAKTX=>GET_FM( exporting IR_GATE = ME->R_GATE ) is initial then ABAP_UNDEFINED
                           when EF_SETBY eq /VCXI/CL_VKSR_GATE=>C_SETBY_USER                               then ABAP_TRUE
                                                                                                           else ABAP_FALSE ).
      endif.

***   Multi-Language Description
      if ET_DKTXT is requested.
        append value #( LANGU = SY-LANGU  "Logon Language
                        DKTXT = RF_DESDD
                      ) to ET_DKTXT.
        ME->GET_WERKS( importing EF_LANGU = data(LF_LANGU) ).
        if LF_LANGU ne SY-LANGU.
          append value #( LANGU = LF_LANGU  "Plant Language
                          DKTXT = RF_DESDD
                        ) to ET_DKTXT.
        endif.
      endif.
    else.
      RF_DESDD = LR_BL_DOC->GET_DESCR( importing ET_DKTXT = ET_DKTXT ).
    endif.

  endmethod.
  method GET_DESDO.

    data: LS_PARAM   type /VCXI/VKSR_S_PARAM,
          LS_DOC_KEY type ZVCXI_XCS_S_DOC_KEY.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_DESDO ).
    move LS_PARAM-STRNG to RF_DESDO.
    move LS_PARAM-SETBY to EF_SETBY.

    EF_DESDO = RF_DESDO.

    check RF_DESDO is not initial.

***----------------------------------------------------------------------------
*** Release Status of Design Document
    LS_DOC_KEY = ZCL_VCXI_XCSS_BL_DOC=>DECRYPT_GUID( IF_GUID = conv #( RF_DESDO ) ).
    ER_BL_DOC = ZCL_VCXI_XCSS_BL_DOC=>GET_INSTANCE( IF_DOKAR = LS_DOC_KEY-DOKAR
                                                    IF_DOKNR = LS_DOC_KEY-DOKNR
                                                    IF_DOKTL = LS_DOC_KEY-DOKTL
                                                    IF_DOKVR = LS_DOC_KEY-DOKVR ).
    check ER_BL_DOC is bound.
    EF_RELEA = ER_BL_DOC->F_FRKNZ.

    if EF_RELEA eq ABAP_TRUE.
      EF_LINK_ICON  = ICON_CONNECT.
      EF_LINK_DESCR = text-T01.
    else.
      EF_LINK_ICON  = ICON_DISCONNECT.
      EF_LINK_DESCR = text-T02.
    endif.

  endmethod.
  method GET_FOVRR_PRTIN.

    data: LF_SETBY type /VCXI/VKSR_SETBY.

*** Get Setby
    ME->GET_PRTIN( importing EF_SETBY = LF_SETBY ).

    if LF_SETBY eq /VCXI/CL_VKSR_GATE=>C_SETBY_USER.
      move ABAP_TRUE to RF_FOVRR.
    else.
      move ABAP_FALSE to RF_FOVRR.
    endif.

  endmethod.
  method GET_MS_INK_SURWT.

    data: LS_SURWT type ZVCXI_F0S_S_SURWT,
          LS_COVRG type ZVCXI_FFS_S_COVRG.
    data: LT_GATE  type /VCXI/VKSR_TT_GATE.
    data: LR_GATE  type ref to /VCXI/CL_VKSR_GATE.


*** Get all Inks
    append lines of ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_FFSS_PR_MS_INK=>C_IPRVT_MS_INK
                                           IF_INUSE = ABAP_TRUE ) to LT_GATE.
    append lines of ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_FFSS_PR_MS_DINK=>C_IPRVT_MS_DINK
                                           IF_INUSE = ABAP_TRUE ) to LT_GATE.

*** Add Surface Weigth of all Inks
    loop at LT_GATE into LR_GATE.
      case type of LR_GATE->R_CLSPR.
***     Analog Ink
        when type ZCL_VCXI_FFSS_PR_MS_INK into data(LR_PR_MS_INK).
***       Get Ink Surface Weight and Coverage
          LS_SURWT = LR_PR_MS_INK->GET_SURWT( ).
          LS_COVRG = LR_PR_MS_INK->GET_COVRG( ).

***     Digital Ink
        when type ZCL_VCXI_FFSS_PR_MS_DINK into data(LR_PR_MS_DINK).
***       Get Ink Surface Weight and Coverage
          LS_SURWT = LR_PR_MS_DINK->GET_SURWT( ).
          LS_COVRG = LR_PR_MS_DINK->GET_COVRG( ).

        when others.
          continue.
      endcase.

***   Adjust Surface Weight by Coverage
      ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_SURWT-SURWT
                                                          IF_CALCV_UOM_1 = LS_SURWT-SURWT_UOM
                                                          IF_CALCV_2     = LS_COVRG-COVRG
                                                          IF_CALCV_UOM_2 = LS_COVRG-COVRG_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_MULTIPLY
                                                          IF_UOM         = LS_SURWT-SURWT_UOM
                                                importing EF_CALCV       = LS_SURWT-SURWT
                                                          EF_CALCV_UOM   = LS_SURWT-SURWT_UOM ).

***   Add to total Surface Weight
      if ES_SURWT-SURWT_UOM is initial.
        move LS_SURWT to ES_SURWT.
      else.
        ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_SURWT-SURWT
                                                            IF_CALCV_UOM_1 = LS_SURWT-SURWT_UOM
                                                            IF_CALCV_2     = ES_SURWT-SURWT
                                                            IF_CALCV_UOM_2 = ES_SURWT-SURWT_UOM
                                                            IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_ADD
                                                            IF_UOM         = ES_SURWT-SURWT_UOM
                                                  importing EF_CALCV       = ES_SURWT-SURWT
                                                            EF_CALCV_UOM   = ES_SURWT-SURWT_UOM ).
      endif.
    endloop.

  endmethod.
  method GET_NOACC.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_NOACC ).
    move LS_PARAM-ATFLV to EF_NOACC.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_NOATC.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_NOATC ).
    move LS_PARAM-ATFLV to EF_NOATC.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_NOCOL.

    types: begin of YS_TINK,
             TINK  type ZVCXI_FFS_TINK,
             NOCOL type ZVCXI_FFS_NOCOL,
           end   of YS_TINK.

    data: LF_PRTEC type ZVCXI_FFS_PRTEC,
          LF_TINK  type ZVCXI_FFS_TINK.
    data: LT_TINK type table of YS_TINK.
    data: LR_GATE       type ref to /VCXI/CL_VKSR_GATE,
          LR_PR_MS_INK  type ref to ZCL_VCXI_FFSS_PR_MS_INK,
          LR_PR_MS_DINK type ref to ZCL_VCXI_FFSS_PR_MS_DINK.
    field-symbols: <S_TINK> type YS_TINK.


***--------------------------------------------------------------------------------------
*** Get Print Technology of Ink Type
    LF_PRTEC = cond #( when IF_TINK is supplied then ZCL_VCXI_FFSS_CUST_PRINT=>GET_TINK_PRTEC( IF_TINK = IF_TINK )
                                                else ZCL_VCXI_FFSS_CUST_PRINT=>C_PRTEC_ANALOG ).
    check LF_PRTEC is not initial.

*** Get all Inks (Analog or Digital)
    ET_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = switch #( LF_PRTEC when ZCL_VCXI_FFSS_CUST_PRINT=>C_PRTEC_ANALOG  then ZCL_VCXI_FFSS_PR_MS_INK=>C_IPRVT_MS_INK
                                                                   when ZCL_VCXI_FFSS_CUST_PRINT=>C_PRTEC_DIGITAL then ZCL_VCXI_FFSS_PR_MS_DINK=>C_IPRVT_MS_DINK )
                                     IF_INUSE = ABAP_TRUE ).

***--------------------------------------------------------------------------------------
*** Filter by Ink Type
    if IF_TINK is supplied.
      loop at ET_GATE into LR_GATE.
        case type of LR_GATE->R_CLSPR.
          when type ZCL_VCXI_FFSS_PR_MS_INK into LR_PR_MS_INK.
            if LR_PR_MS_INK->GET_TINK( ) ne IF_TINK.
              delete ET_GATE.
            endif.
          when type ZCL_VCXI_FFSS_PR_MS_DINK into LR_PR_MS_DINK.
            if LR_PR_MS_DINK->GET_TINK( ) ne IF_TINK.
              delete ET_GATE.
            endif.
          when others.
            delete ET_GATE.
        endcase.
      endloop.
    endif.

***--------------------------------------------------------------------------------------
*** Return Number of Inks
    move LINES( ET_GATE ) to RF_NOCOL.

*** Description
    if EF_DESCR is requested.
      loop at ET_GATE into LR_GATE.
        case type of LR_GATE->R_CLSPR.
          when type ZCL_VCXI_FFSS_PR_MS_INK into LR_PR_MS_INK.
            LF_TINK = LR_PR_MS_INK->GET_TINK( ).
          when type ZCL_VCXI_FFSS_PR_MS_DINK into LR_PR_MS_DINK.
            LF_TINK = LR_PR_MS_DINK->GET_TINK( ).
          when others.
            clear LF_TINK.
        endcase.

        read table LT_TINK assigning <S_TINK> with key TINK = LF_TINK.
        if SY-SUBRC ne 0.
          append value #( TINK = LF_TINK ) to LT_TINK assigning <S_TINK>.
        endif.
        add 1 to <S_TINK>-NOCOL.
      endloop.
      loop at LT_TINK assigning <S_TINK>.
        if SY-TABIX eq 1.
          EF_DESCR = |{ ZCL_VCXI_FFSS_CUST_PRINT=>GET_TINK_DESCR( IF_TINK = <S_TINK>-TINK ) } { <S_TINK>-NOCOL alpha = out }|.
        else.
          EF_DESCR = |{ EF_DESCR } / { ZCL_VCXI_FFSS_CUST_PRINT=>GET_TINK_DESCR( IF_TINK = <S_TINK>-TINK ) } { <S_TINK>-NOCOL alpha = out }|.
        endif.
      endloop.
    endif.

  endmethod.
  method GET_NODDG.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_NODDG ).
    move LS_PARAM-ATFLV to EF_NODDG.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_PD.

    data: LS_PRWID type ZVCXI_FFS_S_PRWID.


    RS_PD-WERKS = ME->GET_WERKS( ).
    RS_PD-TPRIN = ME->GET_TPRIN( ).
    RS_PD-PRCAT = ME->GET_PRCAT( ).
    ME->GET_RENEX( importing EF_RENEX = RS_PD-RENEX ).
    ME->GET_REBEH( importing EF_REBEH = RS_PD-REBEH ).
    ME->GET_PRRWI( importing ES_PRRWI = RS_PD-S_PRRWI ).
    ME->GET_PRRLE( importing ES_PRRLE = RS_PD-S_PRRLE ).
    RS_PD-SETTN   = ME->GET_SETTN( ).

    LS_PRWID = ME->GET_PRWID( ).
    RS_PD-S_OPRIW = value #( OPRIW     = LS_PRWID-PRWID
                             OPRIW_UOM = LS_PRWID-PRWID_UOM ).

  endmethod.
  method GET_PRCAT.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PRCAT ).
    move LS_PARAM-ATWRT to RF_PRCAT.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_PRRLE.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PRRLE ).
    move LS_PARAM-ATFLV to ES_PRRLE-PRRLE.
    move LS_PARAM-UNIT  to ES_PRRLE-PRRLE_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_PRRWI.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PRRWI ).
    move LS_PARAM-ATFLV to ES_PRRWI-PRRWI.
    move LS_PARAM-UNIT  to ES_PRRWI-PRRWI_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_PRTIN.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PRTIN ).
    move LS_PARAM-ATFLV to ES_PRTIN-PRTIN.
    move LS_PARAM-UNIT  to ES_PRTIN-PRTIN_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_PRVDR_DOC_MGR.

    data:  LT_GATE  type /VCXI/VKSR_TT_GATE.

    clear: ER_GATE,
           RR_PR_DOC_MGR.

***------------------------------------------------------------------------------------------------
*** Find Provider for Document Manager
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCSS_PR_DOC_MGR=>C_IPRVT_DOC_MGR
                                     IF_INUSE = ABAP_TRUE ).
    read table LT_GATE into ER_GATE index 1.
    check SY-SUBRC is initial.

    move ER_GATE->R_CLSPR ?to RR_PR_DOC_MGR.

  endmethod.
  method GET_PRVDR_TINFO.

    data: LT_GATE         type        /VCXI/VKSR_TT_GATE.
    data: LR_GATE         type ref to /VCXI/CL_VKSR_GATE.


*** Check if Provider exists
    data(LT_PRVDR_DEF) = ME->R_GATE->GET_PRVDR_DEF( IF_IPRVT = ZCL_VCXI_XCSS_PR_TINFO=>C_IPRVT_XCS_TINFO ).
    EF_ID = value #( LT_PRVDR_DEF[ 1 ]-ID optional ).
    check EF_ID is not initial.

***--------------------------------------------------------------------------------------
*** Get Tooling Information provider
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCSS_PR_TINFO=>C_IPRVT_XCS_TINFO
                                     IF_INUSE = ABAP_TRUE ).

***--------------------------------------------------------------------------------------
*** Get Tooling Information Reference
    loop at LT_GATE into LR_GATE.
      try.
          move LR_GATE->R_CLSPR ?to RR_PR_TINFO.
          check RR_PR_TINFO is bound.
          move LR_GATE to ER_GATE_TINFO.
          exit.

        catch CX_SY_MOVE_CAST_ERROR.
          clear: RR_PR_TINFO, ER_GATE_TINFO.
      endtry.
    endloop.

  endmethod.
  method GET_PRWID.

    data: LF_RENEX type ZVCXI_FFS_RENEX.
    data: LS_PRRWI type ZVCXI_FFS_S_PRRWI,
          LS_FIWID type ZVCXI_F0S_S_FIWID.


    ME->GET_PRRWI( importing ES_PRRWI = LS_PRRWI ).
    ME->GET_RENEX( importing EF_RENEX = LF_RENEX ).

    if LS_PRRWI-PRRWI is not initial.
      RS_PRWID-PRWID     = LS_PRRWI-PRRWI * LF_RENEX.
      RS_PRWID-PRWID_UOM = LS_PRRWI-PRRWI_UOM.
    else.
***   Use Inbound Film Width
      ME->ZIF_VCXI_FFSS_COMM_PREV~GET_FIWID( importing ES_FIWID = LS_FIWID ).
      RS_PRWID-PRWID     = LS_FIWID-FIWID.
      RS_PRWID-PRWID_UOM = LS_FIWID-FIWID_UOM.
    endif.

  endmethod.
  method GET_REBEH.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_REBEH ).
    move LS_PARAM-ATFLV to EF_REBEH.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_RENEX.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_RENEX ).
    move LS_PARAM-ATFLV to EF_RENEX.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_SETTN.

    data: LS_PARAM type /VCXI/VKSR_S_PARAM,
          LS_MAKT  type MAKT.


***----------------------------------------------------------------------------
*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SETTN ).
    move LS_PARAM-ATWRT to RF_SETTN.
    move LS_PARAM-SETBY to EF_SETBY.

    check RF_SETTN is not initial.

***----------------------------------------------------------------------------
    if EF_DESCR is requested.
***   Identify Description
      call function 'MAKT_SINGLE_READ'
        exporting
          MATNR  = RF_SETTN
          SPRAS  = SY-LANGU
        importing
          WMAKT  = LS_MAKT
        exceptions
          others = 0.
      EF_DESCR = LS_MAKT-MAKTX.
    endif.

    if EF_FOTUT is requested.
***   Batch-managed Tool Sets are considered One-Time Usage
      data(LF_WERKS) = ME->GET_WERKS( ).
      select single XCHPF
             into @data(LF_XCHPF)
             from MARC
            where MATNR eq @RF_SETTN
             and  WERKS eq @LF_WERKS.
      if SY-SUBRC eq 0 and
         LF_XCHPF eq ABAP_TRUE.
        EF_FOTUT = ABAP_TRUE.
      endif.
    endif.

  endmethod.
  method GET_STATUS.

***--------------------------------------------------------------------------------------
*** Super Status
    RF_ELMST = SUPER->GET_STATUS( ).

***--------------------------------------------------------------------------------------
*** Check Print Type
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = ME->CHECK_TPRIN( ) ).

***--------------------------------------------------------------------------------------
*** Check Repeat Next
    if ME->CHECK_RENEX( ) eq 0.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                     IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G ).
    else.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                     IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R ).
    endif.

***--------------------------------------------------------------------------------------
*** Check Repeat Behind
    if ME->CHECK_REBEH( ) eq 0.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                     IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G ).
    else.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                     IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R ).
    endif.

***--------------------------------------------------------------------------------------
*** Check Print Width
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = ME->CHECK_PRWID( ) ).

*** Check Design Document Data
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = ME->CHECK_DESDO_DATA( ) ).

*** Check Ink
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = ME->CHECK_INK( ) ).

  endmethod.
  method GET_STATUS_MSG.

***--------------------------------------------------------------------------------------
*** Super Status Messages
    SUPER->GET_STATUS_MSG( IR_MESSAGE = IR_MESSAGE ).

***--------------------------------------------------------------------------------------
*** Check Print Type
    ME->CHECK_TPRIN( IR_MESSAGE = IR_MESSAGE ).

***--------------------------------------------------------------------------------------
*** Check Repeat Next
    if ME->CHECK_RENEX( ) eq 0.
***   Repeat next is maintained.
      IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                               IF_MSGTY = 'S'
                               IF_MSGNO = '353' ).
    else.
***   Repeat next is not maintained.
      IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                               IF_MSGTY = 'E'
                               IF_MSGNO = '354' ).
    endif.

***--------------------------------------------------------------------------------------
*** Check Repeat Behind
    if ME->CHECK_REBEH( ) eq 0.
***   Repeat behind is maintained.
      IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                               IF_MSGTY = 'S'
                               IF_MSGNO = '355' ).
    else.
***   Repeat behind is not maintained.
      IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                               IF_MSGTY = 'E'
                               IF_MSGNO = '356' ).
    endif.

***--------------------------------------------------------------------------------------
*** Check Print Width
    ME->CHECK_PRWID( IR_MESSAGE = IR_MESSAGE ).

*** Check Design Document Data
    ME->CHECK_DESDO_DATA( IR_MESSAGE = IR_MESSAGE ).

*** Check Ink
    ME->CHECK_INK( IR_MESSAGE = IR_MESSAGE ).

  endmethod.
  method GET_TPRIN.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

***----------------------------------------------------------------------------
*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TPRIN ).
    move LS_PARAM-ATWRT to RF_TPRIN.
    move LS_PARAM-SETBY to EF_SETBY.

    check RF_TPRIN is not initial.

***----------------------------------------------------------------------------
    if EF_DESCR is requested.
***   Identify Description
      EF_DESCR = ZCL_VCXI_FFSS_CUST_PRINT=>GET_TPRIN_DESCR( IF_TPRIN = RF_TPRIN ).
    endif.

    if EF_PRTEC is requested.
***   Identify Print Technology
      EF_PRTEC = ZCL_VCXI_FFSS_CUST_PRINT=>GET_TPRIN_PRTEC( IF_TPRIN = RF_TPRIN ).
    endif.

    if EF_FAPCA is requested.
***   Is Adapter/Cylinder allowed for Print Type
      EF_FAPCA = ZCL_VCXI_FFSS_CUST_PRINT=>GET_TPRIN_FAPCA( IF_TPRIN = RF_TPRIN ).
    endif.

  endmethod.
  method HANDLE_EVENT.

    SUPER->HANDLE_EVENT( IF_IEVNT         = IF_IEVNT
                         IT_GATE_PROVIDER = IT_GATE_PROVIDER ).

    case IF_IEVNT.
      when C_IEVNT_I_RECALC_DESDD.
        ME->HANDLE_RECALC_DESDD( ).
      when C_IEVNT_I_LOAD_DATA_DESDO.
        ME->HANDLE_LOAD_DATA_DESDO( ).
      when C_IEVNT_I_INIT_INK.
        ME->HANDLE_INIT_INK( ).
      when C_IEVNT_I_INIT_TINFO.
        ME->HANDLE_INIT_TINFO( ).

      when C_IEVNT_I_RECALC_PRTIN.
        ME->HANDLE_RECALC_PRTIN( ).

***   Event Forwarding for Ink Defaulting
      when C_IEVNT_I_REQ_INK_DEFAULT.
        ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_INK_DEFAULT_REQ ).
    endcase.

  endmethod.
  method HANDLE_INIT_INK.

    data: LF_PRTEC type ZVCXI_FFS_PRTEC.
    data: LR_GATE type ref to /VCXI/CL_VKSR_GATE.


***--------------------------------------------------------------------------------------
*** Delete all Inks
    loop at ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_FFSS_PR_MS_INK=>C_IPRVT_MS_INK
                                   IF_INUSE = ABAP_TRUE ) into LR_GATE.
      LR_GATE->DO_FREE( ).
    endloop.
    loop at ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_FFSS_PR_MS_DINK=>C_IPRVT_MS_DINK
                                   IF_INUSE = ABAP_TRUE ) into LR_GATE.
      LR_GATE->DO_FREE( ).
    endloop.

***--------------------------------------------------------------------------------------
*** Add required Ink Types of Print Type
    loop at ZCL_VCXI_FFSS_CUST_PRINT=>GET_TPRIN_TINK( IF_TPRIN = ME->GET_TPRIN( ) ) into data(LS_TINK_INFO)
                                                                                    where QTYMI gt 0.
      case LS_TINK_INFO-PRTEC.
***     Traditional Inks
        when ZCL_VCXI_FFSS_CUST_PRINT=>C_PRTEC_ANALOG.
          do LS_TINK_INFO-QTYMI times.
            LR_GATE = ME->R_GATE->CREATE_ELEMENT( IF_IPRVT = ZCL_VCXI_FFSS_PR_MS_INK=>C_IPRVT_MS_INK ).
            LR_GATE->SET_INUSE( ABAP_TRUE ).
            cast ZCL_VCXI_FFSS_PR_MS_INK( LR_GATE->R_CLSPR )->SET_TINK( IF_TINK = LS_TINK_INFO-TINK ).
          enddo.

***     Digital Inks
        when ZCL_VCXI_FFSS_CUST_PRINT=>C_PRTEC_DIGITAL.
          do LS_TINK_INFO-QTYMI times.
            LR_GATE = ME->R_GATE->CREATE_ELEMENT( IF_IPRVT = ZCL_VCXI_FFSS_PR_MS_DINK=>C_IPRVT_MS_DINK ).
            cast ZCL_VCXI_FFSS_PR_MS_DINK( LR_GATE->R_CLSPR )->SET_TINK( IF_TINK = LS_TINK_INFO-TINK ).
          enddo.

      endcase.
    endloop.

***--------------------------------------------------------------------------------------
*** Initialize Print Mutation and Repeat
    ME->GET_TPRIN( importing EF_PRTEC = LF_PRTEC ).
    case LF_PRTEC.
      when ZCL_VCXI_FFSS_CUST_PRINT=>C_PRTEC_DIGITAL.
        ME->SET_NOACC( IF_SETBY = SPACE ).
        ME->SET_NOATC( IF_SETBY = SPACE ).
    endcase.

  endmethod.
  method HANDLE_INIT_TINFO.

    data: LF_ID type /VCXI/CKX_ID.
    data: LR_GATE_TINFO type ref to /VCXI/CL_VKSR_GATE.


*** Get Provider Tooling Information
    ME->GET_PRVDR_TINFO( importing EF_ID         = LF_ID
                                   ER_GATE_TINFO = LR_GATE_TINFO ).
    check LF_ID is not initial.


    if ME->IS_TINFO_NEEDED( ).
***   Create Tooling Information if needed
      if LR_GATE_TINFO is not bound.
        ME->R_GATE->CREATE_ELEMENT( IF_ID = LF_ID ).
      endif.
    else.
***   Remove Tooling Information
      if LR_GATE_TINFO is bound.
        LR_GATE_TINFO->DO_FREE( ).
      endif.
    endif.

  endmethod.
  method HANDLE_LOAD_DATA_DESDO.

*** Ensure a Design Document has been specified
    if ME->GET_DESDO( ) is not initial.
***   Load Design Document Data
      ME->SET_DESDO_DATA( ).
    else.
***   Clear data that is directly related to the design document
      ME->SET_SETTN( IF_SETBY = SPACE ).
    endif.

  endmethod.
  method HANDLE_RECALC_DESDD.

    data: LF_FOVRR type ZVCXI_XCS_FOVRR,
          LF_MAKTX type MAKTX.


    if ME->GET_DESDO( ) is initial.
***   Only calcualate if calculation is active
      ME->GET_DESDD( importing EF_FOVRR = LF_FOVRR ).
      check LF_FOVRR eq ABAP_FALSE.

***   Get generated Material Description
      LF_MAKTX = ZCL_VCXI_F0S_BL_MAKTX=>GET_MAKTX( IR_GATE = ME->R_GATE ).

***   Set new Desing Document Description
      ME->SET_DESDD( IF_DESDD = LF_MAKTX ).

    else.
***   Initialize Desing Document Description
      ME->SET_DESDD( IF_SETBY = SPACE ).
    endif.

  endmethod.
  method HANDLE_RECALC_PRTIN.

    data: LF_SETBY type /VCXI/VKSR_SETBY.
    data: LS_SURWT type ZVCXI_F0S_S_SURWT,
          LS_PRTIN type ZVCXI_FFS_S_PRTIN.

*** Only Calcualate if not set by User
    ME->GET_PRTIN( importing ES_PRTIN = LS_PRTIN
                             EF_SETBY = LF_SETBY ).

    check LF_SETBY ne /VCXI/CL_VKSR_GATE=>C_SETBY_USER.

*** Get Surface Weight of all Ink
    ME->GET_MS_INK_SURWT( importing ES_SURWT = LS_SURWT ).

    break HARDCODED.  "Story OPSCVCIS-1176
*** Default of 1µm for each g/m2 of the dry weight
    call function 'ROUND'
      exporting
        INPUT  = LS_SURWT-SURWT
        SIGN   = 'X'
      importing
        OUTPUT = LS_PRTIN-PRTIN.

*** Set Internal Print Thickness
    ME->SET_PRTIN( IS_PRTIN = LS_PRTIN ).

  endmethod.
  method IS_TINFO_NEEDED.

*** Not needed if a Design Document is defined
    if ME->GET_DESDO( ) is not initial.
      RF_NEEDED = ABAP_FALSE.
      return.
    endif.

*** Needed if no condition applied
    RF_NEEDED = ABAP_TRUE.

  endmethod.
  method SET_CTRQW.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_CTRQW ).

    move IS_CTRQW-CTRQW     to LS_PARAM-ATFLV.
    move IS_CTRQW-CTRQW_UOM to LS_PARAM-UNIT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_CTRQW_CHANGED ).
    endif.

  endmethod.
  method SET_DESDD.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_DESDD ).

    move IF_DESDD  to LS_PARAM-STRNG.
    move IF_SETBY  to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_DESDD_CHANGED ).
    endif.

  endmethod.
  method SET_DESDO.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_DESDO ).

    if IF_DESDO is supplied.
      LS_PARAM-STRNG = IF_DESDO.
    endif.

    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-STRNG.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_DESDO_CHANGED ).
    endif.

  endmethod.
  method SET_DESDO_DATA.

    data: LR_DMSEXT_PD type ref to ZCL_VCXI_FFE_DMS_DMSEXT_PD,
          LR_GATE      type ref to /VCXI/CL_VKSR_GATE.

***--------------------------------------------------------------------------------------
*** Get instance of DMS Data Extension for Print Design
    try.
        LR_DMSEXT_PD ?= ZCL_VCXI_FFE_DMS_DMSEXT_PD=>/VCXI/IF_CKX_OBJECT~GET_INSTANCE( IF_GUID = conv #( ME->GET_DESDO( ) ) ).
      catch CX_SY_MOVE_CAST_ERROR into data(LR_CX_SY_MOVE_CAST_ERROR).
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_SY_MOVE_CAST_ERROR ).
    endtry.

***--------------------------------------------------------------------------------------
*** Set Values of Print Design to Conversion Step
    ME->SET_TPRIN( IF_TPRIN = LR_DMSEXT_PD->S_PD-TPRIN
                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    ME->SET_PRCAT( IF_PRCAT = LR_DMSEXT_PD->S_PD-PRCAT
                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    ME->SET_PRRWI( IS_PRRWI = LR_DMSEXT_PD->S_PD-S_PRRWI
                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    ME->SET_PRRLE( IS_PRRLE = LR_DMSEXT_PD->S_PD-S_PRRLE
                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    ME->SET_RENEX( IF_RENEX = LR_DMSEXT_PD->S_PD-RENEX
                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    ME->SET_REBEH( IF_REBEH = LR_DMSEXT_PD->S_PD-REBEH
                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    ME->SET_SETTN( IF_SETTN = LR_DMSEXT_PD->S_PD-SETTN ).

***--------------------------------------------------------------------------------------
*** Raise Events, as a change in the Print Type will initialize all Inks
    ME->R_GATE->RAISE_EVENTS( ).

***--------------------------------------------------------------------------------------
*** Delete all Inks
    loop at ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_FFSS_PR_MS_INK=>C_IPRVT_MS_INK
                                   IF_INUSE = ABAP_TRUE ) into LR_GATE.
      LR_GATE->DO_FREE( ).
    endloop.
    loop at ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_FFSS_PR_MS_DINK=>C_IPRVT_MS_DINK
                                   IF_INUSE = ABAP_TRUE ) into LR_GATE.
      LR_GATE->DO_FREE( ).
    endloop.

*** Load Inks based on Print Design
    loop at LR_DMSEXT_PD->T_PDINK into data(LS_PDINK).
      case ZCL_VCXI_FFSS_CUST_PRINT=>GET_TINK_PRTEC( IF_TINK = LS_PDINK-TINK ).
***     Traditional Inks
        when ZCL_VCXI_FFSS_CUST_PRINT=>C_PRTEC_ANALOG.
          LR_GATE = ME->R_GATE->CREATE_ELEMENT( IF_IPRVT = ZCL_VCXI_FFSS_PR_MS_INK=>C_IPRVT_MS_INK ).
          LR_GATE->SET_INUSE( ABAP_TRUE ).
          cast ZCL_VCXI_FFSS_PR_MS_INK( LR_GATE->R_CLSPR )->SET_PDINK( IS_PDINK = LS_PDINK ).

***     Digital Inks
        when ZCL_VCXI_FFSS_CUST_PRINT=>C_PRTEC_DIGITAL.
          LR_GATE = ME->R_GATE->CREATE_ELEMENT( IF_IPRVT = ZCL_VCXI_FFSS_PR_MS_DINK=>C_IPRVT_MS_DINK ).
          cast ZCL_VCXI_FFSS_PR_MS_DINK( LR_GATE->R_CLSPR )->SET_PDINK( IS_PDINK = LS_PDINK ).

      endcase.
    endloop.

  endmethod.
  method SET_FOVRR_PRTIN.

*** Set Setby for Consumption Scheme
    case IF_FOVRR.
      when ABAP_TRUE.
        ME->SET_PRTIN( IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
      when ABAP_FALSE.
        ME->SET_PRTIN( IF_SETBY = SPACE ).
    endcase.

  endmethod.
  method SET_NOACC.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_NOACC ).

    if IF_NOACC is supplied.
      LS_PARAM-ATFLV = IF_NOACC.
    endif.

    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_NOACC_CHANGED ).
    endif.

  endmethod.
  method SET_NOATC.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_NOATC ).

    if IF_NOATC is supplied.
      LS_PARAM-ATFLV = IF_NOATC.
    endif.

    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_NOATC_CHANGED ).
    endif.

  endmethod.
  method SET_NOCOL.

    data: LF_PRTEC type ZVCXI_FFS_PRTEC,
          LF_NOCOL type ZVCXI_P0S_NOCOL,
          LF_INDEX type I.
    data: LR_GATE       type ref to /VCXI/CL_VKSR_GATE,
          LR_PR_MS_INK  type ref to ZCL_VCXI_FFSS_PR_MS_INK,
          LR_PR_MS_DINK type ref to ZCL_VCXI_FFSS_PR_MS_DINK.


*** Get Print Technology of Ink Type
    LF_PRTEC = cond #( when IF_TINK is supplied then ZCL_VCXI_FFSS_CUST_PRINT=>GET_TINK_PRTEC( IF_TINK = IF_TINK )
                                                else ZCL_VCXI_FFSS_CUST_PRINT=>C_PRTEC_ANALOG ).
    check LF_PRTEC is not initial.

***--------------------------------------------------------------------------------------
*** Get all Inks (Analog or Digital)
    RT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = switch #( LF_PRTEC when ZCL_VCXI_FFSS_CUST_PRINT=>C_PRTEC_ANALOG  then ZCL_VCXI_FFSS_PR_MS_INK=>C_IPRVT_MS_INK
                                                                   when ZCL_VCXI_FFSS_CUST_PRINT=>C_PRTEC_DIGITAL then ZCL_VCXI_FFSS_PR_MS_DINK=>C_IPRVT_MS_DINK )
                                     IF_INUSE = ABAP_TRUE ).

*** Filter if a specific Ink Type is supplied
    if IF_TINK is supplied.
      loop at RT_GATE into LR_GATE.
        case type of LR_GATE->R_CLSPR.
          when type ZCL_VCXI_FFSS_PR_MS_INK into LR_PR_MS_INK.
            if LR_PR_MS_INK->GET_TINK( ) ne IF_TINK.
              delete RT_GATE.
            endif.
          when type ZCL_VCXI_FFSS_PR_MS_DINK into LR_PR_MS_DINK.
            if LR_PR_MS_DINK->GET_TINK( ) ne IF_TINK.
              delete RT_GATE.
            endif.
        endcase.
      endloop.
    endif.

*** Count and compare Inks
    move LINES( RT_GATE ) to LF_NOCOL.
    check LF_NOCOL ne IF_NOCOL.

***--------------------------------------------------------------------------------------
*** Less Inks needed
    if LF_NOCOL gt IF_NOCOL.
      LF_INDEX = IF_NOCOL + 1.
      loop at RT_GATE into LR_GATE from LF_INDEX.
        LR_GATE->DO_FREE( ).
        delete RT_GATE.
      endloop.


***--------------------------------------------------------------------------------------
*** More Inks needed
    else.
      LF_INDEX = IF_NOCOL - LF_NOCOL.
      do LF_INDEX times.
        LR_GATE = ME->R_GATE->CREATE_ELEMENT( IF_IPRVT = switch #( LF_PRTEC when ZCL_VCXI_FFSS_CUST_PRINT=>C_PRTEC_ANALOG  then ZCL_VCXI_FFSS_PR_MS_INK=>C_IPRVT_MS_INK
                                                                            when ZCL_VCXI_FFSS_CUST_PRINT=>C_PRTEC_DIGITAL then ZCL_VCXI_FFSS_PR_MS_DINK=>C_IPRVT_MS_DINK ) ).
        LR_GATE->SET_INUSE( IF_INUSE = ABAP_TRUE ).
        append LR_GATE to RT_GATE.

***     Set Ink Type if supplied
        if IF_TINK is supplied.
          case type of LR_GATE->R_CLSPR.
            when type ZCL_VCXI_FFSS_PR_MS_INK into LR_PR_MS_INK.
              LR_PR_MS_INK->SET_TINK( IF_TINK = IF_TINK ).
            when type ZCL_VCXI_FFSS_PR_MS_DINK into LR_PR_MS_DINK.
              LR_PR_MS_DINK->SET_TINK( IF_TINK = IF_TINK ).
          endcase.
        endif.
      enddo.
    endif.

  endmethod.
  method SET_NODDG.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_NODDG ).

    if IF_NODDG is supplied.
      LS_PARAM-ATFLV = IF_NODDG.
    endif.

    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_NODDG_CHANGED ).
    endif.

  endmethod.
  method SET_PRCAT.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PRCAT ).

    move IF_PRCAT           to LS_PARAM-ATWRT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_PRCAT_CHANGED ).
    endif.

  endmethod.
  method SET_PRRLE.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PRRLE ).

    move IS_PRRLE-PRRLE     to LS_PARAM-ATFLV.
    move IS_PRRLE-PRRLE_UOM to LS_PARAM-UNIT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_PRRLE_CHANGED ).
    endif.

  endmethod.
  method SET_PRRWI.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PRRWI ).

    move IS_PRRWI-PRRWI     to LS_PARAM-ATFLV.
    move IS_PRRWI-PRRWI_UOM to LS_PARAM-UNIT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_PRRWI_CHANGED ).
    endif.

  endmethod.
  method SET_PRTIN.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PRTIN ).

    if IS_PRTIN is supplied.
      move IS_PRTIN-PRTIN     to LS_PARAM-ATFLV.
      move IS_PRTIN-PRTIN_UOM to LS_PARAM-UNIT.
    endif.
    move IF_SETBY           to LS_PARAM-SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
*** Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_PRTIN_CHANGED ).
    endif.

  endmethod.
  method SET_REBEH.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_REBEH ).

    move IF_REBEH           to LS_PARAM-ATFLV.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_REBEH_CHANGED ).
    endif.

  endmethod.
  method SET_RENEX.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_RENEX ).

    move IF_RENEX           to LS_PARAM-ATFLV.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_RENEX_CHANGED ).
    endif.

  endmethod.
  method SET_SETTN.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SETTN ).

    if IF_SETTN is supplied.
      move IF_SETTN  to LS_PARAM-ATWRT.
    endif.

    move IF_SETBY  to LS_PARAM-SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATWRT.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_SETTN_CHANGED ).
    endif.

  endmethod.
  method SET_TPRIN.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TPRIN ).

    move IF_TPRIN           to LS_PARAM-ATWRT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_TPRIN_CHANGED ).
    endif.

  endmethod.
  method ZIF_VCXI_FFSS_COMM_NEXT~GET_FISTX.

    data: LF_INKSI       type        ZVCXI_FFS_INKSI,
          LF_TINK        type        ZVCXI_FFS_TINK,
          LF_TPRIN       type ZVCXI_FFS_TPRIN.
    data: LS_FSPRINT_A   type        ZVCXI_FFSS_S_FSPRINT,
          LS_FSPRINT_B   type        ZVCXI_FFSS_S_FSPRINT.
    data: LT_GATE        type        /VCXI/VKSR_TT_GATE.
    data: LR_GATE        type ref to /VCXI/CL_VKSR_GATE,
          LR_PR_MS_INK   type ref to ZCL_VCXI_FFSS_PR_MS_INK,
          LR_BL_FISTRUCT type ref to ZCL_VCXI_FFSS_BL_FISTRUCT.

    SUPER->ZIF_VCXI_FFSS_COMM_NEXT~GET_FISTX( importing EF_FISTX       = EF_FISTX
                                                        EF_SETBY       = EF_SETBY
                                                        ER_BL_FISTRUCT = LR_BL_FISTRUCT ).

***--------------------------------------------------------------------------------------
*** Get all Inks
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_FFSS_PR_MS_INK=>C_IPRVT_MS_INK
                                     IF_INUSE = ABAP_TRUE ).

*** Collect Ink Information
    loop at LT_GATE into LR_GATE.
      move LR_GATE->R_CLSPR ?to LR_PR_MS_INK.

      LF_INKSI = LR_PR_MS_INK->GET_INKSI( ).
      LF_TINK  = LR_PR_MS_INK->GET_TINK( ).

      case LF_INKSI.
        when ZCL_VCXI_FFSS_PR_MS_INK=>C_INKSI_SIDEA.
          add 1 to LS_FSPRINT_A-NOCOL.
          collect LF_TINK into LS_FSPRINT_A-T_TINK.

        when ZCL_VCXI_FFSS_PR_MS_INK=>C_INKSI_SIDEB.
          add 1 to LS_FSPRINT_B-NOCOL.
          collect LF_TINK into LS_FSPRINT_B-T_TINK.

      endcase.

    endloop.

    check LR_BL_FISTRUCT is bound.

*** Get Print Type
    LF_TPRIN = ME->GET_TPRIN( ).

*** Fallback as long as we do not have inks specified
    if LT_GATE is initial.
      add 1 to LS_FSPRINT_A-NOCOL.
      move LF_TPRIN to LS_FSPRINT_A-TPRIN.
    endif.

*** Add new Print
    if LS_FSPRINT_A-NOCOL gt 0.
      move LF_TPRIN to LS_FSPRINT_A-TPRIN.
      LR_BL_FISTRUCT->ADD_ENTRY( IS_FISTRUCT = LS_FSPRINT_A
                                 IF_TFSDA    = ZCL_VCXI_FFSS_BL_FISTRUCT=>C_TFSDA_PRINT
                                 IF_INDEX    = 1 ).
    endif.

    if LS_FSPRINT_B-NOCOL gt 0.
      move LF_TPRIN to LS_FSPRINT_B-TPRIN.
      LR_BL_FISTRUCT->ADD_ENTRY( IS_FISTRUCT = LS_FSPRINT_B
                                 IF_TFSDA    = ZCL_VCXI_FFSS_BL_FISTRUCT=>C_TFSDA_PRINT ).
    endif.

***--------------------------------------------------------------------------------------
*** Get new Film Structure XML
    LR_BL_FISTRUCT->GET_FISTX( importing EF_FISTX = EF_FISTX ).

***--------------------------------------------------------------------------------------
*** Get Film Structure Instance
    if ER_BL_FISTRUCT is requested.
      ER_BL_FISTRUCT = ZCL_VCXI_FFSS_BL_FISTRUCT=>FACTORY( IF_FISTX = EF_FISTX ).
    endif.

  endmethod.
  method ZIF_VCXI_FFSS_COMM_NEXT~GET_FITIN.

    data: LS_PRTIN type ZVCXI_FFS_S_PRTIN.

***--------------------------------------------------------------------------------------
*** Get Internal Film Thickness (Inbound)
    SUPER->ZIF_VCXI_FFSS_COMM_NEXT~GET_FITIN( importing ES_FITIN = ES_FITIN
                                                        EF_SETBY = EF_SETBY ).

***--------------------------------------------------------------------------------------
*** Get Internal Print Thickness
    ME->GET_PRTIN( importing ES_PRTIN = LS_PRTIN ).

*** Add to total Internal Thickness
    ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_PRTIN-PRTIN
                                                        IF_CALCV_UOM_1 = LS_PRTIN-PRTIN_UOM
                                                        IF_CALCV_2     = ES_FITIN-FITIN
                                                        IF_CALCV_UOM_2 = ES_FITIN-FITIN_UOM
                                                        IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_ADD
                                                        IF_UOM         = ES_FITIN-FITIN_UOM
                                              importing EF_CALCV       = ES_FITIN-FITIN
                                                        EF_CALCV_UOM   = ES_FITIN-FITIN_UOM ).

  endmethod.
  method ZIF_VCXI_FFSS_COMM_NEXT~GET_RELEN.

    data: LS_PRRLE type ZVCXI_FFS_S_PRRLE.

***--------------------------------------------------------------------------------------
*** Get Repeat Length (Inbound)
    SUPER->ZIF_VCXI_FFSS_COMM_NEXT~GET_RELEN( importing ES_RELEN = ES_RELEN
                                                        EF_SETBY = EF_SETBY ).

***--------------------------------------------------------------------------------------
*** Get Print Repeat Length
    ME->GET_PRRLE( importing ES_PRRLE = LS_PRRLE ).
    check LS_PRRLE-PRRLE is not initial.

    if ES_RELEN-RELEN is initial.
      move LS_PRRLE-PRRLE     to ES_RELEN-RELEN.
      move LS_PRRLE-PRRLE_UOM to ES_RELEN-RELEN_UOM.
    else.
      if ZCL_VCXI_XCS_SERVICE_CALC=>COMPARE_WITH_ANY( IF_CALCV_1     = LS_PRRLE-PRRLE
                                                      IF_CALCV_UOM_1 = LS_PRRLE-PRRLE_UOM
                                                      IF_CALCV_2     = ES_RELEN-RELEN
                                                      IF_CALCV_UOM_2 = ES_RELEN-RELEN_UOM
                                                      IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_NE ).
***     Clear Repeat Length if different
        clear ES_RELEN-RELEN.
      endif.
    endif.

  endmethod.
  method ZIF_VCXI_FFSS_COMM_NEXT~GET_SURWT.

    data:  LS_INK_SURWT type ZVCXI_F0S_S_SURWT.

    SUPER->ZIF_VCXI_FFSS_COMM_NEXT~GET_SURWT( importing ES_SURWT = ES_SURWT
                                                        EF_SETBY = EF_SETBY ).

***--------------------------------------------------------------------------------------
*** Get Surface Weight of Inks
    ME->GET_MS_INK_SURWT( importing ES_SURWT = LS_INK_SURWT ).

*** Add to total Surface Weight
    if LS_INK_SURWT-SURWT_UOM is not initial.
      ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_INK_SURWT-SURWT
                                                          IF_CALCV_UOM_1 = LS_INK_SURWT-SURWT_UOM
                                                          IF_CALCV_2     = ES_SURWT-SURWT
                                                          IF_CALCV_UOM_2 = ES_SURWT-SURWT_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_ADD
                                                          IF_UOM         = ES_SURWT-SURWT_UOM
                                                importing EF_CALCV       = ES_SURWT-SURWT
                                                          EF_CALCV_UOM   = ES_SURWT-SURWT_UOM ).
    endif.

  endmethod.
  method ZIF_VCXI_FFSS_COMM_NEXT~GET_TREQW.

    data: LS_CTRQW   type ZVCXI_FFS_S_CTRQW.

    SUPER->ZIF_VCXI_FFSS_COMM_NEXT~GET_TREQW( importing ES_TREQW = ES_TREQW
                                                        EF_SETBY = EF_SETBY ).

*** Compare with Conversion Trim Requirement
    ME->GET_CTRQW( importing ES_CTRQW = LS_CTRQW ).
    if ES_TREQW is initial or
       ZCL_VCXI_XCS_SERVICE_CALC=>COMPARE_WITH_ANY( IF_CALCV_1     = LS_CTRQW-CTRQW
                                                    IF_CALCV_UOM_1 = LS_CTRQW-CTRQW_UOM
                                                    IF_CALCV_2     = ES_TREQW-TREQW
                                                    IF_CALCV_UOM_2 = ES_TREQW-TREQW_UOM
                                                    IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_GT ) eq ABAP_TRUE.
      move LS_CTRQW-CTRQW     to ES_TREQW-TREQW.
      move LS_CTRQW-CTRQW_UOM to ES_TREQW-TREQW_UOM.
    endif.

  endmethod.
  method ZIF_VCXI_XCS_NGREP~FILL.

    data: LT_ATTR type CR_NETGRAPH_JSON->YT_ATTRIBUTE.


*** Get Attribute Data
    CR_NETGRAPH_JSON->ADD_ATTR( exporting IF_VALUE     = ME->GET_TPRIN( )   "Print Type
                                changing  CT_ATTR      = LT_ATTR ).
    CR_NETGRAPH_JSON->ADD_ATTR( exporting IF_VALUE     = ME->GET_NOCOL( )   "No of Colors
                                          IF_ALPHA_OUT = ABAP_TRUE
                                changing  CT_ATTR      = LT_ATTR ).

*** Add Node
    data(LF_NODE_KEY) = CR_NETGRAPH_JSON->ADD_NODE( IF_TITLE      = |{ ME->GET_DESCR( ) }|
                                                    IF_DESCR      = |{ ME->GET_DESCR( ) }|
                                                    IF_ICON       = 'sap-icon://print'
                                                    IF_STATUS     = CR_NETGRAPH_JSON->GET_STATUS_BY_GATE( ME->R_GATE )
                                                    IF_GROUP_KEY  = IF_GROUP_KEY
                                                    IT_ATTR       = LT_ATTR ).

*** Register Gate to Node
    CR_NETGRAPH_JSON->REGISTER_TO_NODE( IR_OBJECT   = ME->R_GATE
                                        IF_NODE_KEY = LF_NODE_KEY ).

  endmethod.
  method ZIF_VCXI_XCSS_DOC_USER~CLEAR_USAGE.

    data: LF_DESDO type ZVCXI_FFS_DESDO.


*** Get Printing Design Document
    LF_DESDO = ME->GET_DESDO( ).

*** Clear Printing Design Document if Key is matching
    if ZCL_VCXI_XCSS_BL_DOC=>DECRYPT_GUID( IF_GUID = conv #( LF_DESDO ) ) eq IS_DOC_KEY.
      ME->SET_DESDO( IF_SETBY = SPACE ).
    endif.

  endmethod.
  method ZIF_VCXI_XCSS_DOC_USER~CREATE_DOC.

    data: LT_GATE type /VCXI/VKSR_TT_GATE.


*** Get Document Manager Provider
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCSS_PR_DOC_MGR=>C_IPRVT_DOC_MGR ).
    check LT_GATE is not initial.

*** Add Document Instance
    cast ZCL_VCXI_XCSS_PR_DOC_MGR( LT_GATE[ 1 ]->R_CLSPR )->ADD_DOCS( IT_DOC_KEY = value #( ( IR_BL_DOC->GET_DOC_KEY( ) ) ) ).

*** Set Design Document
    ME->ZIF_VCXI_XCSS_DOC_USER~SET_USAGE( IT_DOC_KEY = value #( ( IR_BL_DOC->GET_DOC_KEY( ) ) ) ).

  endmethod.
  method ZIF_VCXI_XCSS_DOC_USER~GET_USAGE.

    data: LF_DESDO   type ZVCXI_FFS_DESDO.


*** Get Design Document
    LF_DESDO = ME->GET_DESDO( ).

    if LF_DESDO is not initial.
***   Get Printing Design Document Key of GUID
      append ZCL_VCXI_XCSS_BL_DOC=>DECRYPT_GUID( IF_GUID  = conv #( LF_DESDO ) ) to RT_DOC_KEY.
    endif.

  endmethod.
  method ZIF_VCXI_XCSS_DOC_USER~SET_USAGE.

    data: LF_DESDO  type ZVCXI_FFS_DESDO.
    data: LS_DOC_KEY type ZVCXI_XCS_S_DOC_KEY.


    if IT_DOC_KEY is not initial.
***   Use last Document to support "toggle" in Document Manager Popup
      LS_DOC_KEY = IT_DOC_KEY[ lines( IT_DOC_KEY ) ].

***   Get Design Document through Encryption
      LF_DESDO = ZCL_VCXI_XCSS_BL_DOC=>ENCRYPT_GUID( IF_DOKAR = LS_DOC_KEY-DOKAR
                                                     IF_DOKNR = LS_DOC_KEY-DOKNR
                                                     IF_DOKTL = LS_DOC_KEY-DOKTL
                                                     IF_DOKVR = LS_DOC_KEY-DOKVR ).

***   Set the new Design Document
      ME->SET_DESDO( IF_DESDO = LF_DESDO
                     IF_SETBY = IF_SETBY ).
    else.
***   Remove Design Document
      ME->SET_DESDO( IF_SETBY = SPACE ).
    endif.

  endmethod.
  method ZIF_VCXI_XCSS_TUSER~GET_DESCR.

*** Design Document Description
    RF_DESCR = ME->GET_DESDD( ).

  endmethod.
  method ZIF_VCXI_XCSS_TUSER~GET_DFLT_TCEST.
  endmethod.
  method ZIF_VCXI_XCSS_TUSER~GET_DFLTK.

*** Print Type as Default Key
    RF_DFLTK = ME->GET_TPRIN( ).

  endmethod.
  method ZIF_VCXI_XCSS_TUSER~GET_ISVCE.

    RF_ISVCE = ME->R_GATE->F_ID.

  endmethod.
  method ZIF_VCXI_XCSS_TUSER~GET_WERKS.

    RF_WERKS = ME->GET_WERKS( ).

  endmethod.
