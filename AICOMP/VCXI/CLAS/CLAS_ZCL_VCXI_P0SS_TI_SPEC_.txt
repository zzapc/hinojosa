
class ZCL_VCXI_P0SS_TI_SPEC definition
  public
  inheriting from ZCL_VCXI_XCS_TI
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ
    redefinition .
  methods /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL
    redefinition .
protected section.

  data R_PR_SPEC type ref to ZCL_VCXI_P0SS_PR_SPEC .
  constants C_UC_SHOW_PRSTY type UI_FUNC value 'SHOW_PRSTY'. "#EC NOTEXT

  methods SHOW_PRSTY
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_P0SS_TI_SPEC definition
class ZCL_VCXI_P0SS_TI_SPEC implementation.
method /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ.

  data: LF_TEXT         type GUI_TEXT,
        LF_TPRST        type ZVCXI_P0SS_TPRST,
        LF_PRSTY        type ZVCXI_P0SS_PRSTY,
        LF_TPRST_DESCR  type /VCXI/CKX_DESCR.

  SUPER->/VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_REQ( IR_MENU = IR_MENU ).

***------------------------------------------------------------------------------------------------
*** Show Product Style Customizing
  LF_TPRST = ME->R_PR_SPEC->GET_TPRST( ).
  LF_PRSTY = ME->R_PR_SPEC->GET_PRSTY( ).
  if LF_TPRST is not initial and
     LF_PRSTY is not initial.

*** Get Product Style Description
    select single DESCR from ZVCXI_P0S_TC300T
           into LF_TPRST_DESCR
          where TPRST eq LF_TPRST
           and  LANGU eq SY-LANGU.

    if SY-SUBRC ne 0.
      move LF_TPRST to LF_TPRST_DESCR.
    endif.

    concatenate 'Show'(F10) LF_TPRST_DESCR LF_PRSTY into LF_TEXT separated by SPACE.

*** Set Seperator
    IR_MENU->ADD_SEPARATOR( ).

*** Set Menu
    IR_MENU->ADD_FUNCTION( FCODE = C_UC_SHOW_PRSTY
                           TEXT  = LF_TEXT ).
  endif.


endmethod.
method /VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL.

  case IF_FCODE.
    when C_UC_SHOW_PRSTY.
      ME->SHOW_PRSTY( ).

    when others.
      SUPER->/VCXI/IF_VKSU_TI_EVENT~HANDLE_CONTEXT_SEL( IF_FCODE = IF_FCODE ).
  endcase.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_UIGATE = IR_UIGATE
                      IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
  try.
      move IR_GATE->R_CLSPR ?to ME->R_PR_SPEC.
    catch CX_SY_MOVE_CAST_ERROR.
***   TI ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '802'.
  endtry.

endmethod.
method SHOW_PRSTY.

  data: LF_TPRST   type ZVCXI_P0SS_TPRST,
        LF_PRSTY   type ZVCXI_P0SS_PRSTY,
        LF_MESSAGE type CHAR100.

*** Get Product Style Definition.
  LF_TPRST = ME->R_PR_SPEC->GET_TPRST( ).
  LF_PRSTY = ME->R_PR_SPEC->GET_PRSTY( ).

*** Open Product Style in new Task
  call function 'ZVCXI_P0SSC_PRSTY_START'
    starting new task 'NEW'
    exporting
      IF_TPRST              = LF_TPRST
      IF_PRSTY              = LF_PRSTY
    exceptions
      SYSTEM_FAILURE        = 97  message LF_MESSAGE
      COMMUNICATION_FAILURE = 98  message LF_MESSAGE
      others                = 99.
  if SY-SUBRC   ne 0 and
     LF_MESSAGE is not initial.
    message LF_MESSAGE type 'W'.
  endif.

endmethod.
