
class ZCL_VCXI_P1SS_UI_CSTEP_WIND definition
  public
  inheriting from ZCL_VCXI_P1SS_UI_CSTEP
  create public .

public section.

  data S_CSTEP_WIND_VIS type ZVCXI_P1SS_S_CSTEP_WIND_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
protected section.

  data R_PR_CSTEP_WIND type ref to ZCL_VCXI_P1SS_PR_CSTEP_WIND .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_P1SS_UI_CSTEP_WIND definition
class ZCL_VCXI_P1SS_UI_CSTEP_WIND implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

***--------------------------------------------------------------------------------------
*** Bind UI to Dynpro
    call function 'ZVCXI_P1SS_UI_CSTEP_WIND_BIND'
      exporting
        IR_UI_CSTEP_WIND = ME
      importing
        ES_DYNP          = ME->S_DYNP.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_CSTEP_WIND.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DO_MODIFY_SCREEN.

    try.
        SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                 IF_DYNNR = IF_DYNNR ).

***----------------------------------------------------------------------------
***     Modify Screen
        loop at screen.
***       Handling Dimension changeable if Override Flag is set
          if SCREEN-GROUP1 eq 'OHD'.
            if ME->S_CSTEP_WIND_VIS-FOVRR_HNDDIM eq ABAP_FALSE.
              move 0 to SCREEN-INPUT.
            endif.
          endif.

          modify screen.
        endloop.

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_CSTEP_WIND_VIS type ZVCXI_P1SS_S_CSTEP_WIND_VIS.


*** Call SUPER
    SUPER->DO_UPDATE_VIS( IS_CSTEP_VIS = IS_CSTEP_VIS ).

    check ME->R_PR_CSTEP_WIND is bound.

*** "Cast" Vis Structure
    move-corresponding IS_CSTEP_VIS to LS_CSTEP_WIND_VIS.

***--------------------------------------------------------------------------------------
*** Roll Inner Diameter
    if ME->S_CSTEP_WIND_VIS-S_RODII ne LS_CSTEP_WIND_VIS-S_RODII.
      ME->R_PR_CSTEP_WIND->SET_RODII( IS_RODII = LS_CSTEP_WIND_VIS-S_RODII
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Update Handling Dimension if it is "Overriden"
    if ME->S_CSTEP_WIND_VIS-FOVRR_HNDDIM eq ABAP_TRUE and
       ME->S_CSTEP_WIND_VIS-S_HNDDIM ne LS_CSTEP_WIND_VIS-S_HNDDIM.
      ME->R_PR_CSTEP_WIND->SET_HNDDIM( IS_HNDDIM = LS_CSTEP_WIND_VIS-S_HNDDIM
                                       IF_SETBY  = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Flag Override Handling Dimension
    if ME->S_CSTEP_WIND_VIS-FOVRR_HNDDIM ne LS_CSTEP_WIND_VIS-FOVRR_HNDDIM.
      ME->R_PR_CSTEP_WIND->SET_HNDDIM( IF_FOVRR = LS_CSTEP_WIND_VIS-FOVRR_HNDDIM ).
    endif.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_CSTEP_WIND.

  endmethod.
  method INIT_VIS.

    SUPER->INIT_VIS( ).

    free: ME->S_CSTEP_WIND_VIS.
    ME->S_CSTEP_WIND_VIS-S_CSTEP_VIS = ME->S_CSTEP_VIS.

***--------------------------------------------------------------------------------------
*** Roll Inner & Outer Diamter
    ME->S_CSTEP_WIND_VIS-S_RODII = ME->R_PR_CSTEP_WIND->GET_RODII( ).
    ME->S_CSTEP_WIND_VIS-S_RODIA = ME->R_PR_CSTEP_WIND->GET_RODIA( ).

*** Roll Length
    ME->S_CSTEP_WIND_VIS-S_ROLEN = ME->R_PR_CSTEP_WIND->GET_ROLEN( ).

*** Handling Dimension
    ME->S_CSTEP_WIND_VIS-S_HNDDIM = ME->R_PR_CSTEP_WIND->GET_HNDDIM( importing EF_FOVRR = ME->S_CSTEP_WIND_VIS-FOVRR_HNDDIM ).

  endmethod.
