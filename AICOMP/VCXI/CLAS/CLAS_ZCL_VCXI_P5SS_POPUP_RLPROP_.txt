
class ZCL_VCXI_P5SS_POPUP_RLPROP definition
  public
  create protected .

public section.

  constants C_UC_CANCEL type SYUCOMM value 'CANCEL' ##NO_TEXT.
  data S_POPUP_RLPROP_VIS type ZVCXI_P5SS_S_POPUP_RLPROP_VIS .

  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods DO_USER_COMMAND
    importing
      !IF_UCOMM type SYUCOMM
    raising
      /VCXI/CX_CKX .
  class-methods START
    importing
      !IR_BL_RLBPROP type ref to ZCL_VCXI_P5SS_BL_RLBPROP
      !IR_BL_RLFPROP type ref to ZCL_VCXI_P5SS_BL_RLFPROP
      !IF_WERKS type WERKS_D
      !IS_WEBWI type ZVCXI_PXS_S_WEBWI
      !IS_SURWT type ZVCXI_PXS_S_SURWT
    raising
      /VCXI/CX_CKX .
protected section.

  types:
    begin of YS_JOINT_GRID,
      JOITY type ZVCXI_P0S_JOITY,
      DESCR type /VCXI/CKX_DESCR,
    end of YS_JOINT_GRID .
  types:
    YT_JOINT_GRID type standard table of YS_JOINT_GRID .

  data R_BL_RLFPROP type ref to ZCL_VCXI_P5SS_BL_RLFPROP .
  data R_BL_RLBPROP type ref to ZCL_VCXI_P5SS_BL_RLBPROP .
  data R_GRID_PRINT_COM type ref to ZCL_VCXI_PXSS_GRID_PRINT_COM .

  methods DO_FREE
    raising
      /VCXI/CX_CKX .
  methods INIT_GRID_PRINT_COM
    raising
      /VCXI/CX_CKX .
  methods CONSTRUCTOR
    importing
      !IR_BL_RLFPROP type ref to ZCL_VCXI_P5SS_BL_RLFPROP
      !IR_BL_RLBPROP type ref to ZCL_VCXI_P5SS_BL_RLBPROP
      !IF_WERKS type WERKS_D
      !IS_WEBWI type ZVCXI_PXS_S_WEBWI
      !IS_SURWT type ZVCXI_PXS_S_SURWT
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_CANCEL
    raising
      /VCXI/CX_CKX .
  methods SET_PF_STATUS
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_P5SS_POPUP_RLPROP definition
class ZCL_VCXI_P5SS_POPUP_RLPROP implementation.
  method CONSTRUCTOR.

***--------------------------------------------------------------------------------------
*** Keep Input
    ME->R_BL_RLFPROP = IR_BL_RLFPROP.
    ME->R_BL_RLBPROP = IR_BL_RLBPROP.

***--------------------------------------------------------------------------------------
*** CCB Grade Number
    ME->S_POPUP_RLPROP_VIS-CCBGN = IR_BL_RLFPROP->F_CCBGN.

*** Grammage
    ME->S_POPUP_RLPROP_VIS-S_GRAMM = IR_BL_RLFPROP->S_GRAMM.

*** Web Width
    ME->S_POPUP_RLPROP_VIS-S_WEBWI = IS_WEBWI.

*** Surface Weight
    ME->S_POPUP_RLPROP_VIS-S_SURWT = IS_SURWT.

*** Caliper
    ME->S_POPUP_RLPROP_VIS-S_CALIP = IR_BL_RLFPROP->S_CALIP.

*** Product Hieararchy
    ME->S_POPUP_RLPROP_VIS-PRODH       = IR_BL_RLFPROP->F_PRODH.
    ME->S_POPUP_RLPROP_VIS-PRODH_DESCR = ZCL_VCXI_XCS_SERVICE=>GET_PRODH_DESCR( IF_PRODH = ME->S_POPUP_RLPROP_VIS-PRODH ).

*** Repeat Length
    ME->S_POPUP_RLPROP_VIS-S_REPLE = IR_BL_RLFPROP->S_REPLE.

  endmethod.
  method DO_FREE.

    free: ME->R_BL_RLFPROP,
          ME->R_BL_RLBPROP.

***--------------------------------------------------------------------------------------
*** Free Price Scale Grid
    if ME->R_GRID_PRINT_COM is bound.
      ME->R_GRID_PRINT_COM->DO_FREE( ).
      clear ME->R_GRID_PRINT_COM.
    endif.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Set PF-Status
    ME->SET_PF_STATUS( ).

***----------------------------------------------------------------------------
*** Initialize Printing (Communication) Grid
    ME->INIT_GRID_PRINT_COM( ).

  endmethod.
  method DO_USER_COMMAND.

    case IF_UCOMM.
      when C_UC_CANCEL.
        ME->HANDLE_UC_CANCEL( ).

    endcase.

  endmethod.
  method HANDLE_UC_CANCEL.

*** Leave Popup
    leave to screen 0.

  endmethod.
  method INIT_GRID_PRINT_COM.

    check ME->R_GRID_PRINT_COM is not bound.

***--------------------------------------------------------------------------------------
*** Create Grid for Print
    create object ME->R_GRID_PRINT_COM
      exporting
        IF_CONTAINER_NAME = '0100CC_PRINT'
        IR_PRINT_COM      = ME->R_BL_RLFPROP.

*** Initialize Grid
    ME->R_GRID_PRINT_COM->F_REFRESH_REQ = ABAP_TRUE.
    ME->R_GRID_PRINT_COM->REFRESH_GRID( ).

  endmethod.
  method SET_PF_STATUS.

    set pf-status '0100' of program 'SAPLZVCXI_P5SS_POPUP_RLPROP'.

  endmethod.
  method START.

    data: LR_POPUP_RLPROP type ref to ZCL_VCXI_P5SS_POPUP_RLPROP.

*** Create Instance
    create object LR_POPUP_RLPROP
      exporting
        IR_BL_RLBPROP = IR_BL_RLBPROP
        IR_BL_RLFPROP = IR_BL_RLFPROP
        IF_WERKS      = IF_WERKS
        IS_WEBWI      = IS_WEBWI
        IS_SURWT      = IS_SURWT.

*** Call Popup
    call function 'ZVCXI_P5SS_POPUP_RLPROP_START'
      exporting
        IR_POPUP_RLPROP = LR_POPUP_RLPROP.

*** Free
    LR_POPUP_RLPROP->DO_FREE( ).

  endmethod.
