
class ZCL_VCXI_P0SR_POPUP_RTEEDI definition
  public
  inheriting from ZCL_VCXI_XCSR_POPUP_RTEEDI
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_BL_PRTE type ref to ZCL_VCXI_XCSR_BL_PRTE
      !IR_PR_RTESPV type ref to ZCL_VCXI_XCSR_PR_RTESPV
      !IR_APPDATA type ref to /VCXI/CL_VKSR_APPDATA
      !IF_LOCKED type /VCXI/CKX_LOCKED
      !IF_TABNAME type TABNAME default 'ZVCXI_P0SR_S_RTEEDI_GRID'
    raising
      /VCXI/CX_CKX .
protected section.

  methods FILL_LINE_BY_PSTEP
    redefinition .
  methods FORWARD_COMBO_DATA_CHANGES
    redefinition .
  methods REFRESH_FIELDCAT
    redefinition .
  methods REFRESH_STYLE
    redefinition .
  methods UPDATE_BL_PRTE
    redefinition .
private section.
endclass. "ZCL_VCXI_P0SR_POPUP_RTEEDI definition
class ZCL_VCXI_P0SR_POPUP_RTEEDI implementation.
method CONSTRUCTOR.

  call method SUPER->CONSTRUCTOR
    exporting
      IR_BL_PRTE   = IR_BL_PRTE
      IR_PR_RTESPV = IR_PR_RTESPV
      IR_APPDATA   = IR_APPDATA
      IF_LOCKED    = IF_LOCKED
      IF_TABNAME   = IF_TABNAME.

endmethod.
method FILL_LINE_BY_PSTEP.

  data:  LS_RTEEDI_GRID       type          ZVCXI_P0SR_S_RTEEDI_GRID.
  data:  LR_PCDIR             type ref to   ZIF_VCXI_P0SR_PCDIR.

***------------------------------------------------------------------------------------------------
*** Fill Super Data
  SUPER->FILL_LINE_BY_PSTEP( exporting IR_RTE_PSTEP = IR_RTE_PSTEP
                             changing  CS_DATA      = CS_DATA ).

  move-corresponding CS_DATA to LS_RTEEDI_GRID.

***------------------------------------------------------------------------------------------------
*** BL Production Step Data
  try.
      move IR_RTE_PSTEP->R_BLOBJ ?to LR_PCDIR.
      if LR_PCDIR is bound.
***     PC Direction and Handling Option
        move LR_PCDIR->F_PCDIR to LS_RTEEDI_GRID-PCDIR.
        move LR_PCDIR->F_PCDOP to LS_RTEEDI_GRID-PCDOP.
      endif.
    catch CX_SY_MOVE_CAST_ERROR.
  endtry.

  move-corresponding LS_RTEEDI_GRID to CS_DATA.

endmethod.
method FORWARD_COMBO_DATA_CHANGES.

  data:          LF_PCDIR             type          ZVCXI_P0SP_PCDIR,
                 LF_INDEX             type          I.
  data:          LS_MODI              type          LVC_S_MODI,
                 LS_RTEEDI_GRID       type          ZVCXI_P0SR_S_RTEEDI_GRID.
  field-symbols: <T_DATA>             type standard table,
                 <S_DATA>             type          any.

***--------------------------------------------------------------------------------------
*** Call Super Implemenation
  SUPER->FORWARD_COMBO_DATA_CHANGES( IT_GOOD_CELLS = IT_GOOD_CELLS ).

***--------------------------------------------------------------------------------------
*** Dereferencing of Data Reference
  assign ME->R_DATA->* to <T_DATA>.

***--------------------------------------------------------------------------------------
*** Process updated PC Direction
  loop at IT_GOOD_CELLS into LS_MODI where FIELDNAME eq 'PCDIR'.
*** Find updated Entry
    read table <T_DATA> assigning <S_DATA> index LS_MODI-ROW_ID.
    check SY-SUBRC eq 0.
    move-corresponding <S_DATA> to LS_RTEEDI_GRID.

*** Buffer PC Direction
    move LS_RTEEDI_GRID-PCDIR to LF_PCDIR.

*** Update Combined
    LF_INDEX = LS_MODI-ROW_ID + 1.
    loop at <T_DATA> assigning <S_DATA> from LF_INDEX.
      move-corresponding <S_DATA> to LS_RTEEDI_GRID.
      if LS_RTEEDI_GRID-COMBO ne ABAP_TRUE.
        exit.
      else.
        if LS_RTEEDI_GRID-PCDOP ne ZIF_VCXI_P0SR_PCDIR=>C_PCDOP_MANDATORY.
          move LF_PCDIR to LS_RTEEDI_GRID-PCDIR.
        endif.
      endif.
      move-corresponding LS_RTEEDI_GRID to <S_DATA>.
    endloop.
  endloop.

endmethod.
method REFRESH_FIELDCAT.

  field-symbols: <S_FCAT>        type        LVC_S_FCAT.

  SUPER->REFRESH_FIELDCAT( ).

***----------------------------------------------------------------------------
*** Change Field catalog attributes
  loop at ME->T_FCAT assigning <S_FCAT>.
    case <S_FCAT>-FIELDNAME.
***   PC Direction
      when 'PCDIR'.
        if ME->F_LOCKED eq ABAP_TRUE.
          move ABAP_TRUE     to <S_FCAT>-EDIT.
        endif.
    endcase.
  endloop.

endmethod.
method REFRESH_STYLE.

  data:          LS_RTEEDI_GRID  type          ZVCXI_P0SR_S_RTEEDI_GRID.
  data:          LT_STYLE        type table of LVC_S_STYL.
  data:          LR_PCDIR        type ref to   ZIF_VCXI_P0SR_PCDIR.
  field-symbols: <T_DATA>        type standard table,
                 <S_DATA>        type          any,
                 <S_STYLE>       type          LVC_S_STYL.

  SUPER->REFRESH_STYLE( ).

***----------------------------------------------------------------------------
*** Dereferencing of Data Reference
  assign ME->R_DATA->* to <T_DATA>.

*** Update Style
  loop at <T_DATA> assigning <S_DATA>.
    move-corresponding <S_DATA> to LS_RTEEDI_GRID.
    move LS_RTEEDI_GRID-TSTYL   to LT_STYLE.

***----------------------------------------------------------------------------
*** PC Direction
    try.
        move LS_RTEEDI_GRID-R_RTE_PSTEP->R_BLOBJ ?to LR_PCDIR.
      catch CX_SY_MOVE_CAST_ERROR.
        clear LR_PCDIR.
    endtry.

    append initial line to LT_STYLE assigning <S_STYLE>.
    move 'PCDIR'                                 to <S_STYLE>-FIELDNAME.
    if LR_PCDIR             is bound and
       LS_RTEEDI_GRID-COMBO ne ABAP_TRUE and
       LS_RTEEDI_GRID-PCDOP ne ZIF_VCXI_P0SR_PCDIR=>C_PCDOP_MANDATORY.
      add CL_GUI_ALV_GRID=>MC_STYLE_ENABLED      to <S_STYLE>-STYLE.
    else.
      move CL_GUI_ALV_GRID=>MC_STYLE_DISABLED    to <S_STYLE>-STYLE.
    endif.

***----------------------------------------------------------------------------
*** Update Style Table
    move LT_STYLE to LS_RTEEDI_GRID-TSTYL.
    move-corresponding LS_RTEEDI_GRID to <S_DATA>.

  endloop.

endmethod.
method UPDATE_BL_PRTE.

  data:          LF_INDEX         type          I.
  data:          LS_RTEEDI_GRID   type          ZVCXI_P0SR_S_RTEEDI_GRID.
  data:          LR_RTE_ARBPL     type ref to   /VCXI/CL_VSMR_RTE_ARBPL,
                 LR_RTE_PSTEP     type ref to   /VCXI/CL_VSMR_RTE_PSTEP,
                 LR_PCDIR         type ref to   ZIF_VCXI_P0SR_PCDIR.
  field-symbols: <T_DATA>         type standard table,
                 <S_DATA>         type          any.

***--------------------------------------------------------------------------------------
*** Run Super Implementation
  SUPER->UPDATE_BL_PRTE( ).

***--------------------------------------------------------------------------------------
*** Dereferencing of Data Reference
  assign ME->R_DATA->* to <T_DATA>.

  clear LF_INDEX.
  loop at ME->R_BL_PRTE_NEW->R_RTE_PRTE->T_RTE_ARBPL into LR_RTE_ARBPL.
    loop at LR_RTE_ARBPL->T_RTE_PSTEP into LR_RTE_PSTEP.
      add 1 to LF_INDEX.

      read table <T_DATA> assigning <S_DATA> index LF_INDEX.
      if SY-SUBRC eq 0.
        move-corresponding <S_DATA> to LS_RTEEDI_GRID.

        try.
            move LR_RTE_PSTEP->R_BLOBJ ?to LR_PCDIR.
            if LR_PCDIR is bound.
              LR_PCDIR->SET_PCDIR( IF_PCDIR = LS_RTEEDI_GRID-PCDIR ).
            endif.
          catch CX_SY_MOVE_CAST_ERROR.
            continue.
        endtry.

      endif.
    endloop.
  endloop.

endmethod.
