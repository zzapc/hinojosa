
class ZCL_VCXI_PXSP_UI_ADDON_GUIL definition
  public
  inheriting from ZCL_VCXI_XCSP_UI_ADDON
  create public .

public section.

  data S_ADDON_GUIL_VIS type ZVCXI_PXSP_S_ADDON_GUIL_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  data R_GRID_PCOUT type ref to ZCL_VCXI_P0SP_GRID_PCOUT .
  data R_PR_ADDON_GUIL type ref to ZCL_VCXI_PXSP_PR_ADDON_GUIL .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_PXSP_UI_ADDON_GUIL definition
class ZCL_VCXI_PXSP_UI_ADDON_GUIL implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_PXSP_UI_ADDON_GUIL_BIND'
      exporting
        IR_UI_ADDON_GUIL = ME
      importing
        ES_DYNP          = ME->S_DYNP.

*** Create Grid for GRID_PCOUT
    create object ME->R_GRID_PCOUT
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_PCOUT'.

  endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                      IR_UIGATE = IR_UIGATE
                      IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
  try.
      move IR_GATE->R_CLSPR ?to ME->R_PR_ADDON_GUIL.
    catch CX_SY_MOVE_CAST_ERROR.
***   UI ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '801'.
  endtry.

endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

    if ME->R_GRID_PCOUT is bound.
      ME->R_GRID_PCOUT->DO_FREE( ).
      clear ME->R_GRID_PCOUT.
    endif.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Super PBO
    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize Grid
    if ME->R_GRID_PCOUT is bound.
      ME->R_GRID_PCOUT->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_PCOUT->REFRESH_GRID( ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_ADDON_GUIL_VIS type ZVCXI_PXSP_S_ADDON_GUIL_VIS.


*** Super
    SUPER->DO_UPDATE_VIS( IS_VIS = IS_VIS ).
    check ME->R_PR_ADDON_GUIL is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_ADDON_GUIL_VIS.

***-------------------------------------------------------------------------------------
*** Set Number of Cuts
    if ME->S_ADDON_GUIL_VIS-NOCUT ne LS_ADDON_GUIL_VIS-NOCUT.
      ME->R_PR_ADDON_GUIL->SET_NOCUT( IF_NOCUT = LS_ADDON_GUIL_VIS-NOCUT
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set PC per Cut
    if ME->S_ADDON_GUIL_VIS-PCPCU ne LS_ADDON_GUIL_VIS-PCPCU.
      ME->R_PR_ADDON_GUIL->SET_PCPCU( IF_PCPCU = LS_ADDON_GUIL_VIS-PCPCU
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    if ME->R_GRID_PCOUT is bound.
      ME->R_GRID_PCOUT->CHECK_CHANGED_DATA( ).
    endif.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_ADDON_GUIL.

  endmethod.
  method INIT_VIS.

*** Super
    SUPER->INIT_VIS( ).

    free: ME->S_ADDON_GUIL_VIS.
    ME->S_ADDON_GUIL_VIS-S_ADDON_VIS = ME->S_ADDON_VIS.

***--------------------------------------------------------------------------------------
*** Number of Cuts
    ME->S_ADDON_GUIL_VIS-NOCUT = ME->R_PR_ADDON_GUIL->GET_NOCUT( ).

*** PC per Cut
    ME->S_ADDON_GUIL_VIS-PCPCU = ME->R_PR_ADDON_GUIL->GET_PCPCU( ).

  endmethod.
