
class ZCL_VCXI_PCSS_BL_SCSOP_SLOT definition
  public
  inheriting from ZCL_VCXI_P0SS_BL_SCSOP
  create public .

public section.

  constants C_SCSOP_SLOT_SCORE type ZVCXI_P0S_SCSOP value 'SLOT_SCORE'. "#EC NOTEXT

  methods CONSTRUCTOR
    importing
      !IF_SHTDI type ZVCXI_P0SS_SHTDI
      !IF_CSCOR type ZVCXI_P0SS_CSCOR
    raising
      /VCXI/CX_CKX .
protected section.

  methods CALC_SCSTR
    redefinition .
  methods GET_DIVDI
    redefinition .
  methods READ_SHT_DIM
    redefinition .
private section.
endclass. "ZCL_VCXI_PCSS_BL_SCSOP_SLOT definition
class ZCL_VCXI_PCSS_BL_SCSOP_SLOT implementation.
method CALC_SCSTR.

  data:          LF_NOUPT          type        ZVCXI_P0SS_NOUPT,
                 LF_PARID_TRIM     type        /VCXI/VKS_PARID.
  data:          LS_PCDIM          type        ZVCXI_P0SP_S_PCDIM,
                 LS_SCSTR_CALC     type        ZVCXI_P0SS_S_SCSTR_CALC,
                 LS_TRIRB          type        ZVCXI_P0SS_S_TRIRB,
                 LS_TRILT          type        ZVCXI_P0SS_S_TRILT.
  data:          LT_SCSTR_CALC     type        ZVCXI_P0SS_TT_SCSTR_CALC.
  data:          LR_SHTDIM         type ref to ZIF_VCXI_P0SS_SHTDIM,
                 LR_PRODDIM        type ref to ZIF_VCXI_P0SS_PRODDIM.

  LR_PRODDIM = ME->GET_PRODDIM_PRVDR( IT_PRVDR = IT_PRVDR ).
  LR_SHTDIM = ME->GET_SHTDIM_PRVDR( IT_PRVDR = IT_PRVDR ).
  check LR_SHTDIM is bound.

***------------------------------------------------------------------------------------------------
*** 1 Get Values of Scores
  LT_SCSTR_CALC = ME->GET_SCORES_OF_SCSTR( IT_PRVDR = IT_PRVDR ).

***------------------------------------------------------------------------------------------------
*** If we do not handle Scores -> just take the Sheet Length as absolute value
  if  ME->F_CSCOR is initial.
    clear: LS_SCSTR_CALC, LT_SCSTR_CALC.
    check LR_PRODDIM is bound.
*** Save Values
    LS_PCDIM = LR_SHTDIM->GET_PCDIM( IF_POOTI = ZIF_VCXI_P0SS_SHTDIM=>C_POOTI_NETWT ).
    move LS_PCDIM-PCLEN                         to LS_SCSTR_CALC-SCORP.
    move LS_PCDIM-PCLEN_UOM                     to LS_SCSTR_CALC-SCORP_UOM.

    append LS_SCSTR_CALC to LT_SCSTR_CALC.
  endif.

***------------------------------------------------------------------------------------------------
*** 2 Sort Values
  sort LT_SCSTR_CALC by SCORP.

***------------------------------------------------------------------------------------------------
*** 3 Add Ups (in case of cutting)
  if ME->F_CSCOR is not initial.
    case ME->F_SHTDI.
      when 'A'.
        LF_NOUPT = LR_SHTDIM->GET_NOUPW( ).
      when 'B'.
        LF_NOUPT = LR_SHTDIM->GET_NOUPL( ).
    endcase.

    ME->ADD_UPS( exporting IF_NOUPT      = LF_NOUPT
                 changing  CT_SCSTR_CALC = LT_SCSTR_CALC ).
  endif.

***------------------------------------------------------------------------------------------------
*** 4 Get The Production Width/Length Trim and add it to the absolute value
*** and add the Cutting Score type
  if ME->F_CSCOR is not initial.
    case ME->F_CSCOR.
      when ZCL_VCXI_P0SS_CUST_PRSTY=>C_CSCOR_LENGTH.
***     Load Side Trim
        LF_PARID_TRIM = ZCL_VCXI_P0SS_PR_SCSTR_MGR=>C_TRIM_PARID_TRIPS.
      when ZCL_VCXI_P0SS_CUST_PRSTY=>C_CSCOR_CROSS.
***     Load Length Trim
        LF_PARID_TRIM = ZCL_VCXI_P0SS_PR_SCSTR_MGR=>C_TRIM_PARID_TRIPL.
    endcase.

    IR_PR_SCSTR_MGR->GET_TRIM( exporting IF_PARID    = LF_PARID_TRIM
                               importing ES_TRILT_IO = LS_TRILT
                                         ES_TRIRB_IO = LS_TRIRB ).

    ME->ADD_TRIM( exporting IS_TRILT      = LS_TRILT
                            IS_TRIRB      = LS_TRIRB
                            IF_TSCOR_CUT  = IR_PR_SCSTR_MGR->F_TSCOR_CUT
                  changing  CT_SCSTR_CALC = LT_SCSTR_CALC ).
  endif.
***------------------------------------------------------------------------------------------------
*** 5 Define the right Score String Values ( SCSTR_VALUE ) and return the Hole string
  RF_SCSTR = ME->BUILD_SCSTR( IF_SSTRC      = IR_PR_SCSTR_MGR->F_SSTRC
                              IT_SCSTR_CALC = LT_SCSTR_CALC ).

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IF_SHTDI = IF_SHTDI
                      IF_CSCOR = IF_CSCOR ).

*** Set Score Operation
  move C_SCSOP_SLOT_SCORE to ME->F_SCSOP.

endmethod.
method GET_DIVDI.

*** Nothing to do!

endmethod.
method READ_SHT_DIM.

  data: LS_PCDIM_IN  type        ZVCXI_P0SP_S_PCDIM.
  data: LR_SHTDIM    type ref to ZIF_VCXI_P0SS_SHTDIM.

***------------------------------------------------------------------------------------------------
*** Get Sheet Dimension Net With Trim
  LR_SHTDIM = ME->GET_SHTDIM_PRVDR( IT_PRVDR = IT_PRVDR ).
  LS_PCDIM_IN = LR_SHTDIM->GET_PCDIM( IF_POOTI = ZIF_VCXI_P0SS_SHTDIM=>C_POOTI_NETWT ).

  move LS_PCDIM_IN-PCLEN     to ES_SHTLE-SHTLE.
  move LS_PCDIM_IN-PCLEN_UOM to ES_SHTLE-SHTLE_UOM.

  move LS_PCDIM_IN-PCWID     to ES_SHTWI-SHTWI.
  move LS_PCDIM_IN-PCWID_UOM to ES_SHTWI-SHTWI_UOM.

***------------------------------------------------------------------------------------------------
*** Adjust Dimensions for Cross Score
  if ME->F_CSCOR eq ZCL_VCXI_P0SS_CUST_PRSTY=>C_CSCOR_CROSS.     "Cross is second
*** Get Sheet Dimension Net
    LS_PCDIM_IN = LR_SHTDIM->GET_PCDIM( IF_POOTI = ZIF_VCXI_P0SS_SHTDIM=>C_POOTI_NET ).

*** Trim on Width was already removed
    move LS_PCDIM_IN-PCWID     to ES_SHTWI-SHTWI.
    move LS_PCDIM_IN-PCWID_UOM to ES_SHTWI-SHTWI_UOM.
  endif.

endmethod.
