
class ZCL_VCXI_P3SS_UI_MS_ASPROC definition
  public
  inheriting from ZCL_VCXI_PXSS_UI_MS_XPROC
  create public .

public section.

  data S_MS_ASPROC_VIS type ZVCXI_P3SS_S_MS_ASPROC_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABPT type ZVCXI_XCS_TABPT default '0101S_TAB-TAB'
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  constants C_UC_ASPROP_OUT type SYUCOMM value 'ASPROP_OUT' ##NO_TEXT.
  data R_PR_MS_ASPROC type ref to ZCL_VCXI_P3SS_PR_MS_ASPROC .

  methods HANDLE_UC_ASPROP
    importing
      !IF_UCOMM type SYUCOMM
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_P3SS_UI_MS_ASPROC definition
class ZCL_VCXI_P3SS_UI_MS_ASPROC implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_P3SS_UI_MS_ASPROC_BIND'
      exporting
        IR_UI_MS_ASPROC = ME
      importing
        ES_DYNP         = ME->S_DYNP.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE
                        IF_TABPT  = IF_TABPT ).
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_MS_ASPROC.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DO_MODIFY_SCREEN.

    try.
        SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                 IF_DYNNR = IF_DYNNR ).

        loop at screen.
***       Handling Dimension changeable if Override Flag is set
          if SCREEN-GROUP1 eq 'OHD'.
            if ME->S_MS_ASPROC_VIS-FOVRR_HNDDIM ne ABAP_TRUE.
              move 0 to SCREEN-INPUT.
            endif.
          endif.

          modify screen.
        endloop.

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_MS_ASPROC_VIS type ZVCXI_P3SS_S_MS_ASPROC_VIS.


*** Call SUPER
    SUPER->DO_UPDATE_VIS( IS_VIS = IS_VIS ).
    check ME->R_PR_MS_ASPROC is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_MS_ASPROC_VIS.

***--------------------------------------------------------------------------------------
*** Update Handling Dimension if it is "Overriden"
    if ME->S_MS_ASPROC_VIS-FOVRR_HNDDIM eq ABAP_TRUE and
       ME->S_MS_ASPROC_VIS-S_HNDDIM ne LS_MS_ASPROC_VIS-S_HNDDIM.
      ME->R_PR_MS_ASPROC->SET_HNDDIM( IS_HNDDIM = LS_MS_ASPROC_VIS-S_HNDDIM
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Flag Override Handling Dimension
    if ME->S_MS_ASPROC_VIS-FOVRR_HNDDIM ne LS_MS_ASPROC_VIS-FOVRR_HNDDIM.
      ME->R_PR_MS_ASPROC->SET_HNDDIM( IF_FOVRR = LS_MS_ASPROC_VIS-FOVRR_HNDDIM ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    case IF_UCOMM.
***   Assembly Property Popup
      when C_UC_ASPROP_OUT.
        ME->HANDLE_UC_ASPROP( exporting IF_UCOMM = IF_UCOMM  ).

    endcase.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_MS_ASPROC.

  endmethod.
  method HANDLE_UC_ASPROP.

*** Start Popup of Properties (Outbound)
    ZCL_VCXI_P3SS_POPUP_ASPROP=>START( IR_BL_ASBPROP = ME->R_PR_MS_ASPROC->ZIF_VCXI_P3SS_COMM_NEXT~GET_ASBPX( )
                                       IR_BL_ASFPROP = ME->R_PR_MS_ASPROC->ZIF_VCXI_P3SS_COMM_NEXT~GET_ASFPX( )
                                       IF_WERKS      = ME->R_PR_MS_ASPROC->GET_WERKS( ) ).

  endmethod.
  method INIT_VIS.

    data: LR_BL_ASFPROP type ref to ZCL_VCXI_P3SS_BL_ASFPROP.


    SUPER->INIT_VIS( ).

    free: ME->S_MS_ASPROC_VIS.
    ME->S_MS_ASPROC_VIS-S_MS_XPROC_VIS = ME->S_MS_XPROC_VIS.

***--------------------------------------------------------------------------------------
*** Handling Dimension
    ME->S_MS_ASPROC_VIS-S_HNDDIM = ME->R_PR_MS_ASPROC->GET_HNDDIM( importing EF_FOVRR = ME->S_MS_ASPROC_VIS-FOVRR_HNDDIM ).

***--------------------------------------------------------------------------------------
*** Get Handling Dimension (Outbound)
    LR_BL_ASFPROP = ME->R_PR_MS_ASPROC->ZIF_VCXI_P3SS_COMM_NEXT~GET_ASFPX( ).
    if LR_BL_ASFPROP is bound.
      ME->S_MS_ASPROC_VIS-S_IOSEC_OUT-S_HNDDIM = LR_BL_ASFPROP->S_HNDDIM.
    endif.

  endmethod.
