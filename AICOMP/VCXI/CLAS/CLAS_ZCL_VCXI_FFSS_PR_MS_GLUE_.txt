
class ZCL_VCXI_FFSS_PR_MS_GLUE definition
  public
  inheriting from ZCL_VCXI_XCSS_PR_MS
  create public .

public section.

  interfaces ZIF_VCXI_F0SS_COMM_NEXT .
  interfaces ZIF_VCXI_FFSS_COMM_NEXT .
  interfaces ZIF_VCXI_XCS_PSCALE_MGR .
  interfaces ZIF_VCXI_F0S_LTEXT .

  data R_CUST_GLUE type ref to ZCL_VCXI_FFSS_CUST_GLUE .
  constants C_IPRVT_MS_GLUE type /VCXI/VKSC_IPRVT value 'ZVCXI_FFSS_MS_GLUE' ##NO_TEXT.
  constants C_PARID_APWGT type /VCXI/VKS_PARID value 'ZVCXI_FF_APWGT' ##NO_TEXT.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods GET_APWGT
    exporting
      !ES_APWGT type ZVCXI_FFS_S_APWGT
      !EF_SETBY type /VCXI/VKSR_SETBY
    raising
      /VCXI/CX_CKX .
  methods GET_FOVRR_GLTIN
    exporting
      !EF_FOVRR type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
  methods GET_FOVRR_GLWID
    exporting
      !EF_FOVRR type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
  methods GET_GLTIN
    exporting
      !ES_GLTIN type ZVCXI_FFS_S_GLTIN
      !EF_SETBY type /VCXI/VKSR_SETBY
    raising
      /VCXI/CX_CKX .
  methods GET_GLWID
    exporting
      !ES_GLWID type ZVCXI_FFS_S_GLWID
      !EF_SETBY type /VCXI/VKSR_SETBY
    raising
      /VCXI/CX_CKX .
  methods GET_LTEXT
    exporting
      !EF_LTEXT type ZVCXI_F0S_LTEXT
      !EF_SETBY type /VCXI/VKSR_SETBY
    raising
      /VCXI/CX_CKX .
  methods GET_SLDCO
    exporting
      !ES_SLDCO type ZVCXI_FFS_S_SLDCO
      !EF_SETBY type /VCXI/VKSR_SETBY
    raising
      /VCXI/CX_CKX .
  methods GET_SURWT
    exporting
      !ES_SURWT type ZVCXI_F0S_S_SURWT
      !EF_SETBY type /VCXI/VKSR_SETBY
    raising
      /VCXI/CX_CKX .
  methods GET_TGLUE
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
      !EF_TGLUE type ZVCXI_FFS_TGLUE
    raising
      /VCXI/CX_CKX .
  methods GET_TREQG
    exporting
      !ES_TREQG type ZVCXI_FFS_S_TREQG
      !EF_SETBY type /VCXI/VKSR_SETBY
    raising
      /VCXI/CX_CKX .
  methods SET_APWGT
    importing
      !IS_APWGT type ZVCXI_FFS_S_APWGT
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_FOVRR_GLTIN
    importing
      !IF_FOVRR type ABAP_BOOL optional
    raising
      /VCXI/CX_CKX .
  methods SET_FOVRR_GLWID
    importing
      !IF_FOVRR type ABAP_BOOL optional
    raising
      /VCXI/CX_CKX .
  methods SET_GLTIN
    importing
      !IS_GLTIN type ZVCXI_FFS_S_GLTIN optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_GLWID
    importing
      !IS_GLWID type ZVCXI_FFS_S_GLWID optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_LTEXT
    importing
      !IF_LTEXT type ZVCXI_F0S_LTEXT
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_MAKTX
    importing
      !IF_MAKTX type MAKTX
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_SLDCO
    importing
      !IS_SLDCO type ZVCXI_FFS_S_SLDCO
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_TGLUE
    importing
      !IF_TGLUE type ZVCXI_FFS_TGLUE
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_TREQG
    importing
      !IS_TREQG type ZVCXI_FFS_S_TREQG optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .

  methods GET_MATNR_DESCR
    redefinition .
  methods GET_STATUS
    redefinition .
  methods GET_STATUS_MSG
    redefinition .
  methods GET_WERKS
    redefinition .
  methods HANDLE_EVENT
    redefinition .
protected section.

  constants C_IEVNT_I_RECALC_GLTIN type /VCXI/VKSC_IEVNT value 'RECALC_GLTIN' ##NO_TEXT.
  constants C_IEVNT_I_RECALC_GLWID type /VCXI/VKSC_IEVNT value 'RECALC_GLWID' ##NO_TEXT.
  constants C_IEVNT_I_RECALC_SURWT type /VCXI/VKSC_IEVNT value 'RECALC_SURWT' ##NO_TEXT.
  constants C_IEVNT_I_RECALC_TREQG type /VCXI/VKSC_IEVNT value 'RECALC_TREQG' ##NO_TEXT.
  constants C_IEVNT_I_REQ_BACKWARD type /VCXI/VKSC_IEVNT value 'REQ_BACKWARD' ##NO_TEXT.
  constants C_IEVNT_I_SET_DEFAULT type /VCXI/VKSC_IEVNT value 'SET_DEFAULT' ##NO_TEXT.
  constants C_IEVNT_I_REQ_FORWARD type /VCXI/VKSC_IEVNT value 'REQ_FORWARD' ##NO_TEXT.
  constants C_IEVNT_O_APWGT_CHANGED type /VCXI/VKSC_IEVNT value 'APWGT_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_GLTIN_CHANGED type /VCXI/VKSC_IEVNT value 'GLTIN_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_GLWID_CHANGED type /VCXI/VKSC_IEVNT value 'GLWID_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_SLDCO_CHANGED type /VCXI/VKSC_IEVNT value 'SLDCO_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_SURWT_CHANGED type /VCXI/VKSC_IEVNT value 'SURWT_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_TGLUE_CHANGED type /VCXI/VKSC_IEVNT value 'TGLUE_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_MAKTX_CHANGED type /VCXI/VKSC_IEVNT value 'MAKTX_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_TREQG_CHANGED type /VCXI/VKSC_IEVNT value 'TREQG_CHANGED' ##NO_TEXT.
  constants C_PARID_GLTIN type /VCXI/VKS_PARID value 'ZVCXI_FF_GLTIN' ##NO_TEXT.
  constants C_PARID_GLWID type /VCXI/VKS_PARID value 'ZVCXI_FF_GLWID' ##NO_TEXT.
  constants C_PARID_SLDCO type /VCXI/VKS_PARID value 'ZVCXI_FF_SLDCO' ##NO_TEXT.
  constants C_PARID_SURWT type /VCXI/VKS_PARID value 'ZVCXI_F0_SURWT' ##NO_TEXT.
  constants C_PARID_TGLUE type /VCXI/VKS_PARID value 'ZVCXI_FF_TGLUE' ##NO_TEXT.
  constants C_PARID_TREQW type /VCXI/VKS_PARID value 'ZVCXI_FF_TREQW' ##NO_TEXT.
  constants C_PARID_LTEXT type /VCXI/VKS_PARID value 'ZVCXI_F0_LTEXT' ##NO_TEXT.
  constants C_PARID_MAKTX type /VCXI/VKS_PARID value 'ZVCXI_XC_MAKTX' ##NO_TEXT.
  constants C_PARID_TREQG type /VCXI/VKS_PARID value 'ZVCXI_FF_TREQG' ##NO_TEXT.

  methods CHECK_APWGT
    returning
      value(RF_RETURN) type I
    raising
      /VCXI/CX_CKX .
  methods CHECK_GLWID
    returning
      value(RF_RETURN) type I
    raising
      /VCXI/CX_CKX .
  methods GET_FIWID_SIBLING
    exporting
      !ES_FIWID_MIN type ZVCXI_F0S_S_FIWID
      !ES_FIWID_MAX type ZVCXI_F0S_S_FIWID
    raising
      /VCXI/CX_CKX .
  methods HANDLE_RECALC_GLTIN
    raising
      /VCXI/CX_CKX .
  methods HANDLE_RECALC_GLWID
    raising
      /VCXI/CX_CKX .
  methods HANDLE_RECALC_SURWT
    raising
      /VCXI/CX_CKX .
  methods HANDLE_RECALC_TREQG
    raising
      /VCXI/CX_CKX .
  methods HANDLE_SET_DEFAULT
    raising
      /VCXI/CX_CKX .
  methods SET_SURWT
    importing
      !IS_SURWT type ZVCXI_F0S_S_SURWT optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .

  methods HANDLE_INIT_CUST_MATNR
    redefinition .
  methods HANDLE_INIT_PRICE
    redefinition .
  private section.
endclass. "ZCL_VCXI_FFSS_PR_MS_GLUE definition
class ZCL_VCXI_FFSS_PR_MS_GLUE implementation.
  method CHECK_APWGT.

*** 0 Application Weight is maintained and valid.
*** 1 Application Weight is maintained, but not valid.
*** 2 Application Weight is not maintained.

    data: LS_APWGT type ZVCXI_FFS_S_APWGT.

*** Get Application Weight
    ME->GET_APWGT( importing ES_APWGT = LS_APWGT ).

***--------------------------------------------------------------------------------------
*** Application Weight is not maintained.
    if LS_APWGT-APWGT is initial.
      move 2 to RF_RETURN.
      return.
    endif.

***--------------------------------------------------------------------------------------
    if ME->R_CUST_GLUE is bound.
***   Check Minimum
      if ME->R_CUST_GLUE->S_APWMI is not initial and
         ZCL_VCXI_XCS_SERVICE_CALC=>COMPARE_WITH_ANY( IF_CALCV_1     = LS_APWGT-APWGT
                                                      IF_CALCV_UOM_1 = LS_APWGT-APWGT_UOM
                                                      IF_CALCV_2     = ME->R_CUST_GLUE->S_APWMI-APWMI
                                                      IF_CALCV_UOM_2 = ME->R_CUST_GLUE->S_APWMI-APWMI_UOM
                                                      IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_LT ) eq ABAP_TRUE.
        move 1 to RF_RETURN.
      endif.
***   Check Maximum
      if ME->R_CUST_GLUE->S_APWMA is not initial and
         ZCL_VCXI_XCS_SERVICE_CALC=>COMPARE_WITH_ANY( IF_CALCV_1     = LS_APWGT-APWGT
                                                      IF_CALCV_UOM_1 = LS_APWGT-APWGT_UOM
                                                      IF_CALCV_2     = ME->R_CUST_GLUE->S_APWMA-APWMA
                                                      IF_CALCV_UOM_2 = ME->R_CUST_GLUE->S_APWMA-APWMA_UOM
                                                      IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_GT ) eq ABAP_TRUE.
        move 1 to RF_RETURN.
      endif.
    endif.

  endmethod.
  method CHECK_GLWID.

*** 0 Glue Width is maintained and valid.
*** 1 Glue Width is maintained, but not valid.
*** 2 Glue Width is not maintained.

    data: LS_GLWID     type ZVCXI_FFS_S_GLWID,
          LS_FIWID_MIN type ZVCXI_F0S_S_FIWID.

*** Get Glue Width
    ME->GET_GLWID( importing ES_GLWID = LS_GLWID ).

***--------------------------------------------------------------------------------------
*** Glue Width is not maintained.
    if LS_GLWID-GLWID is initial.
      move 2 to RF_RETURN.
      return.
    endif.

*** Get Film Width of Siblings
    ME->GET_FIWID_SIBLING( importing ES_FIWID_MIN = LS_FIWID_MIN ).

    move 1 to RF_RETURN.

*** Check against Minimum Film Width
    if LS_FIWID_MIN-FIWID is not initial and
       ZCL_VCXI_XCS_SERVICE_CALC=>COMPARE_WITH_ANY( IF_CALCV_1     = LS_GLWID-GLWID
                                                    IF_CALCV_UOM_1 = LS_GLWID-GLWID_UOM
                                                    IF_CALCV_2     = LS_FIWID_MIN-FIWID
                                                    IF_CALCV_UOM_2 = LS_FIWID_MIN-FIWID_UOM
                                                    IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_LE ) eq ABAP_TRUE.
      move 0 to RF_RETURN.
    endif.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_GATE = IR_GATE ).

    move C_PARID_PRICP_W to ME->F_PARID_PRICP.

  endmethod.
  method GET_APWGT.

    data: LS_PARAM type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_APWGT ).

    move LS_PARAM-ATFLV to ES_APWGT-APWGT.
    move LS_PARAM-UNIT  to ES_APWGT-APWGT_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_FIWID_SIBLING.

    data: LF_TABIX_PREV   type        I,
          LF_TABIX_NEXT   type        I.
    data: LS_FIWID        type        ZVCXI_F0S_S_FIWID.
    data: LT_GATE         type        /VCXI/VKSR_TT_GATE.
    data: LR_GATE         type ref to /VCXI/CL_VKSR_GATE,
          LR_GLUE_CARRIER type ref to ZIF_VCXI_FFSS_GLUE_CARRIER.

***--------------------------------------------------------------------------------------
*** Get all "Glue Carrier" Siblings
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZIF_VCXI_FFSS_GLUE_CARRIER=>C_IPRVT_GLUE_CARRIER
                                     IF_INUSE = ABAP_TRUE ).

*** Filter those that ignore Trim Requirement
    loop at LT_GATE into LR_GATE.
      try.
          move LR_GATE->R_CLSPR ?to LR_GLUE_CARRIER.
        catch CX_SY_MOVE_CAST_ERROR.
          clear LR_GLUE_CARRIER.
      endtry.
      if LR_GLUE_CARRIER is bound.
        if LR_GLUE_CARRIER->GET_FITRW( ) eq ABAP_TRUE.
          delete LT_GATE.
        endif.
      else.
        delete LT_GATE.
      endif.
    endloop.

*** Add own Gate to List and sort the list
    append ME->R_GATE to LT_GATE.
    ME->R_GATE->SORT_GATE( changing CT_GATE = LT_GATE ).

***--------------------------------------------------------------------------------------
*** Find own Gate in List
    read table LT_GATE with key TABLE_LINE = ME->R_GATE
                       transporting no fields.
    check SY-SUBRC eq 0.
    LF_TABIX_PREV = SY-TABIX - 1.
    LF_TABIX_NEXT = SY-TABIX + 1.

***--------------------------------------------------------------------------------------
*** Consider previous Sibling
    read table LT_GATE into LR_GATE index LF_TABIX_PREV.
    if SY-SUBRC eq 0.
      try.
          move LR_GATE->R_CLSPR ?to LR_GLUE_CARRIER.
        catch CX_SY_MOVE_CAST_ERROR.
          clear LR_GLUE_CARRIER.
      endtry.
      if LR_GLUE_CARRIER is bound.
        move LR_GLUE_CARRIER->GET_FIWID( ) to LS_FIWID.

***     Use as Min and Max
        move LS_FIWID to ES_FIWID_MIN.
        move LS_FIWID to ES_FIWID_MAX.
      endif.
    endif.

***--------------------------------------------------------------------------------------
*** Consider next Sibling
    read table LT_GATE into LR_GATE index LF_TABIX_NEXT.
    if SY-SUBRC eq 0.
      try.
          move LR_GATE->R_CLSPR ?to LR_GLUE_CARRIER.
        catch CX_SY_MOVE_CAST_ERROR.
          clear LR_GLUE_CARRIER.
      endtry.
      if LR_GLUE_CARRIER is bound.
        move LR_GLUE_CARRIER->GET_FIWID( ) to LS_FIWID.

***     Find Minimum
        if ES_FIWID_MIN is initial.
          move LS_FIWID to ES_FIWID_MIN.
        else.
          if ZCL_VCXI_XCS_SERVICE_CALC=>COMPARE_WITH_ANY( IF_CALCV_1     = LS_FIWID-FIWID
                                                          IF_CALCV_UOM_1 = LS_FIWID-FIWID_UOM
                                                          IF_CALCV_2     = ES_FIWID_MIN-FIWID
                                                          IF_CALCV_UOM_2 = ES_FIWID_MIN-FIWID_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_LT ) eq ABAP_TRUE.
            move LS_FIWID to ES_FIWID_MIN.
          endif.
        endif.

***     Find Maximum
        if ES_FIWID_MAX is initial.
          move LS_FIWID to ES_FIWID_MAX.
        else.
          if ZCL_VCXI_XCS_SERVICE_CALC=>COMPARE_WITH_ANY( IF_CALCV_1     = LS_FIWID-FIWID
                                                          IF_CALCV_UOM_1 = LS_FIWID-FIWID_UOM
                                                          IF_CALCV_2     = ES_FIWID_MAX-FIWID
                                                          IF_CALCV_UOM_2 = ES_FIWID_MAX-FIWID_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_GT ) eq ABAP_TRUE.
            move LS_FIWID to ES_FIWID_MAX.
          endif.
        endif.
      endif.
    endif.

  endmethod.
  method GET_FOVRR_GLTIN.

    data: LF_SETBY type /VCXI/VKSR_SETBY.

*** Get Setby
    ME->GET_GLTIN( importing EF_SETBY = LF_SETBY ).

    if LF_SETBY eq /VCXI/CL_VKSR_GATE=>C_SETBY_USER.
      move ABAP_TRUE  to EF_FOVRR.
    else.
      move ABAP_FALSE to EF_FOVRR.
    endif.

  endmethod.
  method GET_FOVRR_GLWID.

    data: LF_SETBY type /VCXI/VKSR_SETBY.

*** Get Setby
    ME->GET_GLWID( importing EF_SETBY = LF_SETBY ).

    if LF_SETBY eq /VCXI/CL_VKSR_GATE=>C_SETBY_USER.
      move ABAP_TRUE  to EF_FOVRR.
    else.
      move ABAP_FALSE to EF_FOVRR.
    endif.

  endmethod.
  method GET_GLTIN.

    data: LS_PARAM type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_GLTIN ).

    move LS_PARAM-ATFLV to ES_GLTIN-GLTIN.
    move LS_PARAM-UNIT  to ES_GLTIN-GLTIN_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_GLWID.

    data: LS_PARAM type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_GLWID ).

    move LS_PARAM-ATFLV to ES_GLWID-GLWID.
    move LS_PARAM-UNIT  to ES_GLWID-GLWID_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_LTEXT.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_LTEXT ).
    move LS_PARAM-STRNG to EF_LTEXT.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_MATNR_DESCR.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    SUPER->GET_MATNR_DESCR( receiving RF_DESCR = RF_DESCR ).

    if ME->IS_DUMMY( ) eq ABAP_TRUE.
      LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_MAKTX ).

      if LS_PARAM-SETBY eq /VCXI/CL_VKSR_GATE=>C_SETBY_USER.
        move LS_PARAM-STRNG to RF_DESCR.
      endif.
    endif.

  endmethod.
  method GET_SLDCO.

    data: LS_PARAM type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SLDCO ).

    move LS_PARAM-ATFLV to ES_SLDCO-SLDCO.
    move LS_PARAM-UNIT  to ES_SLDCO-SLDCO_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_STATUS.

    data: LS_SLDCO type ZVCXI_FFS_S_SLDCO.
    data: LF_TGLUE type ZVCXI_FFS_TGLUE.

***--------------------------------------------------------------------------------------
*** Super Status
    RF_ELMST = SUPER->GET_STATUS( ).

***--------------------------------------------------------------------------------------
*** Check Application Weight
    if ME->CHECK_APWGT( ) eq 0.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                     IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G ).
    else.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                     IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R ).
    endif.

***--------------------------------------------------------------------------------------
*** Check Glue Width
    if ME->CHECK_GLWID( ) eq 0.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                     IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G ).
    else.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                     IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R ).
    endif.

***--------------------------------------------------------------------------------------
*** Check Glue Type
    ME->GET_TGLUE( importing EF_TGLUE = LF_TGLUE ).
    if LF_TGLUE is not initial.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                     IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G ).
    else.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                     IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R ).
    endif.

***--------------------------------------------------------------------------------------
*** Check Solid Content
    ME->GET_SLDCO( importing ES_SLDCO = LS_SLDCO ).
    if LS_SLDCO-SLDCO is not initial.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                     IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G ).
    else.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                     IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R ).
    endif.

  endmethod.
  method GET_STATUS_MSG.

    data: LS_SLDCO type ZVCXI_FFS_S_SLDCO.
    data: LF_TGLUE type ZVCXI_FFS_TGLUE.

***--------------------------------------------------------------------------------------
*** Super Status Messages
    SUPER->GET_STATUS_MSG( IR_MESSAGE = IR_MESSAGE ).

***--------------------------------------------------------------------------------------
*** Check Application Weight
    case ME->CHECK_APWGT( ).
      when 0.
***     Application Weight is maintained and valid.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                                 IF_MSGTY = 'S'
                                 IF_MSGNO = '411' ).
      when 1.
***     Application Weight is maintained, but not valid.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '431' ).
      when 2.
***     Application Weight is not maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '412' ).
    endcase.

***--------------------------------------------------------------------------------------
*** Check Glue Width
    case ME->CHECK_GLWID( ).
      when 0.
***     Glue Width is maintained and valid.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                                 IF_MSGTY = 'S'
                                 IF_MSGNO = '432' ).

      when 1.
***     Glue Width is maintained, but not valid.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '438' ).

      when 2.
***     Glue Width is not maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '433' ).

    endcase.

***--------------------------------------------------------------------------------------
*** Check Glue Type
    ME->GET_TGLUE( importing EF_TGLUE = LF_TGLUE ).
    if LF_TGLUE is not initial.
***   Glue Type is maintained.
      IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                               IF_MSGTY = 'S'
                               IF_MSGNO = '434' ).
    else.
***   Glue Type is not maintained.
      IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                               IF_MSGTY = 'E'
                               IF_MSGNO = '435' ).

    endif.

***--------------------------------------------------------------------------------------
*** Check Solid Content
    ME->GET_SLDCO( importing ES_SLDCO = LS_SLDCO ).
    if LS_SLDCO-SLDCO is not initial.
***   Solid Content is maintained.
      IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                               IF_MSGTY = 'S'
                               IF_MSGNO = '436' ).
    else.
***   Solid Content is not maintained.
      IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_FFSS'
                               IF_MSGTY = 'E'
                               IF_MSGNO = '437' ).
    endif.

  endmethod.
  method GET_SURWT.

    data: LS_PARAM type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SURWT ).

    move LS_PARAM-ATFLV to ES_SURWT-SURWT.
    move LS_PARAM-UNIT  to ES_SURWT-SURWT_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_TGLUE.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TGLUE ).

    move LS_PARAM-ATWRT to EF_TGLUE.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_TREQG.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TREQG ).
    move LS_PARAM-ATFLV to ES_TREQG-TREQG.
    move LS_PARAM-UNIT  to ES_TREQG-TREQG_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_WERKS.

    data: LT_GATE    type        /VCXI/VKSR_TT_GATE.
    data: LR_GATE    type ref to /VCXI/CL_VKSR_GATE,
          LR_PR_SPEC type ref to ZCL_VCXI_F0SS_PR_SPEC.

    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_F0SS_PR_SPEC=>C_IPRVT_F0_SPEC ).
    read table LT_GATE into LR_GATE index 1.
    if SY-SUBRC eq 0.
      move LR_GATE->R_CLSPR ?to LR_PR_SPEC.
      LR_PR_SPEC->GET_WERKS( importing EF_WERKS = RF_WERKS ).
    else.
      RF_WERKS = SUPER->GET_WERKS( ).
    endif.

  endmethod.
  method HANDLE_EVENT.

    SUPER->HANDLE_EVENT( IF_IEVNT         = IF_IEVNT
                         IT_GATE_PROVIDER = IT_GATE_PROVIDER ).

    case IF_IEVNT.
***   Event Forwarding for Communication
      when C_IEVNT_I_REQ_FORWARD.
        ME->R_GATE->SET_EVENT( IF_IEVNT = ZIF_VCXI_F0SS_COMM_NEXT~C_IEVNT_O_FORWARD_REQ ).
      when C_IEVNT_I_REQ_BACKWARD.
        ME->R_GATE->SET_EVENT( IF_IEVNT = ZIF_VCXI_F0SS_COMM_NEXT~C_IEVNT_O_BACKWARD_REQ ).

      when C_IEVNT_I_INIT_CUST_MATNR.
        ME->HANDLE_INIT_CUST_MATNR( ).

      when C_IEVNT_I_RECALC_SURWT.
        ME->HANDLE_RECALC_SURWT( ).

      when C_IEVNT_I_RECALC_GLWID.
        ME->HANDLE_RECALC_GLWID( ).

      when C_IEVNT_I_RECALC_TREQG.
        ME->HANDLE_RECALC_TREQG( ).

      when C_IEVNT_I_RECALC_GLTIN.
        ME->HANDLE_RECALC_GLTIN( ).

      when C_IEVNT_I_SET_DEFAULT.
        ME->HANDLE_SET_DEFAULT( ).

    endcase.

  endmethod.
  method HANDLE_INIT_CUST_MATNR.

    data: LF_MATNR type MATNR,
          LF_WERKS type WERKS_D,
          LF_DCONF type DATS,
          LF_TGLUE type ZVCXI_FFS_TGLUE.
    data: LS_SLDCO type ZVCXI_FFS_S_SLDCO.

***--------------------------------------------------------------------------------------
    move ME->GET_MATNR( )  to LF_MATNR.
    move ME->GET_WERKS( )  to LF_WERKS.
    ME->R_GATE->GET_CONFIG_INFO( importing EF_DCONF = LF_DCONF ).

    if ME->R_CUST_GLUE          is bound    and
       ME->R_CUST_GLUE->F_MATNR eq LF_MATNR and
       ME->R_CUST_GLUE->F_WERKS eq LF_WERKS.
***   Everything was loaded already...
      return.
    endif.
    clear:  ME->R_CUST_MATNR, ME->R_CUST_GLUE.

***--------------------------------------------------------------------------------------
*** Load the Customizing
    if LF_MATNR is not initial.
      try.
          ME->R_CUST_GLUE = ZCL_VCXI_FFSS_CUST_GLUE=>GET_INSTANCE_GLUE( IF_MATNR = LF_MATNR
                                                                        IF_WERKS = LF_WERKS
                                                                        IF_DATE  = LF_DCONF ).
        catch /VCXI/CX_CKX.
***       It is not needed to handle Exception
          return.
      endtry.
    endif.
    move ME->R_CUST_GLUE to ME->R_CUST_MATNR.

***--------------------------------------------------------------------------------------
*** Set Values for Real Glue
    if ME->IS_DUMMY( ) eq ABAP_FALSE.

      if ME->R_CUST_GLUE is bound.
        move ME->R_CUST_GLUE->F_TGLUE to LF_TGLUE.
        move ME->R_CUST_GLUE->S_SLDCO to LS_SLDCO.
      endif.

***   Set Glue Type
      ME->SET_TGLUE( IF_TGLUE = LF_TGLUE ).

***   Set Solid Content
      ME->SET_SLDCO( IS_SLDCO = LS_SLDCO ).
    else.
***   Set Procurement Option
      ME->SET_PURCH( IF_PURCH = ZCL_VCXI_XCSS_PR_MS=>C_PURCH_DIRECT_PRICE ).
    endif.

  endmethod.
  method HANDLE_INIT_PRICE.

*** No Defaulting for Direct Procurement & Price - as we use Scales only
    if ME->GET_PURCH( ) ne ZCL_VCXI_XCSS_PR_MS=>C_PURCH_DIRECT_PRICE.
      SUPER->HANDLE_INIT_PRICE( ).
    else.
***   Clear Price Data
      ME->SET_PRICE( IF_SETBY = SPACE ).
      ME->SET_PRICP( IF_SETBY = SPACE ).
    endif.

  endmethod.
  method HANDLE_RECALC_GLTIN.

    data: LF_SETBY type /VCXI/VKSR_SETBY.
    data: LS_SURWT type ZVCXI_F0S_S_SURWT,
          LS_GLTIN type ZVCXI_FFS_S_GLTIN.

*** Only Calcualate if not set by User
    ME->GET_GLTIN( importing ES_GLTIN = LS_GLTIN
                             EF_SETBY = LF_SETBY  ).
    check LF_SETBY ne /VCXI/CL_VKSR_GATE=>C_SETBY_USER.

*** Get Surface Weight
    ME->GET_SURWT( importing ES_SURWT = LS_SURWT ).

    break HARDCODED.  "Story OPSCVCIS-1175
*** Default of 1µm for each g/m2 of the dry weight
    call function 'ROUND'
      exporting
        INPUT  = LS_SURWT-SURWT
        SIGN   = 'X'
      importing
        OUTPUT = LS_GLTIN-GLTIN.

*** Set Internal Glue Thickness
    ME->SET_GLTIN( IS_GLTIN = LS_GLTIN ).

  endmethod.
  method HANDLE_RECALC_GLWID.

    data: LF_SETBY type /VCXI/VKSR_SETBY,
          LF_WERKS type WERKS_D,
          LF_TGLUE type ZVCXI_FFS_TGLUE.
    data: LS_FIWID_MIN type ZVCXI_F0S_S_FIWID,
          LS_GLWID     type ZVCXI_FFS_S_GLWID,
          LS_REDUW     type ZVCXI_FFS_S_REDUW.

*** Only Calcualate if not set by User
    ME->GET_GLWID( importing  ES_GLWID = LS_GLWID
                              EF_SETBY = LF_SETBY  ).
    check LF_SETBY ne /VCXI/CL_VKSR_GATE=>C_SETBY_USER.

*** Get Film Width of Siblings
    ME->GET_FIWID_SIBLING( importing ES_FIWID_MIN = LS_FIWID_MIN ).

    if ME->R_CUST_GLUE is bound.

***   Get Werks
      move ME->GET_WERKS( ) to LF_WERKS.

***   Get Glue Type
      ME->GET_TGLUE( importing EF_TGLUE = LF_TGLUE ).

***   Get Glue Width Reduction
      LS_REDUW = ME->R_CUST_GLUE->GET_REDUW( exporting IF_WERKS = LF_WERKS
                                                       IF_TGLUE = LF_TGLUE ).

      if LS_FIWID_MIN is not initial and
         LS_REDUW     is not initial.

***     Calculate Glue Width
        ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_FIWID_MIN-FIWID
                                                            IF_CALCV_UOM_1 = LS_FIWID_MIN-FIWID_UOM
                                                            IF_CALCV_2     = LS_REDUW-REDUW
                                                            IF_CALCV_UOM_2 = LS_REDUW-REDUW_UOM
                                                            IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_SUBTRACT
                                                            IF_UOM         = LS_FIWID_MIN-FIWID_UOM
                                                  importing EF_CALCV       = LS_GLWID-GLWID
                                                            EF_CALCV_UOM   = LS_GLWID-GLWID_UOM ).
      endif.
    endif.

*** No negative Glue Width
    if LS_GLWID-GLWID lt 0.
      move 0 to LS_GLWID-GLWID.
    endif.

*** Set Glue Width
    ME->SET_GLWID( IS_GLWID = LS_GLWID ).

  endmethod.
  method HANDLE_RECALC_SURWT.

    data: LS_APWGT type ZVCXI_FFS_S_APWGT,
          LS_SLDCO type ZVCXI_FFS_S_SLDCO,
          LS_SURWT type ZVCXI_F0S_S_SURWT.

*** Get Application Weight and Solid Content
    ME->GET_APWGT( importing ES_APWGT = LS_APWGT ).
    ME->GET_SLDCO( importing ES_SLDCO = LS_SLDCO ).

*** Calculate Surface Weight
    ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY(
      exporting
        IF_CALCV_1     = LS_APWGT-APWGT
        IF_CALCV_UOM_1 = LS_APWGT-APWGT_UOM
        IF_CALCV_2     = LS_SLDCO-SLDCO
        IF_CALCV_UOM_2 = LS_SLDCO-SLDCO_UOM
        IF_UOM         = LS_APWGT-APWGT_UOM
        IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_MULTIPLY
      importing
        EF_CALCV       = LS_SURWT-SURWT
        EF_CALCV_UOM   = LS_SURWT-SURWT_UOM ).

*** Set new Surface Weight
    ME->SET_SURWT( IS_SURWT = LS_SURWT ).

  endmethod.
  method HANDLE_RECALC_TREQG.

    data: LS_FIWID_MAX type ZVCXI_F0S_S_FIWID,
          LS_TREQG     type ZVCXI_FFS_S_TREQG,
          LS_GLWID     type ZVCXI_FFS_S_GLWID.

*** Get Trim Req of Glue
    ME->GET_TREQG( importing  ES_TREQG = LS_TREQG ).

*** Get Glue Width
    ME->GET_GLWID( importing  ES_GLWID = LS_GLWID ).

    if LS_GLWID-GLWID ne 0.
***   Get Max Film Width of Siblings
      ME->GET_FIWID_SIBLING( importing ES_FIWID_MAX = LS_FIWID_MAX ).

      if LS_FIWID_MAX is not initial.
***     Calculate Trim Req of Glue
        ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_FIWID_MAX-FIWID
                                                            IF_CALCV_UOM_1 = LS_FIWID_MAX-FIWID_UOM
                                                            IF_CALCV_2     = LS_GLWID-GLWID
                                                            IF_CALCV_UOM_2 = LS_GLWID-GLWID_UOM
                                                            IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_SUBTRACT
                                                            IF_UOM         = LS_FIWID_MAX-FIWID_UOM
                                                  importing EF_CALCV       = LS_TREQG-TREQG
                                                            EF_CALCV_UOM   = LS_TREQG-TREQG_UOM ).
      endif.

***   No negative Trim Req of Glue
      if LS_TREQG-TREQG lt 0.
        move 0 to LS_TREQG-TREQG.
      endif.

    else.
***   No Requirement as long as we have no Glue Width maintained
      clear LS_TREQG.
    endif.

*** Set Trim Req of Glue
    ME->SET_TREQG( IS_TREQG = LS_TREQG ).

  endmethod.
  method HANDLE_SET_DEFAULT.

    ZCL_VCXI_XCDR_SERVICE=>SET_DEFAULT( IR_GATE = ME->R_GATE ).

  endmethod.
  method SET_APWGT.

    data: LF_CHANGED type ABAP_BOOL.
    data: LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_APWGT ).

    move IS_APWGT-APWGT      to LS_PARAM-ATFLV.
    move IS_APWGT-APWGT_UOM  to LS_PARAM-UNIT.
    move IF_SETBY            to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_APWGT_CHANGED ).
    endif.

  endmethod.
  method SET_FOVRR_GLTIN.

*** Set Setby for Internal Glue Thickness
    case IF_FOVRR.
      when ABAP_TRUE.
        ME->SET_GLTIN( IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
      when ABAP_FALSE.
        ME->SET_GLTIN( IF_SETBY = SPACE ).
    endcase.

  endmethod.
  method SET_FOVRR_GLWID.

*** Set Setby for Glue Width
    case IF_FOVRR.
      when ABAP_TRUE.
        ME->SET_GLWID( IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
      when ABAP_FALSE.
        ME->SET_GLWID( IF_SETBY = SPACE ).
    endcase.

  endmethod.
  method SET_GLTIN.

    data: LF_CHANGED type ABAP_BOOL.
    data: LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_GLTIN ).

    if IS_GLTIN is supplied.
      move IS_GLTIN-GLTIN     to LS_PARAM-ATFLV.
      move IS_GLTIN-GLTIN_UOM to LS_PARAM-UNIT.
    endif.

    move IF_SETBY           to LS_PARAM-SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_GLTIN_CHANGED ).
    endif.

  endmethod.
  method SET_GLWID.

    data: LF_CHANGED type ABAP_BOOL.
    data: LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_GLWID ).

    if IS_GLWID is supplied.
      move IS_GLWID-GLWID     to LS_PARAM-ATFLV.
      move IS_GLWID-GLWID_UOM to LS_PARAM-UNIT.
    endif.

    move IF_SETBY           to LS_PARAM-SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_GLWID_CHANGED ).
    endif.

  endmethod.
  method SET_LTEXT.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_LTEXT ).

    move IF_LTEXT   to LS_PARAM-STRNG.
    move IF_SETBY   to LS_PARAM-SETBY.

    ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

  endmethod.
  method SET_MAKTX.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_MAKTX ).

    move IF_MAKTX to LS_PARAM-STRNG.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_MAKTX_CHANGED ).
    endif.

  endmethod.
  method SET_SLDCO.

    data: LF_CHANGED type ABAP_BOOL.
    data: LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SLDCO ).

    move IS_SLDCO-SLDCO      to LS_PARAM-ATFLV.
    move IS_SLDCO-SLDCO_UOM  to LS_PARAM-UNIT.
    move IF_SETBY            to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_SLDCO_CHANGED ).
    endif.

  endmethod.
  method SET_SURWT.

    data: LF_CHANGED type ABAP_BOOL.
    data: LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SURWT ).

    move IS_SURWT-SURWT     to LS_PARAM-ATFLV.
    move IS_SURWT-SURWT_UOM to LS_PARAM-UNIT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_SURWT_CHANGED ).
    endif.

  endmethod.
  method SET_TGLUE.

    data: LF_CHANGED type ABAP_BOOL.
    data: LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TGLUE ).

    move IF_TGLUE           to LS_PARAM-ATWRT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_TGLUE_CHANGED ).
    endif.

  endmethod.
  method SET_TREQG.

    data: LF_CHANGED type ABAP_BOOL.
    data: LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TREQG ).

    move IS_TREQG-TREQG     to LS_PARAM-ATFLV.
    move IS_TREQG-TREQG_UOM to LS_PARAM-UNIT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_TREQG_CHANGED ).
    endif.

  endmethod.
  method ZIF_VCXI_F0S_LTEXT~GET_LTEXT.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = ZIF_VCXI_F0S_LTEXT~C_PARID_LTEXT ).
    move LS_PARAM-STRNG to RF_LTEXT.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method ZIF_VCXI_F0S_LTEXT~SET_LTEXT.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = ZIF_VCXI_F0S_LTEXT~C_PARID_LTEXT ).

    move IF_LTEXT   to LS_PARAM-STRNG.
    move IF_SETBY   to LS_PARAM-SETBY.

    ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

  endmethod.
  method ZIF_VCXI_F0SS_COMM_NEXT~DO_BACKWARD.
    return.
  endmethod.
  method ZIF_VCXI_FFSS_COMM_NEXT~GET_FIBPX         ##NEEDED.
  endmethod.
  method ZIF_VCXI_FFSS_COMM_NEXT~GET_FIFPX.
    return.
  endmethod.
  method ZIF_VCXI_FFSS_COMM_NEXT~GET_FISTX.

    data: LS_FSGLUE type        ZVCXI_FFSS_S_FSGLUE.

***--------------------------------------------------------------------------------------
*** Get new Film Structure Instance
    ER_BL_FISTRUCT = ZCL_VCXI_FFSS_BL_FISTRUCT=>FACTORY( ).


***--------------------------------------------------------------------------------------
*** Collect Glue Information
    ME->GET_TGLUE( importing EF_TGLUE = LS_FSGLUE-TGLUE ).

*** Add Glue Information to Structure
    ER_BL_FISTRUCT->ADD_ENTRY( IS_FISTRUCT = LS_FSGLUE
                               IF_TFSDA    = ZCL_VCXI_FFSS_BL_FISTRUCT=>C_TFSDA_GLUE ).


***--------------------------------------------------------------------------------------
*** Get new Film Structure XML
    ER_BL_FISTRUCT->GET_FISTX( importing EF_FISTX = EF_FISTX ).

  endmethod.
  method ZIF_VCXI_FFSS_COMM_NEXT~GET_FITEX.

*** Glue has no External Film Thickness
    clear: ES_FITEX,
           EF_SETBY.

  endmethod.
  method ZIF_VCXI_FFSS_COMM_NEXT~GET_FITIN.

*** Get Internal Glue Thickness
    ME->GET_GLTIN( importing ES_GLTIN = ES_FITIN
                             EF_SETBY = EF_SETBY ).

  endmethod.
  method ZIF_VCXI_FFSS_COMM_NEXT~GET_FIWID.

*** Glue has no Film Width
    clear: ES_FIWID,
           EF_SETBY.

  endmethod.
  method ZIF_VCXI_FFSS_COMM_NEXT~GET_NOUPS.

*** Glue has no #Ups
    clear: EF_NOUPS,
           EF_SETBY.

  endmethod.
  method ZIF_VCXI_FFSS_COMM_NEXT~GET_RELEN.
    return.
  endmethod.
  method ZIF_VCXI_FFSS_COMM_NEXT~GET_SURWT.

*** Get Surface Weight (Inbound)
    ME->GET_SURWT( importing ES_SURWT = ES_SURWT
                             EF_SETBY = EF_SETBY ).

  endmethod.
  method ZIF_VCXI_FFSS_COMM_NEXT~GET_TREQW.

    data: LS_PARAM type /VCXI/VKSR_S_PARAM,
          LS_TREQG type ZVCXI_FFS_S_TREQG.

*** Get Trim Req of Glue
    ME->GET_TREQG( importing  ES_TREQG = LS_TREQG ).

*** Trim Req on Width
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TREQW ).
    move LS_PARAM-ATFLV to ES_TREQW-TREQW.
    move LS_PARAM-UNIT  to ES_TREQW-TREQW_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

*** Calculate Trim Req of Glue
    ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = ES_TREQW-TREQW
                                                        IF_CALCV_UOM_1 = ES_TREQW-TREQW_UOM
                                                        IF_CALCV_2     = LS_TREQG-TREQG
                                                        IF_CALCV_UOM_2 = LS_TREQG-TREQG_UOM
                                                        IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_ADD
                                              importing EF_CALCV       = ES_TREQW-TREQW
                                                        EF_CALCV_UOM   = ES_TREQW-TREQW_UOM ).
  endmethod.
  method ZIF_VCXI_XCS_PSCALE_MGR~GET_PSCALE.

    data: LS_PSQTY       type        ZVCXI_XCS_S_PSQTY,
          LS_PSQTY_MATCH type        ZVCXI_XCS_S_PSQTY.
    data: LT_GATE            type        /VCXI/VKSR_TT_GATE.
    data: LR_PR_PSCALE       type ref to ZCL_VCXI_XCS_PR_PSCALE,
          LR_PR_PSCALE_MATCH type ref to ZCL_VCXI_XCS_PR_PSCALE,
          LR_GATE            type ref to /VCXI/CL_VKSR_GATE.

*** Get Gates of Price Scales
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_XCS_PR_PSCALE=>C_IPRVT_XCS_PSCALE
                                     IF_INUSE  = ABAP_TRUE
                                     IF_SORTED = ABAP_TRUE ).


    loop at LT_GATE into LR_GATE.
      try.
          move LR_GATE->R_CLSPR ?to LR_PR_PSCALE.
          check LR_PR_PSCALE is bound.

***------------------------------------------------------------------------------------------------
***     Get Price Scale Quantity
          move LR_PR_PSCALE->GET_PSQTY( )  to LS_PSQTY.

          if   IS_PSQTY-PSQTY       ge LS_PSQTY-PSQTY and
             ( LS_PSQTY_MATCH-PSQTY le LS_PSQTY-PSQTY or
               LS_PSQTY_MATCH-PSQTY is initial ).
            move LR_PR_PSCALE to LR_PR_PSCALE_MATCH.
            move LS_PSQTY     to LS_PSQTY_MATCH.
          endif.

        catch CX_SY_MOVE_CAST_ERROR.
          continue.
      endtry.
    endloop.

***------------------------------------------------------------------------------------------------
*** If Price Scale was found -> Return Price and Price Unit
    check LR_PR_PSCALE_MATCH is bound.
    ES_PRICE = LR_PR_PSCALE_MATCH->GET_PRICE( ).
    ES_PRICP = LR_PR_PSCALE_MATCH->GET_PRICP( ).

  endmethod.
  method ZIF_VCXI_XCS_PSCALE_MGR~IS_CHANGEABLE.

    data: LF_LOCKED type /VCXI/CKX_LOCKED.

    check ME->R_GATE is bound.
    LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
    move LF_LOCKED to RF_CHANGEABLE.

  endmethod.
