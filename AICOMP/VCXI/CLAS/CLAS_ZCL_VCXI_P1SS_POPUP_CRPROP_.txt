
class ZCL_VCXI_P1SS_POPUP_CRPROP definition
  public
  create protected .

public section.

  constants C_UC_CANCEL type SYUCOMM value 'CANCEL' ##NO_TEXT.
  data S_POPUP_CRPROP_VIS type ZVCXI_P1SS_S_POPUP_CRPROP_VIS .

  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods DO_USER_COMMAND
    importing
      !IF_UCOMM type SYUCOMM
    raising
      /VCXI/CX_CKX .
  class-methods START
    importing
      !IR_BL_CRBPROP type ref to ZCL_VCXI_P1SS_BL_CRBPROP
      !IR_BL_CRFPROP type ref to ZCL_VCXI_P1SS_BL_CRFPROP
      !IF_WERKS type WERKS_D
      !IS_SURWT type ZVCXI_PXS_S_SURWT
    raising
      /VCXI/CX_CKX .
protected section.

  types:
    begin of YS_JOINT_GRID,
      JOITY type ZVCXI_P0S_JOITY,
      DESCR type /VCXI/CKX_DESCR,
    end of YS_JOINT_GRID .
  types:
    YT_JOINT_GRID type standard table of YS_JOINT_GRID .

  data R_BL_CRFPROP type ref to ZCL_VCXI_P1SS_BL_CRFPROP .
  data R_BL_CRBPROP type ref to ZCL_VCXI_P1SS_BL_CRBPROP .
  data R_SALV_JOINT type ref to CL_SALV_TABLE .
  data T_JOINT_GRID type YT_JOINT_GRID .
  data R_GRID_PRINT_COM type ref to ZCL_VCXI_PXSS_GRID_PRINT_COM .

  methods DO_FREE
    raising
      /VCXI/CX_CKX .
  methods INIT_GRID_PRINT_COM
    raising
      /VCXI/CX_CKX .
  methods CONSTRUCTOR
    importing
      !IR_BL_CRFPROP type ref to ZCL_VCXI_P1SS_BL_CRFPROP
      !IR_BL_CRBPROP type ref to ZCL_VCXI_P1SS_BL_CRBPROP
      !IF_WERKS type WERKS_D
      !IS_SURWT type ZVCXI_PXS_S_SURWT
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_CANCEL
    raising
      /VCXI/CX_CKX .
  methods INIT_DATA_JOINT
    raising
      /VCXI/CX_CKX .
  methods INIT_SALV_JOINT
    raising
      /VCXI/CX_CKX .
  methods SET_PF_STATUS
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_P1SS_POPUP_CRPROP definition
class ZCL_VCXI_P1SS_POPUP_CRPROP implementation.
  method CONSTRUCTOR.

***--------------------------------------------------------------------------------------
*** Keep Input
    ME->R_BL_CRFPROP = IR_BL_CRFPROP.
    ME->R_BL_CRBPROP = IR_BL_CRBPROP.

***--------------------------------------------------------------------------------------
*** FEFCO
    move IR_BL_CRBPROP->F_FEFCO to ME->S_POPUP_CRPROP_VIS-FEFCO.
    move ZCL_VCXI_P0SS_CUST_PRSTY=>GET_PRSTY_DESCR( IF_TPRST = ZCL_VCXI_P1SS_PR_CFG=>C_TPRST_FEFCO
                                                    IF_PRSTY = ME->S_POPUP_CRPROP_VIS-FEFCO )
                                                    to ME->S_POPUP_CRPROP_VIS-FEFCO_DESCR.

*** Board ID Production
    move IR_BL_CRFPROP->F_PRBOA to ME->S_POPUP_CRPROP_VIS-PRBOA.
    ME->S_POPUP_CRPROP_VIS-PRBOA_DESCR = IR_BL_CRFPROP->GET_BOARD_DESCR( IF_BOARD = ME->S_POPUP_CRPROP_VIS-PRBOA
                                                                         IF_WERKS = IF_WERKS
                                                                         IF_MBOAR = ZCL_VCXI_P1SS_PR_BOARD=>F_MBOAR_P ).

*** Board ID Sales
    move IR_BL_CRFPROP->F_SABOA to ME->S_POPUP_CRPROP_VIS-SABOA.
    ME->S_POPUP_CRPROP_VIS-SABOA_DESCR = IR_BL_CRFPROP->GET_BOARD_DESCR( IF_BOARD = ME->S_POPUP_CRPROP_VIS-SABOA
                                                                         IF_WERKS = IF_WERKS
                                                                         IF_MBOAR = ZCL_VCXI_P1SS_PR_BOARD=>F_MBOAR_S ).

*** Flute and Description
    ME->S_POPUP_CRPROP_VIS-FLUTE = IR_BL_CRFPROP->GET_FLUTE( importing EF_DESCR = ME->S_POPUP_CRPROP_VIS-FLUTE_DESCR ).

*** Caliper
    ME->S_POPUP_CRPROP_VIS-S_CALIP  = IR_BL_CRFPROP->S_CALIP.

*** Cut Out Area
    ME->S_POPUP_CRPROP_VIS-S_COUAR  = IR_BL_CRBPROP->S_COUAR.

*** Surface Weight
    ME->S_POPUP_CRPROP_VIS-S_SURWT  = IS_SURWT.

*** Additional Weight
    ME->S_POPUP_CRPROP_VIS-S_ADDWT  = IR_BL_CRFPROP->S_ADDWT.

*** Handling Dimension
    ME->S_POPUP_CRPROP_VIS-S_HNDDIM = IR_BL_CRFPROP->S_HNDDIM.

*** Product Hieararchy
    ME->S_POPUP_CRPROP_VIS-PRODH       = IR_BL_CRFPROP->F_PRODH.
    ME->S_POPUP_CRPROP_VIS-PRODH_DESCR = ZCL_VCXI_XCS_SERVICE=>GET_PRODH_DESCR( IF_PRODH = ME->S_POPUP_CRPROP_VIS-PRODH ).

  endmethod.
  method DO_FREE.

    free: ME->R_BL_CRFPROP,
          ME->R_BL_CRBPROP.

***--------------------------------------------------------------------------------------
*** Free Price Scale Grid
    if ME->R_GRID_PRINT_COM is bound.
      ME->R_GRID_PRINT_COM->DO_FREE( ).
      clear ME->R_GRID_PRINT_COM.
    endif.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Set PF-Status
    ME->SET_PF_STATUS( ).

***----------------------------------------------------------------------------
*** Initialize Data and show as Simple ALV for Joint
    ME->INIT_DATA_JOINT( ).
    ME->INIT_SALV_JOINT( ).

*** Initialize Printing (Communication) Grid
    ME->INIT_GRID_PRINT_COM( ).

  endmethod.
  method DO_USER_COMMAND.

    case IF_UCOMM.
      when C_UC_CANCEL.
        ME->HANDLE_UC_CANCEL( ).

    endcase.

  endmethod.
  method HANDLE_UC_CANCEL.

*** Leave Popup
    leave to screen 0.

  endmethod.
  method INIT_DATA_JOINT.

    data: LF_JOITY type ZVCXI_P0S_JOITY.

    free: ME->T_JOINT_GRID.
    check ME->R_BL_CRFPROP is bound.

    loop at ME->R_BL_CRFPROP->T_JOITY into LF_JOITY.
      append value #( JOITY = LF_JOITY
                      DESCR = ZCL_VCXI_P0SS_CUST_PRSTY=>GET_JOITY_DESCR( IF_JOITY = LF_JOITY )
                    ) to ME->T_JOINT_GRID.
    endloop.

  endmethod.
  method INIT_GRID_PRINT_COM.

    check ME->R_GRID_PRINT_COM is not bound.

***--------------------------------------------------------------------------------------
*** Create Grid for Print
    create object ME->R_GRID_PRINT_COM
      exporting
        IF_CONTAINER_NAME = '0100CC_PRINT'
        IR_PRINT_COM      = ME->R_BL_CRFPROP.

*** Initialize Grid
    ME->R_GRID_PRINT_COM->F_REFRESH_REQ = ABAP_TRUE.
    ME->R_GRID_PRINT_COM->REFRESH_GRID( ).

  endmethod.
  method INIT_SALV_JOINT.

    data: LR_CUSTOM_CONTAINER type ref to CL_GUI_CUSTOM_CONTAINER.

    try.
***----------------------------------------------------------------------------
        if ME->R_SALV_JOINT is not bound.
***       Create Container
          create object LR_CUSTOM_CONTAINER
            exporting
              CONTAINER_NAME = '0100CC_JOINT'
              LIFETIME       = CL_GUI_COLUMN_TREE=>LIFETIME_DYNPRO
            exceptions
              others         = 9.
          if SY-SUBRC ne 0.
            /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
          endif.

***       Create Simple ALV
          CL_SALV_TABLE=>FACTORY( exporting R_CONTAINER  = LR_CUSTOM_CONTAINER
                                  importing R_SALV_TABLE = ME->R_SALV_JOINT
                                  changing  T_TABLE      = ME->T_JOINT_GRID ).

          ME->R_SALV_JOINT->GET_COLUMNS( )->SET_OPTIMIZE( ABAP_TRUE ).
          ME->R_SALV_JOINT->GET_FUNCTIONS( )->SET_ALL( ABAP_FALSE ).

***       Display Simple ALV
          ME->R_SALV_JOINT->DISPLAY( ).

***----------------------------------------------------------------------------
        else.
***       Refresh Simple ALV
          ME->R_SALV_JOINT->REFRESH( ).
        endif.

      catch CX_SALV_ERROR into data(LR_CX_SALV_ERROR).
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_SALV_ERROR ).
    endtry.

  endmethod.
  method SET_PF_STATUS.

    set pf-status '0100' of program 'SAPLZVCXI_P1SS_POPUP_CRPROP'.

  endmethod.
  method START.

    data: LR_POPUP_CRPROP type ref to ZCL_VCXI_P1SS_POPUP_CRPROP.

*** Create Instance
    create object LR_POPUP_CRPROP
      exporting
        IR_BL_CRBPROP = IR_BL_CRBPROP
        IR_BL_CRFPROP = IR_BL_CRFPROP
        IF_WERKS      = IF_WERKS
        IS_SURWT      = IS_SURWT.

*** Call Popup
    call function 'ZVCXI_P1SS_POPUP_CRPROP_START'
      exporting
        IR_POPUP_CRPROP = LR_POPUP_CRPROP.

*** Free
    LR_POPUP_CRPROP->DO_FREE( ).

  endmethod.
