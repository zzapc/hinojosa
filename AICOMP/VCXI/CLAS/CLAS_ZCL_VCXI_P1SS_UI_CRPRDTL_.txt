
class ZCL_VCXI_P1SS_UI_CRPRDTL definition
  public
  inheriting from ZCL_VCXI_XCS_UI_TAB
  create public .

public section.

  constants C_UC_LOAD_BOARD type SYUCOMM value 'LOAD_BOARD' ##NO_TEXT.
  data S_CRPRDTL_VIS type ZVCXI_P1SS_S_CRPRDTL_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABPT type ZVCXI_XCS_TABPT default '0100S_CPD_TAB-TAB'
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_VIS
    importing
      !IS_VIS type ANY
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  data R_GRID_PRINT_COM type ref to ZCL_VCXI_PXSS_GRID_PRINT_COM .
  data R_PR_CRPRDTL type ref to ZCL_VCXI_P1SS_PR_CRPRDTL .

  methods GET_IELEM_BY_SCREEN
    importing
      !IF_GROUP2 type SCREEN-GROUP2
    returning
      value(RF_IELEM) type ZVCXI_P0SS_IELEM
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_LOAD_BOARD
    raising
      /VCXI/CX_CKX .
  methods INIT_VIS
    raising
      /VCXI/CX_CKX .
  methods SET_DDVAL_JOITY
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
private section.
endclass. "ZCL_VCXI_P1SS_UI_CRPRDTL definition
class ZCL_VCXI_P1SS_UI_CRPRDTL implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

***--------------------------------------------------------------------------------------
*** Bind UI to Dynpro
    call function 'ZVCXI_P1SS_UI_CRPRDTL_BIND'
      exporting
        IR_UI_CRPRDTL = ME
      importing
        ES_DYNP       = ME->S_DYNP
        EF_ACTIVE_TAB = ME->F_ACTIVE_TAB.

***--------------------------------------------------------------------------------------
*** Create Grid for Print
    create object ME->R_GRID_PRINT_COM
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_PRINT_COM'
        IR_PRINT_COM      = ME->R_PR_CRPRDTL.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE
                        IF_TABPT  = IF_TABPT ).

*** Assign th RP Class to the UI Class
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_CRPRDTL.

      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

***--------------------------------------------------------------------------------------
*** Free Print Grid
    if ME->R_GRID_PRINT_COM is bound.
      ME->R_GRID_PRINT_COM->DO_FREE( ).
      clear ME->R_GRID_PRINT_COM.
    endif.

  endmethod.
  method DO_MODIFY_SCREEN.

    data: LF_BOXPA  type ZVCXI_P0SS_BOXPA.
    data: LR_CUST_PRSTY  type ref to ZCL_VCXI_PXSS_CUST_PRSTY.

    try.
        SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                 IF_DYNNR = IF_DYNNR ).

***     Get FEFCO Customizing and Part of Box
        ME->R_PR_CRPRDTL->GET_FEFCO( importing ER_CUST_PRSTY = LR_CUST_PRSTY ).
        LF_BOXPA = ME->R_PR_CRPRDTL->GET_BOXPA( ).

***----------------------------------------------------------------------------
***     Modify Screen
        loop at screen.
***       Hide Custom Product Sheet W/L
          if SCREEN-GROUP1 eq 'CST'.
            if ME->R_PR_CRPRDTL->CHECK_CUST_DESIGN( ) ne ABAP_TRUE.
              move 0 to SCREEN-ACTIVE.
            endif.
          endif.

***       Hide Joint Dimension if no Joint Flap is defined
          if SCREEN-GROUP1 eq 'NJO' and
             ME->S_CRPRDTL_VIS-JOIPO eq ZCL_VCXI_PXSS_PR_SCORE_MGR=>C_JOIPO_NONE.
            move 0 to SCREEN-ACTIVE.
          endif.

***       Hide Part of Box if not defined by Product Style
          if SCREEN-GROUP1 eq 'BXP'.
            if LR_CUST_PRSTY is not bound or
               LR_CUST_PRSTY->GET_BOXPA( ) is initial.
              move 0 to SCREEN-ACTIVE.
            endif.
          endif.

***       Adjust based on Product Style Element
          if SCREEN-GROUP1 eq 'PSE'.
            if LR_CUST_PRSTY is bound.
              if LR_CUST_PRSTY->IS_IELEM_USED( IF_IELEM = ME->GET_IELEM_BY_SCREEN( IF_GROUP2 = SCREEN-GROUP2 )
                                               IF_BOXPA = LF_BOXPA ) eq ABAP_FALSE.
                move 0 to SCREEN-ACTIVE.
              endif.
            else.
              SCREEN-ACTIVE = switch #( ME->GET_IELEM_BY_SCREEN( IF_GROUP2 = SCREEN-GROUP2 )
                                            when ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_LENGTH then 1
                                            when ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_WIDTH  then 1
                                            when ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_HEIGHT then 1
                                                                                          else 0 ).
            endif.
          endif.

***       Reopen Info Buttons
          if SCREEN-GROUP1 eq 'INF'.
            move 1 to SCREEN-INPUT.
          endif.

          modify screen.
        endloop.

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_PBO.

    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize VIS
    ME->INIT_VIS( ).

*** Define DropDown Values for Joint Type
    ME->SET_DDVAL_JOITY( ).

*** Initialize Grid
    if ME->R_GRID_PRINT_COM is bound.
      ME->R_GRID_PRINT_COM->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_PRINT_COM->REFRESH_GRID( ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Parameter Date Config
    data(LF_DCONF) = ME->R_GATE->GET_APPDATA( )->F_DCONF.
    set parameter id 'ZVCXI_XCS_DCONF' field LF_DCONF.

*** Set Parameter of Valid Board Types and Mode
    set parameter id 'ZVCXI_PCS_TBOAR' field SPACE.
    set parameter id 'ZVCXI_PCS_MBOAR' field SPACE.

*** Set Plant as Parameter
    set parameter id 'WRK' field SPACE.

*** Set only Released BoardIDs are allowed
    set parameter id 'ZVCXI_PCS_RELEASED' field ABAP_TRUE.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_CRPRDTL_VIS  type ZVCXI_P1SS_S_CRPRDTL_VIS.


*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_CRPRDTL_VIS.

    check ME->R_PR_CRPRDTL is bound.

***--------------------------------------------------------------------------------------
*** Set Fefco
    if ME->S_CRPRDTL_VIS-FEFCO ne LS_CRPRDTL_VIS-FEFCO.
      ME->R_PR_CRPRDTL->SET_FEFCO( IF_FEFCO = LS_CRPRDTL_VIS-FEFCO
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Part of Box
    if ME->S_CRPRDTL_VIS-BOXPA ne LS_CRPRDTL_VIS-BOXPA.
      ME->R_PR_CRPRDTL->SET_BOXPA( IF_BOXPA = LS_CRPRDTL_VIS-BOXPA
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Product Length
    if ME->S_CRPRDTL_VIS-S_PROLE ne LS_CRPRDTL_VIS-S_PROLE.
      ME->R_PR_CRPRDTL->SET_PROLE( IS_PROLE = LS_CRPRDTL_VIS-S_PROLE
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Product Width
    if ME->S_CRPRDTL_VIS-S_PROWI ne LS_CRPRDTL_VIS-S_PROWI.
      ME->R_PR_CRPRDTL->SET_PROWI( IS_PROWI = LS_CRPRDTL_VIS-S_PROWI
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Product Height
    if ME->S_CRPRDTL_VIS-S_PROHE ne LS_CRPRDTL_VIS-S_PROHE.
      ME->R_PR_CRPRDTL->SET_PROHE( IS_PROHE = LS_CRPRDTL_VIS-S_PROHE
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Product Height2
    if ME->S_CRPRDTL_VIS-S_PROH2 ne LS_CRPRDTL_VIS-S_PROH2.
      ME->R_PR_CRPRDTL->SET_PROH2( IS_PROH2 = LS_CRPRDTL_VIS-S_PROH2
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Product Sheet Length
    if ME->S_CRPRDTL_VIS-S_SHTPL ne LS_CRPRDTL_VIS-S_SHTPL.
      ME->R_PR_CRPRDTL->SET_SHTPL( IS_SHTPL = LS_CRPRDTL_VIS-S_SHTPL
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Product Sheet Width
    if ME->S_CRPRDTL_VIS-S_SHTPW ne LS_CRPRDTL_VIS-S_SHTPW.
      ME->R_PR_CRPRDTL->SET_SHTPW( IS_SHTPW = LS_CRPRDTL_VIS-S_SHTPW
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Overlap
    if ME->S_CRPRDTL_VIS-S_OVLAP ne LS_CRPRDTL_VIS-S_OVLAP.
      ME->R_PR_CRPRDTL->SET_OVLAP( IS_OVLAP = LS_CRPRDTL_VIS-S_OVLAP
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Tuck in Flap
    if ME->S_CRPRDTL_VIS-S_FTUCK ne LS_CRPRDTL_VIS-S_FTUCK.
      ME->R_PR_CRPRDTL->SET_FTUCK( IS_FTUCK = LS_CRPRDTL_VIS-S_FTUCK
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Dust Flap
    if ME->S_CRPRDTL_VIS-S_FDUST ne LS_CRPRDTL_VIS-S_FDUST.
      ME->R_PR_CRPRDTL->SET_FDUST( IS_FDUST = LS_CRPRDTL_VIS-S_FDUST
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Gap in Flap Top
    if ME->S_CRPRDTL_VIS-S_FGAPT ne LS_CRPRDTL_VIS-S_FGAPT.
      ME->R_PR_CRPRDTL->SET_FGAPT( IS_FGAPT = LS_CRPRDTL_VIS-S_FGAPT
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Gap in Flap Botton
    if ME->S_CRPRDTL_VIS-S_FGAPB ne LS_CRPRDTL_VIS-S_FGAPB.
      ME->R_PR_CRPRDTL->SET_FGAPB( IS_FGAPB = LS_CRPRDTL_VIS-S_FGAPB
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Board
    if ME->S_CRPRDTL_VIS-BOARD ne LS_CRPRDTL_VIS-BOARD.
      ME->R_PR_CRPRDTL->SET_BOARD( IF_BOARD = LS_CRPRDTL_VIS-BOARD
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Flute Orientation
    if ME->S_CRPRDTL_VIS-FLUTO ne LS_CRPRDTL_VIS-FLUTO.
      ME->R_PR_CRPRDTL->SET_FLUTO( IF_FLUTO = LS_CRPRDTL_VIS-FLUTO
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Flute
    if ME->S_CRPRDTL_VIS-FLUTE ne LS_CRPRDTL_VIS-FLUTE.
      ME->R_PR_CRPRDTL->SET_FLUTE( IF_FLUTE = LS_CRPRDTL_VIS-FLUTE
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Caliper
    if ME->S_CRPRDTL_VIS-S_CALIP ne LS_CRPRDTL_VIS-S_CALIP.
      ME->R_PR_CRPRDTL->SET_CALIP( IS_CALIP = LS_CRPRDTL_VIS-S_CALIP
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Surface Weight
    if ME->S_CRPRDTL_VIS-S_SURWT ne LS_CRPRDTL_VIS-S_SURWT.
      ME->R_PR_CRPRDTL->SET_SURWT( IS_SURWT = LS_CRPRDTL_VIS-S_SURWT
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.


***--------------------------------------------------------------------------------------
*** Set Joint Position
    if ME->S_CRPRDTL_VIS-JOIPO ne LS_CRPRDTL_VIS-JOIPO.
      ME->R_PR_CRPRDTL->SET_JOIPO( IF_JOIPO = LS_CRPRDTL_VIS-JOIPO
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Joint Type
    if ME->S_CRPRDTL_VIS-JOITY ne LS_CRPRDTL_VIS-JOITY.
      ME->R_PR_CRPRDTL->SET_JOITY( IF_JOITY = LS_CRPRDTL_VIS-JOITY
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Joint Width
    if ME->S_CRPRDTL_VIS-S_JOIWI ne LS_CRPRDTL_VIS-S_JOIWI.
      ME->R_PR_CRPRDTL->SET_JOIWI( IS_JOIWI = LS_CRPRDTL_VIS-S_JOIWI
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Joint Length
    if ME->S_CRPRDTL_VIS-S_JOILE ne LS_CRPRDTL_VIS-S_JOILE.
      ME->R_PR_CRPRDTL->SET_JOILE( IS_JOILE = LS_CRPRDTL_VIS-S_JOILE
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Joint Extension Top
    if ME->S_CRPRDTL_VIS-S_JEXTT ne LS_CRPRDTL_VIS-S_JEXTT.
      ME->R_PR_CRPRDTL->SET_JEXTT( IS_JEXTT = LS_CRPRDTL_VIS-S_JEXTT
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Joint Extension Bottom
    if ME->S_CRPRDTL_VIS-S_JEXTB ne LS_CRPRDTL_VIS-S_JEXTB.
      ME->R_PR_CRPRDTL->SET_JEXTB( IS_JEXTB = LS_CRPRDTL_VIS-S_JEXTB
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Cut Out Area
    if ME->S_CRPRDTL_VIS-S_COUAR ne LS_CRPRDTL_VIS-S_COUAR.
      ME->R_PR_CRPRDTL->SET_COUAR( IS_COUAR = LS_CRPRDTL_VIS-S_COUAR
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Additional Weight
    if ME->S_CRPRDTL_VIS-S_ADDWT ne LS_CRPRDTL_VIS-S_ADDWT.
      ME->R_PR_CRPRDTL->SET_ADDWT( IS_ADDWT = LS_CRPRDTL_VIS-S_ADDWT
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    if ME->R_GRID_PRINT_COM is bound.
      ME->R_GRID_PRINT_COM->CHECK_CHANGED_DATA( ).
    endif.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    case IF_UCOMM.
      when C_UC_LOAD_BOARD.
        ME->HANDLE_UC_LOAD_BOARD( ).
    endcase.

  endmethod.
  method GET_IELEM_BY_SCREEN.

    RF_IELEM = switch #( IF_GROUP2 when 'PLE' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_LENGTH
                                   when 'PWI' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_WIDTH
                                   when 'PHE' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_HEIGHT
                                   when 'PH2' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_HEIGHT2
                                   when 'OLP' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_OVLAP
                                   when 'FDU' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_FDUST
                                   when 'FTU' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_FTUCK
                                   when 'FGT' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_GAP_FLAP_T
                                   when 'FGB' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_GAP_FLAP_B
                       ).

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_CRPRDTL.

  endmethod.
  method HANDLE_UC_LOAD_BOARD.

    data: LF_BOARD type ZVCXI_PCS_BOARD,
          LF_MBOAR type ZVCXI_PCSS_MBOAR,
          LF_WERKS type WERKS_D.

***--------------------------------------------------------------------------------------
*** Call Search Help
    ZCL_VCXI_PCSS_CUST_BOARD=>SHLP_LOAD_BOARD( exporting IF_DCONF  = ME->R_GATE->GET_APPDATA( )->F_DCONF
                                                         IF_LOCKED = ME->R_GATE->IS_LOCKED( )
                                               importing EF_BOARD  = LF_BOARD
                                                         EF_MBOAR  = LF_MBOAR
                                                         EF_WERKS  = LF_WERKS ).

*** Load the selected Board
    check LF_BOARD is not initial and
          LF_MBOAR is not initial.
    ME->R_PR_CRPRDTL->LOAD_BOARD( IF_BOARD = LF_BOARD
                                  IF_WERKS = LF_WERKS
                                  IF_MBOAR = LF_MBOAR ).

  endmethod.
  method INIT_VIS.

    free: ME->S_CRPRDTL_VIS.
    check ME->R_PR_CRPRDTL is bound.

***------------------------------------------------------------------------------------------------
*** Get Fefco
    ME->S_CRPRDTL_VIS-FEFCO = ME->R_PR_CRPRDTL->GET_FEFCO( importing EF_DESCR = ME->S_CRPRDTL_VIS-FEFCO_DESCR ).

*** Get Part of Box
    ME->S_CRPRDTL_VIS-BOXPA = ME->R_PR_CRPRDTL->GET_BOXPA( ).

*** Get Product Dimension
    ME->S_CRPRDTL_VIS-PRODIM_DESCR = ME->R_PR_CRPRDTL->GET_PRODIM_DESCR( ).
    ME->S_CRPRDTL_VIS-S_PROWI = ME->R_PR_CRPRDTL->GET_PROWI( ).
    ME->S_CRPRDTL_VIS-S_PROLE = ME->R_PR_CRPRDTL->GET_PROLE( ).
    ME->S_CRPRDTL_VIS-S_PROHE = ME->R_PR_CRPRDTL->GET_PROHE( ).
    ME->S_CRPRDTL_VIS-S_PROH2 = ME->R_PR_CRPRDTL->GET_PROH2( ).

*** Get Product Sheet Dimension
    ME->S_CRPRDTL_VIS-S_SHTPW = ME->R_PR_CRPRDTL->GET_SHTPW( ).
    ME->S_CRPRDTL_VIS-S_SHTPL = ME->R_PR_CRPRDTL->GET_SHTPL( ).

*** Get Overlap/Tuck in/Dust Flap
    ME->S_CRPRDTL_VIS-S_OVLAP = ME->R_PR_CRPRDTL->GET_OVLAP( ).
    ME->S_CRPRDTL_VIS-S_FTUCK = ME->R_PR_CRPRDTL->GET_FTUCK( ).
    ME->S_CRPRDTL_VIS-S_FDUST = ME->R_PR_CRPRDTL->GET_FDUST( ).

*** Gap in Flap Top/Botton
    ME->S_CRPRDTL_VIS-S_FGAPT = ME->R_PR_CRPRDTL->GET_FGAPT( ).
    ME->S_CRPRDTL_VIS-S_FGAPB = ME->R_PR_CRPRDTL->GET_FGAPB( ).

***------------------------------------------------------------------------------------------------
*** Get Board ID
    ME->S_CRPRDTL_VIS-BOARD = ME->R_PR_CRPRDTL->GET_BOARD( ).

*** Get Flute Orientation
    ME->S_CRPRDTL_VIS-FLUTO = ME->R_PR_CRPRDTL->GET_FLUTO( ).

*** Get Flute, Caliper, Surface Weight
    ME->S_CRPRDTL_VIS-FLUTE   = ME->R_PR_CRPRDTL->GET_FLUTE( importing EF_DESCR = ME->S_CRPRDTL_VIS-FLUTE_DESCR ).
    ME->S_CRPRDTL_VIS-S_CALIP = ME->R_PR_CRPRDTL->GET_CALIP( ).
    ME->S_CRPRDTL_VIS-S_SURWT = ME->R_PR_CRPRDTL->GET_SURWT( ).

***------------------------------------------------------------------------------------------------
*** Joint Information
    ME->S_CRPRDTL_VIS-JOIPO   = ME->R_PR_CRPRDTL->GET_JOIPO( ).
    ME->S_CRPRDTL_VIS-JOITY   = ME->R_PR_CRPRDTL->GET_JOITY( ).
    ME->S_CRPRDTL_VIS-S_JOIWI = ME->R_PR_CRPRDTL->GET_JOIWI( ).
    ME->S_CRPRDTL_VIS-S_JOILE = ME->R_PR_CRPRDTL->GET_JOILE( ).
    ME->S_CRPRDTL_VIS-S_JEXTT = ME->R_PR_CRPRDTL->GET_JEXTT( ).
    ME->S_CRPRDTL_VIS-S_JEXTB = ME->R_PR_CRPRDTL->GET_JEXTB( ).

***------------------------------------------------------------------------------------------------
*** Cut Out Area
    ME->S_CRPRDTL_VIS-S_COUAR = ME->R_PR_CRPRDTL->GET_COUAR( ).

*** Additional Weight
    ME->S_CRPRDTL_VIS-S_ADDWT = ME->R_PR_CRPRDTL->GET_ADDWT( ).

  endmethod.
  method SET_DDVAL_JOITY.

    data: LS_TC341 type ZVCXI_P0S_TC341.
    data: LT_TC341 type ZCL_VCXI_P0SS_CUST_PRSTY=>YT_TC341,
          LT_VALUE type VRM_VALUES.
    data: LR_CUST_PRSTY type ref to ZCL_VCXI_P0SS_CUST_PRSTY.


***--------------------------------------------------------------------------------------
*** Get Product Style
    try.
        LR_CUST_PRSTY = ZCL_VCXI_P0SS_CUST_PRSTY=>GET_INSTANCE( IF_TPRST = ZCL_VCXI_P1SS_PR_CRPRDTL=>C_TPRST_FEFCO
                                                                IF_PRSTY = ME->S_CRPRDTL_VIS-FEFCO ).
      catch /VCXI/CX_CKX.
        clear: LR_CUST_PRSTY.
    endtry.
    check LR_CUST_PRSTY is bound.

***--------------------------------------------------------------------------------------
*** Get Valid Joint Types for Position
    LR_CUST_PRSTY->GET_JOITY_JOIPO( exporting IF_JOIPO = ME->S_CRPRDTL_VIS-JOIPO
                                    importing ET_TC341 = LT_TC341 ).

*** Filter by Product Style
    delete LT_TC341 where not JOITY in LR_CUST_PRSTY->GET_VALID_JOITY( ).

    loop at LT_TC341 into LS_TC341.
      insert value #( KEY  = LS_TC341-JOITY
                      TEXT = ZCL_VCXI_P0SS_CUST_PRSTY=>GET_JOITY_DESCR( IF_JOITY = LS_TC341-JOITY )
                    ) into table LT_VALUE.
    endloop.

***--------------------------------------------------------------------------------------
*** Set to DropDown List
    call function 'VRM_SET_VALUES'
      exporting
        ID     = 'ZVCXI_P1SS_S_CRPRDTL_VIS-JOITY'
        VALUES = LT_VALUE.

  endmethod.
