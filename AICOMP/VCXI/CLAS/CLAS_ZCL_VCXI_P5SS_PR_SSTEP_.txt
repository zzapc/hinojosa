
class ZCL_VCXI_P5SS_PR_SSTEP definition
  public
  inheriting from ZCL_VCXI_PXSS_PR_SSTEP
  create public .

public section.

  constants C_IPRVT_P5_SSTEP type /VCXI/VKSC_IPRVT value 'ZVCXI_P5SS_SSTEP' ##NO_TEXT.
  constants C_IEVNT_O_TRWIT_CHANGED type /VCXI/VKSC_IEVNT value 'TRWIT_CHANGED' ##NO_TEXT.
  constants C_PARID_TRWIT type /VCXI/VKS_PARID value 'ZVCXI_PX_TRWIT' ##NO_TEXT.

  methods GET_TRWIT
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_TRWIT) type ZVCXI_PXS_S_TRWIT
    raising
      /VCXI/CX_CKX .
  methods SET_TRWIT
    importing
      !IS_TRWIT type ZVCXI_PXS_S_TRWIT
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .

  methods ZIF_VCXI_XCS_NGREP~FILL
    redefinition .
protected section.
private section.
endclass. "ZCL_VCXI_P5SS_PR_SSTEP definition
class ZCL_VCXI_P5SS_PR_SSTEP implementation.
  method GET_TRWIT.

    data: LS_PARAM type /VCXI/VKSR_S_PARAM.

***----------------------------------------------------------------------------
*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TRWIT ).
    move LS_PARAM-ATFLV to RS_TRWIT-TRWIT.
    move LS_PARAM-UNIT  to RS_TRWIT-TRWIT_UOM.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method SET_TRWIT.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TRWIT ).

    move IS_TRWIT-TRWIT     to LS_PARAM-ATFLV.
    move IS_TRWIT-TRWIT_UOM to LS_PARAM-UNIT.
    move IF_SETBY           to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_TRWIT_CHANGED ).
    endif.

  endmethod.
  method ZIF_VCXI_XCS_NGREP~FILL.

    data: LT_ATTR type CR_NETGRAPH_JSON->YT_ATTRIBUTE.


*** Get Attribute Data
    data(LS_TRWIT) = ME->GET_TRWIT( ).
    CR_NETGRAPH_JSON->ADD_ATTR( exporting IF_VALUE = LS_TRWIT-TRWIT
                                          IF_UOM   = LS_TRWIT-TRWIT_UOM
                                changing  CT_ATTR  = LT_ATTR ).

*** Add Node
    data(LF_NODE_KEY) = CR_NETGRAPH_JSON->ADD_NODE( IF_TITLE     = ME->GET_DESCR( )
                                                    IF_DESCR     = ME->GET_DESCR( )
                                                    IF_ICON      = 'sap-icon://split'
                                                    IF_STATUS    = CR_NETGRAPH_JSON->GET_STATUS_BY_GATE( ME->R_GATE )
                                                    IF_GROUP_KEY = IF_GROUP_KEY
                                                    IT_ATTR      = LT_ATTR ).

*** Register Gate to Node
    CR_NETGRAPH_JSON->REGISTER_TO_NODE( IR_OBJECT   = ME->R_GATE
                                        IF_NODE_KEY = LF_NODE_KEY ).

  endmethod.
