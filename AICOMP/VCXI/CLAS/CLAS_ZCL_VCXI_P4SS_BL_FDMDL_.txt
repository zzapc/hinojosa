
class ZCL_VCXI_P4SS_BL_FDMDL definition
  public
  inheriting from ZCL_VCXI_PXSS_BL_FDMDL
  create public .

public section.

  constants C_IFDMV_PR type ZVCXI_PXSS_IFDMV value 'P4_PR' ##NO_TEXT.
  data R_BL_FDMDL_PRDTL type ref to ZCL_VCXI_PXSS_BL_FDMDL read-only .
  constants C_FDMDL_PRPART type ZVCXI_PXSS_FDMDL value 'PRPART' ##NO_TEXT.
  constants C_FDMDL_PRFG type ZVCXI_PXSS_FDMDL value 'PRFG' ##NO_TEXT.

  methods DET_IFDMV
    redefinition .
  methods GET_CLSUI
    redefinition .
  methods GET_SUB_BL_FDMDL
    redefinition .
protected section.

  methods GET_INIT_PR
    importing
      !IF_PRDTL type /VCXI/CKX_ID optional
    changing
      !CT_FASTD_XCOMP type ZVCXI_PXSS_TT_FASTD_XCOMP
      !CT_XCONG type ZVCXI_PXSS_TT_XCONG
    raising
      /VCXI/CX_CKX .

  methods INIT_FDMDL
    redefinition .
private section.
endclass. "ZCL_VCXI_P4SS_BL_FDMDL definition
class ZCL_VCXI_P4SS_BL_FDMDL implementation.
  method DET_IFDMV.

    data: LS_FASTD_XCOMP      type ZVCXI_PXSS_S_FASTD_XCOMP,
          LS_FASTD_XCOMP_DATA type ZVCXI_PXSS_S_FASTD_XCOMP_DATA.


    free: ME->R_BL_FDMDL_PRDTL.

***--------------------------------------------------------------------------------------
*** Get Configuration Structure
    SUPER->DET_IFDMV( importing ET_FASTD_XCOMP = ET_FASTD_XCOMP ).
    check ME->T_GATE_OUT is not initial.

    ME->F_IFDMV = C_IFDMV_PR.

***--------------------------------------------------------------------------------------
*** Search for Procurement Details
    loop at ET_FASTD_XCOMP into LS_FASTD_XCOMP where R_GATE is bound.
      loop at LS_FASTD_XCOMP-T_DATA into LS_FASTD_XCOMP_DATA
                                   where IPARE  eq ME->F_FDMDL
                                    and  R_GATE is bound
                                    and  R_GATE->R_CLSPR is instance of ZIF_VCXI_P4SS_PRDTL.
***     Only Consider Procurement Details that belong to me (e.g. my FG/Part)
        check LS_FASTD_XCOMP_DATA-R_GATE->IS_IN_LINE( IR_GATE = value #( T_GATE_OUT[ 1 ] optional ) ).

***     Get Fast Data Model Instance for Procurement Details
        ME->R_BL_FDMDL_PRDTL = ME->GET_SUB_INSTANCE( IR_GATE_SUB    = LS_FASTD_XCOMP_DATA-R_GATE
                                                     IR_GATE_XCOMP  = LS_FASTD_XCOMP-R_GATE
                                                     IT_FASTD_XCOMP = ET_FASTD_XCOMP ).

***     Use ID of Procurement Details Element as ID
        ME->F_IFDMV = LS_FASTD_XCOMP_DATA-R_GATE->F_ID.
      endloop.
    endloop.

  endmethod.
  method GET_CLSUI.

    RF_CLSUI = 'ZCL_VCXI_P4SS_UI_FDMDL'.

  endmethod.
  method GET_INIT_PR.

    field-symbols: <S_FASTD_XCOMP> type ZVCXI_PXSS_S_FASTD_XCOMP.


***--------------------------------------------------------------------------------------
*** Define Structure
    case ME->F_FDMDL.
      when C_FDMDL_PRFG.
***     As Finished Good
        append value #( ID     = 'COMP_P4'
                        XCSTY  = ZCL_VCXI_PXSS_BL_FASTD=>C_XCSTY_FREESTYLE  "Freestyle Component
                        T_DATA = value #( ( IPARE = ''      ISELF = 'PRFG' )   "R_DATA = new ZVCXI_P4SS_S_FDI_PRFG( ) )
                                          ( IPARE = 'PRFG'  ISELF = 'SPLR' )   "R_DATA = new ZVCXI_P4SS_S_FDI_SPLR( ) )
                                        )
                      ) to CT_FASTD_XCOMP assigning <S_FASTD_XCOMP>.

      when C_FDMDL_PRPART.
***     As Part
        assign CT_FASTD_XCOMP[ ID = 'COMP_P3' ] to <S_FASTD_XCOMP>.
        append value #( IPARE = 'CSTEP_ASMBL'  ISELF = 'PRPART'   T_FDMEX = value #( ( conv #( 'PARAT' ) ) )
                      ) to <S_FASTD_XCOMP>-T_DATA.
    endcase.

***--------------------------------------------------------------------------------------
*** Add Procurement Details
    if IF_PRDTL is not initial and
       <S_FASTD_XCOMP> is assigned.
      append value #( IPARE = ME->F_FDMDL  ISELF = IF_PRDTL ) to <S_FASTD_XCOMP>-T_DATA.
    endif.

  endmethod.
  method GET_SUB_BL_FDMDL.

    data: LF_DEPTH type I.


*** Collect all direct related Sub-Models
    RT_BL_FDMDL = SUPER->GET_SUB_BL_FDMDL( IF_DEPTH = 0 ).
    if ME->R_BL_FDMDL_PRDTL is bound.
      append ME->R_BL_FDMDL_PRDTL to RT_BL_FDMDL.
    endif.

*** "Recursive" Collection
    if IF_DEPTH gt 0.
      LF_DEPTH = IF_DEPTH - 1.
    else.
      LF_DEPTH = IF_DEPTH.
    endif.
    if LF_DEPTH ne 0.
      loop at RT_BL_FDMDL into data(LR_BL_FDMDL).
        append lines of LR_BL_FDMDL->GET_SUB_BL_FDMDL( IF_DEPTH = LF_DEPTH ) to RT_BL_FDMDL.
      endloop.
    endif.

  endmethod.
  method INIT_FDMDL.

*** Get initial Structure based on Model Variant
    case IF_IFDMV.
      when C_IFDMV_PR.
        ME->GET_INIT_PR( changing CT_FASTD_XCOMP = CT_FASTD_XCOMP
                                  CT_XCONG       = CT_XCONG ).
      when others.
***     Procurement with Details
        ME->GET_INIT_PR( exporting IF_PRDTL       = IF_IFDMV
                         changing  CT_FASTD_XCOMP = CT_FASTD_XCOMP
                                   CT_XCONG       = CT_XCONG ).
    endcase.

  endmethod.
