
class ZCL_VCXI_XCDR_GRID definition
  public
  final
  create public .

public section.

  class-methods MODIFY_FCAT
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IT_CONDREC type /VCXI/CKX_TT_OBJECT
      !IF_CC_NAME type SCRFNAME
    changing
      !CT_FCAT type LVC_T_FCAT
    raising
      /VCXI/CX_CKX .
  class-methods MODIFY_STYLE
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IT_CONDREC type /VCXI/CKX_TT_OBJECT
    changing
      !CT_STYL type LVC_T_STYL
    raising
      /VCXI/CX_CKX .
protected section.

  type-pools ABAP .
  class-methods GET_CURRENT_STYLE
    importing
      !IF_STYLE type RAW4
    exporting
      !EF_EDIT type ABAP_BOOL
      !EF_HOTSPOT type ABAP_BOOL
      !EF_F4 type ABAP_BOOL
    exceptions
      /VCXI/CX_CKX .
  class-methods SET_STYLE
    importing
      !IF_OSVIS type ZVCXI_XCD_OSVIS
    changing
      !CF_STYLE type RAW4
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_XCDR_GRID definition
class ZCL_VCXI_XCDR_GRID implementation.
method GET_CURRENT_STYLE.

  data: LF_STYLE_DHN type RAW4,  "has no Hotspot disabled
        LF_STYLE_EH  type RAW4.  "has Hotspot + enabled

  LF_STYLE_DHN = CL_GUI_ALV_GRID=>MC_STYLE_DISABLED + CL_GUI_ALV_GRID=>MC_STYLE_HOTSPOT_NO.
  LF_STYLE_EH  = CL_GUI_ALV_GRID=>MC_STYLE_ENABLED + CL_GUI_ALV_GRID=>MC_STYLE_HOTSPOT.

*** ---------------------------------------------------------
*** Get Edit/Hotspot
  case IF_STYLE+1(1).
*** Only Edit
    when CL_GUI_ALV_GRID=>MC_STYLE_ENABLED+1(1).
      move ABAP_TRUE      to EF_EDIT.
      move ABAP_UNDEFINED to EF_HOTSPOT.
*** Only Disabled
    when CL_GUI_ALV_GRID=>MC_STYLE_DISABLED+1(1).
      move ABAP_FALSE     to EF_EDIT.
      move ABAP_UNDEFINED to EF_HOTSPOT.
*** Only Hotspot
    when CL_GUI_ALV_GRID=>MC_STYLE_HOTSPOT+1(1).
      move ABAP_UNDEFINED to EF_EDIT.
      move ABAP_TRUE      to EF_HOTSPOT.
*** Edit and Hotspot
    when LF_STYLE_EH+1(1).
      move ABAP_TRUE      to EF_EDIT.
      move ABAP_TRUE      to EF_HOTSPOT.
*** No Edit and No Hotspot
    when LF_STYLE_DHN+1(1).
      move ABAP_FALSE     to EF_EDIT.
      move ABAP_FALSE     to EF_HOTSPOT.
*** Nothing defined
    when others.
      move ABAP_UNDEFINED to EF_EDIT.
      move ABAP_UNDEFINED to EF_HOTSPOT.
  endcase.

*** Get F4
  case IF_STYLE(1).
*** On
    when CL_GUI_ALV_GRID=>MC_STYLE_F4(1).
      move ABAP_TRUE      to EF_F4.
*** Off
    when CL_GUI_ALV_GRID=>MC_STYLE_F4_NO(1).
      move ABAP_FALSE     to EF_F4.
*** Not defined
    when others.
      move ABAP_UNDEFINED to EF_F4.
  endcase.

endmethod.
method MODIFY_FCAT.

  data:          LF_FIELDNAME         type        ZVCXI_XCD_ISDEF.
  data:          LS_FCAT              type        LVC_S_FCAT,
                 LS_CR_SIMPACT        type        ZVCXI_XCDC_S_CR_SIMPACT.
  data:          LT_CR_SIMPACT        type        ZVCXI_XCDC_TT_CR_SIMPACT,
                 LT_INPUTFIELD        type        /VCXI/CKX_TT_STRING.
  data:          LR_CONDREC           type ref to ZCL_VCXI_XCDC_CONDREC,
                 LR_OBJECT            type ref to /VCXI/IF_CKX_OBJECT.
  field-symbols: <S_FCAT>             type        LVC_S_FCAT.

  check IT_CONDREC is not initial.

***--------------------------------------------------------------------------------------
*** Find Input Fields
  loop at CT_FCAT into LS_FCAT.
    if LS_FCAT-EDIT eq ABAP_TRUE.
      append LS_FCAT-FIELDNAME to LT_INPUTFIELD.
    endif.
  endloop.

***--------------------------------------------------------------------------------------
*** Collect all valid Fieldcat Impacts
  loop at IT_CONDREC into LR_OBJECT.
    move LR_OBJECT ?to LR_CONDREC.
    check LR_CONDREC is bound and
          LR_CONDREC->R_CONDDEF is bound and
          LR_CONDREC->R_CONDDEF->R_REGDVC is bound and
          LR_CONDREC->T_CR_SIMPACT is not initial.

*** Override old ones
    move LR_CONDREC->T_CR_SIMPACT to LT_CR_SIMPACT.
    sort LT_CR_SIMPACT by SSCGR.

*** Process Screen Group ID
    loop at LT_CR_SIMPACT into LS_CR_SIMPACT.
***   Check Screen Group includes Screen Fields
      read table LR_CONDREC->R_CONDDEF->R_REGDVC->ZIF_VCXI_XCDC_REGDVC_SCRGR~T_REGDVC_SCRGRDEF
            with key ISCGR = LS_CR_SIMPACT-ISCGR
                     TSDEF = ZIF_VCXI_XCDC_REGDVC_SCRGR=>C_TSDEF_AF
            transporting no fields.
      check SY-SUBRC eq 0.

***--------------------------------------------------------------------------------------
***   Modify Fieldcat
      loop at CT_FCAT assigning <S_FCAT>.
        concatenate IF_CC_NAME <S_FCAT>-FIELDNAME into LF_FIELDNAME separated by '@'.
***     Is Column part of Screen Group
        read table LR_CONDREC->R_CONDDEF->R_REGDVC->ZIF_VCXI_XCDC_REGDVC_SCRGR~T_REGDVC_SCRGRDEF
              with key ISCGR = LS_CR_SIMPACT-ISCGR
                       TSDEF = ZIF_VCXI_XCDC_REGDVC_SCRGR=>C_TSDEF_AF
                       ISDEF = LF_FIELDNAME
              transporting no fields.


***     Check Wildcard '*' for specific Grid
        if SY-SUBRC ne 0.
          concatenate IF_CC_NAME ZCL_VCXI_XCDC_CONDREC=>C_ISDEF_ALL into LF_FIELDNAME separated by '@'.
          read table LR_CONDREC->R_CONDDEF->R_REGDVC->ZIF_VCXI_XCDC_REGDVC_SCRGR~T_REGDVC_SCRGRDEF
                with key ISCGR = LS_CR_SIMPACT-ISCGR
                         TSDEF = ZIF_VCXI_XCDC_REGDVC_SCRGR=>C_TSDEF_AF
                         ISDEF = LF_FIELDNAME
                transporting no fields.

***       Check Wildcard '*'
          if SY-SUBRC ne 0.
            read table LR_CONDREC->R_CONDDEF->R_REGDVC->ZIF_VCXI_XCDC_REGDVC_SCRGR~T_REGDVC_SCRGRDEF
                  with key ISCGR = LS_CR_SIMPACT-ISCGR
                           TSDEF = ZIF_VCXI_XCDC_REGDVC_SCRGR=>C_TSDEF_AF
                           ISDEF = ZCL_VCXI_XCDC_CONDREC=>C_ISDEF_ALL
                  transporting no fields.
          endif.
        endif.
        check SY-SUBRC eq 0.

***--------------------------------------------------------------------------------------
***     Control Input/Visiblity
        case LS_CR_SIMPACT-OSVIS.
          when '1'.   "Input
***         Only if fieldcat allowes it
            read table LT_INPUTFIELD with key TABLE_LINE = <S_FCAT>-FIELDNAME
                                     transporting no fields.
            if SY-SUBRC eq 0.
              <S_FCAT>-EDIT   = ABAP_TRUE.
            endif.
            <S_FCAT>-NO_OUT = ABAP_FALSE.

          when '2'.   "Display only
            <S_FCAT>-EDIT   = ABAP_FALSE.
            <S_FCAT>-NO_OUT = ABAP_FALSE.

          when '3'.   "Hide
            <S_FCAT>-NO_OUT = ABAP_TRUE.
            <S_FCAT>-EDIT   = ABAP_FALSE.

          when '4'.   "Display only (Already Visible)
            <S_FCAT>-EDIT   = ABAP_FALSE.
        endcase.
      endloop.
    endloop.
  endloop.

endmethod.
method MODIFY_STYLE.

  data:          LS_CR_SIMPACT      type        ZVCXI_XCDC_S_CR_SIMPACT.
  data:          LT_CR_SIMPACT      type        ZVCXI_XCDC_TT_CR_SIMPACT.
  data:          LR_CONDREC         type ref to ZCL_VCXI_XCDC_CONDREC,
                 LR_OBJECT          type ref to /VCXI/IF_CKX_OBJECT.
  field-symbols: <S_STYL>           type        LVC_S_STYL.

  check IT_CONDREC is not initial.

***--------------------------------------------------------------------------------------
*** Collect all valid Style Impacts
  loop at IT_CONDREC into LR_OBJECT.
    move LR_OBJECT ?to LR_CONDREC.
    check LR_CONDREC is bound and
          LR_CONDREC->R_CONDDEF is bound and
          LR_CONDREC->R_CONDDEF->R_REGDVC is bound and
          LR_CONDREC->T_CR_SIMPACT is not initial.

*** Sort Screen Impacts
    move LR_CONDREC->T_CR_SIMPACT to LT_CR_SIMPACT.
    sort LT_CR_SIMPACT by SSCGR.

***--------------------------------------------------------------------------------------
*** Process Screen Group ID
    loop at LT_CR_SIMPACT into LS_CR_SIMPACT.
***   Check Screen Group includes Screen Fields
      read table LR_CONDREC->R_CONDDEF->R_REGDVC->ZIF_VCXI_XCDC_REGDVC_SCRGR~T_REGDVC_SCRGRDEF
            with key ISCGR = LS_CR_SIMPACT-ISCGR
                     TSDEF = ZIF_VCXI_XCDC_REGDVC_SCRGR=>C_TSDEF_AS
            transporting no fields.

      check SY-SUBRC eq 0.

***--------------------------------------------------------------------------------------
***   Modify Style
      loop at CT_STYL assigning <S_STYL>.
***     Is Column part of Screen Group
        read table LR_CONDREC->R_CONDDEF->R_REGDVC->ZIF_VCXI_XCDC_REGDVC_SCRGR~T_REGDVC_SCRGRDEF
              with key ISCGR = LS_CR_SIMPACT-ISCGR
                       TSDEF = ZIF_VCXI_XCDC_REGDVC_SCRGR=>C_TSDEF_AS
                       ISDEF = <S_STYL>-FIELDNAME
              transporting no fields.

***     Check Wildcard '*'
        if SY-SUBRC ne 0.
          read table LR_CONDREC->R_CONDDEF->R_REGDVC->ZIF_VCXI_XCDC_REGDVC_SCRGR~T_REGDVC_SCRGRDEF
                with key ISCGR = LS_CR_SIMPACT-ISCGR
                         TSDEF = ZIF_VCXI_XCDC_REGDVC_SCRGR=>C_TSDEF_AS
                         ISDEF = ZCL_VCXI_XCDC_CONDREC=>C_ISDEF_ALL
                transporting no fields.
        endif.

        check SY-SUBRC eq 0.

        ZCL_VCXI_XCDR_GRID=>SET_STYLE( exporting IF_OSVIS = LS_CR_SIMPACT-OSVIS
                                       changing  CF_STYLE = <S_STYL>-STYLE ).

      endloop.
    endloop.
  endloop.

endmethod.
method SET_STYLE.

  data: LF_EDIT    type ABAP_BOOL,
        LF_HOTSPOT type ABAP_BOOL,
        LF_F4      type ABAP_BOOL,
        LF_STYLE   type RAW4.

  ZCL_VCXI_XCDR_GRID=>GET_CURRENT_STYLE( exporting IF_STYLE     = CF_STYLE
                                         importing EF_EDIT      = LF_EDIT
                                                   EF_HOTSPOT   = LF_HOTSPOT
                                                   EF_F4        = LF_F4 ).

*** -----------------------------------------------------------------------------
  case IF_OSVIS.
*** Editable
    when '1'.
      if LF_EDIT ne ABAP_UNDEFINED.
        add CL_GUI_ALV_GRID=>MC_STYLE_ENABLED to LF_STYLE.
      endif.
      if LF_HOTSPOT ne ABAP_UNDEFINED.
        add CL_GUI_ALV_GRID=>MC_STYLE_HOTSPOT to LF_STYLE.
      endif.
      if LF_F4 ne ABAP_UNDEFINED.
        add CL_GUI_ALV_GRID=>MC_STYLE_F4 to LF_STYLE.
      endif.

*** Not Editable
    when others.
      if LF_EDIT ne ABAP_UNDEFINED.
        add CL_GUI_ALV_GRID=>MC_STYLE_DISABLED to LF_STYLE.
      endif.
      if LF_HOTSPOT ne ABAP_UNDEFINED.
        add CL_GUI_ALV_GRID=>MC_STYLE_HOTSPOT_NO to LF_STYLE.
      endif.
      if LF_F4 ne ABAP_UNDEFINED.
        add CL_GUI_ALV_GRID=>MC_STYLE_F4_NO to LF_STYLE.
      endif.
  endcase.

  move LF_STYLE to CF_STYLE.

endmethod.
