
class ZCL_VCXI_FBSS_ND_BPART definition
  public
  inheriting from ZCL_VCXI_F0SS_ND
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
    raising
      /VCXI/CX_CKX .
protected section.

  methods INIT_NODE
    redefinition .
private section.
endclass. "ZCL_VCXI_FBSS_ND_BPART definition
class ZCL_VCXI_FBSS_ND_BPART implementation.
  method CONSTRUCTOR.

*** Call Super CONSTRUCTOR
    SUPER->CONSTRUCTOR( IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

*** Assign to cluster - Parent
    move C_PARENT_ASSIGN to ME->F_ASSIGN.

*** Set Node as expandable
    move ABAP_TRUE to ME->F_EXPANDABLE.

    ME->INIT_NODE( ).

  endmethod.
  method INIT_NODE.

    data: LS_BPWID type ZVCXI_FBS_S_BPWID,
          LS_BPLEN type ZVCXI_FBS_S_BPLEN.
    data:           LR_PR_BPART type ref to ZCL_VCXI_FBSS_PR_BPART.
    field-symbols:  <S_NVAL>    type        YS_NVALS.

    SUPER->INIT_NODE( ).

***--------------------------------------------------------------------------------------
*** Check Input Gate is BFG
    try.
        move ME->R_GATE->R_CLSPR ?to LR_PR_BPART.
      catch CX_SY_MOVE_CAST_ERROR.
        clear LR_PR_BPART.
    endtry.

    check LR_PR_BPART is bound.

***--------------------------------------------------------------------------------------
*** Initialize Node Values from Gate
    move 'D' to ME->F_NETTYPE.
    if ME->F_EXPANDED eq ABAP_TRUE.
      move '2' to ME->F_NETTYPE.
    endif.

*** Set Node Description
    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = 0.
    <S_NVAL>-VAL  = LR_PR_BPART->GET_DESCR( ).

*** Set additional Values if collapsed
    check ME->F_EXPANDED eq ABAP_FALSE.

***--------------------------------------------------------------------------------------
*** Get Bag Part Width
    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = 2.
    move text-D01 to <S_NVAL>-VAL.

    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = 3.
    LR_PR_BPART->GET_BPWID( importing ES_BPWID = LS_BPWID ).
    <S_NVAL>-VAL = ME->CONV_ANY_TO_VAL( IF_VALUE = LS_BPWID-BPWID
                                        IF_UOM   = LS_BPWID-BPWID_UOM ).

***--------------------------------------------------------------------------------------
*** Get Bag Part Length
    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = 4.
    move text-D02 to <S_NVAL>-VAL.

    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = 5.
    LR_PR_BPART->GET_BPLEN( importing ES_BPLEN = LS_BPLEN ).
    <S_NVAL>-VAL = ME->CONV_ANY_TO_VAL( IF_VALUE = LS_BPLEN-BPLEN
                                        IF_UOM   = LS_BPLEN-BPLEN_UOM ).

***----------------------------------------------------------------------------
*** Set Status Icon
    ME->SET_STATUS_ICON( ).

  endmethod.
endclass. "ZCL_VCXI_FBSS_ND_BPART implementation
