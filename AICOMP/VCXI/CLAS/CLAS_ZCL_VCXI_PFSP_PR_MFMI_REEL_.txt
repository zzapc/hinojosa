
class ZCL_VCXI_PFSP_PR_MFMI_REEL definition
  public
  inheriting from ZCL_VCXI_XCSP_PR_MFMI
  create public .

public section.

  interfaces ZIF_VCXI_P0SP_MFP .

  aliases C_PARID_PCDIR
    for ZIF_VCXI_P0SP_MFP~C_PARID_PCDIR .
  aliases C_PARID_PCHEI
    for ZIF_VCXI_P0SP_MFP~C_PARID_PCHEI .
  aliases C_PARID_PCLEN
    for ZIF_VCXI_P0SP_MFP~C_PARID_PCLEN .
  aliases C_PARID_PCTYP
    for ZIF_VCXI_P0SP_MFP~C_PARID_PCTYP .
  aliases C_PARID_PCUPS
    for ZIF_VCXI_P0SP_MFP~C_PARID_PCUPS .
  aliases C_PARID_PCWID
    for ZIF_VCXI_P0SP_MFP~C_PARID_PCWID .
  aliases GET_PCDIM
    for ZIF_VCXI_P0SP_MFP~GET_PCDIM .
  aliases SET_PCDIM
    for ZIF_VCXI_P0SP_MFP~SET_PCDIM .

  constants C_IPRVT_PF_MFMI_REEL type /VCXI/VKSC_IPRVT value 'ZVCXI_PFSP_MFMI_REEL'. "#EC NOTEXT
  constants C_PARID_BAWGP_A type /VCXI/VKS_PARID value 'ZVCXI_P0_BAWGP_A'. "#EC NOTEXT
  constants C_PARID_BAWGT type /VCXI/VKS_PARID value 'ZVCXI_P0_BAWGT'. "#EC NOTEXT

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods GET_BAWGP
    returning
      value(RS_BAWGP) type ZVCXI_P0S_S_BAWGP
    raising
      /VCXI/CX_CKX .
  methods GET_BAWGT
    returning
      value(RS_BAWGT) type ZVCXI_P0S_S_BAWGT
    raising
      /VCXI/CX_CKX .
  methods SET_BAWGP
    importing
      !IS_BAWGP type ZVCXI_P0S_S_BAWGP
      !IF_SETBY type /VCXI/VKSR_SETBY optional
    raising
      /VCXI/CX_CKX .
  methods SET_BAWGT
    importing
      !IS_BAWGT type ZVCXI_P0S_S_BAWGT
      !IF_SETBY type /VCXI/VKSR_SETBY optional
    raising
      /VCXI/CX_CKX .

  methods GET_STATUS
    redefinition .
  methods GET_STATUS_MSG
    redefinition .
protected section.

  aliases GET_PCDIR
    for ZIF_VCXI_P0SP_MFP~GET_PCDIR .
  aliases GET_PCHEI
    for ZIF_VCXI_P0SP_MFP~GET_PCHEI .
  aliases GET_PCLEN
    for ZIF_VCXI_P0SP_MFP~GET_PCLEN .
  aliases GET_PCTYP
    for ZIF_VCXI_P0SP_MFP~GET_PCTYP .
  aliases GET_PCUPS
    for ZIF_VCXI_P0SP_MFP~GET_PCUPS .
  aliases GET_PCWID
    for ZIF_VCXI_P0SP_MFP~GET_PCWID .
  aliases SET_PCDIR
    for ZIF_VCXI_P0SP_MFP~SET_PCDIR .
  aliases SET_PCHEI
    for ZIF_VCXI_P0SP_MFP~SET_PCHEI .
  aliases SET_PCLEN
    for ZIF_VCXI_P0SP_MFP~SET_PCLEN .
  aliases SET_PCTYP
    for ZIF_VCXI_P0SP_MFP~SET_PCTYP .
  aliases SET_PCUPS
    for ZIF_VCXI_P0SP_MFP~SET_PCUPS .
  aliases SET_PCWID
    for ZIF_VCXI_P0SP_MFP~SET_PCWID .

  constants C_IEVNT_O_BAWGT_CHANGED type /VCXI/VKSC_IEVNT value 'BAWGT_CHANGED'. "#EC NOTEXT
  constants C_IEVNT_O_PCDIM_CHANGED type /VCXI/VKSC_IEVNT value 'PCDIM_CHANGED'. "#EC NOTEXT

  methods CONV_TO_CONSU
    redefinition .
private section.
endclass. "ZCL_VCXI_PFSP_PR_MFMI_REEL definition
class ZCL_VCXI_PFSP_PR_MFMI_REEL implementation.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_GATE = IR_GATE ).

*** Set Dimension-Depending Parameter IDS's
  move C_PARID_PRICP_W to ME->F_PARID_PRICP.
  move C_PARID_CONSU_W to ME->F_PARID_CONSU.

endmethod.
method CONV_TO_CONSU.

  data:  LS_BAWGT         type ZVCXI_P0S_S_BAWGT,
         LS_BAWGP         type ZVCXI_P0S_S_BAWGP,
         LS_PCWID         type ZVCXI_P0SP_S_PCWID,
         LS_CALCV_AREA    type ZVCXI_XCS_S_CALCV,
         LS_CALCV         type ZVCXI_XCS_S_CALCV.

*** Get Basis Weight
  move ME->GET_BAWGT( ) to LS_BAWGT.
  move ME->GET_BAWGP( ) to LS_BAWGP.

*** Calculate Area depending on Machine Quantity In
  LS_PCWID = ME->GET_PCWID( ).
  ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = IS_MQTYI-MQTYI
                                                      IF_CALCV_UOM_1 = IS_MQTYI-MQTYI_UOM
                                                      IF_CALCV_2     = LS_PCWID-PCWID
                                                      IF_CALCV_UOM_2 = LS_PCWID-PCWID_UOM
                                                      IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_MULTIPLY
                                                      IF_UOM         = LS_BAWGP-BAWGP_UOM
                                            importing EF_CALCV       = LS_CALCV_AREA-CALCV
                                                      EF_CALCV_UOM   = LS_CALCV_AREA-CALCV_UOM ).


*** Calculate Weight
  if LS_BAWGP-BAWGP is not initial.

    ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_CALCV_AREA-CALCV
                                                        IF_CALCV_UOM_1 = LS_CALCV_AREA-CALCV_UOM
                                                        IF_CALCV_2     = LS_BAWGP-BAWGP
                                                        IF_CALCV_UOM_2 = LS_BAWGP-BAWGP_UOM
                                                        IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_DIVIDE
                                              importing EF_CALCV       = LS_CALCV-CALCV
                                                        EF_CALCV_UOM   = LS_CALCV-CALCV_UOM ).

    RS_CONSU-CONSU   = LS_CALCV-CALCV * LS_BAWGT-BAWGT.
  else.
    RS_CONSU-CONSU   = 0.
  endif.
  RS_CONSU-CONSU_UOM = LS_BAWGT-BAWGT_UOM.

endmethod.
method GET_BAWGP.

  data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

  LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_BAWGP_A ).
  move LS_PARAM-ATFLV to RS_BAWGP-BAWGP.
  move LS_PARAM-UNIT  to RS_BAWGP-BAWGP_UOM.

endmethod.
method GET_BAWGT.

  data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

  LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_BAWGT ).
  move LS_PARAM-ATFLV to RS_BAWGT-BAWGT.
  move LS_PARAM-UNIT  to RS_BAWGT-BAWGT_UOM.

endmethod.
method GET_STATUS.

  data: LS_MFISP type ZVCXI_XCSP_S_MFISP,
        LS_BAWGT type ZVCXI_P0S_S_BAWGT,
        LS_BAWGP type ZVCXI_P0S_S_BAWGP.

  RF_ELMST = SUPER->GET_STATUS( ).

***------------------------------------------------------------------
*** Check Setup Factor
  LS_MFISP = ME->GET_MFISP( ).
  if LS_MFISP-MFISP is not initial.
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G ).
  else.
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R ).
  endif.

***------------------------------------------------------------------
*** Check Consumption Factor
  if ME->GET_MFICF( ) is not initial.
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G ).
  else.
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R ).
  endif.

***------------------------------------------------------------------
*** Check Base Weight
  LS_BAWGT = ME->GET_BAWGT( ).
  if LS_BAWGT-BAWGT is not initial.
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G ).
  else.
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R ).
  endif.

***------------------------------------------------------------------
*** Check Base Weight Unit
  LS_BAWGP = ME->GET_BAWGP( ).
  if LS_BAWGP-BAWGP is not initial.
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G ).
  else.
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R ).
  endif.

endmethod.
method GET_STATUS_MSG.

  data: LS_MFISP type ZVCXI_XCSP_S_MFISP,
        LS_BAWGT type ZVCXI_P0S_S_BAWGT,
        LS_BAWGP type ZVCXI_P0S_S_BAWGP.

  SUPER->GET_STATUS_MSG( IR_MESSAGE = IR_MESSAGE ).

***------------------------------------------------------------------
*** Check Setup Factor
  LS_MFISP = ME->GET_MFISP( ).
  if LS_MFISP-MFISP is not initial.
***  Setup Factor is maintained.
    IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_PCSP'
                             IF_MSGTY = 'S'
                             IF_MSGNO = '101' ).
  else.
***  Setup Factor is not maintained.
    IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_PCSP'
                             IF_MSGTY = 'E'
                             IF_MSGNO = '102' ).
  endif.

***------------------------------------------------------------------
*** Check Consumption Factor
  if ME->GET_MFICF( ) is not initial.
***  Consumption Factor is maintained.
    IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_PCSP'
                             IF_MSGTY = 'S'
                             IF_MSGNO = '103' ).
  else.
***  Consumption Factor is not maintained.
    IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_PCSP'
                             IF_MSGTY = 'E'
                             IF_MSGNO = '104' ).
  endif.

***------------------------------------------------------------------
*** Check Base Weight
  LS_BAWGT = ME->GET_BAWGT( ).
  if LS_BAWGT-BAWGT is not initial.
***  Base Weight is maintained.
    IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_PCSP'
                             IF_MSGTY = 'S'
                             IF_MSGNO = '105' ).
  else.
***  Base Weight is not maintained.
    IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_PCSP'
                             IF_MSGTY = 'E'
                             IF_MSGNO = '106' ).
  endif.

***------------------------------------------------------------------
*** Check Base Weight Unit
  LS_BAWGP = ME->GET_BAWGP( ).
  if LS_BAWGP-BAWGP is not initial.
***  Base Weight Unit is maintained.
    IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_PCSP'
                             IF_MSGTY = 'S'
                             IF_MSGNO = '107' ).
  else.
***  Base Weight Unit is not maintained.
    IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_PCSP'
                             IF_MSGTY = 'E'
                             IF_MSGNO = '108' ).
  endif.

endmethod.
method SET_BAWGP.

  data:  LF_CHANGED type ABAP_BOOL.
  data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

  LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_BAWGP_A ).

  move IS_BAWGP-BAWGP                      to LS_PARAM-ATFLV.
  move IS_BAWGP-BAWGP_UOM                  to LS_PARAM-UNIT.
  move IF_SETBY                            to LS_PARAM-SETBY.

  LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

  if LF_CHANGED eq ABAP_TRUE.
*** Send Event
    ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_BAWGT_CHANGED ).
  endif.

endmethod.
method SET_BAWGT.

  data:  LF_CHANGED type ABAP_BOOL.
  data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

  LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_BAWGT ).

  move IS_BAWGT-BAWGT                      to LS_PARAM-ATFLV.
  move IS_BAWGT-BAWGT_UOM                  to LS_PARAM-UNIT.
  move IF_SETBY                            to LS_PARAM-SETBY.

  LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

  if LF_CHANGED eq ABAP_TRUE.
*** Send Event
    ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_BAWGT_CHANGED ).
  endif.

endmethod.
method ZIF_VCXI_P0SP_MFP~GET_PCDIM.

  data: LS_PCLEN type ZVCXI_P0SP_S_PCLEN,
        LS_PCWID type ZVCXI_P0SP_S_PCWID,
        LS_PCHEI type ZVCXI_P0SP_S_PCHEI.

***------------------------------------------------------------------
*** PC Type
  move ME->GET_PCTYP( ) to RS_PCDIM-PCTYP.

***------------------------------------------------------------------
*** PC Length
  move ME->GET_PCLEN( ) to LS_PCLEN.
  move-corresponding LS_PCLEN to RS_PCDIM.

*** PC Width
  move ME->GET_PCWID( ) to LS_PCWID.
  move-corresponding LS_PCWID to RS_PCDIM.

*** PC Height
  move ME->GET_PCHEI( ) to LS_PCHEI.
  move-corresponding LS_PCHEI to RS_PCDIM.

***------------------------------------------------------------------
*** PC UPs
  move ME->GET_PCUPS( ) to RS_PCDIM-PCUPS.

*** Number of Outs on MFMI are 1
  move 1                to RS_PCDIM-NOOUT.

***------------------------------------------------------------------
*** PC Direction
  move ME->GET_PCDIR( ) to RS_PCDIM-PCDIR.

endmethod.
  method ZIF_VCXI_P0SP_MFP~GET_PCDIR.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PCDIR ).
    RF_PCDIR = LS_PARAM-ATWRT.
    EF_SETBY = LS_PARAM-SETBY.

  endmethod.
  method ZIF_VCXI_P0SP_MFP~GET_PCHEI.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PCHEI ).
    RS_PCHEI-PCHEI     = LS_PARAM-ATFLV.
    RS_PCHEI-PCHEI_UOM = LS_PARAM-UNIT.
    EF_SETBY           = LS_PARAM-SETBY.

  endmethod.
  method ZIF_VCXI_P0SP_MFP~GET_PCLEN.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PCLEN ).
    RS_PCLEN-PCLEN     = LS_PARAM-ATFLV.
    RS_PCLEN-PCLEN_UOM = LS_PARAM-UNIT.
    EF_SETBY           = LS_PARAM-SETBY.

  endmethod.
  method ZIF_VCXI_P0SP_MFP~GET_PCTYP.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PCTYP ).
    RF_PCTYP = LS_PARAM-ATWRT.
    EF_SETBY = LS_PARAM-SETBY.

  endmethod.
  method ZIF_VCXI_P0SP_MFP~GET_PCUPS.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PCUPS ).
    RF_PCUPS = LS_PARAM-ATFLV.
    EF_SETBY = LS_PARAM-SETBY.

  endmethod.
  method ZIF_VCXI_P0SP_MFP~GET_PCWID.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PCWID ).
    RS_PCWID-PCWID     = LS_PARAM-ATFLV.
    RS_PCWID-PCWID_UOM = LS_PARAM-UNIT.
    EF_SETBY           = LS_PARAM-SETBY.

  endmethod.
method ZIF_VCXI_P0SP_MFP~SET_PCDIM.

  data: LS_PCLEN type ZVCXI_P0SP_S_PCLEN,
        LS_PCWID type ZVCXI_P0SP_S_PCWID,
        LS_PCHEI type ZVCXI_P0SP_S_PCHEI,
        LS_PCDIM type ZVCXI_P0SP_S_PCDIM.

  LS_PCDIM =  ME->GET_PCDIM( ).

***------------------------------------------------------------------
*** Set PC Type
  if IS_PCDIM-PCTYP ne LS_PCDIM-PCTYP.
    ME->SET_PCTYP( IF_PCTYP = IS_PCDIM-PCTYP
                   IF_SETBY = IF_SETBY ).
  endif.

***------------------------------------------------------------------
*** Set PC Length
  if IS_PCDIM-PCLEN ne LS_PCDIM-PCLEN.
    move-corresponding IS_PCDIM to LS_PCLEN.
    ME->SET_PCLEN( IS_PCLEN = LS_PCLEN
                   IF_SETBY = IF_SETBY ).
  endif.

*** Set PC Width
  if IS_PCDIM-PCWID ne LS_PCDIM-PCWID.
    move-corresponding IS_PCDIM to LS_PCWID.
    ME->SET_PCWID( IS_PCWID = LS_PCWID
                   IF_SETBY = IF_SETBY ).
  endif.

*** Set PC Height
  if IS_PCDIM-PCHEI ne LS_PCDIM-PCHEI.
    move-corresponding IS_PCDIM to LS_PCHEI.
    ME->SET_PCHEI( IS_PCHEI = LS_PCHEI
                   IF_SETBY = IF_SETBY ).
  endif.

***------------------------------------------------------------------
*** Set PC Ups
  if IS_PCDIM-PCUPS ne LS_PCDIM-PCUPS.
    ME->SET_PCUPS( IF_PCUPS = IS_PCDIM-PCUPS
                   IF_SETBY = IF_SETBY ).
  endif.

*** Set Number of Outs
  "Value will be ignored

***------------------------------------------------------------------
*** Set PC Direction
  if IS_PCDIM-PCDIR ne LS_PCDIM-PCDIR.
    ME->SET_PCDIR( IF_PCDIR = IS_PCDIM-PCDIR
                   IF_SETBY = IF_SETBY ).
  endif.

endmethod.
method ZIF_VCXI_P0SP_MFP~SET_PCDIR.

  data:  LF_CHANGED type ABAP_BOOL.
  data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

  LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PCDIR ).

  move IF_PCDIR                            to LS_PARAM-ATWRT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC    to LS_PARAM-SETBY.

  LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

  if LF_CHANGED eq ABAP_TRUE.
*** Send Event
    ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_PCDIM_CHANGED ).
  endif.

endmethod.
method ZIF_VCXI_P0SP_MFP~SET_PCHEI.

  data:  LF_CHANGED type ABAP_BOOL.
  data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

  LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PCHEI ).

  move IS_PCHEI-PCHEI                      to LS_PARAM-ATFLV.
  move IS_PCHEI-PCHEI_UOM                  to LS_PARAM-UNIT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC    to LS_PARAM-SETBY.

  LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

  if LF_CHANGED eq ABAP_TRUE.
*** Send Event
    ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_PCDIM_CHANGED ).
  endif.

endmethod.
method ZIF_VCXI_P0SP_MFP~SET_PCLEN.

  data:  LF_CHANGED type ABAP_BOOL.
  data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

  LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PCLEN ).

  move IS_PCLEN-PCLEN                      to LS_PARAM-ATFLV.
  move IS_PCLEN-PCLEN_UOM                  to LS_PARAM-UNIT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC    to LS_PARAM-SETBY.

  LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

  if LF_CHANGED eq ABAP_TRUE.
*** Send Event
    ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_PCDIM_CHANGED ).
  endif.

endmethod.
method ZIF_VCXI_P0SP_MFP~SET_PCTYP.

  data:  LF_CHANGED type ABAP_BOOL.
  data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

  LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PCTYP ).

  move IF_PCTYP                            to LS_PARAM-ATWRT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC    to LS_PARAM-SETBY.

  LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

  if LF_CHANGED eq ABAP_TRUE.
*** Send Event
    ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_PCDIM_CHANGED ).
  endif.

endmethod.
method ZIF_VCXI_P0SP_MFP~SET_PCUPS.

  data:  LF_CHANGED type ABAP_BOOL.
  data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

  LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PCUPS ).

  move IF_PCUPS                            to LS_PARAM-ATFLV.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC    to LS_PARAM-SETBY.

  LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

  if LF_CHANGED eq ABAP_TRUE.
*** Send Event
    ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_PCDIM_CHANGED ).
  endif.

endmethod.
method ZIF_VCXI_P0SP_MFP~SET_PCWID.

  data:  LF_CHANGED type ABAP_BOOL.
  data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

  LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_PCWID ).

  move IS_PCWID-PCWID                      to LS_PARAM-ATFLV.
  move IS_PCWID-PCWID_UOM                  to LS_PARAM-UNIT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC    to LS_PARAM-SETBY.

  LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

  if LF_CHANGED eq ABAP_TRUE.
*** Send Event
    ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_PCDIM_CHANGED ).
  endif.

endmethod.
