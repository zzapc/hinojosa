
class ZCL_VCXI_XCUC_GRID_MUSCE definition
  public
  final
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_MUCEL type ref to ZCL_VCXI_XCUC_MUCEL
    raising
      /VCXI/CX_CKX .
  methods DO_FREE
    raising
      /VCXI/CX_CKX .
  methods REFRESH_GRID
    raising
      /VCXI/CX_CKX .
protected section.

  data T_MUSCE_GRID type ZVCXI_XCUC_TT_MUSCE_GRID .
  data R_CONTAINER type ref to CL_GUI_CUSTOM_CONTAINER .
  data R_GRID type ref to CL_GUI_ALV_GRID .
  data T_FIELDCAT type LVC_T_FCAT .
  data R_MUCEL type ref to ZCL_VCXI_XCUC_MUCEL .

  methods GET_LAYOUT
    returning
      value(RS_LAYOUT) type LVC_S_LAYO
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_ADD
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_DEL
    raising
      /VCXI/CX_CKX .
  methods INIT_DATA
    raising
      /VCXI/CX_CKX .
  methods BUILD_FIELDCAT
    raising
      /VCXI/CX_CKX .
  methods INIT_CONTAINER
    raising
      /VCXI/CX_CKX .
  methods INIT_GRID
    raising
      /VCXI/CX_CKX .
  methods HANDLE_TOOLBAR
    for event TOOLBAR of CL_GUI_ALV_GRID
    importing
      !E_OBJECT
      !E_INTERACTIVE .
  methods HANDLE_UCOMM
    for event USER_COMMAND of CL_GUI_ALV_GRID
    importing
      !E_UCOMM .
private section.
endclass. "ZCL_VCXI_XCUC_GRID_MUSCE definition
class ZCL_VCXI_XCUC_GRID_MUSCE implementation.
  method BUILD_FIELDCAT .

    call function 'LVC_FIELDCATALOG_MERGE'
      exporting
        I_STRUCTURE_NAME       = 'ZVCXI_XCUC_S_MUSCE_GRID'
      changing
        CT_FIELDCAT            = ME->T_FIELDCAT
      exceptions
        INCONSISTENT_INTERFACE = 1
        PROGRAM_ERROR          = 2
        others                 = 3.

    if SY-SUBRC ne 0.
      /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
    endif.

  endmethod.
  method CONSTRUCTOR.

    ME->R_MUCEL = IR_MUCEL.

  endmethod.
method DO_FREE .

*** Check for Changes
  if ME->R_GRID is bound.
    ME->R_GRID->CHECK_CHANGED_DATA( ).
  endif.

  if ME->R_GRID is bound.
    ME->R_GRID->FREE( exceptions
                        CNTL_ERROR        = 1
                        CNTL_SYSTEM_ERROR = 2
                        others            = 3 ).
    if SY-SUBRC <> 0.
      /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
    endif.
    clear ME->R_GRID.
  endif.

  if ME->R_CONTAINER is bound.
    ME->R_CONTAINER->FREE( exceptions
                             CNTL_ERROR        = 1
                             CNTL_SYSTEM_ERROR = 2
                             others            = 3 ).
    if SY-SUBRC <> 0.
      /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
    endif.
    clear ME->R_CONTAINER.
  endif.

  clear ME->R_MUCEL.

endmethod.
  method GET_LAYOUT.

    RS_LAYOUT-CWIDTH_OPT = 'X'.
    RS_LAYOUT-SEL_MODE   = 'A'.

  endmethod.
  method HANDLE_TOOLBAR.

    data:  LF_DISABLED type ABAP_BOOL.

    try.
***     Lock Status
        if ME->R_MUCEL->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ) eq ABAP_FALSE.
          move ABAP_TRUE to LF_DISABLED.
        endif.

***----------------------------------------------------------------------------
***     Insert Toolbar elements
        append value #( FUNCTION  = 'ADD'
                        TEXT      = 'Add'(F01)
                        ICON      = ICON_INSERT_ROW
                        DISABLED  = LF_DISABLED
                      ) to E_OBJECT->MT_TOOLBAR.

        append value #( FUNCTION  = 'DEL'
                        TEXT      = 'Delete'(F02)
                        ICON      = ICON_DELETE_ROW
                        DISABLED  = LF_DISABLED
                      ) to E_OBJECT->MT_TOOLBAR.

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        LR_CX_CKX->SHOW_MESSAGE( ).
    endtry.

  endmethod.
  method HANDLE_UC_ADD.

    data: LF_CANCELED       type        ABAP_BOOL.
    data: LT_OBJECT_ALL   type /VCXI/CKX_TT_OBJECT,
          LT_OBJECT_STYLE type /VCXI/CKX_TT_OBJECT_STYLE,
          LT_OBJECT_SEL   type /VCXI/CKX_TT_OBJECT,
          LT_MUSCE        type /VCXI/CKX_TT_OBJECT.
    data: LR_CUPRFL_CVC type ref to /VCXI/CL_VKCC_CUPRFL,
          LR_CUPRFL_SVC type ref to /VCXI/CL_VKSC_CUPRFL,
          LR_CUELMNT    type ref to /VCXI/CL_VKSC_CUELMNT,
          LR_OBJECT     type ref to /VCXI/IF_CKX_OBJECT.
    field-symbols: <S_OBJECT_STYLE>  type        /VCXI/CKX_S_OBJECT_STYLE.


***--------------------------------------------------------------------------------------
*** Select CoreVC Profile and get corresponding SmartVC Profile
    LR_CUPRFL_CVC = /VCXI/CL_VKCCU_SHLP_CUPRFL=>CALL_SHLP( ).
    check LR_CUPRFL_CVC is bound.
    LR_CUPRFL_SVC = /VCXI/CL_VKSC_CUPRFL=>GET_INSTANCE_BY_CMPRK( IS_CMPRK = LR_CUPRFL_CVC->S_CMPRK ).
    check LR_CUPRFL_SVC is bound.

***--------------------------------------------------------------------------------------
*** Get all Tree Elements
    LT_OBJECT_ALL = LR_CUPRFL_SVC->GET_REGISTERED_CUELMNT( ).
    /VCXI/CL_VKSC_SERVICE=>SORT_CUELMNT( changing CT_OBJECT = LT_OBJECT_ALL ).

*** Define Style
    loop at LT_OBJECT_ALL into LR_OBJECT.
      move LR_OBJECT ?to LR_CUELMNT.
      check LR_CUELMNT is bound.

      append initial line to LT_OBJECT_STYLE assigning <S_OBJECT_STYLE>.
      move LR_OBJECT to <S_OBJECT_STYLE>-R_OBJECT.

***   Disable and Mark exsiting
      if LINE_EXISTS( ME->R_MUCEL->T_MUSCE[ TABLE_LINE = LR_CUELMNT ] ).
        <S_OBJECT_STYLE>-DISABLED = ABAP_TRUE.
        <S_OBJECT_STYLE>-STYLE    = CL_COLUMN_TREE_MODEL=>STYLE_EMPHASIZED_POSITIVE.
      endif.

***   Image
      if LR_CUELMNT->R_REGELMNT             is bound and
         LR_CUELMNT->R_REGELMNT->F_TIICO_ID is not initial.
        move LR_CUELMNT->R_REGELMNT->F_TIICO_ID    to <S_OBJECT_STYLE>-IMAGE.
      else.
        if LR_CUELMNT->R_REG_ELMTY is bound.
          move LR_CUELMNT->R_REG_ELMTY->F_TIICO_ID to <S_OBJECT_STYLE>-IMAGE.
        endif.
      endif.

***   Expand Parents of Existing
      if <S_OBJECT_STYLE>-STYLE eq CL_COLUMN_TREE_MODEL=>STYLE_EMPHASIZED_POSITIVE.
        move LR_CUELMNT->/VCXI/IF_CKX_OBJECT_H~GET_PARENT( ) to LR_OBJECT.
        if LR_OBJECT is bound.
          read table LT_OBJECT_STYLE assigning <S_OBJECT_STYLE> with key R_OBJECT = LR_OBJECT.
          if SY-SUBRC eq 0.
            move ABAP_TRUE to <S_OBJECT_STYLE>-EXPAND.
          endif.
        endif.
      endif.
    endloop.

***------------------------------------------------------------------------------------------------
*** Call Search Help
    call function '/VCXI/CKX_OBJECT_POPUP_START'
      exporting
        IT_OBJECT_ALL    = LT_OBJECT_ALL
        IT_OBJECT_STYLE  = LT_OBJECT_STYLE
        IF_SORT_BY_ID    = ABAP_FALSE
        IF_SORT_BY_DESCR = ABAP_FALSE
        IF_EXPAND_ALL    = ABAP_FALSE
        IF_SELMODE       = CL_COLUMN_TREE_MODEL=>NODE_SEL_MODE_MULTIPLE
      importing
        ET_OBJECT_SEL    = LT_OBJECT_SEL
        EF_CANCELED      = LF_CANCELED.

    check LF_CANCELED ne ABAP_TRUE.

***------------------------------------------------------------------------------------------------
*** Add selected Customizing Element
    LT_MUSCE = ME->R_MUCEL->T_MUSCE.
    append lines of LT_OBJECT_SEL to LT_MUSCE.
    ME->R_MUCEL->SET_MUSCE( IT_MUSCE = LT_MUSCE ).

  endmethod.
  method HANDLE_UC_DEL.

    data: LS_ROW   type LVC_S_ROW.
    data: LT_ROW   type LVC_T_ROW,
          LT_MUSCE type /VCXI/CKX_TT_OBJECT.


*** Get selected Rows
    ME->R_GRID->GET_SELECTED_ROWS( importing ET_INDEX_ROWS = LT_ROW ).

    LT_MUSCE = ME->R_MUCEL->T_MUSCE.

*** Remove selected SmartVC Customizing Elements
    loop at LT_ROW into LS_ROW.
      delete LT_MUSCE where TABLE_LINE eq ME->T_MUSCE_GRID[ LS_ROW-INDEX ]-R_CUELMNT.
    endloop.

*** Set remaining Customizing Element
    ME->R_MUCEL->SET_MUSCE( IT_MUSCE = LT_MUSCE ).

  endmethod.
method HANDLE_UCOMM.

  try.
      case E_UCOMM.
        when 'ADD'.
          ME->HANDLE_UC_ADD( ).
        when 'DEL'.
          ME->HANDLE_UC_DEL( ).
      endcase.

    catch /VCXI/CX_CKX into data(LR_CX_CKX).
      LR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

endmethod.
method INIT_CONTAINER .

  if ME->R_CONTAINER is not bound.
    create object ME->R_CONTAINER
      exporting
        CONTAINER_NAME              = '0110CC_MUSCE'
      exceptions
        CNTL_ERROR                  = 1
        CNTL_SYSTEM_ERROR           = 2
        CREATE_ERROR                = 3
        LIFETIME_ERROR              = 4
        LIFETIME_DYNPRO_DYNPRO_LINK = 5
        others                      = 6.
    if SY-SUBRC ne 0.
      /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
    endif.
  endif.

endmethod.
  method INIT_DATA.

    data: LR_CUELMNT type ref to /VCXI/CL_VKSC_CUELMNT,
          LR_OBJECT  type ref to /VCXI/IF_CKX_OBJECT.

    clear ME->T_MUSCE_GRID.

    loop at ME->R_MUCEL->T_MUSCE into LR_OBJECT.
      LR_CUELMNT ?= LR_OBJECT.

      append value #( R_CUELMNT = LR_CUELMNT
                      S_CMPRK   = LR_CUELMNT->R_CUPRFL->R_CVC_CUPRFL->S_CMPRK
                      ICELM     = LR_CUELMNT->/VCXI/IF_CKX_OBJECT~GET_ID( )
                      DESCR     = LR_CUELMNT->/VCXI/IF_CKX_OBJECT~GET_DESCR( )
                    ) to ME->T_MUSCE_GRID.
    endloop.

  endmethod.
method INIT_GRID .

  if ME->R_GRID is not bound.
    create object ME->R_GRID
      exporting
        I_PARENT          = ME->R_CONTAINER
        I_APPL_EVENTS     = 'X'
      exceptions
        ERROR_CNTL_CREATE = 1
        ERROR_CNTL_INIT   = 2
        ERROR_CNTL_LINK   = 3
        ERROR_DP_CREATE   = 4
        others            = 5.
    if SY-SUBRC ne 0.
      /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
    endif.

*** Set Eventhandler for Toolbar
    set handler ME->HANDLE_TOOLBAR             for ME->R_GRID.
    set handler ME->HANDLE_UCOMM               for ME->R_GRID.

  endif.

endmethod.
  method REFRESH_GRID .

*** Initialized Data
    ME->INIT_DATA( ).

*** Build Field Catalog
    ME->BUILD_FIELDCAT( ).

*** Initial Display
    if ME->R_GRID is not bound.
      ME->INIT_CONTAINER( ).
      ME->INIT_GRID( ).

      ME->R_GRID->SET_TABLE_FOR_FIRST_DISPLAY( exporting
                                                 IT_TOOLBAR_EXCLUDING          = value #( ( CL_GUI_ALV_GRID=>MC_FC_EXCL_ALL ) )
                                                 IS_LAYOUT                     = ME->GET_LAYOUT( )
                                               changing
                                                 IT_FIELDCATALOG               = ME->T_FIELDCAT
                                                 IT_OUTTAB                     = ME->T_MUSCE_GRID
                                               exceptions
                                                 INVALID_PARAMETER_COMBINATION = 1
                                                 PROGRAM_ERROR                 = 2
                                                 TOO_MANY_LINES                = 3
                                                 others                        = 4 ).
      if SY-SUBRC ne 0.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
      endif.

*** Update of Data
    else.
      ME->R_GRID->SET_FRONTEND_FIELDCATALOG( IT_FIELDCATALOG = ME->T_FIELDCAT ).
      ME->R_GRID->SET_FRONTEND_LAYOUT( IS_LAYOUT = ME->GET_LAYOUT( ) ).
      ME->R_GRID->REFRESH_TABLE_DISPLAY( exceptions FINISHED = 1 ).
      if SY-SUBRC ne 0.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
      endif.
    endif.

  endmethod.
