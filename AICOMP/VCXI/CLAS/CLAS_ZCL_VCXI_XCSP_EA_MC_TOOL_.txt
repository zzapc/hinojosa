
class ZCL_VCXI_XCSP_EA_MC_TOOL definition
  public
  inheriting from ZCL_VCXI_XCSP_EA_MC
  create public .

public section.

  interfaces /VCXI/IF_VKCR_EXPL_PRT .

  methods /VCXI/IF_VKCR_EXPL_BOM~GET_BOM
    redefinition .
  methods /VCXI/IF_VKCR_EXPL_BOM~FILTER_CPBRG
    redefinition .
protected section.

  methods GET_BOM_CONSU
    redefinition .
private section.
endclass. "ZCL_VCXI_XCSP_EA_MC_TOOL definition
class ZCL_VCXI_XCSP_EA_MC_TOOL implementation.
  method /VCXI/IF_VKCR_EXPL_BOM~FILTER_CPBRG.

*** Perform SUPER
    SUPER->/VCXI/IF_VKCR_EXPL_BOM~FILTER_CPBRG( changing CT_CPBRG = CT_CPBRG ).

*** No BOM explosion if tool is not Batch Managed
    if cast ZCL_VCXI_XCSP_PR_MC_TOOL( ME->R_PR_MC )->GET_XCHPF( ) ne ABAP_TRUE.
      free CT_CPBRG.
    endif.

  endmethod.
  method /VCXI/IF_VKCR_EXPL_BOM~GET_BOM.

*** Call Super Implementation
    if IS_EXQTY is supplied.
      RS_EXBOM = SUPER->/VCXI/IF_VKCR_EXPL_BOM~GET_BOM( IS_EXQTY = IS_EXQTY
                                                        IF_BMENG = IF_BMENG
                                                        IF_BMEIN = IF_BMEIN ).
    else.
      RS_EXBOM = SUPER->/VCXI/IF_VKCR_EXPL_BOM~GET_BOM( IF_BMENG = IF_BMENG
                                                        IF_BMEIN = IF_BMEIN ).
    endif.

*** Mark as Fixed Quantity
    move ABAP_TRUE to RS_EXBOM-FMENG.
    move ABAP_TRUE to RS_EXBOM-FMENG_X.

*** Mark as not costing relevant
    move ABAP_FALSE to RS_EXBOM-SANKA.
    move ABAP_TRUE  to RS_EXBOM-SANKA_X.

  endmethod.
  method /VCXI/IF_VKCR_EXPL_PRT~FILTER_CPBRG.

    return.   "no need to filter

  endmethod.
  method /VCXI/IF_VKCR_EXPL_PRT~GET_PRT.

    data:  LS_CONSU   type        ZVCXI_XCSP_S_CONSU.

*** Type Material
    move 'M'                             to RS_EXPRT-FHMAR.
    move ABAP_TRUE                       to RS_EXPRT-FHMAR_X.

*** Material Number
    move ME->R_PR_MC->GET_MATNR( )       to RS_EXPRT-MATNR.
    move ABAP_TRUE                       to RS_EXPRT-MATNR_X.

*** Get Consumption
    move ME->R_PR_MC->GET_CONSU( )       to LS_CONSU.
    move LS_CONSU-CONSU                  to RS_EXPRT-MGVGW.
    move ABAP_TRUE                       to RS_EXPRT-MGVGW_X.
    move LS_CONSU-CONSU_UOM              to RS_EXPRT-MGEINH.
    move ABAP_TRUE                       to RS_EXPRT-MGEINH_X.

  endmethod.
  method GET_BOM_CONSU.

*** No adjustment to Explosion Quantity due to "Fix Quantity" flag
    RS_CONSU = ME->R_PR_MC->GET_CONSU( ).

  endmethod.
endclass. "ZCL_VCXI_XCSP_EA_MC_TOOL implementation
