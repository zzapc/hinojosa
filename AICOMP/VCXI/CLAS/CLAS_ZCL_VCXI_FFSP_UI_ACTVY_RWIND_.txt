
class ZCL_VCXI_FFSP_UI_ACTVY_RWIND definition
  public
  inheriting from /VCXI/CL_VKSRU_CLSUI
  create public .

public section.

  data F_CURSOR type SCRFNAME read-only .
  data S_ACTVY_RWIND_VIS type ZVCXI_FFSP_S_ACTVY_RWIND_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods SET_CURSOR
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_ACTVY_RWIND_VIS
    importing
      !IS_ACTVY_RWIND_VIS type ZVCXI_FFSP_S_ACTVY_RWIND_VIS
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  data R_GRID_MC_CORE type ref to ZCL_VCXI_FFSP_GRID_MC_CORE .
  data R_PR_ACTVY_RWIND type ref to ZCL_VCXI_FFSP_PR_ACTVY_RWIND .

  methods INIT_VIS
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
private section.
endclass. "ZCL_VCXI_FFSP_UI_ACTVY_RWIND definition
class ZCL_VCXI_FFSP_UI_ACTVY_RWIND implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_FFSP_UI_ACTVY_RWIND_BIND'
      exporting
        IR_UI_ACTVY_RWIND = ME
      importing
        ES_DYNP           = ME->S_DYNP.

*** Create Grid for GRID_MC_CORE
    create object ME->R_GRID_MC_CORE
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_MC_CORE'.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_ACTVY_RWIND.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

    if ME->R_GRID_MC_CORE is bound.
      ME->R_GRID_MC_CORE->DO_FREE( ).
      clear ME->R_GRID_MC_CORE.
    endif.

  endmethod.
  method DO_MODIFY_SCREEN.

    data: LF_LOCKED type        /VCXI/CKX_LOCKED,
          LF_INUSE  type        /VCXI/VKSR_INUSE.
    data:  LR_CX_CKX type ref to /VCXI/CX_CKX.

    SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                             IF_DYNNR = IF_DYNNR ).

    try.

        if IF_REPID eq ME->S_DYNP-REPID and
           IF_DYNNR eq ME->S_DYNP-DYNNR.

          LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
          LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***       Modify Scree using DynamicVC
          ZCL_VCXI_XCDR_SERVICE=>MODIFY_SCREEN( IR_GATE = ME->R_GATE ).

          loop at screen.
            if LF_LOCKED ne ABAP_TRUE or
               LF_INUSE  ne ABAP_TRUE.
              move 0 to SCREEN-INPUT.
            endif.
            modify screen.
          endloop.
        endif.

      catch /VCXI/CX_CKX into LR_CX_CKX.
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
method DO_PBO.

  ME->INIT_VIS( ).

  if ME->R_GRID_MC_CORE is bound.
    move ABAP_TRUE to ME->R_GRID_MC_CORE->F_REFRESH_REQ.
    ME->R_GRID_MC_CORE->REFRESH_GRID( ).
  endif.

  set cursor field ME->F_CURSOR.
  clear ME->F_CURSOR.

endmethod.
  method DO_UPDATE_ACTVY_RWIND_VIS.

*** Set Winding Side
    if ME->S_ACTVY_RWIND_VIS-RWSID ne IS_ACTVY_RWIND_VIS-RWSID.
      ME->R_PR_ACTVY_RWIND->SET_RWSID( exporting IF_RWSID = IS_ACTVY_RWIND_VIS-RWSID
                                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Winding Direction
    if ME->S_ACTVY_RWIND_VIS-RWDIR ne IS_ACTVY_RWIND_VIS-RWDIR.
      ME->R_PR_ACTVY_RWIND->SET_RWDIR( exporting IF_RWDIR = IS_ACTVY_RWIND_VIS-RWDIR
                                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Eyemark Position
    if ME->S_ACTVY_RWIND_VIS-EYEMP ne IS_ACTVY_RWIND_VIS-EYEMP.
      ME->R_PR_ACTVY_RWIND->SET_EYEMP( exporting IF_EYEMP = IS_ACTVY_RWIND_VIS-EYEMP
                                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    if ME->R_GRID_MC_CORE is bound.
      ME->R_GRID_MC_CORE->CHECK_CHANGED_DATA( ).
    endif.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_ACTVY_RWIND.

  endmethod.
  method INIT_VIS.

    free: ME->S_ACTVY_RWIND_VIS.

***--------------------------------------------------------------------------------------
*** Get Winding Side
    ME->R_PR_ACTVY_RWIND->GET_RWSID( importing EF_RWSID = ME->S_ACTVY_RWIND_VIS-RWSID ).

*** Get Winding Direction
    ME->R_PR_ACTVY_RWIND->GET_RWDIR( importing EF_RWDIR = ME->S_ACTVY_RWIND_VIS-RWDIR ).

*** Get Eyemark Position
    ME->R_PR_ACTVY_RWIND->GET_EYEMP( importing EF_EYEMP = ME->S_ACTVY_RWIND_VIS-EYEMP ).

  endmethod.
  method SET_CURSOR.

    data:  LF_CURSOR type SCRFNAME.

    get cursor field LF_CURSOR.
    if LF_CURSOR is not initial.
      move LF_CURSOR to ME->F_CURSOR.
    endif.

  endmethod.
