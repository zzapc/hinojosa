
class ZCL_VCXI_PCSR_PR_RTESPV_MCH definition
  public
  inheriting from ZCL_VCXI_P0SR_PR_RTESPV_MCH
  create public .

public section.

  methods CREATE_BL_PRTE
    redefinition .
protected section.

  methods GET_MFMI
    redefinition .
private section.
endclass. "ZCL_VCXI_PCSR_PR_RTESPV_MCH definition
class ZCL_VCXI_PCSR_PR_RTESPV_MCH implementation.
method CREATE_BL_PRTE.

  data:  LR_RTE_PRTE  type ref to /VCXI/CL_VSMR_RTE_PRTE.

  if IR_RTE_PRTE is bound.
    move IR_RTE_PRTE to LR_RTE_PRTE.
  else.
    create object LR_RTE_PRTE.
  endif.

  create object RR_BL_PRTE
    type
      ZCL_VCXI_PCSR_BL_PRTE
    exporting
      IR_RTE_PRTE           = LR_RTE_PRTE
      IR_MFMI               = ME->R_MFMI
      IR_MFSI               = ME->R_MFSI
      IT_CSPLIT             = ME->T_CSPLIT
      IF_DATE               = ME->F_DATE
      IF_LIFNR_REQ          = ME->F_LIFNR_REQ.

endmethod.
method GET_MFMI.

  data:          LS_MFMI_P      type          ZVCXI_PCSR_S_MFMI_P,
                 LS_HULEN       type          ZVCXI_P0S_S_HULEN,
                 LS_HUWID       type          ZVCXI_P0S_S_HUWID,
                 LS_HUHEI       type          ZVCXI_P0S_S_HUHEI,
                 LS_PRICE       type          ZVCXI_XCS_S_PRICE,
                 LS_PRICP       type          ZVCXI_XCS_S_PRICP,
                 LS_PSQTY       type          ZVCXI_XCS_S_PSQTY.
  data:          LT_GATE        type          /VCXI/VKSR_TT_GATE.
  data:          LR_GATE        type ref to   /VCXI/CL_VKSR_GATE,
                 LR_PR_SPEC_MCH type ref to   ZCL_VCXI_PCSS_PR_SPEC_MCH,
                 LR_PR_PSCALE   type ref to   ZCL_VCXI_XCS_PR_PSCALE.
  field-symbols: <T_MFMI>       type standard table,
                 <S_PSCALE>     type          ZVCXI_XCSR_S_PSCALE.

***------------------------------------------------------------------------------------------------
*** Find Provider for Specification
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_P0SS_PR_SPEC_MCH=>C_IPRVT_P0_SPEC_MCH
                                   IF_INUSE = ABAP_TRUE ).
  check LT_GATE is not initial.

  read table LT_GATE into LR_GATE index 1.

  try.
      move LR_GATE->R_CLSPR ?to LR_PR_SPEC_MCH.
    catch CX_SY_MOVE_CAST_ERROR.
      clear: LR_PR_SPEC_MCH.
  endtry.
  check LR_PR_SPEC_MCH is bound.

*** Create Data Reference
  create data RR_MFMI type table of ZVCXI_PCSR_S_MFMI_P.
  assign RR_MFMI->* to <T_MFMI>.

  move ME->F_MFMI_MATNR             to LS_MFMI_P-MATNR.
  move ABAP_TRUE                    to LS_MFMI_P-PURCH.
  move ABAP_TRUE                    to LS_MFMI_P-SANKA.

  LS_HULEN = LR_PR_SPEC_MCH->GET_HULEN( ).
  move LS_HULEN-HULEN               to LS_MFMI_P-PCLEN.
  move LS_HULEN-HULEN_UOM           to LS_MFMI_P-PCLEN_UOM.

  LS_HUWID = LR_PR_SPEC_MCH->GET_HUWID( ).
  move LS_HUWID-HUWID               to LS_MFMI_P-PCWID.
  move LS_HUWID-HUWID_UOM           to LS_MFMI_P-PCWID_UOM.

  LS_HUHEI = LR_PR_SPEC_MCH->GET_HUHEI( ).
  move LS_HUHEI-HUHEI               to LS_MFMI_P-PCHEI.
  move LS_HUHEI-HUHEI_UOM           to LS_MFMI_P-PCHEI_UOM.

*** BaseWeight will not be set because we ony have the weihgt including the Pallet

*** always 1 Up
  move 1                            to LS_MFMI_P-PCUPS.

  move 'A'                          to LS_MFMI_P-PCDIR.

  move 'ZVCXI_P0_PRODUCT'           to LS_MFMI_P-PCTYP.

  move LR_PR_SPEC_MCH->GET_FLUTE( ) to LS_MFMI_P-FLUTE.

*** Set always Consumption factor and per to 1
  move 1                            to LS_MFMI_P-MFICF.
  move 1                            to LS_MFMI_P-MFICP.

***------------------------------------------------------------------------------------------------
*** Find Provider for Price Scale
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCS_PR_PSCALE=>C_IPRVT_XCS_PSCALE
                                   IF_INUSE = ABAP_TRUE ).

  loop at LT_GATE into LR_GATE.
    try.
        move LR_GATE->R_CLSPR ?to LR_PR_PSCALE.
      catch CX_SY_MOVE_CAST_ERROR.
        clear: LR_PR_PSCALE.
    endtry.
    check LR_PR_PSCALE is bound.

    append initial line to LS_MFMI_P-T_PSCALE assigning <S_PSCALE>.

    move LR_GATE->F_ID to <S_PSCALE>-ISVCE.

    LS_PSQTY = LR_PR_PSCALE->GET_PSQTY( ).
    move LS_PSQTY-PSQTY     to <S_PSCALE>-PSQTY.
    move LS_PSQTY-PSQTY_UOM to <S_PSCALE>-PSQTY_UOM.

    LS_PRICE = LR_PR_PSCALE->GET_PRICE( ).
    move LS_PRICE-PRICE      to <S_PSCALE>-PRICE.
    move LS_PRICE-PRICE_CURR to <S_PSCALE>-PRICE_CURR.

    LS_PRICP = LR_PR_PSCALE->GET_PRICP( ).
    move LS_PRICP-PRICP     to <S_PSCALE>-PRICP.
    move LS_PRICP-PRICP_UOM to <S_PSCALE>-PRICP_UOM.

  endloop.

*** Append Material Flow Material to Data Reference
  append LS_MFMI_P to <T_MFMI>.

endmethod.
endclass. "ZCL_VCXI_PCSR_PR_RTESPV_MCH implementation
