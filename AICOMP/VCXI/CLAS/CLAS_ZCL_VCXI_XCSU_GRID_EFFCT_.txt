
class ZCL_VCXI_XCSU_GRID_EFFCT definition
  public
  inheriting from ZCL_VCXI_XCS_GRID
  create public .

public section.

  type-pools ABAP .
  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABNAME type TABNAME default 'ZVCXI_XCSU_S_EFFCT_GRID'
      !IF_CONTAINER_NAME type SCRFNAME
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL default ABAP_TRUE
    raising
      /VCXI/CX_CKX .
protected section.

  data T_PURCHASED type BSPTAGTAB .
  data F_PURCHASED type CHAR1 .

  methods GET_FIELDCAT
    redefinition .
  methods GET_GRID_DATA
    redefinition .
  methods HANDLE_DATA_CHANGED_FINISHED
    redefinition .
private section.
endclass. "ZCL_VCXI_XCSU_GRID_EFFCT definition
class ZCL_VCXI_XCSU_GRID_EFFCT implementation.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_GATE           = IR_GATE
                      IR_UIGATE         = IR_UIGATE
                      IF_TABNAME        = IF_TABNAME
                      IF_CONTAINER_NAME = IF_CONTAINER_NAME
                      IF_APPL_EVENTS    = IF_APPL_EVENTS ).

endmethod.
method GET_FIELDCAT.

  data:          LF_LOCKED       type        /VCXI/CKX_LOCKED,
                 LF_INUSE        type        /VCXI/VKSR_INUSE,
                 LF_FOVRR_EFFCT  type        ZVCXI_XCS_FOVRR.
  data:          LS_FCAT         type        LVC_S_FCAT.
  data:          LR_PR_PACKMAT   type ref to ZCL_VCXI_XCSU_PR_PACKMAT.
  field-symbols: <S_FCAT>        type        LVC_S_FCAT.

***----------------------------------------------------------------------------
*** Get Standard Field Catalog
  RT_FCAT = SUPER->GET_FIELDCAT( ).

*** Get Lock and In-Use Status
  LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
  LF_INUSE  = ME->R_GATE->IS_INUSE( ).

*** Get Packaging Material Reference
  try.
      move ME->R_GATE->R_CLSPR ?to LR_PR_PACKMAT.
    catch CX_SY_MOVE_CAST_ERROR.
      clear: LR_PR_PACKMAT.
  endtry.
  if LR_PR_PACKMAT                     is bound and
     LR_PR_PACKMAT->GET_FOVRR_EFFCT( ) eq ABAP_TRUE.
    move ABAP_TRUE to LF_FOVRR_EFFCT.
  endif.

***----------------------------------------------------------------------------
*** Adjust Field Catalog
  loop at RT_FCAT assigning <S_FCAT>.
    case <S_FCAT>-FIELDNAME.
      when 'DIMVA'.
        if LF_LOCKED      eq ABAP_TRUE and
           LF_INUSE       eq ABAP_TRUE and
           LF_FOVRR_EFFCT eq ABAP_TRUE.
          move ABAP_TRUE    to <S_FCAT>-EDIT.
        endif.

***   Overwrite Dimension
      when 'DIMEN'.
        move ABAP_TRUE to <S_FCAT>-NO_OUT.
      when 'DIMEN_DESCR'.
        read table RT_FCAT into LS_FCAT
                           with key FIELDNAME = 'DIMEN'.
        if SY-SUBRC eq 0.
          move LS_FCAT-COL_POS   to <S_FCAT>-COL_POS.
          move LS_FCAT-REPTEXT   to <S_FCAT>-REPTEXT.
          move LS_FCAT-SCRTEXT_L to <S_FCAT>-SCRTEXT_L.
          move LS_FCAT-SCRTEXT_M to <S_FCAT>-SCRTEXT_M.
          move LS_FCAT-SCRTEXT_S to <S_FCAT>-SCRTEXT_S.
        endif.

***   Overwrite Effect
      when 'EFFCT'.
        move ABAP_TRUE to <S_FCAT>-NO_OUT.
      when 'EFFCT_DESCR'.
        read table RT_FCAT into LS_FCAT
                           with key FIELDNAME = 'EFFCT'.
        if SY-SUBRC eq 0.
          move LS_FCAT-COL_POS   to <S_FCAT>-COL_POS.
          move LS_FCAT-REPTEXT   to <S_FCAT>-REPTEXT.
          move LS_FCAT-SCRTEXT_L to <S_FCAT>-SCRTEXT_L.
          move LS_FCAT-SCRTEXT_M to <S_FCAT>-SCRTEXT_M.
          move LS_FCAT-SCRTEXT_S to <S_FCAT>-SCRTEXT_S.
        endif.

    endcase.
  endloop.

endmethod.
method GET_GRID_DATA.

  data:          LS_EFFCT             type          ZVCXI_XCSU_S_EFFCT,
                 LS_EFFCT_GRID        type          ZVCXI_XCSU_S_EFFCT_GRID.
  data:          LT_EFFCT             type          ZVCXI_XCSU_TT_EFFCT.
  data:          LR_PR_PACKMAT        type ref to   ZCL_VCXI_XCSU_PR_PACKMAT,
                 LR_PR_CPM            type ref to   ZCL_VCXI_XCSU_PR_CPM.
  field-symbols: <T_DATA>             type standard table,
                 <S_DATA>             type          any.

*** Dereferencing of Data Reference
  assign IR_DATA->* to <T_DATA>.


***------------------------------------------------------------------------------------------------
*** First get Packaging Material Reference
  try.
      move ME->R_GATE->R_CLSPR ?to LR_PR_PACKMAT.
    catch CX_SY_MOVE_CAST_ERROR.
      clear: LR_PR_PACKMAT.
  endtry.
  if LR_PR_PACKMAT is bound.
*** Get Data from Packaging Material
    LR_PR_PACKMAT->GET_EFFCT( importing ET_EFFCT = LT_EFFCT ).
  else.

***------------------------------------------------------------------------------------------------
*** Second Packaging Material Reference
    try.
        move ME->R_GATE->R_CLSPR ?to LR_PR_CPM.
      catch CX_SY_MOVE_CAST_ERROR.
        clear: LR_PR_CPM.
    endtry.
    if LR_PR_CPM is bound.
***   Get Data from Configured Packaging Material
      LR_PR_CPM->GET_EFFCT( importing ET_EFFCT = LT_EFFCT ).
    endif.
  endif.

  check LT_EFFCT is not initial.

***------------------------------------------------------------------------------------------------
*** Add Entries
  loop at LT_EFFCT into LS_EFFCT.
    append initial line to <T_DATA> assigning <S_DATA>.

    move LS_EFFCT to LS_EFFCT_GRID-S_EFFCT.
    LS_EFFCT_GRID-DIMEN_DESCR = ZCL_VCXI_XCSU_CUST_SHIPU=>GET_DESCR_FOR_DIMEN( IF_DIMEN = LS_EFFCT-DIMEN ).
    LS_EFFCT_GRID-EFFCT_DESCR = ZCL_VCXI_XCSU_CUST_SHIPU=>GET_DESCR_FOR_EFFCT( IF_EFFCT = LS_EFFCT-EFFCT ).

    move-corresponding LS_EFFCT_GRID to <S_DATA>.
  endloop.

endmethod.
method HANDLE_DATA_CHANGED_FINISHED.

  data:          LF_ROW_ID            type          I,
                 LF_UPDATE            type          ABAP_BOOL.
  data:          LS_EFFCT             type          ZVCXI_XCSU_S_EFFCT.
  data:          LT_EFFCT             type          ZVCXI_XCSU_TT_EFFCT.
  data:          LR_PR_PACKMAT        type ref to   ZCL_VCXI_XCSU_PR_PACKMAT,
                 LR_CX_CKX            type ref to   /VCXI/CX_CKX.
  field-symbols: <T_DATA>             type standard table,
                 <S_DATA>             type          any.

  check E_MODIFIED eq ABAP_TRUE.

  try.

      SUPER->HANDLE_DATA_CHANGED_FINISHED( E_MODIFIED    = E_MODIFIED
                                           ET_GOOD_CELLS = ET_GOOD_CELLS ).

***   Dereferencing of Data Reference
      assign ME->R_DATA->* to <T_DATA>.

***   Get Packaging Material Reference
      try.
          move ME->R_GATE->R_CLSPR ?to LR_PR_PACKMAT.
        catch CX_SY_MOVE_CAST_ERROR.
          clear: LR_PR_PACKMAT.
      endtry.

      check LR_PR_PACKMAT is bound.

***   Get Data and set Update Flag
      loop at <T_DATA> assigning <S_DATA>.
        move SY-TABIX to LF_ROW_ID.

        move-corresponding <S_DATA> to LS_EFFCT.
        append LS_EFFCT to LT_EFFCT.

        read table ET_GOOD_CELLS with key ROW_ID = LF_ROW_ID
                                 transporting no fields.
        check SY-SUBRC eq 0.
        move ABAP_TRUE to LF_UPDATE.

      endloop.

***   Update Effect Data
      check LF_UPDATE eq ABAP_TRUE.

      LR_PR_PACKMAT->SET_EFFCT( IT_EFFCT = LT_EFFCT
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).


      ME->R_GATE->RAISE_EVENTS( ).

    catch /VCXI/CX_CKX into LR_CX_CKX.
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

*** Request Refresh for ALV
  move ABAP_TRUE to ME->F_REFRESH_REQ.

endmethod.
