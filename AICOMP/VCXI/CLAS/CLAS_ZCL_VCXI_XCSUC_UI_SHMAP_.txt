
class ZCL_VCXI_XCSUC_UI_SHMAP definition
  public
  create public .

public section.

  constants C_UC_CD type SYUCOMM value 'CD'. "#EC NOTEXT
  constants C_UC_TOGGLE type SYUCOMM value 'TOGGLE'. "#EC NOTEXT
  data F_CURSOR type SCRFNAME .
  data R_SHMAP type ref to ZCL_VCXI_XCSUC_SHMAP read-only .
  constants C_UC_CHECK type SYUCOMM value 'CHECK'. "#EC NOTEXT

  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods DO_USER_COMMAND
    changing
      !CF_UCOMM type SYUCOMM .
  methods SET_CURSOR .
  class-methods START_UI
    raising
      /VCXI/CX_CKX .
  methods CONSTRUCTOR
    raising
      /VCXI/CX_CKX .
protected section.

  data R_GRID_SHULI type ref to ZCL_VCXI_XCSUC_GRID_SHULI .
  constants C_UC_BACK type SYUCOMM value 'BACK'. "#EC NOTEXT
  constants C_UC_CANCEL type SYUCOMM value 'CANCEL'. "#EC NOTEXT
  constants C_UC_EXIT type SYUCOMM value 'EXIT'. "#EC NOTEXT
  constants C_UC_SAVE type SYUCOMM value 'SAVE'. "#EC NOTEXT
  data F_REFRESH_REQ type ABAP_BOOL .
  data R_GRID_PMATY type ref to ZCL_VCXI_XCSUC_GRID_PMATY .
  data R_GRID_SHUTY type ref to ZCL_VCXI_XCSUC_GRID_SHUTY .
  data R_MSG type ref to /VCXI/CL_CKXM_MESSAGE .

  methods DO_FREE
    raising
      /VCXI/CX_CKX .
  methods CHECK_FOR_CHANGES
    returning
      value(RF_CANCELED) type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_BACK
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_CANCEL
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_EXIT
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_SAVE
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_TOGGLE
    raising
      /VCXI/CX_CKX .
  methods SET_PFSTATUS
    raising
      /VCXI/CX_CKX .
  methods SET_TITLE
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_CD
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_CHECK
    raising
      /VCXI/CX_CKX .
private section.

  data S_DYNP type /VCXI/CKX_S_DYNP_KEY .
endclass. "ZCL_VCXI_XCSUC_UI_SHMAP definition
class ZCL_VCXI_XCSUC_UI_SHMAP implementation.
method CHECK_FOR_CHANGES.

  data:  LF_ANSWER          type C.

*** If Changes are allowed
  if ME->R_SHMAP->F_CHANGED eq ABAP_TRUE.
    call function 'POPUP_TO_CONFIRM'
      exporting
        TEXT_QUESTION  = 'Changed data will be lost. Do you want to save?'(Q01)
      importing
        ANSWER         = LF_ANSWER
      exceptions
        TEXT_NOT_FOUND = 1
        others         = 2.
    if SY-SUBRC ne 0.
      /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
    endif.

    case LF_ANSWER.
      when '1'.
***     Save and than go on
        ME->HANDLE_UC_SAVE( ).
        move ABAP_FALSE to RF_CANCELED.
      when '2'.
***     Do nothing but go on
        move ABAP_FALSE to RF_CANCELED.
      when 'A'.
***     Cancel
        move ABAP_TRUE to RF_CANCELED.
    endcase.
  endif.

endmethod.
method CONSTRUCTOR.

  data: LR_OBJECT type ref to /VCXI/IF_CKX_OBJECT.

***--------------------------------------------------------------------------------------
*** Check Authorization
  ZCL_VCXI_XCSUC_SHMAP=>CHECK_AUTH( IF_ACTVT = ZCL_VCXI_XCSUC_SHMAP=>C_ACTVT_DISPLAY ).

*** Bind UI Framework
  call function 'ZVCXI_XCSUC_UI_SHMAP_BIND'
    exporting
      IR_UI_SHMAP = ME
    importing
      RS_DYNP     = ME->S_DYNP.

*** Get Shipping Info Mapping Customizing
  LR_OBJECT = ZCL_VCXI_XCSUC_SHMAP=>/VCXI/IF_CKX_OBJECT~GET_INSTANCE( IF_GUID = SPACE ).

  try.
      move LR_OBJECT ?to   ME->R_SHMAP.
    catch CX_SY_MOVE_CAST_ERROR.
      clear: ME->R_SHMAP.
  endtry.

***----------------------------------------------------------------------------
  if ME->R_GRID_SHUTY is not bound.
*** Create Grid for Shipping Unit Type Mapping
    create object ME->R_GRID_SHUTY
      exporting
        IR_CUSTOMIZING = ME->R_SHMAP.
  endif.

***----------------------------------------------------------------------------
  if ME->R_GRID_SHULI is not bound.
*** Create Grid for Shipping Unit Limit Mapping
    create object ME->R_GRID_SHULI
      exporting
        IR_CUSTOMIZING = ME->R_SHMAP.
  endif.

***----------------------------------------------------------------------------
  if ME->R_GRID_PMATY is not bound.
*** Create Grid for Packaging Material Type Mapping
    create object ME->R_GRID_PMATY
      exporting
        IR_CUSTOMIZING = ME->R_SHMAP.
  endif.

***----------------------------------------------------------------------------
*** Create Message Object
  if ME->R_MSG is not bound.
    create object ME->R_MSG.
  endif.

endmethod.
method DO_FREE.

***------------------------------------------------------------------
*** Free Shipping Unit Type Grid
  if ME->R_GRID_SHUTY is bound.
    ME->R_GRID_SHUTY->DO_FREE( ).
    clear ME->R_GRID_SHUTY.
  endif.

***------------------------------------------------------------------
*** Free Shipping Unit Limit Grid
  if ME->R_GRID_SHULI is bound.
    ME->R_GRID_SHULI->DO_FREE( ).
    clear ME->R_GRID_SHULI.
  endif.

***------------------------------------------------------------------
*** Free Packaging Material Type Grid
  if ME->R_GRID_PMATY is bound.
    ME->R_GRID_PMATY->DO_FREE( ).
    clear ME->R_GRID_PMATY.
  endif.

*** Free Shipping Mapping Customizing
  ME->R_SHMAP->/VCXI/IF_CKX_OBJECT_ACTION~HANDLE_ACTION( IF_FCODE = ZCL_VCXI_XCSUC_SHMAP=>C_FCODE_SHMAP_FREE ).

endmethod.
method DO_PBO.

  data: LR_CX_CKX type ref to /VCXI/CX_CKX.

  try.

      ME->SET_PFSTATUS( ).
      ME->SET_TITLE( ).

***   Refresh Grid
      if ME->R_GRID_SHUTY is bound.
        if ME->F_REFRESH_REQ eq ABAP_TRUE.
          move ABAP_TRUE to ME->R_GRID_SHUTY->F_REFRESH_ALL_REQ.
          move ABAP_TRUE to ME->R_GRID_SHUTY->F_REFRESH_REQ.
        endif.
        ME->R_GRID_SHUTY->REFRESH_GRID( ).
      endif.

      if ME->R_GRID_SHULI is bound.
        if ME->F_REFRESH_REQ eq ABAP_TRUE.
          move ABAP_TRUE to ME->R_GRID_SHULI->F_REFRESH_ALL_REQ.
          move ABAP_TRUE to ME->R_GRID_SHULI->F_REFRESH_REQ.
        endif.
        ME->R_GRID_SHULI->REFRESH_GRID( ).
      endif.

      if ME->R_GRID_PMATY is bound.
        if ME->F_REFRESH_REQ eq ABAP_TRUE.
          move ABAP_TRUE to ME->R_GRID_PMATY->F_REFRESH_ALL_REQ.
          move ABAP_TRUE to ME->R_GRID_PMATY->F_REFRESH_REQ.
        endif.
        ME->R_GRID_PMATY->REFRESH_GRID( ).
      endif.

      move ABAP_FALSE to ME->F_REFRESH_REQ.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      LR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

endmethod.
method DO_USER_COMMAND .

  data:  LR_CX_CKX type ref to /VCXI/CX_CKX.

  try.

      if ME->R_GRID_PMATY is bound.
        ME->R_GRID_PMATY->CHECK_CHANGED_DATA( ).
      endif.

      if ME->R_GRID_SHULI is bound.
        ME->R_GRID_SHULI->CHECK_CHANGED_DATA( ).
      endif.

      if ME->R_GRID_SHUTY is bound.
        ME->R_GRID_SHUTY->CHECK_CHANGED_DATA( ).
      endif.

      case CF_UCOMM.
        when ZCL_VCXI_XCSUC_UI_SHMAP=>C_UC_BACK.
          ME->HANDLE_UC_BACK( ).
        when ZCL_VCXI_XCSUC_UI_SHMAP=>C_UC_EXIT.
          ME->HANDLE_UC_EXIT( ).
        when ZCL_VCXI_XCSUC_UI_SHMAP=>C_UC_CANCEL.
          ME->HANDLE_UC_CANCEL( ).
        when ZCL_VCXI_XCSUC_UI_SHMAP=>C_UC_SAVE.
          ME->HANDLE_UC_SAVE( ).
        when ZCL_VCXI_XCSUC_UI_SHMAP=>C_UC_TOGGLE.
          ME->HANDLE_UC_TOGGLE( ).
        when ZCL_VCXI_XCSUC_UI_SHMAP=>C_UC_CD.
          ME->HANDLE_UC_CD( ).
        when ZCL_VCXI_XCSUC_UI_SHMAP=>C_UC_CHECK.
          ME->HANDLE_UC_CHECK( ).
      endcase.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      LR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

  clear CF_UCOMM.

endmethod.
method HANDLE_UC_BACK .

*** Check for changes and ask user to save them
  if ME->CHECK_FOR_CHANGES( ) ne ABAP_TRUE.
    ME->DO_FREE( ).
    leave to screen 0.
  endif.

endmethod.
method HANDLE_UC_CANCEL .

*** Check for changes and ask user to save them
  if ME->CHECK_FOR_CHANGES( ) ne ABAP_TRUE.
    ME->DO_FREE( ).
    leave to screen 0.
  endif.

endmethod.
method HANDLE_UC_CD.

  data:  LF_CDOCL                type          CDHDR-OBJECTCLAS,
         LF_CDOID                type          CDHDR-OBJECTID,
         LF_APPLICATIONID        type          REPID,
         LF_MSGV2                type          SYMSGV.
  data:  LT_CDRED_STR            type          CDRED_STR_TAB,
         LT_EDITPOS_WITH_HEADER  type table of CDRED.
  data:  LR_OBJECT_CD            type ref to   /VCXI/IF_CKX_OBJECT_CD.

***----------------------------------------------------------------------------
*** Get Change Document Info
  try.
      move ME->R_SHMAP ?to LR_OBJECT_CD.
      check LR_OBJECT_CD is bound.
    catch CX_SY_MOVE_CAST_ERROR.
***   No change document positions found
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'I'
          F_MSGID = 'CD'
          F_MSGNO = '649'
          F_MSGV1 = 'SY-SUBRC'
          F_MSGV2 = LF_MSGV2.
  endtry.

  LR_OBJECT_CD->GET_INFO( importing EF_CDOCL = LF_CDOCL
                                    EF_CDOID = LF_CDOID ).


***----------------------------------------------------------------------------
*** Read Change Documents
  call function 'CHANGEDOCUMENT_READ'
    exporting
      OBJECTCLASS       = LF_CDOCL
      OBJECTID          = LF_CDOID
    importing
      ET_CDRED_STR      = LT_CDRED_STR[]
    tables
      EDITPOS           = LT_EDITPOS_WITH_HEADER
    exceptions
      NO_POSITION_FOUND = 1
      others            = 2.
  case SY-SUBRC.
    when 0.
***   Display the Records
      LF_APPLICATIONID = LF_CDOCL.
      call function 'CHANGEDOCUMENT_DISPLAY'
        exporting
          I_APPLICATIONID  = LF_APPLICATIONID
          FLG_AUTOCONDENSE = 'X'
          I_OBJECTCLAS     = LF_CDOCL
          IT_CDRED_STR     = LT_CDRED_STR[]
        tables
          I_CDRED          = LT_EDITPOS_WITH_HEADER.
    when 1.
***   No change document positions found
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'I'
          F_MSGID = 'CD'
          F_MSGNO = '649'
          F_MSGV1 = 'SY-SUBRC'
          F_MSGV2 = LF_MSGV2.
    when 2.
***   Error in change document creation/analysis: & &
      move SY-SUBRC to LF_MSGV2.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'I'
          F_MSGID = 'CD'
          F_MSGNO = '327'
          F_MSGV1 = 'SY-SUBRC'
          F_MSGV2 = LF_MSGV2.

  endcase.

endmethod.
method HANDLE_UC_CHECK.

*** Call Check of Packaging Code
  ME->R_SHMAP->DO_CHECK( IR_MSG = ME->R_MSG ).

  if ME->R_MSG->T_MSG is not initial.
    ME->R_MSG->DISPLAY_MSG( ).
    ME->R_MSG->INIT_MSG( ).
  endif.

endmethod.
method HANDLE_UC_EXIT .

*** Check for changes and ask user to save them
  if ME->CHECK_FOR_CHANGES( ) ne ABAP_TRUE.
    ME->DO_FREE( ).
    leave to screen 0.
  endif.

endmethod.
method HANDLE_UC_SAVE.

  data:  LR_OBJECT_ACTION type ref to /VCXI/IF_CKX_OBJECT_ACTION.

*** First do Check
  ME->HANDLE_UC_CHECK( ).

  try.

***   Save
      move ME->R_SHMAP ?to LR_OBJECT_ACTION.
      check LR_OBJECT_ACTION is bound.

      LR_OBJECT_ACTION->HANDLE_ACTION( IF_FCODE = ZCL_VCXI_XCSUC_SHMAP=>C_FCODE_SHMAP_SAVE ).

***   Request Screen Refresh
      ME->F_REFRESH_REQ = ABAP_TRUE.

    catch CX_SY_MOVE_CAST_ERROR.
      exit.
  endtry.

endmethod.
method HANDLE_UC_TOGGLE.

  data:  LR_OBJECT_LOCKING type ref to /VCXI/IF_CKX_OBJECT_LOCKING.

***------------------------------------------------------------------
*** Only works if object is open
  check ME->R_SHMAP is bound.
  move ME->R_SHMAP ?to LR_OBJECT_LOCKING.

  if LR_OBJECT_LOCKING->IS_LOCKED( ) eq ABAP_TRUE.
*** Check for changes and ask user to save them
    check ME->CHECK_FOR_CHANGES( ) ne ABAP_TRUE.

    LR_OBJECT_LOCKING->DO_UNLOCK( ).

  else.
    LR_OBJECT_LOCKING->DO_LOCK( ).
  endif.

*** Request Screen Refresh
  ME->F_REFRESH_REQ = ABAP_TRUE.

endmethod.
method SET_CURSOR.

  data:  LF_CURSOR type SCRFNAME.

  get cursor field LF_CURSOR.
  if LF_CURSOR is not initial.
    move LF_CURSOR to ME->F_CURSOR.
  endif.

endmethod.
method SET_PFSTATUS.

  data:  LT_EXCLUDE type standard table of SYUCOMM.

***----------------------------------------------------------------------------
*** Exclude SAVE and ADD_* - When not Locked
  if ME->R_SHMAP->F_CHANGED ne ABAP_TRUE.
    append ZCL_VCXI_XCSUC_UI_SHMAP=>C_UC_SAVE      to LT_EXCLUDE.
  endif.

***----------------------------------------------------------------------------
*** Set PF-Status
  set pf-status '0100' of program ME->S_DYNP-REPID excluding LT_EXCLUDE.

endmethod.
method SET_TITLE.

  data:  LR_OBJECT_LOCKING type ref to /VCXI/IF_CKX_OBJECT_LOCKING.

  if ME->R_SHMAP is bound.
    move ME->R_SHMAP ?to LR_OBJECT_LOCKING.

    if LR_OBJECT_LOCKING->IS_LOCKED( ) eq ABAP_TRUE.
      set titlebar '0100_EDIT' of program ME->S_DYNP-REPID.
    else.
      set titlebar '0100_DISP' of program ME->S_DYNP-REPID.
    endif.
  else.
    set titlebar '0100' of program ME->S_DYNP-REPID.
  endif.

endmethod.
method START_UI.

  data:  LR_UI_SHMAP type ref to ZCL_VCXI_XCSUC_UI_SHMAP.

  BREAK ADD_AUTHCHECK_KFISCHER.
**** Check Authorization
*  ZCL_VCXI_XCDC_DVCCOND=>CHECK_AUTH( IF_ACTVT = ZCL_VCXI_XCDC_DVCCOND=>C_ACTVT_DISPLAY ).

*** Create Instance of UI
  create object LR_UI_SHMAP.

*** Call Screen
  call function 'ZVCXI_XCSUC_UI_SHMAP_START'.

endmethod.
