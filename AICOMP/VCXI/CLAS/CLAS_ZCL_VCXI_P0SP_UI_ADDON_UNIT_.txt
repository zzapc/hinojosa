
class ZCL_VCXI_P0SP_UI_ADDON_UNIT definition
  public
  inheriting from ZCL_VCXI_XCSP_UI_ADDON
  create public .

public section.

  data S_ADDON_UNIT_VIS type ZVCXI_P0SP_S_ADDON_UNIT_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  data R_GRID_MC_PACKMAT type ref to ZCL_VCXI_XCSP_GRID_MC_PACKMAT .
  data R_PR_ADDON_UNIT type ref to ZCL_VCXI_P0SP_PR_ADDON_UNIT .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_P0SP_UI_ADDON_UNIT definition
class ZCL_VCXI_P0SP_UI_ADDON_UNIT implementation.
method ACTIVATE.

  SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
  call function 'ZVCXI_P0SP_UI_ADDON_UNIT_BIND'
    exporting
      IR_UI_ADDON_UNIT = ME
    importing
      ES_DYNP          = ME->S_DYNP.

*** Create Grid for GRID_MC_Packaging Material
  create object ME->R_GRID_MC_PACKMAT
    exporting
      IR_GATE           = ME->R_GATE
      IR_UIGATE         = ME->R_UIGATE
      IF_CONTAINER_NAME = '0100CC_GRID_MC_PACKMAT'.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                      IR_UIGATE = IR_UIGATE
                      IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
  try.
      move IR_GATE->R_CLSPR ?to ME->R_PR_ADDON_UNIT.
    catch CX_SY_MOVE_CAST_ERROR.
***   UI ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '801'.
  endtry.

endmethod.
method DEACTIVATE.

  SUPER->DEACTIVATE( ).

  if ME->R_GRID_MC_PACKMAT is bound.
    ME->R_GRID_MC_PACKMAT->DO_FREE( ).
    clear ME->R_GRID_MC_PACKMAT.
  endif.

endmethod.
method DO_MODIFY_SCREEN.

  data:  LF_LOCKED type        /VCXI/CKX_LOCKED,
         LF_INUSE  type        /VCXI/VKSR_INUSE.
  data:  LR_CX_CKX type ref to /VCXI/CX_CKX.

  SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                           IF_DYNNR = IF_DYNNR ).

  try.

      if IF_REPID eq ME->S_DYNP-REPID and
         IF_DYNNR eq ME->S_DYNP-DYNNR.

        LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
        LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***     Modify Scree using DynamicVC
        ZCL_VCXI_XCDR_SERVICE=>MODIFY_SCREEN( IR_GATE = ME->R_GATE ).

        loop at screen.
          if LF_LOCKED ne ABAP_TRUE or
             LF_INUSE  ne ABAP_TRUE.
            move 0 to SCREEN-INPUT.
          endif.
          modify screen.
        endloop.
      endif.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Super PBO
    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize Grid
    if ME->R_GRID_MC_PACKMAT is bound.
      ME->R_GRID_MC_PACKMAT->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_MC_PACKMAT->REFRESH_GRID( ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_ADDON_UNIT_VIS type ZVCXI_P0SP_S_ADDON_UNIT_VIS.


*** Super
    SUPER->DO_UPDATE_VIS( IS_VIS = IS_VIS ).
    check ME->R_PR_ADDON_UNIT is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_ADDON_UNIT_VIS.

***-------------------------------------------------------------------------------------
*** Set PC Length
    if ME->S_ADDON_UNIT_VIS-S_PCLEN ne LS_ADDON_UNIT_VIS-S_PCLEN.
      ME->R_PR_ADDON_UNIT->SET_PCLEN( IS_PCLEN = LS_ADDON_UNIT_VIS-S_PCLEN
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set PC Width
    if ME->S_ADDON_UNIT_VIS-S_PCWID ne LS_ADDON_UNIT_VIS-S_PCWID.
      ME->R_PR_ADDON_UNIT->SET_PCWID( IS_PCWID = LS_ADDON_UNIT_VIS-S_PCWID
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set PC Height
    if ME->S_ADDON_UNIT_VIS-S_PCHEI ne LS_ADDON_UNIT_VIS-S_PCHEI.
      ME->R_PR_ADDON_UNIT->SET_PCHEI( IS_PCHEI = LS_ADDON_UNIT_VIS-S_PCHEI
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set PC Ups
    if ME->S_ADDON_UNIT_VIS-PCUPS ne LS_ADDON_UNIT_VIS-PCUPS.
      ME->R_PR_ADDON_UNIT->SET_PCUPS( IF_PCUPS = LS_ADDON_UNIT_VIS-PCUPS
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Shipping Unit
    if ME->S_ADDON_UNIT_VIS-SHUNT ne LS_ADDON_UNIT_VIS-SHUNT.
      ME->R_PR_ADDON_UNIT->SET_SHUNT( IF_SHUNT = LS_ADDON_UNIT_VIS-SHUNT
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Previous Quantity
    if ME->S_ADDON_UNIT_VIS-PRQTY ne LS_ADDON_UNIT_VIS-PRQTY.
      ME->R_PR_ADDON_UNIT->SET_PRQTY( IS_PRQTY = LS_ADDON_UNIT_VIS-S_PRQTY
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set PC Type
    if ME->S_ADDON_UNIT_VIS-PCTYP ne LS_ADDON_UNIT_VIS-PCTYP.
      ME->R_PR_ADDON_UNIT->SET_PCTYP( IF_PCTYP = LS_ADDON_UNIT_VIS-PCTYP
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER  ).
    endif.

  endmethod.
method DO_USER_COMMAND.

  SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

  if ME->R_GRID_MC_PACKMAT is bound.
    ME->R_GRID_MC_PACKMAT->CHECK_CHANGED_DATA( ).
  endif.

endmethod.
method HANDLE_GATE_FREED.

  SUPER->HANDLE_GATE_FREED( ).

  free: ME->R_PR_ADDON_UNIT.

endmethod.
  method INIT_VIS.

*** Super
    SUPER->INIT_VIS( ).

    free: ME->S_ADDON_UNIT_VIS.
    ME->S_ADDON_UNIT_VIS-S_ADDON_VIS = ME->S_ADDON_VIS.

***--------------------------------------------------------------------------------------
*** PC Dimension
    ME->S_ADDON_UNIT_VIS-S_PCLEN = ME->R_PR_ADDON_UNIT->GET_PCLEN( ).
    ME->S_ADDON_UNIT_VIS-S_PCWID = ME->R_PR_ADDON_UNIT->GET_PCWID( ).
    ME->S_ADDON_UNIT_VIS-S_PCHEI = ME->R_PR_ADDON_UNIT->GET_PCHEI( ).
    ME->S_ADDON_UNIT_VIS-PCUPS = ME->R_PR_ADDON_UNIT->GET_PCUPS( ).

*** Get PC Type and Description
    ME->S_ADDON_UNIT_VIS-PCTYP       = ME->R_PR_ADDON_UNIT->GET_PCTYP( ).
    ME->S_ADDON_UNIT_VIS-PCTYP_DESCR = ME->R_PR_ADDON_UNIT->GET_PCTYP_DESCR( ).

***----------------------------------------------------------------------------
*** Get Shipping Unit and Description
    ME->S_ADDON_UNIT_VIS-SHUNT       = ME->R_PR_ADDON_UNIT->GET_SHUNT( ).
    ME->S_ADDON_UNIT_VIS-SHUNT_DESCR = ME->R_PR_ADDON_UNIT->GET_SHUNT_DESCR( ).

***----------------------------------------------------------------------------
*** Get Previous Quantity
    ME->S_ADDON_UNIT_VIS-S_PRQTY = ME->R_PR_ADDON_UNIT->GET_PRQTY( ).

  endmethod.
