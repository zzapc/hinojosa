
class ZCL_VCXI_PFSS_EA_SPEC definition
  public
  inheriting from ZCL_VCXI_P0SS_EA_SPEC
  create public .

public section.

  methods GET_SBINFO
    exporting
      !EF_PAPGR type ZVCXI_PFSS_PAPGR
      !ES_GRAMM type ZVCXI_P0SS_S_GRAMM
      !ES_CALIP type ZVCXI_P0SS_S_CALIP
      !ES_AGGRA type ZVCXI_PFSS_S_AGGRA
      !ES_INGRA type ZVCXI_PFSS_S_INGRA
      !EF_GRDIR type ZVCXI_PFSS_GRDIR
    raising
      /VCXI/CX_CKX .
  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
protected section.

  data R_PR_PF_SPEC type ref to ZCL_VCXI_PFSS_PR_SPEC .
private section.
endclass. "ZCL_VCXI_PFSS_EA_SPEC definition
class ZCL_VCXI_PFSS_EA_SPEC implementation.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_GATE = IR_GATE ).

*** Assign the PR Class to the Element API Class
  try.
      move ME->R_GATE->R_CLSPR ?to ME->R_PR_PF_SPEC.
    catch CX_SY_MOVE_CAST_ERROR.
***   Element API ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '804'.
  endtry.

endmethod.
method GET_SBINFO.

  data: LS_PROLE type ZVCXI_P0S_S_PROLE,
        LS_PROWI type ZVCXI_P0S_S_PROWI.

  check ME->R_PR_PF_SPEC is bound.

***--------------------------------------------------------------------------------------
*** Paper Grade
  if EF_PAPGR is requested.
    move ME->R_PR_PF_SPEC->GET_PAPGR( ) to EF_PAPGR.
  endif.

*** Grammage
  if ES_GRAMM is requested.
    move ME->R_PR_PF_SPEC->GET_GRAMM( ) to ES_GRAMM.
  endif.

*** Caliper
  if ES_CALIP is requested.
    move ME->R_PR_PF_SPEC->GET_CALIP( IF_POOTI = '*' ) to ES_CALIP.
  endif.

***--------------------------------------------------------------------------------------
*** Against Grain
  if ES_AGGRA is requested.
    move ME->R_PR_PF_SPEC->GET_PROWI( ) to LS_PROWI.
    move LS_PROWI-PROWI     to ES_AGGRA-AGGRA.
    move LS_PROWI-PROWI_UOM to ES_AGGRA-AGGRA_UOM.
  endif.

*** In Grain
  if ES_INGRA is requested.
    move ME->R_PR_PF_SPEC->GET_PROLE( ) to LS_PROLE.
    move LS_PROLE-PROLE     to ES_INGRA-INGRA.
    move LS_PROLE-PROLE_UOM to ES_INGRA-INGRA_UOM.
  endif.

*** Grain Direction
  if EF_GRDIR is requested.
    move ME->R_PR_PF_SPEC->GET_GRDIR( ) to EF_GRDIR.
  endif.

endmethod.
