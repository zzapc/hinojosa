
class ZCL_VCXI_PCSS_CUST_PRPRP definition
  public
  inheriting from ZCL_VCXI_XCS_CUST_MATNR
  create protected .

public section.

  data F_CUPST type ZVCXI_PCS_CUPST read-only .
  data F_NOCOL type ZVCXI_P0S_NOCOL read-only .
  data F_TPRPR type ZVCXI_PCS_TPRPR read-only .
  data S_BAWGP type ZVCXI_PCS_S_BAWGP read-only .
  data S_BAWGT type ZVCXI_PCS_S_BAWGT read-only .
  data S_CALIP type ZVCXI_P0SS_S_CALIP read-only .
  data S_CYLIN type ZVCXI_PCS_S_CYLIN read-only .
  data F_CYLBA type ZVCXI_PCS_CYLBA read-only .
  data S_PAWID type ZVCXI_PCS_S_PAWID read-only .

  methods CONSTRUCTOR
    importing
      !IF_MATNR type MATNR
      !IF_WERKS type WERKS_D
      !IF_DATE type DATS
    raising
      /VCXI/CX_CKX .
  class-methods GET_INSTANCE_PRPRP
    importing
      !IF_MATNR type MATNR
      !IF_WERKS type WERKS_D
      !IF_DATE type DATS optional
    returning
      value(RR_INSTANCE) type ref to ZCL_VCXI_PCSS_CUST_PRPRP
    raising
      /VCXI/CX_CKX .
protected section.

  constants C_SETID_PCS_PRPRP type /VCXI/CKXC_SETID value 'ZVCXI_PCS_PRPRP'. "#EC NOTEXT
private section.
endclass. "ZCL_VCXI_PCSS_CUST_PRPRP definition
class ZCL_VCXI_PCSS_CUST_PRPRP implementation.
method CONSTRUCTOR.

  data:  LF_OBJNUM    type        OBJNUM.
  data:  LS_SETVA     type        /VCXI/CKXC_S_SETVA,
         LS_MAPVC     type        /VCXI/CKXC_S_MAPVC,
         LS_MAPVN     type        /VCXI/CKXC_S_MAPVN.
  data:  LT_SETVA     type        /VCXI/CKXC_TT_SETVA.

  try.
      SUPER->CONSTRUCTOR( IF_MATNR = IF_MATNR
                          IF_WERKS = IF_WERKS
                          IF_DATE  = IF_DATE ).

***   Get Preprint Paper Info
      move ME->F_MATNR to LF_OBJNUM.
      LT_SETVA = /VCXI/CL_CKXC_CCM=>GET_SETVA( IF_SETID  = C_SETID_PCS_PRPRP
                                               IF_OBJNUM = LF_OBJNUM ).

      loop at LT_SETVA into LS_SETVA.
        clear: LS_MAPVC,
               LS_MAPVN.

        case LS_SETVA-MAPID.
          when 'BAWGT'.
            read table LS_SETVA-T_MAPVN into LS_MAPVN index 1.
            move LS_MAPVN-ATFLV     to ME->S_BAWGT-BAWGT.
            move LS_MAPVN-ATFLV_UOM to ME->S_BAWGT-BAWGT_UOM.

          when 'BAWGP'.
            read table LS_SETVA-T_MAPVN into LS_MAPVN index 1.
            move LS_MAPVN-ATFLV     to ME->S_BAWGP-BAWGP.
            move LS_MAPVN-ATFLV_UOM to ME->S_BAWGP-BAWGP_UOM.

          when 'CALIP'.
            read table LS_SETVA-T_MAPVN into LS_MAPVN index 1.
            move LS_MAPVN-ATFLV     to ME->S_CALIP-CALIP.
            move LS_MAPVN-ATFLV_UOM to ME->S_CALIP-CALIP_UOM.

          when 'TPRPR'.
            read table LS_SETVA-T_MAPVC into LS_MAPVC index 1.
            move LS_MAPVC-VAMAP     to ME->F_TPRPR.

          when 'CYLIN'.
            read table LS_SETVA-T_MAPVN into LS_MAPVN index 1.
            move LS_MAPVN-ATFLV     to ME->S_CYLIN-CYLIN.
            move LS_MAPVN-ATFLV_UOM to ME->S_CYLIN-CYLIN_UOM.

          when 'CUPST'.
            read table LS_SETVA-T_MAPVN into LS_MAPVN index 1.
            move LS_MAPVN-ATFLV     to ME->F_CUPST.

          when 'NOCOL'.
            read table LS_SETVA-T_MAPVN into LS_MAPVN index 1.
            move LS_MAPVN-ATFLV     to ME->F_NOCOL.

          when 'PAWID'.
            read table LS_SETVA-T_MAPVN into LS_MAPVN index 1.
            move LS_MAPVN-ATFLV     to ME->S_PAWID-PAWID.
            move LS_MAPVN-ATFLV_UOM to ME->S_PAWID-PAWID_UOM.

        endcase.
      endloop.

***   Get Flag if assigned PreprintType is Cylinder based
      ME->F_CYLBA = ZCL_VCXI_PCSS_CUST_BOARD=>IS_TPRPR_CYLBA( IF_TPRPR = ME->F_TPRPR ).

    cleanup.
      ME->DO_FREE( ).
  endtry.

endmethod.
method GET_INSTANCE_PRPRP.

  data:  LS_INSTANCE type YS_INSTANCE.

  read table ZCL_VCXI_XCS_CUST_MATNR=>T_INSTANCE
             into LS_INSTANCE
             with key MATNR = IF_MATNR
                      WERKS = IF_WERKS
                      DATE  = IF_DATE.

  if SY-SUBRC eq 0.
    try.
        move LS_INSTANCE-R_INSTANCE ?to RR_INSTANCE.
      catch CX_SY_MOVE_CAST_ERROR.
        delete ZCL_VCXI_XCS_CUST_MATNR=>T_INSTANCE where R_INSTANCE eq LS_INSTANCE-R_INSTANCE.
        clear RR_INSTANCE.
    endtry.
  endif.

  if RR_INSTANCE is initial.
    try.
        create object RR_INSTANCE
          exporting
            IF_MATNR = IF_MATNR
            IF_WERKS = IF_WERKS
            IF_DATE  = IF_DATE.
      cleanup.
        clear RR_INSTANCE.
    endtry.
  endif.

endmethod.
