
class ZCL_VCXI_PCSS_GRID_MS_BOARD definition
  public
  inheriting from ZCL_VCXI_XCS_GRID
  create public .

public section.

  type-pools ABAP .
  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_CONTAINER_NAME type SCRFNAME
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL default ABAP_TRUE
    raising
      /VCXI/CX_CKX .
protected section.

  constants C_FIELDNAME_RGATE type FIELDNAME value 'R_GATE' ##NO_TEXT.
  constants C_INFO_FNAME type LVC_CIFNM value 'ROWCOLOR' ##NO_TEXT.
  constants C_ROWCOLOR_INACTIVE type CHAR04 value 'C211' ##NO_TEXT.
  data F_SUBPA_IS_ACTIV type ABAP_BOOL .
  data R_PR_BOARD type ref to ZCL_VCXI_PCSS_PR_BOARD .

  methods CHANGE_STYLE_PAPER
    importing
      !IF_SUBPA type ZVCXI_PCSS_SUBPA
      !IF_PREPRINT type ABAP_BOOL
      !IF_PURCH type ZVCXI_XCS_PURCH
      !IF_DUMMY type ABAP_BOOL
    changing
      !CT_STYL type LVC_T_STYL
    raising
      /VCXI/CX_CKX .
  methods CHANGE_STYLE_STARCH
    importing
      !IF_PURCH type ZVCXI_XCS_PURCH
    changing
      !CT_STYL type LVC_T_STYL
    raising
      /VCXI/CX_CKX .
  methods CHANGE_STYLE_WAX
    importing
      !IF_PURCH type ZVCXI_XCS_PURCH
    changing
      !CT_STYL type LVC_T_STYL
    raising
      /VCXI/CX_CKX .
  methods HANDLE_HOTSPOT_CLICK
    for event HOTSPOT_CLICK of CL_GUI_ALV_GRID
    importing
      !E_ROW_ID
      !E_COLUMN_ID
      !ES_ROW_NO .
  methods HANDLE_DOUBLE_CLICK
    for event DOUBLE_CLICK of CL_GUI_ALV_GRID
    importing
      !E_ROW
      !E_COLUMN
      !ES_ROW_NO .
  methods GET_MS_WAX_VIS
    importing
      !IR_PR_MS_WAX type ref to ZCL_VCXI_PCSS_PR_MS_WAX
    returning
      value(RS_MS_WAX_VIS) type ZVCXI_PCSS_S_MS_BOARD_VIS
    raising
      /VCXI/CX_CKX .
  methods GET_MS_STARCH_VIS
    importing
      !IR_PR_MS_STARCH type ref to ZCL_VCXI_PCSS_PR_MS_STARCH
    returning
      value(RS_MS_STARCH_VIS) type ZVCXI_PCSS_S_MS_BOARD_VIS
    raising
      /VCXI/CX_CKX .
  methods GET_MS_PAPER_VIS
    importing
      !IR_PR_MS_PAPER type ref to ZCL_VCXI_PCSS_PR_MS_PAPER
    returning
      value(RS_MS_PAPER_VIS) type ZVCXI_PCSS_S_MS_BOARD_VIS
    raising
      /VCXI/CX_CKX .
  methods SET_MS_PAPER_VIS
    importing
      !IR_PR_MS_PAPER type ref to ZCL_VCXI_PCSS_PR_MS_PAPER
      !IS_MS_PAPER_VIS type ZVCXI_PCSS_S_MS_BOARD_VIS
    raising
      /VCXI/CX_CKX .
  methods SET_MS_STARCH_VIS
    importing
      !IR_PR_MS_STARCH type ref to ZCL_VCXI_PCSS_PR_MS_STARCH
      !IS_MS_STARCH_VIS type ZVCXI_PCSS_S_MS_BOARD_VIS
    raising
      /VCXI/CX_CKX .
  methods SET_MS_WAX_VIS
    importing
      !IR_PR_MS_WAX type ref to ZCL_VCXI_PCSS_PR_MS_WAX
      !IS_MS_WAX_VIS type ZVCXI_PCSS_S_MS_BOARD_VIS
    raising
      /VCXI/CX_CKX .

  methods GET_FIELDCAT
    redefinition .
  methods GET_GRID_DATA
    redefinition .
  methods GET_LAYOUT
    redefinition .
  methods HANDLE_DATA_CHANGED_FINISHED
    redefinition .
  methods SET_EVENT
    redefinition .
private section.
endclass. "ZCL_VCXI_PCSS_GRID_MS_BOARD definition
class ZCL_VCXI_PCSS_GRID_MS_BOARD implementation.
method CHANGE_STYLE_PAPER.

  data: LF_STYLE_ENABLED       type LVC_STYLE,
        LF_STYLE_ENABLED_SUBPA type LVC_STYLE.

  free: CT_STYL.

*** --------------------------------------------------------------------------------------------
*** Set Style Details -> only Editable if Substitute Paper is activ
  if IF_SUBPA            eq ABAP_TRUE and
     ME->F_STYLE_ENABLED eq CL_GUI_ALV_GRID=>MC_STYLE_ENABLED.
    move ME->F_STYLE_ENABLED to LF_STYLE_ENABLED.
  elseif IF_SUBPA eq ABAP_FALSE.
    move ME->F_STYLE_DISABLED to LF_STYLE_ENABLED.
  endif.

  if ME->F_STYLE_ENABLED eq CL_GUI_ALV_GRID=>MC_STYLE_ENABLED.
    if IF_PREPRINT eq ABAP_TRUE.
      LF_STYLE_ENABLED_SUBPA = ME->F_STYLE_DISABLED + CL_GUI_ALV_GRID=>MC_STYLE_HOTSPOT_NO.
    else.
      LF_STYLE_ENABLED_SUBPA = ME->F_STYLE_ENABLED + CL_GUI_ALV_GRID=>MC_STYLE_HOTSPOT.
    endif.
  else.
    LF_STYLE_ENABLED_SUBPA = ME->F_STYLE_DISABLED + CL_GUI_ALV_GRID=>MC_STYLE_HOTSPOT_NO.
  endif.

*** Set Matnr = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MATNR'
                              IF_STYLE     = LF_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Material Description = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MATNR_DESCR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Substitute Paper = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'SUBPA'
                              IF_STYLE     = LF_STYLE_ENABLED_SUBPA
                    changing  CT_STYL      = CT_STYL ).

*** Set Purchase = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PURCH'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

***----------------------------------------------------------------------------
*** Adjust for Purchase
  if IF_PURCH eq ABAP_TRUE.
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                                IF_STYLE     = LF_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).

    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                                IF_STYLE     = LF_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).
  else.
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                                IF_STYLE     = ME->F_STYLE_DISABLED
                      changing  CT_STYL      = CT_STYL ).

    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                                IF_STYLE     = ME->F_STYLE_DISABLED
                      changing  CT_STYL      = CT_STYL ).
 endif.

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR'
                              IF_STYLE     = ME->F_STYLE_DISABLED    "Paper will never be purchased
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE_CURR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP_UOM'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR_DESCR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

***----------------------------------------------------------------------------
*** Set Base Weight, Base Weight Unit and Caliper = Editable if it is a dummy
  if IF_DUMMY eq ABAP_TRUE.
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGT'
                                IF_STYLE     = LF_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).

    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGP'
                                IF_STYLE     = LF_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).

    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'CALIP'
                                IF_STYLE     = LF_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).
  else.
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGT'
                                IF_STYLE     = ME->F_STYLE_DISABLED
                      changing  CT_STYL      = CT_STYL ).

    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGP'
                                IF_STYLE     = ME->F_STYLE_DISABLED
                      changing  CT_STYL      = CT_STYL ).

    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'CALIP'
                                IF_STYLE     = ME->F_STYLE_DISABLED
                      changing  CT_STYL      = CT_STYL ).
  endif.

***----------------------------------------------------------------------------
*** Set Base Weight, Base Weight Unit and Caliper (Unit) = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGT_UOM'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGP_UOM'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'CALIP_UOM'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).
endmethod.
method CHANGE_STYLE_STARCH.

  data: LF_STYLE_ENABLED        type LVC_STYLE,
        LF_STYLE_DISABLED_SUBPA type LVC_STYLE.

  free: CT_STYL.

*** --------------------------------------------------------------------------------------------
*** Set Style Details -> only Editable if Material is purchased
  if IF_PURCH            eq ABAP_TRUE and
     ME->F_STYLE_ENABLED eq CL_GUI_ALV_GRID=>MC_STYLE_ENABLED.
    move ME->F_STYLE_ENABLED  to LF_STYLE_ENABLED.
  elseif IF_PURCH eq ABAP_FALSE.
    move ME->F_STYLE_DISABLED to LF_STYLE_ENABLED.
  endif.

  LF_STYLE_DISABLED_SUBPA = ME->F_STYLE_DISABLED + CL_GUI_ALV_GRID=>MC_STYLE_HOTSPOT_NO.

*** --------------------------------------------------------------------------------------------
*** Set Style Details -> all DISABLED because nothing needs to be edit for Starch
*** Set Sheet Matnr = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MATNR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Material Description = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MATNR_DESCR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Purchase = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PURCH'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Price and Price Unit = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                              IF_STYLE     = LF_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE_CURR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                              IF_STYLE     = LF_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP_UOM'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Vendor and Description = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR_DESCR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Substitute Paper = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'SUBPA'
                              IF_STYLE     = LF_STYLE_DISABLED_SUBPA
                    changing  CT_STYL      = CT_STYL ).

*** Set Base Weight, Base Weight Unit and Caliper = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGT'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGT_UOM'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGP'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGP_UOM'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'CALIP'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'CALIP_UOM'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).
endmethod.
method CHANGE_STYLE_WAX.

  data: LF_STYLE_ENABLED        type LVC_STYLE,
        LF_STYLE_DISABLED_SUBPA type LVC_STYLE.

  free: CT_STYL.

*** --------------------------------------------------------------------------------------------
*** Set Style Details -> only Editable if Material is purchased
  if IF_PURCH            eq ABAP_TRUE and
     ME->F_STYLE_ENABLED eq CL_GUI_ALV_GRID=>MC_STYLE_ENABLED.
    move ME->F_STYLE_ENABLED  to LF_STYLE_ENABLED.
  elseif IF_PURCH eq ABAP_FALSE.
    move ME->F_STYLE_DISABLED to LF_STYLE_ENABLED.
  endif.

  LF_STYLE_DISABLED_SUBPA = ME->F_STYLE_DISABLED + CL_GUI_ALV_GRID=>MC_STYLE_HOTSPOT_NO.

*** --------------------------------------------------------------------------------------------
*** Set Style Details -> all DISABLED because nothing needs to be edit for Wax
*** Set Sheet Matnr = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MATNR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Material Description = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MATNR_DESCR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Purchase = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PURCH'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Price and Price Unit = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                              IF_STYLE     = LF_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE_CURR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                              IF_STYLE     = LF_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP_UOM'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Vendor and Description = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR_DESCR'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Substitute Paper = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'SUBPA'
                              IF_STYLE     = LF_STYLE_DISABLED_SUBPA
                    changing  CT_STYL      = CT_STYL ).

*** Set Base Weight and Base Weight Unit = Not Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGT'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGT_UOM'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGP'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'BAWGP_UOM'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'CALIP'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).

  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'CALIP_UOM'
                              IF_STYLE     = ME->F_STYLE_DISABLED
                    changing  CT_STYL      = CT_STYL ).
endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_GATE           = IR_GATE
                      IR_UIGATE         = IR_UIGATE
                      IF_TABNAME        = 'ZVCXI_PCSS_S_MS_BOARD_GRID'
                      IF_CONTAINER_NAME = IF_CONTAINER_NAME
                      IF_APPL_EVENTS    = IF_APPL_EVENTS ).

  try.
      move IR_GATE->R_CLSPR ?to ME->R_PR_BOARD.

    catch CX_SY_MOVE_CAST_ERROR.
      clear: ME->R_PR_BOARD.
  endtry.

endmethod.
method GET_FIELDCAT.

  data:          LF_LOCKED          type        /VCXI/CKX_LOCKED,
                 LF_INUSE           type        /VCXI/VKSR_INUSE,
                 LF_HIDE_SUBPA_DATA type        ABAP_BOOL.
  field-symbols: <S_FCAT>           type        LVC_S_FCAT.

***----------------------------------------------------------------------------
*** Get Standard Field Catalog
  RT_FCAT = SUPER->GET_FIELDCAT( ).

*** Get Lock and In-Use Status
  LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
  LF_INUSE  = ME->R_GATE->IS_INUSE( ).

*** Get Show Hide Data
  if ME->F_SUBPA_IS_ACTIV eq ABAP_FALSE.
    LF_HIDE_SUBPA_DATA  = ABAP_TRUE.
  else.
    LF_HIDE_SUBPA_DATA  = ABAP_FALSE.
  endif.

***----------------------------------------------------------------------------
*** Adjust Field Catalog
  loop at RT_FCAT assigning <S_FCAT>.
    case <S_FCAT>-FIELDNAME.
      when 'POSID' or 'GATKY' or 'R_GATE' or 'INFO_FNAME' or 'BPAID'.
        move ABAP_TRUE    to <S_FCAT>-TECH.

***   Always Visible and Editable and Checkbox
      when 'SUBPA'.
        move ABAP_TRUE to <S_FCAT>-CHECKBOX.
        if LF_LOCKED eq ABAP_TRUE and
           LF_INUSE  eq ABAP_TRUE.
          move ABAP_TRUE  to <S_FCAT>-EDIT.
          move ABAP_TRUE  to <S_FCAT>-HOTSPOT.
        endif.

***   Procurement Option
      when 'PURCH'.
        move LF_HIDE_SUBPA_DATA to <S_FCAT>-NO_OUT.
        if LF_LOCKED          eq ABAP_TRUE and
           LF_INUSE           eq ABAP_TRUE and
           LF_HIDE_SUBPA_DATA eq ABAP_FALSE.
          move ABAP_TRUE    to <S_FCAT>-EDIT.
        endif.

***   Editable if Substitute Paper is Aktiv
      when 'PRICE' or 'PRICP' or 'BAWGT' or 'BAWGP' or 'CALIP'.
        move LF_HIDE_SUBPA_DATA to <S_FCAT>-NO_OUT.
        if LF_LOCKED eq ABAP_TRUE and
           LF_INUSE  eq ABAP_TRUE.
          move ME->F_SUBPA_IS_ACTIV to <S_FCAT>-EDIT.
        endif.

      when 'PRICE_CURR' or 'PRICP_UOM' or 'BAWGT_UOM' or 'BAWGP_UOM' or 'CALIP_UOM'.
        move LF_HIDE_SUBPA_DATA to <S_FCAT>-NO_OUT.

***   Always Hide Vendor and Description -> Not needed for Board Materials
      when 'LIFNR' or 'LIFNR_DESCR'.
        move ABAP_TRUE to <S_FCAT>-TECH.

    endcase.
  endloop.

endmethod.
method GET_GRID_DATA.

  data:          LF_PREPRINT          type          ABAP_BOOL.
  data:          LS_MS_BOARD_VIS      type          ZVCXI_PCSS_S_MS_BOARD_VIS,
                 LS_MS_BOARD_GRID     type          ZVCXI_PCSS_S_MS_BOARD_GRID.
  data:          LT_GATE              type          /VCXI/VKSR_TT_GATE,
                 LT_PRPRP_STR         type          ZVCXI_PCSS_TT_PRPRP_STR.
  data:          LR_GATE              type ref to   /VCXI/CL_VKSR_GATE,
                 LR_PR_MS_PAPER       type ref to   ZCL_VCXI_PCSS_PR_MS_PAPER,
                 LR_PR_MS_STARCH      type ref to   ZCL_VCXI_PCSS_PR_MS_STARCH,
                 LR_PR_MS_WAX         type ref to   ZCL_VCXI_PCSS_PR_MS_WAX.
  field-symbols: <T_DATA>             type standard table,
                 <S_DATA>             type          any.

*** Clear Indicator for Substitute Paper
  clear: ME->F_SUBPA_IS_ACTIV.

*** --------------------------------------------------------------------------------------------
*** Get All Preprint Papers
  LT_PRPRP_STR = ME->R_PR_BOARD->GET_PRPRP_STR( ).

*** Dereferencing of Data Reference
  assign IR_DATA->* to <T_DATA>.

*** --------------------------------------------------------------------------------------------
*** Get Gates of Material Specification - Paper
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_PCSS_PR_MS_PAPER=>C_IPRVT_PC_MS_PAPER
                                   IF_INUSE  = ABAP_TRUE
                                   IF_SORTED = ABAP_TRUE ).

  loop at LT_GATE into LR_GATE.
    clear: LS_MS_BOARD_GRID, LR_PR_MS_PAPER, LF_PREPRINT.
*** --------------------------------------------------------------------------------------------
*** Get Paper
    try.
        move LR_GATE->R_CLSPR ?to LR_PR_MS_PAPER.
        check LR_PR_MS_PAPER is bound.

        move LR_GATE->GET_POSID( ) to LS_MS_BOARD_GRID-POSID.
        move LR_GATE->F_GATKY      to LS_MS_BOARD_GRID-GATKY.
        move LR_GATE               to LS_MS_BOARD_GRID-R_GATE.

        LS_MS_BOARD_VIS = ME->GET_MS_PAPER_VIS( IR_PR_MS_PAPER = LR_PR_MS_PAPER ).
        move-corresponding LS_MS_BOARD_VIS to LS_MS_BOARD_GRID.

        if LS_MS_BOARD_VIS-SUBPA eq ABAP_TRUE.
          move ABAP_TRUE to ME->F_SUBPA_IS_ACTIV.
        endif.

        read table LT_PRPRP_STR with key BPAID = LS_MS_BOARD_VIS-BPAID
                                transporting no fields.
        if SY-SUBRC = 0.
          LF_PREPRINT = ABAP_TRUE.
          LS_MS_BOARD_GRID-ROWCOLOR = C_ROWCOLOR_INACTIVE.
        endif.

*** --------------------------------------------------------------------------------------------
***     Set Style Details
        ME->CHANGE_STYLE_PAPER( exporting IF_SUBPA    = LS_MS_BOARD_GRID-SUBPA
                                          IF_PREPRINT = LF_PREPRINT
                                          IF_PURCH    = LS_MS_BOARD_GRID-PURCH
                                          IF_DUMMY    = LR_PR_MS_PAPER->IS_DUMMY( )
                                changing  CT_STYL     = LS_MS_BOARD_GRID-T_STYL ).

***     Call DVC Service - Modify Style
        ZCL_VCXI_XCDR_SERVICE=>MODIFY_STYLE( exporting IR_GATE = LR_GATE
                                             changing  CT_STYL = LS_MS_BOARD_GRID-T_STYL ).

*** --------------------------------------------------------------------------------------------
***     Add Entry
        append initial line to <T_DATA> assigning <S_DATA>.
        move-corresponding LS_MS_BOARD_GRID to <S_DATA>.
        continue.

      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.
  endloop.

*** --------------------------------------------------------------------------------------------
*** Get Gates of Material Specification - Starch
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_PCSS_PR_MS_STARCH=>C_IPRVT_PC_MS_STARCH
                                   IF_INUSE  = ABAP_TRUE
                                   IF_SORTED = ABAP_TRUE ).

  loop at LT_GATE into LR_GATE.
    clear: LS_MS_BOARD_GRID, LR_PR_MS_STARCH.
*** --------------------------------------------------------------------------------------------
*** Get Starch
    try.
        move LR_GATE->R_CLSPR ?to LR_PR_MS_STARCH.
        check LR_PR_MS_STARCH is bound.

        move LR_GATE->GET_POSID( ) to LS_MS_BOARD_GRID-POSID.
        move LR_GATE->F_GATKY      to LS_MS_BOARD_GRID-GATKY.
        move LR_GATE               to LS_MS_BOARD_GRID-R_GATE.

        LS_MS_BOARD_VIS = ME->GET_MS_STARCH_VIS( IR_PR_MS_STARCH = LR_PR_MS_STARCH ).
        move-corresponding LS_MS_BOARD_VIS to LS_MS_BOARD_GRID.

*** --------------------------------------------------------------------------------------------
***     Set Style Details
        ME->CHANGE_STYLE_STARCH( exporting IF_PURCH = LS_MS_BOARD_GRID-PURCH
                                 changing  CT_STYL  = LS_MS_BOARD_GRID-T_STYL ).

***     Call DVC Service - Modify Style
        ZCL_VCXI_XCDR_SERVICE=>MODIFY_STYLE( exporting IR_GATE = LR_GATE
                                             changing  CT_STYL = LS_MS_BOARD_GRID-T_STYL ).

*** --------------------------------------------------------------------------------------------
***     Add Entry
        append initial line to <T_DATA> assigning <S_DATA>.
        move-corresponding LS_MS_BOARD_GRID to <S_DATA>.
        continue.

      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.

  endloop.

*** --------------------------------------------------------------------------------------------
*** Get Gates of Material Specification - Wax
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_PCSS_PR_MS_WAX=>C_IPRVT_PC_MS_WAX
                                   IF_INUSE  = ABAP_TRUE
                                   IF_SORTED = ABAP_TRUE ).

  loop at LT_GATE into LR_GATE.
    clear: LS_MS_BOARD_GRID, LR_PR_MS_WAX.
*** --------------------------------------------------------------------------------------------
*** Get Wax
    try.
        move LR_GATE->R_CLSPR ?to LR_PR_MS_WAX.
        check LR_PR_MS_WAX is bound.

        move LR_GATE->GET_POSID( ) to LS_MS_BOARD_GRID-POSID.
        move LR_GATE->F_GATKY      to LS_MS_BOARD_GRID-GATKY.
        move LR_GATE               to LS_MS_BOARD_GRID-R_GATE.

        LS_MS_BOARD_VIS = ME->GET_MS_WAX_VIS( IR_PR_MS_WAX = LR_PR_MS_WAX ).
        move-corresponding LS_MS_BOARD_VIS to LS_MS_BOARD_GRID.

*** --------------------------------------------------------------------------------------------
***     Set Style Details
        ME->CHANGE_STYLE_WAX( exporting IF_PURCH = LS_MS_BOARD_GRID-PURCH
                              changing  CT_STYL  = LS_MS_BOARD_GRID-T_STYL ).
***     Call DVC Service - Modify Style
        ZCL_VCXI_XCDR_SERVICE=>MODIFY_STYLE( exporting IR_GATE = LR_GATE
                                             changing  CT_STYL = LS_MS_BOARD_GRID-T_STYL ).

*** --------------------------------------------------------------------------------------------
***     Add Entry
        append initial line to <T_DATA> assigning <S_DATA>.
        move-corresponding LS_MS_BOARD_GRID to <S_DATA>.
        continue.

      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.
  endloop.

endmethod.
method GET_LAYOUT.

  RS_LAYOUT = SUPER->GET_LAYOUT( ).

*** Set Name of Color Fieldname
  RS_LAYOUT-INFO_FNAME = C_INFO_FNAME.

endmethod.
method GET_MS_PAPER_VIS.

  data: LS_BAWGT type ZVCXI_PCS_S_BAWGT,
        LS_BAWGP type ZVCXI_PCS_S_BAWGP,
        LS_CALIP type ZVCXI_P0SS_S_CALIP.

*** Material
  move IR_PR_MS_PAPER->GET_MATNR( )         to RS_MS_PAPER_VIS-MATNR.
  move IR_PR_MS_PAPER->GET_MATNR_DESCR( )   to RS_MS_PAPER_VIS-MATNR_DESCR.

*** Price
  move IR_PR_MS_PAPER->GET_PRICE( )         to RS_MS_PAPER_VIS-S_PRICE.
  move IR_PR_MS_PAPER->GET_PRICP( )         to RS_MS_PAPER_VIS-S_PRICP.

*** Procurement Option
  move IR_PR_MS_PAPER->GET_PURCH( )         to RS_MS_PAPER_VIS-PURCH.

*** Board Paper ID
  move IR_PR_MS_PAPER->GET_BPAID( )         to RS_MS_PAPER_VIS-BPAID.

*** Substitute Paper
  move IR_PR_MS_PAPER->GET_SUBPA( )         to RS_MS_PAPER_VIS-SUBPA.

*** Base Weight
  move IR_PR_MS_PAPER->GET_BAWGT( )         to LS_BAWGT.
  move-corresponding LS_BAWGT               to RS_MS_PAPER_VIS.

*** Base Weight
  move IR_PR_MS_PAPER->GET_BAWGP( )         to LS_BAWGP.
  move-corresponding LS_BAWGP               to RS_MS_PAPER_VIS.

*** Caliper
  move IR_PR_MS_PAPER->GET_CALIP( )         to LS_CALIP.
  move-corresponding LS_CALIP               to RS_MS_PAPER_VIS.

endmethod.
method GET_MS_STARCH_VIS.

*** Material
  move IR_PR_MS_STARCH->GET_MATNR( )         to RS_MS_STARCH_VIS-MATNR.
  move IR_PR_MS_STARCH->GET_MATNR_DESCR( )   to RS_MS_STARCH_VIS-MATNR_DESCR.

endmethod.
method GET_MS_WAX_VIS.

*** Material
  move IR_PR_MS_WAX->GET_MATNR( )         to RS_MS_WAX_VIS-MATNR.
  move IR_PR_MS_WAX->GET_MATNR_DESCR( )   to RS_MS_WAX_VIS-MATNR_DESCR.

endmethod.
method HANDLE_DATA_CHANGED_FINISHED.

  data:          LS_MS_BOARD_VIS      type          ZVCXI_PCSS_S_MS_BOARD_VIS,
                 LS_MS_BOARD_GRID     type          ZVCXI_PCSS_S_MS_BOARD_GRID.
  data:          LR_PR_MS_PAPER       type ref to   ZCL_VCXI_PCSS_PR_MS_PAPER,
                 LR_PR_MS_STARCH      type ref to   ZCL_VCXI_PCSS_PR_MS_STARCH,
                 LR_PR_MS_WAX         type ref to   ZCL_VCXI_PCSS_PR_MS_WAX,
                 LR_CX_CKX            type ref to   /VCXI/CX_CKX.
  field-symbols: <T_DATA>             type standard table,
                 <S_DATA>             type          any.

  check E_MODIFIED eq ABAP_TRUE.

  try.
      SUPER->HANDLE_DATA_CHANGED_FINISHED( E_MODIFIED    = E_MODIFIED
                                           ET_GOOD_CELLS = ET_GOOD_CELLS ).

***   Dereferencing of Data Reference
      assign ME->R_DATA->* to <T_DATA>.

      loop at <T_DATA> assigning <S_DATA>.
***     Refresh References
        clear: LR_PR_MS_PAPER, LR_PR_MS_STARCH, LR_PR_MS_WAX.

        move-corresponding <S_DATA> to LS_MS_BOARD_GRID.
        check LS_MS_BOARD_GRID-R_GATE is bound.

        try.
            move LS_MS_BOARD_GRID-R_GATE->R_CLSPR ?to LR_PR_MS_PAPER.
          catch CX_SY_MOVE_CAST_ERROR.
            clear: LR_PR_MS_PAPER.
        endtry.

        if LR_PR_MS_PAPER is bound.
***       Set Values to PR Class
          move-corresponding LS_MS_BOARD_GRID to LS_MS_BOARD_VIS.
          ME->SET_MS_PAPER_VIS( IR_PR_MS_PAPER  = LR_PR_MS_PAPER
                                IS_MS_PAPER_VIS = LS_MS_BOARD_VIS ).
          continue.

        endif.

        try.
            move LS_MS_BOARD_GRID-R_GATE->R_CLSPR ?to LR_PR_MS_STARCH.
          catch CX_SY_MOVE_CAST_ERROR.
            clear: LR_PR_MS_STARCH.
        endtry.

        if LR_PR_MS_STARCH is bound.
***     Set Values to PR Class
          move-corresponding LS_MS_BOARD_GRID to LS_MS_BOARD_VIS.
          ME->SET_MS_STARCH_VIS( IR_PR_MS_STARCH  = LR_PR_MS_STARCH
                                 IS_MS_STARCH_VIS = LS_MS_BOARD_VIS ).
          continue.

        endif.

        try.
            move LS_MS_BOARD_GRID-R_GATE->R_CLSPR ?to LR_PR_MS_WAX.
          catch CX_SY_MOVE_CAST_ERROR.
            clear: LR_PR_MS_WAX.
        endtry.

        if LR_PR_MS_WAX is bound.
***     Set Values to PR Class
          move-corresponding LS_MS_BOARD_GRID to LS_MS_BOARD_VIS.
          ME->SET_MS_WAX_VIS( IR_PR_MS_WAX  = LR_PR_MS_WAX
                              IS_MS_WAX_VIS = LS_MS_BOARD_VIS ).

        endif.

      endloop.

***   Request Refresh for ALV
      move ABAP_TRUE to ME->F_REFRESH_REQ.

      ME->R_GATE->RAISE_EVENTS( ).

    catch /VCXI/CX_CKX into LR_CX_CKX.
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

*** Request Refresh for ALV
  move ABAP_TRUE to ME->F_REFRESH_REQ.

endmethod.
method HANDLE_DOUBLE_CLICK.

  data:          LR_CX_CKX            type ref to   /VCXI/CX_CKX,
                 LR_GATE              type ref to   /VCXI/CL_VKSR_GATE.
  field-symbols: <T_DATA>             type standard table,
                 <S_DATA>             type          any,
                 <F_FIELD>            type          any.

  try.

***   Dereferencing of Data Reference
      assign ME->R_DATA->* to <T_DATA>.

      loop at <T_DATA> assigning <S_DATA>.
        check SY-TABIX  eq E_ROW-INDEX.

***     Open Subscreen
        assign component C_FIELDNAME_RGATE of structure <S_DATA> to <F_FIELD>.
        check <F_FIELD> is assigned.
        move <F_FIELD> to LR_GATE.

***     Check Gate is bound and Provider has an own UI
        check LR_GATE is bound and
              LR_GATE->GET_CLSUI( ) is not initial.
***     Open UI
        ME->R_UIGATE->OPEN_SUBSCREEN( IR_GATE = LR_GATE ).

***     Expand and select in Tree
        ME->R_UIGATE->EXPAND_TREE_NODE( IF_GATKY = LR_GATE->F_GATKY ).
        ME->R_UIGATE->SELECT_TREE_NODE( IF_GATKY = LR_GATE->F_GATKY ).

      endloop.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

endmethod.
method HANDLE_HOTSPOT_CLICK.

  data:          LS_MS_BOARD_GRID     type          ZVCXI_PCSS_S_MS_BOARD_GRID.
  data:          LR_PR_MS_PAPER       type ref to   ZCL_VCXI_PCSS_PR_MS_PAPER,
                 LR_CX_CKX            type ref to   /VCXI/CX_CKX.
  field-symbols: <T_DATA>             type standard table,
                 <S_DATA>             type          any.

  try.

***   Dereferencing of Data Reference
      assign ME->R_DATA->* to <T_DATA>.

      loop at <T_DATA> assigning <S_DATA>.
        check SY-TABIX  eq E_ROW_ID.

        try.
            move-corresponding <S_DATA> to LS_MS_BOARD_GRID.

            move LS_MS_BOARD_GRID-R_GATE->R_CLSPR ?to LR_PR_MS_PAPER.
            check LR_PR_MS_PAPER is bound.

***         Set Values to PR Class
            case E_COLUMN_ID-FIELDNAME.

              when 'SUBPA'.
                case LS_MS_BOARD_GRID-SUBPA.
                  when ABAP_TRUE.  move ABAP_FALSE to LS_MS_BOARD_GRID-SUBPA.
                  when ABAP_FALSE. move ABAP_TRUE  to LS_MS_BOARD_GRID-SUBPA.
                endcase.
                LR_PR_MS_PAPER->SET_SUBPA( IF_SUBPA = LS_MS_BOARD_GRID-SUBPA ).

            endcase.

          catch CX_SY_MOVE_CAST_ERROR.
            clear: LR_PR_MS_PAPER.
        endtry.
      endloop.

      ME->R_GATE->RAISE_EVENTS( ).

    catch /VCXI/CX_CKX into LR_CX_CKX.
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

*** Request Refresh for ALV
  move ABAP_TRUE to ME->F_REFRESH_REQ.

endmethod.
method SET_EVENT.

  SUPER->SET_EVENT( ).

*** Set Handler for Doubleclick
  set handler ME->HANDLE_DOUBLE_CLICK        for ME->R_GRID.

*** Set Hotspot
  set handler ME->HANDLE_HOTSPOT_CLICK       for ME->R_GRID.

endmethod.
method SET_MS_PAPER_VIS.

  data:  LS_PRICE          type ZVCXI_XCS_S_PRICE,
         LS_PRICP          type ZVCXI_XCS_S_PRICP,
         LS_BAWGT          type ZVCXI_PCS_S_BAWGT,
         LS_BAWGP          type ZVCXI_PCS_S_BAWGP,
         LS_CALIP          type ZVCXI_P0SS_S_CALIP,
         LS_MS_PAPER_VIS   type ZVCXI_PCSS_S_MS_BOARD_VIS.

*** Get Current data to do Update only if Value changed.
  LS_MS_PAPER_VIS = ME->GET_MS_PAPER_VIS( IR_PR_MS_PAPER = IR_PR_MS_PAPER ).

*** Material Number
  if IS_MS_PAPER_VIS-MATNR ne LS_MS_PAPER_VIS-MATNR.
    IR_PR_MS_PAPER->SET_MATNR( IF_MATNR = IS_MS_PAPER_VIS-MATNR
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Price
  if IS_MS_PAPER_VIS-PRICE ne LS_MS_PAPER_VIS-PRICE.
    move-corresponding IS_MS_PAPER_VIS to LS_PRICE.
    IR_PR_MS_PAPER->SET_PRICE( IS_PRICE = LS_PRICE
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

  if IS_MS_PAPER_VIS-PRICP ne LS_MS_PAPER_VIS-PRICP.
    move-corresponding IS_MS_PAPER_VIS to LS_PRICP.
    IR_PR_MS_PAPER->SET_PRICP( IS_PRICP = LS_PRICP
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Base Weight
  if IS_MS_PAPER_VIS-BAWGT ne LS_MS_PAPER_VIS-BAWGT.
    move-corresponding IS_MS_PAPER_VIS to LS_BAWGT.
    IR_PR_MS_PAPER->SET_BAWGT( IS_BAWGT = LS_BAWGT
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

  if IS_MS_PAPER_VIS-BAWGP ne LS_MS_PAPER_VIS-BAWGP.
    move-corresponding IS_MS_PAPER_VIS to LS_BAWGP.
    IR_PR_MS_PAPER->SET_BAWGP( IS_BAWGP = LS_BAWGP
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Caliper
  if IS_MS_PAPER_VIS-CALIP ne LS_MS_PAPER_VIS-CALIP.
    move-corresponding IS_MS_PAPER_VIS to LS_CALIP.
    IR_PR_MS_PAPER->SET_CALIP( IS_CALIP = LS_CALIP
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

endmethod.
method SET_MS_STARCH_VIS.

  data: LS_MS_STARCH_VIS  type ZVCXI_PCSS_S_MS_BOARD_VIS.


*** Get Current data to do Update only if Value changed.
  LS_MS_STARCH_VIS = ME->GET_MS_STARCH_VIS( IR_PR_MS_STARCH = IR_PR_MS_STARCH ).

*** Procurement Option
  if IS_MS_STARCH_VIS-PURCH ne LS_MS_STARCH_VIS-PURCH.
    IR_PR_MS_STARCH->SET_PURCH( IF_PURCH = IS_MS_STARCH_VIS-PURCH
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Price
  if IS_MS_STARCH_VIS-PRICE ne LS_MS_STARCH_VIS-PRICE.
    IR_PR_MS_STARCH->SET_PRICE( IS_PRICE = IS_MS_STARCH_VIS-S_PRICE
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

  if IS_MS_STARCH_VIS-PRICP ne LS_MS_STARCH_VIS-PRICP.
    IR_PR_MS_STARCH->SET_PRICP( IS_PRICP = IS_MS_STARCH_VIS-S_PRICP
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

endmethod.
method SET_MS_WAX_VIS.

  data:  LS_MS_WAX_VIS  type ZVCXI_PCSS_S_MS_BOARD_VIS.


*** Get Current data to do Update only if Value changed.
  LS_MS_WAX_VIS = ME->GET_MS_WAX_VIS( IR_PR_MS_WAX = IR_PR_MS_WAX ).

*** Procurement Option
  if IS_MS_WAX_VIS-PURCH ne LS_MS_WAX_VIS-PURCH.
    IR_PR_MS_WAX->SET_PURCH( IF_PURCH = IS_MS_WAX_VIS-PURCH
                             IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Price
  if IS_MS_WAX_VIS-PRICE ne LS_MS_WAX_VIS-PRICE.
    IR_PR_MS_WAX->SET_PRICE( IS_PRICE = IS_MS_WAX_VIS-S_PRICE
                             IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

  if IS_MS_WAX_VIS-PRICP ne LS_MS_WAX_VIS-PRICP.
    IR_PR_MS_WAX->SET_PRICP( IS_PRICP = IS_MS_WAX_VIS-S_PRICP
                             IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

endmethod.
