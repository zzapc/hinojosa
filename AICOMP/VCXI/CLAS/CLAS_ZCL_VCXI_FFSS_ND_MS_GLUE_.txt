
class ZCL_VCXI_FFSS_ND_MS_GLUE definition
  public
  inheriting from ZCL_VCXI_F0SS_ND
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
    raising
      /VCXI/CX_CKX .
protected section.

  methods INIT_NODE
    redefinition .
private section.
endclass. "ZCL_VCXI_FFSS_ND_MS_GLUE definition
class ZCL_VCXI_FFSS_ND_MS_GLUE implementation.
  method CONSTRUCTOR.

*** Call Super Constructor
    SUPER->CONSTRUCTOR( IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

  endmethod.
  method INIT_NODE.

    data:           LF_TGLUE            type        ZVCXI_FFS_TGLUE,
                    LF_FL               type        NETVALSFL.
    data:           LS_GLWID            type        ZVCXI_FFS_S_GLWID.
    data:           LR_PR_MS_GLUE       type ref to ZCL_VCXI_FFSS_PR_MS_GLUE.
    field-symbols:  <S_NVAL>            type        YS_NVALS.

***----------------------------------------------------------------------------
*** Call Super
    SUPER->INIT_NODE( ).

***----------------------------------------------------------------------------
*** Check Input Gate is EXTR
    try.
        move ME->R_GATE->R_CLSPR ?to LR_PR_MS_GLUE.
      catch CX_SY_MOVE_CAST_ERROR.
        clear LR_PR_MS_GLUE.
    endtry.

    check LR_PR_MS_GLUE is bound.

*** Initialize Node Values from Gate
    move 1 to ME->F_NETTYPE.

***----------------------------------------------------------------------------
*** Set Node Description
    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = 0.
    <S_NVAL>-VAL  = LR_PR_MS_GLUE->GET_DESCR( ).

*** Left out Number '1' for Icon Field
    move 1 to LF_FL.

*** Get Glue Width
    add 1 to LF_FL.
    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = LF_FL.
    move text-D02   to <S_NVAL>-VAL.

    add 1 to LF_FL.
    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = LF_FL.
    LR_PR_MS_GLUE->GET_GLWID( importing ES_GLWID = LS_GLWID ).
    <S_NVAL>-VAL = ME->CONV_ANY_TO_VAL( IF_VALUE = LS_GLWID-GLWID
                                        IF_UOM   = LS_GLWID-GLWID_UOM ).

*** Get Glue Type
    add 1 to LF_FL.
    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = LF_FL.
    move text-D03   to <S_NVAL>-VAL.

    add 1 to LF_FL.
    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = LF_FL.
    LR_PR_MS_GLUE->GET_TGLUE( importing EF_TGLUE = LF_TGLUE ).
    <S_NVAL>-VAL = LF_TGLUE.

*** Set Status Icon
    ME->SET_STATUS_ICON( ).

  endmethod.
endclass. "ZCL_VCXI_FFSS_ND_MS_GLUE implementation
