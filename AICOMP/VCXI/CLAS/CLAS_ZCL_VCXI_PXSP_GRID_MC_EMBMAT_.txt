
class ZCL_VCXI_PXSP_GRID_MC_EMBMAT definition
  public
  inheriting from ZCL_VCXI_XCS_GRID_PRVDR
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABNAME type TABNAME default 'ZVCXI_PXSP_S_MC_EMBMAT_GRID'
      !IF_CONTAINER_NAME type SCRFNAME
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL default ABAP_TRUE
    raising
      /VCXI/CX_CKX .
protected section.

  methods CHANGE_STYLE_EMBOS
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IS_DATA type ANY
    changing
      !CT_STYL type LVC_T_STYL
    raising
      /VCXI/CX_CKX .
  methods GET_EMBOS_VIS
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    changing
      !CS_DATA type ANY
    raising
      /VCXI/CX_CKX .
  methods SET_EMBOS_VIS
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IS_DATA type ANY
    raising
      /VCXI/CX_CKX .
  methods GET_MC_EMBMAT_VIS
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    changing
      !CS_DATA type ANY
    raising
      /VCXI/CX_CKX .
  methods SET_MC_EMBMAT_VIS
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IS_DATA type ANY
    raising
      /VCXI/CX_CKX .

  methods GET_FIELDCAT
    redefinition .
  methods GET_GRID_DATA
    redefinition .
  methods HANDLE_DATA_CHANGED_FINISHED
    redefinition .
private section.
endclass. "ZCL_VCXI_PXSP_GRID_MC_EMBMAT definition
class ZCL_VCXI_PXSP_GRID_MC_EMBMAT implementation.
  method CHANGE_STYLE_EMBOS.

    data: LF_STYLE_ENABLED type LVC_STYLE.
    data: LS_MC_EMBMAT_VIS type ZVCXI_PXSP_S_MC_EMBMAT_VIS.


    free CT_STYL.

***--------------------------------------------------------------------------------------
*** "Cast" generic structure to Vis Structure
    move-corresponding IS_DATA to LS_MC_EMBMAT_VIS.

*** Disable Material Fields if Material is not supported
    LF_STYLE_ENABLED = switch #( LS_MC_EMBMAT_VIS-FEMBM when ABAP_TRUE then ME->F_STYLE_ENABLED
                                                                       else CL_GUI_ALV_GRID=>MC_STYLE_DISABLED ).

***--------------------------------------------------------------------------------------
*** Set Embossing Type = Editable
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'TEMBO'
                                IF_STYLE     = ME->F_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).

*** Set Embossing Type Description = Not Editable
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'TEMBO_DESCR'
                                IF_STYLE     = ME->F_STYLE_DISABLED
                      changing  CT_STYL      = CT_STYL ).

***--------------------------------------------------------------------------------------
*** Set Material = Enable
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MATNR'
                                IF_STYLE     = LF_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).

*** Set Average Consumption = Editable
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'AVGCO'
                                IF_STYLE     = LF_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).

*** Set Average Consumption per = Editable
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'AVGCP'
                                IF_STYLE     = LF_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).

*** Set Purchase Option = Enable
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PURCH'
                                IF_STYLE     = LF_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).

*** Purchase Embossing Material
    case LS_MC_EMBMAT_VIS-PURCH.
      when ZCL_VCXI_XCSS_PR_MS=>C_PURCH_DIRECT_PRICE.
***     Set Vendor = Enable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR'
                                    IF_STYLE     = LF_STYLE_ENABLED
                          changing  CT_STYL      = CT_STYL ).
***     Set Price = Enable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                                    IF_STYLE     = LF_STYLE_ENABLED
                          changing  CT_STYL      = CT_STYL ).
***     Set Price Per = Enable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                                    IF_STYLE     = LF_STYLE_ENABLED
                          changing  CT_STYL      = CT_STYL ).

      when ZCL_VCXI_XCSS_PR_MS=>C_PURCH_DIRECT.
***     Set Vendor = Enable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR'
                                    IF_STYLE     = LF_STYLE_ENABLED
                          changing  CT_STYL      = CT_STYL ).
***     Set Price = Disable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                                    IF_STYLE     = ME->F_STYLE_DISABLED
                          changing  CT_STYL      = CT_STYL ).
***     Set Price Per = Disable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                                    IF_STYLE     = ME->F_STYLE_DISABLED
                          changing  CT_STYL      = CT_STYL ).

      when ZCL_VCXI_XCSS_PR_MS=>C_PURCH_STOCK.
***     Set Vendor = Disable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR'
                                    IF_STYLE     = ME->F_STYLE_DISABLED
                          changing  CT_STYL      = CT_STYL ).
***     Set Price = Disable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                                    IF_STYLE     = ME->F_STYLE_DISABLED
                          changing  CT_STYL      = CT_STYL ).
***     Set Price Per = Disable
        ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                                    IF_STYLE     = ME->F_STYLE_DISABLED
                          changing  CT_STYL      = CT_STYL ).
    endcase.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_GATE           = IR_GATE
                        IR_UIGATE         = IR_UIGATE
                        IF_TABNAME        = IF_TABNAME
                        IF_CONTAINER_NAME = IF_CONTAINER_NAME
                        IF_APPL_EVENTS    = IF_APPL_EVENTS ).

  endmethod.
  method GET_EMBOS_VIS.

    data: LS_MC_EMBMAT_VIS type ZVCXI_PXSP_S_MC_EMBMAT_VIS.
    data: LR_PR_EMBOS      type ref to ZCL_VCXI_PXSP_PR_EMBOS.


***--------------------------------------------------------------------------------------
*** Get PR Class
    try.
        move IR_GATE->R_CLSPR ?to LR_PR_EMBOS.
      catch CX_SY_MOVE_CAST_ERROR.
    endtry.
    check LR_PR_EMBOS is bound.

*** "Cast" generic structure to Vis Structure
    move-corresponding CS_DATA to LS_MC_EMBMAT_VIS.

***--------------------------------------------------------------------------------------
*** Embossing Type
    LS_MC_EMBMAT_VIS-TEMBO = LR_PR_EMBOS->GET_TEMBO( importing EF_DESCR = LS_MC_EMBMAT_VIS-TEMBO_DESCR ).

***--------------------------------------------------------------------------------------
*** "Cast" Vis Structure back to generic structure
    move-corresponding LS_MC_EMBMAT_VIS to CS_DATA.

  endmethod.
  method GET_FIELDCAT.

    data: LF_LOCKED type /VCXI/CKX_LOCKED,
          LF_INUSE  type /VCXI/VKSR_INUSE,
          LF_FEMBM  type ZVCXI_PXS_FEMBM,
          LF_LIFNR  type ABAP_BOOL,
          LF_PRICE  type ABAP_BOOL.
    data: LS_MC_EMBMAT_GRID type ZVCXI_PXSP_S_MC_EMBMAT_GRID.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type ANY,
                   <S_FCAT> type LVC_S_FCAT.

***----------------------------------------------------------------------------
*** Get Standard Field Catalog
    RT_FCAT = SUPER->GET_FIELDCAT( ).

*** Get Lock and In-Use Status
    LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
    LF_INUSE  = ME->R_GATE->IS_INUSE( ).

*** Dereferencing of Data Reference
    assign ME->R_DATA->* to <T_DATA>.

    loop at <T_DATA> assigning <S_DATA>.
      move-corresponding <S_DATA> to LS_MC_EMBMAT_GRID.
***   Find Embossin Material
      if LS_MC_EMBMAT_GRID-FEMBM eq ABAP_TRUE.
        LF_FEMBM = ABAP_TRUE.
      endif.
***   Find Purchased Embossing Material
      case LS_MC_EMBMAT_GRID-PURCH.
        when ZCL_VCXI_XCSS_PR_MS=>C_PURCH_DIRECT_PRICE.
          LF_LIFNR = ABAP_TRUE.
          LF_PRICE = ABAP_TRUE.
        when ZCL_VCXI_XCSS_PR_MS=>C_PURCH_DIRECT.
          LF_LIFNR = ABAP_TRUE.
      endcase.
    endloop.

***----------------------------------------------------------------------------
*** Adjust Field Catalog
    loop at RT_FCAT assigning <S_FCAT>.
      case <S_FCAT>-FIELDNAME.
        when 'POSID' or 'GATKY' or 'R_GATE' or 'T_STYL' or 'FEMBM'.
          move ABAP_TRUE  to <S_FCAT>-TECH.
          move 0          to <S_FCAT>-COL_POS.

        when 'TEMBO'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE  to <S_FCAT>-EDIT.
          endif.

        when 'MATNR'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE  to <S_FCAT>-EDIT.
          endif.

        when 'EMBLE' or 'EMBWI'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE  to <S_FCAT>-EDIT.
          endif.

        when 'PURCH'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE  to <S_FCAT>-EDIT.
          endif.

        when 'LIFNR'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE  to <S_FCAT>-EDIT.
          endif.

        when 'PRICE' or 'PRICP'.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE  to <S_FCAT>-EDIT.
          endif.

      endcase.

***----------------------------------------------------------------------------
***   Hide Field if not Embossing Material
      if LF_FEMBM ne ABAP_TRUE.
        if <S_FCAT>-FIELDNAME eq 'MATNR' or <S_FCAT>-FIELDNAME eq 'MATNR_DESCR' or
           <S_FCAT>-FIELDNAME eq 'PURCH' or
           <S_FCAT>-FIELDNAME eq 'EMBLE' or <S_FCAT>-FIELDNAME eq 'EMBLE_UOM' or
           <S_FCAT>-FIELDNAME eq 'EMBWI' or <S_FCAT>-FIELDNAME eq 'EMBWI_UOM'.
          move ABAP_TRUE to <S_FCAT>-NO_OUT.
        endif.
      endif.
***   Hide Field if not purchase
      if LF_LIFNR ne ABAP_TRUE.
        if <S_FCAT>-FIELDNAME eq 'LIFNR' or <S_FCAT>-FIELDNAME eq 'LIFNR_DESCR'.
          move ABAP_TRUE to <S_FCAT>-NO_OUT.
        endif.
      endif.
      if LF_PRICE ne ABAP_TRUE.
        if <S_FCAT>-FIELDNAME eq 'PRICE' or <S_FCAT>-FIELDNAME eq 'PRICE_CURR' or
           <S_FCAT>-FIELDNAME eq 'PRICP' or <S_FCAT>-FIELDNAME eq 'PRICP_UOM'.
          move ABAP_TRUE to <S_FCAT>-NO_OUT.
        endif.
      endif.

    endloop.

  endmethod.
  method GET_GRID_DATA.

    data: LS_LAYOUT type LVC_S_LAYO.
    data: LT_GATE type /VCXI/VKSR_TT_GATE.
    data: LR_GATE type ref to /VCXI/CL_VKSR_GATE.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type          ANY,
                   <T_STYL> type          LVC_T_STYL.

***--------------------------------------------------------------------------------------
*** Dereferencing of Data Reference
    assign IR_DATA->* to <T_DATA>.

*** Get Layout
    LS_LAYOUT = ME->GET_LAYOUT( ).

*** Get Gates of Provider
    append lines of ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_PXSP_PR_MC_EMBMAT=>C_IPRVT_PX_MC_EMBMAT
                                           IF_INUSE  = ABAP_TRUE ) to LT_GATE.
    append lines of ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_PXSP_PR_EMBOS=>C_IPRVT_PX_EMBOS
                                           IF_INUSE  = ABAP_TRUE ) to LT_GATE.
    ME->R_GATE->SORT_GATE( changing CT_GATE = LT_GATE ).

***--------------------------------------------------------------------------------------
    loop at LT_GATE into LR_GATE.
***   Add Line with Gate Key Data
      append initial line to <T_DATA> assigning <S_DATA>.
      move-corresponding value ZVCXI_PXSP_S_MC_EMBMAT_GRID( POSID  = LR_GATE->GET_POSID( )
                                                            GATKY  = LR_GATE->F_GATKY
                                                            R_GATE = LR_GATE
                                                          ) to <S_DATA>.

***   Get Visualization Data
*{   REPLACE        HEDK925687                                        1
*\      case type of LR_GATE->R_CLSPR.
*\        when type ZCL_VCXI_PXSP_PR_MC_EMBMAT.
*\          ME->GET_MC_EMBMAT_VIS( exporting IR_GATE = LR_GATE
*\                                 changing  CS_DATA = <S_DATA> ).
*\        when type ZCL_VCXI_PXSP_PR_EMBOS.
*\          ME->GET_EMBOS_VIS( exporting IR_GATE = LR_GATE
*\                             changing  CS_DATA = <S_DATA> ).
*\      endcase.
data lr_embmat type ref to ZCL_VCXI_PXSP_PR_MC_EMBMAT.
data lr_embos type ref to ZCL_VCXI_PXSP_PR_EMBOS.
try.
  lr_embmat ?=  LR_GATE->R_CLSPR.
          ME->GET_MC_EMBMAT_VIS( exporting IR_GATE = LR_GATE
                                 changing  CS_DATA = <S_DATA> ).
          catch cx_sy_move_cast_error.
            endtry.
try.
  lr_embos ?= LR_GATE->R_CLSPR.
          ME->GET_EMBOS_VIS( exporting IR_GATE = LR_GATE
                             changing  CS_DATA = <S_DATA> ).
          catch cx_sy_move_cast_error.
            endtry.
*}   REPLACE

***   Set Style Details
      if LS_LAYOUT-STYLEFNAME is not initial.
        assign component LS_LAYOUT-STYLEFNAME of structure <S_DATA> to <T_STYL>.
        if SY-SUBRC eq 0.
          ME->CHANGE_STYLE_EMBOS( exporting IR_GATE = LR_GATE
                                            IS_DATA = <S_DATA>
                                   changing CT_STYL = <T_STYL> ).
***       Call DVC Servcie - Modify Style
          ZCL_VCXI_XCDR_SERVICE=>MODIFY_STYLE( exporting IR_GATE = LR_GATE
                                               changing  CT_STYL = <T_STYL> ).
        endif.
      endif.
    endloop.

  endmethod.
  method GET_MC_EMBMAT_VIS.

    data: LS_MC_EMBMAT_VIS type ZVCXI_PXSP_S_MC_EMBMAT_VIS.
    data: LR_PR_MC_EMBMAT type ref to ZCL_VCXI_PXSP_PR_MC_EMBMAT.


***--------------------------------------------------------------------------------------
*** Get PR Class
    try.
        move IR_GATE->R_CLSPR ?to LR_PR_MC_EMBMAT.
      catch CX_SY_MOVE_CAST_ERROR.
    endtry.
    check LR_PR_MC_EMBMAT is bound.

*** "Cast" generic structure to Vis Structure
    move-corresponding CS_DATA to LS_MC_EMBMAT_VIS.

***--------------------------------------------------------------------------------------
*** Material
    LS_MC_EMBMAT_VIS-MATNR       = LR_PR_MC_EMBMAT->GET_MATNR( ).
    LS_MC_EMBMAT_VIS-MATNR_DESCR = LR_PR_MC_EMBMAT->GET_MATNR_DESCR( ).

*** Consumption
    LS_MC_EMBMAT_VIS-S_CONSU     = LR_PR_MC_EMBMAT->GET_CONSU( ).

*** Embossing Type
    LS_MC_EMBMAT_VIS-TEMBO       = LR_PR_MC_EMBMAT->GET_TEMBO( importing EF_DESCR = LS_MC_EMBMAT_VIS-TEMBO_DESCR
                                                                         EF_FEMBM = LS_MC_EMBMAT_VIS-FEMBM ).

*** Get Average Consumption and Consumption per
    LS_MC_EMBMAT_VIS-S_AVGCO     = LR_PR_MC_EMBMAT->GET_AVGCO( ).
    LS_MC_EMBMAT_VIS-S_AVGCP     = LR_PR_MC_EMBMAT->GET_AVGCP( ).

*** Procurement Option
    LS_MC_EMBMAT_VIS-PURCH       = LR_PR_MC_EMBMAT->GET_PURCH( ).
*** Vendor
    LS_MC_EMBMAT_VIS-LIFNR       = LR_PR_MC_EMBMAT->GET_LIFNR( ).
    LS_MC_EMBMAT_VIS-LIFNR_DESCR = LR_PR_MC_EMBMAT->GET_LIFNR_DESCR( ).
*** Price and Price Per
    LS_MC_EMBMAT_VIS-S_PRICE     = LR_PR_MC_EMBMAT->GET_PRICE( ).
    LS_MC_EMBMAT_VIS-S_PRICP     = LR_PR_MC_EMBMAT->GET_PRICP( ).

***--------------------------------------------------------------------------------------
*** "Cast" Vis Structure back to generic structure
    move-corresponding LS_MC_EMBMAT_VIS to CS_DATA.

  endmethod.
  method HANDLE_DATA_CHANGED_FINISHED.

    data: LS_MC_EMBMAT_GRID type ZVCXI_PXSP_S_MC_EMBMAT_GRID.
    field-symbols: <T_DATA> type standard table,
                   <S_DATA> type          ANY.

    check E_MODIFIED eq ABAP_TRUE.

    try.
        SUPER->HANDLE_DATA_CHANGED_FINISHED( E_MODIFIED    = E_MODIFIED
                                             ET_GOOD_CELLS = ET_GOOD_CELLS ).

***     Dereferencing of Data Reference
        assign ME->R_DATA->* to <T_DATA>.

        loop at <T_DATA> assigning <S_DATA>.
          move-corresponding <S_DATA> to LS_MC_EMBMAT_GRID.

          check LS_MC_EMBMAT_GRID-R_GATE is bound and
                LS_MC_EMBMAT_GRID-R_GATE->F_FREED is initial.

***       Set Values to PR Class
*{   REPLACE        HEDK925687                                        1
*\          case type of LS_MC_EMBMAT_GRID-R_GATE->R_CLSPR.
*\            when type ZCL_VCXI_PXSP_PR_MC_EMBMAT.
*\              ME->SET_MC_EMBMAT_VIS( IR_GATE = LS_MC_EMBMAT_GRID-R_GATE
*\                                     IS_DATA = <S_DATA> ).
*\            when type ZCL_VCXI_PXSP_PR_EMBOS.
*\              ME->SET_EMBOS_VIS( IR_GATE = LS_MC_EMBMAT_GRID-R_GATE
*\                                 IS_DATA = <S_DATA> ).
*\          endcase.
*\        endloop.
data lr_embmat type ref to ZCL_VCXI_PXSP_PR_MC_EMBMAT.
data lr_embos type ref to ZCL_VCXI_PXSP_PR_EMBOS.
try.
  lr_embmat ?=  LS_MC_EMBMAT_GRID-R_GATE->R_CLSPR.
          ME->GET_MC_EMBMAT_VIS( exporting IR_GATE = LS_MC_EMBMAT_GRID-R_GATE
                                 changing  CS_DATA = <S_DATA> ).
          catch cx_sy_move_cast_error.
            endtry.
try.
  lr_embos ?= LS_MC_EMBMAT_GRID-R_GATE->R_CLSPR.
          ME->GET_EMBOS_VIS( exporting IR_GATE = LS_MC_EMBMAT_GRID-R_GATE
                             changing  CS_DATA = <S_DATA> ).
          catch cx_sy_move_cast_error.
            endtry.
        endloop.
*}   REPLACE

        ME->R_GATE->RAISE_EVENTS( ).

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

*** Request Refresh for ALV
    move ABAP_TRUE to ME->F_REFRESH_REQ.

  endmethod.
  method SET_EMBOS_VIS.

    data: LS_MC_EMBMAT_VIS_OLD type ZVCXI_PXSP_S_MC_EMBMAT_VIS,
          LS_MC_EMBMAT_VIS_NEW type ZVCXI_PXSP_S_MC_EMBMAT_VIS.
    data: LR_PR_EMBOS     type ref to ZCL_VCXI_PXSP_PR_EMBOS.


***--------------------------------------------------------------------------------------
*** Get PR Class
*{   REPLACE        HEDK925687                                        1
*\    check IR_GATE->R_CLSPR is instance of ZCL_VCXI_PXSP_PR_EMBOS.
data lr_embos type ref to ZCL_VCXI_PXSP_PR_EMBOS.
try.
  lr_embos ?= IR_GATE->R_CLSPR.
catch cx_sy_move_cast_error.
  exit.
  endtry.
*}   REPLACE
    LR_PR_EMBOS ?= IR_GATE->R_CLSPR.
    check LR_PR_EMBOS is bound.

*** Get old Values
    ME->GET_MC_EMBMAT_VIS( exporting IR_GATE = IR_GATE
                           changing  CS_DATA = LS_MC_EMBMAT_VIS_OLD ).

*** Get new Values
    move-corresponding IS_DATA to LS_MC_EMBMAT_VIS_NEW.

***--------------------------------------------------------------------------------------
*** Embossing Type
    if LS_MC_EMBMAT_VIS_NEW-TEMBO ne LS_MC_EMBMAT_VIS_OLD-TEMBO.
      LR_PR_EMBOS->SET_TEMBO( IF_TEMBO = LS_MC_EMBMAT_VIS_NEW-TEMBO
                              IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method SET_MC_EMBMAT_VIS.

    data: LS_MC_EMBMAT_VIS_OLD type ZVCXI_PXSP_S_MC_EMBMAT_VIS,
          LS_MC_EMBMAT_VIS_NEW type ZVCXI_PXSP_S_MC_EMBMAT_VIS.
    data: LR_PR_MC_EMBMAT type ref to ZCL_VCXI_PXSP_PR_MC_EMBMAT.


***--------------------------------------------------------------------------------------
*** Get PR Class
*{   REPLACE        HEDK925687                                        1
*\    check IR_GATE->R_CLSPR is instance of ZCL_VCXI_PXSP_PR_MC_EMBMAT.
data lr_embmat type ref to ZCL_VCXI_PXSP_PR_MC_EMBMAT.
try.
  lr_embmat ?=  IR_GATE->R_CLSPR.
catch cx_sy_move_cast_error.
  exit.
  endtry.
*}   REPLACE
    LR_PR_MC_EMBMAT ?= IR_GATE->R_CLSPR.
    check LR_PR_MC_EMBMAT is bound.

*** Get old Values
    ME->GET_MC_EMBMAT_VIS( exporting IR_GATE = IR_GATE
                           changing  CS_DATA = LS_MC_EMBMAT_VIS_OLD ).

*** Get new Values
    move-corresponding IS_DATA to LS_MC_EMBMAT_VIS_NEW.

***--------------------------------------------------------------------------------------
*** Material
    if LS_MC_EMBMAT_VIS_NEW-MATNR ne LS_MC_EMBMAT_VIS_OLD-MATNR.
      LR_PR_MC_EMBMAT->SET_MATNR( IF_MATNR = LS_MC_EMBMAT_VIS_NEW-MATNR
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Embossing Type
    if LS_MC_EMBMAT_VIS_NEW-TEMBO ne LS_MC_EMBMAT_VIS_OLD-TEMBO.
      LR_PR_MC_EMBMAT->SET_TEMBO( IF_TEMBO = LS_MC_EMBMAT_VIS_NEW-TEMBO
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Average Consumption  & Consumption per
    if LS_MC_EMBMAT_VIS_NEW-S_AVGCO ne LS_MC_EMBMAT_VIS_OLD-S_AVGCO.
      LR_PR_MC_EMBMAT->SET_AVGCO( IS_AVGCO = LS_MC_EMBMAT_VIS_NEW-S_AVGCO
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

    if LS_MC_EMBMAT_VIS_NEW-S_AVGCP ne LS_MC_EMBMAT_VIS_OLD-S_AVGCP.
      LR_PR_MC_EMBMAT->SET_AVGCP( IS_AVGCP = LS_MC_EMBMAT_VIS_NEW-S_AVGCP
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Procurement Option
    if LS_MC_EMBMAT_VIS_NEW-PURCH ne LS_MC_EMBMAT_VIS_OLD-PURCH.
      LR_PR_MC_EMBMAT->SET_PURCH( IF_PURCH = LS_MC_EMBMAT_VIS_NEW-PURCH
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Vendor
    if LS_MC_EMBMAT_VIS_NEW-LIFNR ne LS_MC_EMBMAT_VIS_OLD-LIFNR.
      LR_PR_MC_EMBMAT->SET_LIFNR( IF_LIFNR = LS_MC_EMBMAT_VIS_NEW-LIFNR
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Price
    if LS_MC_EMBMAT_VIS_NEW-PRICE ne LS_MC_EMBMAT_VIS_OLD-PRICE.
      LR_PR_MC_EMBMAT->SET_PRICE( IS_PRICE = LS_MC_EMBMAT_VIS_NEW-S_PRICE
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Price Per
    if LS_MC_EMBMAT_VIS_NEW-PRICP ne LS_MC_EMBMAT_VIS_OLD-PRICP.
      LR_PR_MC_EMBMAT->SET_PRICP( IS_PRICP = LS_MC_EMBMAT_VIS_NEW-S_PRICP
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
