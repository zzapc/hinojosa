
class ZCL_VCXI_XCSS_PR_TINFO definition
  public
  inheriting from /VCXI/CL_VKSR_CLSPR
  create public .

public section.

  constants C_IEVNT_O_FEXTT_CHANGED type /VCXI/VKSC_IEVNT value 'FEXTT_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_FOTUT_CHANGED type /VCXI/VKSC_IEVNT value 'FOTUT_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_LIFNR_CHANGED type /VCXI/VKSC_IEVNT value 'LIFNR_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_MRKUP_CHANGED type /VCXI/VKSC_IEVNT value 'MRKUP_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_TCCBC_CHANGED type /VCXI/VKSC_IEVNT value 'TCCBC_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_TCEST_CHANGED type /VCXI/VKSC_IEVNT value 'TCEST_CHANGED' ##NO_TEXT.
  constants C_IPRVT_XCS_TINFO type /VCXI/VKSC_IPRVT value 'ZVCXI_XCSS_TINFO' ##NO_TEXT.
  constants C_PARID_FEXTT type /VCXI/VKS_PARID value 'ZVCXI_XC_FEXTT' ##NO_TEXT.
  constants C_PARID_FOTUT type /VCXI/VKS_PARID value 'ZVCXI_XC_FOTUT' ##NO_TEXT.
  constants C_PARID_LIFNR type /VCXI/VKS_PARID value 'ZVCXI_XC_LIFNR' ##NO_TEXT.
  constants C_PARID_MRKUP type /VCXI/VKS_PARID value 'ZVCXI_XC_MRKUP' ##NO_TEXT.
  constants C_PARID_TCCBC type /VCXI/VKS_PARID value 'ZVCXI_XC_TCCBC' ##NO_TEXT.
  constants C_PARID_TCEST type /VCXI/VKS_PARID value 'ZVCXI_XC_TCEST' ##NO_TEXT.
  constants C_PARID_TLTIM_L type /VCXI/VKS_PARID value 'ZVCXI_XC_TLTIM_L' ##NO_TEXT.
  constants C_PARID_TLTIM_P type /VCXI/VKS_PARID value 'ZVCXI_XC_TLTIM_P' ##NO_TEXT.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods GET_FEXTT
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RF_FEXTT) type ZVCXI_XCS_FEXTT
    raising
      /VCXI/CX_CKX .
  methods GET_FOTUT
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RF_FOTUT) type ZVCXI_XCS_FOTUT
    raising
      /VCXI/CX_CKX .
  methods GET_LIFNR
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
      !EF_DESCR type /VCXI/CKX_DESCR
      !EF_WAERS type WAERS
    returning
      value(RF_LIFNR) type LIFNR
    raising
      /VCXI/CX_CKX .
  methods GET_MRKUP
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_MRKUP) type ZVCXI_XCS_S_MRKUP
    raising
      /VCXI/CX_CKX .
  methods GET_PRVDR_TUSER
    exporting
      !EF_DESCR type /VCXI/CKX_DESCR
      !ER_GATE_TUSER type ref to /VCXI/CL_VKSR_GATE
    returning
      value(RR_TUSER) type ref to ZIF_VCXI_XCSS_TUSER
    raising
      /VCXI/CX_CKX .
  methods GET_TCCBC
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_TCCBC) type ZVCXI_XCS_S_TCCBC
    raising
      /VCXI/CX_CKX .
  methods GET_TCEST
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
      !ES_TCEST_LOCAL type ZVCXI_XCS_S_TCEST
    returning
      value(RS_TCEST) type ZVCXI_XCS_S_TCEST
    raising
      /VCXI/CX_CKX .
  methods GET_TCINT
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_TCINT) type ZVCXI_XCS_S_TCINT
    raising
      /VCXI/CX_CKX .
  methods GET_TLTIM
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_TLTIM) type ZVCXI_XCS_S_TLTIM
    raising
      /VCXI/CX_CKX .
  methods GET_TPRCE
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
      !EF_FOVRR type ZVCXI_XCS_FOVRR
      !ES_TPRCE_LOCAL type ZVCXI_XCS_S_TPRCE
    returning
      value(RS_TPRCE) type ZVCXI_XCS_S_TPRCE
    raising
      /VCXI/CX_CKX .
  methods GET_WERKS
    returning
      value(RF_WERKS) type WERKS_D
    raising
      /VCXI/CX_CKX .
  methods SET_FEXTT
    importing
      !IF_FEXTT type ZVCXI_XCS_FEXTT
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_FOTUT
    importing
      !IF_FOTUT type ZVCXI_XCS_FOTUT
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_LIFNR
    importing
      !IF_LIFNR type LIFNR optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_MRKUP
    importing
      !IS_MRKUP type ZVCXI_XCS_S_MRKUP optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_TCCBC
    importing
      !IS_TCCBC type ZVCXI_XCS_S_TCCBC optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_TCEST
    importing
      !IS_TCEST type ZVCXI_XCS_S_TCEST optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_TLTIM
    importing
      !IS_TLTIM type ZVCXI_XCS_S_TLTIM optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_TPRCE
    importing
      !IS_TPRCE type ZVCXI_XCS_S_TPRCE optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
      !IF_FOVRR type ZVCXI_XCS_FOVRR optional
    raising
      /VCXI/CX_CKX .

  methods GET_STATUS
    redefinition .
  methods GET_STATUS_MSG
    redefinition .
  methods HANDLE_EVENT
    redefinition .
protected section.

  constants C_IEVNT_I_INIT_BY_FEXTT type /VCXI/VKSC_IEVNT value 'INIT_BY_FEXTT' ##NO_TEXT.
  constants C_IEVNT_I_INIT_BY_FOTUT type /VCXI/VKSC_IEVNT value 'INIT_BY_FOTUT' ##NO_TEXT.
  constants C_IEVNT_I_RECALC_MRKUP type /VCXI/VKSC_IEVNT value 'RECALC_MRKUP' ##NO_TEXT.
  constants C_IEVNT_I_RECALC_TCINT type /VCXI/VKSC_IEVNT value 'RECALC_TCINT' ##NO_TEXT.
  constants C_IEVNT_I_RECALC_TPRCE type /VCXI/VKSC_IEVNT value 'RECALC_TPRCE' ##NO_TEXT.
  constants C_IEVNT_I_SET_DEFAULT type /VCXI/VKSC_IEVNT value 'SET_DEFAULT' ##NO_TEXT.
  constants C_IEVNT_O_TPRCE_CHANGED type /VCXI/VKSC_IEVNT value 'TPRCE_CHANGED' ##NO_TEXT.
  constants C_PARID_TCINT type /VCXI/VKS_PARID value 'ZVCXI_XC_TCINT' ##NO_TEXT.
  constants C_PARID_TPRCE type /VCXI/VKS_PARID value 'ZVCXI_XC_TPRCE' ##NO_TEXT.
  data F_PARID_TLTIM type /VCXI/VKS_PARID .

  methods CHECK_LIFNR
    importing
      !IR_MESSAGE type ref to /VCXI/CL_CKXM_MESSAGE optional
    returning
      value(RF_ELMST) type /VCXI/VKSR_ELMST
    raising
      /VCXI/CX_CKX .
  methods GET_KUNAG
    exporting
      !EF_WAERS type WAERS
    returning
      value(RF_KUNAG) type KUNAG
    raising
      /VCXI/CX_CKX .
  methods HANDLE_INIT_BY_FEXTT
    raising
      /VCXI/CX_CKX .
  methods HANDLE_INIT_BY_FOTUT
    raising
      /VCXI/CX_CKX .
  methods HANDLE_RECALC_MRKUP
    raising
      /VCXI/CX_CKX .
  methods HANDLE_RECALC_TCINT
    raising
      /VCXI/CX_CKX .
  methods HANDLE_RECALC_TPRCE
    raising
      /VCXI/CX_CKX .
  methods HANDLE_SET_DEFAULT
    raising
      /VCXI/CX_CKX .
  methods SET_TCINT
    importing
      !IS_TCINT type ZVCXI_XCS_S_TCINT
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
private section.

  methods INIT_PARID
    raising
      /VCXI/CX_CKX .
endclass. "ZCL_VCXI_XCSS_PR_TINFO definition
class ZCL_VCXI_XCSS_PR_TINFO implementation.
  method CHECK_LIFNR.

    data: LF_LIFNR type LIFNR.


***----------------------------------------------------------------------------
*** Get Vendor and check if maintained
    LF_LIFNR =  ME->GET_LIFNR( ).
    if LF_LIFNR is initial.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G.
      exit.
    endif.

***----------------------------------------------------------------------------
*** Perform Check
    try.
        ZCL_VCXI_XCS_SERVICE=>CHECK_LIFNR( IF_LIFNR = LF_LIFNR
                                           IF_WERKS = ME->GET_WERKS( ) ).
      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
        if IR_MESSAGE is bound.
          IR_MESSAGE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
        endif.
        exit.
    endtry.

***----------------------------------------------------------------------------
*** Check was successful
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G.
    if IR_MESSAGE is bound.
      IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_XCSS'
                               IF_MSGTY = 'S'
                               IF_MSGNO = '008' ).
    endif.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_GATE = IR_GATE ).

*** Initialize dimension-based Parameter ID
    ME->INIT_PARID( ).

  endmethod.
  method GET_FEXTT.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

***----------------------------------------------------------------------------
*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_FEXTT ).
    RF_FEXTT = LS_PARAM-ATWRT.
    EF_SETBY = LS_PARAM-SETBY.

  endmethod.
  method GET_FOTUT.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

***----------------------------------------------------------------------------
*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_FOTUT ).
    RF_FOTUT = LS_PARAM-ATWRT.
    EF_SETBY = LS_PARAM-SETBY.

  endmethod.
  method GET_KUNAG.

    data: LR_APPDATA type ref to ZCL_VCXI_XCS_APPDATA.


*** Get Application Data
    try.
        LR_APPDATA = cast #( ME->R_GATE->GET_APPDATA( ) ).
      catch CX_SY_MOVE_CAST_ERROR.
    endtry.
    check LR_APPDATA is bound.

*** Return Sold-to Party
    RF_KUNAG = LR_APPDATA->S_KNVV_AG-KUNNR.
    EF_WAERS = LR_APPDATA->S_KNVV_AG-WAERS.

  endmethod.
  method GET_LIFNR.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

***----------------------------------------------------------------------------
*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_LIFNR ).
    RF_LIFNR = LS_PARAM-ATWRT.
    EF_SETBY = LS_PARAM-SETBY.

    check RF_LIFNR is not initial.

***----------------------------------------------------------------------------
    if EF_DESCR is requested.
      EF_DESCR = ZCL_VCXI_XCS_SERVICE=>GET_LIFNR_NAME1( IF_LIFNR = RF_LIFNR ).
    endif.

    if EF_WAERS is requested.
      select single WAERS
             from LFM1
             into @EF_WAERS
            where LIFNR eq @RF_LIFNR.
    endif.

  endmethod.
  method GET_MRKUP.

    data: LS_PARAM type /VCXI/VKSR_S_PARAM.

***----------------------------------------------------------------------------
*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_MRKUP ).
    RS_MRKUP = value #( MRKUP     = LS_PARAM-ATFLV
                        MRKUP_UOM = LS_PARAM-UNIT ).
    EF_SETBY = LS_PARAM-SETBY.

  endmethod.
  method GET_PRVDR_TUSER.

    data: LT_GATE         type        /VCXI/VKSR_TT_GATE.
    data: LR_GATE         type ref to /VCXI/CL_VKSR_GATE.


***--------------------------------------------------------------------------------------
*** Get Provider
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZIF_VCXI_XCSS_TUSER=>C_IPRVT_XCS_TUSER
                                     IF_INUSE = ABAP_TRUE ).

***--------------------------------------------------------------------------------------
*** Get Reference
    loop at LT_GATE into LR_GATE.
      try.
          move LR_GATE->R_CLSPR ?to RR_TUSER.
          check RR_TUSER is bound.
          move LR_GATE to ER_GATE_TUSER.
          exit.

        catch CX_SY_MOVE_CAST_ERROR.
          clear: RR_TUSER, ER_GATE_TUSER.
      endtry.
    endloop.

***--------------------------------------------------------------------------------------
    check RR_TUSER is bound.

    if EF_DESCR is requested.
      EF_DESCR = RR_TUSER->GET_DESCR( ).
    endif.

  endmethod.
  method GET_STATUS.

    RF_ELMST = SUPER->GET_STATUS( ).

***----------------------------------------------------------------------------
*** Check Vendor
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = ME->CHECK_LIFNR( ) ).

**** Check Price
*    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
*                                                   IF_ELMST2 = ME->CHECK_PRICE( ) ).

  endmethod.
  method GET_STATUS_MSG.

    SUPER->GET_STATUS_MSG( IR_MESSAGE = IR_MESSAGE ).

***----------------------------------------------------------------------------
*** Check Vendor
    ME->CHECK_LIFNR( IR_MESSAGE = IR_MESSAGE ).

**** Check Price
*    ME->CHECK_PRICE( IR_MESSAGE = IR_MESSAGE ).

  endmethod.
  method GET_TCCBC.

    data: LS_PARAM type /VCXI/VKSR_S_PARAM.

***----------------------------------------------------------------------------
*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TCCBC ).
    RS_TCCBC = value #( TCCBC     = LS_PARAM-ATFLV
                        TCCBC_UOM = LS_PARAM-UNIT ).
    EF_SETBY = LS_PARAM-SETBY.

  endmethod.
  method GET_TCEST.

    data: LF_DCONF type /VCXI/VKCR_DCONF.
    data: LS_PARAM type /VCXI/VKSR_S_PARAM.


***----------------------------------------------------------------------------
*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TCEST ).
    RS_TCEST = value #( TCEST      = LS_PARAM-ATFLV
                        TCEST_CURR = LS_PARAM-CURKY ).
    EF_SETBY = LS_PARAM-SETBY.


***----------------------------------------------------------------------------
*** Get in Local Currency (of Company Code)
    if ES_TCEST_LOCAL is requested.
      ME->R_GATE->GET_CONFIG_INFO( importing EF_DCONF = LF_DCONF
                                             EF_WAERS = ES_TCEST_LOCAL-TCEST_CURR ).
      if RS_TCEST-TCEST_CURR is not initial.
        call function 'CONVERT_TO_LOCAL_CURRENCY'
          exporting
            DATE             = LF_DCONF
            FOREIGN_AMOUNT   = RS_TCEST-TCEST
            FOREIGN_CURRENCY = RS_TCEST-TCEST_CURR
            LOCAL_CURRENCY   = ES_TCEST_LOCAL-TCEST_CURR
          importing
            LOCAL_AMOUNT     = ES_TCEST_LOCAL-TCEST
          exceptions
            NO_RATE_FOUND    = 1
            OVERFLOW         = 2
            NO_FACTORS_FOUND = 3
            NO_SPREAD_FOUND  = 4
            DERIVED_2_TIMES  = 5.
        if SY-SUBRC ne 0.
          /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
        endif.
      else.
        ES_TCEST_LOCAL-TCEST = 0.
      endif.
    endif.

  endmethod.
  method GET_TCINT.

    data: LS_PARAM type /VCXI/VKSR_S_PARAM.

***----------------------------------------------------------------------------
*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TCINT ).
    RS_TCINT = value #( TCINT      = LS_PARAM-ATFLV
                        TCINT_CURR = LS_PARAM-CURKY ).
    EF_SETBY = LS_PARAM-SETBY.

  endmethod.
  method GET_TLTIM.

    data: LS_PARAM type /VCXI/VKSR_S_PARAM.

***----------------------------------------------------------------------------
*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = ME->F_PARID_TLTIM ).
    RS_TLTIM = value #( TLTIM     = LS_PARAM-ATFLV
                        TLTIM_UOM = LS_PARAM-UNIT ).
    EF_SETBY = LS_PARAM-SETBY.

  endmethod.
  method GET_TPRCE.

    data: LF_DCONF type /VCXI/VKCR_DCONF.
    data: LS_PARAM type /VCXI/VKSR_S_PARAM.


***----------------------------------------------------------------------------
*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TPRCE ).
    RS_TPRCE = value #( TPRCE      = LS_PARAM-ATFLV
                        TPRCE_CURR = LS_PARAM-CURKY ).
    EF_SETBY = LS_PARAM-SETBY.

    EF_FOVRR = switch #( EF_SETBY when /VCXI/CL_VKSR_GATE=>C_SETBY_USER then ABAP_TRUE ).


***----------------------------------------------------------------------------
*** Get in Local Currency (of Company Code)
    if ES_TPRCE_LOCAL is requested.
      ME->R_GATE->GET_CONFIG_INFO( importing EF_DCONF = LF_DCONF
                                             EF_WAERS = ES_TPRCE_LOCAL-TPRCE_CURR ).
      if RS_TPRCE-TPRCE_CURR is not initial.
        call function 'CONVERT_TO_LOCAL_CURRENCY'
          exporting
            DATE             = LF_DCONF
            FOREIGN_AMOUNT   = RS_TPRCE-TPRCE
            FOREIGN_CURRENCY = RS_TPRCE-TPRCE_CURR
            LOCAL_CURRENCY   = ES_TPRCE_LOCAL-TPRCE_CURR
          importing
            LOCAL_AMOUNT     = ES_TPRCE_LOCAL-TPRCE
          exceptions
            NO_RATE_FOUND    = 1
            OVERFLOW         = 2
            NO_FACTORS_FOUND = 3
            NO_SPREAD_FOUND  = 4
            DERIVED_2_TIMES  = 5.
        if SY-SUBRC ne 0.
          /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
        endif.
      else.
        ES_TPRCE_LOCAL-TPRCE = 0.
      endif.
    endif.

  endmethod.
  method GET_WERKS.

    RF_WERKS = ME->R_GATE->GET_APPDATA( )->F_WERKS.

  endmethod.
  method HANDLE_EVENT.

    SUPER->HANDLE_EVENT( IF_IEVNT         = IF_IEVNT
                         IT_GATE_PROVIDER = IT_GATE_PROVIDER ).

    case IF_IEVNT.
      when C_IEVNT_I_SET_DEFAULT.
        ME->HANDLE_SET_DEFAULT( ).

      when C_IEVNT_I_INIT_BY_FOTUT.
        ME->HANDLE_INIT_BY_FOTUT( ).
      when C_IEVNT_I_INIT_BY_FEXTT.
        ME->HANDLE_INIT_BY_FEXTT( ).

      when C_IEVNT_I_RECALC_MRKUP.
        ME->HANDLE_RECALC_MRKUP( ).
      when C_IEVNT_I_RECALC_TPRCE.
        ME->HANDLE_RECALC_TPRCE( ).
      when C_IEVNT_I_RECALC_TCINT.
        ME->HANDLE_RECALC_TCINT( ).

    endcase.

  endmethod.
  method HANDLE_INIT_BY_FEXTT.

*** Initialize if marked as External Tool
    if ME->GET_FEXTT( ).
      ME->SET_TCEST( IF_SETBY = SPACE ).
      ME->SET_MRKUP( IF_SETBY = SPACE ).
      ME->SET_TPRCE( IF_SETBY = SPACE ).
      ME->SET_TCCBC( IF_SETBY = SPACE ).
      ME->SET_LIFNR( IF_SETBY = SPACE ).
    endif.

  endmethod.
  method HANDLE_INIT_BY_FOTUT.

*** Initialize if marked as One-Time Usage Tool
    if ME->GET_FOTUT( ).
      ME->SET_TLTIM( IF_SETBY = SPACE ).
    endif.

  endmethod.
  method HANDLE_RECALC_MRKUP.

    data: LF_FOVRR type ZVCXI_XCS_FOVRR.
    data: LS_MRKUP type ZVCXI_XCS_S_MRKUP.


*** Check Tool Price has been overwritten
     ME->GET_TPRCE( importing EF_FOVRR       = LF_FOVRR
                              ES_TPRCE_LOCAL = data(LS_TPRCE_LOCAL) ).
    check LF_FOVRR eq ABAP_TRUE.

*** Markup = Tool Price / Estimated Cost
    ME->GET_TCEST( importing ES_TCEST_LOCAL = data(LS_TCEST_LOCAL) ).
    if LS_TCEST_LOCAL-TCEST ne 0.
      try.
          LS_MRKUP-MRKUP = ( ( LS_TPRCE_LOCAL-TPRCE / LS_TCEST_LOCAL-TCEST ) - 1 ) * 100.
        catch CX_ROOT.
          LS_MRKUP-MRKUP = '999.99'.    "Maximum
      endtry.
    else.
      LS_MRKUP-MRKUP = 0.
    endif.
    if LS_MRKUP-MRKUP lt 0.
      LS_MRKUP-MRKUP = 0.
    endif.
    LS_MRKUP-MRKUP_UOM = '%'.

    ME->SET_MRKUP( IS_MRKUP = LS_MRKUP ).

  endmethod.
  method HANDLE_RECALC_TCINT.

*** Get Tool Costs and Price in Local Currency
    ME->GET_TCEST( importing ES_TCEST_LOCAL = data(LS_TCEST_LOCAL) ).
    ME->GET_TPRCE( importing ES_TPRCE_LOCAL = data(LS_TPRCE_LOCAL) ).
    data(LS_TCCBC) = ME->GET_TCCBC( ).

*** Internal Tool Costs = Estimated Cost – (Tool Price * Customer %)
    data(LS_TCINT) = value ZVCXI_XCS_S_TCINT( TCINT      = LS_TCEST_LOCAL-TCEST - ( LS_TPRCE_LOCAL-TPRCE * LS_TCCBC-TCCBC / 100 )
                                              TCINT_CURR = LS_TCEST_LOCAL-TCEST_CURR ).
    if LS_TCINT-TCINT lt 0.
      LS_TCINT-TCINT = 0.
    endif.

    ME->SET_TCINT( IS_TCINT = LS_TCINT ).

  endmethod.
  method HANDLE_RECALC_TPRCE.

    data: LF_FOVRR type ZVCXI_XCS_FOVRR,
          LF_DCONF type /VCXI/VKCR_DCONF.
    data: LS_TPRCE_LOCAL type ZVCXI_XCS_S_TPRCE,
          LS_TPRCE       type ZVCXI_XCS_S_TPRCE.


*** Check Tool Price has not been overwritten
    ME->GET_TPRCE( importing EF_FOVRR = LF_FOVRR ).
    check LF_FOVRR ne ABAP_TRUE.

***----------------------------------------------------------------------------
*** Tool Price = Estimated Cost + Markup
    ME->GET_TCEST( importing ES_TCEST_LOCAL = data(LS_TCEST_LOCAL) ).
    LS_TPRCE_LOCAL = value #( TPRCE      = LS_TCEST_LOCAL-TCEST * ( 1 + ME->GET_MRKUP( )-MRKUP / 100 )
                              TPRCE_CURR = LS_TCEST_LOCAL-TCEST_CURR ).

***----------------------------------------------------------------------------
*** Convert to Customer Currency
    ME->GET_KUNAG( importing EF_WAERS = LS_TPRCE-TPRCE_CURR ).
    if LS_TPRCE-TPRCE_CURR is not initial.
      ME->R_GATE->GET_CONFIG_INFO( importing EF_DCONF = LF_DCONF ).
      call function 'CONVERT_TO_FOREIGN_CURRENCY'
        exporting
          DATE             = LF_DCONF
          FOREIGN_CURRENCY = LS_TPRCE-TPRCE_CURR
          LOCAL_AMOUNT     = LS_TPRCE_LOCAL-TPRCE
          LOCAL_CURRENCY   = LS_TPRCE_LOCAL-TPRCE_CURR
        importing
          FOREIGN_AMOUNT   = LS_TPRCE-TPRCE
        exceptions
          NO_RATE_FOUND    = 1
          OVERFLOW         = 2
          NO_FACTORS_FOUND = 3
          NO_SPREAD_FOUND  = 4
          DERIVED_2_TIMES  = 5.
      if SY-SUBRC ne 0.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
      endif.
    else.
      LS_TPRCE = LS_TPRCE_LOCAL.
    endif.

*** Set new Tool Price
    ME->SET_TPRCE( IS_TPRCE = LS_TPRCE ).

  endmethod.
  method HANDLE_SET_DEFAULT.

    ZCL_VCXI_XCDR_SERVICE=>SET_DEFAULT( IR_GATE = ME->R_GATE ).

  endmethod.
  method INIT_PARID.

    data:  LS_PARAM    type /VCXI/VKSR_S_PARAM.


    loop at ME->R_GATE->GET_PARAM_ALL( ) into LS_PARAM
                    where PARTY eq /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC
                     and  ( PARID eq C_PARID_TLTIM_P or
                            PARID eq C_PARID_TLTIM_L ).
      move LS_PARAM-PARID to ME->F_PARID_TLTIM.
      exit.
    endloop.

  endmethod.
  method SET_FEXTT.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_FEXTT ).

    move IF_FEXTT to LS_PARAM-ATWRT.
    move IF_SETBY to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_FEXTT_CHANGED ).
    endif.

  endmethod.
  method SET_FOTUT.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_FOTUT ).

    move IF_FOTUT to LS_PARAM-ATWRT.
    move IF_SETBY to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_FOTUT_CHANGED ).
    endif.

  endmethod.
  method SET_LIFNR.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_LIFNR ).

    if IF_LIFNR is supplied.
      LS_PARAM-ATWRT = IF_LIFNR.
    endif.
    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATWRT.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_LIFNR_CHANGED ).
    endif.

  endmethod.
  method SET_MRKUP.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_MRKUP ).

    if IS_MRKUP is supplied.
      LS_PARAM-ATFLV = IS_MRKUP-MRKUP.
      LS_PARAM-UNIT  = IS_MRKUP-MRKUP_UOM.
    endif.
    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_MRKUP_CHANGED ).
    endif.

  endmethod.
  method SET_TCCBC.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TCCBC ).

    if IS_TCCBC is supplied.
      LS_PARAM-ATFLV = IS_TCCBC-TCCBC.
      LS_PARAM-UNIT  = IS_TCCBC-TCCBC_UOM.
    endif.
    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_TCCBC_CHANGED ).
    endif.

  endmethod.
  method SET_TCEST.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TCEST ).

    if IS_TCEST is supplied.
      LS_PARAM-ATFLV = IS_TCEST-TCEST.
      LS_PARAM-CURKY = IS_TCEST-TCEST_CURR.
    endif.
    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_TCEST_CHANGED ).
    endif.

  endmethod.
  method SET_TCINT.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TCINT ).

    LS_PARAM-ATFLV = IS_TCINT-TCINT.
    LS_PARAM-CURKY = IS_TCINT-TCINT_CURR.
    LS_PARAM-SETBY = IF_SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

*    if LF_CHANGED eq ABAP_TRUE.
****   Send Event
*      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_TCINT_CHANGED ).
*    endif.

  endmethod.
  method SET_TLTIM.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = ME->F_PARID_TLTIM ).

    if IS_TLTIM is supplied.
      LS_PARAM-ATFLV = IS_TLTIM-TLTIM.
      LS_PARAM-UNIT  = IS_TLTIM-TLTIM_UOM.
    endif.
    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

*    if LF_CHANGED eq ABAP_TRUE.
****   Send Event
*      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_TLTIM_CHANGED ).
*    endif.

  endmethod.
  method SET_TPRCE.

    data: LF_SETBY   type /VCXI/VKSR_SETBY,
          LF_CHANGED type ABAP_BOOL.
    data: LS_PARAM   type /VCXI/VKSR_S_PARAM.


*** Identify correct SETBY
    if IF_FOVRR is supplied.
      LF_SETBY = switch #( IF_FOVRR when ABAP_TRUE then /VCXI/CL_VKSR_GATE=>C_SETBY_USER
                                                   else SPACE ).
    else.
      LF_SETBY = IF_SETBY.
    endif.

*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_TPRCE ).

    if IS_TPRCE is supplied.
      LS_PARAM-ATFLV = IS_TPRCE-TPRCE.
      LS_PARAM-CURKY = IS_TPRCE-TPRCE_CURR.
    endif.
    LS_PARAM-SETBY = LF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_TPRCE_CHANGED ).
    endif.

  endmethod.
