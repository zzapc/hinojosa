
class ZCL_VCXI_FFSP_UI_ADDON_LAMI definition
  public
  inheriting from ZCL_VCXI_XCSP_UI_ADDON
  create public .

public section.

  data S_ADDON_LAMI_VIS type ZVCXI_FFSP_S_ADDON_LAMI_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  data R_GRID_MC_GLUE type ref to ZCL_VCXI_FFSP_GRID_MC_GLUE .
  data R_GRID_MC_RECI type ref to ZCL_VCXI_FFSP_GRID_MC_RECI .
  data R_PR_ADDON_LAMI type ref to ZCL_VCXI_FFSP_PR_ADDON_LAMI .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_FFSP_UI_ADDON_LAMI definition
class ZCL_VCXI_FFSP_UI_ADDON_LAMI implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_FFSP_UI_ADDON_LAMI_BIND'
      exporting
        IR_UI_ADDON_LAMI = ME
      importing
        ES_DYNP          = ME->S_DYNP.

*** Create Grid for GRID_MC_GLUE
    create object ME->R_GRID_MC_GLUE
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_MC_GLUE'.

*** Create Grid for GRID_MC_RECI
    create object ME->R_GRID_MC_RECI
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_MC_RECI'.

  endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                      IR_UIGATE = IR_UIGATE
                      IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
  try.
      move IR_GATE->R_CLSPR ?to ME->R_PR_ADDON_LAMI.
    catch CX_SY_MOVE_CAST_ERROR.
***   UI ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '801'.
  endtry.

endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

    if ME->R_GRID_MC_GLUE is bound.
      ME->R_GRID_MC_GLUE->DO_FREE( ).
      clear ME->R_GRID_MC_GLUE.
    endif.

    if ME->R_GRID_MC_RECI is bound.
      ME->R_GRID_MC_RECI->DO_FREE( ).
      clear ME->R_GRID_MC_RECI.
    endif.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Super PBO
    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize Grid
    if ME->R_GRID_MC_GLUE is bound.
      ME->R_GRID_MC_GLUE->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_MC_GLUE->REFRESH_GRID( ).
    endif.

    if ME->R_GRID_MC_RECI is bound.
      ME->R_GRID_MC_RECI->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_MC_RECI->REFRESH_GRID( ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    if ME->R_GRID_MC_GLUE is bound.
      ME->R_GRID_MC_GLUE->CHECK_CHANGED_DATA( ).
    endif.

    if ME->R_GRID_MC_RECI is bound.
      ME->R_GRID_MC_RECI->CHECK_CHANGED_DATA( ).
    endif.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_ADDON_LAMI.

  endmethod.
  method INIT_VIS.

*** Super
    SUPER->INIT_VIS( ).

    free: ME->S_ADDON_LAMI_VIS.
    ME->S_ADDON_LAMI_VIS-S_ADDON_VIS = ME->S_ADDON_VIS.

  endmethod.
