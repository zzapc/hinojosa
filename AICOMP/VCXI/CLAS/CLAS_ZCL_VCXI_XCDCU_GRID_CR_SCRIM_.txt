
class ZCL_VCXI_XCDCU_GRID_CR_SCRIM definition
  public
  inheriting from ZCL_VCXI_XCDCU_GRID
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IF_TABNAME type TABNAME optional
      !IF_CONTAINER_NAME type SCRFNAME optional
      !IR_CUSTOMIZING type ref to OBJECT
    raising
      /VCXI/CX_CKX .
protected section.

  types:
    begin of YS_OSVIS,
                  OSVIS       type ZVCXI_XCD_OSVIS,
                  OSVIS_DESCR type /VCXI/CKX_DESCR,
         end of YS_OSVIS .
  types:
    YT_OSVIS type standard table of YS_OSVIS with default key .

  constants C_FUNC_ADD type UI_FUNC value 'ADD'. "#EC NOTEXT
  constants C_FUNC_DEL type UI_FUNC value 'DELETE'. "#EC NOTEXT
  data R_CONDREC type ref to ZCL_VCXI_XCDC_CONDREC .
  data T_OSVIS type YT_OSVIS .

  methods ADD_SCRGR
    raising
      /VCXI/CX_CKX .
  methods DELETE_SCRGR
    importing
      !IT_CR_SIMPACT type ZVCXI_XCDC_TT_CR_SIMPACT
    raising
      /VCXI/CX_CKX .
  methods CHECK_SEL_DELETE
    returning
      value(RF_OKAY) type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
  methods HANDLE_TOOLBAR
    for event TOOLBAR of CL_GUI_ALV_GRID
    importing
      !E_OBJECT
      !E_INTERACTIVE .
  methods HANDLE_USER_COMMAND
    for event USER_COMMAND of CL_GUI_ALV_GRID
    importing
      !E_UCOMM .
  methods INIT_OSVIS_DESCR
    raising
      /VCXI/CX_CKX .

  methods GET_FIELDCAT
    redefinition .
  methods GET_GRID_DATA
    redefinition .
  methods GET_LAYOUT
    redefinition .
  methods HANDLE_DATA_CHANGED_FINISHED
    redefinition .
  methods INIT_LOCKED
    redefinition .
  methods SET_EVENT
    redefinition .
private section.
endclass. "ZCL_VCXI_XCDCU_GRID_CR_SCRIM definition
class ZCL_VCXI_XCDCU_GRID_CR_SCRIM implementation.
method ADD_SCRGR.

  data:          LS_RETURN         type                   DDSHRETVAL,
                 LS_CD_SCRIM       type                   ZVCXI_XCDC_S_REGDVC_SCRGR,
                 LS_CR_SCRIM       type                   ZVCXI_XCDC_S_CR_SIMPACT.
  data:          LT_RETURN         type table of          DDSHRETVAL,
                 LT_CD_SCRGR_SHLP  type standard table of ZVCXI_XCDCU_S_CD_SCRGR_SHLP.
  field-symbols: <S_CD_SCRGR_SHLP> type                   ZVCXI_XCDCU_S_CD_SCRGR_SHLP.

  check ME->R_CONDREC                      is bound and
        ME->R_CONDREC->R_CONDDEF           is bound and
        ME->R_CONDREC->R_CONDDEF->R_REGDVC is bound.
  loop at ME->R_CONDREC->R_CONDDEF->R_REGDVC->ZIF_VCXI_XCDC_REGDVC_SCRGR~T_REGDVC_SCRGR into LS_CD_SCRIM.
*** Show only SCRIMs which are not already used.
    read table ME->R_CONDREC->T_CR_SIMPACT
    with key ISCGR = LS_CD_SCRIM-ISCGR
    transporting no fields.
    check SY-SUBRC ne 0.

    append initial line to LT_CD_SCRGR_SHLP assigning <S_CD_SCRGR_SHLP>.
    move LS_CD_SCRIM-ISCGR to <S_CD_SCRGR_SHLP>-ISCGR.
    move LS_CD_SCRIM-DESCR to <S_CD_SCRGR_SHLP>-ISCGR_DESCR.

  endloop.

  sort LT_CD_SCRGR_SHLP by ISCGR_DESCR.

  call function 'F4IF_INT_TABLE_VALUE_REQUEST'
    exporting
      DDIC_STRUCTURE  = 'ZVCXI_XCDCU_S_CD_SCRGR_SHLP'
      RETFIELD        = 'ISCGR'
      VALUE_ORG       = 'S'
      MULTIPLE_CHOICE = 'X'
    tables
      VALUE_TAB       = LT_CD_SCRGR_SHLP
      RETURN_TAB      = LT_RETURN
    exceptions
      PARAMETER_ERROR = 1
      NO_VALUES_FOUND = 2
      others          = 3.
  if SY-SUBRC ne 0.
    /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
  endif.

*** Process Result
  loop at LT_RETURN into LS_RETURN.
    clear: LS_CR_SCRIM-ISCGR.
    move  LS_RETURN-FIELDVAL to LS_CR_SCRIM-ISCGR.

*** Add Default Element
    ME->R_CONDREC->ADD_CR_SIMPACT( IS_CR_SIMPACT = LS_CR_SCRIM ).

  endloop.

endmethod.
method CHECK_SEL_DELETE.

  data: LT_CELL type LVC_T_CELL,
        LT_ROW  type LVC_T_ROW.

  ME->R_GRID->GET_SELECTED_CELLS( importing ET_CELL = LT_CELL ).
  ME->R_GRID->GET_SELECTED_ROWS( importing ET_INDEX_ROWS = LT_ROW ).

*** Delete is only available if Cells are selected
  if lines( LT_CELL ) gt 0 or
     lines( LT_ROW )  gt 0.
    move ABAP_TRUE to RF_OKAY.
  else.
    move ABAP_FALSE to RF_OKAY.
  endif.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IF_TABNAME        = 'ZVCXI_XCDCU_S_CR_SCRIM_GRID'
                      IF_CONTAINER_NAME = '0110CC_GRID_SCRIM'
                      IR_CUSTOMIZING    = IR_CUSTOMIZING ).


*** Try to Assign Customizing Reference
  try.
      move IR_CUSTOMIZING ?to ME->R_CONDREC.
    catch CX_SY_MOVE_CAST_ERROR.
***   Condition Record Customizing Reference can't be assigned.
      /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE(
          IF_MSGTY = 'E'
          IF_MSGID = 'ZVCXI_XCDC'
          IF_MSGNO = '252' ).
  endtry.

  check ME->R_CONDREC is bound.
  ME->F_LOCKED = ME->R_CONDREC->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ).

*** Init Visibility Options - Description
  ME->INIT_OSVIS_DESCR( ).

endmethod.
method DELETE_SCRGR.

  data: LS_CR_SIMPACT type ZVCXI_XCDC_S_CR_SIMPACT.

  loop at IT_CR_SIMPACT into LS_CR_SIMPACT.
*** Delete by ID
    ME->R_CONDREC->DELETE_CR_SIMPACT( IS_CR_SIMPACT = LS_CR_SIMPACT ).

  endloop.

endmethod.
method GET_FIELDCAT.

  field-symbols: <S_FCAT>           type        LVC_S_FCAT.

***----------------------------------------------------------------------------
*** Get Standard Field Catalog
  RT_FCAT = SUPER->GET_FIELDCAT( ).

***----------------------------------------------------------------------------
*** Adjust Field Catalog
  loop at RT_FCAT assigning <S_FCAT>.
    case <S_FCAT>-FIELDNAME.
      when 'ISCGR'.
        move ABAP_TRUE    to <S_FCAT>-TECH.

      when 'SSCGR' or 'OSVIS'.
        if ME->F_LOCKED eq ABAP_TRUE.
          move ABAP_TRUE to <S_FCAT>-EDIT.
        endif.

      when 'OSOBL'.
        move ABAP_TRUE to <S_FCAT>-CHECKBOX.
        if ME->F_LOCKED eq ABAP_TRUE.
          move ABAP_TRUE to <S_FCAT>-EDIT.
        endif.
    endcase.
  endloop.

endmethod.
method GET_GRID_DATA.

  data:          LS_CR_SCRIM_GRID     type          ZVCXI_XCDCU_S_CR_SCRIM_GRID,
                 LS_CD_SCRIM          type          ZVCXI_XCDC_S_REGDVC_SCRGR,
                 LS_CR_SCRIM          type          ZVCXI_XCDC_S_CR_SIMPACT,
                 LS_OSVIS             type          YS_OSVIS.
  data:          LT_CR_SCRIM          type          ZVCXI_XCDC_TT_CR_SIMPACT.
  field-symbols: <T_DATA>             type standard table,
                 <S_DATA>             type          any.

*** Dereferencing of Data Reference
  assign IR_DATA->* to <T_DATA>.

  free: <T_DATA>.

*** --------------------------------------------------------------------------------------------
*** Get all Condition Values
  move ME->R_CONDREC->T_CR_SIMPACT to LT_CR_SCRIM.
  sort LT_CR_SCRIM by SSCGR.
  loop at LT_CR_SCRIM into LS_CR_SCRIM.
    clear: LS_CR_SCRIM_GRID.

    move-corresponding LS_CR_SCRIM to LS_CR_SCRIM_GRID.
*** Get Description of Registry DVC.
    read table ME->R_CONDREC->R_CONDDEF->R_REGDVC->ZIF_VCXI_XCDC_REGDVC_SCRGR~T_REGDVC_SCRGR
      with key ISCGR = LS_CR_SCRIM_GRID-ISCGR
      into LS_CD_SCRIM.

*** Get Description
    move LS_CD_SCRIM-DESCR to LS_CR_SCRIM_GRID-ISCGR_DESCR.

*** Get Description of Handling Option - Override
    read table ME->T_OSVIS with key OSVIS = LS_CR_SCRIM_GRID-OSVIS into LS_OSVIS.
    if SY-SUBRC = 0.
      move LS_OSVIS-OSVIS_DESCR to LS_CR_SCRIM_GRID-OSVIS_DESCR.
    endif.

*** --------------------------------------------------------------------------------------------
*** Add Entry
    append initial line to <T_DATA> assigning <S_DATA>.
    move-corresponding LS_CR_SCRIM_GRID to <S_DATA>.

  endloop.

  sort <T_DATA> by ('ISCGR_DESCR').

endmethod.
method GET_LAYOUT.

  RS_LAYOUT = SUPER->GET_LAYOUT( ).

*** Set Toolbar to Activ
  move ABAP_FALSE to RS_LAYOUT-NO_TOOLBAR.
  move 'A'        to RS_LAYOUT-SEL_MODE.

endmethod.
method HANDLE_DATA_CHANGED_FINISHED.

  data:          LS_CR_SIMPACT        type          ZVCXI_XCDC_S_CR_SIMPACT.
  data:          LR_CX_CKX            type ref to   /VCXI/CX_CKX.
  field-symbols: <T_DATA>             type standard table,
                 <S_DATA>             type          any.

  check E_MODIFIED eq ABAP_TRUE.

  try.

      SUPER->HANDLE_DATA_CHANGED_FINISHED( exporting E_MODIFIED    = E_MODIFIED
                                                     ET_GOOD_CELLS = ET_GOOD_CELLS ).

***   Dereferencing of Data Reference
      assign ME->R_DATA->* to <T_DATA>.

      loop at <T_DATA> assigning <S_DATA>.

        read table ET_GOOD_CELLS with key ROW_ID = SY-TABIX transporting no fields.
        check SY-SUBRC eq 0.
        move-corresponding <S_DATA> to LS_CR_SIMPACT.
        ME->R_CONDREC->UPDATE_CR_SIMPACT( IS_CR_SIMPACT = LS_CR_SIMPACT ).
      endloop.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      LR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

*** Request Refresh for ALV
  move ABAP_TRUE to ME->F_REFRESH_REQ.

endmethod.
method HANDLE_TOOLBAR.

  data:  LS_TOOLBAR      type        STB_BUTTON.
  data:  LF_DISABLED     type        ABAP_BOOL.
  data:  LR_CX_CKX       type ref to /VCXI/CX_CKX.

  try.

      if ME->R_CONDREC->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ) eq ABAP_FALSE.
        move ABAP_TRUE to LF_DISABLED.
      endif.

***----------------------------------------------------------------------------
***   Insert Toolbar elements Add and Delete
      clear LS_TOOLBAR.
      move C_FUNC_ADD                 to LS_TOOLBAR-FUNCTION.
      move 'Add'(002)                 to LS_TOOLBAR-TEXT.
      move C_BTYPE_BUTTON             to LS_TOOLBAR-BUTN_TYPE.
      move ICON_INSERT_ROW            to LS_TOOLBAR-ICON.
      move LF_DISABLED                to LS_TOOLBAR-DISABLED.
      append LS_TOOLBAR               to E_OBJECT->MT_TOOLBAR.

      clear LS_TOOLBAR.
      move C_FUNC_DEL                 to LS_TOOLBAR-FUNCTION.
      move 'Delete'(003)              to LS_TOOLBAR-TEXT.
      move C_BTYPE_BUTTON             to LS_TOOLBAR-BUTN_TYPE.
      move ICON_DELETE_ROW            to LS_TOOLBAR-ICON.
      move LF_DISABLED                to LS_TOOLBAR-DISABLED.
      append LS_TOOLBAR               to E_OBJECT->MT_TOOLBAR.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      LR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

endmethod.
method HANDLE_USER_COMMAND.

  data:          LS_CELL         type          LVC_S_CELL,
                 LS_ROW          type          LVC_S_ROW.
  data:          LT_CELL         type          LVC_T_CELL,
                 LT_ROW          type          LVC_T_ROW,
                 LT_CR_SIMPACT   type          ZVCXI_XCDC_TT_CR_SIMPACT.
  data:          LR_CX_CKX       type ref to   /VCXI/CX_CKX.
  field-symbols: <T_DATA>        type standard table,
                 <S_CR_SIMPACT>  type          ZVCXI_XCDC_S_CR_SIMPACT,
                 <S_DATA>        type          any.

  try.

***   Get Value Data
      assign ME->R_DATA->* to <T_DATA>.
      free: <T_DATA>.
      ME->GET_GRID_DATA( IR_DATA = ME->R_DATA ).

      ME->R_GRID->GET_SELECTED_CELLS( importing ET_CELL = LT_CELL ).
      ME->R_GRID->GET_SELECTED_ROWS( importing ET_INDEX_ROWS = LT_ROW ).

      case E_UCOMM.

        when C_FUNC_ADD.
***     Add new Screen Group
          ME->ADD_SCRGR( ).

        when C_FUNC_DEL.
***     Delete Screen Group
          if ME->CHECK_SEL_DELETE( ) eq ABAP_FALSE.
***         No Row Selected. Please Select all Row's you wanna Delete.
            raise exception type /VCXI/CX_CKX
             exporting
                F_MSGTY = 'E'
                F_MSGID = 'ZVCXI_XCDCU'
                F_MSGNO = '301'.
          endif.

***       Get Selected Screen Group's
          loop at LT_CELL into LS_CELL.
            read table <T_DATA> assigning <S_DATA> index LS_CELL-ROW_ID-INDEX.
            check SY-SUBRC = 0.

            append initial line to LT_CR_SIMPACT assigning <S_CR_SIMPACT>.
            move-corresponding <S_DATA> to <S_CR_SIMPACT>.
          endloop.

          loop at LT_ROW into LS_ROW.
            read table <T_DATA> assigning <S_DATA> index LS_ROW-INDEX.
            check SY-SUBRC = 0.

            append initial line to LT_CR_SIMPACT assigning <S_CR_SIMPACT>.
            move-corresponding <S_DATA> to <S_CR_SIMPACT>.
          endloop.

***       Delete Screen Group's
          ME->DELETE_SCRGR( IT_CR_SIMPACT = LT_CR_SIMPACT ).

      endcase.

      ME->F_REFRESH_REQ = ABAP_TRUE.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      LR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

endmethod.
method INIT_LOCKED.

  ME->F_LOCKED = ME->R_CONDREC->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ).

endmethod.
method INIT_OSVIS_DESCR.

  data:          LT_DD07V  type table of DD07V.
  data:          LS_DD07V  type          DD07V.
  field-symbols: <S_OSVIS> type          YS_OSVIS.

*** Get Domain Values
  call function 'DDIF_DOMA_GET'
    exporting
      NAME          = 'ZVCXI_XCD_OSVIS'
      LANGU         = SY-LANGU
    tables
      DD07V_TAB     = LT_DD07V
    exceptions
      ILLEGAL_INPUT = 1
      others        = 2.
  if SY-SUBRC <> 0.
    /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
  endif.

  loop at LT_DD07V into LS_DD07V.
    append initial line to ME->T_OSVIS assigning <S_OSVIS>.
    move LS_DD07V-DOMVALUE_L to <S_OSVIS>-OSVIS.
    move LS_DD07V-DDTEXT     to <S_OSVIS>-OSVIS_DESCR.
  endloop.

endmethod.
method SET_EVENT.

  SUPER->SET_EVENT( ).

*** Add Event for Double Click, UserCommand and Toolbar
  set handler ME->HANDLE_USER_COMMAND for ME->R_GRID.
  set handler ME->HANDLE_TOOLBAR      for ME->R_GRID.

endmethod.
