
class ZCL_VCXI_P5SP_SERVICE definition
  public
  final
  create private .

public section.

  class-data F_DIMID_AREA type DIMID .
  class-data F_DIMID_LENGTH type DIMID .

  class-methods GET_WEBWI
    importing
      !IT_RLDIM type ZVCXI_P5SP_TT_RLDIM
    returning
      value(RS_WEBWI) type ZVCXI_PXS_S_WEBWI
    raising
      /VCXI/CX_CKX .
  class-methods CLASS_CONSTRUCTOR .
  class-methods CONV_L_TO_A
    importing
      !IS_CALCV type ZVCXI_XCS_S_CALCV
      !IS_RLDIM type ZVCXI_P5SP_S_RLDIM
    returning
      value(RS_CALCV) type ZVCXI_XCS_S_CALCV
    raising
      /VCXI/CX_CKX .
  class-methods CONV_A_TO_L
    importing
      !IS_CALCV type ZVCXI_XCS_S_CALCV
      !IS_RLDIM type ZVCXI_P5SP_S_RLDIM
    returning
      value(RS_CALCV) type ZVCXI_XCS_S_CALCV
    raising
      /VCXI/CX_CKX .
  class-methods CONV_A_TO_P
    importing
      !IS_CALCV type ZVCXI_XCS_S_CALCV
      !IS_RLDIM type ZVCXI_P5SP_S_RLDIM
    returning
      value(RS_CALCV) type ZVCXI_XCS_S_CALCV
    raising
      /VCXI/CX_CKX .
  class-methods CONV_L_TO_P
    importing
      !IS_CALCV type ZVCXI_XCS_S_CALCV
      !IS_RLDIM type ZVCXI_P5SP_S_RLDIM
    returning
      value(RS_CALCV) type ZVCXI_XCS_S_CALCV
    raising
      /VCXI/CX_CKX .
  class-methods CONV_P_TO_A
    importing
      !IS_CALCV type ZVCXI_XCS_S_CALCV
      !IS_RLDIM type ZVCXI_P5SP_S_RLDIM
    returning
      value(RS_CALCV) type ZVCXI_XCS_S_CALCV
    raising
      /VCXI/CX_CKX .
  class-methods CONV_P_TO_L
    importing
      !IS_CALCV type ZVCXI_XCS_S_CALCV
      !IS_RLDIM type ZVCXI_P5SP_S_RLDIM
    returning
      value(RS_CALCV) type ZVCXI_XCS_S_CALCV
    raising
      /VCXI/CX_CKX .
protected section.
private section.
endclass. "ZCL_VCXI_P5SP_SERVICE definition
class ZCL_VCXI_P5SP_SERVICE implementation.
  method CLASS_CONSTRUCTOR.

*** Area
    call function 'DIMENSION_GET'
      exporting
        LENGTH              = 2
      importing
        DIMID               = F_DIMID_AREA
      exceptions
        DIMENSION_NOT_FOUND = 1.

*** Length
    call function 'DIMENSION_GET'
      exporting
        LENGTH              = 1
      importing
        DIMID               = F_DIMID_LENGTH
      exceptions
        DIMENSION_NOT_FOUND = 1.


  endmethod.
  method CONV_A_TO_L.

    check IS_RLDIM-WEBWI is not initial.

*** Calculate Quantity
    ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = IS_CALCV-CALCV
                                                        IF_CALCV_UOM_1 = IS_CALCV-CALCV_UOM
                                                        IF_CALCV_2     = IS_RLDIM-WEBWI
                                                        IF_CALCV_UOM_2 = IS_RLDIM-WEBWI_UOM
                                                        IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_DIVIDE
                                              importing EF_CALCV       = RS_CALCV-CALCV
                                                        EF_CALCV_UOM   = RS_CALCV-CALCV_UOM ).

  endmethod.
  method CONV_A_TO_P.

    data: LS_CALCV_LENGTH  type ZVCXI_XCS_S_CALCV.


*** Convert Area to Length
    LS_CALCV_LENGTH = ZCL_VCXI_P5SP_SERVICE=>CONV_A_TO_L( IS_CALCV = IS_CALCV
                                                          IS_RLDIM = IS_RLDIM ).
*** Convert Length to PC
    RS_CALCV = ZCL_VCXI_P5SP_SERVICE=>CONV_L_TO_P( IS_CALCV = LS_CALCV_LENGTH
                                                   IS_RLDIM = IS_RLDIM ).

  endmethod.
  method CONV_L_TO_A.

*** Calculate Quantity
    ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = IS_CALCV-CALCV
                                                        IF_CALCV_UOM_1 = IS_CALCV-CALCV_UOM
                                                        IF_CALCV_2     = IS_RLDIM-WEBWI
                                                        IF_CALCV_UOM_2 = IS_RLDIM-WEBWI_UOM
                                                        IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_MULTIPLY
                                              importing EF_CALCV       = RS_CALCV-CALCV
                                                        EF_CALCV_UOM   = RS_CALCV-CALCV_UOM ).

  endmethod.
  method CONV_L_TO_P.

    check IS_RLDIM-RLETA is not initial.

*** Calculate Rolls based on Target Roll Length
    ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = IS_CALCV-CALCV
                                                        IF_CALCV_UOM_1 = IS_CALCV-CALCV_UOM
                                                        IF_CALCV_2     = IS_RLDIM-RLETA
                                                        IF_CALCV_UOM_2 = IS_RLDIM-RLETA_UOM
                                                        IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_DIVIDE
                                              importing EF_CALCV       = RS_CALCV-CALCV ).
    RS_CALCV-CALCV_UOM = 'ST'.

  endmethod.
  method CONV_P_TO_A.

*** PC to Length
    RS_CALCV = ZCL_VCXI_P5SP_SERVICE=>CONV_P_TO_L( IS_CALCV = IS_CALCV
                                                   IS_RLDIM = IS_RLDIM ).

*** Length to Area
    RS_CALCV = ZCL_VCXI_P5SP_SERVICE=>CONV_L_TO_A( IS_CALCV = RS_CALCV
                                                   IS_RLDIM = IS_RLDIM ).

  endmethod.
  method CONV_P_TO_L.

*** Calculate Length based on Target Roll Length
    RS_CALCV-CALCV     = IS_CALCV-CALCV * IS_RLDIM-RLETA.
    RS_CALCV-CALCV_UOM = IS_RLDIM-RLETA_UOM.

  endmethod.
  method GET_WEBWI.

    loop at IT_RLDIM into data(LS_RLDIM).
***   Consider Outs
*{   REPLACE        HEDK927906                                        1
*\      LS_RLDIM-WEBWI *= LS_RLDIM-NOOUT.
      LS_RLDIM-WEBWI = LS_RLDIM-WEBWI * LS_RLDIM-NOOUT.
*}   REPLACE

      if RS_WEBWI is initial.
        RS_WEBWI = LS_RLDIM-S_WEBWI.
      else.
        ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = RS_WEBWI-WEBWI
                                                            IF_CALCV_UOM_1 = RS_WEBWI-WEBWI_UOM
                                                            IF_CALCV_2     = LS_RLDIM-WEBWI
                                                            IF_CALCV_UOM_2 = LS_RLDIM-WEBWI_UOM
                                                            IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_ADD
                                                            IF_UOM         = RS_WEBWI-WEBWI_UOM
                                                  importing EF_CALCV       = RS_WEBWI-WEBWI
                                                            EF_CALCV_UOM   = RS_WEBWI-WEBWI_UOM ).
      endif.
    endloop.

  endmethod.
