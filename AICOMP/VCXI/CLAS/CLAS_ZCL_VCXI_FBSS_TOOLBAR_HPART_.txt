
class ZCL_VCXI_FBSS_TOOLBAR_HPART definition
  public
  inheriting from ZCL_VCXI_FBSS_TOOLBAR_PRVDR
  create public .

public section.

  constants C_PRVDR_PARID type /VCXI/VKS_PARID value 'ZVCXI_FB_TPHND' ##NO_TEXT.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_CONTAINER_NAME type SCRFNAME
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL optional
      !IR_BL_FASTD type ref to ZCL_VCXI_FBSS_BL_FASTD optional
    raising
      /VCXI/CX_CKX .
protected section.

  data R_BL_FASTD type ref to ZCL_VCXI_FBSS_BL_FASTD .

  methods DEL_FCOMP
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    exporting
      !EF_SFDAT type ZVCXI_F0SS_SFDAT
    raising
      /VCXI/CX_CKX .

  methods DEL_PRVDR
    redefinition .
  methods GET_MENU_ITEMS
    redefinition .
  methods GET_PRVDR_PARAM
    redefinition .
  methods SET_PRVDR_PARAM
    redefinition .
private section.
endclass. "ZCL_VCXI_FBSS_TOOLBAR_HPART definition
class ZCL_VCXI_FBSS_TOOLBAR_HPART implementation.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_GATE           = IR_GATE
                        IF_CONTAINER_NAME = IF_CONTAINER_NAME
                        IR_UIGATE         = IR_UIGATE
                        IF_APPL_EVENTS    = IF_APPL_EVENTS
                        IF_IPRVT          = SPACE ).

    move IR_BL_FASTD to ME->R_BL_FASTD.

  endmethod.
  method DEL_FCOMP.

    data: LT_GATE_PART  type /VCXI/VKSR_TT_GATE.

*** Check if Handle Part still has "Standard Configuration"
    ME->R_BL_FASTD->GET_FASTD_BY_PART( exporting IR_GATE_PART = IR_GATE
                                       importing EF_SFDAT     = EF_SFDAT ).

*** Don't continue in case of Non-Standard Configuration
    check EF_SFDAT eq ZCL_VCXI_FBSS_BL_FASTD=>C_SFDAT_STANDARD.

*** Delete Film Components by list of Parts
    append IR_GATE to LT_GATE_PART.
    ME->R_BL_FASTD->DELETE_FCOMP_BY_PARTS( IT_GATE_PART = LT_GATE_PART ).

  endmethod.
  method DEL_PRVDR.

    data: LF_PARAM_VAL type        /VCXI/CKX_ID,
          LF_SFDAT     type        ZVCXI_F0SS_SFDAT.
    data: LR_GATE   type ref to /VCXI/CL_VKSR_GATE.

*** Free selected Provider
    loop at ME->T_PRVDR_SELECTED into LR_GATE where TABLE_LINE is bound.

***   Check if it's Menu Item
      read table ME->T_PRVDR_DEF with key ID = LR_GATE->F_ID transporting no fields.
      check SY-SUBRC = 0.

***   Check if Delete Disabled
      LF_PARAM_VAL = ME->GET_PRVDR_PARAM( exporting IR_GATE = LR_GATE ).
      check ME->CHECK_PRVDR_PARAM_DEL( IF_ID        = LR_GATE->F_ID
                                       IF_PARAM_VAL = LF_PARAM_VAL ) eq ABAP_FALSE.

***--------------------------------------------------------------------------------------
***   Delete Film Components
***   ( not allowed if HPart is removed from Fast Data and configuration is "non-standard" )
      if ME->R_BL_FASTD is bound.
        ME->DEL_FCOMP( exporting IR_GATE  = LR_GATE
                       importing EF_SFDAT = LF_SFDAT ).

***     Structural changes are not allowed, due to non-standard configuration.
        if LF_SFDAT eq ZCL_VCXI_FBSS_BL_FASTD=>C_SFDAT_NONSTANDARD.
          message id 'ZVCXI_FFSS' type 'S' number 501 display like 'W'.
          continue.
        endif.
      endif.

***--------------------------------------------------------------------------------------
***   Free Handle Part
      LR_GATE->DO_FREE( ).

    endloop.

*** Process Events
    ME->R_GATE->RAISE_EVENTS( ).

***--------------------------------------------------------------------------------------
*** Refresh is required
    ME->F_REFRESH_REQ = ABAP_TRUE.

  endmethod.
  method GET_MENU_ITEMS.

    data: LF_THNDL      type        ZVCXI_FBS_THNDL.
    data: LS_MENU_ITEMS type        /VCXI/VKSR_S_PRVDR_DEF,
          LS_TC112      type        ZVCXI_FBS_TC112,
          LS_TC111T     type        ZVCXI_FBS_TC111T,
          LS_ATTRI      type        /VCXI/VKS_S_ATTRI.
    data: LR_PR_HANDLE  type ref to ZCL_VCXI_FBSS_PR_HANDLE.

    clear ME->T_MENU_ITEMS.

*** Get Handle PR
    try.
        move ME->R_GATE->R_CLSPR ?to LR_PR_HANDLE.
      catch CX_SY_MOVE_CAST_ERROR.
        clear LR_PR_HANDLE.
    endtry.

    check LR_PR_HANDLE is bound.

*** Get Handle Type
    LR_PR_HANDLE->GET_THNDL( importing EF_THNDL = LF_THNDL ).

*** Create Menu Items list
    loop at ZCL_VCXI_FBSSC_BAGTY=>T_TC112 into LS_TC112 where THNDL = LF_THNDL.

      clear LS_MENU_ITEMS.
      move LS_TC112-ICELM     to  LS_MENU_ITEMS-ID.
      move LS_TC112-QTYMI     to  LS_MENU_ITEMS-QTYMI.
      move LS_TC112-QTYMA     to  LS_MENU_ITEMS-QTYMA.

      read table ZCL_VCXI_FBSSC_BAGTY=>T_TC111T into LS_TC111T with key TPHND = LS_TC112-TPHND.
      if SY-SUBRC = 0.
        move LS_TC111T-DESCR  to  LS_MENU_ITEMS-DESCR.
      else.
        move LS_TC112-TPHND   to  LS_MENU_ITEMS-DESCR.
      endif.

      clear LS_ATTRI.
      move ME->C_PRVDR_PARID  to  LS_ATTRI-ATTID.
      move LS_TC112-TPHND     to  LS_ATTRI-ATTVA.
      move 'IMPL'             to  LS_ATTRI-ATTMO.
      append LS_ATTRI         to  LS_MENU_ITEMS-T_ATTRI.
      append LS_MENU_ITEMS    to  ME->T_MENU_ITEMS.

    endloop.

  endmethod.
  method GET_PRVDR_PARAM.

    data: LS_PARAM  type  /VCXI/VKSR_S_PARAM.

    LS_PARAM = IR_GATE->GET_PARAM( IF_PARID = ME->C_PRVDR_PARID ).

    move LS_PARAM-ATWRT to RF_PARAM_VAL.

  endmethod.
  method SET_PRVDR_PARAM.

    data: LS_PARAM  type  /VCXI/VKSR_S_PARAM.

    LS_PARAM = IR_GATE->GET_PARAM( IF_PARID = ME->C_PRVDR_PARID ).
    move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC   to LS_PARAM-SETBY.
    move IF_PARAM_VAL                       to LS_PARAM-ATWRT.

    IR_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

  endmethod.
