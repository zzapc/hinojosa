
class ZCL_VCXI_XCSS_PR_MRP_AREA definition
  public
  inheriting from /VCXI/CL_VKSR_CLSPR
  create public .

public section.

  constants C_IEVNT_O_BERID_CHANGED type /VCXI/VKSC_IEVNT value 'BERID_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_BSTFE_CHANGED type /VCXI/VKSC_IEVNT value 'BSTFE_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_BSTMA_CHANGED type /VCXI/VKSC_IEVNT value 'BSTMA_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_BSTMI_CHANGED type /VCXI/VKSC_IEVNT value 'BSTMI_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_DISLS_CHANGED type /VCXI/VKSC_IEVNT value 'DISLS_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_DISMM_CHANGED type /VCXI/VKSC_IEVNT value 'DISMM_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_DISPO_CHANGED type /VCXI/VKSC_IEVNT value 'DISPO_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_EISBE_CHANGED type /VCXI/VKSC_IEVNT value 'EISBE_CHANGED' ##NO_TEXT.
  constants C_IPRVT_XCS_MRP_AREA type /VCXI/VKSC_IPRVT value 'ZVCXI_XCSS_MRP_AREA' ##NO_TEXT.
  constants C_PARID_BERID type /VCXI/VKS_PARID value 'ZVCXI_XC_BERID' ##NO_TEXT.
  constants C_PARID_BSTFE_A type /VCXI/VKS_PARID value 'ZVCXI_XC_BSTFE_A' ##NO_TEXT.
  constants C_PARID_BSTFE_L type /VCXI/VKS_PARID value 'ZVCXI_XC_BSTFE_L' ##NO_TEXT.
  constants C_PARID_BSTFE_P type /VCXI/VKS_PARID value 'ZVCXI_XC_BSTFE_P' ##NO_TEXT.
  constants C_PARID_BSTMA_A type /VCXI/VKS_PARID value 'ZVCXI_XC_BSTMA_A' ##NO_TEXT.
  constants C_PARID_BSTMA_L type /VCXI/VKS_PARID value 'ZVCXI_XC_BSTMA_L' ##NO_TEXT.
  constants C_PARID_BSTMA_P type /VCXI/VKS_PARID value 'ZVCXI_XC_BSTMA_P' ##NO_TEXT.
  constants C_PARID_BSTMI_A type /VCXI/VKS_PARID value 'ZVCXI_XC_BSTMI_A' ##NO_TEXT.
  constants C_PARID_BSTMI_L type /VCXI/VKS_PARID value 'ZVCXI_XC_BSTMI_L' ##NO_TEXT.
  constants C_PARID_BSTMI_P type /VCXI/VKS_PARID value 'ZVCXI_XC_BSTMI_P' ##NO_TEXT.
  constants C_PARID_DISLS type /VCXI/VKS_PARID value 'ZVCXI_XC_DISLS' ##NO_TEXT.
  constants C_PARID_DISMM type /VCXI/VKS_PARID value 'ZVCXI_XC_DISMM' ##NO_TEXT.
  constants C_PARID_DISPO type /VCXI/VKS_PARID value 'ZVCXI_XC_DISPO' ##NO_TEXT.
  constants C_PARID_EISBE_A type /VCXI/VKS_PARID value 'ZVCXI_XC_EISBE_A' ##NO_TEXT.
  constants C_PARID_EISBE_L type /VCXI/VKS_PARID value 'ZVCXI_XC_EISBE_L' ##NO_TEXT.
  constants C_PARID_EISBE_P type /VCXI/VKS_PARID value 'ZVCXI_XC_EISBE_P' ##NO_TEXT.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods GET_BERID
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
      !EF_BERTX type BERTX
      !EF_WERKS type WERKDP
    returning
      value(RF_BERID) type BERID
    raising
      /VCXI/CX_CKX .
  methods GET_BSTFE
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_BSTFE) type ZVCXI_XCS_S_BSTFE
    raising
      /VCXI/CX_CKX .
  methods GET_BSTMA
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_BSTMA) type ZVCXI_XCS_S_BSTMA
    raising
      /VCXI/CX_CKX .
  methods GET_BSTMI
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_BSTMI) type ZVCXI_XCS_S_BSTMI
    raising
      /VCXI/CX_CKX .
  methods GET_DISLS
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
      !EF_LOSLT type LOSLT
      !ES_T439A type T439A
    returning
      value(RF_DISLS) type DISLS
    raising
      /VCXI/CX_CKX .
  methods GET_DISMM
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
      !EF_DIBEZ type DIBEZ
      !ES_T438A type T438A
    returning
      value(RF_DISMM) type DISMM
    raising
      /VCXI/CX_CKX .
  methods GET_DISPO
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
      !EF_DSNAM type DSNAM
    returning
      value(RF_DISPO) type DISPO
    raising
      /VCXI/CX_CKX .
  methods GET_EISBE
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_EISBE) type ZVCXI_XCS_S_EISBE
    raising
      /VCXI/CX_CKX .
  methods GET_PRVDR_MRP
    exporting
      !ER_GATE type ref to /VCXI/CL_VKSR_GATE
    returning
      value(RR_PR_MRP) type ref to ZCL_VCXI_XCSS_PR_MRP
    raising
      /VCXI/CX_CKX .
  methods GET_WERKS
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
      !EF_DESCR type /VCXI/CKX_DESCR
    returning
      value(RF_WERKS) type WERKS_D
    raising
      /VCXI/CX_CKX .
  methods SET_BERID
    importing
      !IF_BERID type BERID
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_BSTFE
    importing
      !IS_BSTFE type ZVCXI_XCS_S_BSTFE optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_BSTMA
    importing
      !IS_BSTMA type ZVCXI_XCS_S_BSTMA optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_BSTMI
    importing
      !IS_BSTMI type ZVCXI_XCS_S_BSTMI optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_DISLS
    importing
      !IF_DISLS type DISLS
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_DISMM
    importing
      !IF_DISMM type DISMM
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_DISPO
    importing
      !IF_DISPO type DISPO
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_EISBE
    importing
      !IS_EISBE type ZVCXI_XCS_S_EISBE optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .

  methods GET_DESCR
    redefinition .
  methods GET_STATUS
    redefinition .
  methods GET_STATUS_MSG
    redefinition .
  methods HANDLE_EVENT
    redefinition .
protected section.

  constants C_IEVNT_I_SET_DEFAULT type /VCXI/VKSC_IEVNT value 'SET_DEFAULT' ##NO_TEXT.
  data F_PARID_BSTFE type /VCXI/VKS_PARID .
  data F_PARID_BSTMA type /VCXI/VKS_PARID .
  data F_PARID_BSTMI type /VCXI/VKS_PARID .
  data F_PARID_EISBE type /VCXI/VKS_PARID .

  methods CHECK_DISPO
    importing
      !IR_MESSAGE type ref to /VCXI/CL_CKXM_MESSAGE optional
    returning
      value(RF_ELMST) type /VCXI/VKSR_ELMST
    raising
      /VCXI/CX_CKX .
  methods CHECK_DISMM
    importing
      !IR_MESSAGE type ref to /VCXI/CL_CKXM_MESSAGE optional
    returning
      value(RF_ELMST) type /VCXI/VKSR_ELMST
    raising
      /VCXI/CX_CKX .
  methods CHECK_DISLS
    importing
      !IR_MESSAGE type ref to /VCXI/CL_CKXM_MESSAGE optional
    returning
      value(RF_ELMST) type /VCXI/VKSR_ELMST
    raising
      /VCXI/CX_CKX .
  methods CHECK_BSTMA
    importing
      !IR_MESSAGE type ref to /VCXI/CL_CKXM_MESSAGE optional
    returning
      value(RF_ELMST) type /VCXI/VKSR_ELMST
    raising
      /VCXI/CX_CKX .
  methods CHECK_BSTFE
    importing
      !IR_MESSAGE type ref to /VCXI/CL_CKXM_MESSAGE optional
    returning
      value(RF_ELMST) type /VCXI/VKSR_ELMST
    raising
      /VCXI/CX_CKX .
  methods HANDLE_SET_DEFAULT
    raising
      /VCXI/CX_CKX .
private section.

  methods INIT_PARID
    raising
      /VCXI/CX_CKX .
endclass. "ZCL_VCXI_XCSS_PR_MRP_AREA definition
class ZCL_VCXI_XCSS_PR_MRP_AREA implementation.
  method CHECK_BSTFE.

*** Use SAP standard to check
    call function 'MARC_BSTFE'
      exporting
        P_BSTFE          = ME->GET_BSTFE( )-BSTFE
*       P_DISPR          = ME->GET_DISPR( )
        P_DISPR          = SPACE
        P_DISLS          = ME->GET_DISLS( )
        P_KZ_NO_WARN     = 'N'
      exceptions
        P_ERR_MARC_BSTFE = 1
        ERR_T439A        = 2.
    if SY-SUBRC ne 0.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
      if IR_MESSAGE is bound.
        IR_MESSAGE->ADD_MESSAGE_BY_SY( ).
      endif.
    endif.

  endmethod.
  method CHECK_BSTMA.

*** Use SAP standard to check
    call function 'MARC_BSTMA'
      exporting
        P_BSTMA          = ME->GET_BSTMA( )-BSTMA
        P_BSTMI          = ME->GET_BSTMI( )-BSTMI
        P_BSTRF          = 0  "Do not check Rounding value for purchase order quantity
        P_KZ_NO_WARN     = 'N'
      exceptions
        P_ERR_MARC_BSTMA = 1.
    if SY-SUBRC ne 0.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
      if IR_MESSAGE is bound.
        IR_MESSAGE->ADD_MESSAGE_BY_SY( ).
      endif.
    endif.

  endmethod.
  method CHECK_DISLS.

    ME->GET_DISMM( importing ES_T438A = data(LS_T438A) ).
    data(LF_DISLS) = ME->GET_DISLS( ).

    if LF_DISLS is initial and
       LS_T438A-DISVF ne 'N'.  "No MRP
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
      if IR_MESSAGE is bound.
***     Enter the lot size (materials planning)
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'M3'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '071' ).
      endif.
    else.
***   Check Entry
      call function 'T439A_SINGLE_READ'
        exporting
          T439A_DISLS = LF_DISLS
        exceptions
          NOT_FOUND   = 1.
      if SY-SUBRC ne 0.
        RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
        if IR_MESSAGE is bound.
          IR_MESSAGE->ADD_MESSAGE_BY_SY( ).
        endif.
      endif.
    endif.

  endmethod.
  method CHECK_DISMM.

    data(LF_DISMM) = ME->GET_DISMM( ).

    if LF_DISMM is initial.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.

    else.
***   Check Entry
      call function 'T438A_SINGLE_READ'
        exporting
          T438A_DISMM = LF_DISMM
        exceptions
          others      = 1.
      if SY-SUBRC ne 0.
        RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
        if IR_MESSAGE is bound.
          IR_MESSAGE->ADD_MESSAGE_BY_SY( ).
        endif.
      endif.
    endif.

  endmethod.
  method CHECK_DISPO.

    ME->GET_DISMM( importing ES_T438A = data(LS_T438A) ).
    data(LF_DISPO) = ME->GET_DISPO( ).

    if LF_DISPO is initial and
       LS_T438A-DISVF ne 'N'.  "No MRP
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
      if IR_MESSAGE is bound.
***     Enter the MRP controller
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'M3'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '069' ).
      endif.
    else.
***   Use SAP standard to check
      call function 'MARC_DISPO'
        exporting
          P_DISPO        = ME->GET_DISPO( )
          P_WERKS        = ME->GET_WERKS( )
          P_KZ_NO_WARN   = 'N'
        exceptions
          ERR_MARC_DISPO = 1
          ERR_T024D      = 2.
      if SY-SUBRC ne 0.
        RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
        if IR_MESSAGE is bound.
          IR_MESSAGE->ADD_MESSAGE_BY_SY( ).
        endif.
      endif.
    endif.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_GATE = IR_GATE ).

*** Initialize dimension-based Parameter ID
    ME->INIT_PARID( ).

  endmethod.
  method GET_BERID.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

***----------------------------------------------------------------------------
*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_BERID ).
    move LS_PARAM-ATWRT to RF_BERID.
    move LS_PARAM-SETBY to EF_SETBY.

***----------------------------------------------------------------------------
    if EF_BERTX is requested or
       EF_WERKS is requested.
***   Identify Description and Plant
      select single BERTX, WERZG
             into ( @EF_BERTX, @EF_WERKS )
             from MDLV
            where BERID = @RF_BERID.
    endif.

  endmethod.
  method GET_BSTFE.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

***----------------------------------------------------------------------------
*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = ME->F_PARID_BSTFE ).
    RS_BSTFE = value #( BSTFE     = LS_PARAM-ATFLV
                        BSTFE_UOM = LS_PARAM-UNIT ).
    EF_SETBY = LS_PARAM-SETBY.

  endmethod.
  method GET_BSTMA.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

***----------------------------------------------------------------------------
*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = ME->F_PARID_BSTMA ).
    RS_BSTMA = value #( BSTMA     = LS_PARAM-ATFLV
                        BSTMA_UOM = LS_PARAM-UNIT ).
    EF_SETBY = LS_PARAM-SETBY.

  endmethod.
  method GET_BSTMI.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

***----------------------------------------------------------------------------
*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = ME->F_PARID_BSTMI ).
    RS_BSTMI = value #( BSTMI     = LS_PARAM-ATFLV
                        BSTMI_UOM = LS_PARAM-UNIT ).
    EF_SETBY = LS_PARAM-SETBY.

  endmethod.
  method GET_DESCR.

    RF_DESCR = ME->GET_BERID( ).

  endmethod.
  method GET_DISLS.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

***----------------------------------------------------------------------------
*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_DISLS ).
    RF_DISLS = LS_PARAM-ATWRT.
    EF_SETBY = LS_PARAM-SETBY.

    check RF_DISLS is not initial.

***----------------------------------------------------------------------------
*** Description
    if EF_LOSLT is requested.
      select single LOSLT
             into EF_LOSLT
             from T439T
            where DISLS eq RF_DISLS
             and  SPRAS eq SY-LANGU.
      if SY-SUBRC ne 0.
        clear EF_LOSLT.
      endif.
    endif.

*** MRP Settings
    if ES_T439A is requested.
      call function 'T439A_SINGLE_READ'
        exporting
          T439A_DISLS = RF_DISLS
        importing
          WT439A      = ES_T439A
        exceptions
          others      = 1.
      if SY-SUBRC ne 0.
        clear ES_T439A.
      endif.
    endif.

  endmethod.
  method GET_DISMM.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

***----------------------------------------------------------------------------
*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_DISMM ).
    RF_DISMM = LS_PARAM-ATWRT.
    EF_SETBY = LS_PARAM-SETBY.

    check RF_DISMM is not initial.

***----------------------------------------------------------------------------
*** Description
    if EF_DIBEZ is requested.
      select single DIBEZ
             into EF_DIBEZ
             from T438T
            where SPRAS eq SY-LANGU
             and  DISMM eq RF_DISMM.
      if SY-SUBRC ne 0.
        clear EF_DIBEZ.
      endif.
    endif.

    if ES_T438A is requested.
      call function 'T438A_SINGLE_READ'
        exporting
          T438A_DISMM = RF_DISMM
        importing
          WT438A      = ES_T438A
        exceptions
          others      = 1.
      if SY-SUBRC ne 0.
        clear ES_T438A.
      endif.
    endif.

  endmethod.
  method GET_DISPO.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

***----------------------------------------------------------------------------
*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_DISPO ).
    RF_DISPO = LS_PARAM-ATWRT.
    EF_SETBY = LS_PARAM-SETBY.

    check RF_DISPO is not initial.

***----------------------------------------------------------------------------
*** Description
    if EF_DSNAM is requested.
      data(LF_WERKS) = ME->GET_WERKS( ).
      select single DSNAM
             into @EF_DSNAM
             from T024D
            where WERKS eq @LF_WERKS
             and  DISPO eq @RF_DISPO.
      if SY-SUBRC ne 0.
        clear EF_DSNAM.
      endif.
    endif.

  endmethod.
  method GET_EISBE.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

***----------------------------------------------------------------------------
*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = ME->F_PARID_EISBE ).
    RS_EISBE = value #( EISBE     = LS_PARAM-ATFLV
                        EISBE_UOM = LS_PARAM-UNIT ).
    EF_SETBY = LS_PARAM-SETBY.

  endmethod.
  method GET_PRVDR_MRP.

*** Get all Provider
    data(LT_GATE) = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCSS_PR_MRP=>C_IPRVT_XCS_MRP
                                           IF_INUSE = ABAP_TRUE ).

*** Get Reference
    loop at LT_GATE into data(LR_GATE).
      try.
          move LR_GATE->R_CLSPR ?to RR_PR_MRP.
          check RR_PR_MRP is bound.
          move LR_GATE to ER_GATE.
          exit.

        catch CX_SY_MOVE_CAST_ERROR.
          clear: RR_PR_MRP, ER_GATE.
      endtry.
    endloop.

  endmethod.
  method GET_STATUS.

    RF_ELMST = SUPER->GET_STATUS( ).

***----------------------------------------------------------------------------
*** Check MRP Type
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = ME->CHECK_DISMM( ) ).

*** Check MRP Controller
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = ME->CHECK_DISPO( ) ).

***----------------------------------------------------------------------------
*** Check Lot Sizing Procedure in Materials Planning
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = ME->CHECK_DISLS( ) ).

*** Check Maximum Lot Size
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = ME->CHECK_BSTMA( ) ).

*** Check Fix Lot Size
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = ME->CHECK_BSTFE( ) ).

  endmethod.
  method GET_STATUS_MSG.

    SUPER->GET_STATUS_MSG( IR_MESSAGE = IR_MESSAGE ).

***----------------------------------------------------------------------------
*** Check MRP Type
    ME->CHECK_DISMM( IR_MESSAGE = IR_MESSAGE ).

*** Check MRP Controller
    ME->CHECK_DISPO( IR_MESSAGE = IR_MESSAGE ).

***----------------------------------------------------------------------------
*** Check Lot Sizing Procedure in Materials Planning
    ME->CHECK_DISLS( IR_MESSAGE = IR_MESSAGE ).

*** Check Maximum Lot Size
    ME->CHECK_BSTMA( IR_MESSAGE = IR_MESSAGE ).

*** Check Fix Lot Size
    ME->CHECK_BSTFE( IR_MESSAGE = IR_MESSAGE ).

  endmethod.
  method GET_WERKS.

    data(LR_PR_MRP) = ME->GET_PRVDR_MRP( ).
    check LR_PR_MRP is bound.

    if EF_DESCR is requested.
      RF_WERKS = LR_PR_MRP->GET_WERKS( importing EF_SETBY = EF_SETBY
                                                 EF_DESCR = EF_DESCR ).
    else.
      RF_WERKS = LR_PR_MRP->GET_WERKS( importing EF_SETBY = EF_SETBY ).
    endif.

  endmethod.
  method HANDLE_EVENT.

    SUPER->HANDLE_EVENT( IF_IEVNT         = IF_IEVNT
                         IT_GATE_PROVIDER = IT_GATE_PROVIDER ).

    case IF_IEVNT.
      when C_IEVNT_I_SET_DEFAULT.
        ME->HANDLE_SET_DEFAULT( ).

    endcase.

  endmethod.
  method HANDLE_SET_DEFAULT.

    ZCL_VCXI_XCDR_SERVICE=>SET_DEFAULT( IR_GATE = ME->R_GATE ).

  endmethod.
  method INIT_PARID.

    loop at ME->R_GATE->GET_PARAM_ALL( ) into data(LS_PARAM)
                    where PARTY eq /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC.

***   Minimum Lot Size
      if LS_PARAM-PARID eq C_PARID_BSTMI_P or
         LS_PARAM-PARID eq C_PARID_BSTMI_L or
         LS_PARAM-PARID eq C_PARID_BSTMI_A.
        move LS_PARAM-PARID to ME->F_PARID_BSTMI.
      endif.
***   Maximum Lot Size
      if LS_PARAM-PARID eq C_PARID_BSTMA_P or
         LS_PARAM-PARID eq C_PARID_BSTMA_L or
         LS_PARAM-PARID eq C_PARID_BSTMA_A.
        move LS_PARAM-PARID to ME->F_PARID_BSTMA.
      endif.
***   Fix Lot Size
      if LS_PARAM-PARID eq C_PARID_BSTFE_P or
         LS_PARAM-PARID eq C_PARID_BSTFE_L or
         LS_PARAM-PARID eq C_PARID_BSTFE_A.
        move LS_PARAM-PARID to ME->F_PARID_BSTFE.
      endif.
***   Safety Stock
      if LS_PARAM-PARID eq C_PARID_EISBE_P or
         LS_PARAM-PARID eq C_PARID_EISBE_L or
         LS_PARAM-PARID eq C_PARID_EISBE_A.
        move LS_PARAM-PARID to ME->F_PARID_EISBE.
      endif.

    endloop.

  endmethod.
  method SET_BERID.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_BERID ).

    move IF_BERID to LS_PARAM-ATWRT.
    move IF_SETBY to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_BERID_CHANGED ).
    endif.

  endmethod.
  method SET_BSTFE.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = ME->F_PARID_BSTFE ).

    if IS_BSTFE is supplied.
      LS_PARAM-ATFLV = IS_BSTFE-BSTFE.
      LS_PARAM-UNIT  = IS_BSTFE-BSTFE_UOM.
    endif.
    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_BSTFE_CHANGED ).
    endif.

  endmethod.
  method SET_BSTMA.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = ME->F_PARID_BSTMA ).

    if IS_BSTMA is supplied.
      LS_PARAM-ATFLV = IS_BSTMA-BSTMA.
      LS_PARAM-UNIT  = IS_BSTMA-BSTMA_UOM.
    endif.
    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_BSTMA_CHANGED ).
    endif.

  endmethod.
  method SET_BSTMI.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = ME->F_PARID_BSTMI ).

    if IS_BSTMI is supplied.
      LS_PARAM-ATFLV = IS_BSTMI-BSTMI.
      LS_PARAM-UNIT  = IS_BSTMI-BSTMI_UOM.
    endif.
    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_BSTMI_CHANGED ).
    endif.

  endmethod.
  method SET_DISLS.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_DISLS ).

    move IF_DISLS to LS_PARAM-ATWRT.
    move IF_SETBY to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_DISLS_CHANGED ).
    endif.

  endmethod.
  method SET_DISMM.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_DISMM ).

    move IF_DISMM to LS_PARAM-ATWRT.
    move IF_SETBY to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_DISMM_CHANGED ).
    endif.

  endmethod.
  method SET_DISPO.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_DISPO ).

    move IF_DISPO to LS_PARAM-ATWRT.
    move IF_SETBY to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_DISPO_CHANGED ).
    endif.

  endmethod.
  method SET_EISBE.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = ME->F_PARID_EISBE ).

    if IS_EISBE is supplied.
      LS_PARAM-ATFLV = IS_EISBE-EISBE.
      LS_PARAM-UNIT  = IS_EISBE-EISBE_UOM.
    endif.
    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_EISBE_CHANGED ).
    endif.

  endmethod.
