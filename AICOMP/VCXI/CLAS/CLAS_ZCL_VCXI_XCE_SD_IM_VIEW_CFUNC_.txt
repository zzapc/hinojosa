
class ZCL_VCXI_XCE_SD_IM_VIEW_CFUNC definition
  public
  create public .

public section.

  interfaces IF_BADI_INTERFACE .
  interfaces IF_BADI_SDOC_VIEW_CUSTOM_FUNC .
protected section.

  constants C_FUNC_MU type SALV_DE_FUNCTION value 'ZVCXI_MURDE' ##NO_TEXT.

  methods HANDLE_UC_MU
    importing
      !IT_SELECTED_ROWS type SALV_T_ROW
      !IT_RESULT type STANDARD TABLE
    changing
      !CT_MESSAGE type TDT_SDOC_MSG
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_XCE_SD_IM_VIEW_CFUNC definition
class ZCL_VCXI_XCE_SD_IM_VIEW_CFUNC implementation.
  method HANDLE_UC_MU.

    data: LT_MUROB type ZVCXI_XCUR_TT_MUROB.
    data: LR_MURDE type ref to ZCL_VCXI_XCUR_MURDE,
          LR_MSG   type ref to /VCXI/CL_CKXM_MESSAGE.


***--------------------------------------------------------------------------------------
*** Create new Mass Update Definition
    create object LR_MURDE.

***--------------------------------------------------------------------------------------
*** Get selected Sales Documents as Objects
    loop at IT_SELECTED_ROWS into data(LS_SELECTED_ROW).
      check LINE_EXISTS( IT_RESULT[ LS_SELECTED_ROW ] ).
      append corresponding #( IT_RESULT[ LS_SELECTED_ROW ] ) to LT_MUROB.
    endloop.

*** Add new Group with Sales Documents
    LR_MURDE->ADD_MURGR( IT_MUROB = LT_MUROB ).

***--------------------------------------------------------------------------------------
*** Call Popup to maintain Mass Update
    ZCL_VCXI_XCUR_POPUP_MURDE=>START( exporting IR_MURDE = LR_MURDE
                                      importing ER_MSG   = LR_MSG ).
    if LR_MSG is bound.
      append lines of corresponding TDT_SDOC_MSG( LR_MSG->T_MSG ) to CT_MESSAGE.
    endif.

  endmethod.
  method IF_BADI_SDOC_VIEW_CUSTOM_FUNC~DEFINE_CUSTOM_FUNCTION.

*** SmartVC Mass Change
    insert value #( NAME = C_FUNC_MU
                    TEXT = conv #( text-F01 )
                  ) into table CT_CUSTOM_FUNCTION.

  endmethod.
  method IF_BADI_SDOC_VIEW_CUSTOM_FUNC~DO_CUSTOM_FUNCTION.

    try.
        case IV_NAME.
          when C_FUNC_MU.    "SmartVC Mass Update
            ME->HANDLE_UC_MU( exporting IT_SELECTED_ROWS = IT_SELECTED_ROWS
                                        IT_RESULT        = IT_RESULT
                              changing  CT_MESSAGE       = CT_MESSAGE ).
        endcase.

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        append corresponding #( LR_CX_CKX->GET_BAPIRET2( ) mapping MSGTY  = TYPE
                                                                   MSGID  = ID
                                                                   MSGNO  = NUMBER
                                                                   MSGV1  = MESSAGE_V1
                                                                   MSGV2  = MESSAGE_V2
                                                                   MSGV3  = MESSAGE_V3
                                                                   MSGV4  = MESSAGE_V4
                                                                   MSGTXT = MESSAGE
                              ) to CT_MESSAGE.
    endtry.

  endmethod.
endclass. "ZCL_VCXI_XCE_SD_IM_VIEW_CFUNC implementation
