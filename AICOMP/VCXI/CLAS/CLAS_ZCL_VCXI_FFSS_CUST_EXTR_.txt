
class ZCL_VCXI_FFSS_CUST_EXTR definition
  public
  create public .

public section.

  class-methods CLASS_CONSTRUCTOR .
  class-methods GET_FIRNG
    importing
      !IF_WERKS type WERKS_D
      !IF_TEXTR type ZVCXI_F0S_TEXTR
    returning
      value(RT_FIRNG) type ZVCXI_FFSS_TT_FIRNG
    raising
      /VCXI/CX_CKX .
protected section.

  types:
    YT_TC510 type standard table of ZVCXI_FFS_TC510 with default key .

  class-data T_TC510 type YT_TC510 .
private section.
endclass. "ZCL_VCXI_FFSS_CUST_EXTR definition
class ZCL_VCXI_FFSS_CUST_EXTR implementation.
  method CLASS_CONSTRUCTOR.

*** Load Extrusion Width Ranges
    select * from ZVCXI_FFS_TC510 into table T_TC510.

  endmethod.
  method GET_FIRNG.

    data: LS_TC510 type ZVCXI_FFS_TC510.
    data: LT_TC510 type YT_TC510.
    field-symbols: <S_FIRNG> type ZVCXI_FFSS_S_FIRNG.

***--------------------------------------------------------------------------------------
*** Find matching Ranges based on Access Sequence (Plant, Extrusion Type)
    move T_TC510 to LT_TC510.
    delete LT_TC510 where WERKS ne IF_WERKS or
                          TEXTR ne IF_TEXTR.

    if LINES( LT_TC510 ) lt 1.
***   Find with only Extrusion Type
      move T_TC510 to LT_TC510.
      delete LT_TC510 where WERKS ne SPACE or
                            TEXTR ne IF_TEXTR.
    endif.

*** Add found results to return table
    loop at LT_TC510 into LS_TC510.
      append initial line to RT_FIRNG assigning <S_FIRNG>.
      move-corresponding LS_TC510 to <S_FIRNG>.
    endloop.

  endmethod.
