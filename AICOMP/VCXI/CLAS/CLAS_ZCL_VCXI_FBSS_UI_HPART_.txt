
class ZCL_VCXI_FBSS_UI_HPART definition
  public
  inheriting from ZCL_VCXI_F0S_UI_TAB
  create public .

public section.

  data S_HPART_VIS type ZVCXI_FBSS_S_HPART_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABPT type ZVCXI_F0S_TABPT default '0100S_HP_TAB-TAB'
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_OUTPUT_VIS
    importing
      !IS_HPART_VIS type ZVCXI_FBSS_S_HPART_VIS
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_PBO
    redefinition .
protected section.

  data R_PR_HPART type ref to ZCL_VCXI_FBSS_PR_HPART .
  data R_TOOLBAR type ref to ZCL_VCXI_F0S_TOOLBAR_PRVDR .

  methods ADJUST_SCREEN_BY_OELMN
    importing
      !IF_OELMN type ZVCXI_FBS_OELMN
    changing
      !CS_SCREEN type SCREEN
    raising
      /VCXI/CX_CKX .
  methods INIT_VIS
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
private section.
endclass. "ZCL_VCXI_FBSS_UI_HPART definition
class ZCL_VCXI_FBSS_UI_HPART implementation.
  method ACTIVATE.

    data: LS_TC112  type  ZVCXI_FBS_TC112.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_FBSS_UI_HPART_BIND'
      exporting
        IR_UI_HPART   = ME
      importing
        ES_DYNP       = ME->S_DYNP
        EF_ACTIVE_TAB = ME->F_ACTIVE_TAB.

*** Get Handle Part Customizing
    move ME->R_PR_HPART->GET_HPART_CUSTOMIZING( ) to LS_TC112.
    check LS_TC112-OHNIN ne 'N'.

*** Create Toolbar for Provider
    create object ME->R_TOOLBAR
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_HP_TOOLBAR'.

  endmethod.
  method ADJUST_SCREEN_BY_OELMN.

    case IF_OELMN.
      when 'O'.
      when 'M'.
        move 2 to CS_SCREEN-REQUIRED.
      when 'N'.
        move 1 to CS_SCREEN-INVISIBLE.
        move 0 to CS_SCREEN-ACTIVE.
    endcase.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE
                        IF_TABPT  = IF_TABPT ).
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_HPART.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

*** Free Toolbar
    if ME->R_TOOLBAR is bound.
      ME->R_TOOLBAR->DO_FREE( ).
      clear ME->R_TOOLBAR.
    endif.

  endmethod.
  method DO_MODIFY_SCREEN.

    data: LF_LOCKED type        /VCXI/CKX_LOCKED,
          LF_INUSE  type        /VCXI/VKSR_INUSE.
    data: LS_TC112  type        ZVCXI_FBS_TC112.
    data: LR_CX_CKX type ref to /VCXI/CX_CKX.

    try.
        SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                 IF_DYNNR = IF_DYNNR ).

***     Get Handle Part Customizing
        move ME->R_PR_HPART->GET_HPART_CUSTOMIZING( ) to LS_TC112.

        LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
        LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***     Modify Scree using DynamicVC
        ZCL_VCXI_XCDR_SERVICE=>MODIFY_SCREEN( IR_GATE = ME->R_GATE ).

        loop at screen.

          if LF_LOCKED eq ABAP_TRUE or
             LF_INUSE  eq ABAP_TRUE.

            case SCREEN-GROUP1.
              when 'OHC'.

                ME->ADJUST_SCREEN_BY_OELMN( exporting IF_OELMN  = LS_TC112-OHNCO
                                            changing  CS_SCREEN = SCREEN ).
              when 'OHI'.

                ME->ADJUST_SCREEN_BY_OELMN( exporting IF_OELMN  = LS_TC112-OHNIN
                                            changing  CS_SCREEN = SCREEN ).
            endcase.

          endif.

***       Hide Toolbar
          if      SCREEN-NAME     eq '0100CC_HP_TOOLBAR'
              and LS_TC112-OHNIN  eq 'N'.
            move 1 to SCREEN-INVISIBLE.
            move 0 to SCREEN-ACTIVE.
          endif.

***       Reopen Info Buttons
          if SCREEN-GROUP1 eq 'INF'.
            move 1 to SCREEN-INPUT.
          endif.
          modify screen.

        endloop.

      catch /VCXI/CX_CKX into LR_CX_CKX.
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_PBO.

    data: LT_PRVDR_SELECTED type /VCXI/VKSR_TT_GATE.

    SUPER->DO_PBO( ).

*** Initialize Visualization Structure
    ME->INIT_VIS( ).

*** Initialize Toolbar and provide active tab as Selected Provider (for inserting/deletion)
    if ME->R_TOOLBAR is bound.
      append ME->R_OLD_GATE to LT_PRVDR_SELECTED.
      ME->R_TOOLBAR->REFRESH_TOOLBAR( IT_PRVDR_SELECTED = LT_PRVDR_SELECTED ).
    endif.

  endmethod.
  method DO_UPDATE_OUTPUT_VIS.

    check ME->R_PR_HPART is bound.

*** Set Handle Part Type
    if ME->S_HPART_VIS-TPHND ne IS_HPART_VIS-TPHND.
      ME->R_PR_HPART->SET_TPHND( IF_TPHND = IS_HPART_VIS-TPHND
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Handle Part Width
    if ME->S_HPART_VIS-HPWID ne IS_HPART_VIS-HPWID.
      ME->R_PR_HPART->SET_HPWID( IS_HPWID = IS_HPART_VIS-S_HPWID
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Handle Part Length
    if ME->S_HPART_VIS-HPLEN ne IS_HPART_VIS-HPLEN.
      ME->R_PR_HPART->SET_HPLEN( IS_HPLEN = IS_HPART_VIS-S_HPLEN
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Handle Part Factor
    if ME->S_HPART_VIS-HPFCT ne IS_HPART_VIS-HPFCT.
      ME->R_PR_HPART->SET_HPFCT( IF_HPFCT = IS_HPART_VIS-HPFCT
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Handle Part Cutout Area
    if ME->S_HPART_VIS-COARE ne IS_HPART_VIS-COARE.
      ME->R_PR_HPART->SET_COARE( IS_COARE = IS_HPART_VIS-S_COARE
                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_HPART.

  endmethod.
  method INIT_VIS.

    free: ME->S_HPART_VIS.
    check ME->R_PR_HPART is bound.

*** Handle Part Type
    ME->R_PR_HPART->GET_TPHND( importing EF_TPHND = ME->S_HPART_VIS-TPHND ).

*** Handle Part Width
    ME->R_PR_HPART->GET_HPWID( importing ES_HPWID = ME->S_HPART_VIS-S_HPWID ).

*** Handle Part Length
    ME->R_PR_HPART->GET_HPLEN( importing ES_HPLEN = ME->S_HPART_VIS-S_HPLEN ).

*** Handle Part Weight
    ME->R_PR_HPART->GET_HPWGT( importing ES_HPWGT = ME->S_HPART_VIS-S_HPWGT ).

*** Handle Part Factor
    ME->R_PR_HPART->GET_HPFCT( importing EF_HPFCT = ME->S_HPART_VIS-HPFCT ).

*** Handle Part Cutout Area
    ME->R_PR_HPART->GET_COARE( importing ES_COARE = ME->S_HPART_VIS-S_COARE ).

*** Handle Part Area
    ME->R_PR_HPART->GET_HPARE( importing ES_HPARE = ME->S_HPART_VIS-S_HPARE ).

  endmethod.
