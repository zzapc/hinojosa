
class ZCL_VCXI_P1SU_PR_SHIPMGR definition
  public
  inheriting from ZCL_VCXI_PXSU_PR_SHIPMGR
  create public .

public section.

  methods GET_BASE_DIMEN
    redefinition .
  methods GET_BASE_PRQTY
    redefinition .
  methods GET_NOUPS
    redefinition .
protected section.
private section.
endclass. "ZCL_VCXI_P1SU_PR_SHIPMGR definition
class ZCL_VCXI_P1SU_PR_SHIPMGR implementation.
  method GET_BASE_DIMEN.

    data: LS_PROWT type ZVCXI_PXS_S_PROWT.
    data: LR_PR_CFG      type ref to ZCL_VCXI_P1SS_PR_CFG,
          LR_COMM_NEXT   type ref to ZIF_VCXI_P1SS_COMM_NEXT,
          LR_BL_CRFPROP  type ref to ZCL_VCXI_P1SS_BL_CRFPROP,
          LR_PR_AREA_WGT type ref to ZCL_VCXI_PXSS_PR_AREA_WGT.


***--------------------------------------------------------------------------------------
*** Get Corrugator Forward Property XML (Outbound)
    case type of ME->R_GATE->GET_PARENT( )->R_CLSPR.
      when type ZCL_VCXI_P1SS_PR_CFG into LR_PR_CFG.
        LR_BL_CRFPROP ?= LR_PR_CFG->GET_CRFPX( ).
      when type ZIF_VCXI_P1SS_COMM_NEXT into LR_COMM_NEXT.
        LR_BL_CRFPROP ?= LR_COMM_NEXT->GET_CRFPX( ).
    endcase.

*** Set Dimensions
    if LR_BL_CRFPROP is bound.
      RT_DIMEN = value #( ( DIMEN     = ZCL_VCXI_XCSU_CUST_SHIPU=>C_DIMEN_LENGTH
                            DIMVA     = LR_BL_CRFPROP->S_HNDDIM-HNDLE
                            DIMVA_UOM = LR_BL_CRFPROP->S_HNDDIM-HNDLE_UOM )
                          ( DIMEN     = ZCL_VCXI_XCSU_CUST_SHIPU=>C_DIMEN_WIDTH
                            DIMVA     = LR_BL_CRFPROP->S_HNDDIM-HNDWI
                            DIMVA_UOM = LR_BL_CRFPROP->S_HNDDIM-HNDWI_UOM )
                          ( DIMEN     = ZCL_VCXI_XCSU_CUST_SHIPU=>C_DIMEN_HEIGHT
                            DIMVA     = LR_BL_CRFPROP->S_HNDDIM-HNDHE
                            DIMVA_UOM = LR_BL_CRFPROP->S_HNDDIM-HNDHE_UOM )
                         ).
    endif.

***--------------------------------------------------------------------------------------
*** Add Product Weight
    LR_PR_AREA_WGT = ME->GET_PRVDR_AREA_WGT( ).
    if LR_PR_AREA_WGT is bound.
      LS_PROWT = LR_PR_AREA_WGT->GET_PROWT( ).

      append value #( DIMEN     = ZCL_VCXI_XCSU_CUST_SHIPU=>C_DIMEN_WEIGHT
                      DIMVA     = LS_PROWT-PROWT
                      DIMVA_UOM = LS_PROWT-PROWT_UOM
                    ) to RT_DIMEN.
    endif.

  endmethod.
  method GET_BASE_PRQTY.

    RS_PRQTY-PRQTY     = 1.
    RS_PRQTY-PRQTY_UOM = 'ST'.

  endmethod.
  method GET_NOUPS.

    data: LR_PR_CFG    type ref to ZCL_VCXI_P1SS_PR_CFG,
          LR_COMM_NEXT type ref to ZIF_VCXI_P1SS_COMM_NEXT.


    case type of ME->R_GATE->GET_PARENT( )->R_CLSPR.
      when type ZCL_VCXI_P1SS_PR_CFG into LR_PR_CFG.
        RF_NOUPS = LR_PR_CFG->GET_NOUPS( ).
      when type ZIF_VCXI_P1SS_COMM_NEXT into LR_COMM_NEXT.
        RF_NOUPS = LR_COMM_NEXT->GET_NOUPS( ).
    endcase.

  endmethod.
endclass. "ZCL_VCXI_P1SU_PR_SHIPMGR implementation
