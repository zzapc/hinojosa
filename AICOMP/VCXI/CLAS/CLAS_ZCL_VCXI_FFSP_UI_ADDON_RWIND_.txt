
class ZCL_VCXI_FFSP_UI_ADDON_RWIND definition
  public
  inheriting from ZCL_VCXI_XCSP_UI_ADDON
  create public .

public section.

  data S_ADDON_RWIND_VIS type ZVCXI_FFSP_S_ADDON_RWIND_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  data R_GRID_MC_CORE type ref to ZCL_VCXI_FFSP_GRID_MC_CORE .
  data R_PR_ADDON_RWIND type ref to ZCL_VCXI_FFSP_PR_ADDON_RWIND .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_FFSP_UI_ADDON_RWIND definition
class ZCL_VCXI_FFSP_UI_ADDON_RWIND implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_FFSP_UI_ADDON_RWIND_BIND'
      exporting
        IR_UI_ADDON_RWIND = ME
      importing
        ES_DYNP           = ME->S_DYNP.

*** Create Grid for Cores
    create object ME->R_GRID_MC_CORE
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_MC_CORE'.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_ADDON_RWIND.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

    if ME->R_GRID_MC_CORE is bound.
      ME->R_GRID_MC_CORE->DO_FREE( ).
      clear ME->R_GRID_MC_CORE.
    endif.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Super PBO
    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize Grid
    if ME->R_GRID_MC_CORE is bound.
      ME->R_GRID_MC_CORE->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_MC_CORE->REFRESH_GRID( ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_ADDON_RWIND_VIS type ZVCXI_FFSP_S_ADDON_RWIND_VIS.


*** Super
    SUPER->DO_UPDATE_VIS( IS_VIS = IS_VIS ).
    check ME->R_PR_ADDON_RWIND is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_ADDON_RWIND_VIS.

***-------------------------------------------------------------------------------------
*** Set Winding Side
    if ME->S_ADDON_RWIND_VIS-RWSID ne LS_ADDON_RWIND_VIS-RWSID.
      ME->R_PR_ADDON_RWIND->SET_RWSID( exporting IF_RWSID = LS_ADDON_RWIND_VIS-RWSID
                                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Winding Direction
    if ME->S_ADDON_RWIND_VIS-RWDIR ne LS_ADDON_RWIND_VIS-RWDIR.
      ME->R_PR_ADDON_RWIND->SET_RWDIR( exporting IF_RWDIR = LS_ADDON_RWIND_VIS-RWDIR
                                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Eyemark Position
    if ME->S_ADDON_RWIND_VIS-EYEMP ne LS_ADDON_RWIND_VIS-EYEMP.
      ME->R_PR_ADDON_RWIND->SET_EYEMP( exporting IF_EYEMP = LS_ADDON_RWIND_VIS-EYEMP
                                                 IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    if ME->R_GRID_MC_CORE is bound.
      ME->R_GRID_MC_CORE->CHECK_CHANGED_DATA( ).
    endif.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_ADDON_RWIND.

  endmethod.
  method INIT_VIS.

*** Super
    SUPER->INIT_VIS( ).

    free: ME->S_ADDON_RWIND_VIS.
    ME->S_ADDON_RWIND_VIS-S_ADDON_VIS = ME->S_ADDON_VIS.

***--------------------------------------------------------------------------------------
*** Get Winding Side
    ME->R_PR_ADDON_RWIND->GET_RWSID( importing EF_RWSID = ME->S_ADDON_RWIND_VIS-RWSID ).

*** Get Winding Direction
    ME->R_PR_ADDON_RWIND->GET_RWDIR( importing EF_RWDIR = ME->S_ADDON_RWIND_VIS-RWDIR ).

*** Get Eyemark Position
    ME->R_PR_ADDON_RWIND->GET_EYEMP( importing EF_EYEMP = ME->S_ADDON_RWIND_VIS-EYEMP ).

  endmethod.
