
class ZCL_VCXI_PXSU_PR_SHIPMGR definition
  public
  inheriting from ZCL_VCXI_XCSU_PR_SHIPMGR
  abstract
  create public .

public section.

  interfaces ZIF_VCXI_XCS_NGREP .

  methods GET_NOUPS
  abstract
    returning
      value(RF_NOUPS) type ZVCXI_PXS_NOUPS
    raising
      /VCXI/CX_CKX .

  methods GET_KUNWE
    redefinition .
  methods GET_WERKS
    redefinition .
  methods HANDLE_EVENT
    redefinition .
protected section.

  constants C_IEVNT_I_REQ_WERKS_CHANGED type /VCXI/VKSC_IEVNT value 'REQUEST_WERKS_CHANGED' ##NO_TEXT.

  methods GET_PRVDR_SPEC
    exporting
      !ER_GATE type ref to /VCXI/CL_VKSR_GATE
      !ER_PR_SPEC type ref to ZCL_VCXI_PXSS_PR_SPEC
    raising
      /VCXI/CX_CKX .
  methods GET_PRVDR_AREA_WGT
    exporting
      !ER_GATE_AREA_WGT type ref to /VCXI/CL_VKSR_GATE
    returning
      value(RR_PR_AREA_WGT) type ref to ZCL_VCXI_PXSS_PR_AREA_WGT
    raising
      /VCXI/CX_CKX .

  methods GET_DFLT_APPDATA
    redefinition .
private section.
endclass. "ZCL_VCXI_PXSU_PR_SHIPMGR definition
class ZCL_VCXI_PXSU_PR_SHIPMGR implementation.
  method GET_DFLT_APPDATA.

    data: LF_VKORG type VKORG,
          LF_VTWEG type VTWEG,
          LF_SPART type SPART.
    data: LR_APPDATA type ref to ZCL_VCXI_XCS_APPDATA.


*** Get standard Application Data
    LR_APPDATA ?= SUPER->GET_DFLT_APPDATA( ).

*** Get Sales Area
    move LR_APPDATA->S_KNVV_WE-VKORG to LF_VKORG.
    if LF_VKORG is initial.
      move LR_APPDATA->S_KNVV_AG-VKORG to LF_VKORG.
    endif.

    move LR_APPDATA->S_KNVV_WE-VTWEG to LF_VTWEG.
    if LF_VTWEG is initial.
      move LR_APPDATA->S_KNVV_AG-VTWEG to LF_VTWEG.
    endif.

    move LR_APPDATA->S_KNVV_WE-SPART to LF_SPART.
    if LF_SPART is initial.
      move LR_APPDATA->S_KNVV_AG-SPART to LF_SPART.
    endif.

*** Get Instance of Application data
    RR_APPDATA = ZCL_VCXI_XCSUC_APPDATA=>GET_INSTANCE( IF_WERKS    = ME->GET_WERKS( )
                                                       IF_DCONF    = LR_APPDATA->F_DCONF
                                                       IF_KUNNR_WE = ME->GET_KUNWE( )
                                                       IF_KUNNR_AG = LR_APPDATA->S_KNA1_AG-KUNNR
                                                       IF_VKORG    = LF_VKORG
                                                       IF_VTWEG    = LF_VTWEG
                                                       IF_SPART    = LF_SPART
                                                       IF_CMATN    = LR_APPDATA->S_CMPRK-CMATN
                                                       IF_PRFID    = LR_APPDATA->S_CMPRK-PRFID ).

  endmethod.
  method GET_KUNWE.

*** Get Ship-To based on Receiving Plant of Connection Out
    if ME->R_GATE->GET_PARENT( )->R_CLSPR is instance of ZCL_VCXI_PXSS_PR_XCONO.
***   Get related Connection In
      RF_KUNWE = cast ZCL_VCXI_PXSS_PR_XCONO( ME->R_GATE->GET_PARENT( )->R_CLSPR )->GET_KUNWE( ).

*** Otherwise get standard
    else.
      RF_KUNWE = SUPER->GET_KUNWE( ).
    endif.

  endmethod.
  method GET_PRVDR_AREA_WGT.

    data: LT_GATE         type        /VCXI/VKSR_TT_GATE.
    data: LR_GATE         type ref to /VCXI/CL_VKSR_GATE.

***--------------------------------------------------------------------------------------
*** Get Area & Weight provider
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_PXSS_PR_AREA_WGT=>C_IPRVT_PX_AREA_WGT
                                     IF_INUSE = ABAP_TRUE ).

***--------------------------------------------------------------------------------------
*** Get Area & Weight Reference
    loop at LT_GATE into LR_GATE.
      try.
          move LR_GATE->R_CLSPR ?to RR_PR_AREA_WGT.
          check RR_PR_AREA_WGT is bound.
          move LR_GATE to ER_GATE_AREA_WGT.
          exit.

        catch CX_SY_MOVE_CAST_ERROR.
          clear: RR_PR_AREA_WGT, ER_GATE_AREA_WGT.
      endtry.
    endloop.

  endmethod.
  method GET_PRVDR_SPEC.

    data:  LT_GATE  type /VCXI/VKSR_TT_GATE.

    clear: ER_GATE,
           ER_PR_SPEC.

***------------------------------------------------------------------------------------------------
*** Find Provider for Specification
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_PXSS_PR_SPEC=>C_IPRVT_PX_SPEC
                                     IF_INUSE = ABAP_TRUE ).
    read table LT_GATE into ER_GATE index 1.
    check SY-SUBRC is initial.

    move ER_GATE->R_CLSPR ?to ER_PR_SPEC.

  endmethod.
  method GET_WERKS.

    data: LR_PR_SPEC type ref to ZCL_VCXI_PXSS_PR_SPEC.

    ME->GET_PRVDR_SPEC( importing ER_PR_SPEC = LR_PR_SPEC ).
    check LR_PR_SPEC is bound.
    RF_WERKS = LR_PR_SPEC->GET_WERKS( ).

  endmethod.
  method HANDLE_EVENT.

    SUPER->HANDLE_EVENT( IF_IEVNT         = IF_IEVNT
                         IT_GATE_PROVIDER = IT_GATE_PROVIDER ).

    case IF_IEVNT.
      when C_IEVNT_I_REQ_WERKS_CHANGED.
        ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_WERKS_CHANGED ).
    endcase.

  endmethod.
  method ZIF_VCXI_XCS_NGREP~FILL.

    data: LT_GATE_CPM    type /VCXI/VKSR_TT_GATE,
          LT_GATE_CPMCNF type /VCXI/VKSR_TT_GATE.


*** Get Parent Gate
    data(LR_GATE_PARENT) = ME->R_GATE->GET_PARENT( ).

***--------------------------------------------------------------------------------------
*** Get all Configured Packaging Materials of Shipping Units
    loop at ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCSU_PR_SHIPUNIT=>C_IPRVT_XCS_SHIPUNIT
                                   IF_INUSE = ABAP_TRUE ) into data(LR_GATE_SHIPUNIT).
      append lines of LR_GATE_SHIPUNIT->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCSU_PR_CPM=>C_IPRVT_XCS_CPM
                                                   IF_INUSE = ABAP_TRUE ) to LT_GATE_CPM.
    endloop.
    ME->R_GATE->SORT_GATE( changing CT_GATE = LT_GATE_CPM ).

*** Get Configuration of Configured Packaging Material
    loop at LT_GATE_CPM into data(LR_GATE_CPM).
      append lines of LR_GATE_CPM->GET_PRVDR( IF_IPRVT = ZIF_VCXI_XCSU_CPMCNF=>C_IPRVT_XCS_CPMCNF
                                              IF_INUSE = ABAP_TRUE ) to LT_GATE_CPMCNF.
    endloop.

***--------------------------------------------------------------------------------------
*** Process Configuration of Configured Packaging Material
    loop at LT_GATE_CPMCNF into data(LR_GATE_CPMCNF)
                          where TABLE_LINE->R_CLSPR is instance of ZCL_VCXI_PXSS_PR_XCPMCNF.
***   Get Connection In
      cast ZCL_VCXI_PXSS_PR_XCPMCNF( LR_GATE_CPMCNF->R_CLSPR )->GET_PRVDR_XCONI( importing ER_GATE = data(LR_GATE_XCONI) ).
      if LR_GATE_XCONI is not initial.
***     Register Connection In to Assembly Step
        CR_NETGRAPH_JSON->REGISTER_TO_NODE( IR_OBJECT   = LR_GATE_XCONI
                                            IF_NODE_KEY = CR_NETGRAPH_JSON->GET_NODE_KEY_BY_OBJECT( LR_GATE_PARENT ) ).
      endif.
    endloop.

  endmethod.
