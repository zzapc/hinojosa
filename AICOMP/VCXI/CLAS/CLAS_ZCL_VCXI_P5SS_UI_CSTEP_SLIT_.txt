
class ZCL_VCXI_P5SS_UI_CSTEP_SLIT definition
  public
  inheriting from ZCL_VCXI_P5SS_UI_CSTEP
  create public .

public section.

  data S_CSTEP_SLIT_VIS type ZVCXI_P5SS_S_CSTEP_SLIT_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
protected section.

  data R_PR_CSTEP_SLIT type ref to ZCL_VCXI_P5SS_PR_CSTEP_SLIT .

  methods INIT_VIS
    redefinition .
  methods HANDLE_GATE_FREED
    redefinition .
private section.
endclass. "ZCL_VCXI_P5SS_UI_CSTEP_SLIT definition
class ZCL_VCXI_P5SS_UI_CSTEP_SLIT implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

***--------------------------------------------------------------------------------------
*** Bind UI to Dynpro
    call function 'ZVCXI_P5SS_UI_CSTEP_SLIT_BIND'
      exporting
        IR_UI_CSTEP_SLIT = ME
      importing
        ES_DYNP          = ME->S_DYNP.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_CSTEP_SLIT.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_CSTEP_SLIT_VIS type ZVCXI_P5SS_S_CSTEP_SLIT_VIS.


*** Call SUPER
    SUPER->DO_UPDATE_VIS( IS_CSTEP_VIS = IS_CSTEP_VIS ).

    check ME->R_PR_CSTEP_SLIT is bound.

*** "Cast" Vis Structure
    move-corresponding IS_CSTEP_VIS to LS_CSTEP_SLIT_VIS.

***--------------------------------------------------------------------------------------
*** Number of Outs (Total)
    if ME->S_CSTEP_SLIT_VIS-NOUTT ne LS_CSTEP_SLIT_VIS-NOUTT.
      ME->R_PR_CSTEP_SLIT->SET_NOUTT( IF_NOUTT = LS_CSTEP_SLIT_VIS-NOUTT
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Trim on Width (Total)
    if ME->S_CSTEP_SLIT_VIS-S_TRWIT ne LS_CSTEP_SLIT_VIS-S_TRWIT.
      ME->R_PR_CSTEP_SLIT->SET_TRWIT( IS_TRWIT = LS_CSTEP_SLIT_VIS-S_TRWIT
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_CSTEP_SLIT.

  endmethod.
  method INIT_VIS.

    SUPER->INIT_VIS( ).

    free: ME->S_CSTEP_SLIT_VIS.
    ME->S_CSTEP_SLIT_VIS-S_CSTEP_VIS = ME->S_CSTEP_VIS.

***--------------------------------------------------------------------------------------
*** Number of Outs (Total)
    ME->S_CSTEP_SLIT_VIS-NOUTT = ME->R_PR_CSTEP_SLIT->GET_NOUTT( ).

*** Trim on Width (Total)
    ME->S_CSTEP_SLIT_VIS-S_TRWIT = ME->R_PR_CSTEP_SLIT->GET_TRWIT( ).

  endmethod.
