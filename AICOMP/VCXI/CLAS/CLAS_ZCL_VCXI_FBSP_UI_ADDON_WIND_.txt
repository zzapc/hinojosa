
class ZCL_VCXI_FBSP_UI_ADDON_WIND definition
  public
  inheriting from ZCL_VCXI_XCSP_UI_ADDON
  create public .

public section.

  data S_ADDON_WIND_VIS type ZVCXI_FBSP_S_ADDON_WIND_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  data R_GRID_MC_WNMAT type ref to ZCL_VCXI_FBSP_GRID_MC_WNMAT .
  data R_PR_ADDON_WIND type ref to ZCL_VCXI_FBSP_PR_ADDON_WIND .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_FBSP_UI_ADDON_WIND definition
class ZCL_VCXI_FBSP_UI_ADDON_WIND implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_FBSP_UI_ADDON_WIND_BIND'
      exporting
        IR_UI_ADDON_WIND = ME
      importing
        ES_DYNP          = ME->S_DYNP.

*** Create Grid for MC Winding Material
    create object ME->R_GRID_MC_WNMAT
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_MC_WNMAT'.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_ADDON_WIND.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

    if ME->R_GRID_MC_WNMAT is bound.
      ME->R_GRID_MC_WNMAT->DO_FREE( ).
      clear ME->R_GRID_MC_WNMAT.
    endif.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Super PBO
    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize Grid
    if ME->R_GRID_MC_WNMAT is bound.
      ME->R_GRID_MC_WNMAT->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_MC_WNMAT->REFRESH_GRID( ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    if ME->R_GRID_MC_WNMAT is bound.
      ME->R_GRID_MC_WNMAT->CHECK_CHANGED_DATA( ).
    endif.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_ADDON_WIND.

  endmethod.
  method INIT_VIS.

*** Super
    SUPER->INIT_VIS( ).

    free: ME->S_ADDON_WIND_VIS.
    ME->S_ADDON_WIND_VIS-S_ADDON_VIS = ME->S_ADDON_VIS.

  endmethod.
