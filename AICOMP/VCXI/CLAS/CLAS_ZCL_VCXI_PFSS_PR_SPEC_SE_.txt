
class ZCL_VCXI_PFSS_PR_SPEC_SE definition
  public
  inheriting from ZCL_VCXI_P0SS_PR_SPEC_SE
  create public .

public section.

  methods GET_GRDIR
    returning
      value(RF_GRDIR) type ZVCXI_PFSS_GRDIR
    raising
      /VCXI/CX_CKX .
  methods IS_MS_SHTPR_NO_DUMMY
    returning
      value(RF_RETURN) type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
  methods GET_GRAMM
    returning
      value(RS_GRAMM) type ZVCXI_P0SS_S_GRAMM
    raising
      /VCXI/CX_CKX .
  methods GET_PAPGR
    returning
      value(RF_PAPGR) type ZVCXI_PFSS_PAPGR
    raising
      /VCXI/CX_CKX .
  methods SET_GRAMM
    importing
      !IS_GRAMM type ZVCXI_P0SS_S_GRAMM
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_PAPGR
    importing
      !IF_PAPGR type ZVCXI_PFSS_PAPGR
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .

  methods GET_STATUS
    redefinition .
  methods GET_STATUS_MSG
    redefinition .
  methods SET_FSHTP
    redefinition .
protected section.

  methods CHECK_BOARD
    importing
      !IR_MESSAGE type ref to /VCXI/CL_CKXM_MESSAGE optional
    returning
      value(RF_ELMST) type /VCXI/VKSR_ELMST
    raising
      /VCXI/CX_CKX .
  methods GET_MS_SHTPR
    importing
      !IF_CREATE type ABAP_BOOL default ABAP_FALSE
    returning
      value(RR_GATE_MS_SHTPR) type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods GET_UCB
    importing
      !IF_CREATE type ABAP_BOOL default ABAP_FALSE
    returning
      value(RR_GATE_UCB) type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods CREATE_UCB
    returning
      value(RR_GATE_UCB) type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods REMOVE_EMPTY_UCB
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_PFSS_PR_SPEC_SE definition
class ZCL_VCXI_PFSS_PR_SPEC_SE implementation.
  method CHECK_BOARD.

    data: LR_GATE   type ref to /VCXI/CL_VKSR_GATE,
          LR_PR_UCB type ref to ZCL_VCXI_PFSS_PR_UCB.


***----------------------------------------------------------------------------
*** Get Unconverted Board
    LR_GATE = ME->GET_UCB( ).
    if LR_GATE is bound.
      try.
          move LR_GATE->R_CLSPR ?to LR_PR_UCB.
        catch CX_SY_MOVE_CAST_ERROR.
          clear LR_PR_UCB.
      endtry.
    endif.

***----------------------------------------------------------------------------
*** Check Board
    if LR_PR_UCB is bound or
       ME->GET_FSHTP( ) eq ABAP_TRUE.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G.
      if IR_MESSAGE is bound.
***     Board Information is maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_PFSS'
                                 IF_MSGTY = 'S'
                                 IF_MSGNO = '202' ).
      endif.
    else.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
      if IR_MESSAGE is bound.
***     Board Information is not maintained. Check UCB or Sheet Procurement.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_PFSS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '203' ).
      endif.
    endif.

  endmethod.
method CREATE_UCB.

  data:  LS_PRVDR_DEF type /VCXI/VKSR_S_PRVDR_DEF.
  data:  LT_PRVDR_DEF type /VCXI/VKSR_TT_PRVDR_DEF.

*** Get Unconverted Board Provider
  LT_PRVDR_DEF = ME->R_GATE->GET_PRVDR_DEF( IF_IPRVT = ZCL_VCXI_PFSS_PR_UCB=>C_IPRVT_PF_UCB
                                            IF_TPRVR = /VCXI/IF_VKSC_REGELMNT_PRVTY=>C_TPRVR_CHILD ).
  if lines( LT_PRVDR_DEF ) ne 1.
*** Provider &1 is not available for &2.
    /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE(
        IF_MSGTY = 'E'
        IF_MSGID = 'ZVCXI_XCS'
        IF_MSGNO = '010'
        IF_MSGV1 = ZCL_VCXI_PFSS_PR_UCB=>C_IPRVT_PF_UCB
        IF_MSGV2 = ME->R_GATE->F_ID ).
  endif.

  read table LT_PRVDR_DEF into LS_PRVDR_DEF index 1.

*** Create Unconverted Board
  RR_GATE_UCB = ME->R_GATE->CREATE_ELEMENT( IF_ID = LS_PRVDR_DEF-ID ).

*** Set Inuse if not already done
  if RR_GATE_UCB->IS_INUSE( ) ne ABAP_TRUE.
    RR_GATE_UCB->SET_INUSE( IF_INUSE = ABAP_TRUE ).
  endif.

endmethod.
method GET_GRAMM.

  data:  LR_GATE        type ref to /VCXI/CL_VKSR_GATE,
         LR_PR_UCB      type ref to ZCL_VCXI_PFSS_PR_UCB,
         LR_PR_MS_SHTPR type ref to ZCL_VCXI_PFSS_PR_MS_SHTPR.

  LR_GATE = ME->GET_UCB( ).
  if LR_GATE is bound.
    try.
        move LR_GATE->R_CLSPR ?to LR_PR_UCB.
      catch CX_SY_MOVE_CAST_ERROR.
        clear LR_PR_UCB.
    endtry.

*** Get Grammage of Unconverted Board
    check LR_PR_UCB is bound.
    RS_GRAMM = LR_PR_UCB->GET_GRAMM( ).

  else.

    LR_GATE = ME->GET_MS_SHTPR( ).
    if LR_GATE is bound.
      try.
          move LR_GATE->R_CLSPR ?to LR_PR_MS_SHTPR.
        catch CX_SY_MOVE_CAST_ERROR.
          clear LR_PR_MS_SHTPR.
      endtry.

***   Get Grammage of Sheet Procurement
      check LR_PR_MS_SHTPR is bound.
      RS_GRAMM = LR_PR_MS_SHTPR->GET_GRAMM( ).

    endif.
  endif.

endmethod.
method GET_GRDIR.

  data:  LS_AGGRA       type        ZVCXI_PFSS_S_AGGRA,
         LS_INGRA       type        ZVCXI_PFSS_S_INGRA,
         LS_SHTLE       type        ZVCXI_P0SS_S_SHTLE,
         LS_SHTWI       type        ZVCXI_P0SS_S_SHTWI.
  data:  LR_GATE        type ref to /VCXI/CL_VKSR_GATE,
         LR_PR_UCB      type ref to ZCL_VCXI_PFSS_PR_UCB,
         LR_PR_MS_SHTPR type ref to ZCL_VCXI_PFSS_PR_MS_SHTPR.

  LR_GATE = ME->GET_UCB( ).
  if LR_GATE is bound.
    try.
        move LR_GATE->R_CLSPR ?to LR_PR_UCB.
      catch CX_SY_MOVE_CAST_ERROR.
        clear LR_PR_UCB.
    endtry.

*** Get Paper Grade of Unconverted Board
    check LR_PR_UCB is bound.
    RF_GRDIR = LR_PR_UCB->GET_GRDIR( ).

  else.

    LR_GATE = ME->GET_MS_SHTPR( ).
    if LR_GATE is bound.
      try.
          move LR_GATE->R_CLSPR ?to LR_PR_MS_SHTPR.
        catch CX_SY_MOVE_CAST_ERROR.
          clear LR_PR_MS_SHTPR.
      endtry.

***   Get Paper Grade of Sheet Procurement
      check LR_PR_MS_SHTPR is bound.

***   Get Sheet Size
      LS_SHTWI = LR_PR_MS_SHTPR->GET_SHTWI( ).
      LS_SHTLE = LR_PR_MS_SHTPR->GET_SHTLE( ).

      move LS_SHTWI-SHTWI     to LS_AGGRA-AGGRA.
      move LS_SHTWI-SHTWI_UOM to LS_AGGRA-AGGRA_UOM.

      move LS_SHTLE-SHTLE     to LS_INGRA-INGRA.
      move LS_SHTLE-SHTLE_UOM to LS_INGRA-INGRA_UOM.

***   Get Grain Direction
      RF_GRDIR = ZCL_VCXI_PFSS_CUST_SBOARD=>GET_GRDIR( IS_AGGRA = LS_AGGRA
                                                       IS_INGRA = LS_INGRA ).

    endif.
  endif.

endmethod.
method GET_MS_SHTPR.

  data:  LT_GATE            type        /VCXI/VKSR_TT_GATE.
  data:  LR_GATE            type ref to /VCXI/CL_VKSR_GATE,
         LR_PR_MS_SHTPR     type ref to ZCL_VCXI_PFSS_PR_MS_SHTPR.

***------------------------------------------------------------------
*** Material Specification - Sheet Procurement
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_PFSS_PR_MS_SHTPR=>C_IPRVT_PF_MS_SHTPR
                                   IF_INUSE  = ABAP_TRUE ).

***----------------------------------------------------------------------------
*** Get Material Specification - Sheet Procurement
  loop at LT_GATE into LR_GATE.
    try.
        move LR_GATE->R_CLSPR ?to LR_PR_MS_SHTPR.
        check LR_PR_MS_SHTPR is bound.
        move LR_GATE to RR_GATE_MS_SHTPR.
        exit.

      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.
  endloop.


  check IF_CREATE        eq ABAP_TRUE and
        RR_GATE_MS_SHTPR is not bound.

  LR_GATE = ME->CREATE_SHTPR( ).
  check LR_GATE is bound.
  try.
      move LR_GATE->R_CLSPR ?to LR_PR_MS_SHTPR.
      check LR_PR_MS_SHTPR is bound.
      move LR_GATE to RR_GATE_MS_SHTPR.
    catch CX_SY_MOVE_CAST_ERROR.
      clear: LR_PR_MS_SHTPR, RR_GATE_MS_SHTPR.
  endtry.

endmethod.
method GET_PAPGR.

  data:  LR_GATE        type ref to /VCXI/CL_VKSR_GATE,
         LR_PR_UCB      type ref to ZCL_VCXI_PFSS_PR_UCB,
         LR_PR_MS_SHTPR type ref to ZCL_VCXI_PFSS_PR_MS_SHTPR.

  LR_GATE = ME->GET_UCB( ).
  if LR_GATE is bound.
    try.
        move LR_GATE->R_CLSPR ?to LR_PR_UCB.
      catch CX_SY_MOVE_CAST_ERROR.
        clear LR_PR_UCB.
    endtry.

*** Get Paper Grade of Unconverted Board
    check LR_PR_UCB is bound.
    RF_PAPGR = LR_PR_UCB->GET_PAPGR( ).

  else.

    LR_GATE = ME->GET_MS_SHTPR( ).
    if LR_GATE is bound.
      try.
          move LR_GATE->R_CLSPR ?to LR_PR_MS_SHTPR.
        catch CX_SY_MOVE_CAST_ERROR.
          clear LR_PR_MS_SHTPR.
      endtry.

***   Get Paper Grade of Sheet Procurement
      check LR_PR_MS_SHTPR is bound.
      RF_PAPGR = LR_PR_MS_SHTPR->GET_PAPGR( ).

    endif.
  endif.

endmethod.
  method GET_STATUS.

*** Super Status
    RF_ELMST = SUPER->GET_STATUS( ).

***----------------------------------------------------------------------------
*** Check Board
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = ME->CHECK_BOARD( ) ).

  endmethod.
  method GET_STATUS_MSG.

    SUPER->GET_STATUS_MSG( IR_MESSAGE = IR_MESSAGE ).

***----------------------------------------------------------------------------
*** Check Board
    ME->CHECK_BOARD( IR_MESSAGE = IR_MESSAGE ).

  endmethod.
method GET_UCB.

  data:  LT_GATE       type        /VCXI/VKSR_TT_GATE.
  data:  LR_GATE       type ref to /VCXI/CL_VKSR_GATE,
         LR_PR_UCB     type ref to ZCL_VCXI_PFSS_PR_UCB.

***------------------------------------------------------------------
*** Unconverted Board
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ZCL_VCXI_PFSS_PR_UCB=>C_IPRVT_PF_UCB
                                   IF_INUSE  = ABAP_TRUE ).

***----------------------------------------------------------------------------
*** Get Unconverted Board
  loop at LT_GATE into LR_GATE.
    try.
        move LR_GATE->R_CLSPR ?to LR_PR_UCB.
        check LR_PR_UCB is bound.
        move LR_GATE to RR_GATE_UCB.
        exit.

      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.
  endloop.


  check IF_CREATE   eq ABAP_TRUE and
        RR_GATE_UCB is not bound.

  LR_GATE = ME->CREATE_UCB( ).
  check LR_GATE is bound.
  try.
      move LR_GATE->R_CLSPR ?to LR_PR_UCB.
      check LR_PR_UCB is bound.
      move LR_GATE to RR_GATE_UCB.
    catch CX_SY_MOVE_CAST_ERROR.
      clear: LR_PR_UCB, RR_GATE_UCB.
  endtry.

endmethod.
method IS_MS_SHTPR_NO_DUMMY.

  data:  LR_GATE        type ref to /VCXI/CL_VKSR_GATE,
         LR_PR_MS_SHTPR type ref to ZCL_VCXI_PFSS_PR_MS_SHTPR.

*** Get Material Specification - Sheet Procurement
  LR_GATE = ME->GET_MS_SHTPR( ).
  check LR_GATE is bound.
  try.
      move LR_GATE->R_CLSPR ?to LR_PR_MS_SHTPR.
    catch CX_SY_MOVE_CAST_ERROR.
      clear LR_PR_MS_SHTPR.
  endtry.

  check LR_PR_MS_SHTPR->IS_DUMMY( ) eq ABAP_FALSE.
  move ABAP_TRUE to RF_RETURN.

endmethod.
method REMOVE_EMPTY_UCB.

  data:  LS_GRAMM       type        ZVCXI_P0SS_S_GRAMM.
  data:  LR_PR_UCB      type ref to ZCL_VCXI_PFSS_PR_UCB.

  try.
      move IR_GATE->R_CLSPR ?to LR_PR_UCB.
    catch CX_SY_MOVE_CAST_ERROR.
      clear: LR_PR_UCB.
  endtry.

  check LR_PR_UCB is bound.
  LS_GRAMM = LR_PR_UCB->GET_GRAMM( ).

  if LS_GRAMM-GRAMM          is initial and
     LR_PR_UCB->GET_PAPGR( ) is initial.
    IR_GATE->DO_FREE( ).
  endif.

endmethod.
  method SET_FSHTP.

    data:  LF_PAPGR           type        ZVCXI_PFSS_PAPGR.
    data:  LS_GRAMM           type        ZVCXI_P0SS_S_GRAMM.
    data: LR_GATE          type ref to /VCXI/CL_VKSR_GATE,
          LR_GATE_UCB      type ref to /VCXI/CL_VKSR_GATE,
          LR_GATE_MS_SHTPR type ref to /VCXI/CL_VKSR_GATE,
          LR_PR_MS_SHTPR   type ref to ZCL_VCXI_PFSS_PR_MS_SHTPR,
          LR_PR_UCB        type ref to ZCL_VCXI_PFSS_PR_UCB.

***------------------------------------------------------------------------------------------------
*** Set Flag if Sheet Procurement is there
    LR_GATE = ME->GET_MS_SHTPR( ).

    if LR_GATE is bound.
      try.
          move LR_GATE->R_CLSPR ?to LR_PR_MS_SHTPR.
        catch CX_SY_MOVE_CAST_ERROR.
          clear: LR_PR_MS_SHTPR.
      endtry.
    endif.

*** Free Sheet Procurement
    if IF_FSHTP is initial.
      if LR_PR_MS_SHTPR is bound.
        LR_PR_MS_SHTPR->DO_FREE( ).
        free: LR_PR_MS_SHTPR.
      endif.
    else.

***   Set Sheet Procurement
      if LR_PR_MS_SHTPR is not bound.
***     Create Sheet Procurement
        LR_GATE_MS_SHTPR = ME->CREATE_SHTPR( ).

***     Clear Unconverted Board References as they are not longer needed
        LR_GATE_UCB = ME->GET_UCB( ).
        check LR_GATE_UCB is bound.
        try.
            move LR_GATE_UCB->R_CLSPR ?to LR_PR_UCB.
          catch CX_SY_MOVE_CAST_ERROR.
            clear: LR_PR_UCB.
        endtry.

        if LR_PR_UCB is bound.

          try.
              move LR_GATE_MS_SHTPR->R_CLSPR ?to LR_PR_MS_SHTPR.
            catch CX_SY_MOVE_CAST_ERROR.
              clear: LR_PR_MS_SHTPR.
          endtry.

          if LR_PR_MS_SHTPR is bound.
            LF_PAPGR = LR_PR_UCB->GET_PAPGR( ).
            LS_GRAMM = LR_PR_UCB->GET_GRAMM( ).

            LR_PR_MS_SHTPR->SET_PAPGR( IF_PAPGR = LF_PAPGR ).
            LR_PR_MS_SHTPR->SET_GRAMM( IS_GRAMM = LS_GRAMM ).

          endif.
        endif.

        LR_GATE_UCB->DO_FREE( ).
        clear: LR_GATE_UCB.
      endif.
    endif.

  endmethod.
method SET_GRAMM.

  data:  LF_CREATE      type        ABAP_BOOL.
  data:  LR_GATE        type ref to /VCXI/CL_VKSR_GATE,
         LR_PR_UCB      type ref to ZCL_VCXI_PFSS_PR_UCB,
         LR_PR_MS_SHTPR type ref to ZCL_VCXI_PFSS_PR_MS_SHTPR.

*** Set Grammage
  if IS_GRAMM-GRAMM is not initial.
    LF_CREATE = ABAP_TRUE.
  else.
*** Delete Grammage
    LF_CREATE = ABAP_TRUE.
  endif.

  if ME->GET_FSHTP( ) is initial.
*** Get Unconverted Board
    LR_GATE = ME->GET_UCB( IF_CREATE = LF_CREATE ).
    if LR_GATE is bound.
      try.
          move LR_GATE->R_CLSPR ?to LR_PR_UCB.
        catch CX_SY_MOVE_CAST_ERROR.
          clear LR_PR_UCB.
      endtry.

      check LR_PR_UCB is bound.
      LR_PR_UCB->SET_GRAMM( IS_GRAMM = IS_GRAMM
                            IF_SETBY = IF_SETBY ).

      check LF_CREATE eq ABAP_FALSE.
      ME->REMOVE_EMPTY_UCB( IR_GATE = LR_GATE ).
    endif.

  else.
*** Get Material Specification
    LR_GATE = ME->GET_MS_SHTPR( ).
    if LR_GATE is bound.
      try.
          move LR_GATE->R_CLSPR ?to LR_PR_MS_SHTPR.
        catch CX_SY_MOVE_CAST_ERROR.
          clear LR_PR_MS_SHTPR.
      endtry.

      check LR_PR_MS_SHTPR is bound.
      LR_PR_MS_SHTPR->SET_GRAMM( IS_GRAMM = IS_GRAMM
                                 IF_SETBY = IF_SETBY ).
    endif.

  endif.

endmethod.
method SET_PAPGR.

  data:  LF_CREATE      type        ABAP_BOOL.
  data:  LR_GATE        type ref to /VCXI/CL_VKSR_GATE,
         LR_PR_UCB      type ref to ZCL_VCXI_PFSS_PR_UCB,
         LR_PR_MS_SHTPR type ref to ZCL_VCXI_PFSS_PR_MS_SHTPR.

*** Set Paper Grade ( UCB )
  if IF_PAPGR is not initial.
    LF_CREATE = ABAP_TRUE.
  else.
*** Delete Paper Grade ( UCB )
    LF_CREATE = ABAP_FALSE.
  endif.

  if ME->GET_FSHTP( ) is initial.
*** Get Unconverted Board
    LR_GATE = ME->GET_UCB( IF_CREATE = LF_CREATE ).
    if LR_GATE is bound.
      try.
          move LR_GATE->R_CLSPR ?to LR_PR_UCB.
        catch CX_SY_MOVE_CAST_ERROR.
          clear LR_PR_UCB.
      endtry.

      check LR_PR_UCB is bound.
      LR_PR_UCB->SET_PAPGR( IF_PAPGR = IF_PAPGR
                            IF_SETBY = IF_SETBY ).

      check LF_CREATE eq ABAP_FALSE.
      ME->REMOVE_EMPTY_UCB( IR_GATE = LR_GATE ).
    endif.

  else.
*** Get Material Specification - Sheet Procurement
    LR_GATE = ME->GET_MS_SHTPR( ).
    if LR_GATE is bound.
      try.
          move LR_GATE->R_CLSPR ?to LR_PR_MS_SHTPR.
        catch CX_SY_MOVE_CAST_ERROR.
          clear LR_PR_MS_SHTPR.
      endtry.

      check LR_PR_MS_SHTPR is bound.
      LR_PR_MS_SHTPR->SET_PAPGR( IF_PAPGR = IF_PAPGR
                                 IF_SETBY = IF_SETBY ).

    endif.
  endif.

endmethod.
