
class ZCL_VCXI_FFSS_BL_FIBPROP definition
  public
  create public .

public section.

  data T_CSPLIT type ZVCXI_FFSS_TT_CSPLIT read-only .

  class-methods FACTORY
    importing
      !IF_FIBPX type ZVCXI_FFS_FIBPX optional
    returning
      value(RR_BL_FIBPROP) type ref to ZCL_VCXI_FFSS_BL_FIBPROP
    raising
      /VCXI/CX_CKX .
  methods SET_CSPLIT
    importing
      !IT_CSPLIT type ZVCXI_FFSS_TT_CSPLIT
    raising
      /VCXI/CX_CKX .
  methods MERGE
    importing
      !IR_BL_FIBPROP type ref to ZCL_VCXI_FFSS_BL_FIBPROP
    raising
      /VCXI/CX_CKX .
  methods SET_FIBPX
    importing
      !IF_FIBPX type ZVCXI_FFS_FIBPX
    raising
      /VCXI/CX_CKX .
  methods GET_FIBPX
    returning
      value(RF_FIBPX) type ZVCXI_FFS_FIBPX
    raising
      /VCXI/CX_CKX .
protected section.
private section.
endclass. "ZCL_VCXI_FFSS_BL_FIBPROP definition
class ZCL_VCXI_FFSS_BL_FIBPROP implementation.
  method FACTORY.

*** Create BL Reference
enhancement-section FACTORY spots ZVCXI_FFSS_ES_BL_FIBPROP .
    create object RR_BL_FIBPROP.
end-enhancement-section.

*** Set Film Property XML
    RR_BL_FIBPROP->SET_FIBPX( IF_FIBPX = IF_FIBPX ).

  endmethod.
  method GET_FIBPX.

***--------------------------------------------------------------------------------------
*** Serialize to XML
    call transformation ID
         source T_CSPLIT  = ME->T_CSPLIT
         result xml RF_FIBPX.

  endmethod.
  method MERGE.

    data: LS_CSPLIT            type       ZVCXI_FFSS_S_CSPLIT.
    data: LT_CSPLIT            type       ZVCXI_FFSS_TT_CSPLIT.
    field-symbols: <S_CSPLIT>  type       ZVCXI_FFSS_S_CSPLIT.

    check IR_BL_FIBPROP is bound.

*** Merge Cost Split Distribution
    move ME->T_CSPLIT to LT_CSPLIT.
    loop at IR_BL_FIBPROP->T_CSPLIT into LS_CSPLIT.
      read table LT_CSPLIT assigning <S_CSPLIT> with key ICSPL = LS_CSPLIT-ICSPL.
      if SY-SUBRC ne 0.
***     Add new Entry for this ID
        append initial line to LT_CSPLIT assigning <S_CSPLIT>.
        move LS_CSPLIT-ICSPL   to <S_CSPLIT>-ICSPL.
        move LS_CSPLIT-S_DCSPL to <S_CSPLIT>-S_DCSPL.
***     Set Component Ratio
        move LS_CSPLIT-COMPR   to <S_CSPLIT>-COMPR.
      else.
***     Add to Cost Split for this ID
        ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = <S_CSPLIT>-DCSPL
                                                            IF_CALCV_UOM_1 = <S_CSPLIT>-DCSPL_UOM
                                                            IF_CALCV_2     = LS_CSPLIT-DCSPL
                                                            IF_CALCV_UOM_2 = LS_CSPLIT-DCSPL_UOM
                                                            IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_ADD
                                                            IF_UOM         = <S_CSPLIT>-DCSPL_UOM
                                                  importing EF_CALCV       = <S_CSPLIT>-DCSPL
                                                            EF_CALCV_UOM   = <S_CSPLIT>-DCSPL_UOM ).
        add LS_CSPLIT-COMPR to <S_CSPLIT>-COMPR.
      endif.

    endloop.
    ME->SET_CSPLIT( IT_CSPLIT = LT_CSPLIT ).

  endmethod.
  method SET_CSPLIT.

    move IT_CSPLIT to ME->T_CSPLIT.

  endmethod.
  method SET_FIBPX.

***--------------------------------------------------------------------------------------
*** Deserialize XML
    if IF_FIBPX is not initial.
      call transformation ID
           source xml IF_FIBPX
           result T_CSPLIT   = ME->T_CSPLIT.
    endif.

  endmethod.
