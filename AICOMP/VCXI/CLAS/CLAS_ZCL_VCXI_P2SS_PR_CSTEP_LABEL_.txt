
class ZCL_VCXI_P2SS_PR_CSTEP_LABEL definition
  public
  inheriting from ZCL_VCXI_P2SS_PR_CSTEP
  create public .

public section.

  interfaces ZIF_VCXI_XCS_PSCALE_MGR .

  constants C_IEVNT_O_LIFNR_CHANGED type /VCXI/VKSC_IEVNT value 'LIFNR_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_MAPPL_CHANGED type /VCXI/VKSC_IEVNT value 'MAPPL_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_SCRAP_CHANGED type /VCXI/VKSC_IEVNT value 'SCRAP_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_SHTDI_CHANGED type /VCXI/VKSC_IEVNT value 'SHTDI_CHANGED' ##NO_TEXT.
  constants C_IEVNT_O_SPEED_CHANGED type /VCXI/VKSC_IEVNT value 'SPEED_CHANGED' ##NO_TEXT.
  constants C_IPRVT_P2_CSTEP_LABEL type /VCXI/VKSC_IPRVT value 'ZVCXI_P2SS_CSTEP_LABEL' ##NO_TEXT.
  constants C_MAPPL_MANUAL type ZVCXI_PXS_MAPPL value 'M' ##NO_TEXT.
  constants C_MAPPL_OUTSOURCE type ZVCXI_PXS_MAPPL value 'O' ##NO_TEXT.
  constants C_MAPPL_STANDARD type ZVCXI_PXS_MAPPL value 'S' ##NO_TEXT.
  constants C_PARID_LIFNR type /VCXI/VKS_PARID value 'ZVCXI_XC_LIFNR' ##NO_TEXT.
  constants C_PARID_MAPPL type /VCXI/VKS_PARID value 'ZVCXI_PX_MAPPL' ##NO_TEXT.
  constants C_PARID_MSRSR type /VCXI/VKS_PARID value 'ZVCXI_XC_MSRSR_P' ##NO_TEXT.
  constants C_PARID_MSRST type /VCXI/VKS_PARID value 'ZVCXI_XC_MSRST' ##NO_TEXT.
  constants C_PARID_SCRAP type /VCXI/VKS_PARID value 'ZVCXI_XC_SCRAP' ##NO_TEXT.
  constants C_PARID_SHTDI type /VCXI/VKS_PARID value 'ZVCXI_P0_SHTDI' ##NO_TEXT.

  methods GET_LIFNR
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
      !EF_DESCR type /VCXI/CKX_DESCR
    returning
      value(RF_LIFNR) type LIFNR
    raising
      /VCXI/CX_CKX .
  methods GET_MSRS
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_MSRS) type /VCXI/VSM_S_MSRS
    raising
      /VCXI/CX_CKX .
  methods GET_SCRAP
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RS_SCRAP) type ZVCXI_XCSP_S_SCRAP
    raising
      /VCXI/CX_CKX .
  methods GET_SHTDI
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
      !EF_DESCR type /VCXI/CKX_DESCR
    returning
      value(RF_SHTDI) type ZVCXI_P0SS_SHTDI
    raising
      /VCXI/CX_CKX .
  methods GET_MAPPL
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RF_MAPPL) type ZVCXI_PXS_MAPPL
    raising
      /VCXI/CX_CKX .
  methods SET_LIFNR
    importing
      !IF_LIFNR type LIFNR optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_MSRS
    importing
      !IS_MSRS type /VCXI/VSM_S_MSRS optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_SCRAP
    importing
      !IS_SCRAP type ZVCXI_XCSP_S_SCRAP optional
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_SHTDI
    importing
      !IF_SHTDI type ZVCXI_P0SS_SHTDI
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods SET_MAPPL
    importing
      !IF_MAPPL type ZVCXI_PXS_MAPPL
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .

  methods GET_STATUS
    redefinition .
  methods GET_STATUS_MSG
    redefinition .
  methods HANDLE_EVENT
    redefinition .
  methods ZIF_VCXI_P2SS_COMM_NEXT~GET_FCFPX
    redefinition .
protected section.

  constants C_IEVNT_I_INIT_BY_MAPPL type /VCXI/VKSC_IEVNT value 'INIT_BY_MAPPL' ##NO_TEXT.

  methods HANDLE_INIT_BY_MAPPL
    raising
      /VCXI/CX_CKX .
  methods CHECK_MSRS
    importing
      value(IR_MESSAGE) type ref to /VCXI/CL_CKXM_MESSAGE optional
    returning
      value(RF_ELMST) type /VCXI/VKSR_ELMST
    raising
      /VCXI/CX_CKX .
  methods CHECK_PSCALE
    importing
      !IR_MESSAGE type ref to /VCXI/CL_CKXM_MESSAGE optional
    returning
      value(RF_ELMST) type /VCXI/VKSR_ELMST
    raising
      /VCXI/CX_CKX .
  methods GET_MS_LABEL_ADDWT
    returning
      value(RS_ADDWT) type ZVCXI_PXS_S_ADDWT
    raising
      /VCXI/CX_CKX .
  methods CHECK_MS_LABEL
    importing
      !IR_MESSAGE type ref to /VCXI/CL_CKXM_MESSAGE optional
    returning
      value(RF_ELMST) type /VCXI/VKSR_ELMST
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_P2SS_PR_CSTEP_LABEL definition
class ZCL_VCXI_P2SS_PR_CSTEP_LABEL implementation.
  method CHECK_MS_LABEL.

*** Check Minimum Number of Labels
    data(LT_GATE) = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_PXSS_PR_MS_LABEL=>C_IPRVT_PX_MS_LABEL
                                           IF_INUSE = ABAP_TRUE ).

*** Check Minimum
    if LINES( LT_GATE ) lt 1.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
      if IR_MESSAGE is bound.
***     Minimum one Label is required.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_PXSS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '371' ).
      endif.
    endif.

  endmethod.
  method CHECK_MSRS.

    check ME->GET_MAPPL( ) eq C_MAPPL_MANUAL.

*** Check Machine Speed
    data(LS_MSRS) = ME->GET_MSRS( ).
    if LS_MSRS-MSRSR is not initial and
       LS_MSRS-MSRST is not initial.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_G.
      if IR_MESSAGE is bound.
***     Machine Speed is maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_PXSS'
                                 IF_MSGTY = 'S'
                                 IF_MSGNO = '354' ).
      endif.
    else.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
      if IR_MESSAGE is bound.
***     Machine Speed is not maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_PXSS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '355' ).
      endif.
    endif.

  endmethod.
  method CHECK_PSCALE.

    check ME->GET_MAPPL( ) eq C_MAPPL_OUTSOURCE.

***--------------------------------------------------------------------------------------
*** Check Price Scales
    if ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCS_PR_PSCALE=>C_IPRVT_XCS_PSCALE
                                     IF_INUSE = ABAP_TRUE ) is initial.
      RF_ELMST = /VCXI/CL_VKSR_SERVICE=>C_ELMST_R.
      if IR_MESSAGE is bound.
***     No Price Scales maintained.
        IR_MESSAGE->ADD_MESSAGE( IF_MSGID = 'ZVCXI_PXSS'
                                 IF_MSGTY = 'E'
                                 IF_MSGNO = '353' ).
      endif.
    endif.

  endmethod.
  method GET_LIFNR.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_LIFNR ).
    RF_LIFNR = LS_PARAM-ATWRT.
    EF_SETBY = LS_PARAM-SETBY.

    check RF_LIFNR is not initial.

    if EF_DESCR is requested.
      EF_DESCR = ZCL_VCXI_XCS_SERVICE=>GET_LIFNR_NAME1( IF_LIFNR = RF_LIFNR ).
    endif.

  endmethod.
  method GET_MAPPL.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_MAPPL ).
    RF_MAPPL = LS_PARAM-ATWRT.
    EF_SETBY = LS_PARAM-SETBY.

  endmethod.
  method GET_MS_LABEL_ADDWT.

    loop at ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_PXSS_PR_MS_LABEL=>C_IPRVT_PX_MS_LABEL
                                   IF_INUSE = ABAP_TRUE ) into data(LR_GATE).
***   Get Additional Weight of Labels
      data(LS_ADDWT) = cast ZCL_VCXI_PXSS_PR_MS_LABEL( LR_GATE->R_CLSPR )->GET_ADDWT( ).

      check LS_ADDWT-ADDWT_UOM is not initial.

      if RS_ADDWT is initial.
        RS_ADDWT = LS_ADDWT.
      else.
        ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_ADDWT-ADDWT
                                                            IF_CALCV_UOM_1 = LS_ADDWT-ADDWT_UOM
                                                            IF_CALCV_2     = RS_ADDWT-ADDWT
                                                            IF_CALCV_UOM_2 = RS_ADDWT-ADDWT_UOM
                                                            IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_ADD
                                                            IF_UOM         = RS_ADDWT-ADDWT_UOM
                                                  importing EF_CALCV       = RS_ADDWT-ADDWT
                                                            EF_CALCV_UOM   = RS_ADDWT-ADDWT_UOM ).
      endif.
    endloop.

  endmethod.
  method GET_MSRS.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_MSRSR ).
    RS_MSRS-MSRSR     = LS_PARAM-ATFLV.
    RS_MSRS-MSRSR_UOM = LS_PARAM-UNIT.
    EF_SETBY          = LS_PARAM-SETBY.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_MSRST ).
    RS_MSRS-MSRST     = LS_PARAM-ATFLV.
    RS_MSRS-MSRST_UOM = LS_PARAM-UNIT.

  endmethod.
  method GET_SCRAP.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SCRAP ).
    RS_SCRAP = value #( SCRAP     = LS_PARAM-ATFLV
                        SCRAP_UOM = LS_PARAM-UNIT ).
    EF_SETBY = LS_PARAM-SETBY.

  endmethod.
  method GET_SHTDI.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.
    data:  LT_D07V    type standard table of DD07V.


***----------------------------------------------------------------------------
*** Get Parameter Value
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SHTDI ).
    move LS_PARAM-ATWRT to RF_SHTDI.
    move LS_PARAM-SETBY to EF_SETBY.

***----------------------------------------------------------------------------
    if EF_DESCR is requested.
***   Identify Description
      call function 'DD_DOMVALUES_GET'
        exporting
          DOMNAME   = 'ZVCXI_P0SS_SHTDI'
          TEXT      = 'X'
          LANGU     = SY-LANGU
        tables
          DD07V_TAB = LT_D07V.
      EF_DESCR = value #( LT_D07V[ DOMVALUE_L = RF_SHTDI ]-DDTEXT optional ).
    endif.

  endmethod.
  method GET_STATUS.

*** Super Status
    RF_ELMST = SUPER->GET_STATUS( ).

***--------------------------------------------------------------------------------------
*** Check Machine Speed
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = ME->CHECK_MSRS( ) ).

*** Check Price Scales
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = ME->CHECK_PSCALE( ) ).

***--------------------------------------------------------------------------------------
*** Check Label
    RF_ELMST = /VCXI/CL_VKSR_SERVICE=>MERGE_ELMST( IF_ELMST1 = RF_ELMST
                                                   IF_ELMST2 = ME->CHECK_MS_LABEL( ) ).

  endmethod.
  method GET_STATUS_MSG.

    SUPER->GET_STATUS_MSG( IR_MESSAGE = IR_MESSAGE ).

***----------------------------------------------------------------------------
*** Check Machine Speed
    ME->CHECK_MSRS( IR_MESSAGE = IR_MESSAGE ).

*** Check Price Scales
    ME->CHECK_PSCALE( IR_MESSAGE = IR_MESSAGE ).

***--------------------------------------------------------------------------------------
*** Check Label
    ME->CHECK_MS_LABEL( IR_MESSAGE = IR_MESSAGE ).

  endmethod.
  method HANDLE_EVENT.

    SUPER->HANDLE_EVENT( IF_IEVNT         = IF_IEVNT
                         IT_GATE_PROVIDER = IT_GATE_PROVIDER ).

    case IF_IEVNT.
      when C_IEVNT_I_INIT_BY_MAPPL.
        ME->HANDLE_INIT_BY_MAPPL( ).

    endcase.

  endmethod.
  method HANDLE_INIT_BY_MAPPL.

    data: LR_GATE type ref to /VCXI/CL_VKSR_GATE.


    data(LF_MAPPL) = ME->GET_MAPPL( ).


    if LF_MAPPL ne C_MAPPL_MANUAL.
      ME->SET_MSRS( IF_SETBY = SPACE ).
    endif.

    if LF_MAPPL ne C_MAPPL_OUTSOURCE.
      ME->SET_LIFNR( IF_SETBY = SPACE ).
      loop at ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCS_PR_PSCALE=>C_IPRVT_XCS_PSCALE
                                     IF_INUSE = ABAP_TRUE ) into LR_GATE.
        LR_GATE->DO_FREE( ).
      endloop.
    endif.

    if LF_MAPPL ne C_MAPPL_MANUAL and
       LF_MAPPL ne C_MAPPL_OUTSOURCE.
      ME->SET_SCRAP( IF_SETBY = SPACE ).
    endif.

  endmethod.
  method SET_LIFNR.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_LIFNR ).

    if IF_LIFNR is supplied.
      LS_PARAM-ATWRT = IF_LIFNR.
    endif.

    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATWRT.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_LIFNR_CHANGED ).
    endif.

  endmethod.
  method SET_MAPPL.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_MAPPL ).

    move IF_MAPPL to LS_PARAM-ATWRT.
    move IF_SETBY to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
***   Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_MAPPL_CHANGED ).
    endif.

  endmethod.
  method SET_MSRS.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

*** Rate
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_MSRSR ).

    if IS_MSRS is supplied.
      LS_PARAM-ATFLV = IS_MSRS-MSRSR.
      LS_PARAM-UNIT  = IS_MSRS-MSRSR_UOM.
    endif.

    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_SPEED_CHANGED ).
    endif.

*** Time
    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_MSRST ).

    if IS_MSRS is supplied.
      LS_PARAM-ATFLV = IS_MSRS-MSRST.
      LS_PARAM-UNIT  = IS_MSRS-MSRST_UOM.
    endif.

    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_SPEED_CHANGED ).
    endif.

  endmethod.
  method SET_SCRAP.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SCRAP ).

    if IS_SCRAP is supplied.
      LS_PARAM-ATFLV = IS_SCRAP-SCRAP.
      LS_PARAM-UNIT  = IS_SCRAP-SCRAP_UOM.
    endif.

    LS_PARAM-SETBY = IF_SETBY.
    if LS_PARAM-SETBY is initial.
      clear LS_PARAM-ATFLV.
    endif.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_SCRAP_CHANGED ).
    endif.

  endmethod.
  method SET_SHTDI.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_SHTDI ).

    move IF_SHTDI  to LS_PARAM-ATWRT.
    move IF_SETBY  to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_SHTDI_CHANGED ).
    endif.

  endmethod.
  method ZIF_VCXI_P2SS_COMM_NEXT~GET_FCFPX.

*** Super
    RR_BL_FCFPROP = SUPER->ZIF_VCXI_P2SS_COMM_NEXT~GET_FCFPX( importing EF_SETBY = EF_SETBY ).
    check RR_BL_FCFPROP is bound.

***--------------------------------------------------------------------------------------
*** Add Additional Weight of all Label
    RR_BL_FCFPROP->MERGE_ADDWT( IS_ADDWT = ME->GET_MS_LABEL_ADDWT( ) ).

***--------------------------------------------------------------------------------------
*** Get new Folding Carton Forward Property XML
    if EF_FCFPX is requested.
      EF_FCFPX = RR_BL_FCFPROP->GET_FCFPX( ).
    endif.

  endmethod.
  method ZIF_VCXI_XCS_PSCALE_MGR~GET_PSCALE.

    data: LS_PSQTY       type ZVCXI_XCS_S_PSQTY,
          LS_PSQTY_MATCH type ZVCXI_XCS_S_PSQTY,
          LS_UOMCF       type ZVCXI_XCS_S_UOMCF.
    data: LT_GATE type /VCXI/VKSR_TT_GATE.
    data: LR_GATE            type ref to /VCXI/CL_VKSR_GATE,
          LR_PR_PSCALE       type ref to ZCL_VCXI_XCS_PR_PSCALE,
          LR_PR_PSCALE_MATCH type ref to ZCL_VCXI_XCS_PR_PSCALE.


*** Get Gates of Price Scales
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCS_PR_PSCALE=>C_IPRVT_XCS_PSCALE
                                     IF_INUSE = ABAP_TRUE ).

    loop at LT_GATE into LR_GATE.
      try.
          move LR_GATE->R_CLSPR ?to LR_PR_PSCALE.
          check LR_PR_PSCALE is bound.

***------------------------------------------------------------------------------------------------
***       Get Price Scale Quantity
          move LR_PR_PSCALE->GET_PSQTY( )  to LS_PSQTY.

***       Convert Price Scale Quantity if needed
          if LS_PSQTY-PSQTY_UOM ne IS_PSQTY-PSQTY_UOM.
            read table IT_UOMCF into LS_UOMCF with key UOM = LS_PSQTY-PSQTY_UOM.
            LS_PSQTY-PSQTY_UOM = IS_PSQTY-PSQTY_UOM.
            if LS_UOMCF-UOMCF is not initial.
              LS_PSQTY-PSQTY     = LS_PSQTY-PSQTY / LS_UOMCF-UOMCF.
            else.
              clear: LS_PSQTY-PSQTY.
            endif.
          endif.

***       Check conversion was successful
          check IS_PSQTY-PSQTY_UOM eq LS_PSQTY-PSQTY_UOM.

***       Compare
          if   IS_PSQTY-PSQTY       ge LS_PSQTY-PSQTY and
             ( LS_PSQTY_MATCH-PSQTY le LS_PSQTY-PSQTY or
               LS_PSQTY_MATCH-PSQTY is initial ).
            move LR_PR_PSCALE to LR_PR_PSCALE_MATCH.
            move LS_PSQTY     to LS_PSQTY_MATCH.
          endif.

        catch CX_SY_MOVE_CAST_ERROR.
          clear: LR_PR_PSCALE.
      endtry.
    endloop.

***------------------------------------------------------------------------------------------------
*** If Price Scale was found -> Return Price and Price Unit
    check LR_PR_PSCALE_MATCH is bound.
    ES_PRICE = LR_PR_PSCALE_MATCH->GET_PRICE( ).
    ES_PRICP = LR_PR_PSCALE_MATCH->GET_PRICP( ).

*** Check if Conversion is needed for Price Unit
    if ES_PRICP-PRICP_UOM ne IS_PSQTY-PSQTY_UOM.
      read table IT_UOMCF into LS_UOMCF with key UOM = LS_PSQTY-PSQTY_UOM.
      ES_PRICP-PRICP_UOM = IS_PSQTY-PSQTY_UOM.
      if LS_UOMCF-UOMCF is not initial.
        ES_PRICE-PRICE     = ES_PRICE-PRICE * LS_UOMCF-UOMCF.
      endif.
    endif.

  endmethod.
  method ZIF_VCXI_XCS_PSCALE_MGR~IS_CHANGEABLE.

*** Changeable depending on Application Mode
    RF_CHANGEABLE = switch #( ME->GET_MAPPL( ) when C_MAPPL_OUTSOURCE then ABAP_TRUE
                                                                      else ABAP_FALSE ).

  endmethod.
