
class ZCL_VCXI_FFSP_GRID_MC_CORE definition
  public
  inheriting from ZCL_VCXI_XCS_GRID_PRVDR
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_CONTAINER_NAME type SCRFNAME optional
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IF_APPL_EVENTS type ABAP_BOOL default ABAP_TRUE
      !IF_TABNAME type TABNAME default 'ZVCXI_FFSP_S_MC_CORE_GRID'
    raising
      /VCXI/CX_CKX .
protected section.

  data F_PURCHASED type ABAP_BOOL .

  methods CHANGE_STYLE_MC_CORE
    importing
      !IF_PURCH type ZVCXI_XCS_PURCH
    changing
      !CT_STYL type LVC_T_STYL
    raising
      /VCXI/CX_CKX .
  methods GET_MC_CORE_VIS
    importing
      !IR_PR_MC_CORE type ref to ZCL_VCXI_FFSP_PR_MC_CORE
    returning
      value(RS_MC_CORE_VIS) type ZVCXI_FFSP_S_MC_CORE_VIS
    raising
      /VCXI/CX_CKX .
  methods SET_MC_CORE_VIS
    importing
      !IR_PR_MC_CORE type ref to ZCL_VCXI_FFSP_PR_MC_CORE
      !IS_MC_CORE_VIS type ZVCXI_FFSP_S_MC_CORE_VIS
    raising
      /VCXI/CX_CKX .

  methods GET_FIELDCAT
    redefinition .
  methods GET_GRID_DATA
    redefinition .
  methods HANDLE_DATA_CHANGED_FINISHED
    redefinition .
private section.
endclass. "ZCL_VCXI_FFSP_GRID_MC_CORE definition
class ZCL_VCXI_FFSP_GRID_MC_CORE implementation.
method CHANGE_STYLE_MC_CORE.

  free CT_STYL.

***--------------------------------------------------------------------------------------
*** Set Ink Matnr = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'MATNR'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Text = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'TEXT'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Average Consumption = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'AVGCO'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Average Consumption per = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'AVGCP'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

*** Set Rolls per Core = Editable
  ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'ROPCO'
                              IF_STYLE     = ME->F_STYLE_ENABLED
                    changing  CT_STYL      = CT_STYL ).

***--------------------------------------------------------------------------------------
*** Set Vendor Price and Price Unit = Editable if PURCH = ABAP_TRUE
  if IF_PURCH eq ABAP_TRUE.
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR'
                                IF_STYLE     = ME->F_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).

    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                                IF_STYLE     = ME->F_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).

    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                                IF_STYLE     = ME->F_STYLE_ENABLED
                      changing  CT_STYL      = CT_STYL ).

  else.
    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'LIFNR'
                                IF_STYLE     = ME->F_STYLE_DISABLED
                      changing  CT_STYL      = CT_STYL ).

    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICE'
                                IF_STYLE     = ME->F_STYLE_DISABLED
                      changing  CT_STYL      = CT_STYL ).

    ME->CHANGE_STYLE( exporting IF_FIELDNAME = 'PRICP'
                                IF_STYLE     = ME->F_STYLE_DISABLED
                      changing  CT_STYL      = CT_STYL ).

  endif.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_GATE           = IR_GATE
                      IR_UIGATE         = IR_UIGATE
                      IF_TABNAME        = IF_TABNAME
                      IF_CONTAINER_NAME = IF_CONTAINER_NAME
                      IF_APPL_EVENTS    = IF_APPL_EVENTS ).

*** Define Provider Type for Button
  ME->F_IPRVT = ZCL_VCXI_FFSP_PR_MC_CORE=>C_IPRVT_MC_CORE.

endmethod.
method GET_FIELDCAT.

  data: LF_LOCKED type        /VCXI/CKX_LOCKED,
        LF_INUSE  type        /VCXI/VKSR_INUSE,
        LF_COLPOS type        I.
  field-symbols: <S_FCAT>  type        LVC_S_FCAT.

***----------------------------------------------------------------------------
*** Get Standard Field Catalog
  RT_FCAT = SUPER->GET_FIELDCAT( ).

*** Get Lock and In-Use Status
  LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
  LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***----------------------------------------------------------------------------
*** Adjust Field Catalog
  loop at RT_FCAT assigning <S_FCAT>.
    case <S_FCAT>-FIELDNAME.
      when 'POSID' or 'GATKY' or 'R_GATE' or 'T_STYL'.
        move ABAP_TRUE    to <S_FCAT>-TECH.
        move 0            to <S_FCAT>-COL_POS.

***   Set Edit for all fields which should Editable at all
      when 'MATNR' or 'AVGCO' or 'AVGCP' or 'TEXT' or 'ROPCO'.
        if LF_LOCKED eq ABAP_TRUE and
           LF_INUSE  eq ABAP_TRUE.
          move ABAP_TRUE    to <S_FCAT>-EDIT.
        endif.

***   Purchase Flag always editable
      when 'PURCH'.
        move ABAP_TRUE    to <S_FCAT>-CHECKBOX.
        if LF_LOCKED eq ABAP_TRUE and
           LF_INUSE  eq ABAP_TRUE.
          move ABAP_TRUE    to <S_FCAT>-EDIT.
        endif.

***   Set Edit for Price fields if it is purchased
      when 'LIFNR' or 'PRICE' or 'PRICP'.
        if ME->F_PURCHASED eq ABAP_TRUE.
          if LF_LOCKED eq ABAP_TRUE and
             LF_INUSE  eq ABAP_TRUE.
            move ABAP_TRUE    to <S_FCAT>-EDIT.
          endif.
        else.
          move ABAP_TRUE    to <S_FCAT>-NO_OUT.
        endif.

      when 'LIFNR_DESCR' or 'PRICE_CURR' or 'PRICP_UOM'.
        if ME->F_PURCHASED eq ABAP_FALSE.
          move ABAP_TRUE    to <S_FCAT>-NO_OUT.
        endif.

    endcase.

*** Set Position for each Field
    move 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'MATNR'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'MATNR_DESCR'. <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'CONSU'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'CONSU_UOM'.   <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'TEXT'.        <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'AVGCO'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'AVGCO_UOM'.   <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'AVGCP'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'AVGCP_UOM'.   <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'ROPCO'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'PURCH'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'LIFNR'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'LIFNR_DESCR'. <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'PRICE'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'PRICE_CURR'.  <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'PRICP'.       <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.
    if <S_FCAT>-FIELDNAME eq 'PRICP_UOM'.   <S_FCAT>-COL_POS = LF_COLPOS.  endif.
    add 1 to LF_COLPOS.

  endloop.

  sort RT_FCAT by COL_POS.

endmethod.
method GET_GRID_DATA.

  data: LS_MC_CORE_GRID type          ZVCXI_FFSP_S_MC_CORE_GRID.
  data: LT_GATE         type          /VCXI/VKSR_TT_GATE.
  data: LR_GATE         type ref to   /VCXI/CL_VKSR_GATE,
        LR_PR_MC_CORE   type ref to   ZCL_VCXI_FFSP_PR_MC_CORE.
  field-symbols: <T_DATA> type standard table,
                 <S_DATA> type          ANY.

  clear: ME->F_PURCHASED.

*** Dereferencing of Data Reference
  assign IR_DATA->* to <T_DATA>.

*** Get Gates of defined Provider
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT  = ME->F_IPRVT
                                   IF_INUSE  = ABAP_TRUE
                                   IF_SORTED = ABAP_TRUE ).

  loop at LT_GATE into LR_GATE.
    check LR_GATE->IS_INUSE( ) eq ABAP_TRUE and
          LR_GATE->R_CLSPR     is bound.

    clear: LS_MC_CORE_GRID.

    try.
        move LR_GATE->R_CLSPR ?to LR_PR_MC_CORE.
        check LR_PR_MC_CORE is bound.

***--------------------------------------------------------------------------------------
***     Get Data from PR Class
        move LR_GATE->GET_POSID( ) to LS_MC_CORE_GRID-POSID.
        move LR_GATE->F_GATKY      to LS_MC_CORE_GRID-GATKY.
        move LR_GATE               to LS_MC_CORE_GRID-R_GATE.

        LS_MC_CORE_GRID-S_MC_CORE_VIS = ME->GET_MC_CORE_VIS( IR_PR_MC_CORE = LR_PR_MC_CORE ).

        if LS_MC_CORE_GRID-PURCH eq ABAP_TRUE.
          move LS_MC_CORE_GRID-PURCH to ME->F_PURCHASED.
        endif.

***--------------------------------------------------------------------------------------
***     Set Style Details
        ME->CHANGE_STYLE_MC_CORE( exporting IF_PURCH = LS_MC_CORE_GRID-PURCH
                                  changing  CT_STYL  = LS_MC_CORE_GRID-T_STYL ).

***     Call DVC Service - Modify Style
        ZCL_VCXI_XCDR_SERVICE=>MODIFY_STYLE( exporting IR_GATE = LR_GATE
                                             changing  CT_STYL = LS_MC_CORE_GRID-T_STYL ).

***--------------------------------------------------------------------------------------
***     Add Entry
        append initial line to <T_DATA> assigning <S_DATA>.
        move-corresponding LS_MC_CORE_GRID to <S_DATA>.

      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.
  endloop.

endmethod.
method GET_MC_CORE_VIS.

  data: LS_CONSU type ZVCXI_XCSP_S_CONSU,
        LS_PRICE type ZVCXI_XCS_S_PRICE,
        LS_PRICP type ZVCXI_XCS_S_PRICP.

***----------------------------------------------------------------------------
*** Get Material Number and Description
  move IR_PR_MC_CORE->GET_MATNR( )       to RS_MC_CORE_VIS-MATNR.
  move IR_PR_MC_CORE->GET_MATNR_DESCR( ) to RS_MC_CORE_VIS-MATNR_DESCR.

*** Get Consumption
  move IR_PR_MC_CORE->GET_CONSU( )       to LS_CONSU.
  move-corresponding LS_CONSU            to RS_MC_CORE_VIS.

*** Get Average Consumption and Consumption per
  IR_PR_MC_CORE->GET_AVGCO( importing ES_AVGCO = RS_MC_CORE_VIS-S_AVGCO ).
  IR_PR_MC_CORE->GET_AVGCP( importing ES_AVGCP = RS_MC_CORE_VIS-S_AVGCP ).

*** Get Rolls/Core
  IR_PR_MC_CORE->GET_ROPCO( importing EF_ROPCO = RS_MC_CORE_VIS-ROPCO ).

***----------------------------------------------------------------------------
*** Get Vendor, Description and Purchase Flag and Text
  move IR_PR_MC_CORE->GET_LIFNR( )       to RS_MC_CORE_VIS-LIFNR.
  move IR_PR_MC_CORE->GET_LIFNR_DESCR( ) to RS_MC_CORE_VIS-LIFNR_DESCR.
  move IR_PR_MC_CORE->GET_PURCH( )       to RS_MC_CORE_VIS-PURCH.
  move IR_PR_MC_CORE->GET_TEXT( )        to RS_MC_CORE_VIS-TEXT.

*** Get Price
  move IR_PR_MC_CORE->GET_PRICE( )       to LS_PRICE.
  move-corresponding LS_PRICE            to RS_MC_CORE_VIS.

*** Get Price Unit
  move IR_PR_MC_CORE->GET_PRICP( )       to LS_PRICP.
  move-corresponding LS_PRICP            to RS_MC_CORE_VIS.

endmethod.
method HANDLE_DATA_CHANGED_FINISHED.

  data: LS_MC_CORE_GRID type          ZVCXI_FFSP_S_MC_CORE_GRID.
  data: LR_PR_MC_CORE   type ref to   ZCL_VCXI_FFSP_PR_MC_CORE,
        LR_CX_CKX       type ref to   /VCXI/CX_CKX.
  field-symbols: <T_DATA> type standard table,
                 <S_DATA> type          ANY.

  check E_MODIFIED eq ABAP_TRUE.

  try.

      SUPER->HANDLE_DATA_CHANGED_FINISHED( E_MODIFIED    = E_MODIFIED
                                           ET_GOOD_CELLS = ET_GOOD_CELLS ).

***   Dereferencing of Data Reference
      assign ME->R_DATA->* to <T_DATA>.

      loop at <T_DATA> assigning <S_DATA>.
        move-corresponding <S_DATA> to LS_MC_CORE_GRID.

        move LS_MC_CORE_GRID-R_GATE->R_CLSPR ?to LR_PR_MC_CORE.

***     Set Values to PR Class
        ME->SET_MC_CORE_VIS( IR_PR_MC_CORE  = LR_PR_MC_CORE
                             IS_MC_CORE_VIS = LS_MC_CORE_GRID-S_MC_CORE_VIS ).
      endloop.


      ME->R_GATE->RAISE_EVENTS( ).

    catch /VCXI/CX_CKX into LR_CX_CKX.
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

*** Request Refresh for ALV
  move ABAP_TRUE to ME->F_REFRESH_REQ.

endmethod.
method SET_MC_CORE_VIS.

  data: LS_PRICE       type ZVCXI_XCS_S_PRICE,
        LS_PRICP       type ZVCXI_XCS_S_PRICP,
        LS_MC_CORE_VIS type ZVCXI_FFSP_S_MC_CORE_VIS.

***----------------------------------------------------------------------------
*** Get Current data to do Update only if Value changed.
  LS_MC_CORE_VIS = ME->GET_MC_CORE_VIS( IR_PR_MC_CORE = IR_PR_MC_CORE ).

***----------------------------------------------------------------------------
*** Material
  if LS_MC_CORE_VIS-MATNR ne IS_MC_CORE_VIS-MATNR.
    IR_PR_MC_CORE->SET_MATNR( IF_MATNR = IS_MC_CORE_VIS-MATNR
                              IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Text
  if LS_MC_CORE_VIS-TEXT ne IS_MC_CORE_VIS-TEXT.
    IR_PR_MC_CORE->SET_TEXT( IF_TEXT = IS_MC_CORE_VIS-TEXT
                            IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Average Consumption  & Consumption per
  if LS_MC_CORE_VIS-AVGCO ne IS_MC_CORE_VIS-AVGCO.
    IR_PR_MC_CORE->SET_AVGCO( IS_AVGCO = IS_MC_CORE_VIS-S_AVGCO
                              IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

  if LS_MC_CORE_VIS-AVGCP ne IS_MC_CORE_VIS-AVGCP.
    IR_PR_MC_CORE->SET_AVGCP( IS_AVGCP = IS_MC_CORE_VIS-S_AVGCP
                              IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Rolls/Core
  if LS_MC_CORE_VIS-ROPCO ne IS_MC_CORE_VIS-ROPCO.
    IR_PR_MC_CORE->SET_ROPCO( IF_ROPCO = IS_MC_CORE_VIS-ROPCO
                              IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

***----------------------------------------------------------------------------
*** Purchase Flag
  if LS_MC_CORE_VIS-PURCH ne IS_MC_CORE_VIS-PURCH.
    IR_PR_MC_CORE->SET_PURCH( IF_PURCH = IS_MC_CORE_VIS-PURCH
                             IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Vendor
  if LS_MC_CORE_VIS-LIFNR ne IS_MC_CORE_VIS-LIFNR.
    IR_PR_MC_CORE->SET_LIFNR( IF_LIFNR = IS_MC_CORE_VIS-LIFNR
                             IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Price
  if LS_MC_CORE_VIS-PRICE ne IS_MC_CORE_VIS-PRICE.
    move-corresponding IS_MC_CORE_VIS to LS_PRICE.
    IR_PR_MC_CORE->SET_PRICE( IS_PRICE = LS_PRICE
                             IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Price per
  if LS_MC_CORE_VIS-PRICP ne IS_MC_CORE_VIS-PRICP.
    move-corresponding IS_MC_CORE_VIS to LS_PRICP.
    IR_PR_MC_CORE->SET_PRICP( IS_PRICP = LS_PRICP
                             IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

endmethod.
