
class ZCL_VCXI_PCSS_PR_COMP_FG definition
  public
  inheriting from ZCL_VCXI_P0SS_PR_COMP_FG
  create public .

public section.
protected section.

  methods HANDLE_INIT_CALIP
    redefinition .
private section.
endclass. "ZCL_VCXI_PCSS_PR_COMP_FG definition
class ZCL_VCXI_PCSS_PR_COMP_FG implementation.
method HANDLE_INIT_CALIP.

  data:  LS_CALIP           type        ZVCXI_P0SS_S_CALIP.
  data:  LT_GATE            type        /VCXI/VKSR_TT_GATE.
  data:  LR_GATE            type ref to /VCXI/CL_VKSR_GATE,
         LR_GATE_SPEC       type ref to /VCXI/CL_VKSR_GATE,
         LR_ICONT           type ref to ZIF_VCXI_P0SS_ICONT,
         LR_PR_BOARD        type ref to ZCL_VCXI_PCSS_PR_BOARD,
         LR_PR_MS_SHTPR     type ref to ZCL_VCXI_PCSS_PR_MS_SHTPR.

***------------------------------------------------------------------------------------------------
*** Get Context
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_P0SS_PR_SPEC_SE=>C_IPRVT_P0_SPEC_SE
                                   IF_INUSE = ABAP_TRUE ).
  read table LT_GATE into LR_GATE_SPEC index 1.
  try.
      move LR_GATE_SPEC->R_CLSPR ?to LR_ICONT.
    catch CX_SY_MOVE_CAST_ERROR.
      clear: LR_ICONT.
  endtry.
  check LR_ICONT is bound.

***------------------------------------------------------------------------------------------------
*** Get Board Reference
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_PCSS_PR_BOARD=>C_IPRVT_PC_BOARD
                                   IF_INUSE = ABAP_TRUE ).


  loop at LT_GATE into LR_GATE.
*** Check is child of Specification
    if LR_GATE->GET_PARENT( ) eq LR_GATE_SPEC.

***   Check Board Mode against Context
      try.
          move LR_GATE->R_CLSPR ?to LR_PR_BOARD.
        catch CX_SY_MOVE_CAST_ERROR.
          clear: LR_PR_BOARD, LR_GATE.
      endtry.
      check LR_PR_BOARD is bound.

      if ( LR_PR_BOARD->GET_MBOAR( ) eq ZCL_VCXI_PCSS_PR_BOARD=>C_MBOAR_S   and
           LR_ICONT->GET_ICONT( )    eq ZCL_VCXI_PCSS_PR_SPEC=>C_ICONT_SALES    ) or
         ( LR_PR_BOARD->GET_MBOAR( ) eq ZCL_VCXI_PCSS_PR_BOARD=>C_MBOAR_P   and
           LR_ICONT->GET_ICONT( )    eq ZCL_VCXI_PCSS_PR_SPEC=>C_ICONT_PROD     ) .
        exit.
      else.
        clear LR_PR_BOARD.
      endif.
    endif.
  endloop.

  if LR_PR_BOARD is not bound.
***------------------------------------------------------------------------------------------------
*** Get Sheet Procurement Reference
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_P0SS_PR_MS_SHTPR=>C_IPRVT_P0_MS_SHTPR
                                     IF_INUSE = ABAP_TRUE ).

    read table LT_GATE into LR_GATE index 1.
    try.
        if LR_GATE          is bound and
           LR_GATE->R_CLSPR is bound.
          move LR_GATE->R_CLSPR ?to LR_PR_MS_SHTPR.
        endif.
      catch CX_SY_MOVE_CAST_ERROR.
        clear: LR_PR_MS_SHTPR.
    endtry.

    check LR_PR_MS_SHTPR is bound.

*** Get Caliper by Sheet Procurement
    LS_CALIP = LR_PR_MS_SHTPR->GET_CALIP( ).

  else.

*** Get Caliper by Board ID
    LS_CALIP = LR_PR_BOARD->GET_CALIP( ).

  endif.

*** Set Caliper
  check LS_CALIP is not initial.
  ME->SET_CALIP( IS_CALIP = LS_CALIP ).

endmethod.
endclass. "ZCL_VCXI_PCSS_PR_COMP_FG definition
endclass. "ZCL_VCXI_PCSS_PR_COMP_FG implementation
