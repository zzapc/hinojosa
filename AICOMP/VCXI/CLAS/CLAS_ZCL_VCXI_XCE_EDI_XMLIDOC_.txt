
class ZCL_VCXI_XCE_EDI_XMLIDOC definition
  public
  create public .

public section.

  constants C_SEGNAM_BODY type EDI_SEGNAM value 'ZVCXI_XCE_EDI_BODY' ##NO_TEXT.
  constants C_SEGNAM_HEADER type EDI_SEGNAM value 'ZVCXI_XCE_EDI_HEADER' ##NO_TEXT.

  class-methods SEND
    importing
      !IF_RCVPRT type EDI_RCVPRT
      !IF_RCVPRN type EDI_RCVPRN
      !IF_MESTYP type EDI_MESTYP
      !IS_IDOC_HEADER type ZVCXI_XCE_EDI_S_IDOC_HEADER
      !IF_XML type STRING
    raising
      /VCXI/CX_CKX .
  class-methods GET_XML
    importing
      !IF_DOCNUM type EDI_DOCNUM
    returning
      value(RF_XML) type STRING
    raising
      /VCXI/CX_CKX .
protected section.
private section.
endclass. "ZCL_VCXI_XCE_EDI_XMLIDOC definition
class ZCL_VCXI_XCE_EDI_XMLIDOC implementation.
  method GET_XML.

*** Load Instance of Business Object
    data(LR_BOB_IDOC) = new ZCL_VCXI_XCI_BOB_IDOC( )->INIT_FROM_DB( IF_DOCNUM = IF_DOCNUM ).

*** Get XML from Segment
    RF_XML = LR_BOB_IDOC->GET_SEGMENT_AS_STRING( IF_SEGNAM = C_SEGNAM_BODY ).

  endmethod.
  method SEND.

    data: LS_EDK13 type EDK13,
          LS_EDP13 type EDP13.


***------------------------------------------------------------------------------------------------
*** Read Partner Profile for Outb. Processing
    LS_EDK13 = value #( RCVPRN = IF_RCVPRN
                        RCVPRT = IF_RCVPRT
                        MESTYP = IF_MESTYP ).

    call function 'EDI_PARTNER_APPL_READ_OUT'
      exporting
        REC_EDK13           = LS_EDK13
      importing
        REC_EDP13           = LS_EDP13
      exceptions
        PARTNER_IS_INACTIVE = 1
        PARTNER_IS_TEMPLATE = 2
        PARTNER_NOT_FOUND   = 3.

***------------------------------------------------------------------------------------------------
*** Load new Instance of Business Object
    data(LR_BOB_IDOC) = new ZCL_VCXI_XCI_BOB_IDOC( )->INIT_NEW( IF_IDOCTP = LS_EDP13-IDOCTYP
                                                                IF_MESTYP = LS_EDP13-MESTYP
                                                                IF_RCVPRT = LS_EDP13-RCVPRT
                                                                IF_RCVPRN = LS_EDP13-RCVPRN
                                                                IF_RCVPOR = LS_EDP13-RCVPOR ).
*** Set Header Segment
    LR_BOB_IDOC->ADD_DATA( IF_SEGNAM = C_SEGNAM_HEADER
                           IF_SDATA  = conv #( IS_IDOC_HEADER ) ).
*** Set XML to Body Segment
    LR_BOB_IDOC->SET_STRING_TO_SEGMENT( IF_SEGNAM = C_SEGNAM_BODY
                                        IF_STRING = IF_XML ).

*** Send the IDoc
    LR_BOB_IDOC->SEND_IDOC( IF_COMMIT = ABAP_TRUE ).

  endmethod.
