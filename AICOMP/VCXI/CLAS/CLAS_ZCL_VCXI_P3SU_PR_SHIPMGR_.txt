
class ZCL_VCXI_P3SU_PR_SHIPMGR definition
  public
  inheriting from ZCL_VCXI_PXSU_PR_SHIPMGR
  create public .

public section.

  methods GET_BASE_DIMEN
    redefinition .
  methods GET_BASE_PRQTY
    redefinition .
  methods GET_NOUPS
    redefinition .
protected section.

  methods GET_PRVDR_COMM_PREV
    exporting
      !ER_GATE type ref to /VCXI/CL_VKSR_GATE
    returning
      value(RR_COMM_PREV) type ref to ZIF_VCXI_P3SS_COMM_PREV
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_P3SU_PR_SHIPMGR definition
class ZCL_VCXI_P3SU_PR_SHIPMGR implementation.
  method GET_BASE_DIMEN.

    data: LS_PROWT type ZVCXI_PXS_S_PROWT.
    data: LR_COMM_PREV   type ref to ZIF_VCXI_P3SS_COMM_PREV,
          LR_BL_ASFPROP  type ref to ZCL_VCXI_P3SS_BL_ASFPROP,
          LR_PR_AREA_WGT type ref to ZCL_VCXI_PXSS_PR_AREA_WGT.


***--------------------------------------------------------------------------------------
    LR_COMM_PREV = ME->GET_PRVDR_COMM_PREV( ).
    if LR_COMM_PREV is bound.
***   Get Assembly Forward Property XML (Outbound)
      LR_BL_ASFPROP ?= LR_COMM_PREV->GET_ASFPX( ).

***   Set Dimensions
      if LR_BL_ASFPROP is bound.
        RT_DIMEN = value #( ( DIMEN     = ZCL_VCXI_XCSU_CUST_SHIPU=>C_DIMEN_LENGTH
                              DIMVA     = LR_BL_ASFPROP->S_HNDDIM-HNDLE
                              DIMVA_UOM = LR_BL_ASFPROP->S_HNDDIM-HNDLE_UOM )
                            ( DIMEN     = ZCL_VCXI_XCSU_CUST_SHIPU=>C_DIMEN_WIDTH
                              DIMVA     = LR_BL_ASFPROP->S_HNDDIM-HNDWI
                              DIMVA_UOM = LR_BL_ASFPROP->S_HNDDIM-HNDWI_UOM )
                            ( DIMEN     = ZCL_VCXI_XCSU_CUST_SHIPU=>C_DIMEN_HEIGHT
                              DIMVA     = LR_BL_ASFPROP->S_HNDDIM-HNDHE
                              DIMVA_UOM = LR_BL_ASFPROP->S_HNDDIM-HNDHE_UOM )
                           ).
      endif.
    endif.

***--------------------------------------------------------------------------------------
*** Add Product Weight
    LR_PR_AREA_WGT = ME->GET_PRVDR_AREA_WGT( ).
    if LR_PR_AREA_WGT is bound.
      LS_PROWT = LR_PR_AREA_WGT->GET_PROWT( ).

      append value #( DIMEN     = ZCL_VCXI_XCSU_CUST_SHIPU=>C_DIMEN_WEIGHT
                      DIMVA     = LS_PROWT-PROWT
                      DIMVA_UOM = LS_PROWT-PROWT_UOM
                    ) to RT_DIMEN.
    endif.

  endmethod.
  method GET_BASE_PRQTY.

    RS_PRQTY-PRQTY     = 1.
    RS_PRQTY-PRQTY_UOM = 'ST'.

  endmethod.
  method GET_NOUPS.

    RF_NOUPS = 1.

  endmethod.
  method GET_PRVDR_COMM_PREV.

    data: LT_GATE         type        /VCXI/VKSR_TT_GATE.
    data: LR_GATE         type ref to /VCXI/CL_VKSR_GATE.

***--------------------------------------------------------------------------------------
*** Get Communication with Previous provider
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZIF_VCXI_P3SS_COMM_PREV=>C_IPRVT_COMM_PREV
                                     IF_INUSE = ABAP_TRUE ).

***--------------------------------------------------------------------------------------
*** Get Communication with Previous Reference
    loop at LT_GATE into LR_GATE.
      try.
          move LR_GATE->R_CLSPR ?to RR_COMM_PREV.
          check RR_COMM_PREV is bound.
          move LR_GATE to ER_GATE.
          exit.

        catch CX_SY_MOVE_CAST_ERROR.
          clear: RR_COMM_PREV, ER_GATE.
      endtry.
    endloop.

  endmethod.
