
class ZCL_VCXI_PXM_CAD_WUI_INB definition
  public
  inheriting from /VCXI/CL_CKMRU_CLWUI
  create public .

public section.

  data S_WUI_INB_VIS type ZVCXI_PXM_CAD_S_WUI_INB_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_RECORD type ref to /VCXI/CL_CKMR_RECORD
    raising
      /VCXI/CX_CKX .
  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_VIS
    importing
      !IS_VIS type ANY
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
protected section.

  data R_DCO_INB type ref to ZCL_VCXI_PXM_CAD_DCO_INB .
  constants C_UC_SHOW_XML type SYUCOMM value 'WUI_SHOW_XML' ##NO_TEXT.

  methods HANDLE_UC_SHOW_XML
    raising
      /VCXI/CX_CKX .
  methods INIT_VIS
    raising
      /VCXI/CX_CKX .
  methods MODIFY_SCREEN
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_PXM_CAD_WUI_INB definition
class ZCL_VCXI_PXM_CAD_WUI_INB implementation.
  method ACTIVATE.

*** Bind UI
    call function 'ZVCXI_PXM_CAD_WUI_INB_BIND'
      exporting
        IR_WUI_INB = ME
      importing
        ES_DYNP    = ME->S_DYNP.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_RECORD = IR_RECORD ).

*** Map DCO
    ME->R_RECORD->MAP_DCO( changing CR_DCO = ME->R_DCO_INB ).

  endmethod.
  method DO_PBO.

*** Initialize VIS Structure
    ME->INIT_VIS( ).

*** Modify Screen
    ME->MODIFY_SCREEN( ).

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_WUI_INB_VIS type ZVCXI_PXM_CAD_S_WUI_INB_VIS.


*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_WUI_INB_VIS.

***--------------------------------------------------------------------------------------
*** Set Start Conditions
    ME->R_DCO_INB->SET_FSIDA( IF_FSIDA = LS_WUI_INB_VIS-FSIDA ).

  endmethod.
  method DO_USER_COMMAND.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    case IF_UCOMM.
      when C_UC_SHOW_XML.
        ME->HANDLE_UC_SHOW_XML( ).
    endcase.

  endmethod.
  method HANDLE_UC_SHOW_XML.

    call function '/VCXI/CKX_XML_POPUP_START'
      exporting
        IF_XSTRING = ME->R_DCO_INB->F_XXML.

  endmethod.
  method INIT_VIS.

    clear: ME->S_WUI_INB_VIS.

*** CAD Project Owner
    ME->S_WUI_INB_VIS-CADPO = ME->R_DCO_INB->F_CADPO.

*** CAD Project ID
    ME->S_WUI_INB_VIS-CADPI = ME->R_DCO_INB->F_CADPI.

*** Save Integration Data (Flag)
    ME->S_WUI_INB_VIS-FSIDA = ME->R_DCO_INB->F_FSIDA .

  endmethod.
  method MODIFY_SCREEN.

*** Is Record Locked?
    data(LF_LOCKED) = ME->R_RECORD->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ).

*** Adjust Screen
    loop at screen.
      if LF_LOCKED ne ABAP_TRUE.
        move 0 to SCREEN-INPUT.
        move 0 to SCREEN-REQUIRED.
      endif.

***   Reopen Info Buttons
      if SCREEN-GROUP1 eq 'INF'.
        move 1 to SCREEN-INPUT.
      endif.

      modify screen.
    endloop.

  endmethod.
