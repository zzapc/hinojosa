
class ZCL_VCXI_P0SP_UI_ADDON_CHOP definition
  public
  inheriting from ZCL_VCXI_XCSP_UI_ADDON
  create public .

public section.

  data S_ADDON_CHOP_VIS type ZVCXI_P0SP_S_ADDON_CHOP_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_ADDON_CHOP_VIS
    importing
      !IS_ADDON_CHOP_VIS type ZVCXI_P0SP_S_ADDON_CHOP_VIS
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
protected section.

  data R_PR_ADDON_CHOP type ref to ZCL_VCXI_P0SP_PR_ADDON_CHOP .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_P0SP_UI_ADDON_CHOP definition
class ZCL_VCXI_P0SP_UI_ADDON_CHOP implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_P0SP_UI_ADDON_CHOP_BIND'
      exporting
        IR_UI_ADDON_CHOP = ME
      importing
        ES_DYNP          = ME->S_DYNP.

  endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                      IR_UIGATE = IR_UIGATE
                      IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
  try.
      move IR_GATE->R_CLSPR ?to ME->R_PR_ADDON_CHOP.
    catch CX_SY_MOVE_CAST_ERROR.
***   UI ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '801'.
  endtry.

endmethod.
method DO_UPDATE_ADDON_CHOP_VIS.

  data:  LS_PCLEN   type          ZVCXI_P0SP_S_PCLEN,
         LS_PCWID   type          ZVCXI_P0SP_S_PCWID.

*** Set PC Length and Width
  if ME->S_ADDON_CHOP_VIS-PCLEN ne IS_ADDON_CHOP_VIS-PCLEN.
    move-corresponding IS_ADDON_CHOP_VIS to LS_PCLEN.
    ME->R_PR_ADDON_CHOP->SET_PCLEN( IS_PCLEN = LS_PCLEN
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

  if ME->S_ADDON_CHOP_VIS-PCWID ne IS_ADDON_CHOP_VIS-PCWID.
    move-corresponding IS_ADDON_CHOP_VIS to LS_PCWID.
    ME->R_PR_ADDON_CHOP->SET_PCWID( IS_PCWID = LS_PCWID
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
  endif.

*** Set Number Ups
  if ME->S_ADDON_CHOP_VIS-PCUPS ne IS_ADDON_CHOP_VIS-PCUPS.
    ME->R_PR_ADDON_CHOP->SET_PCUPS( IF_PCUPS = IS_ADDON_CHOP_VIS-PCUPS
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER  ).
  endif.

*** Set PC Type
  if ME->S_ADDON_CHOP_VIS-PCTYP ne IS_ADDON_CHOP_VIS-PCTYP.
    ME->R_PR_ADDON_CHOP->SET_PCTYP( IF_PCTYP = IS_ADDON_CHOP_VIS-PCTYP
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER  ).
  endif.

endmethod.
  method DO_UPDATE_VIS.

    data: LS_ADDON_CHOP_VIS type ZVCXI_P0SP_S_ADDON_CHOP_VIS.


*** Super
    SUPER->DO_UPDATE_VIS( IS_VIS = IS_VIS ).
    check ME->R_PR_ADDON_CHOP is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_ADDON_CHOP_VIS.

***-------------------------------------------------------------------------------------
*** Set PC Length
    if ME->S_ADDON_CHOP_VIS-S_PCLEN ne LS_ADDON_CHOP_VIS-S_PCLEN.
      ME->R_PR_ADDON_CHOP->SET_PCLEN( IS_PCLEN = LS_ADDON_CHOP_VIS-S_PCLEN
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set PC Width
    if ME->S_ADDON_CHOP_VIS-S_PCWID ne LS_ADDON_CHOP_VIS-S_PCWID.
      ME->R_PR_ADDON_CHOP->SET_PCWID( IS_PCWID = LS_ADDON_CHOP_VIS-S_PCWID
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Number Ups
    if ME->S_ADDON_CHOP_VIS-PCUPS ne LS_ADDON_CHOP_VIS-PCUPS.
      ME->R_PR_ADDON_CHOP->SET_PCUPS( IF_PCUPS = LS_ADDON_CHOP_VIS-PCUPS
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER  ).
    endif.

*** Set PC Type
    if ME->S_ADDON_CHOP_VIS-PCTYP ne LS_ADDON_CHOP_VIS-PCTYP.
      ME->R_PR_ADDON_CHOP->SET_PCTYP( IF_PCTYP = LS_ADDON_CHOP_VIS-PCTYP
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER  ).
    endif.

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_ADDON_CHOP.

  endmethod.
  method INIT_VIS.

*** Super
    SUPER->INIT_VIS( ).

    free: ME->S_ADDON_CHOP_VIS.
    ME->S_ADDON_CHOP_VIS-S_ADDON_VIS = ME->S_ADDON_VIS.

***--------------------------------------------------------------------------------------
*** PC Dimension
    ME->S_ADDON_CHOP_VIS-S_PCLEN = ME->R_PR_ADDON_CHOP->GET_PCLEN( ).
    ME->S_ADDON_CHOP_VIS-S_PCWID = ME->R_PR_ADDON_CHOP->GET_PCWID( ).

*** Get Number Ups
    ME->S_ADDON_CHOP_VIS-PCUPS = ME->R_PR_ADDON_CHOP->GET_PCUPS( ).

*** Get PC Type and Description
    ME->S_ADDON_CHOP_VIS-PCTYP       = ME->R_PR_ADDON_CHOP->GET_PCTYP( ).
    ME->S_ADDON_CHOP_VIS-PCTYP_DESCR = ME->R_PR_ADDON_CHOP->GET_PCTYP_DESCR( ).

  endmethod.
