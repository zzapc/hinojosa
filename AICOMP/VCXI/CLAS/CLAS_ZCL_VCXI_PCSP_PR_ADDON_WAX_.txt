
class ZCL_VCXI_PCSP_PR_ADDON_WAX definition
  public
  inheriting from ZCL_VCXI_XCSP_PR_ADDON
  create public .

public section.

  methods HANDLE_EVENT
    redefinition .
protected section.

  constants C_IEVNT_I_RECALC_MC_CONSU type /VCXI/VKSC_IEVNT value 'RECALC_MC_CONSU'. "#EC NOTEXT

  methods HANDLE_RECALC_MC_CONSU
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_PCSP_PR_ADDON_WAX definition
class ZCL_VCXI_PCSP_PR_ADDON_WAX implementation.
method HANDLE_EVENT.

  SUPER->HANDLE_EVENT( IF_IEVNT         = IF_IEVNT
                       IT_GATE_PROVIDER = IT_GATE_PROVIDER ).

  case IF_IEVNT.
    when C_IEVNT_I_RECALC_MC_CONSU.
      ME->HANDLE_RECALC_MC_CONSU( ).
  endcase.

endmethod.
method HANDLE_RECALC_MC_CONSU.

  data:  LT_GATE            type        /VCXI/VKSR_TT_GATE.
  data:  LS_MQTYI           type        ZVCXI_XCSP_S_MQTYI.
  data:  LR_GATE            type ref to /VCXI/CL_VKSR_GATE,
         LR_PR_MACHINE      type ref to ZCL_VCXI_XCSP_PR_MACHINE,
         LR_PR_MC           type ref to ZCL_VCXI_XCSP_PR_MC.

***------------------------------------------------------------------
*** Get all Machine Quantity and Setup Waste
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCSP_PR_MACHINE=>C_IPRVT_XCS_MACHINE
                                   IF_INUSE = ABAP_TRUE ).

  read table LT_GATE into LR_GATE index 1.
  if SY-SUBRC = 0.

    clear: LR_PR_MACHINE.
    try.
        move LR_GATE->R_CLSPR ?to LR_PR_MACHINE.
      catch CX_SY_MOVE_CAST_ERROR.
        clear: LR_PR_MACHINE.
    endtry.
    if LR_PR_MACHINE is bound.
      move LR_PR_MACHINE->GET_MQTYI( ) to LS_MQTYI.
    endif.

  endif.

***------------------------------------------------------------------
*** Get all Material Consumptions
  LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCSP_PR_MC=>C_IPRVT_XCS_MC
                                   IF_INUSE = ABAP_TRUE ).

***----------------------------------------------------------------------------
*** Start Recalc Consumption of all Material Consumptions
  loop at LT_GATE into LR_GATE.
    try.

        clear: LR_PR_MC.
        move LR_GATE->R_CLSPR ?to LR_PR_MC.
        check LR_PR_MC is bound.
        LR_PR_MC->CALC_CONSU( IS_MQTYI = LS_MQTYI ).

      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.

  endloop.

endmethod.
endclass. "ZCL_VCXI_PCSP_PR_ADDON_WAX implementation
