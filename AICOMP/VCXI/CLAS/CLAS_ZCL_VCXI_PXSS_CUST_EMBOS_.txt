
class ZCL_VCXI_PXSS_CUST_EMBOS definition
  public
  create protected .

public section.

  class-methods GET_TEMBO_DESCR
    importing
      !IF_TEMBO type ZVCXI_PXS_TEMBO
    returning
      value(RF_DESCR) type /VCXI/CKX_DESCR
    raising
      /VCXI/CX_CKX .
  class-methods GET_FEMBM
    importing
      !IF_TEMBO type ZVCXI_PXS_TEMBO
    returning
      value(RF_FEMBM) type ZVCXI_PXS_FEMBM
    raising
      /VCXI/CX_CKX .
protected section.
private section.
endclass. "ZCL_VCXI_PXSS_CUST_EMBOS definition
class ZCL_VCXI_PXSS_CUST_EMBOS implementation.
  method GET_FEMBM.

    statics: LT_TC150 type sorted table of ZVCXI_PXS_TC150 with unique key TEMBO.
    data: LS_TC150 type ZVCXI_PXS_TC150.


    if not LINE_EXISTS( LT_TC150[ TEMBO = IF_TEMBO ] ).
      select single TEMBO FEMBM
         into corresponding fields of LS_TC150
         from ZVCXI_PXS_TC150
        where TEMBO eq IF_TEMBO.
      if SY-SUBRC ne 0.
***     Keep initial value in Buffer
        LS_TC150 = value #( TEMBO = IF_TEMBO ).
      endif.
      insert LS_TC150 into table LT_TC150.
    endif.

    RF_FEMBM = LT_TC150[ TEMBO = IF_TEMBO ]-FEMBM.

  endmethod.
  method GET_TEMBO_DESCR.

    statics: LT_TC150T type sorted table of ZVCXI_PXS_TC150T with unique key TEMBO.
    data: LS_TC150T type ZVCXI_PXS_TC150T.


    if not LINE_EXISTS( LT_TC150T[ TEMBO = IF_TEMBO ] ).
      select single *
         into LS_TC150T
         from ZVCXI_PXS_TC150T
        where TEMBO eq IF_TEMBO
         and  LANGU eq SY-LANGU.
      if SY-SUBRC ne 0.
***     Keep initial value in Buffer
        LS_TC150T = value #( TEMBO = IF_TEMBO ).
      endif.
      insert LS_TC150T into table LT_TC150T.
    endif.

    RF_DESCR = LT_TC150T[ TEMBO = IF_TEMBO ]-DESCR.

  endmethod.
