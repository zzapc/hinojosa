
class ZCL_VCXI_P2SS_UI_FCFG definition
  public
  inheriting from ZCL_VCXI_PXSS_UI_XFG
  create public .

public section.

  data S_FCFG_VIS type ZVCXI_P2SS_S_FCFG_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABPT type ZVCXI_XCS_TABPT default '0100S_FG_TAB-TAB'
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DEACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
  methods DO_PBO
    redefinition .
protected section.

  constants C_UC_FCPROP type SYUCOMM value 'FCFG_FCPROP' ##NO_TEXT.
  data R_PR_FCFG type ref to ZCL_VCXI_P2SS_PR_FCFG .
  data R_GRID_PRINT_COM type ref to ZCL_VCXI_PXSS_GRID_PRINT_COM .

  methods GET_IELEM_BY_SCREEN
    importing
      !IF_GROUP2 type SCREEN-GROUP2
    returning
      value(RF_IELEM) type ZVCXI_P0SS_IELEM
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_FCPROP
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_P2SS_UI_FCFG definition
class ZCL_VCXI_P2SS_UI_FCFG implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_P2SS_UI_FCFG_BIND'
      exporting
        IR_UI_FCFG    = ME
      importing
        ES_DYNP       = ME->S_DYNP
        EF_ACTIVE_TAB = ME->F_ACTIVE_TAB.

***--------------------------------------------------------------------------------------
*** Create Grid for Print
    create object ME->R_GRID_PRINT_COM
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_PRINT_COM'
        IR_PRINT_COM      = ME->R_PR_FCFG.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE
                        IF_TABPT  = IF_TABPT ).

*** Assign th RP Class to the UI Class
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_FCFG.
      catch CX_SY_MOVE_CAST_ERROR.
***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

***--------------------------------------------------------------------------------------
*** Free Printing Grid
    if ME->R_GRID_PRINT_COM is bound.
      ME->R_GRID_PRINT_COM->DO_FREE( ).
      clear ME->R_GRID_PRINT_COM.
    endif.

  endmethod.
  method DO_MODIFY_SCREEN.

    data: LF_BOXPA  type ZVCXI_P0SS_BOXPA.
    data: LR_CUST_PRSTY  type ref to ZCL_VCXI_PXSS_CUST_PRSTY.

    try.
        SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                                 IF_DYNNR = IF_DYNNR ).

***     Get ECMA Customizing and Part of Box
        ME->R_PR_FCFG->GET_ECMA( importing ER_CUST_PRSTY = LR_CUST_PRSTY ).
        LF_BOXPA = ME->R_PR_FCFG->GET_BOXPA( ).

***----------------------------------------------------------------------------
***     Modify Screen
        loop at screen.
***       Hide Custom Product Sheet W/L
          if SCREEN-GROUP1 eq 'CST'.
            if LR_CUST_PRSTY is not bound or
               LR_CUST_PRSTY->GET_FCUSD( ) ne ABAP_TRUE.
              move 0 to SCREEN-ACTIVE.
            endif.
          endif.

***       Hide Joint Dimension if no Joint Flap is defined
          if SCREEN-GROUP1 eq 'NJO' and
             ME->S_FCFG_VIS-JOIPO eq ZCL_VCXI_PXSS_PR_SCORE_MGR=>C_JOIPO_NONE.
            move 0 to SCREEN-ACTIVE.
          endif.

***       Hide Part of Box if not defined by Product Style
          if SCREEN-GROUP1 eq 'BXP'.
            if LR_CUST_PRSTY is not bound or
               LR_CUST_PRSTY->GET_BOXPA( ) is initial.
              move 0 to SCREEN-ACTIVE.
            endif.
          endif.

***       Adjust based on Product Style Element
          if SCREEN-GROUP1 eq 'PSE'.
            if LR_CUST_PRSTY is bound.
              if LR_CUST_PRSTY->IS_IELEM_USED( IF_IELEM = ME->GET_IELEM_BY_SCREEN( IF_GROUP2 = SCREEN-GROUP2 )
                                               IF_BOXPA = LF_BOXPA ) eq ABAP_FALSE.
                move 0 to SCREEN-ACTIVE.
              endif.
            else.
              SCREEN-ACTIVE = switch #( ME->GET_IELEM_BY_SCREEN( IF_GROUP2 = SCREEN-GROUP2 )
                                            when ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_LENGTH then 1
                                            when ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_WIDTH  then 1
                                            when ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_HEIGHT then 1
                                                                                          else 0 ).
            endif.
          endif.

***       Hide Print Override Flag if undefined
          if SCREEN-NAME cp '*-FOVRR_PRINT' and
             ME->S_FCFG_VIS-FOVRR_PRINT eq ABAP_UNDEFINED.
            move 0 to SCREEN-ACTIVE.
          endif.

***       Reopen Info Buttons
          if SCREEN-GROUP1 eq 'INF'.
            move 1 to SCREEN-INPUT.
          endif.

          modify screen.
        endloop.

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Super PBO
    SUPER->DO_PBO( ).

***--------------------------------------------------------------------------------------
*** Initialize Grid
    if ME->R_GRID_PRINT_COM is bound.
      ME->R_GRID_PRINT_COM->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_PRINT_COM->REFRESH_GRID( ).
    endif.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_FCFG_VIS  type ZVCXI_P2SS_S_FCFG_VIS.


*** Super
    SUPER->DO_UPDATE_VIS( IS_VIS = IS_VIS ).
    check ME->R_PR_FCFG is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_FCFG_VIS.

***--------------------------------------------------------------------------------------
*** Set ECMA Code
    if ME->S_FCFG_VIS-ECMA ne LS_FCFG_VIS-ECMA.
      ME->R_PR_FCFG->SET_ECMA( IF_ECMA  = LS_FCFG_VIS-ECMA
                               IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Part of Box
    if ME->S_FCFG_VIS-BOXPA ne LS_FCFG_VIS-BOXPA.
      ME->R_PR_FCFG->SET_BOXPA( IF_BOXPA = LS_FCFG_VIS-BOXPA
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Product Length
    if ME->S_FCFG_VIS-S_PROLE ne LS_FCFG_VIS-S_PROLE.
      ME->R_PR_FCFG->SET_PROLE( IS_PROLE = LS_FCFG_VIS-S_PROLE
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Product Width
    if ME->S_FCFG_VIS-S_PROWI ne LS_FCFG_VIS-S_PROWI.
      ME->R_PR_FCFG->SET_PROWI( IS_PROWI = LS_FCFG_VIS-S_PROWI
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Product Height
    if ME->S_FCFG_VIS-S_PROHE ne LS_FCFG_VIS-S_PROHE.
      ME->R_PR_FCFG->SET_PROHE( IS_PROHE = LS_FCFG_VIS-S_PROHE
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Product Height2
    if ME->S_FCFG_VIS-S_PROH2 ne LS_FCFG_VIS-S_PROH2.
      ME->R_PR_FCFG->SET_PROH2( IS_PROH2 = LS_FCFG_VIS-S_PROH2
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Product Sheet Length
    if ME->S_FCFG_VIS-S_SHTPL ne LS_FCFG_VIS-S_SHTPL.
      ME->R_PR_FCFG->SET_SHTPL( IS_SHTPL = LS_FCFG_VIS-S_SHTPL
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Product Sheet Width
    if ME->S_FCFG_VIS-S_SHTPW ne LS_FCFG_VIS-S_SHTPW.
      ME->R_PR_FCFG->SET_SHTPW( IS_SHTPW = LS_FCFG_VIS-S_SHTPW
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Tuck in Flap
    if ME->S_FCFG_VIS-S_FTUCK ne LS_FCFG_VIS-S_FTUCK.
      ME->R_PR_FCFG->SET_FTUCK( IS_FTUCK = LS_FCFG_VIS-S_FTUCK
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Dust Flap
    if ME->S_FCFG_VIS-S_FDUST ne LS_FCFG_VIS-S_FDUST.
      ME->R_PR_FCFG->SET_FDUST( IS_FDUST = LS_FCFG_VIS-S_FDUST
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Grain Orientation
    if ME->S_FCFG_VIS-GRAIO ne LS_FCFG_VIS-GRAIO.
      ME->R_PR_FCFG->SET_GRAIO( IF_GRAIO = LS_FCFG_VIS-GRAIO
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Cut Out Area
    if ME->S_FCFG_VIS-S_COUAR ne LS_FCFG_VIS-S_COUAR.
      ME->R_PR_FCFG->SET_COUAR( IS_COUAR = LS_FCFG_VIS-S_COUAR
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Joint Position
    if ME->S_FCFG_VIS-JOIPO ne LS_FCFG_VIS-JOIPO.
      ME->R_PR_FCFG->SET_JOIPO( IF_JOIPO = LS_FCFG_VIS-JOIPO
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Joint Width
    if ME->S_FCFG_VIS-S_JOIWI ne LS_FCFG_VIS-S_JOIWI.
      ME->R_PR_FCFG->SET_JOIWI( IS_JOIWI = LS_FCFG_VIS-S_JOIWI
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Joint Length
    if ME->S_FCFG_VIS-S_JOILE ne LS_FCFG_VIS-S_JOILE.
      ME->R_PR_FCFG->SET_JOILE( IS_JOILE = LS_FCFG_VIS-S_JOILE
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Joint Extension Top
    if ME->S_FCFG_VIS-S_JEXTT ne LS_FCFG_VIS-S_JEXTT.
      ME->R_PR_FCFG->SET_JEXTT( IS_JEXTT = LS_FCFG_VIS-S_JEXTT
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Joint Extension Bottom
    if ME->S_FCFG_VIS-S_JEXTB ne LS_FCFG_VIS-S_JEXTB.
      ME->R_PR_FCFG->SET_JEXTB( IS_JEXTB = LS_FCFG_VIS-S_JEXTB
                                IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Override for Print
    if ME->S_FCFG_VIS-FOVRR_PRINT ne LS_FCFG_VIS-FOVRR_PRINT.
      ME->R_PR_FCFG->SET_FOVRR_PRINT( IF_FOVRR = LS_FCFG_VIS-FOVRR_PRINT ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    if ME->R_GRID_PRINT_COM is bound.
      ME->R_GRID_PRINT_COM->CHECK_CHANGED_DATA( ).
    endif.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

    case IF_UCOMM.
      when C_UC_FCPROP.
        ME->HANDLE_UC_FCPROP( ).

    endcase.

  endmethod.
  method GET_IELEM_BY_SCREEN.

    RF_IELEM = switch #( IF_GROUP2 when 'PLE' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_LENGTH
                                   when 'PWI' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_WIDTH
                                   when 'PHE' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_HEIGHT
                                   when 'PH2' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_HEIGHT2
                                   when 'FDU' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_FDUST
                                   when 'FTU' then ZCL_VCXI_P0SS_CUST_PRSTY=>C_IELEM_FTUCK ).

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_FCFG.

  endmethod.
  method HANDLE_UC_FCPROP.

*** Start Popup
    ZCL_VCXI_P2SS_POPUP_FCPROP=>START( IR_BL_FCBPROP = ME->R_PR_FCFG->GET_FCBPX( )
                                       IR_BL_FCFPROP = ME->R_PR_FCFG->GET_FCFPX( )
                                       IF_WERKS      = ME->R_PR_FCFG->GET_WERKS( )
                                       IS_SURWT      = ME->R_PR_FCFG->GET_SURWT( ) ).

  endmethod.
  method INIT_VIS.

    data: LR_BL_FCFPROP type ref to ZCL_VCXI_P2SS_BL_FCFPROP.


    free: ME->S_FCFG_VIS.

    SUPER->INIT_VIS( ).
    ME->S_FCFG_VIS-S_XFG_VIS = ME->S_XFG_VIS.
    check ME->R_PR_FCFG is bound.

***------------------------------------------------------------------------------------------------
*** Get ECMA
    ME->S_FCFG_VIS-ECMA = ME->R_PR_FCFG->GET_ECMA( importing EF_DESCR = ME->S_FCFG_VIS-ECMA_DESCR ).

*** Get Part of Box
    ME->S_FCFG_VIS-BOXPA = ME->R_PR_FCFG->GET_BOXPA( ).

*** Get Product Dimension
    ME->S_FCFG_VIS-PRODIM_DESCR = ME->R_PR_FCFG->GET_PRODIM_DESCR( ).
    ME->S_FCFG_VIS-S_PROWI = ME->R_PR_FCFG->GET_PROWI( ).
    ME->S_FCFG_VIS-S_PROLE = ME->R_PR_FCFG->GET_PROLE( ).
    ME->S_FCFG_VIS-S_PROHE = ME->R_PR_FCFG->GET_PROHE( ).
    ME->S_FCFG_VIS-S_PROH2 = ME->R_PR_FCFG->GET_PROH2( ).

*** Get Product Sheet Dimension
    ME->S_FCFG_VIS-S_SHTPW = ME->R_PR_FCFG->GET_SHTPW( ).
    ME->S_FCFG_VIS-S_SHTPL = ME->R_PR_FCFG->GET_SHTPL( ).

*** Get Tuck in/Dust Flap
    ME->S_FCFG_VIS-S_FTUCK = ME->R_PR_FCFG->GET_FTUCK( ).
    ME->S_FCFG_VIS-S_FDUST = ME->R_PR_FCFG->GET_FDUST( ).

*** Get Grain Orientation
    ME->S_FCFG_VIS-GRAIO = ME->R_PR_FCFG->GET_GRAIO( ).

*** Get Cut Out Area
    ME->S_FCFG_VIS-S_COUAR = ME->R_PR_FCFG->GET_COUAR( ).

***------------------------------------------------------------------------------------------------
*** Get Sheet Dimension (Outbound)
    ME->S_FCFG_VIS-S_IOSEC-S_SHTLE = ME->R_PR_FCFG->GET_SHTLE( ).
    ME->S_FCFG_VIS-S_IOSEC-S_SHTWI = ME->R_PR_FCFG->GET_SHTWI( ).
    ME->S_FCFG_VIS-S_IOSEC-GRADI   = ZCL_VCXI_P2SS_BL_FCFPROP=>DET_GRADI( IS_SHTWI = ME->S_FCFG_VIS-S_IOSEC-S_SHTWI
                                                                          IS_SHTLE = ME->S_FCFG_VIS-S_IOSEC-S_SHTLE ).

*** Number Ups (Outbound)
    ME->S_FCFG_VIS-S_IOSEC-NOUPS = ME->R_PR_FCFG->GET_NOUPS( ).

*** Get Caliper, Paper Grade and Grammage (Outbound)
    LR_BL_FCFPROP = ME->R_PR_FCFG->GET_FCFPX( ).
    if LR_BL_FCFPROP is bound.
      ME->S_FCFG_VIS-S_IOSEC-S_CALIP = LR_BL_FCFPROP->S_CALIP.
      ME->S_FCFG_VIS-S_IOSEC-PAPGR   = LR_BL_FCFPROP->F_PAPGR.
      ME->S_FCFG_VIS-S_IOSEC-S_GRAMM = LR_BL_FCFPROP->S_GRAMM.
    endif.

***------------------------------------------------------------------------------------------------
*** Joint Dimensions
    ME->S_FCFG_VIS-JOIPO   = ME->R_PR_FCFG->GET_JOIPO( ).
    ME->S_FCFG_VIS-S_JOIWI = ME->R_PR_FCFG->GET_JOIWI( ).
    ME->S_FCFG_VIS-S_JOILE = ME->R_PR_FCFG->GET_JOILE( ).
    ME->S_FCFG_VIS-S_JEXTT = ME->R_PR_FCFG->GET_JEXTT( ).
    ME->S_FCFG_VIS-S_JEXTB = ME->R_PR_FCFG->GET_JEXTB( ).

***------------------------------------------------------------------------------------------------
*** Override for Print
    ME->S_FCFG_VIS-FOVRR_PRINT = ME->R_PR_FCFG->GET_FOVRR_PRINT( ).

  endmethod.
