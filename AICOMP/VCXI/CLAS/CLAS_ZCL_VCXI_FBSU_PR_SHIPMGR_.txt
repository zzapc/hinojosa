
class ZCL_VCXI_FBSU_PR_SHIPMGR definition
  public
  inheriting from ZCL_VCXI_F0SU_PR_SHIPMGR
  create public .

public section.

  methods GET_BASE_DIMEN
    redefinition .
  methods GET_BASE_PRQTY
    redefinition .
  methods GET_NOUPS
    redefinition .
protected section.
private section.
endclass. "ZCL_VCXI_FBSU_PR_SHIPMGR definition
class ZCL_VCXI_FBSU_PR_SHIPMGR implementation.
  method GET_BASE_DIMEN.

    data: LS_BGWGT        type ZVCXI_FBS_S_BGWGT,
          LS_CALCV_WEIGHT type ZVCXI_XCS_S_CALCV.
    data: LR_PR_BFG     type ref to ZCL_VCXI_FBSS_PR_BFG,
          LR_COMM_NEXT  type ref to ZIF_VCXI_FBSS_COMM_NEXT,
          LR_BL_BGFPROP type ref to ZCL_VCXI_FBSS_BL_BGFPROP.


***--------------------------------------------------------------------------------------
*** Get Bag Forward Property XML (Outbound) and Bag Weight
    case type of ME->R_GATE->GET_PARENT( )->R_CLSPR.
      when type ZCL_VCXI_FBSS_PR_BFG into LR_PR_BFG.
        LR_BL_BGFPROP ?= LR_PR_BFG->GET_BGFPX( ).
        LR_PR_BFG->GET_BGWGT( importing ES_BGWGT = LS_BGWGT ).
      when type ZIF_VCXI_FBSS_COMM_NEXT into LR_COMM_NEXT.
        LR_COMM_NEXT->GET_BGFPX( importing ER_BL_BGFPROP = LR_BL_BGFPROP ).
        LR_COMM_NEXT->GET_BGWGT( importing ES_BGWGT = LS_BGWGT ).
    endcase.

***--------------------------------------------------------------------------------------
*** Calculate Total Weight based on Base Quantity
    LS_CALCV_WEIGHT-CALCV     = LS_BGWGT-BGWGT * ME->GET_BASE_PRQTY( )-PRQTY.
    LS_CALCV_WEIGHT-CALCV_UOM = LS_BGWGT-BGWGT_UOM.

*** Add Additional Weight
    if LR_BL_BGFPROP->S_ADDWT-ADDWT_UOM is not initial.
      ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_CALCV_WEIGHT-CALCV
                                                          IF_CALCV_UOM_1 = LS_CALCV_WEIGHT-CALCV_UOM
                                                          IF_CALCV_2     = LR_BL_BGFPROP->S_ADDWT-ADDWT
                                                          IF_CALCV_UOM_2 = LR_BL_BGFPROP->S_ADDWT-ADDWT_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_ADD
                                                          IF_UOM         = LS_CALCV_WEIGHT-CALCV_UOM
                                                importing EF_CALCV       = LS_CALCV_WEIGHT-CALCV
                                                          EF_CALCV_UOM   = LS_CALCV_WEIGHT-CALCV_UOM ).
    endif.

***--------------------------------------------------------------------------------------
*** Set Dimensions
    if LR_BL_BGFPROP is bound.
      RT_DIMEN = value #( ( DIMEN     = ZCL_VCXI_XCSU_CUST_SHIPU=>C_DIMEN_LENGTH
                            DIMVA     = LR_BL_BGFPROP->S_HNDDIM-HNDLE
                            DIMVA_UOM = LR_BL_BGFPROP->S_HNDDIM-HNDLE_UOM )
                          ( DIMEN     = ZCL_VCXI_XCSU_CUST_SHIPU=>C_DIMEN_WIDTH
                            DIMVA     = LR_BL_BGFPROP->S_HNDDIM-HNDWI
                            DIMVA_UOM = LR_BL_BGFPROP->S_HNDDIM-HNDWI_UOM )
                          ( DIMEN     = ZCL_VCXI_XCSU_CUST_SHIPU=>C_DIMEN_HEIGHT
                            DIMVA     = LR_BL_BGFPROP->S_HNDDIM-HNDHE
                            DIMVA_UOM = LR_BL_BGFPROP->S_HNDDIM-HNDHE_UOM )
                          ( DIMEN     = ZCL_VCXI_XCSU_CUST_SHIPU=>C_DIMEN_WEIGHT
                            DIMVA     = LS_CALCV_WEIGHT-CALCV
                            DIMVA_UOM = LS_CALCV_WEIGHT-CALCV_UOM )
                         ).
    endif.

  endmethod.
  method GET_BASE_PRQTY.

    data: LR_PR_BFG     type ref to ZCL_VCXI_FBSS_PR_BFG,
          LR_COMM_NEXT  type ref to ZIF_VCXI_FBSS_COMM_NEXT,
          LR_BL_BGBPROP type ref to ZCL_VCXI_FBSS_BL_BGBPROP.


***--------------------------------------------------------------------------------------
*** Get Bag Forward Property XML (Outbound)
    case type of ME->R_GATE->GET_PARENT( )->R_CLSPR.
      when type ZCL_VCXI_FBSS_PR_BFG into LR_PR_BFG.
        LR_PR_BFG->ZIF_VCXI_FBSS_COMM_PREV~GET_BGBPX( importing ER_BL_BGBPROP = LR_BL_BGBPROP ).
      when type ZIF_VCXI_FBSS_COMM_NEXT into LR_COMM_NEXT.
        LR_COMM_NEXT->GET_BGBPX( importing ER_BL_BGBPROP = LR_BL_BGBPROP ).
    endcase.

    RS_PRQTY-PRQTY = cond #( when LR_BL_BGBPROP->S_WIDIM-WIBST ne 0 then LR_BL_BGBPROP->S_WIDIM-WIBST     "Wicket Dimension
                             when LR_BL_BGBPROP->S_WNDIM-BGPRL ne 0 then LR_BL_BGBPROP->S_WNDIM-BGPRL     "Winding Dimension
                             else 1 ).
    RS_PRQTY-PRQTY_UOM = 'ST'.

  endmethod.
  method GET_NOUPS.

    RF_NOUPS = 1.

  endmethod.
endclass. "ZCL_VCXI_FBSU_PR_SHIPMGR implementation
