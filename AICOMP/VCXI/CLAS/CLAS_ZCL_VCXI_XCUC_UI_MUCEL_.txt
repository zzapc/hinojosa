
class ZCL_VCXI_XCUC_UI_MUCEL definition
  public
  final
  create protected .

public section.

  data F_ACTIVE_TAB type STRING read-only .
  data S_MUCEL_VIS type ZVCXI_XCUC_S_MUCEL_VIS read-only .

  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_VIS
    importing
      !IS_MUCEL_VIS type ZVCXI_XCUC_S_MUCEL_VIS
    raising
      /VCXI/CX_CKX .
  methods DO_USER_COMMAND
    changing
      !CF_UCOMM type SYUCOMM
    raising
      /VCXI/CX_CKX .
  class-methods START_UI
    importing
      !IR_MUCEL type ref to ZCL_VCXI_XCUC_MUCEL
    raising
      /VCXI/CX_CKX .
protected section.

  constants C_UC_BACK type SYUCOMM value 'BACK' ##NO_TEXT.
  constants C_UC_CANCEL type SYUCOMM value 'CANCEL' ##NO_TEXT.
  constants C_UC_EXIT type SYUCOMM value 'EXIT' ##NO_TEXT.
  constants C_UC_SAVE type SYUCOMM value 'SAVE' ##NO_TEXT.
  constants C_UC_TOGGLE type SYUCOMM value 'TOGGLE' ##NO_TEXT.
  data R_GRID_MUCAO type ref to ZCL_VCXI_XCUC_GRID_MUCAO .
  data R_GRID_MUCAE type ref to ZCL_VCXI_XCUC_GRID_MUCAE .
  data R_GRID_MUCFE type ref to ZCL_VCXI_XCUC_GRID_MUCFE .
  data R_GRID_MUCFP type ref to ZCL_VCXI_XCUC_GRID_MUCFP .
  data R_GRID_MUCUE type ref to ZCL_VCXI_XCUC_GRID_MUCUE .
  data R_GRID_MUCUP type ref to ZCL_VCXI_XCUC_GRID_MUCUP .
  data R_GRID_MUSCE type ref to ZCL_VCXI_XCUC_GRID_MUSCE .
  data R_GRID_MUSMD type ref to ZCL_VCXI_XCUC_GRID_MUSMD .
  data R_MUCEL type ref to ZCL_VCXI_XCUC_MUCEL .

  methods CHECK_FOR_CHANGES
    returning
      value(RF_CANCELED) type ABAP_BOOL
    raising
      /VCXI/CX_CKX .
  methods CONSTRUCTOR
    importing
      !IR_MUCEL type ref to ZCL_VCXI_XCUC_MUCEL
    raising
      /VCXI/CX_CKX .
  methods DO_FREE
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_BACK
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_CANCEL
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_EXIT
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_SAVE
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_TOGGLE
    raising
      /VCXI/CX_CKX .
  methods INIT_VIS
    raising
      /VCXI/CX_CKX .
  methods MODIFY_SCREEN
    raising
      /VCXI/CX_CKX .
  methods SET_PF_STATUS
    raising
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_XCUC_UI_MUCEL definition
class ZCL_VCXI_XCUC_UI_MUCEL implementation.
method CHECK_FOR_CHANGES .

  data:  LF_ANSWER          type C.

*** Check if Changes are allowed
  check ME->R_MUCEL->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ) ne ABAP_FALSE.

*** If Changes are allowed
  if ME->R_MUCEL->F_CHANGED eq ABAP_TRUE.
    call function 'POPUP_TO_CONFIRM'
      exporting
        TEXT_QUESTION  = 'Open changes exist. Do you want to save first?'(Q01)
      importing
        ANSWER         = LF_ANSWER
      exceptions
        TEXT_NOT_FOUND = 1
        others         = 2.
    if SY-SUBRC ne 0.
      /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
    endif.

    case LF_ANSWER.
      when '1'.
***     Save and than go on
        ME->HANDLE_UC_SAVE( ).
        move ABAP_FALSE to RF_CANCELED.
      when '2'.
***     Do nothing but go on
        move ABAP_FALSE to RF_CANCELED.
      when 'A'.
***     Cancel
        move ABAP_TRUE to RF_CANCELED.
    endcase.
  endif.

endmethod.
  method CONSTRUCTOR.

    ME->R_MUCEL = IR_MUCEL.

*** Assigned SmartVC Customizing Elements
    create object ME->R_GRID_MUSCE
      exporting
        IR_MUCEL = ME->R_MUCEL.

*** Filter Parameter
    create object ME->R_GRID_MUCFP
      exporting
        IR_MUCEL = ME->R_MUCEL.

*** Filter Exit
    create object ME->R_GRID_MUCFE
      exporting
        IR_MUCEL = ME->R_MUCEL.

*** Update Parameter
    create object ME->R_GRID_MUCUP
      exporting
        IR_MUCEL = ME->R_MUCEL.

*** Update Exit
    create object ME->R_GRID_MUCUE
      exporting
        IR_MUCEL = ME->R_MUCEL.

*** Assigned SmartMD Models
    create object ME->R_GRID_MUSMD
      exporting
        IR_MUCEL = ME->R_MUCEL.

*** Authorization Object
    create object ME->R_GRID_MUCAO
      exporting
        IR_MUCEL = ME->R_MUCEL.

*** Authorization Exit
    create object ME->R_GRID_MUCAE
      exporting
        IR_MUCEL = ME->R_MUCEL.

  endmethod.
  method DO_FREE.

    clear ME->R_MUCEL.

*** Assigned SmartVC Customizing Elements
    if ME->R_GRID_MUSCE is bound.
      ME->R_GRID_MUSCE->DO_FREE( ).
    endif.

*** Filter Parameter
    if ME->R_GRID_MUCFP is bound.
      ME->R_GRID_MUCFP->DO_FREE( ).
    endif.

*** Filter Exit
    if ME->R_GRID_MUCFE is bound.
      ME->R_GRID_MUCFE->DO_FREE( ).
    endif.

*** Update Parameter
    if ME->R_GRID_MUCUP is bound.
      ME->R_GRID_MUCUP->DO_FREE( ).
    endif.

*** Update Exit
    if ME->R_GRID_MUCUE is bound.
      ME->R_GRID_MUCUE->DO_FREE( ).
    endif.

*** Assigned SmartMD Models
    if ME->R_GRID_MUSMD is bound.
      ME->R_GRID_MUSMD->DO_FREE( ).
    endif.

*** Authorization Object
    if ME->R_GRID_MUCAO is bound.
      ME->R_GRID_MUCAO->DO_FREE( ).
    endif.

*** Authorization Exit
    if ME->R_GRID_MUCAE is bound.
      ME->R_GRID_MUCAE->DO_FREE( ).
    endif.

  endmethod.
  method DO_PBO.

***----------------------------------------------------------------------------
*** Set GUI Status
    ME->SET_PF_STATUS( ).

*** Initialize Visualization
    ME->INIT_VIS( ).

*** Modify Screen
    ME->MODIFY_SCREEN( ).

***----------------------------------------------------------------------------
*** Assigned SmartVC Customizing Elements
    if ME->R_GRID_MUSCE is bound.
      ME->R_GRID_MUSCE->REFRESH_GRID( ).
    endif.

*** Filter Parameter
    if ME->R_GRID_MUCFP is bound.
      ME->R_GRID_MUCFP->REFRESH_GRID( ).
    endif.

*** Filter Exit
    if ME->R_GRID_MUCFE is bound.
      ME->R_GRID_MUCFE->REFRESH_GRID( ).
    endif.

*** Update Parameter
    if ME->R_GRID_MUCUP is bound.
      ME->R_GRID_MUCUP->REFRESH_GRID( ).
    endif.

*** Update Exit
    if ME->R_GRID_MUCUE is bound.
      ME->R_GRID_MUCUE->REFRESH_GRID( ).
    endif.

*** Assigned SmartMD Models
    if ME->R_GRID_MUSMD is bound.
      ME->R_GRID_MUSMD->REFRESH_GRID( ).
    endif.

*** Authorization Object
    if ME->R_GRID_MUCAO is bound.
      ME->R_GRID_MUCAO->REFRESH_GRID( ).
    endif.

*** Authorization Exit
    if ME->R_GRID_MUCAE is bound.
      ME->R_GRID_MUCAE->REFRESH_GRID( ).
    endif.


  endmethod.
method DO_UPDATE_VIS.

  if ME->S_MUCEL_VIS-IMUCE ne IS_MUCEL_VIS-IMUCE.
    ME->R_MUCEL->SET_ID( IF_ID = IS_MUCEL_VIS-IMUCE ).
  endif.

  if ME->S_MUCEL_VIS-DESCR ne IS_MUCEL_VIS-DESCR.
    ME->R_MUCEL->SET_DESCR( IF_DESCR = IS_MUCEL_VIS-DESCR ).
  endif.

endmethod.
  method DO_USER_COMMAND.

***--------------------------------------------------------------------------------------
*** Check for Changes on Grid
    if ME->R_GRID_MUCFP is bound.
      ME->R_GRID_MUCFP->CHECK_CHANGED_DATA( ).
    endif.
    if ME->R_GRID_MUCFE is bound.
      ME->R_GRID_MUCFE->CHECK_CHANGED_DATA( ).
    endif.
    if ME->R_GRID_MUCUP is bound.
      ME->R_GRID_MUCUP->CHECK_CHANGED_DATA( ).
    endif.
    if ME->R_GRID_MUCUE is bound.
      ME->R_GRID_MUCUE->CHECK_CHANGED_DATA( ).
    endif.

***--------------------------------------------------------------------------------------
*** Handle User Command
    case CF_UCOMM.
      when C_UC_BACK.
        ME->HANDLE_UC_BACK( ).
      when C_UC_EXIT.
        ME->HANDLE_UC_EXIT( ).
      when C_UC_CANCEL.
        ME->HANDLE_UC_CANCEL( ).

      when C_UC_SAVE.
        ME->HANDLE_UC_SAVE( ).
      when C_UC_TOGGLE.
        ME->HANDLE_UC_TOGGLE( ).

      when '0100TAB_MUSCE' or
           '0100TAB_MUCF'  or
           '0100TAB_MUCU'  or
           '0100TAB_MUSMD' or
           '0100TAB_MUCA'.
        ME->F_ACTIVE_TAB = CF_UCOMM.

    endcase.

    clear CF_UCOMM.

  endmethod.
  method HANDLE_UC_BACK.

*** Check for changes and ask user to save them
    if ME->CHECK_FOR_CHANGES( ) ne ABAP_TRUE.
      ME->R_MUCEL->/VCXI/IF_CKX_OBJECT_LOCKING~DO_UNLOCK( ).
      leave to screen 0.
    endif.

  endmethod.
  method HANDLE_UC_CANCEL.

*** Check for changes and ask user to save them
    if ME->CHECK_FOR_CHANGES( ) ne ABAP_TRUE.
      ME->R_MUCEL->/VCXI/IF_CKX_OBJECT_LOCKING~DO_UNLOCK( ).
      leave to screen 0.
    endif.

  endmethod.
  method HANDLE_UC_EXIT.

*** Check for changes and ask user to save them
    if ME->CHECK_FOR_CHANGES( ) ne ABAP_TRUE.
      ME->R_MUCEL->/VCXI/IF_CKX_OBJECT_LOCKING~DO_UNLOCK( ).
      leave to screen 0.
    endif.

  endmethod.
  method HANDLE_UC_SAVE.

*** Perform the Save
    ME->R_MUCEL->/VCXI/IF_CKX_OBJECT_ACTION~HANDLE_ACTION( IF_FCODE = ZCL_VCXI_XCUC_MUCEL=>C_FCODE_SAVE ).
    commit work and wait.

*** Successfully saved.
    message id 'ZVCXI_XCUC' type 'S' number '001'.

  endmethod.
  method HANDLE_UC_TOGGLE.

    if R_MUCEL->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ) ne ABAP_FALSE.
***   Check for changes and ask user to save them
      check ME->CHECK_FOR_CHANGES( ) ne ABAP_TRUE.

      R_MUCEL->/VCXI/IF_CKX_OBJECT_LOCKING~DO_UNLOCK( ).
    else.
      R_MUCEL->/VCXI/IF_CKX_OBJECT_LOCKING~DO_LOCK( ).
    endif.

  endmethod.
  method INIT_VIS.

    clear: ME->S_MUCEL_VIS.

*** Get ID and Description
    ME->S_MUCEL_VIS-IMUCE = ME->R_MUCEL->/VCXI/IF_CKX_OBJECT~GET_ID( ).
    ME->S_MUCEL_VIS-DESCR = ME->R_MUCEL->/VCXI/IF_CKX_OBJECT~GET_DESCR( ).

*** Get Log and Source
    ME->S_MUCEL_VIS-S_LOG_EXT = /VCXI/CL_CKX_SERVICE=>GET_LOG_EXT( IS_LOG = ME->R_MUCEL->/VCXI/IF_CKX_OBJECT~GET_LOG( ) ).
    ME->S_MUCEL_VIS-S_SRC     = ME->R_MUCEL->S_SRC.

  endmethod.
  method MODIFY_SCREEN.

    data: LF_LOCKED type /VCXI/CKX_LOCKED.

*** Get Lock Status
    LF_LOCKED = ME->R_MUCEL->/VCXI/IF_CKX_OBJECT_LOCKING~IS_LOCKED( ).

***------------------------------------------------------------------------------------------------
*** Modify Screen
    loop at screen.
      if LF_LOCKED     ne ABAP_TRUE and
         SCREEN-GROUP1 ne 'TAB'.          "Keep Tab open
        move 0 to SCREEN-INPUT.
        move 0 to SCREEN-REQUIRED.
      endif.
      modify screen.
    endloop.

  endmethod.
  method SET_PF_STATUS.

    set pf-status '0100' of program 'SAPLZVCXI_XCUC_UI_MUCEL'.

  endmethod.
  method START_UI.

    data: LR_UI_MUCEL type ref to ZCL_VCXI_XCUC_UI_MUCEL.


    check IR_MUCEL is bound.

*** Check Authorization
    ZCL_VCXI_XCUC_MUCEL=>CHECK_AUTH( IR_MUCEL = IR_MUCEL
                                     IF_ACTVT  = ZCL_VCXI_XCUC_MUCEL=>C_ACTVT_DISPLAY ).

*** Create Instance of UI
    create object LR_UI_MUCEL
      exporting
        IR_MUCEL = IR_MUCEL.

*** Start UI Screen
    call function 'ZVCXI_XCUC_UI_MUCEL_START'
      exporting
        IR_UI_MUCEL = LR_UI_MUCEL.

*** Free Instance
    LR_UI_MUCEL->DO_FREE( ).

  endmethod.
