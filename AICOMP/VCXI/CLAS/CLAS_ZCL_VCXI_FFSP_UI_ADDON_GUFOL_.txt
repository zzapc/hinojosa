
class ZCL_VCXI_FFSP_UI_ADDON_GUFOL definition
  public
  inheriting from ZCL_VCXI_XCSP_UI_ADDON
  create public .

public section.

  data S_ADDON_GUFOL_VIS type ZVCXI_FFSP_S_ADDON_GUFOL_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
protected section.

  data R_PR_ADDON_GUFOL type ref to ZCL_VCXI_FFSP_PR_ADDON_GUFOL .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_FFSP_UI_ADDON_GUFOL definition
class ZCL_VCXI_FFSP_UI_ADDON_GUFOL implementation.
  method ACTIVATE.

  SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
  call function 'ZVCXI_FFSP_UI_ADDON_GUFOL_BIND'
    exporting
      IR_UI_ADDON_GUFOL = ME
    importing
      ES_DYNP          = ME->S_DYNP.

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_ADDON_GUFOL.
      catch CX_SY_MOVE_CAST_ERROR.
***   UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DO_MODIFY_SCREEN.

    data: LF_LOCKED type        /VCXI/CKX_LOCKED,
          LF_INUSE  type        /VCXI/VKSR_INUSE.
    data:  LR_CX_CKX type ref to /VCXI/CX_CKX.

    SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                             IF_DYNNR = IF_DYNNR ).

    try.

        if IF_REPID eq ME->S_DYNP-REPID and
           IF_DYNNR eq ME->S_DYNP-DYNNR.

          LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
          LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***     Modify Scree using DynamicVC
          ZCL_VCXI_XCDR_SERVICE=>MODIFY_SCREEN( IR_GATE = ME->R_GATE ).

          loop at screen.
            if LF_LOCKED ne ABAP_TRUE or
               LF_INUSE  ne ABAP_TRUE.
              move 0 to SCREEN-INPUT.
            endif.
            modify screen.
          endloop.
        endif.

      catch /VCXI/CX_CKX into LR_CX_CKX.
        ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_ADDON_GUFOL_VIS type ZVCXI_FFSP_S_ADDON_GUFOL_VIS.


*** Super
    SUPER->DO_UPDATE_VIS( IS_VIS = IS_VIS ).
    check ME->R_PR_ADDON_GUFOL is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_ADDON_GUFOL_VIS.

***-------------------------------------------------------------------------------------
*** Set Gusset Left
    if ME->S_ADDON_GUFOL_VIS-S_GULEF ne LS_ADDON_GUFOL_VIS-S_GULEF.
      ME->R_PR_ADDON_GUFOL->SET_GULEF( IS_GULEF = LS_ADDON_GUFOL_VIS-S_GULEF
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Gusset Right
    if ME->S_ADDON_GUFOL_VIS-S_GURIG ne LS_ADDON_GUFOL_VIS-S_GURIG.
      ME->R_PR_ADDON_GUFOL->SET_GURIG( IS_GURIG = LS_ADDON_GUFOL_VIS-S_GURIG
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method HANDLE_GATE_FREED.

  SUPER->HANDLE_GATE_FREED( ).

  free: ME->R_PR_ADDON_GUFOL.

  endmethod.
  method INIT_VIS.

*** Super
    SUPER->INIT_VIS( ).

    free: ME->S_ADDON_GUFOL_VIS.
    ME->S_ADDON_GUFOL_VIS-S_ADDON_VIS = ME->S_ADDON_VIS.

***--------------------------------------------------------------------------------------
*** Get Gusset Left
    ME->R_PR_ADDON_GUFOL->GET_GULEF( importing ES_GULEF = ME->S_ADDON_GUFOL_VIS-S_GULEF ).

*** Get Gusset Right
    ME->R_PR_ADDON_GUFOL->GET_GURIG( importing ES_GURIG = ME->S_ADDON_GUFOL_VIS-S_GURIG ).

  endmethod.
