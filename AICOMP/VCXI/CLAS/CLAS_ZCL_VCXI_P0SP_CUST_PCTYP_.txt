
class ZCL_VCXI_P0SP_CUST_PCTYP definition
  public
  create protected .

public section.

  class-methods CLASS_CONSTRUCTOR .
  class-methods GET_PCTYP_DESCR
    importing
      !IF_PCTYP type ZVCXI_P0SP_PCTYP
    returning
      value(RF_DESCR) type /VCXI/CKX_DESCR
    raising
      /VCXI/CX_CKX .
  class-methods GET_ROUND_PCTYP
    importing
      !IF_PCTYP type ZVCXI_P0SP_PCTYP
    changing
      value(CF_VALUE) type ANY
    raising
      /VCXI/CX_CKX .
protected section.

  types:
    YT_TC010 type standard table of ZVCXI_P0S_TC010 with default key .
  types:
    YT_TC010T type standard table of ZVCXI_P0S_TC010T with default key .
  types:
    begin of YS_PCTYP_DESCR,
           PCTYP type ZVCXI_P0SP_PCTYP,
           DESCR type /VCXI/CKX_DESCR,
         end   of YS_PCTYP_DESCR .
  types:
    YT_PCTYP_DESCR type standard table of YS_PCTYP_DESCR .

  class-data T_PCTYP_DESCR type YT_PCTYP_DESCR .
  class-data T_TC010 type YT_TC010 .

  class-methods INIT_PCTYP_DESCR
    exceptions
      /VCXI/CX_CKX .
private section.
endclass. "ZCL_VCXI_P0SP_CUST_PCTYP definition
class ZCL_VCXI_P0SP_CUST_PCTYP implementation.
method CLASS_CONSTRUCTOR.

*** Initialize PC Type Descriptions
  INIT_PCTYP_DESCR( ).

endmethod.
method GET_PCTYP_DESCR.

  data:  LS_PCTYP_DESCR type YS_PCTYP_DESCR.

  read table T_PCTYP_DESCR into LS_PCTYP_DESCR
                           with key PCTYP = IF_PCTYP.
  if SY-SUBRC eq 0.
    move LS_PCTYP_DESCR-DESCR to RF_DESCR.
  endif.

endmethod.
method GET_ROUND_PCTYP.

  data:  LS_TC010 type ZVCXI_P0S_TC010.

  read table T_TC010 into LS_TC010
                     with key PCTYP = IF_PCTYP.
  check SY-SUBRC eq 0 and
        LS_TC010-FPCQR eq ABAP_TRUE.

  CF_VALUE = ceil( CF_VALUE ).

endmethod.
method INIT_PCTYP_DESCR.

  data:          LS_TC010        type          ZVCXI_P0S_TC010,
                 LS_TC010T       type          ZVCXI_P0S_TC010T.
  data:          LT_TC010T       type table of ZVCXI_P0S_TC010T.
  field-symbols: <S_PCTYP_DESCR> type          YS_PCTYP_DESCR.

  select *
         into table T_TC010
         from ZVCXI_P0S_TC010.
  check SY-SUBRC eq 0.

  select *
         into table LT_TC010T
         from ZVCXI_P0S_TC010T
         for all entries in T_TC010
        where PCTYP eq T_TC010-PCTYP
         and  LANGU eq SY-LANGU.

  loop at T_TC010 into LS_TC010.
    read table LT_TC010T into LS_TC010T
                         with key PCTYP = LS_TC010-PCTYP.
    append initial line to T_PCTYP_DESCR assigning <S_PCTYP_DESCR>.
    move LS_TC010-PCTYP   to <S_PCTYP_DESCR>-PCTYP.
    move LS_TC010T-DESCR  to <S_PCTYP_DESCR>-DESCR.
  endloop.

endmethod.
