
class ZCL_VCXI_PCSP_UI_ADDON_WAX definition
  public
  inheriting from ZCL_VCXI_XCSP_UI_ADDON
  create public .

public section.

  data S_ADDON_WAX_VIS type ZVCXI_PCSP_S_ADDON_WAX_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
protected section.

  data R_PR_ADDON_WAX type ref to ZCL_VCXI_PCSP_PR_ADDON_WAX .

  methods DO_UPDATE_MC_WAX_VIS
    importing
      !IS_MC_WAX_VIS type ZVCXI_PCSP_S_MC_WAX_VIS
    raising
      /VCXI/CX_CKX .
  methods INIT_VIS_MC_WAX
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_PCSP_UI_ADDON_WAX definition
class ZCL_VCXI_PCSP_UI_ADDON_WAX implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
    call function 'ZVCXI_PCSP_UI_ADDON_WAX_BIND'
      exporting
        IR_UI_ADDON_WAX = ME
      importing
        ES_DYNP         = ME->S_DYNP.

  endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                      IR_UIGATE = IR_UIGATE
                      IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
  try.
      move IR_GATE->R_CLSPR ?to ME->R_PR_ADDON_WAX.
    catch CX_SY_MOVE_CAST_ERROR.
***   UI ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '801'.
  endtry.

endmethod.
  method DO_UPDATE_MC_WAX_VIS.

    data: LT_GATE              type          /VCXI/VKSR_TT_GATE.
    data: LR_GATE      type ref to   /VCXI/CL_VKSR_GATE,
          LR_PR_MC_WAX type ref to   ZCL_VCXI_PCSP_PR_MC_WAX.


*** Get Gates of Material Consumption
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCSP_PR_MC=>C_IPRVT_XCS_MC
                                     IF_INUSE = ABAP_TRUE ).

    read table LT_GATE into LR_GATE index 1.

    try.
        move LR_GATE->R_CLSPR ?to LR_PR_MC_WAX.
        check LR_PR_MC_WAX is bound.

***     Set Material Number
        if ME->S_ADDON_WAX_VIS-MATNR ne IS_MC_WAX_VIS-MATNR.
          LR_PR_MC_WAX->SET_MATNR( IF_MATNR = IS_MC_WAX_VIS-MATNR
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
        endif.

***     Set Average Consumption and Consumption per
        if ME->S_ADDON_WAX_VIS-S_AVGCO ne IS_MC_WAX_VIS-S_AVGCO.
          LR_PR_MC_WAX->SET_AVGCO( IS_AVGCO = IS_MC_WAX_VIS-S_AVGCO
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
        endif.
        if ME->S_ADDON_WAX_VIS-S_AVGCP ne IS_MC_WAX_VIS-S_AVGCP.
          LR_PR_MC_WAX->SET_AVGCP( IS_AVGCP = IS_MC_WAX_VIS-S_AVGCP
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
        endif.

***     Set Wax Factor
        if ME->S_ADDON_WAX_VIS-WAXFA ne IS_MC_WAX_VIS-WAXFA.
          LR_PR_MC_WAX->SET_WAXFA( IF_WAXFA = IS_MC_WAX_VIS-WAXFA
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
        endif.

      catch CX_SY_MOVE_CAST_ERROR.
        clear: LR_PR_MC_WAX.
    endtry.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_ADDON_WAX_VIS type ZVCXI_PCSP_S_ADDON_WAX_VIS.


*** Super
    SUPER->DO_UPDATE_VIS( IS_VIS = IS_VIS ).
    check ME->R_PR_ADDON_WAX is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_ADDON_WAX_VIS.

***-------------------------------------------------------------------------------------
*** Update of Material Consumption Wax
    ME->DO_UPDATE_MC_WAX_VIS( IS_MC_WAX_VIS = S_ADDON_WAX_VIS-S_MC_WAX_VIS ).

  endmethod.
  method HANDLE_GATE_FREED.

    SUPER->HANDLE_GATE_FREED( ).

    free: ME->R_PR_ADDON_WAX.

  endmethod.
  method INIT_VIS.

*** Super
    SUPER->INIT_VIS( ).

    free: ME->S_ADDON_WAX_VIS.
    ME->S_ADDON_WAX_VIS-S_ADDON_VIS = ME->S_ADDON_VIS.

***--------------------------------------------------------------------------------------
    ME->INIT_VIS_MC_WAX( ).

  endmethod.
  method INIT_VIS_MC_WAX.

    data: LS_MC_WAX_VIS type ZVCXI_PCSP_S_MC_WAX_VIS.
    data: LT_GATE       type /VCXI/VKSR_TT_GATE.
    data: LR_GATE      type ref to   /VCXI/CL_VKSR_GATE,
          LR_PR_MC_WAX type ref to   ZCL_VCXI_PCSP_PR_MC_WAX.

***--------------------------------------------------------------------------------------
*** Get Gates of Material Consumption
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCSP_PR_MC=>C_IPRVT_XCS_MC
                                     IF_INUSE = ABAP_TRUE ).

    read table LT_GATE into LR_GATE index 1.
    check SY-SUBRC eq 0.

    try.
        move LR_GATE->R_CLSPR ?to LR_PR_MC_WAX.
        check LR_PR_MC_WAX is bound.

***     Get Material Number
        move LR_PR_MC_WAX->GET_MATNR( )       to LS_MC_WAX_VIS-MATNR.
        move LR_PR_MC_WAX->GET_MATNR_DESCR( ) to LS_MC_WAX_VIS-MATNR_DESCR.

***     Get Average Consumption and Consumption per
        LS_MC_WAX_VIS-S_AVGCO = LR_PR_MC_WAX->GET_AVGCO( ).
        LS_MC_WAX_VIS-S_AVGCP = LR_PR_MC_WAX->GET_AVGCP( ).

***     Get Wax Factor
        LS_MC_WAX_VIS-WAXFA   = LR_PR_MC_WAX->GET_WAXFA( ).

***     Get Wax Consumption
        LS_MC_WAX_VIS-S_CONSU = LR_PR_MC_WAX->GET_CONSU( ).

***--------------------------------------------------------------------------------------
***     Add to Overall Vis.
        ME->S_ADDON_WAX_VIS-S_MC_WAX_VIS = LS_MC_WAX_VIS.

      catch CX_SY_MOVE_CAST_ERROR.
        clear: LR_PR_MC_WAX.
    endtry.

  endmethod.
