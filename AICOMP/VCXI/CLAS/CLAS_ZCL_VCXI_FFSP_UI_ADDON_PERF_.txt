
class ZCL_VCXI_FFSP_UI_ADDON_PERF definition
  public
  inheriting from ZCL_VCXI_XCSP_UI_ADDON
  create public .

public section.

  data S_ADDON_PERF_VIS type ZVCXI_FFSP_S_ADDON_PERF_VIS read-only .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
protected section.

  data R_PR_ADDON_PERF type ref to ZCL_VCXI_FFSP_PR_ADDON_PERF .

  methods DO_UPDATE_MC_TOOL_VIS
    importing
      !IS_MC_TOOL_VIS type ZVCXI_FFSP_S_MC_TOOL_VIS
    raising
      /VCXI/CX_CKX .
  methods INIT_VIS_MC_TOOL
    raising
      /VCXI/CX_CKX .

  methods HANDLE_GATE_FREED
    redefinition .
  methods INIT_VIS
    redefinition .
private section.
endclass. "ZCL_VCXI_FFSP_UI_ADDON_PERF definition
class ZCL_VCXI_FFSP_UI_ADDON_PERF implementation.
method ACTIVATE.

  SUPER->ACTIVATE( ).

*** Bind UI to Dynpro
  call function 'ZVCXI_FFSP_UI_ADDON_PERF_BIND'
    exporting
      IR_UI_ADDON_PERF = ME
    importing
      ES_DYNP          = ME->S_DYNP.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                      IR_UIGATE = IR_UIGATE
                      IR_GATE   = IR_GATE ).

*** Assign th RP Class to the UI Class
  try.
      move IR_GATE->R_CLSPR ?to ME->R_PR_ADDON_PERF.
    catch CX_SY_MOVE_CAST_ERROR.
***   UI ABAP Class is not compatible with PR ABAP Class.
      raise exception type /VCXI/CX_CKX
        exporting
          F_MSGTY = 'E'
          F_MSGID = '/VCXI/VKSR'
          F_MSGNO = '801'.
  endtry.

endmethod.
method DO_MODIFY_SCREEN.

  data: LF_FLASR  type        ZVCXI_F0S_FLASR,
        LF_LOCKED type        /VCXI/CKX_LOCKED,
        LF_INUSE  type        /VCXI/VKSR_INUSE.
  data: LR_CX_CKX   type ref to /VCXI/CX_CKX.

  try.
      SUPER->DO_MODIFY_SCREEN( IF_REPID = IF_REPID
                               IF_DYNNR = IF_DYNNR ).

***   Get Perforation Type Flag - Laser
      LF_FLASR = ZCL_VCXI_F0S_CUST_PERF=>GET_FLASR( IF_TPERF = ME->S_ADDON_PERF_VIS-TPERF ).

      LF_LOCKED = ME->R_GATE->IS_LOCKED( ).
      LF_INUSE  = ME->R_GATE->IS_INUSE( ).

***   Modify Scree using DynamicVC
      ZCL_VCXI_XCDR_SERVICE=>MODIFY_SCREEN( IR_GATE = ME->R_GATE ).

      loop at screen.

        if LF_LOCKED eq ABAP_TRUE or
           LF_INUSE  eq ABAP_TRUE.

          case SCREEN-GROUP1.

***         Check Number of Laser Lines
            when 'NLA'.
              if ME->S_ADDON_PERF_VIS-TPERF is initial .
                move 1 to SCREEN-INVISIBLE.
                move 0 to SCREEN-ACTIVE.
              else.
                if LF_FLASR eq ABAP_TRUE.
                  move 2 to SCREEN-REQUIRED.
                else.
                  move 1 to SCREEN-INVISIBLE.
                  move 0 to SCREEN-ACTIVE.
                endif.
              endif.

***         Check Die Cut Tool Number
            when 'DCU'.
              if ME->S_ADDON_PERF_VIS-TPERF is initial.
                move 1 to SCREEN-INVISIBLE.
                move 0 to SCREEN-ACTIVE.
              else.
                if LF_FLASR eq ABAP_FALSE.
                  if SCREEN-INPUT eq 1.
                    if SCREEN-GROUP2 ne 'NRQ'.
                      move 2 to SCREEN-REQUIRED.
                    endif.
                  endif.
                else.
                  move 1 to SCREEN-INVISIBLE.
                  move 0 to SCREEN-ACTIVE.
                endif.
              endif.
          endcase.

        endif.

***     Lock everything if not in change mode
        if LF_LOCKED ne ABAP_TRUE or
           LF_INUSE  ne ABAP_TRUE.
          move 0 to SCREEN-INPUT.
        endif.

***     Reopen Info Buttons
        if SCREEN-GROUP1 eq 'INF'.
          move 1 to SCREEN-INPUT.
        endif.

        modify screen.
      endloop.

    catch /VCXI/CX_CKX into LR_CX_CKX.
      ME->R_GATE->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
  endtry.

endmethod.
  method DO_UPDATE_MC_TOOL_VIS.

    data: LF_TPERF type ZVCXI_F0S_TPERF.
    data: LT_GATE  type /VCXI/VKSR_TT_GATE.
    data: LR_GATE       type ref to /VCXI/CL_VKSR_GATE,
          LR_PR_MC_TOOL type ref to ZCL_VCXI_XCSP_PR_MC_TOOL.


*** Get Gates of Material Consumption
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCSP_PR_MC=>C_IPRVT_XCS_MC
                                     IF_INUSE = ABAP_TRUE ).

    read table LT_GATE into LR_GATE index 1.
    if LR_GATE is not bound.
      if IS_MC_TOOL_VIS-MATNR is not initial.

***     Create new Material Consumption Tool
        LR_GATE = ME->R_GATE->CREATE_ELEMENT( IF_ID = 'MC_TOOL' ).
        if LR_GATE is bound .
          LR_GATE->SET_INUSE( IF_INUSE = ABAP_TRUE ).
        endif.
      endif.

    else.
***   Get Perforation Type Flag - Laser
      ME->R_PR_ADDON_PERF->GET_TPERF( importing EF_TPERF = LF_TPERF ).
      if ZCL_VCXI_F0S_CUST_PERF=>GET_FLASR( IF_TPERF = LF_TPERF ) eq ABAP_TRUE.
***     Set Inuse Material Consumption Tool to False
        LR_GATE->SET_INUSE( IF_INUSE = ABAP_FALSE ).
        LR_GATE->DO_FREE( ).

      else.

        if IS_MC_TOOL_VIS-MATNR is initial.
***       Set Inuse Material Consumption Tool to False
          LR_GATE->SET_INUSE( IF_INUSE = ABAP_FALSE ).
          LR_GATE->DO_FREE( ).
        endif.
      endif.

    endif.

    check LR_GATE is bound.

    try.
        move LR_GATE->R_CLSPR ?to LR_PR_MC_TOOL.
        check LR_PR_MC_TOOL is bound.

***     Set Material Number
        if ME->S_ADDON_PERF_VIS-MATNR ne IS_MC_TOOL_VIS-MATNR.
          LR_PR_MC_TOOL->SET_MATNR( IF_MATNR = IS_MC_TOOL_VIS-MATNR
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
        endif.

***     Set Text
        if ME->S_ADDON_PERF_VIS-TEXT ne IS_MC_TOOL_VIS-TEXT.
          LR_PR_MC_TOOL->SET_TEXT( IF_TEXT = IS_MC_TOOL_VIS-TEXT
                                   IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
        endif.

***     Set Average Consumption and Consumption per
        if ME->S_ADDON_PERF_VIS-AVGCO ne IS_MC_TOOL_VIS-AVGCO.
          LR_PR_MC_TOOL->SET_AVGCO( IS_AVGCO = IS_MC_TOOL_VIS-S_AVGCO
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
        endif.

        if ME->S_ADDON_PERF_VIS-AVGCP ne IS_MC_TOOL_VIS-AVGCP.
          LR_PR_MC_TOOL->SET_AVGCP( IS_AVGCP = IS_MC_TOOL_VIS-S_AVGCP
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
        endif.

      catch CX_SY_MOVE_CAST_ERROR.
        clear: LR_PR_MC_TOOL.
    endtry.

  endmethod.
  method DO_UPDATE_VIS.

    data: LF_NOLAS type         ZVCXI_F0S_NOLAS.
    data: LS_ADDON_PERF_VIS type ZVCXI_FFSP_S_ADDON_PERF_VIS.


*** Super
    SUPER->DO_UPDATE_VIS( IS_VIS = IS_VIS ).
    check ME->R_PR_ADDON_PERF is bound.

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_ADDON_PERF_VIS.

***-------------------------------------------------------------------------------------
*** Set Perforation Type
    if ME->S_ADDON_PERF_VIS-TPERF ne LS_ADDON_PERF_VIS-TPERF.
      ME->R_PR_ADDON_PERF->SET_TPERF( IF_TPERF = LS_ADDON_PERF_VIS-TPERF
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Get Perforation Type Flag - Laser
    if ZCL_VCXI_F0S_CUST_PERF=>GET_FLASR( IF_TPERF = LS_ADDON_PERF_VIS-TPERF ) eq ABAP_TRUE.
***   Only set parameter value if Laser is allowed based on Customizing
      move LS_ADDON_PERF_VIS-NOLAS to LF_NOLAS.
    endif.

*** Set Number of Laser Lines
    if ME->S_ADDON_PERF_VIS-NOLAS ne LF_NOLAS.
      ME->R_PR_ADDON_PERF->SET_NOLAS( IF_NOLAS = LF_NOLAS
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

***-------------------------------------------------------------------------------------
*** Update Material Consumption Tool
    ME->DO_UPDATE_MC_TOOL_VIS( IS_MC_TOOL_VIS = LS_ADDON_PERF_VIS-S_MC_TOOL_VIS ).

  endmethod.
method HANDLE_GATE_FREED.

  SUPER->HANDLE_GATE_FREED( ).

  free: ME->R_PR_ADDON_PERF.

endmethod.
  method INIT_VIS.

*** Super
    SUPER->INIT_VIS( ).

    free: ME->S_ADDON_PERF_VIS.
    ME->S_ADDON_PERF_VIS-S_ADDON_VIS = ME->S_ADDON_VIS.

***--------------------------------------------------------------------------------------
*** Get Perforation Type
    ME->R_PR_ADDON_PERF->GET_TPERF( importing EF_TPERF = ME->S_ADDON_PERF_VIS-TPERF ).

*** Get Number of Laser Lines
    ME->R_PR_ADDON_PERF->GET_NOLAS( importing EF_NOLAS = ME->S_ADDON_PERF_VIS-NOLAS ).

***--------------------------------------------------------------------------------------
*** Initialize Material Consumption Vis
    ME->INIT_VIS_MC_TOOL( ).

  endmethod.
  method INIT_VIS_MC_TOOL.

    data: LT_GATE type /VCXI/VKSR_TT_GATE.
    data: LR_GATE       type ref to /VCXI/CL_VKSR_GATE,
          LR_PR_MC_TOOL type ref to ZCL_VCXI_XCSP_PR_MC_TOOL.

***--------------------------------------------------------------------------------------
*** Get Gates of Material Consumption
    LT_GATE = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_XCSP_PR_MC=>C_IPRVT_XCS_MC
                                     IF_INUSE = ABAP_TRUE ).
    read table LT_GATE into LR_GATE index 1.
    if SY-SUBRC eq 0.

      try.
          move LR_GATE->R_CLSPR ?to LR_PR_MC_TOOL.
        catch CX_SY_MOVE_CAST_ERROR.
          clear: LR_PR_MC_TOOL.
      endtry.
      check LR_PR_MC_TOOL is bound.

***   Check if Tool is allowed based on Customizing
      if ZCL_VCXI_F0S_CUST_PERF=>GET_FLASR( IF_TPERF = ME->S_ADDON_PERF_VIS-TPERF ) eq ABAP_TRUE.
***     Remove MC Tool
        LR_GATE->DO_FREE( ).

      else.
***     Get Material Number
        move LR_PR_MC_TOOL->GET_MATNR( )       to ME->S_ADDON_PERF_VIS-MATNR.
        move LR_PR_MC_TOOL->GET_MATNR_DESCR( ) to ME->S_ADDON_PERF_VIS-MATNR_DESCR.

***     Get Text
        move LR_PR_MC_TOOL->GET_TEXT( )       to ME->S_ADDON_PERF_VIS-TEXT.

***     Get Average Consumption and Consumption per
        ME->S_ADDON_PERF_VIS-S_AVGCO = LR_PR_MC_TOOL->GET_AVGCO( ).
        ME->S_ADDON_PERF_VIS-S_AVGCP = LR_PR_MC_TOOL->GET_AVGCP( ).

***     Get Consumption
        ME->S_ADDON_PERF_VIS-S_CONSU = LR_PR_MC_TOOL->GET_CONSU( ).

      endif.
    endif.

  endmethod.
