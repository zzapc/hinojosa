
class ZCL_VCXI_PCSR_BL_PSTEP_CORR definition
  public
  inheriting from ZCL_VCXI_P0SR_BL_PSTEP
  create public .

public section.

  interfaces ZIF_VCXI_P0SR_ADJ_PCDIM .
  interfaces ZIF_VCXI_P0SR_CONV_ATP .
  interfaces ZIF_VCXI_P0SR_CONV_LTP .
  interfaces ZIF_VCXI_P0SR_PCDIR .
  interfaces ZIF_VCXI_PCSR_ADJ_FLUTE .

  constants C_IPSTP_CORR type /VCXI/VSMC_IPSTP value 'PC_PS_CORR'. "#EC NOTEXT

  methods CONSTRUCTOR
    importing
      !IF_IPSTP type /VCXI/VSMC_IPSTP default C_IPSTP_CORR
      !IF_DATE type DATS
      !IR_APPDATA type ref to /VCXI/CL_VKSR_APPDATA
    raising
      /VCXI/CX_CKX .

  methods ADD_ASPECTS_TO_RTE_PSTEP
    redefinition .
  methods ADD_LIMITS_TO_RTE_PSTEP
    redefinition .
  methods COMPARE
    redefinition .
  methods COPY
    redefinition .
  methods DESERIALIZE
    redefinition .
  methods GET_ARBPL_INFO
    redefinition .
  methods GET_SPVRE_MF
    redefinition .
  methods INIT_BY_SPEC
    redefinition .
  methods SERIALIZE
    redefinition .
protected section.

  data R_ARBPL type ref to /VCXI/CL_VSMC_ARBPL .
  constants C_IAATY_BOARD type /VCXI/VSMC_IAATY value 'PC_AA_BOARD'. "#EC NOTEXT
  constants C_IAATY_FLUTE type /VCXI/VSMC_IAATY value 'PC_AA_FLUTE'. "#EC NOTEXT
  data F_BOARD type ZVCXI_PCS_BOARD .
  data F_FLUTE type ZVCXI_PCS_FLUTE .
  data S_COWID type ZVCXI_PCS_S_COWID .
  data S_MS_STARCH type ZVCXI_PCSR_S_MS_STARCH .
  data S_PCDIM_OUT type ZVCXI_P0SP_S_PCDIM .
  data T_MS_PAPER type ZVCXI_PCSR_TT_MS_PAPER .
  data T_SCORE type ZVCXI_P0SR_TT_SCORE .

  methods INIT_BY_SPEC_BOARD
    importing
      !IT_PRVDR type /VCXI/VKSR_TT_GATE
    raising
      /VCXI/CX_CKX .
  methods INIT_BY_SPEC_PAPER
    importing
      !IT_PRVDR type /VCXI/VKSR_TT_GATE
    raising
      /VCXI/CX_CKX .
  methods INIT_BY_SPEC_SCORE
    importing
      !IT_PRVDR type /VCXI/VKSR_TT_GATE
    raising
      /VCXI/CX_CKX .
  methods INIT_BY_SPEC_STARCH
    importing
      !IT_PRVDR type /VCXI/VKSR_TT_GATE
    raising
      /VCXI/CX_CKX .
  methods ADD_SPVRE_MC_STARCH
    importing
      !IR_BL_RTESPV type ref to ZCL_VCXI_XCSR_BL_RTESPV
      !IR_SPVRE_ADDON type ref to /VCXI/CL_VKSR_SPVRE
    changing
      !CT_SPVRE type /VCXI/VKSR_TT_SPVRE
    raising
      /VCXI/CX_CKX .
  methods ADD_SPVRE_MFMI_PAPER
    importing
      !IR_BL_RTESPV type ref to ZCL_VCXI_XCSR_BL_RTESPV
      !IR_SPVRE_MFMGR type ref to /VCXI/CL_VKSR_SPVRE
    changing
      !CT_SPVRE type /VCXI/VKSR_TT_SPVRE
    raising
      /VCXI/CX_CKX .
  methods INIT_BY_SPEC_COWID
    importing
      !IT_PRVDR type /VCXI/VKSR_TT_GATE
    raising
      /VCXI/CX_CKX .
  methods INIT_BY_SPEC_PCDIM
    importing
      !IT_PRVDR type /VCXI/VKSR_TT_GATE
    raising
      /VCXI/CX_CKX .
  methods FILL_SPVRE_ADDON_CORR
    importing
      !IR_BL_RTESPV type ref to ZCL_VCXI_XCSR_BL_RTESPV
      !IR_SPVRE_ADDON type ref to /VCXI/CL_VKSR_SPVRE
    changing
      !CT_SPVRE type /VCXI/VKSR_TT_SPVRE
    raising
      /VCXI/CX_CKX .

  methods FILL_SPVRE_ADDON
    redefinition .
private section.
endclass. "ZCL_VCXI_PCSR_BL_PSTEP_CORR definition
class ZCL_VCXI_PCSR_BL_PSTEP_CORR implementation.
method ADD_ASPECTS_TO_RTE_PSTEP.

  data:          LS_RTE_SAVAL  type        /VCXI/VSMR_S_RTE_SAVAL.
  field-symbols: <S_AVVAL>     type        /VCXI/VSMR_S_RTE_RANGE_AVVAL.

***----------------------------------------------------------------------------
*** Get Super
  SUPER->ADD_ASPECTS_TO_RTE_PSTEP( IR_RTE_PSTEP = IR_RTE_PSTEP ).


***----------------------------------------------------------------------------
*** Aspect - PC_AA_BOARD
  try.
      clear LS_RTE_SAVAL.
      append initial line         to LS_RTE_SAVAL-I_AVVAL assigning <S_AVVAL>.
      move 'I'                    to <S_AVVAL>-SIGN.
      move 'EQ'                   to <S_AVVAL>-OPTION.
      move ME->F_BOARD            to <S_AVVAL>-LOW.

      IR_RTE_PSTEP->SET_RTE_SAVAL( IF_IAATY  = C_IAATY_BOARD
                                   II_AVVAL  = LS_RTE_SAVAL-I_AVVAL ).

    catch /VCXI/CX_CKX.
  endtry.

***----------------------------------------------------------------------------
*** Aspect - PC_AA_FLUTE
  try.
      clear LS_RTE_SAVAL.
      append initial line         to LS_RTE_SAVAL-I_AVVAL assigning <S_AVVAL>.
      move 'I'                    to <S_AVVAL>-SIGN.
      move 'EQ'                   to <S_AVVAL>-OPTION.
      move ME->F_FLUTE            to <S_AVVAL>-LOW.

      IR_RTE_PSTEP->SET_RTE_SAVAL( IF_IAATY  = C_IAATY_FLUTE
                                   II_AVVAL  = LS_RTE_SAVAL-I_AVVAL ).

    catch /VCXI/CX_CKX.
  endtry.

endmethod.
method ADD_LIMITS_TO_RTE_PSTEP.

  data:          LF_SCORE_NUM     type        I,
                 LF_LDUOM         type        /VCXI/VSMC_LDUOM,
                 LF_LDVAL         type        /VCXI/VSMR_LDVAL.
  data:          LS_SCORE         type        ZVCXI_P0SR_S_SCORE,
                 LS_SCORE_FIRST   type        ZVCXI_P0SR_S_SCORE,
                 LS_SCORE_MIN     type        ZVCXI_P0SR_S_SCORE.
  data:          LI_LVVAL         type        /VCXI/VSMR_TT_RTE_RANGE_LVVAL.
  data:          LT_LTYPE         type        /VCXI/CKX_TT_OBJECT.
  data:          LR_LTYPE         type ref to /VCXI/CL_VSMC_LTYPE,
                 LR_OBJECT        type ref to /VCXI/IF_CKX_OBJECT.
  field-symbols: <S_LVVAL>        type        /VCXI/VSMR_S_RTE_RANGE_LVVAL.

  SUPER->ADD_LIMITS_TO_RTE_PSTEP( IR_RTE_PSTEP = IR_RTE_PSTEP ).

***----------------------------------------------------------------------------
*** Ananlyze Scores
  loop at ME->T_SCORE into LS_SCORE where CSCOR eq 'L'.
*** Count Length Scores
    add 1 to LF_SCORE_NUM.

*** Get First and minimal Score
    case LF_SCORE_NUM.
      when 1.
        move LS_SCORE to LS_SCORE_FIRST.
      when 2.
        move LS_SCORE to LS_SCORE_MIN.
      when others.
        if LS_SCORE-SCORP lt LS_SCORE_MIN-SCORP.
          move LS_SCORE to LS_SCORE_MIN.
        endif.
    endcase.
  endloop.

***----------------------------------------------------------------------------
*** Get all linked Limits
  LT_LTYPE = ME->R_PSTEP->/VCXI/IF_VSMC_LINK_LTYPE~GET_LINKED_LTYPE( ).
  loop at LT_LTYPE into LR_OBJECT.
    move LR_OBJECT ?to LR_LTYPE.

    free: LF_LDVAL,
          LF_LDUOM,
          LI_LVVAL.

***----------------------------------------------------------------------------
    case LR_LTYPE->/VCXI/IF_CKX_OBJECT~GET_ID( ).
***   Sheet Length (Outbound)
      when 'P0_LT_DO_SHTLEN'.
        move ME->S_PCDIM_OUT-PCLEN       to LF_LDVAL.
        move ME->S_PCDIM_OUT-PCLEN_UOM   to LF_LDUOM.

***   Sheet Width (Outbound)
      when 'P0_LT_DO_SHTWID'.
        move ME->S_PCDIM_OUT-PCWID       to LF_LDVAL.
        move ME->S_PCDIM_OUT-PCWID_UOM   to LF_LDUOM.

***   Board ID
      when 'PC_LT_VP_BOARD'.
        append initial line to LI_LVVAL  assigning <S_LVVAL>.
        move 'I'                         to <S_LVVAL>-SIGN.
        move 'EQ'                        to <S_LVVAL>-OPTION.
        move ME->F_BOARD                 to <S_LVVAL>-LOW.

***   Flute Type
      when 'PC_LT_VP_FLUTE'.
        append initial line to LI_LVVAL  assigning <S_LVVAL>.
        move 'I'                         to <S_LVVAL>-SIGN.
        move 'EQ'                        to <S_LVVAL>-OPTION.
        move ME->F_FLUTE                 to <S_LVVAL>-LOW.

***   Score Type
      when 'PC_LT_VP_SCORE_TYPE'.
        if ME->T_SCORE is not initial.
          loop at ME->T_SCORE into LS_SCORE.
            append initial line to LI_LVVAL assigning <S_LVVAL>.
            move 'I'                       to <S_LVVAL>-SIGN.
            move 'EQ'                      to <S_LVVAL>-OPTION.
            move LS_SCORE-TSCOR            to <S_LVVAL>-LOW.
          endloop.
        else.
***       Not applicable Limit
          IR_RTE_PSTEP->SET_LIMIT_NA( IR_LTYPE = LR_LTYPE ).
          continue.
        endif.

***   Number of Scores
      when 'PC_LT_DP_SCORE_NUM'.
        if LF_SCORE_NUM is not initial.
          move LF_SCORE_NUM                to LF_LDVAL.
          move 'ST'                        to LF_LDUOM.
        else.
***       Not applicable Limit
          IR_RTE_PSTEP->SET_LIMIT_NA( IR_LTYPE = LR_LTYPE ).
          continue.
        endif.

***   Distance from edge of the sheet to the first score
      when 'PC_LT_DP_DISTANCESCTOKN'.
        if LS_SCORE_FIRST-ISCOR is not initial.
          move LS_SCORE_FIRST-SCORP      to LF_LDVAL.
          move LS_SCORE_FIRST-SCORP_UOM  to LF_LDUOM.
        else.
***       Not applicable Limit
          IR_RTE_PSTEP->SET_LIMIT_NA( IR_LTYPE = LR_LTYPE ).
          continue.
        endif.

***   Minimum Score Distance
      when 'PC_LT_DP_SCORE_DIST_MIN'.
        if LS_SCORE_MIN-ISCOR is not initial.
          move LS_SCORE_MIN-SCORP        to LF_LDVAL.
          move LS_SCORE_MIN-SCORP_UOM    to LF_LDUOM.
        else.
***       Not applicable Limit
          IR_RTE_PSTEP->SET_LIMIT_NA( IR_LTYPE = LR_LTYPE ).
          continue.
        endif.

      when others.
        continue.
    endcase.

***----------------------------------------------------------------------------
*** Set
    case LR_LTYPE->F_MLTYP.
      when /VCXI/CL_VSMC_LTYPE=>C_MLTYP_D.
        IR_RTE_PSTEP->SET_RTE_LDIM( IR_LTYPE = LR_LTYPE
                                    IF_LDVAL = LF_LDVAL
                                    IF_LDUOM = LF_LDUOM ).
      when /VCXI/CL_VSMC_LTYPE=>C_MLTYP_V.
        sort LI_LVVAL.
        delete adjacent duplicates from LI_LVVAL.

        IR_RTE_PSTEP->SET_RTE_LVAL( IR_LTYPE = LR_LTYPE
                                    II_LVVAL = LI_LVVAL ).
    endcase.
  endloop.

***----------------------------------------------------------------------------
*** Set Workcenter Restriction
  if ME->R_ARBPL is bound.
    clear IR_RTE_PSTEP->T_ARBPL.
    append ME->R_ARBPL to IR_RTE_PSTEP->T_ARBPL.
  endif.

endmethod.
method ADD_SPVRE_MC_STARCH.

  data:  LS_LEOKY            type        /VCXI/VKSR_S_LEOKY,
         LS_PARAM            type        /VCXI/VKSR_S_PARAM.
  data:  LR_SPVCE            type ref to /VCXI/CL_VKSC_SPVCE,
         LR_SPVRE_MC_STARCH  type ref to /VCXI/CL_VKSR_SPVRE.

***------------------------------------------------------------------------------------------------
*** Get Customizing Element
  LR_SPVCE = IR_BL_RTESPV->GET_SPVCE( IF_ID           = 'MC_STARCH'
                                      IR_SPVCE_PARENT = IR_SPVRE_ADDON->R_SPVCE ).

*** Create Supervisor Runtime Element
  create object LR_SPVRE_MC_STARCH
    exporting
      IR_SPVCE  = LR_SPVCE
      IR_PARENT = IR_SPVRE_ADDON.
  append LR_SPVRE_MC_STARCH to CT_SPVRE.

*** Set In Use
  LR_SPVRE_MC_STARCH->SET_INUSE( IF_INUSE = ABAP_TRUE ).

***------------------------------------------------------------------------------------------------
*** Set Material Number
  clear LS_LEOKY.
  move ME->S_MS_STARCH-MATNR                       to LS_LEOKY-LEOKY.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_LEOKY-SETBY.
  LR_SPVRE_MC_STARCH->SET_LEOKY( IS_LEOKY = LS_LEOKY ).

***------------------------------------------------------------------------------------------------
*** Set Average Consumption
  clear LS_PARAM.
  move ZCL_VCXI_PCSP_PR_MC_STARCH=>C_PARID_AVGCO_W to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC           to LS_PARAM-PARTY.
  move ME->S_MS_STARCH-AVGCO                       to LS_PARAM-ATFLV.
  move ME->S_MS_STARCH-AVGCO_UOM                   to LS_PARAM-UNIT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
  LR_SPVRE_MC_STARCH->SET_PARAM( IS_PARAM = LS_PARAM ).

*** Set Average Consumption Per
  clear LS_PARAM.
  move ZCL_VCXI_PCSP_PR_MC_STARCH=>C_PARID_AVGCP_A to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC           to LS_PARAM-PARTY.
  move ME->S_MS_STARCH-AVGCP                       to LS_PARAM-ATFLV.
  move ME->S_MS_STARCH-AVGCP_UOM                   to LS_PARAM-UNIT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
  LR_SPVRE_MC_STARCH->SET_PARAM( IS_PARAM = LS_PARAM ).

***------------------------------------------------------------------------------------------------
*** Set Starch Factor
  clear LS_PARAM.
  move ZCL_VCXI_PCSP_PR_MC_STARCH=>C_PARID_STARF   to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC           to LS_PARAM-PARTY.
  move ME->S_MS_STARCH-STARF                       to LS_PARAM-ATFLV.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
  LR_SPVRE_MC_STARCH->SET_PARAM( IS_PARAM = LS_PARAM ).

***------------------------------------------------------------------------------------------------
*** Set Purchase Flag
  clear LS_PARAM.
  move ZCL_VCXI_PCSP_PR_MC_STARCH=>C_PARID_PURCH   to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR           to LS_PARAM-PARTY.
  move ME->S_MS_STARCH-PURCH                       to LS_PARAM-ATWRT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
  LR_SPVRE_MC_STARCH->SET_PARAM( IS_PARAM = LS_PARAM ).

  check ME->S_MS_STARCH-PURCH eq ABAP_TRUE.

***------------------------------------------------------------------------------------------------
*** Set Price
  clear LS_PARAM.
  move ZCL_VCXI_PCSP_PR_MC_STARCH=>C_PARID_PRICE   to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC           to LS_PARAM-PARTY.
  move ME->S_MS_STARCH-PRICE                       to LS_PARAM-ATFLV.
  move ME->S_MS_STARCH-PRICE_CURR                  to LS_PARAM-CURKY.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
  LR_SPVRE_MC_STARCH->SET_PARAM( IS_PARAM = LS_PARAM ).

***------------------------------------------------------------------------------------------------
*** Set Price Unit
  clear LS_PARAM.
  move ZCL_VCXI_PCSP_PR_MC_STARCH=>C_PARID_PRICP_W  to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC            to LS_PARAM-PARTY.
  move ME->S_MS_STARCH-PRICP                        to LS_PARAM-ATFLV.
  move ME->S_MS_STARCH-PRICP_UOM                    to LS_PARAM-UNIT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC             to LS_PARAM-SETBY.
  LR_SPVRE_MC_STARCH->SET_PARAM( IS_PARAM = LS_PARAM ).

***------------------------------------------------------------------------------------------------
*** Set Vendor
  clear LS_PARAM.
  move ZCL_VCXI_PCSP_PR_MC_STARCH=>C_PARID_LIFNR    to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR            to LS_PARAM-PARTY.
  move ME->S_MS_STARCH-LIFNR                        to LS_PARAM-ATWRT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC             to LS_PARAM-SETBY.
  LR_SPVRE_MC_STARCH->SET_PARAM( IS_PARAM = LS_PARAM ).

endmethod.
method ADD_SPVRE_MFMI_PAPER.

  data:  LS_MS_PAPER         type        ZVCXI_PCSR_S_MS_PAPER,
         LS_LEOKY            type        /VCXI/VKSR_S_LEOKY,
         LS_PARAM            type        /VCXI/VKSR_S_PARAM.
  data:  LR_SPVCE            type ref to /VCXI/CL_VKSC_SPVCE,
         LR_SPVRE_MFMI_PAPER type ref to /VCXI/CL_VKSR_SPVRE.

***------------------------------------------------------------------------------------------------
*** Get Customizing Element
  LR_SPVCE = IR_BL_RTESPV->GET_SPVCE( IF_ID           = 'MFMI_PAPER'
                                      IR_SPVCE_PARENT = IR_SPVRE_MFMGR->R_SPVCE ).

  loop at ME->T_MS_PAPER into LS_MS_PAPER.

*** Create Supervisor Runtime Element
    create object LR_SPVRE_MFMI_PAPER
      exporting
        IR_SPVCE  = LR_SPVCE
        IR_PARENT = IR_SPVRE_MFMGR.
    append LR_SPVRE_MFMI_PAPER to CT_SPVRE.

*** Set In Use
    LR_SPVRE_MFMI_PAPER->SET_INUSE( IF_INUSE = ABAP_TRUE ).

***------------------------------------------------------------------------------------------------
*** Set Material Number
    clear LS_LEOKY.
    move LS_MS_PAPER-MATNR                            to LS_LEOKY-LEOKY.
    move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC             to LS_LEOKY-SETBY.
    LR_SPVRE_MFMI_PAPER->SET_LEOKY( IS_LEOKY = LS_LEOKY ).

***------------------------------------------------------------------------------------------------
*** Set Paper ID
    clear LS_PARAM.
    move ZCL_VCXI_XCSP_PR_MF=>C_PARID_IMFXC          to LS_PARAM-PARID.
    move /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR           to LS_PARAM-PARTY.
    move LS_MS_PAPER-BPAID                           to LS_PARAM-ATWRT.
    move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
    LR_SPVRE_MFMI_PAPER->SET_PARAM( IS_PARAM = LS_PARAM ).

***------------------------------------------------------------------------------------------------
*** Set Basis Weight
    clear LS_PARAM.
    move ZCL_VCXI_PCSP_PR_MFMI_PAPER=>C_PARID_BAWGT   to LS_PARAM-PARID.
    move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC            to LS_PARAM-PARTY.
    move LS_MS_PAPER-BAWGT                            to LS_PARAM-ATFLV.
    move LS_MS_PAPER-BAWGT_UOM                        to LS_PARAM-UNIT.
    move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC             to LS_PARAM-SETBY.
    LR_SPVRE_MFMI_PAPER->SET_PARAM( IS_PARAM = LS_PARAM ).

*** Set Basis Weight Per
    clear LS_PARAM.
    move ZCL_VCXI_PCSP_PR_MFMI_PAPER=>C_PARID_BAWGP_A  to LS_PARAM-PARID.
    move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC             to LS_PARAM-PARTY.
    move LS_MS_PAPER-BAWGP                             to LS_PARAM-ATFLV.
    move LS_MS_PAPER-BAWGP_UOM                         to LS_PARAM-UNIT.
    move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC              to LS_PARAM-SETBY.
    LR_SPVRE_MFMI_PAPER->SET_PARAM( IS_PARAM = LS_PARAM ).

*** Set Paper Width
    clear LS_PARAM.
    move ZCL_VCXI_PCSP_PR_MFMI_PAPER=>C_PARID_PAWID    to LS_PARAM-PARID.
    move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC             to LS_PARAM-PARTY.
    move ME->S_COWID-COWID                             to LS_PARAM-ATFLV.
    move ME->S_COWID-COWID_UOM                         to LS_PARAM-UNIT.
    move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC              to LS_PARAM-SETBY.
    LR_SPVRE_MFMI_PAPER->SET_PARAM( IS_PARAM = LS_PARAM ).

***------------------------------------------------------------------------------------------------
*** Set MFMI_PAPER Factor
    clear LS_PARAM.
    move ZCL_VCXI_PCSP_PR_MFMI_PAPER=>C_PARID_MFICF  to LS_PARAM-PARID.
    move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC           to LS_PARAM-PARTY.
    move LS_MS_PAPER-BPFAC                           to LS_PARAM-ATFLV.
    move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
    LR_SPVRE_MFMI_PAPER->SET_PARAM( IS_PARAM = LS_PARAM ).

***------------------------------------------------------------------------------------------------
*** Set Procurement Option
    clear LS_PARAM.
    move ZCL_VCXI_PCSP_PR_MFMI_PAPER=>C_PARID_PURCH   to LS_PARAM-PARID.
    move /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR            to LS_PARAM-PARTY.
    move LS_MS_PAPER-PURCH                            to LS_PARAM-ATWRT.
    move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC             to LS_PARAM-SETBY.
    LR_SPVRE_MFMI_PAPER->SET_PARAM( IS_PARAM = LS_PARAM ).

    check LS_MS_PAPER-PURCH ne ZIF_VCXI_XCSP_MFM=>C_PURCH_STOCK.

***------------------------------------------------------------------------------------------------
*** Set Text
    clear LS_PARAM.
    move ZCL_VCXI_PCSP_PR_MFMI_PAPER=>C_PARID_TEXT    to LS_PARAM-PARID.
    move /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR            to LS_PARAM-PARTY.
    move LS_MS_PAPER-TEXT                             to LS_PARAM-ATWRT.
    move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC             to LS_PARAM-SETBY.
    LR_SPVRE_MFMI_PAPER->SET_PARAM( IS_PARAM = LS_PARAM ).

*** Set Vendor Number
    clear LS_PARAM.
    move ZCL_VCXI_PCSP_PR_MFMI_PAPER=>C_PARID_LIFNR   to LS_PARAM-PARID.
    move /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR            to LS_PARAM-PARTY.
    move LS_MS_PAPER-LIFNR                            to LS_PARAM-ATWRT.
    move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC             to LS_PARAM-SETBY.
    LR_SPVRE_MFMI_PAPER->SET_PARAM( IS_PARAM = LS_PARAM ).

*** Set Price
    clear LS_PARAM.
    move ZCL_VCXI_PCSP_PR_MFMI_PAPER=>C_PARID_PRICE   to LS_PARAM-PARID.
    move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC            to LS_PARAM-PARTY.
    move LS_MS_PAPER-PRICE                            to LS_PARAM-ATFLV.
    move LS_MS_PAPER-PRICE_CURR                       to LS_PARAM-CURKY.
    move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC             to LS_PARAM-SETBY.
    LR_SPVRE_MFMI_PAPER->SET_PARAM( IS_PARAM = LS_PARAM ).

*** Set Price Unit
    clear LS_PARAM.
    move ZCL_VCXI_PCSP_PR_MFMI_PAPER=>C_PARID_PRICP_W  to LS_PARAM-PARID.
    move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC             to LS_PARAM-PARTY.
    move LS_MS_PAPER-PRICP                             to LS_PARAM-ATFLV.
    move LS_MS_PAPER-PRICP_UOM                         to LS_PARAM-UNIT.
    move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC              to LS_PARAM-SETBY.
    LR_SPVRE_MFMI_PAPER->SET_PARAM( IS_PARAM = LS_PARAM ).

  endloop.

endmethod.
method COMPARE.

  data:  LR_BL_PSTEP_CORR type ref to ZCL_VCXI_PCSR_BL_PSTEP_CORR.

*** Super Compare
  RF_SIMILAR = SUPER->COMPARE( IR_BL_PSTEP = IR_BL_PSTEP ).
  check RF_SIMILAR eq ABAP_TRUE.

  move IR_BL_PSTEP ?to LR_BL_PSTEP_CORR.

***----------------------------------------------------------------------------
*** PC Dimensions
  if ME->S_PCDIM_OUT eq LR_BL_PSTEP_CORR->S_PCDIM_OUT.
    move ABAP_TRUE  to RF_SIMILAR.
  else.
    move ABAP_FALSE to RF_SIMILAR.
    exit.
  endif.

***----------------------------------------------------------------------------
*** Board ID, Flute and Corrugation Width
  if ME->F_BOARD eq LR_BL_PSTEP_CORR->F_BOARD and
     ME->F_FLUTE eq LR_BL_PSTEP_CORR->F_FLUTE and
     ME->S_COWID eq LR_BL_PSTEP_CORR->S_COWID and
     ME->R_ARBPL eq LR_BL_PSTEP_CORR->R_ARBPL.
    move ABAP_TRUE  to RF_SIMILAR.
  else.
    move ABAP_FALSE to RF_SIMILAR.
    exit.
  endif.

***----------------------------------------------------------------------------
*** Paper and Starch
  if ME->T_MS_PAPER  eq LR_BL_PSTEP_CORR->T_MS_PAPER  and
     ME->S_MS_STARCH eq LR_BL_PSTEP_CORR->S_MS_STARCH.
    move ABAP_TRUE  to RF_SIMILAR.
  else.
    move ABAP_FALSE to RF_SIMILAR.
    exit.
  endif.

***----------------------------------------------------------------------------
*** Scores
  if ME->T_SCORE eq LR_BL_PSTEP_CORR->T_SCORE.
    move ABAP_TRUE  to RF_SIMILAR.
  else.
    move ABAP_FALSE to RF_SIMILAR.
    exit.
  endif.

endmethod.
method CONSTRUCTOR.

  SUPER->CONSTRUCTOR( IF_IPSTP   = IF_IPSTP
                      IF_DATE    = IF_DATE
                      IR_APPDATA = IR_APPDATA ).

endmethod.
method COPY.

  data:  LR_BL_PSTEP_CORR type ref to ZCL_VCXI_PCSR_BL_PSTEP_CORR.

*** Super Copy
  RR_BL_PSTEP = SUPER->COPY( ).
  move RR_BL_PSTEP ?to LR_BL_PSTEP_CORR.

***----------------------------------------------------------------------------
*** PC Dimensions
  move ME->S_PCDIM_OUT   to LR_BL_PSTEP_CORR->S_PCDIM_OUT.

***----------------------------------------------------------------------------
*** Board ID, Flute and Corrugation Width
  move ME->F_BOARD       to LR_BL_PSTEP_CORR->F_BOARD.
  move ME->F_FLUTE       to LR_BL_PSTEP_CORR->F_FLUTE.
  move ME->S_COWID       to LR_BL_PSTEP_CORR->S_COWID.
  move ME->R_ARBPL       to LR_BL_PSTEP_CORR->R_ARBPL.

***----------------------------------------------------------------------------
*** Paper and Starch
  move ME->T_MS_PAPER    to LR_BL_PSTEP_CORR->T_MS_PAPER.
  move ME->S_MS_STARCH   to LR_BL_PSTEP_CORR->S_MS_STARCH.

***----------------------------------------------------------------------------
*** Scores
  move ME->T_SCORE       to LR_BL_PSTEP_CORR->T_SCORE.

endmethod.
method DESERIALIZE.

  data:  LF_XML         type STRING,
         LF_GUID_ARBPL  type /VCXI/CKX_GUID.

  check IF_XML is not initial.

*** Deserialize
  call transformation ID
       source xml IF_XML
       result SUPER_XML    = LF_XML

              S_PCDIM_OUT  = ME->S_PCDIM_OUT

              F_BOARD      = ME->F_BOARD
              F_FLUTE      = ME->F_FLUTE
              S_COWID      = ME->S_COWID
              F_GUID_ARBPL = LF_GUID_ARBPL

              T_MS_PAPER   = ME->T_MS_PAPER
              S_MS_STARCH  = ME->S_MS_STARCH

              T_SCORE      = ME->T_SCORE.

  if LF_GUID_ARBPL is not initial.
    ME->R_ARBPL ?= /VCXI/CL_VSMC_ARBPL=>/VCXI/IF_CKX_OBJECT~GET_INSTANCE( IF_GUID = LF_GUID_ARBPL ).
  endif.

*** Super Deserialize
  SUPER->DESERIALIZE( IF_XML = LF_XML ).

endmethod.
method FILL_SPVRE_ADDON.

  SUPER->FILL_SPVRE_ADDON( exporting IR_BL_RTESPV   = IR_BL_RTESPV
                                     IR_SPVRE_ADDON = IR_SPVRE_ADDON
                                     IR_ARBPL       = IR_ARBPL
                           changing  CT_SPVRE       = CT_SPVRE ).

  case IR_SPVRE_ADDON->GET_ID( ).
    when 'PC_AT_CORR'.
      ME->FILL_SPVRE_ADDON_CORR( exporting IR_BL_RTESPV   = IR_BL_RTESPV
                                           IR_SPVRE_ADDON = IR_SPVRE_ADDON
                                 changing  CT_SPVRE       = CT_SPVRE ).
  endcase.

endmethod.
method FILL_SPVRE_ADDON_CORR.

  data:  LS_PARAM type /VCXI/VKSR_S_PARAM.

***------------------------------------------------------------------------------------------------
*** Set PC Type
  clear LS_PARAM.
  move ZCL_VCXI_PCSP_PR_ADDON_CORR=>C_PARID_PCTYP   to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR            to LS_PARAM-PARTY.
  move ME->S_PCDIM_OUT-PCTYP                        to LS_PARAM-ATWRT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC             to LS_PARAM-SETBY.
  IR_SPVRE_ADDON->SET_PARAM( IS_PARAM = LS_PARAM ).

***------------------------------------------------------------------------------------------------
*** Set Length
  clear LS_PARAM.
  move ZCL_VCXI_PCSP_PR_ADDON_CORR=>C_PARID_PCLEN  to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC           to LS_PARAM-PARTY.
  move ME->S_PCDIM_OUT-PCLEN                       to LS_PARAM-ATFLV.
  move ME->S_PCDIM_OUT-PCLEN_UOM                   to LS_PARAM-UNIT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
  IR_SPVRE_ADDON->SET_PARAM( IS_PARAM = LS_PARAM ).

*** Set Width
  clear LS_PARAM.
  move ZCL_VCXI_PCSP_PR_ADDON_CORR=>C_PARID_PCWID  to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC           to LS_PARAM-PARTY.
  move ME->S_PCDIM_OUT-PCWID                       to LS_PARAM-ATFLV.
  move ME->S_PCDIM_OUT-PCWID_UOM                   to LS_PARAM-UNIT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
  IR_SPVRE_ADDON->SET_PARAM( IS_PARAM = LS_PARAM ).

*** Set Height
  clear LS_PARAM.
  move ZCL_VCXI_PCSP_PR_ADDON_CORR=>C_PARID_PCHEI  to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC           to LS_PARAM-PARTY.
  move ME->S_PCDIM_OUT-PCHEI                       to LS_PARAM-ATFLV.
  move ME->S_PCDIM_OUT-PCHEI_UOM                   to LS_PARAM-UNIT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
  IR_SPVRE_ADDON->SET_PARAM( IS_PARAM = LS_PARAM ).

*** Set Ups
  clear LS_PARAM.
  move ZCL_VCXI_PCSP_PR_ADDON_CORR=>C_PARID_PCUPS  to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC           to LS_PARAM-PARTY.
  move ME->S_PCDIM_OUT-PCUPS                       to LS_PARAM-ATFLV.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
  IR_SPVRE_ADDON->SET_PARAM( IS_PARAM = LS_PARAM ).

*** Set PC Direction
  clear LS_PARAM.
  move ZCL_VCXI_PCSP_PR_ADDON_CORR=>C_PARID_PCDIR  to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR           to LS_PARAM-PARTY.
  move ME->S_PCDIM_OUT-PCDIR                       to LS_PARAM-ATWRT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
  IR_SPVRE_ADDON->SET_PARAM( IS_PARAM = LS_PARAM ).

***------------------------------------------------------------------------------------------------
*** Set Board ID
  clear LS_PARAM.
  move ZCL_VCXI_PCSP_PR_ADDON_CORR=>C_PARID_BOARD  to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR           to LS_PARAM-PARTY.
  move ME->F_BOARD                                 to LS_PARAM-ATWRT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
  IR_SPVRE_ADDON->SET_PARAM( IS_PARAM = LS_PARAM ).

*** Set Flute
  clear LS_PARAM.
  move ZCL_VCXI_PCSP_PR_ADDON_CORR=>C_PARID_FLUTE  to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_CHAR           to LS_PARAM-PARTY.
  move ME->F_FLUTE                                 to LS_PARAM-ATWRT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
  IR_SPVRE_ADDON->SET_PARAM( IS_PARAM = LS_PARAM ).

*** Set Corrugation Width
  clear LS_PARAM.
  move ZCL_VCXI_PCSP_PR_ADDON_CORR=>C_PARID_COWID  to LS_PARAM-PARID.
  move /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC           to LS_PARAM-PARTY.
  move ME->S_COWID-COWID                           to LS_PARAM-ATFLV.
  move ME->S_COWID-COWID_UOM                       to LS_PARAM-UNIT.
  move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC            to LS_PARAM-SETBY.
  IR_SPVRE_ADDON->SET_PARAM( IS_PARAM = LS_PARAM ).

***------------------------------------------------------------------------------------------------
*** Add Material Consumption for Starch
  ME->ADD_SPVRE_MC_STARCH( exporting IR_BL_RTESPV   = IR_BL_RTESPV
                                     IR_SPVRE_ADDON = IR_SPVRE_ADDON
                           changing  CT_SPVRE       = CT_SPVRE ).

endmethod.
method GET_ARBPL_INFO.

***------------------------------------------------------------------
*** Standard (Overrule existing)
  move 'GRP_CORR' to CF_ID_GRP.
  move 'CORR'     to CF_ID_ARBPL.

endmethod.
method GET_SPVRE_MF.

***------------------------------------------------------------------------------------------------
*** Add Paper
  ME->ADD_SPVRE_MFMI_PAPER( exporting IR_BL_RTESPV   = IR_BL_RTESPV
                                      IR_SPVRE_MFMGR = IR_SPVRE_MFMGR
                            changing  CT_SPVRE       = CT_SPVRE ).
  move ABAP_TRUE to CF_MFI.

***------------------------------------------------------------------------------------------------
*** Add MFSO PC
  ME->ADD_SPVRE_MFSO_P( exporting IR_BL_RTESPV   = IR_BL_RTESPV
                                  IR_SPVRE_MFMGR = IR_SPVRE_MFMGR
                        changing  CT_SPVRE       = CT_SPVRE ).
  move ABAP_TRUE to CF_MFO.

endmethod.
method INIT_BY_SPEC.

  SUPER->INIT_BY_SPEC( IR_GATE  = IR_GATE
                       IT_PRVDR = IT_PRVDR ).

***------------------------------------------------------------------------------------------------
*** Get Board and Flute
  ME->INIT_BY_SPEC_BOARD( IT_PRVDR = IT_PRVDR ).

*** Get PC Dimensions
  ME->INIT_BY_SPEC_PCDIM( IT_PRVDR = IT_PRVDR ).

*** Get Corrugatioin Width
  ME->INIT_BY_SPEC_COWID( IT_PRVDR = IT_PRVDR ).

***------------------------------------------------------------------------------------------------
*** Starch
  ME->INIT_BY_SPEC_STARCH( IT_PRVDR = IT_PRVDR ).

*** Paper
  ME->INIT_BY_SPEC_PAPER( IT_PRVDR = IT_PRVDR ).

***------------------------------------------------------------------------------------------------
*** Scores
  ME->INIT_BY_SPEC_SCORE( IT_PRVDR = IT_PRVDR ).

endmethod.
method INIT_BY_SPEC_BOARD.

  data:  LF_ARBPL          type        ARBPL,
         LF_GUID           type        /VCXI/CKX_GUID.
  data:  LR_GATE           type ref to /VCXI/CL_VKSR_GATE,
         LR_PR_BOARD       type ref to ZCL_VCXI_PCSS_PR_BOARD,
         LR_APPDATA        type ref to /VCXI/CL_VKSR_APPDATA.

***------------------------------------------------------------------------------------------------
*** Get Board Reference
  loop at IT_PRVDR into LR_GATE.
    try.
        move LR_GATE->R_CLSPR ?to LR_PR_BOARD.
        if LR_PR_BOARD is bound.
          exit.
        endif.
      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.
  endloop.
  check LR_PR_BOARD is bound.

***------------------------------------------------------------------------------------------------
  move LR_PR_BOARD->GET_BOARD( ) to ME->F_BOARD.
  move LR_PR_BOARD->GET_FLUTE( ) to ME->F_FLUTE.

***------------------------------------------------------------------------------------------------
*** Get Work Center
  move LR_GATE->GET_APPDATA( )   to LR_APPDATA.
  move LR_PR_BOARD->GET_ARBPL( ) to LF_ARBPL.
  if LF_ARBPL is not initial.
    LF_GUID = /VCXI/CL_VSMC_ARBPL=>ENCRYPT_GUID( IF_WERKS = LR_APPDATA->F_WERKS
                                                 IF_ARBPL = LF_ARBPL ).
    ME->R_ARBPL ?= /VCXI/CL_VSMC_ARBPL=>/VCXI/IF_CKX_OBJECT~GET_INSTANCE( IF_GUID = LF_GUID ).
  endif.

endmethod.
method INIT_BY_SPEC_COWID.

  data:  LS_BOARW          type        ZVCXI_PCSS_S_BOARW.
  data:  LR_GATE           type ref to /VCXI/CL_VKSR_GATE,
         LR_SELFD          type ref to ZIF_VCXI_PCSS_SELFD.

***------------------------------------------------------------------------------------------------
*** Get Sheet Reference
  loop at IT_PRVDR into LR_GATE.
    try.
        move LR_GATE->R_CLSPR ?to LR_SELFD.
        if LR_SELFD is bound.
          exit.
        endif.
      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.
  endloop.
  check LR_SELFD is bound.

***------------------------------------------------------------------------------------------------
*** Get PC Dimensions
  LR_SELFD->GET_BOARW( importing ES_BOARW = LS_BOARW ).
  move LS_BOARW-BOARW     to ME->S_COWID-COWID.
  move LS_BOARW-BOARW_UOM to ME->S_COWID-COWID_UOM.

endmethod.
method INIT_BY_SPEC_PAPER.

  data: LF_BPAID type        ZVCXI_PCS_BPAID.
  data: LS_BAWGT type        ZVCXI_PCS_S_BAWGT,
        LS_BAWGP type        ZVCXI_PCS_S_BAWGP.
  data: LR_GATE        type ref to /VCXI/CL_VKSR_GATE,
        LR_PR_MS_PAPER type ref to ZCL_VCXI_PCSS_PR_MS_PAPER,
        LR_PR_MS_PRPRP type ref to ZCL_VCXI_PCSS_PR_MS_PRPRP.
  field-symbols: <S_MS_PAPER>      type        ZVCXI_PCSR_S_MS_PAPER.

  loop at IT_PRVDR into LR_GATE.
    try.
        move LR_GATE->R_CLSPR ?to LR_PR_MS_PAPER.
        check LR_PR_MS_PAPER is bound.

        append initial line to ME->T_MS_PAPER assigning <S_MS_PAPER>.

        move LR_PR_MS_PAPER->GET_MATNR( ) to <S_MS_PAPER>-MATNR.
        move LR_PR_MS_PAPER->GET_BPAID( ) to <S_MS_PAPER>-BPAID.

        move LR_PR_MS_PAPER->GET_BAWGT( ) to LS_BAWGT.
        move-corresponding LS_BAWGT       to <S_MS_PAPER>.
        move LR_PR_MS_PAPER->GET_BAWGP( ) to LS_BAWGP.
        move-corresponding LS_BAWGP       to <S_MS_PAPER>.

        move LR_PR_MS_PAPER->GET_BPFAC( ) to <S_MS_PAPER>-BPFAC.

***     Purchasing Info
        move LR_PR_MS_PAPER->GET_PURCH( ) to <S_MS_PAPER>-PURCH.
        move LR_PR_MS_PAPER->GET_LIFNR( ) to <S_MS_PAPER>-LIFNR.
        move LR_PR_MS_PAPER->GET_PRICE( ) to <S_MS_PAPER>-S_PRICE.
        move LR_PR_MS_PAPER->GET_PRICP( ) to <S_MS_PAPER>-S_PRICP.

      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.
  endloop.

*** Replace Paper with Preprinted Paper
  loop at IT_PRVDR into LR_GATE.
    try.
        move LR_GATE->R_CLSPR ?to LR_PR_MS_PRPRP.
        check LR_PR_MS_PRPRP is bound.

        move LR_PR_MS_PRPRP->GET_BPAID( ) to LF_BPAID.

        read table ME->T_MS_PAPER assigning <S_MS_PAPER>
                   with key BPAID = LF_BPAID.
        check SY-SUBRC eq 0.

        move LR_PR_MS_PRPRP->GET_MATNR( ) to <S_MS_PAPER>-MATNR.

        move LR_PR_MS_PRPRP->GET_BAWGT( ) to LS_BAWGT.
        move-corresponding LS_BAWGT       to <S_MS_PAPER>.
        move LR_PR_MS_PRPRP->GET_BAWGP( ) to LS_BAWGP.
        move-corresponding LS_BAWGP       to <S_MS_PAPER>.

***     Purchasing Info
        move LR_PR_MS_PRPRP->GET_PURCH( ) to <S_MS_PAPER>-PURCH.
        move LR_PR_MS_PRPRP->GET_LIFNR( ) to <S_MS_PAPER>-LIFNR.
        move LR_PR_MS_PRPRP->GET_PRICE( ) to <S_MS_PAPER>-S_PRICE.
        move LR_PR_MS_PRPRP->GET_PRICP( ) to <S_MS_PAPER>-S_PRICP.

      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.
  endloop.

endmethod.
method INIT_BY_SPEC_PCDIM.

  data:  LR_GATE           type ref to /VCXI/CL_VKSR_GATE,
         LR_SHTDIM         type ref to ZIF_VCXI_P0SS_SHTDIM.

***------------------------------------------------------------------------------------------------
*** Get Sheet Dimension Reference
  loop at IT_PRVDR into LR_GATE.
    try.
        move LR_GATE->R_CLSPR ?to LR_SHTDIM.
        if LR_SHTDIM is bound.
          exit.
        endif.
      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.
  endloop.
  check LR_SHTDIM is bound.

***------------------------------------------------------------------------------------------------
*** Get PC Dimensions
  ME->S_PCDIM_OUT = LR_SHTDIM->GET_PCDIM( IF_POOTI = ZIF_VCXI_P0SS_SHTDIM=>C_POOTI_GROSS ).

*** Get PC Type and Direction
  ZCL_VCXI_P0SR_CUST_PSTEP=>GET_PSTEP_INFO( exporting IF_IPSTP = ME->F_IPSTP
                                            importing EF_PCTYP = ME->S_PCDIM_OUT-PCTYP
                                                      EF_PCDIR = ME->ZIF_VCXI_P0SR_PCDIR~F_PCDIR
                                                      EF_PCDOP = ME->ZIF_VCXI_P0SR_PCDIR~F_PCDOP ).

  if ME->ZIF_VCXI_P0SR_PCDIR~F_PCDIR is initial.
    move ME->S_PCDIM_OUT-PCDIR to ME->ZIF_VCXI_P0SR_PCDIR~F_PCDIR.
  endif.

*** Set Direction
  ME->ZIF_VCXI_P0SR_PCDIR~SET_PCDIR( IF_PCDIR = ME->ZIF_VCXI_P0SR_PCDIR~F_PCDIR ).

endmethod.
method INIT_BY_SPEC_SCORE.

  data:          LF_SSTRC         type        ZVCXI_P0SS_SSTRC,
                 LF_SCSTR         type        ZVCXI_P0SS_SCSTR,
                 LF_NSCOR         type        ZVCXI_P0SS_NSCOR.
  data:          LS_SCSTR_SCORP   type        ZVCXI_P0SS_S_SCSTR_SCORP,
                 LS_SCORP_PREV    type        ZVCXI_P0SS_S_SCORP,
                 LS_SCORP_ABS     type        ZVCXI_P0SS_S_SCORP.
  data:          LT_GATE_SCORE    type        /VCXI/VKSR_TT_GATE,
                 LT_SCSTR_SCORP   type        ZVCXI_P0SS_TT_SCSTR_SCORP.
  data:          LR_GATE          type ref to /VCXI/CL_VKSR_GATE,
                 LR_PR_SCORE      type ref to ZCL_VCXI_P0SS_PR_SCORE,
                 LR_PR_SCSTR      type ref to ZCL_VCXI_P0SS_PR_SCSTR,
                 LR_PR_SCSTR_MGR  type ref to ZCL_VCXI_P0SS_PR_SCSTR_MGR.
  field-symbols: <S_SCORE>        type        ZVCXI_P0SR_S_SCORE.

***----------------------------------------------------------------------------
*** Get Score String Calculation
  loop at IT_PRVDR into LR_GATE.
    try.
        move LR_GATE->R_CLSPR ?to LR_PR_SCSTR_MGR.
        if LR_PR_SCSTR_MGR is bound.
          exit.
        endif.
      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.
  endloop.
  check LR_PR_SCSTR_MGR is bound.

  LF_SSTRC = LR_PR_SCSTR_MGR->F_SSTRC.

***----------------------------------------------------------------------------
*** Get Tape Position of Score String
  loop at IT_PRVDR into LR_GATE.
    try.
        move LR_GATE->R_CLSPR ?to LR_PR_SCSTR.
        if LR_PR_SCSTR is bound.
          exit.
        endif.
      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.
  endloop.
  check LR_PR_SCSTR is bound.

  LF_SCSTR = LR_PR_SCSTR->GET_SCSTR( ).

***------------------------------------------------------------------------------------------------
*** Get Score Reference
  loop at IT_PRVDR into LR_GATE.
    try.
        move LR_GATE->R_CLSPR ?to LR_PR_SCORE.
        check LR_PR_SCORE is bound.

***     Save Score Gate
        append LR_GATE to LT_GATE_SCORE.

      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.
  endloop.

***----------------------------------------------------------------------------
*** Get absolute Score Positions of Score String
  LT_SCSTR_SCORP = ZCL_VCXI_P0SS_CUST_SCSTR=>GET_SCSTR_SCORP( IF_SCSTR       = LF_SCSTR
                                                              IF_SSTRC       = LF_SSTRC
                                                              IF_CSCOR       = ZCL_VCXI_P0SS_CUST_PRSTY=>C_CSCOR_LENGTH
                                                              IT_GATE_SCORE  = LT_GATE_SCORE
                                                              IF_UOM         = ME->S_PCDIM_OUT-PCLEN_UOM ).

***------------------------------------------------------------------------------------------------
*** Get Scores and Positions
  loop at LT_SCSTR_SCORP into LS_SCSTR_SCORP.
    add 1 to LF_NSCOR.
    append initial line to ME->T_SCORE assigning <S_SCORE>.
    move-corresponding LS_SCSTR_SCORP             to <S_SCORE>.
    move LF_NSCOR                                 to <S_SCORE>-NSCOR.
    move ZCL_VCXI_P0SS_CUST_PRSTY=>C_CSCOR_LENGTH to <S_SCORE>-CSCOR.
    concatenate <S_SCORE>-CSCOR <S_SCORE>-NSCOR into <S_SCORE>-ISCOR.
  endloop.


*** Calculate Relative Positions
  loop at ME->T_SCORE assigning <S_SCORE>.
*** Buffer Absolute Position
    move <S_SCORE>-SCORP     to LS_SCORP_ABS-SCORP.
    move <S_SCORE>-SCORP_UOM to LS_SCORP_ABS-SCORP_UOM.

    if SY-TABIX ne 1.
***   Calculate Relative to previous Position
      ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = <S_SCORE>-SCORP
                                                          IF_CALCV_UOM_1 = <S_SCORE>-SCORP_UOM
                                                          IF_CALCV_2     = LS_SCORP_PREV-SCORP
                                                          IF_CALCV_UOM_2 = LS_SCORP_PREV-SCORP_UOM
                                                          IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_SUBTRACT
                                                          IF_UOM         = <S_SCORE>-SCORP_UOM
                                                importing EF_CALCV       = <S_SCORE>-SCORP
                                                          EF_CALCV_UOM   = <S_SCORE>-SCORP_UOM ).
    endif.

*** Keep absolute position as "previous"
    move LS_SCORP_ABS to LS_SCORP_PREV.
  endloop.

endmethod.
method INIT_BY_SPEC_STARCH.

  data:  LS_AVGCO          type        ZVCXI_XCS_S_AVGCO,
         LS_AVGCP          type        ZVCXI_XCS_S_AVGCP.
  data:  LR_GATE           type ref to /VCXI/CL_VKSR_GATE,
         LR_PR_MS_STARCH   type ref to ZCL_VCXI_PCSS_PR_MS_STARCH.

***------------------------------------------------------------------------------------------------
*** Get Starch Reference
  loop at IT_PRVDR into LR_GATE.
    try.
        move LR_GATE->R_CLSPR ?to LR_PR_MS_STARCH.
        if LR_PR_MS_STARCH is bound.
          exit.
        endif.
      catch CX_SY_MOVE_CAST_ERROR.
        continue.
    endtry.
  endloop.
  check LR_PR_MS_STARCH is bound.

***------------------------------------------------------------------------------------------------
*** Get Starch Info
  move LR_PR_MS_STARCH->GET_MATNR( ) to ME->S_MS_STARCH-MATNR.

  move LR_PR_MS_STARCH->GET_AVGCO( ) to LS_AVGCO.
  move-corresponding LS_AVGCO to ME->S_MS_STARCH.
  move LR_PR_MS_STARCH->GET_AVGCP( ) to LS_AVGCP.
  move-corresponding LS_AVGCP to ME->S_MS_STARCH.

  move LR_PR_MS_STARCH->GET_STARF( ) to ME->S_MS_STARCH-STARF.

*** Purchasing Info
  move LR_PR_MS_STARCH->GET_PURCH( ) to ME->S_MS_STARCH-PURCH.
  move LR_PR_MS_STARCH->GET_LIFNR( ) to ME->S_MS_STARCH-LIFNR.
  move LR_PR_MS_STARCH->GET_PRICE( ) to ME->S_MS_STARCH-S_PRICE.
  move LR_PR_MS_STARCH->GET_PRICP( ) to ME->S_MS_STARCH-S_PRICP.

endmethod.
method SERIALIZE.

  data:  LF_GUID_ARBPL  type /VCXI/CKX_GUID.

*** Super Serialize
  RF_XML = SUPER->SERIALIZE( ).

  if ME->R_ARBPL is bound.
    move ME->R_ARBPL->/VCXI/IF_CKX_OBJECT~GET_GUID( ) to LF_GUID_ARBPL.
  endif.

*** Serialize
  call transformation ID
       source SUPER_XML    = RF_XML

              S_PCDIM_OUT  = ME->S_PCDIM_OUT

              F_BOARD      = ME->F_BOARD
              F_FLUTE      = ME->F_FLUTE
              S_COWID      = ME->S_COWID
              F_GUID_ARBPL = LF_GUID_ARBPL

              T_MS_PAPER   = ME->T_MS_PAPER
              S_MS_STARCH  = ME->S_MS_STARCH

              T_SCORE      = ME->T_SCORE

       result xml RF_XML.

endmethod.
method ZIF_VCXI_P0SR_ADJ_PCDIM~CALC_PCDIM_IN.
endmethod.
method ZIF_VCXI_P0SR_ADJ_PCDIM~CALC_PCDIM_OUT.

  append ME->S_PCDIM_OUT to CT_PCDIM.

endmethod.
method ZIF_VCXI_P0SR_CONV_ATP~CONV_PC_TO_AREA.

  data:          LF_NOUPW     type F.
  data:          LS_PCARE     type ZVCXI_P0SP_S_PCARE.
  field-symbols: <S_AREADIM>  type ZVCXI_P0SP_S_AREADIM.

***------------------------------------------------------------------------------------------------
  if ME->S_COWID-COWID is initial.
*** Calculate Single Sheet Area
    ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = ME->S_PCDIM_OUT-PCLEN
                                                        IF_CALCV_UOM_1 = ME->S_PCDIM_OUT-PCLEN_UOM
                                                        IF_CALCV_2     = ME->S_PCDIM_OUT-PCWID
                                                        IF_CALCV_UOM_2 = ME->S_PCDIM_OUT-PCWID_UOM
                                                        IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_MULTIPLY
                                              importing EF_CALCV       = LS_PCARE-PCARE
                                                        EF_CALCV_UOM   = LS_PCARE-PCARE_UOM ).

    append initial line to RT_AREADIM assigning <S_AREADIM>.
    move-corresponding LS_PCARE              to <S_AREADIM>.
    move               ME->S_PCDIM_OUT-PCUPS to <S_AREADIM>-PCUPS.


***------------------------------------------------------------------------------------------------
  else.
    check ME->S_PCDIM_OUT-PCWID is not initial.

*** Check how many times the Sheet fits in the Corrugation Width
    ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = ME->S_COWID-COWID
                                                        IF_CALCV_UOM_1 = ME->S_COWID-COWID_UOM
                                                        IF_CALCV_2     = ME->S_PCDIM_OUT-PCWID
                                                        IF_CALCV_UOM_2 = ME->S_PCDIM_OUT-PCWID_UOM
                                                        IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_DIVIDE
                                              importing EF_CALCV       = LF_NOUPW ).

*** Calculate Corrugation Area (Multi-Sheet + Siderun)
    ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = ME->S_PCDIM_OUT-PCLEN
                                                        IF_CALCV_UOM_1 = ME->S_PCDIM_OUT-PCLEN_UOM
                                                        IF_CALCV_2     = ME->S_COWID-COWID
                                                        IF_CALCV_UOM_2 = ME->S_COWID-COWID_UOM
                                                        IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_MULTIPLY
                                              importing EF_CALCV       = LS_PCARE-PCARE
                                                        EF_CALCV_UOM   = LS_PCARE-PCARE_UOM ).

    append initial line to RT_AREADIM assigning <S_AREADIM>.
    move-corresponding LS_PCARE to <S_AREADIM>.
    <S_AREADIM>-PCUPS = ME->S_PCDIM_OUT-PCUPS * floor( LF_NOUPW ).

  endif.

endmethod.
method ZIF_VCXI_P0SR_PCDIR~SET_PCDIR.

  move IF_PCDIR to ME->ZIF_VCXI_P0SR_PCDIR~F_PCDIR.

*** Turn PC Dimension OUT
  ME->S_PCDIM_OUT = ME->TURN_PCDIM( IS_PCDIM = ME->S_PCDIM_OUT
                                    IF_PCDIR = IF_PCDIR ).

endmethod.
method ZIF_VCXI_PCSR_ADJ_FLUTE~ADJ_FLUTE.

  move ME->F_FLUTE to CF_FLUTE.

endmethod.
