
class ZCL_VCXI_FFSS_ND_EXTR definition
  public
  inheriting from ZCL_VCXI_F0SS_ND
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
    raising
      /VCXI/CX_CKX .
protected section.

  methods INIT_NODE
    redefinition .
private section.
endclass. "ZCL_VCXI_FFSS_ND_EXTR definition
class ZCL_VCXI_FFSS_ND_EXTR implementation.
  method CONSTRUCTOR.

*** call SUPER CONSTRUCTOR
    SUPER->CONSTRUCTOR( IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE ).

  endmethod.
  method INIT_NODE.

    data: LF_TFILM type        ZVCXI_F0S_TFILM,
          LF_FL    type        NETVALSFL.
    data: LS_FITIN type        ZVCXI_FFS_S_FITIN,
          LS_FIWID type        ZVCXI_F0S_S_FIWID.
    data:           LR_EXTR             type ref to ZCL_VCXI_FFSS_PR_EXTR.
    field-symbols:  <S_NVAL>            type        YS_NVALS.

***----------------------------------------------------------------------------
*** Call Super
    SUPER->INIT_NODE( ).

***----------------------------------------------------------------------------
*** Check Input Gate is EXTR
    try.
        move ME->R_GATE->R_CLSPR ?to LR_EXTR.
      catch CX_SY_MOVE_CAST_ERROR.
        clear LR_EXTR.
    endtry.

    check LR_EXTR is bound.

*** Initialize Node Values from Gate
    move 1 to ME->F_NETTYPE.

***----------------------------------------------------------------------------
*** Set Node Description
    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = 0.
    <S_NVAL>-VAL  = LR_EXTR->GET_DESCR( ).
*** Get Film Type
    LR_EXTR->GET_TFILM( importing EF_TFILM = LF_TFILM ).
*** Set 'Extrusion' + Film Type
    concatenate <S_NVAL>-VAL LF_TFILM into <S_NVAL>-VAL separated by ' - '.

*** Left out Number '1' for Icon Field
    move 1 to LF_FL.

*** Get Film Width (Outbound)
    add 1 to LF_FL.
    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = LF_FL.
    move text-D02   to <S_NVAL>-VAL.

    add 1 to LF_FL.
    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = LF_FL.
    LR_EXTR->ZIF_VCXI_FFSS_COMM_NEXT~GET_FIWID( importing ES_FIWID = LS_FIWID ).
    <S_NVAL>-VAL = ME->CONV_ANY_TO_VAL( IF_VALUE = LS_FIWID-FIWID
                                        IF_UOM   = LS_FIWID-FIWID_UOM ).

*** Get Internal Film Thickness (Inbound)
    add 1 to LF_FL.
    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = LF_FL.
    move text-D03   to <S_NVAL>-VAL.

    add 1 to LF_FL.
    append initial line to ME->T_NVALS assigning <S_NVAL>.
    <S_NVAL>-FL   = LF_FL.
    LR_EXTR->GET_FITIN( importing ES_FITIN = LS_FITIN ).
    <S_NVAL>-VAL = ME->CONV_ANY_TO_VAL( IF_VALUE = LS_FITIN-FITIN
                                        IF_UOM   = LS_FITIN-FITIN_UOM ).

*** Set Status Icon
    ME->SET_STATUS_ICON( ).

  endmethod.
endclass. "ZCL_VCXI_FFSS_ND_EXTR implementation
