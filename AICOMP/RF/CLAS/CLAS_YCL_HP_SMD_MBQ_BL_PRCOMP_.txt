
class YCL_HP_SMD_MBQ_BL_PRCOMP definition
  public
  inheriting from ZCL_VCXI_P4M_MBQ_BL_PRCOMP
  final
  create public .

public section.

  constants C_ISVCE_CORRDETAILS type ZVCXI_XCS_ISVCE value 'CRPRDTL' ##NO_TEXT.

  methods FILL_IDAT
    redefinition .
protected section.

  methods ADJUST_ACTDAT_MAT_MARA
    importing
      !IR_ACTDAT type ref to DATA
      !IR_SVCEL type ref to /VCXI/CL_VKSRA_SVC_ELEMENT
      !IF_SOKEY type ZVCXI_XCM_MBQ_SOKEY
    raising
      /VCXI/CX_CKX .
  methods GET_ACTDAT_MLI
    importing
      !IR_SVCEL_SOURCE type ref to /VCXI/CL_VKSRA_SVC_ELEMENT
      !IF_SOKEY type ZVCXI_XCM_MBQ_SOKEY
      !IF_WERKS type WERKS_D
      !IF_DISST type /VCXI/VKC_DISST
    exporting
      !EF_SKIP type ABAP_BOOL
    returning
      value(RR_ACTDAT) type ref to DATA
    raising
      /VCXI/CX_CKX .
  methods ADJUST_ACTDAT_MAT_MARM
    importing
      !IR_ACTDAT type ref to DATA
      !IR_SVCEL type ref to /VCXI/CL_VKSRA_SVC_ELEMENT
      !IF_SOKEY type ZVCXI_XCM_MBQ_SOKEY
    raising
      /VCXI/CX_CKX .

  methods GET_ACTDAT
    redefinition .
  methods GET_ACTDAT_MAT
    redefinition .
  methods GET_ACTDAT_PURIR
    redefinition .
  methods GET_MATDAT_MARM
    redefinition .
  private section.
endclass. "YCL_HP_SMD_MBQ_BL_PRCOMP definition
class YCL_HP_SMD_MBQ_BL_PRCOMP implementation.
  method ADJUST_ACTDAT_MAT_MARA.

    data: LF_CSPEC type YHP_SVCS_CSPEC.
    data: LS_PARAM type /VCXI/VKSR_S_PARAM.
    data: LT_SVCEL type /VCXI/CKX_TT_OBJECT.
    data: LR_SVCEL    type ref to /VCXI/CL_VKSRA_SVC_ELEMENT,
          LR_EA_PRODI type ref to YCL_HP_SVCS_EA_PRODI.
    field-symbols: <S_ACTDAT_MAT> type ZVCXI_XCM_MBQ_S_ACTDAT_MAT.

    check IR_ACTDAT is bound.
    assign IR_ACTDAT->* to <S_ACTDAT_MAT>.

*** Sales Division
    <S_ACTDAT_MAT>-S_MATDAT_MARA-SPART = ME->R_BOB_SDOC->S_VBAK-SPART.

*** Product Hierarchy (Do for all except tools)
    if IF_SOKEY ne ME->C_SOKEY_TOOL_DCT and
       IF_SOKEY ne ME->C_SOKEY_TOOL_SET_PRINT.

***   If its not an own procurement component, use only SVC Element itself
      if ME->R_SVCEL_XCOMP->/VCXI/IF_CKX_OBJECT~GET_ID( ) ne 'COMP_P4'.
        LT_SVCEL = value #( ( IR_SVCEL ) ).
      endif.

      <S_ACTDAT_MAT>-S_MATDAT_MARA-PRDHA = YCL_HP_X_SRV_PRODH=>GET_PRODH( IR_SVCEL_XCOMP = ME->R_SVCEL_XCOMP
                                                                          IF_MTART       = <S_ACTDAT_MAT>-S_MATDAT_MARA-MATL_TYPE
                                                                          IF_SOKEY       = IF_SOKEY
                                                                          IR_SVC         = ME->R_BOB_SDOC->GET_SVC( IF_POSNR = ME->F_POSNR )
                                                                          IT_SVCEL       = LT_SVCEL ).
    endif.

*** Get Area and Weight
    LR_SVCEL = YCL_HP_SVC_SRV_VE=>GET_SVCEL_RELATED_CHILD( IF_SVCEL_ID = C_ISVCE_AREA_WGT
                                                           IR_SVCEL    = IR_SVCEL ).
    if LR_SVCEL is bound.

***   Net Blank Weight -> Net Weight
      LS_PARAM = LR_SVCEL->GET_PARAM( IF_PARID = 'ZVCXI_PX_NBLWT' ).
      <S_ACTDAT_MAT>-S_MATDAT_MARA-NTGEW = LS_PARAM-ATFLV.

***   Sold Blank Weight -> Gross Weight
      LS_PARAM = LR_SVCEL->GET_PARAM( IF_PARID = 'ZVCXI_PX_SBLWT' ).
      <S_ACTDAT_MAT>-S_MATDAT_MARA-BRGEW = LS_PARAM-ATFLV.
      <S_ACTDAT_MAT>-S_MATDAT_MARA-GEWEI = LS_PARAM-UNIT.

      free LR_SVCEL.

    endif.

*** Legacy Material
    try.
        if ME->R_BOB_SDOC->S_VBAK-AUART eq 'ZMI'.
          <S_ACTDAT_MAT>-S_MATDAT_MARA-BISMT = value #( ME->R_BOB_SDOC->T_VBKD[ POSNR = ME->F_POSNR ]-BSTKD ).
        endif.
      catch CX_SY_ITAB_LINE_NOT_FOUND.
    endtry.

  endmethod.
  method ADJUST_ACTDAT_MAT_MARM.

    data: LS_PROLE type	ZVCXI_P0S_S_PROLE,
          LS_PROHE type	ZVCXI_P0S_S_PROHE,
          LS_PROWI type	ZVCXI_P0S_S_PROWI,
          LS_MARM  type MARM.

    data: LR_SVCEL        type ref to /VCXI/CL_VKSRA_SVC_ELEMENT.
    data: LS_SHTLE type ZVCXI_P0SS_S_SHTLE,
          LS_SHTWI type ZVCXI_P0SS_S_SHTWI,
          LS_PARAM type /VCXI/VKSR_S_PARAM.


    field-symbols: <S_ACTDAT_MAT>  type ZVCXI_XCM_MBQ_S_ACTDAT_MAT,
                   <S_MATDAT_MARM> type ZVCXI_XCM_MBQ_S_MATDAT_MARM.

    check IR_ACTDAT is bound.
    assign IR_ACTDAT->* to <S_ACTDAT_MAT>.


*** Get CFG Info
    LR_SVCEL  = YCL_HP_SVC_SRV_VE=>GET_SVCEL_RELATED( IF_SVCEL_ID = C_ISVCE_CORRDETAILS
                                                      IR_SVCEL    = IR_SVCEL ).
    check LR_SVCEL is not initial.

    LS_PARAM =  LR_SVCEL->GET_PARAM( 'ZVCXI_P0_PROHE' ).
    LS_PROHE-PROHE = LS_PARAM-ATFLV.
    LS_PROHE-PROHE_UOM = LS_PARAM-UNIT.
    clear LS_PARAM.

    LS_PARAM =  LR_SVCEL->GET_PARAM( 'ZVCXI_P0_PROLE' ).
    LS_PROLE-PROLE = LS_PARAM-ATFLV.
    LS_PROLE-PROLE_UOM = LS_PARAM-UNIT.
    clear LS_PARAM.

    LS_PARAM =  LR_SVCEL->GET_PARAM( 'ZVCXI_P0_PROWI' ).
    LS_PROWI-PROWI = LS_PARAM-ATFLV.
    LS_PROWI-PROWI_UOM = LS_PARAM-UNIT.
    clear LS_PARAM.


*Only one unit of measurement will be maintain in the template
    select single MEINH UMREZ UMREN BRGEW
    into corresponding fields of LS_MARM
    from MARM
    where MATNR eq <S_ACTDAT_MAT>-TMATN.

    append value #( MEINH = LS_MARM-MEINH
                    UMREZ = LS_MARM-UMREZ
                    UMREN = LS_MARM-UMREN
                    LAENG = LS_PROLE-PROLE
                    BREIT = LS_PROWI-PROWI
                    HOEHE = LS_PROHE-PROHE
                    MEABM = LS_PROHE-PROHE_UOM
                    BRGEW = LS_MARM-BRGEW  ) to <S_ACTDAT_MAT>-T_MATDAT_MARM assigning <S_MATDAT_MARM>.
  endmethod.
  method FILL_IDAT.
    data:          LS_SVCEL type ZVCXI_XCI_S_SVCEL.
    field-symbols: <S_SVCPA> type ZVCXI_XCI_S_SVCPA.
    SUPER->FILL_IDAT(  IF_IPARE       = IF_IPARE
                       IF_CPARE       = IF_CPARE
                       IR_SVC         = IR_SVC
                       IR_IDAT_SVC    = IR_IDAT_SVC
                       IR_CUELMNT_SOV = IR_CUELMNT_SOV
                       IT_MATLI       = IT_MATLI
                       IT_DMSLI       =  IT_DMSLI ).

*** Set incomplete pallet flag to calculated for FG
    read table IR_IDAT_SVC->T_SVCEL into LS_SVCEL
    with key IPARE = ZCL_VCXI_XCSU_PR_SHIPMGR=>C_IPRVT_XCS_SHIPMGR
             CPARE = IF_CPARE ISELF = ZCL_VCXI_XCSU_PR_SHIPINFO=>C_IPRVT_XCS_SHIPINFO.
    if SY-SUBRC eq 0.
      read table LS_SVCEL-T_SVCPA assigning <S_SVCPA>
      with key PARID = YCL_HP_SVCU_PR_SHIPINFO=>C_PARID_INCPA
               SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER.
      if SY-SUBRC eq 0.
        move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC to  <S_SVCPA>-SETBY.
        IR_IDAT_SVC->UPDATE_SVCEL( IS_SVCEL = LS_SVCEL ).
      endif.
    endif.
  endmethod.
  method GET_ACTDAT.

    SUPER->GET_ACTDAT( exporting IF_ACTID        = IF_ACTID
                                 IR_SVCEL_SOURCE = IR_SVCEL_SOURCE
                                 IF_SOKEY        = IF_SOKEY
                                 IF_WERKS        = IF_WERKS
                                 IF_DISST        = IF_DISST
                       importing EF_SKIP         = EF_SKIP
                       receiving RR_ACTDAT       = RR_ACTDAT ).

***-------------------------------------------------------------------*
*** Handle additional activity control IDs
    case IF_ACTID.
      when YCL_HP_SMD_MBQ_CUST=>C_ACTID_MLI.  " Material List (Inclusion/Exclusion)
        RR_ACTDAT = ME->GET_ACTDAT_MLI( exporting IR_SVCEL_SOURCE = IR_SVCEL_SOURCE
                                                  IF_SOKEY        = IF_SOKEY
                                                  IF_WERKS        = IF_WERKS
                                                  IF_DISST        = IF_DISST
                                        importing EF_SKIP         = EF_SKIP ).
      when others.
        return.
    endcase.

  endmethod.
  method GET_ACTDAT_MAT.

    RR_ACTDAT = SUPER->GET_ACTDAT_MAT( exporting  IR_SVCEL_SOURCE = IR_SVCEL_SOURCE
                                                  IF_SOKEY        = IF_SOKEY
                                                  IF_WERKS        = IF_WERKS
                                                  IF_DISST        = IF_DISST
                                       importing  EF_SKIP         = EF_SKIP ).

***-------------------------------------------------------------------*
*** Adjust Material Data
    ME->ADJUST_ACTDAT_MAT_MARA( IR_ACTDAT = RR_ACTDAT
                                IR_SVCEL  = IR_SVCEL_SOURCE
                                IF_SOKEY  = IF_SOKEY ).

*JBL 02/11/2022 Adjust MARM
*** Adjust Material Data
    ME->ADJUST_ACTDAT_MAT_MARM( IR_ACTDAT = RR_ACTDAT
                                IR_SVCEL  = IR_SVCEL_SOURCE
                                IF_SOKEY  = IF_SOKEY ).
  endmethod.
  method GET_ACTDAT_MLI.

    RR_ACTDAT = YCL_HP_SMD_MBQ_SRV_BL_XCOMP=>GET_ACTDAT_MLI( exporting IR_SVCEL_SOURCE = IR_SVCEL_SOURCE
                                                                       IF_SOKEY        = IF_SOKEY
                                                                       IF_WERKS        = IF_WERKS
                                                                       IF_DISST        = IF_DISST
                                                             importing EF_SKIP         = EF_SKIP ).


  endmethod.
  method GET_ACTDAT_PURIR.
    data: LF_LAND1_L type LAND1_GP,
          LF_LAND1_T type LAND1,
          LF_BUKRS   type BUKRS,
          LF_STCD1   type STCD1,
          LF_XEGLD   type XEGLD.

    field-symbols: <S_ACTDAT_PURIR> type ZVCXI_XCM_MBQ_S_ACTDAT_PURIR.

*    call method SUPER->GET_ACTDAT_PURIR
*      exporting
*        IR_SVCEL_SOURCE = IR_SVCEL_SOURCE
*        IF_SOKEY        = IF_SOKEY
*        IF_WERKS        = IF_WERKS
*        IF_DISST        = IF_DISST
*      importing
*        EF_SKIP         = EF_SKIP
*      receiving
*        RR_ACTDAT       = RR_ACTDAT.


*** Call SUPER to create Data Reference
    RR_ACTDAT = SUPER->GET_ACTDAT_PURIR( exporting IR_SVCEL_SOURCE = IR_SVCEL_SOURCE
                                                   IF_SOKEY        = IF_SOKEY
                                                   IF_WERKS        = IF_WERKS
                                                   IF_DISST        = IF_DISST
                                         importing EF_SKIP         = EF_SKIP ).
    check RR_ACTDAT is bound.
    assign RR_ACTDAT->* to <S_ACTDAT_PURIR>.
***    Add Purchase Organization Vendor
    if <S_ACTDAT_PURIR>-LIFNR is not initial.
      select single EKORG into <S_ACTDAT_PURIR>-EKORG
        from LFM1
        where LIFNR = <S_ACTDAT_PURIR>-LIFNR.
    endif.

***GAPVC17 EESPINASSE 05062022 Add Taxe Code
    if <S_ACTDAT_PURIR>-LIFNR is not initial.
*1.	Read vendor country code – LFA1.LAND1
      select single LAND1 STCD1 into ( LF_LAND1_L, LF_STCD1 )
        from LFA1
        where LIFNR = <S_ACTDAT_PURIR>-LIFNR.
*2.	Read company code country code – T001.LAND1
      select single T001K~BUKRS into LF_BUKRS
       from T001W
       inner join T001K
             on T001K~BWKEY = T001W~BWKEY
        where WERKS = IF_WERKS.
      select single LAND1 from T001 into LF_LAND1_T
        where BUKRS = LF_BUKRS.
*3.	Compare LFA1.LAND1 and T001.LAND1
      if LF_LAND1_L = LF_LAND1_T.
*a.	If the values are the same --> Vendor is National provider
*   i.      Check first character of the Vendor Tax Code LFA1-STCD1
        if LF_STCD1(1) = 'N'.
*     1.  If it starts with “N”, then set EINE.MWSKZ value as “B2”
          <S_ACTDAT_PURIR>-MWSKZ = 'B2'.
        else.
*     2.  Else (all other values), then set EINE.MWSKZ value to “S3”
          <S_ACTDAT_PURIR>-MWSKZ = 'S3'.
        endif.
      else.
*b.	If the values are different --> check for European or International
*  i.      Read T005.XEGLD (Indicator: European Union Member?), and check its value:
        select single XEGLD from T005 into LF_XEGLD
          where LAND1 = LF_LAND1_L.
*     1.  If T005.XEGLD equal “X”, then vendor is European therefore set EINE.MWSKZ to “A3”
        if LF_XEGLD = ABAP_TRUE.
          <S_ACTDAT_PURIR>-MWSKZ = 'A3'.
        else.
*     2.  If T005.XEGLD is blank, the vendor is International therefore set EINE.MWSKZ to “I3”
          <S_ACTDAT_PURIR>-MWSKZ = 'I3'.
        endif.
      endif.
    endif.
  endmethod.
  method GET_MATDAT_MARM.
**try.
*CALL METHOD SUPER->GET_MATDAT_MARM
*  EXPORTING
*    IR_SVCEL_SOURCE =
*  RECEIVING
*    RT_MATDAT_MARM  =
*    .
** catch /VCXI/CX_CKX .
**endtry.
*JBL 31/10/2022 Save MARM data for Purchased
    data: LS_PARAM type /VCXI/VKSR_S_PARAM,
          LS_CALCV type ZVCXI_XCS_S_CALCV.
    data: LR_SVCEL    type ref to /VCXI/CL_VKSRA_SVC_ELEMENT.

    field-symbols: <S_MATDAT_MARM> type ZVCXI_XCM_MBQ_S_MATDAT_MARM.

*{   REPLACE        HEDK925687
***    SUPER->GET_MATDAT_MARM( exporting IR_SVCEL_SOURCE     = IR_SVCEL_SOURCE
***                            receiving RT_MATDAT_MARM      = RT_MATDAT_MARM ).
    SUPER->GET_MATDAT_MARM( exporting IR_SVCEL_SOURCE     = IR_SVCEL_SOURCE
                                      IF_TMATN            = IF_TMATN
                            receiving RT_MATDAT_MARM      = RT_MATDAT_MARM ).
*}   REPLACE

*** Get Element API Reference
    data(LR_CLSEA) = IR_SVCEL_SOURCE->GET_CLSEA( ).
    check LR_CLSEA is bound.
*** Type CFG Finished good
    try.
*** Get Area and Weight
        LR_SVCEL = YCL_HP_SVC_SRV_VE=>GET_SVCEL_RELATED_CHILD( IF_SVCEL_ID = C_ISVCE_AREA_WGT
                                                               IR_SVCEL    = IR_SVCEL_SOURCE ).
        if LR_SVCEL is bound.
*** Sold Blank Area
          LS_PARAM = LR_SVCEL->GET_PARAM( IF_PARID = 'ZVCXI_PX_SBLAR' ).

          if LS_PARAM-ATFLV is not initial and LS_PARAM-UNIT is not initial.
***       Add Conversion to M2 for Semi finished good
            append value #( MEINH = LS_PARAM-UNIT ) to RT_MATDAT_MARM assigning <S_MATDAT_MARM>.
            LS_CALCV-CALCV = LS_PARAM-ATFLV.
            LS_CALCV-CALCV_UOM = LS_PARAM-UNIT.
            ZCL_VCXI_XCS_SERVICE_UNIT=>CALC_UMREZ_UMREN( exporting IS_CALCV_BASE = value #( CALCV     = 1
                                                                                            CALCV_UOM = 'PC' )
                                                                   IS_CALCV_ALT  = LS_CALCV
                                                         importing EF_UMREZ      = <S_MATDAT_MARM>-UMREZ
                                                                   EF_UMREN      = <S_MATDAT_MARM>-UMREN ).

          endif.
        endif.
      catch CX_SY_MOVE_CAST_ERROR.
    endtry.
  endmethod.
