
class YCL_HP_SMD_MBQ_BL_CRCOMP definition
  public
  inheriting from ZCL_VCXI_P1M_MBQ_BL_CRCOMP
  final
  create public .

public section.

  constants C_BESKZ_X type BESKZ value 'X' ##NO_TEXT.
  constants C_ISVCE_PRODI type ZVCXI_XCS_ISVCE value 'PRODI' ##NO_TEXT.
  constants C_TMAT_KIT type MATNR value 'T_KIT' ##NO_TEXT.
  constants C_ISVCE_SHIPINFO type ZVCXI_XCS_ISVCE value 'SHIPINFO' ##NO_TEXT.
  constants C_ISVCE_SHIPMGR type ZVCXI_XCS_ISVCE value 'SHIPMGR' ##NO_TEXT.
  constants C_ISVCE_AREA_WGT type ZVCXI_XCS_ISVCE value 'AREA_WGT' ##NO_TEXT.
  constants C_ZSTATP_SMD type ZSSD_STAT_POS value 'A7' ##NO_TEXT.
  constants C_LETY1 type LVS_LETYP1 value 'PL' ##NO_TEXT.
  constants C_SOKEY_PX_SB type ZVCXI_XCM_MBQ_SOKEY value 'PX_SB' ##NO_TEXT.
  constants C_SHIPMGR_CRCONO type ZVCXI_XCS_ISVCE value 'SHIPMGR_CRCONO' ##NO_TEXT.

  methods FILL_IDAT
    redefinition .
protected section.

  methods GET_LOVC_TOOL_PRINT
    importing
      !IR_SVCEL_SOURCE type ref to /VCXI/CL_VKSRA_SVC_ELEMENT
      !IF_SOKEY type ZVCXI_XCM_MBQ_SOKEY
      !IF_WERKS type WERKS_D
    returning
      value(RT_VALUE) type /VCXI/VKCR_TT_VALUE
    raising
      /VCXI/CX_CKX .
  methods GET_LOVC_TOOL_VALUE
    importing
      !IR_SVCEL_SOURCE type ref to /VCXI/CL_VKSRA_SVC_ELEMENT
      !IF_SOKEY type ZVCXI_XCM_MBQ_SOKEY
    changing
      !CT_VALUE type /VCXI/VKCR_TT_VALUE
    raising
      /VCXI/CX_CKX .
  methods GET_ACTDAT_TLR
    importing
      !IR_SVCEL_SOURCE type ref to /VCXI/CL_VKSRA_SVC_ELEMENT
      !IF_SOKEY type ZVCXI_XCM_MBQ_SOKEY
      !IF_WERKS type WERKS_D
      !IF_DIMEN type ZVCXI_PXS_S_PRODIM optional
      !IF_DISST type /VCXI/VKC_DISST
    exporting
      !EF_SKIP type ABAP_BOOL
    returning
      value(RR_ACTDAT) type ref to DATA
    raising
      /VCXI/CX_CKX .
  methods ADJUST_ACTDAT_MAT_TOOL
    importing
      !IR_ACTDAT type ref to DATA
      !IR_SVCEL_SOURCE type ref to /VCXI/CL_VKSRA_SVC_ELEMENT
      !IF_SOKEY type ZVCXI_XCM_MBQ_SOKEY
      !IF_WERKS type WERKS_D
    raising
      /VCXI/CX_CKX .
  methods ADJUST_ACTDAT_MAT_MLGN
    importing
      !IR_ACTDAT type ref to DATA
      !IR_SVCEL type ref to /VCXI/CL_VKSRA_SVC_ELEMENT
      !IF_SOKEY type ZVCXI_XCM_MBQ_SOKEY optional
    raising
      /VCXI/CX_CKX .
  methods ADJUST_ACTDAT_MAT_MARA
    importing
      !IR_ACTDAT type ref to DATA
      !IR_SVCEL type ref to /VCXI/CL_VKSRA_SVC_ELEMENT
      !IF_SOKEY type ZVCXI_XCM_MBQ_SOKEY
    raising
      /VCXI/CX_CKX .
  methods ADJUST_ACTDAT_MAT_MARM
    importing
      !IR_SVCEL type ref to /VCXI/CL_VKSRA_SVC_ELEMENT
      value(IR_ACTDAT) type ref to DATA
      !IF_SOKEY type ZVCXI_XCM_MBQ_SOKEY
    raising
      /VCXI/CX_CKX .
  methods ADJUST_ACTDAT_MAT_MVKE
    importing
      !IR_ACTDAT type ref to DATA
      !IR_SVCEL type ref to /VCXI/CL_VKSRA_SVC_ELEMENT
    raising
      /VCXI/CX_CKX .
  methods GET_LOVC_PURCH
    importing
      !IR_SVCEL_SOURCE type ref to /VCXI/CL_VKSRA_SVC_ELEMENT
    returning
      value(RF_PURCH) type ZVCXI_XCS_PURCH
    raising
      /VCXI/CX_CKX .
  methods GET_ACTDAT_MLI
    importing
      !IR_SVCEL_SOURCE type ref to /VCXI/CL_VKSRA_SVC_ELEMENT
      !IF_SOKEY type ZVCXI_XCM_MBQ_SOKEY
      !IF_WERKS type WERKS_D
      !IF_DISST type /VCXI/VKC_DISST
    exporting
      !EF_SKIP type ABAP_BOOL
    returning
      value(RR_ACTDAT) type ref to DATA
    raising
      /VCXI/CX_CKX .
  methods GET_WEIGHTS
    importing
      !IR_SVCEL type ref to /VCXI/CL_VKSRA_SVC_ELEMENT
      !IF_SOKEY type ZVCXI_XCM_MBQ_SOKEY optional
    exporting
      !EF_BRGEW type BRGEW
      !EF_NTGEW type NTGEW
      !EF_GEWEI type GEWEI
    raising
      /VCXI/CX_CKX .
  methods CHECK_CREATE_TOOL_PRINT
    importing
      !IF_ACTID type ZVCXI_XCM_MBQ_ACTID
      !IR_SVCEL_SOURCE type ref to /VCXI/CL_VKSRA_SVC_ELEMENT
      !IF_SOKEY type ZVCXI_XCM_MBQ_SOKEY
      !IF_WERKS type WERKS_D
    returning
      value(RF_SKIP) type ABAP_BOOL
    raising
      /VCXI/CX_CKX .

  methods ADJUST_SVCEL_CFG
    redefinition .
  methods FILL_CFG_AS_CUSTOM
    redefinition .
  methods GET_ACTDAT
    redefinition .
  methods GET_ACTDAT_CMIR
    redefinition .
  methods GET_ACTDAT_MAT
    redefinition .
  methods GET_ACTDAT_PURIR
    redefinition .
  methods GET_LOVC
    redefinition .
  methods GET_LOVC_TOOL_DCT
    redefinition .
  methods GET_MATDAT_MARM
    redefinition .
  methods ANALYZE_MS_INK
    redefinition .
private section.

  methods GET_LEGMA
    importing
      !IS_VALEX type ANY
    returning
      value(RF_LEGMA) type MATNR .
endclass. "YCL_HP_SMD_MBQ_BL_CRCOMP definition
class YCL_HP_SMD_MBQ_BL_CRCOMP implementation.
method ADJUST_ACTDAT_MAT_MARA.

  data: LF_CSPEC type YHP_SVCS_CSPEC.
  data: LS_PARAM type /VCXI/VKSR_S_PARAM.
  data: LR_SVCEL    type ref to /VCXI/CL_VKSRA_SVC_ELEMENT,
        LR_EA_PRODI type ref to YCL_HP_SVCS_EA_PRODI.
  field-symbols: <S_ACTDAT_MAT> type ZVCXI_XCM_MBQ_S_ACTDAT_MAT.

  check IR_ACTDAT is bound.
  assign IR_ACTDAT->* to <S_ACTDAT_MAT>.

*** Sales Division
  <S_ACTDAT_MAT>-S_MATDAT_MARA-SPART = ME->R_BOB_SDOC->S_VBAK-SPART.

*** Product Hierarchy (Do for all except tools)
  if IF_SOKEY ne ME->C_SOKEY_TOOL_DCT and
     IF_SOKEY ne ME->C_SOKEY_TOOL_SET_PRINT.
    <S_ACTDAT_MAT>-S_MATDAT_MARA-PRDHA = YCL_HP_X_SRV_PRODH=>GET_PRODH( IR_SVCEL_XCOMP = ME->R_SVCEL_XCOMP
                                                                        IF_MTART = <S_ACTDAT_MAT>-S_MATDAT_MARA-MATL_TYPE
                                                                        IR_SVC   = ME->R_BOB_SDOC->GET_SVC( IF_POSNR = ME->F_POSNR ) ).
  endif.

*** Material Group
  <S_ACTDAT_MAT>-S_MATDAT_MARA-MATKL = <S_ACTDAT_MAT>-S_MATDAT_MARA-PRDHA.

*** Legacy Material
  try.
      if ME->R_BOB_SDOC->S_VBAK-AUART eq 'ZMI'.
        <S_ACTDAT_MAT>-S_MATDAT_MARA-BISMT = value #( ME->R_BOB_SDOC->T_VBKD[ POSNR = ME->F_POSNR ]-BSTKD ).
      endif.
    catch CX_SY_ITAB_LINE_NOT_FOUND.
  endtry.

*** Get Area and Weight
  ME->GET_WEIGHTS( exporting  IR_SVCEL = IR_SVCEL
                   importing  EF_BRGEW = <S_ACTDAT_MAT>-S_MATDAT_MARA-BRGEW
                              EF_NTGEW = <S_ACTDAT_MAT>-S_MATDAT_MARA-NTGEW
                              EF_GEWEI = <S_ACTDAT_MAT>-S_MATDAT_MARA-GEWEI ).

*** Get Product Info
  LR_SVCEL = YCL_HP_SVC_SRV_VE=>GET_SVCEL_RELATED_CHILD( IF_SVCEL_ID = C_ISVCE_PRODI
                                                         IR_SVCEL    = IR_SVCEL ).
  if LR_SVCEL is bound.

    try.

***     Get Element API Reference
        LR_EA_PRODI ?= LR_SVCEL->GET_CLSEA( ).
        free LR_SVCEL.

      catch CX_SY_MOVE_CAST_ERROR into data(LR_CX_SY_MOVE_CAST_ERROR).
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_SY_MOVE_CAST_ERROR ).

    endtry.

*** Get Client Specification
    LF_CSPEC = LR_EA_PRODI->GET_CSPEC( ).
    <S_ACTDAT_MAT>-S_MATDAT_MARA-FERTH = LF_CSPEC.

  endif.

endmethod.
  method ADJUST_ACTDAT_MAT_MARM.

    data: LS_PROLE type	ZVCXI_P0S_S_PROLE,
          LS_PROHE type	ZVCXI_P0S_S_PROHE,
          LS_PROWI type	ZVCXI_P0S_S_PROWI,
          LS_MARM  type MARM.

    data: LR_SVCEL        type ref to /VCXI/CL_VKSRA_SVC_ELEMENT,
          LR_EA_CFG       type ref to YCL_HP_SVCS_EA_CFG,
          LR_EA_MS_CRPROC type ref to ZCL_VCXI_P1SS_EA_MS_CRPROC,
          LR_BL_CRBPROP   type ref to ZCL_VCXI_P1SS_BL_CRBPROP,
          LR_BL_CRFPROP   type ref to ZCL_VCXI_P1SS_BL_CRFPROP,
          LR_EA_CRCONO    type ref to ZCL_VCXI_P1SS_EA_CRCONO.

    data: LS_SHTLE type ZVCXI_P0SS_S_SHTLE,
          LS_SHTWI type ZVCXI_P0SS_S_SHTWI,
          LS_PARAM type /VCXI/VKSR_S_PARAM.





    field-symbols: <S_ACTDAT_MAT>  type ZVCXI_XCM_MBQ_S_ACTDAT_MAT,
                   <S_MATDAT_MARM> type ZVCXI_XCM_MBQ_S_MATDAT_MARM.

    check IR_ACTDAT is bound.
    assign IR_ACTDAT->* to <S_ACTDAT_MAT>.

    case IF_SOKEY.
      when C_SOKEY_CFG.
*** Get CFG Info
        LR_SVCEL  = YCL_HP_SVC_SRV_VE=>GET_SVCEL_RELATED( IF_SVCEL_ID = C_ISVCE_CFG
                                                          IR_SVCEL    = IR_SVCEL ).
        check LR_SVCEL is not initial.
*** Get Element API Reference
        try.
            LR_EA_CFG ?= LR_SVCEL->GET_CLSEA( ).
          catch CX_SY_MOVE_CAST_ERROR into data(LR_CX_SY_MOVE_CAST_ERROR).
            /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_SY_MOVE_CAST_ERROR ).
        endtry.

        check LR_EA_CFG is bound.
        LS_PROLE = LR_EA_CFG->GET_PROLE( ).
        LS_PROHE = LR_EA_CFG->GET_PROHE( ).
        LS_PROWI = LR_EA_CFG->GET_PROWI( ).
      when C_SOKEY_CRPROC.
*** Get MS_CRPROC Info
        LR_SVCEL  = YCL_HP_SVC_SRV_VE=>GET_SVCEL_RELATED( IF_SVCEL_ID = C_ISVCE_MS_CRPROC
                                                          IR_SVCEL    = IR_SVCEL ).
        check LR_SVCEL is not initial.
*** Get Element API Reference
        try.
            LR_EA_MS_CRPROC ?= LR_SVCEL->GET_CLSEA( ).
          catch CX_SY_MOVE_CAST_ERROR into LR_CX_SY_MOVE_CAST_ERROR.
            /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_SY_MOVE_CAST_ERROR ).
        endtry.

        check LR_EA_MS_CRPROC is bound.

        LR_BL_CRBPROP =  LR_EA_MS_CRPROC->GET_CRBPX( ).
        LR_BL_CRBPROP->GET_SHTDIM( importing ES_SHTLE = LS_SHTLE
                                             ES_SHTWI = LS_SHTWI ).

        LS_PARAM = LR_SVCEL->GET_PARAM( ZCL_VCXI_P1SS_PR_MS_CRPROC=>C_PARID_CALIP ).

        LS_PROLE-PROLE = LS_SHTLE-SHTLE.
        LS_PROHE-PROHE =  LS_PARAM-ATFLV.
        LS_PROHE-PROHE_UOM = LS_PARAM-UNIT.
        LS_PROWI-PROWI = LS_SHTWI-SHTWI.

      when C_SOKEY_CSFG.
*** Get CSFG Info
        LR_SVCEL  = YCL_HP_SVC_SRV_VE=>GET_SVCEL_RELATED( IF_SVCEL_ID = C_ISVCE_CRCONO
                                                          IR_SVCEL    = IR_SVCEL ).
        check LR_SVCEL is not initial.
**** Get Element API Reference
        try.
            LR_EA_CRCONO ?= LR_SVCEL->GET_CLSEA( ).
          catch CX_SY_MOVE_CAST_ERROR into LR_CX_SY_MOVE_CAST_ERROR.
            /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_SY_MOVE_CAST_ERROR ).
        endtry.
*
        check LR_EA_CRCONO is bound.
        LR_EA_CRCONO->GET_CRBPX( )->GET_SHTDIM( importing ES_SHTLE = LS_SHTLE
                                                          ES_SHTWI = LS_SHTWI ).

        LR_BL_CRFPROP = LR_EA_CRCONO->GET_CRFPX( ).

        LS_PROLE-PROLE = LS_SHTLE-SHTLE.
        LS_PROWI-PROWI = LS_SHTWI-SHTWI.
        if LR_BL_CRFPROP is bound.
          LS_PROHE-PROHE = LR_BL_CRFPROP->S_CALIP-CALIP.
          LS_PROHE-PROHE_UOM = LR_BL_CRFPROP->S_CALIP-CALIP_UOM.
        endif.
    endcase.
*Only one unit of measurement will be maintain in the template
    select single MEINH UMREZ UMREN BRGEW
    into corresponding fields of LS_MARM
    from MARM
    where MATNR eq <S_ACTDAT_MAT>-TMATN.

    append value #( MEINH = LS_MARM-MEINH
                    UMREZ = LS_MARM-UMREZ
                    UMREN = LS_MARM-UMREN
                    LAENG = LS_PROLE-PROLE
                    BREIT = LS_PROWI-PROWI
                    HOEHE = LS_PROHE-PROHE
                    MEABM = LS_PROHE-PROHE_UOM
                    BRGEW = LS_MARM-BRGEW  ) to <S_ACTDAT_MAT>-T_MATDAT_MARM assigning <S_MATDAT_MARM>.


  endmethod.
  method ADJUST_ACTDAT_MAT_MLGN.
    data: LF_SHUTY  type ZVCXI_XCSU_SHUTY.

    data: LS_TBQTY  type ZVCXI_XCSU_S_TBQTY.


    data: LR_SVCEL    type ref to /VCXI/CL_VKSRA_SVC_ELEMENT,
          LR_OBJECT   type ref to /VCXI/IF_CKX_OBJECT,
          LR_EA_SHIPU type ref to ZCL_VCXI_XCSU_EA_SHIPUNIT.

    data: LT_SVCEL        type        /VCXI/CKX_TT_OBJECT.

    field-symbols: <S_ACTDAT_MAT>  type ZVCXI_XCM_MBQ_S_ACTDAT_MAT,
                   <S_MATDAT_MLGN> type ZVCXI_XCM_MBQ_S_MATDAT_MLGN.

    check IR_ACTDAT is bound.
    assign IR_ACTDAT->* to <S_ACTDAT_MAT>.


    if IF_SOKEY  = C_SOKEY_CSFG.
*** Get Shipping & Unitization
      LR_SVCEL  = YCL_HP_SVC_SRV_VE=>GET_SVCEL_RELATED( IF_SVCEL_ID = C_SHIPMGR_CRCONO
                                                        IR_SVCEL    = IR_SVCEL ).
    else.
*** Get Shipping Manager
      LR_SVCEL  = YCL_HP_SVC_SRV_VE=>GET_SVCEL_RELATED( IF_SVCEL_ID = C_ISVCE_SHIPMGR
                                                        IR_SVCEL    = IR_SVCEL ).
    endif.
    check LR_SVCEL is not initial.

*** Get All Children of SmartVC Element
    LT_SVCEL = LR_SVCEL->/VCXI/IF_CKX_OBJECT_H~GET_CHILDREN( ).
*** Loop through all Children of SmartVC Element
    loop at LT_SVCEL into LR_OBJECT.
      move LR_OBJECT ?to LR_SVCEL.

*** Get Element API Reference
      try.
          LR_EA_SHIPU ?= LR_SVCEL->GET_CLSEA( ).
          check LR_EA_SHIPU is bound.
          LR_EA_SHIPU->GET_SHUTY( importing EF_SHUTY = LF_SHUTY ).
          if LF_SHUTY eq YCL_HP_SVCU_PR_SHIPINFO=>C_SHUTY_UNIT.
            exit.
          else.
            clear: LR_EA_SHIPU, LF_SHUTY.
          endif.
        catch CX_SY_MOVE_CAST_ERROR into data(LR_CX_SY_MOVE_CAST_ERROR).
          continue.
      endtry.
    endloop.

    check LR_EA_SHIPU is bound.

    LR_EA_SHIPU->GET_TBQTY( importing ES_TBQTY = LS_TBQTY ).
    case IF_SOKEY.
      when C_SOKEY_PX_SB or C_SOKEY_DMS_PRINT or C_SOKEY_TOOL_DCT or C_SOKEY_TOOL_SET_PRINT
           or C_SOKEY_CRPROC.
      when others.
        loop at <S_ACTDAT_MAT>-T_MATDAT_MLGN assigning <S_MATDAT_MLGN>.
          try.
              move LS_TBQTY-TBQTY to <S_MATDAT_MLGN>-LHMG1.
              move LS_TBQTY-TBQTY_UOM to <S_MATDAT_MLGN>-LHME1.
              move C_LETY1 to <S_MATDAT_MLGN>-LETY1.
            catch CX_SY_ARITHMETIC_OVERFLOW.
              continue.
          endtry.
        endloop.
    endcase.
  endmethod.
  method ADJUST_ACTDAT_MAT_MVKE.


    data: LR_SVCEL    type ref to /VCXI/CL_VKSRA_SVC_ELEMENT,
          LR_EA_PRODI type ref to  YCL_HP_SVCS_EA_PRODI.

    field-symbols: <S_ACTDAT_MAT>  type ZVCXI_XCM_MBQ_S_ACTDAT_MAT,
                   <S_MATDAT_MVKE> type ZVCXI_XCM_MBQ_S_MATDAT_MVKE.

    check IR_ACTDAT is bound.
    assign IR_ACTDAT->* to <S_ACTDAT_MAT>.

*** Get Product Info
    LR_SVCEL  = YCL_HP_SVC_SRV_VE=>GET_SVCEL_RELATED( IF_SVCEL_ID = C_ISVCE_PRODI
                                                      IR_SVCEL    = IR_SVCEL ).
    check LR_SVCEL is not initial.
*** Get Element API Reference
    try.
        LR_EA_PRODI ?= LR_SVCEL->GET_CLSEA( ).
      catch CX_SY_MOVE_CAST_ERROR into data(LR_CX_SY_MOVE_CAST_ERROR).
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_SY_MOVE_CAST_ERROR ).
    endtry.




    loop at <S_ACTDAT_MAT>-T_MATDAT_MVKE assigning <S_MATDAT_MVKE>.

*** Check not sheet procurement or kits header.
      if GET_LOVC_PURCH( IR_SVCEL_SOURCE  = IR_SVCEL ) ne ZCL_VCXI_PXSS_PR_MS_XPROC=>C_PURCH_DIRECT_PRICE.
*** Set Material Group 1.
        <S_MATDAT_MVKE>-MVGR1 = ME->R_BOB_SDOC->T_VBAP[ POSNR = ME->F_POSNR ]-MVGR1.
      endif.

      check LR_EA_PRODI is bound.
*** Get FSC Type
      if LR_EA_PRODI->GET_FSCTY( ) is not initial.
        move ABAP_TRUE to <S_MATDAT_MVKE>-PRAT1.
      else.
        move ABAP_FALSE to <S_MATDAT_MVKE>-PRAT1.
      endif.
    endloop.

  endmethod.
  method ADJUST_ACTDAT_MAT_TOOL.

    constants: LC_FIELD_LEGMA type FIELDNAME value 'LEGMA',
               LC_FIELD_TOSTA type FIELDNAME value 'TOSTA'.
    data: LS_TD01  type YHP_SMD_MBQ_TD01,
          LS_TC002 type YRF_TPR_TC002,
          LS_TD111 type ZVCXI_XCM_TD111.
    data: LR_SVC   type ref to /VCXI/CL_VKSRA_SVC,
          LR_VALEX type ref to DATA.
    data: LR_BOB_MAT_SOURCE type ref to ZCL_VCXI_XCI_BOB_MAT.
    field-symbols: <S_VALEX>       type ANY,
                   <F_LEGMA>       type MATNR,
                   <F_TOSTA>       type YRF_TPR_TOSTA,
                   <S_ACTDAT_MAT>  type ZVCXI_XCM_MBQ_S_ACTDAT_MAT,
                   <S_MATDAT_MARC> type ZVCXI_XCM_MBQ_S_MATDAT_MARC.



    check IR_ACTDAT is bound.
    assign IR_ACTDAT->* to <S_ACTDAT_MAT>.

** Get right Reference Material Information
    ZCL_VCXI_XCM_MBQ_CUST=>READ_REFMAT( exporting IF_SOKEY    = IF_SOKEY
                                                  IF_WERKS    = IF_WERKS
                                        importing ER_BOB_MAT  = LR_BOB_MAT_SOURCE
                                        receiving RS_TD111    = LS_TD111 ).

*** Get Tool Status from VC Value Extraction
    YCL_HP_SMD_MBQ_CUST=>GET_TLCNF( exporting IF_SOKEY = IF_SOKEY
                                    importing ES_TD01  = LS_TD01 ).

    LR_SVC = ME->R_BOB_SDOC->GET_SVC( IF_POSNR = ME->F_POSNR ).
    if  LR_SVC is bound.
      LR_VALEX = LR_SVC->GET_VALEX( IF_IVEMO       = LS_TD01-IVEMO
                                    IR_SVC_ELEMENT = IR_SVCEL_SOURCE ).
      if LR_VALEX is bound.
        assign LR_VALEX->* to <S_VALEX>.
        check <S_VALEX> is assigned.
        assign component LC_FIELD_LEGMA of structure <S_VALEX> to <F_LEGMA>.
        assign component LC_FIELD_TOSTA of structure <S_VALEX> to <F_TOSTA>.
      endif.
    endif.

    if <F_TOSTA> is not assigned.
***   Tool Status Field not found for Source Key &1 Value Extraction ID &2.
      /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( IF_MSGID = 'YHP_SMD_MBQ'
                                            IF_MSGTY = 'E'
                                            IF_MSGNO = '010'
                                            IF_MSGV1 = IF_SOKEY
                                            IF_MSGV2 = LS_TD01-IVEMO ).
    endif.

*** Get Old Material Number
    <S_ACTDAT_MAT>-S_MATDAT_MARA-BISMT = <F_LEGMA>.

*** Generate Material Number
    if <F_LEGMA> is not initial.
      case IF_SOKEY.
        when ME->C_SOKEY_TOOL_DCT.
          <S_ACTDAT_MAT>-S_MATDAT_MARA-MATNR = |{ YCL_HP_X_SRV_PRODH=>C_LV1_UT }| & |{ IF_WERKS }{ <F_LEGMA> }|.
        when ME->C_SOKEY_TOOL_SET_PRINT.
          <S_ACTDAT_MAT>-S_MATDAT_MARA-MATNR = |{ YCL_HP_X_SRV_PRODH=>C_LV1_UC }| & |{ IF_WERKS }{ <F_LEGMA> }|.
      endcase.

      "For migrated tools we create them with Released status
      <F_TOSTA> = YCL_RF_TPM_TOOL=>C_TOSTA_YREL.

    endif.

*** Get Material Tool Status from Customizing
    LS_TC002 = YCL_HP_SMD_MBQ_CUST=>GET_TLSTA( IF_WERKS = IF_WERKS
                                               IF_TOSTA = <F_TOSTA> ).

*** Get material type
    <S_ACTDAT_MAT>-S_MATDAT_MARA-MATL_TYPE = LS_TD111-MTART.

*** Set Material Client Data Status
    <S_ACTDAT_MAT>-S_MATDAT_MARA-MSTAE = LS_TC002-MSTAE.

*** Set Product Hierarchy for tools
    <S_ACTDAT_MAT>-S_MATDAT_MARA-PRDHA = YCL_HP_X_SRV_PRODH=>GET_PRODH( IR_SVCEL_XCOMP = IR_SVCEL_SOURCE
                                                                        IF_MTART       = <S_ACTDAT_MAT>-S_MATDAT_MARA-MATL_TYPE
                                                                        IF_SOKEY       = IF_SOKEY
                                                                        IR_SVC         = LR_SVC ).

*** Material Group
    <S_ACTDAT_MAT>-S_MATDAT_MARA-MATKL = <S_ACTDAT_MAT>-S_MATDAT_MARA-PRDHA.

*** Set Material Plant Data Status
    loop at <S_ACTDAT_MAT>-T_MATDAT_MARC assigning <S_MATDAT_MARC>.
      <S_MATDAT_MARC>-MMSTA = LS_TC002-MMSTA.
    endloop.

*** Change Procurement Type and Special Procurement Type
*{   REPLACE        HEDK925765
***    <S_MATDAT_MARC>-BESKZ = LR_BOB_MAT_SOURCE->S_PLANTDATA-PROC_TYPE.
***    <S_MATDAT_MARC>-SOBSL = LR_BOB_MAT_SOURCE->S_PLANTDATA-SPPROCTYPE.
    <S_MATDAT_MARC>-PROC_TYPE = LR_BOB_MAT_SOURCE->S_PLANTDATA-PROC_TYPE.
    <S_MATDAT_MARC>-SPPROCTYPE = LR_BOB_MAT_SOURCE->S_PLANTDATA-SPPROCTYPE.
*}   REPLACE
*** Set Legacy Material for Tools

  endmethod.
  method ADJUST_SVCEL_CFG.

    call method SUPER->ADJUST_SVCEL_CFG
      exporting
        IR_SVCEL         = IR_SVCEL
        IR_IDAT_SVC      = IR_IDAT_SVC
        IT_MATLI         = IT_MATLI
        IT_DMSLI         = IT_DMSLI
      importing
        EF_SKIP_CHILDREN = EF_SKIP_CHILDREN
      changing
        CS_SVCEL         = CS_SVCEL.

*   if IR_SVCEL->/VCXI/IF_CKX_OBJECT~GET_ID( ) eq C_ISVCE_CFG.
*      LR_EA_CRCONO ?= IR_SVCEL->GET_CLSEA( ).
*
****   Get Corrugator Backward Property XML (Outbound)
*      LR_BL_CRBPROP = LR_EA_CRCONO->GET_CRBPX( ).
*
*      case LR_BL_CRBPROP->F_FEFCO.
*        when '0110'.    "Sheet
*          ME->FILL_CFG_AS_SHEET( exporting IR_BL_CRBPROP = LR_BL_CRBPROP
*                                           IR_IDAT_SVC   = IR_IDAT_SVC
*                                 changing  CS_SVCEL      = CS_SVCEL ).
*        when others.
*          ME->FILL_CFG_AS_CUSTOM( exporting IR_BL_CRBPROP = LR_BL_CRBPROP
*                                            IR_IDAT_SVC   = IR_IDAT_SVC
*                                  changing  CS_SVCEL      = CS_SVCEL ).
*      endcase.
*    endif.

  endmethod.
  method ANALYZE_MS_INK.

*** We are redefining the method so it doesn't call the parent method because we can't use the standard ink tool creation, as it doesn't work with the tooling cockpit, so we need to skip this.

*CALL METHOD SUPER->ANALYZE_MS_INK
*  EXPORTING
*    IR_SVCEL_MS_INK =
*    IF_DISST        =
*  CHANGING
*    CT_MATLI        =
*    CT_DMSLI        =
*    .

  endmethod.
  method CHECK_CREATE_TOOL_PRINT.

    data: LF_NOCOL          type ZVCXI_P0S_NOCOL.
    data: LR_EA_CSTEP_PRINT type ref to YCL_HP_SVCS_EA_CSTEP_PRINT.

***-------------------------------------------------------------------*
*** clean start
    clear RF_SKIP.

*** Check we have a printing tool
    check IF_SOKEY eq C_SOKEY_TOOL_SET_PRINT.

*** Get EA of CSTEP Printing
    try.
        LR_EA_CSTEP_PRINT = cast #( IR_SVCEL_SOURCE->GET_CLSEA( ) ).
      catch CX_SY_MOVE_CAST_ERROR.
        clear LR_EA_CSTEP_PRINT.
    endtry.

*** Get Number of Inks; in case of 0 inks then skip tool creation
    check LR_EA_CSTEP_PRINT is bound.
    if LR_EA_CSTEP_PRINT->GET_NOCOL_EXCL_GEINK( ) eq 0.
      RF_SKIP = ABAP_TRUE.
    endif.


  endmethod.
  method FILL_CFG_AS_CUSTOM.
    data: LS_SHTLE type ZVCXI_P0SS_S_SHTLE,
          LS_SHTWI type ZVCXI_P0SS_S_SHTWI.


***--------------------------------------------------------------------------------------
*** FEFCO
    append value #( PARID = ZCL_VCXI_P1SS_PR_CFG=>C_PARID_FEFCO
                    ATWRT = '1000'    "Custom Design
                    SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC
                  ) to CS_SVCEL-T_SVCPA.

*** Product Dimensions
    append value #( PARID = ZCL_VCXI_P1SS_PR_CFG=>C_PARID_PROLE
                    ATFLV = IR_BL_CRBPROP->S_PRODIM-PROLE
                    UNIT  = IR_BL_CRBPROP->S_PRODIM-PROLE_UOM
                    SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC
                  ) to CS_SVCEL-T_SVCPA.
    append value #( PARID = ZCL_VCXI_P1SS_PR_CFG=>C_PARID_PROWI
                    ATFLV = IR_BL_CRBPROP->S_PRODIM-PROWI
                    UNIT  = IR_BL_CRBPROP->S_PRODIM-PROWI_UOM
                    SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC
                  ) to CS_SVCEL-T_SVCPA.
    append value #( PARID = ZCL_VCXI_P1SS_PR_CFG=>C_PARID_PROHE
                    ATFLV = IR_BL_CRBPROP->S_PRODIM-PROHE
                    UNIT  = IR_BL_CRBPROP->S_PRODIM-PROHE_UOM
                    SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC
                  ) to CS_SVCEL-T_SVCPA.

*** Product Sheet Dimension
    IR_BL_CRBPROP->GET_SHTDIM( importing ES_SHTLE = LS_SHTLE
                                         ES_SHTWI = LS_SHTWI ).

    append value #( PARID = ZCL_VCXI_P1SS_PR_CFG=>C_PARID_SHTPL
                    ATFLV = LS_SHTLE-SHTLE
                    UNIT  = LS_SHTLE-SHTLE_UOM
                    SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC
                  ) to CS_SVCEL-T_SVCPA.
    append value #( PARID = ZCL_VCXI_P1SS_PR_CFG=>C_PARID_SHTPW
                    ATFLV = LS_SHTWI-SHTWI
                    UNIT  = LS_SHTWI-SHTWI_UOM
                    SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC
                  ) to CS_SVCEL-T_SVCPA.

*** Flute Orientation
    append value #( PARID = ZCL_VCXI_P1SS_PR_CFG=>C_PARID_FLUTO
                    ATWRT = 'V'    "Vertical
                    SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC
                  ) to CS_SVCEL-T_SVCPA.

*** Cut Out Area
    append value #( PARID = ZCL_VCXI_P1SS_PR_CFG=>C_PARID_COUAR
                    ATFLV = IR_BL_CRBPROP->S_COUAR-COUAR
                    UNIT  = IR_BL_CRBPROP->S_COUAR-COUAR_UOM
                    SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC
                  ) to CS_SVCEL-T_SVCPA.

*** Joint Flap Dimension
    append value #( PARID = ZCL_VCXI_P1SS_PR_CFG=>C_PARID_JOIWI
                    ATFLV = IR_BL_CRBPROP->S_JOIDIM-JOIWI
                    UNIT  = IR_BL_CRBPROP->S_JOIDIM-JOIWI_UOM
                    SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC
                  ) to CS_SVCEL-T_SVCPA.
    append value #( PARID = ZCL_VCXI_P1SS_PR_CFG=>C_PARID_JOIPO
                    ATWRT = IR_BL_CRBPROP->S_JOIDIM-JOIPO
                    SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_CALC
                  ) to CS_SVCEL-T_SVCPA.

    data: LT_SCORE type ZVCXI_PXSS_TT_SCORE_COM.
    LT_SCORE[] = IR_BL_CRBPROP->T_SCORE[].
    sort LT_SCORE by ISCOR.
***--------------------------------------------------------------------------------------
*** Add Score Manager
    ME->ADD_SCORE_MGR( IF_IPARE     = CS_SVCEL-ISELF
                       IF_CPARE     = CS_SVCEL-CSELF
                       IR_IDAT_SVC  = IR_IDAT_SVC
                       IF_PANOR     = 'CUSTOM'
                       IF_SSCOR     = 'Z'  "Not Manual as it will be reordered
                       IT_SCORE_COM = LT_SCORE
                       IT_TAPE_COM  = IR_BL_CRBPROP->T_TAPE_COM ).

  endmethod.
  method FILL_IDAT.
    data:          LS_SVCEL type ZVCXI_XCI_S_SVCEL.
    field-symbols: <S_SVCPA> type ZVCXI_XCI_S_SVCPA.

    SUPER->FILL_IDAT(  IF_IPARE       = IF_IPARE
                       IF_CPARE       = IF_CPARE
                       IR_SVC         = IR_SVC
                       IR_IDAT_SVC    = IR_IDAT_SVC
                       IR_CUELMNT_SOV = IR_CUELMNT_SOV
                       IT_MATLI       = IT_MATLI
                       IT_DMSLI       =  IT_DMSLI ).

*** Set incomplete pallet flag to calculated in FG
    read table IR_IDAT_SVC->T_SVCEL into LS_SVCEL
    with key IPARE = ZCL_VCXI_XCSU_PR_SHIPMGR=>C_IPRVT_XCS_SHIPMGR
             CPARE = IF_CPARE ISELF = ZCL_VCXI_XCSU_PR_SHIPINFO=>C_IPRVT_XCS_SHIPINFO.
    if SY-SUBRC eq 0.
      READ TABLE LS_SVCEL-T_SVCPA ASSIGNING <S_SVCPA>
      with key PARID = YCL_HP_SVCU_PR_SHIPINFO=>C_PARID_INCPA
               SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER.
      if sy-subrc eq 0.
        move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC to  <S_SVCPA>-SETBY.
        IR_IDAT_SVC->UPDATE_SVCEL( IS_SVCEL = LS_SVCEL ).
      endif.
    endif.


  endmethod.
  method GET_ACTDAT.

***-------------------------------------------------------------------*
*** First check if tools needs to be created
    case IF_SOKEY.

      when C_SOKEY_TOOL_SET_PRINT. " Printing Tool
        EF_SKIP = ME->CHECK_CREATE_TOOL_PRINT( IF_ACTID        = IF_ACTID
                                               IR_SVCEL_SOURCE = IR_SVCEL_SOURCE
                                               IF_SOKEY        = IF_SOKEY
                                               IF_WERKS        = IF_WERKS ).

      when others.                 " Allow normal processing
        clear EF_SKIP.
    endcase.

*** Check if processing can continue
    check EF_SKIP is initial.

***-------------------------------------------------------------------*
*** Call super
    SUPER->GET_ACTDAT( exporting IF_ACTID        = IF_ACTID
                                 IR_SVCEL_SOURCE = IR_SVCEL_SOURCE
                                 IF_SOKEY        = IF_SOKEY
                                 IF_WERKS        = IF_WERKS
                                 IF_DISST        = IF_DISST
                       importing EF_SKIP         = EF_SKIP
                       receiving RR_ACTDAT       = RR_ACTDAT ).

    case IF_ACTID.
***--------------------------------------------------------------------------------------
***   Loop to create materials
      when YCL_HP_SMD_MBQ_CUST=>C_ACTID_TLR.    " Tool Record Creation
*JBL 01/12/2022 Get Prohe like in ACH
data lr_clsea TYPE REF TO /VCXI/CL_VKSR_CLSEA.
data lr_eastep type ref to ZCL_VCXI_P1SS_EA_CSTEP.
data LS_PRODIM type ZVCXI_PXS_S_PRODIM.
 lr_clsea = IR_SVCEL_SOURCE->GET_CLSEA( ).
TRY.
    lr_eastep ?=  lr_clsea.
data(LR_BL_CRBPROP) = LR_EASTEP->GET_CRBPX_NEXT( ).
clear ls_prodim.
     LS_PRODIM = LR_BL_CRBPROP->S_PRODIM.
  CATCH cx_sy_move_cast_error.
ENDTRY.

        RR_ACTDAT = ME->GET_ACTDAT_TLR( exporting IR_SVCEL_SOURCE = IR_SVCEL_SOURCE
                                                  IF_SOKEY        = IF_SOKEY
                                                  IF_WERKS        = IF_WERKS
                                                  IF_DISST        = IF_DISST
                                            if_dimen =  LS_PRODIM
                                        importing EF_SKIP         = EF_SKIP ).

      when YCL_HP_SMD_MBQ_CUST=>C_ACTID_MLI.  " Material List (Inclusion/Exclusion)
        RR_ACTDAT = ME->GET_ACTDAT_MLI( exporting IR_SVCEL_SOURCE = IR_SVCEL_SOURCE
                                                  IF_SOKEY        = IF_SOKEY
                                                  IF_WERKS        = IF_WERKS
                                                  IF_DISST        = IF_DISST
                                        importing EF_SKIP         = EF_SKIP ).

    endcase.

  endmethod.
  method GET_ACTDAT_CMIR.

    data: LR_SVCEL type ref to /VCXI/CL_VKSRA_SVC_ELEMENT.
    data: LR_EA_SHIPINFO type ref to  YCL_HP_SVCU_EA_SHIPINFO.
    field-symbols: <S_ACTDAT_CMIR> type ZVCXI_XCM_MBQ_S_ACTDAT_CMIR.

    try.
        call method SUPER->GET_ACTDAT_CMIR
          exporting
            IR_SVCEL_SOURCE = IR_SVCEL_SOURCE
            IF_SOKEY        = IF_SOKEY
            IF_WERKS        = IF_WERKS
            IF_DISST        = IF_DISST
          importing
            EF_SKIP         = EF_SKIP
          receiving
            RR_ACTDAT       = RR_ACTDAT.
      catch /VCXI/CX_CKX .
     /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ). "JBL 03/10/2022 Check for duplicates here
    endtry.

    check RR_ACTDAT is bound.
    assign RR_ACTDAT->* to <S_ACTDAT_CMIR>.

*** Get Shipping Info
    LR_SVCEL  = YCL_HP_SVC_SRV_VE=>GET_SVCEL_RELATED( IF_SVCEL_ID = C_ISVCE_SHIPINFO
                                                      IR_SVCEL    = IR_SVCEL_SOURCE ).
    check LR_SVCEL is not initial.
*** Get Element API Reference
    try.
      LR_EA_SHIPINFO ?= LR_SVCEL->GET_CLSEA( ).
      catch CX_SY_MOVE_CAST_ERROR into data(LR_CX_SY_MOVE_CAST_ERROR).
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_SY_MOVE_CAST_ERROR ).
    endtry.
    check LR_EA_SHIPINFO is bound.

***Get Field Palet Completo
    <S_ACTDAT_CMIR>-ZZPALET_COMPLETO = LR_EA_SHIPINFO->GET_INCPA( ).

  endmethod.
  method GET_ACTDAT_MAT.

    RR_ACTDAT = SUPER->GET_ACTDAT_MAT( exporting IR_SVCEL_SOURCE = IR_SVCEL_SOURCE
                                                 IF_SOKEY        = IF_SOKEY
                                                 IF_WERKS        = IF_WERKS
                                                 IF_DISST        = IF_DISST ).
*** Adjust Sales Data
    ME->ADJUST_ACTDAT_MAT_MVKE( IR_ACTDAT = RR_ACTDAT
                                IR_SVCEL  = IR_SVCEL_SOURCE ).
*** Adjust Warehouse Data
    ME->ADJUST_ACTDAT_MAT_MLGN( IR_ACTDAT = RR_ACTDAT
                                IR_SVCEL  = IR_SVCEL_SOURCE
                                IF_SOKEY  = IF_SOKEY ).

*** Adjust Material Data
    ME->ADJUST_ACTDAT_MAT_MARA( IR_ACTDAT = RR_ACTDAT
                                IR_SVCEL  = IR_SVCEL_SOURCE
                                IF_SOKEY  = IF_SOKEY ).

*** Adjust Material Data
    ME->ADJUST_ACTDAT_MAT_MARM( IR_ACTDAT = RR_ACTDAT
                                IR_SVCEL  = IR_SVCEL_SOURCE
                                IF_SOKEY  = IF_SOKEY ).

*** Adjust Material Data for Tools
    case IF_SOKEY.
      when ME->C_SOKEY_TOOL_DCT or ME->C_SOKEY_TOOL_SET_PRINT.
        ME->ADJUST_ACTDAT_MAT_TOOL( IR_ACTDAT       = RR_ACTDAT
                                    IR_SVCEL_SOURCE = IR_SVCEL_SOURCE
                                    IF_SOKEY        = IF_SOKEY
                                    IF_WERKS        = IF_WERKS ).
    endcase.

  endmethod.
  method GET_ACTDAT_MLI.

    RR_ACTDAT = YCL_HP_SMD_MBQ_SRV_BL_XCOMP=>GET_ACTDAT_MLI( exporting IR_SVCEL_SOURCE = IR_SVCEL_SOURCE
                                                                       IF_SOKEY        = IF_SOKEY
                                                                       IF_WERKS        = IF_WERKS
                                                                       IF_DISST        = IF_DISST
                                                             importing EF_SKIP         = EF_SKIP ).

  endmethod.
  method GET_ACTDAT_PURIR.

    data: LF_LAND1_L type LAND1_GP,
          LF_LAND1_T type LAND1,
          LF_BUKRS   type BUKRS,
          LF_STCD1   type STCD1,
          LF_XEGLD   type XEGLD.

    field-symbols: <S_ACTDAT_PURIR> type ZVCXI_XCM_MBQ_S_ACTDAT_PURIR.
    RR_ACTDAT = SUPER->GET_ACTDAT_PURIR( exporting IR_SVCEL_SOURCE = IR_SVCEL_SOURCE
                                                   IF_SOKEY        = IF_SOKEY
                                                   IF_WERKS        = IF_WERKS
                                                   IF_DISST        = IF_DISST
                                         importing EF_SKIP         = EF_SKIP ).
    check RR_ACTDAT is bound.
    assign RR_ACTDAT->* to <S_ACTDAT_PURIR>.
***    Add Purchase Organization Vendor
    if <S_ACTDAT_PURIR>-LIFNR is not initial.
      select single EKORG into <S_ACTDAT_PURIR>-EKORG
        from LFM1
        where LIFNR = <S_ACTDAT_PURIR>-LIFNR.
    endif.

***GAPVC17 EESPINASSE 05062022 Add Taxe Code but only for purchasing
    if <S_ACTDAT_PURIR>-LIFNR is not initial.
*1.	Read vendor country code – LFA1.LAND1
      select single LAND1 STCD1 into ( LF_LAND1_L, LF_STCD1 )
        from LFA1
        where LIFNR = <S_ACTDAT_PURIR>-LIFNR.
*2.	Read company code country code – T001.LAND1
      select single T001K~BUKRS into LF_BUKRS
      from T001W
      inner join T001K
            on T001K~BWKEY = T001W~BWKEY
       where WERKS = IF_WERKS.
      select single LAND1 from T001 into LF_LAND1_T
        where BUKRS = LF_BUKRS.
*3.	Compare LFA1.LAND1 and T001.LAND1
      if LF_LAND1_L = LF_LAND1_T.
*a.	If the values are the same --> Vendor is National provider
*   i.      Check first character of the Vendor Tax Code LFA1-STCD1
        if LF_STCD1(1) = 'N'.
*     1.  If it starts with “N”, then set EINE.MWSKZ value as “B2”
          <S_ACTDAT_PURIR>-MWSKZ = 'B2'.
        else.
*     2.  Else (all other values), then set EINE.MWSKZ value to “S3”
          <S_ACTDAT_PURIR>-MWSKZ = 'S3'.
        endif.
      else.
*b.	If the values are different --> check for European or International
*  i.      Read T005.XEGLD (Indicator: European Union Member?), and check its value:
        select single XEGLD from T005 into LF_XEGLD
          where LAND1 = LF_LAND1_L.
*     1.  If T005.XEGLD equal “X”, then vendor is European therefore set EINE.MWSKZ to “A3”
        if LF_XEGLD = ABAP_TRUE.
          <S_ACTDAT_PURIR>-MWSKZ = 'A3'.
        else.
*     2.  If T005.XEGLD is blank, the vendor is International therefore set EINE.MWSKZ to “I3”
          <S_ACTDAT_PURIR>-MWSKZ = 'I3'.
        endif.
      endif.
    endif.
  endmethod.
  method GET_ACTDAT_TLR.

    data: LS_TD01 type YHP_SMD_MBQ_TD01.
    data: LR_SVC             type ref to /VCXI/CL_VKSRA_SVC,
          LR_FG_SVCEL_SOURCE type ref to /VCXI/CL_VKSRA_SVC_ELEMENT,
          LR_VALEX           type ref to DATA.
    field-symbols: <S_ACTDAT_TLR> type YHP_SMD_MBQ_S_ACTDAT_TLR,
                   <S_VALEX>      type ANY,
                   <F_LEGMA>      type ANY,
                   <F_TLORD>      type ANY,
                   <F_SOCTR>      type ANY,
   <F_PROHE>      type ANY,
 <F_PROHE_UOM>  type ANY.


***--------------------------------------------------------------------------------------
*** Create Data Reference
    create data RR_ACTDAT type YHP_SMD_MBQ_S_ACTDAT_TLR.
    assign RR_ACTDAT->* to <S_ACTDAT_TLR>.

***--------------------------------------------------------------------------------------
*** Define Control Data
    <S_ACTDAT_TLR>-SOKEY        = IF_SOKEY.
    <S_ACTDAT_TLR>-WERKS        = IF_WERKS.
    <S_ACTDAT_TLR>-S_SVCAD      = ME->GET_SVCAD( IR_SVCEL_SOURCE = IR_SVCEL_SOURCE ).

*** Find Source Key of related FG
    LR_FG_SVCEL_SOURCE = YCL_HP_SVC_SRV_VE=>GET_SVCEL_RELATED( IF_SVCEL_ID = 'CFG'
                                                                             IR_SVCEL    = IR_SVCEL_SOURCE ).
*    IRC 27/06/2023 "HEDK926141
    CHECK LR_FG_SVCEL_SOURCE IS NOT INITIAL.

    <S_ACTDAT_TLR>-FG_LEVEL_SOURCE = LR_FG_SVCEL_SOURCE->GET_LEVEL( ).

*** Define Control Data - Tool data: Tool Order and Sales Order Control
    YCL_HP_SMD_MBQ_CUST=>GET_TLCNF( exporting IF_SOKEY = IF_SOKEY
                                    importing ES_TD01  = LS_TD01 ).

    LR_SVC = ME->R_BOB_SDOC->GET_SVC( IF_POSNR = ME->F_POSNR ).
    if  LR_SVC is bound.
      LR_VALEX = LR_SVC->GET_VALEX( IF_IVEMO       = LS_TD01-IVEMO
                                    IR_SVC_ELEMENT = IR_SVCEL_SOURCE ).
      if LR_VALEX is bound.
        assign LR_VALEX->* to <S_VALEX>.
      endif.
    endif.

***
*JBL 02/12/2022 Get PROHE value from CRBPX
  assign component 'PROHE' of structure <S_VALEX> to <F_PROHE>.
    if SY-SUBRC is initial.
     move if_dimen-PROHE to <f_prohe>.
    endif.
  assign component 'PROHE_UOM' of structure <S_VALEX> to <F_PROHE_UOM>.
    if SY-SUBRC is initial.
    move if_dimen-PROHE_UOM to <f_prohe_uom>.
    endif.
    assign component 'LEGMA' of structure <S_VALEX> to <F_LEGMA>.
    if SY-SUBRC is initial.
      if <F_LEGMA> is not initial.
        assign component 'TLORD' of structure <S_VALEX> to <F_TLORD>.
        if SY-SUBRC is initial.
          clear <F_TLORD>.
        endif.
        assign component 'SOCTR' of structure <S_VALEX> to <F_SOCTR>.
        if SY-SUBRC is initial.
          clear <F_SOCTR>.
        endif.
      endif.
    endif.

    move-corresponding <S_VALEX> to <S_ACTDAT_TLR>.

  endmethod.
  method GET_LEGMA.

FIELD-SYMBOLS:
               <f_legma> TYPE matnr.

    assign component 'LEGMA' of structure IS_VALEX to <F_LEGMA>.
    check SY-SUBRC is initial.
    RF_LEGMA = <F_LEGMA>.

  endmethod.
  method GET_LOVC.

    RT_VALUE = SUPER->GET_LOVC( IR_SVCEL_SOURCE = IR_SVCEL_SOURCE
                                IF_SOKEY        = IF_SOKEY
                                IF_WERKS        = IF_WERKS ).

    case IF_SOKEY.
***   Print Set Tool
      when C_SOKEY_TOOL_SET_PRINT.
        RT_VALUE = ME->GET_LOVC_TOOL_PRINT( IR_SVCEL_SOURCE = IR_SVCEL_SOURCE
                                            IF_SOKEY        = IF_SOKEY
                                            IF_WERKS        = IF_WERKS ).
    endcase.

  endmethod.
  method GET_LOVC_PURCH.

    data: LR_SVCEL_CRPROC TYPE REF TO /VCXI/CL_VKSRA_SVC_ELEMENT,
          LR_EA_MS_CRPROC type ref to ZCL_VCXI_P1SS_EA_MS_CRPROC.

*** Get Procurement
    LR_SVCEL_CRPROC  = YCL_HP_SVC_SRV_VE=>GET_SVCEL_RELATED( IF_SVCEL_ID = C_ISVCE_MS_CRPROC
                                                             IR_SVCEL    = IR_SVCEL_SOURCE ).

    check LR_SVCEL_CRPROC is bound.
    LR_EA_MS_CRPROC ?= LR_SVCEL_CRPROC->GET_CLSEA( ).
***     Get Purchasing Information
    LR_EA_MS_CRPROC->GET_PURCH_INFO( importing EF_PURCH  = RF_PURCH ).

  endmethod.
  method GET_LOVC_TOOL_DCT.

    RT_VALUE = SUPER->GET_LOVC_TOOL_DCT( IR_SVCEL_SOURCE = IR_SVCEL_SOURCE
                                         IF_SOKEY        = IF_SOKEY
                                         IF_WERKS        = IF_WERKS ).

    ME->GET_LOVC_TOOL_VALUE( exporting IR_SVCEL_SOURCE = IR_SVCEL_SOURCE
                                       IF_SOKEY        = IF_SOKEY
                             changing  CT_VALUE        = RT_VALUE ).

  endmethod.
  method GET_LOVC_TOOL_PRINT.

    data: LT_CABN type TT_CABN.
    data: LR_BOB_MAT type ref to ZCL_VCXI_XCI_BOB_MAT.


***--------------------------------------------------------------------------------------
*** Get Material Business Object of Reference Material
    ZCL_VCXI_XCM_MBQ_CUST=>READ_REFMAT( exporting IF_SOKEY   = IF_SOKEY
                                                  IF_WERKS   = IF_WERKS
                                        importing ER_BOB_MAT = LR_BOB_MAT ).

*** Get LO-VC values of Reference
    RT_VALUE = LR_BOB_MAT->GET_LOVC( ).

*** Get Print Set Tool Values
    ME->GET_LOVC_TOOL_VALUE( exporting IR_SVCEL_SOURCE = IR_SVCEL_SOURCE
                                       IF_SOKEY        = IF_SOKEY
                             changing  CT_VALUE        = RT_VALUE ).

  endmethod.
  method GET_LOVC_TOOL_VALUE.

    data: LS_TD01 type YHP_SMD_MBQ_TD01,
          LS_TD02 type YHP_SMD_MBQ_TD02.
    data: LT_TD02 type YHP_SMD_MBQ_TT_TD02.
    data: LR_SVC   type ref to /VCXI/CL_VKSRA_SVC,
          LR_VALEX type ref to DATA.
    field-symbols: <S_VALEX> type ANY,
                   <S_VALUE> type /VCXI/VKCR_S_VALUE,
                   <F_FLDVA> type ANY,
                   <F_FLDUN> type ANY,
                   <F_FLDCU> type ANY.


*** Get Customizing for Tool
    YCL_HP_SMD_MBQ_CUST=>GET_TLCNF( exporting IF_SOKEY = IF_SOKEY
                                    importing ES_TD01  = LS_TD01
                                              ET_TD02  = LT_TD02 ).

*** SmartVC Value Extraction for Tool
    LR_SVC = ME->R_BOB_SDOC->GET_SVC( IF_POSNR = ME->F_POSNR ).
    if  LR_SVC is bound.
      LR_VALEX = LR_SVC->GET_VALEX( IF_IVEMO       = LS_TD01-IVEMO
                                    IR_SVC_ELEMENT = IR_SVCEL_SOURCE ).
      if LR_VALEX is bound.
        assign LR_VALEX->* to <S_VALEX>.
      endif.
    endif.

*** Populate values for all LO-VC characteristics
    loop at CT_VALUE assigning <S_VALUE>.

      read table LT_TD02
        into LS_TD02
        with key ATNAM = <S_VALUE>-ATNAM.

      check SY-SUBRC eq 0.

      <S_VALUE>-ATAUT = 8.
      <S_VALUE>-ATCOD = 1.

***   Add Characteristic value Character
      assign component LS_TD02-FLDVA of structure <S_VALEX> to <F_FLDVA>.
      if SY-SUBRC ne 0.
***     Field for Value &1 not found in VC Value Extraction ID &2.
        /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( IF_MSGID = 'YHP_SMD_MBQ'
                                              IF_MSGTY = 'E'
                                              IF_MSGNO = '008'
                                              IF_MSGV1 = LS_TD02-FLDVA
                                              IF_MSGV2 = LS_TD01-IVEMO ).
      endif.

***   In case of legacy material (migrated tool) we shouldn't map these values
      if ME->GET_LEGMA( <S_VALEX> ) is not initial.
        case <S_VALUE>-ATNAM.
          when 'HP_TLORD'.
            continue.
          when 'HP_SOCTR'.
            continue.
        endcase.
      endif.

      case <S_VALUE>-ATFOR.
        when 'CHAR'.

***       Add Characteristic value Character
          <S_VALUE>-ATWRT = <F_FLDVA>.

        when 'NUM'.

***       Add Characteristic value Numeric
          try.
              <S_VALUE>-ATFLV = <F_FLDVA>.
            catch CX_SY_CONVERSION_NO_NUMBER.
***           Characteristic &1 value &2 is not numeric.
              /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( IF_MSGID = 'YHP_SMD_MBQ'
                                                    IF_MSGTY = 'E'
                                                    IF_MSGNO = '003'
                                                    IF_MSGV1 = LS_TD02-ATNAM
                                                    IF_MSGV2 = <F_FLDVA> ).
          endtry.
*          assign component LS_TD02-FLDUN of structure <S_VALEX> to <F_FLDUN>.
*          if <S_VALUE>-UNIT is not initial.
*            try.
*                ZCL_VCXI_XCS_SERVICE_UNIT=>CONVERT_UNIT( exporting IF_UOM_OUT   = <S_VALUE>-UNIT
*                                                                   IF_UOM_IN    = <F_FLDUN>
*                                                                   IF_VALUE_IN  = <S_VALUE>-ATFLV
*                                                         importing EF_VALUE_OUT = <S_VALUE>-ATFLV ).
*              catch /VCXI/CX_CKX.
****             Characteristic &1 conversion failed for Value &2 Unit from &3 Unit to &4.
*                /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( IF_MSGID = 'YHP_SMD_MBQ'
*                                                      IF_MSGTY = 'E'
*                                                      IF_MSGNO = '004'
*                                                      IF_MSGV1 = LS_TD02-ATNAM
*                                                      IF_MSGV2 = <F_FLDVA>
*                                                      IF_MSGV3 = <F_FLDUN>
*                                                      IF_MSGV4 = <S_VALUE>-UNIT ).
*            endtry.
*          endif.

        when 'CURR'.

***       Add Characteristic value Currency
          try.
              <S_VALUE>-ATFLV = <F_FLDVA>.
            catch CX_SY_CONVERSION_NO_NUMBER.
***           Characteristic &1 value &2 is not numeric.
              /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( IF_MSGID = 'YHP_SMD_MBQ'
                                                    IF_MSGTY = 'E'
                                                    IF_MSGNO = '003'
                                                    IF_MSGV1 = LS_TD02-ATNAM
                                                    IF_MSGV2 = <F_FLDVA> ).
          endtry.
*          assign component LS_TD02-FLDCU of structure <S_VALEX> to <F_FLDCU>.
*          if <S_VALUE>-CURKY ne <F_FLDCU>.
****         Characteristic &1 unit &2 is not allowed.
*            /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( IF_MSGID = 'YHP_SMD_MBQ'
*                                                  IF_MSGTY = 'E'
*                                                  IF_MSGNO = '006'
*                                                  IF_MSGV1 = LS_TD02-ATNAM
*                                                  IF_MSGV2 = <F_FLDCU> ).
*          endif.

        when others.
***       Characteristic &1 has unsupported characteristic type &2.
          /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( IF_MSGID = 'YHP_SMD_MBQ'
                                                IF_MSGTY = 'E'
                                                IF_MSGNO = '005'
                                                IF_MSGV1 = LS_TD02-ATNAM
                                                IF_MSGV2 = <S_VALUE>-ATFOR ).

      endcase.

***   For migrated tools we create them with Released status
      if ME->GET_LEGMA( <S_VALEX> ) is not initial and <S_VALUE>-ATNAM = 'HP_TOSTA'.
        <S_VALUE>-ATWRT = YCL_RF_TPM_TOOL=>C_TOSTA_YREL.
      endif.
*JBL 05/12/2022 Get Prohe like in ACH
data lr_clsea TYPE REF TO /VCXI/CL_VKSR_CLSEA.
data lr_eastep type ref to ZCL_VCXI_P1SS_EA_CSTEP.
data LS_PRODIM type ZVCXI_PXS_S_PRODIM.
 lr_clsea = IR_SVCEL_SOURCE->GET_CLSEA( ).
TRY.
    lr_eastep ?=  lr_clsea.
data(LR_BL_CRBPROP) = LR_EASTEP->GET_CRBPX_NEXT( ).
clear ls_prodim.
     LS_PRODIM = LR_BL_CRBPROP->S_PRODIM.
  CATCH cx_sy_move_cast_error.
ENDTRY.
    case <S_VALUE>-ATNAM.
    when 'HP_PROHE'.
   <S_VALUE>-ATFLV = ls_prodim-PROHE.
  <s_value>-unit = ls_prodim-PROhE_UOM.
    endcase.
    endloop.

  endmethod.
  method GET_MATDAT_MARM.
    data: LS_PARAM type /VCXI/VKSR_S_PARAM,
          LS_CALCV type ZVCXI_XCS_S_CALCV.
    data: LR_SVCEL    type ref to /VCXI/CL_VKSRA_SVC_ELEMENT.

    field-symbols: <S_MATDAT_MARM> type ZVCXI_XCM_MBQ_S_MATDAT_MARM.

*{   REPLACE        HEDK925765
***    SUPER->GET_MATDAT_MARM( exporting IR_SVCEL_SOURCE     = IR_SVCEL_SOURCE
***                            receiving RT_MATDAT_MARM      = RT_MATDAT_MARM ).
    SUPER->GET_MATDAT_MARM( exporting IR_SVCEL_SOURCE     = IR_SVCEL_SOURCE
                                      IF_TMATN            = IF_TMATN
                            receiving RT_MATDAT_MARM      = RT_MATDAT_MARM ).
*}   REPLACE

*** Get Element API Reference
    data(LR_CLSEA) = IR_SVCEL_SOURCE->GET_CLSEA( ).
    check LR_CLSEA is bound.
*** Type CFG Finished good
    try.
        data(LR_EA_CFG) = cast YCL_HP_SVCS_EA_CFG( LR_CLSEA ) .
*** Get Area and Weight
        LR_SVCEL = YCL_HP_SVC_SRV_VE=>GET_SVCEL_RELATED_CHILD( IF_SVCEL_ID = C_ISVCE_AREA_WGT
                                                               IR_SVCEL    = IR_SVCEL_SOURCE ).
        if LR_SVCEL is bound.
*** Sold Blank Area
          LS_PARAM = LR_SVCEL->GET_PARAM( IF_PARID = 'ZVCXI_PX_SBLAR' ).

          if LS_PARAM-ATFLV is not initial and LS_PARAM-UNIT is not initial.
***       Add Conversion to M2 for Semi finished good "COMMENT BY IRODRIGUEZ BY OPHJVC-289 21/03/2023
*            append value #( MEINH = LS_PARAM-UNIT ) to RT_MATDAT_MARM assigning <S_MATDAT_MARM>.
*            LS_CALCV-CALCV = LS_PARAM-ATFLV.
*            LS_CALCV-CALCV_UOM = LS_PARAM-UNIT.
*            ZCL_VCXI_XCS_SERVICE_UNIT=>CALC_UMREZ_UMREN( exporting IS_CALCV_BASE = value #( CALCV     = 1
*                                                                                            CALCV_UOM = 'PC' )
*                                                                   IS_CALCV_ALT  = LS_CALCV
*                                                         importing EF_UMREZ      = <S_MATDAT_MARM>-UMREZ
*                                                                   EF_UMREN      = <S_MATDAT_MARM>-UMREN ).  ""COMMENT BY IRODRIGUEZ BY OPHJVC-289

          endif.
        endif.
      catch CX_SY_MOVE_CAST_ERROR.
***   Type CSFG Semi finished good
        try.
            data(LR_EA_CRCONO) = cast ZCL_VCXI_P1SS_EA_CRCONO( LR_CLSEA ) .
*** Get Area and Weight
            LR_SVCEL = YCL_HP_SVC_SRV_VE=>GET_SVCEL_RELATED_CHILD( IF_SVCEL_ID = C_ISVCE_AREA_WGT
                                                                   IR_SVCEL    = IR_SVCEL_SOURCE ).
            if LR_SVCEL is bound.
*** Sold Blank Area
              LS_PARAM = LR_SVCEL->GET_PARAM( IF_PARID = 'ZVCXI_PX_SBLAR' ).

              if LS_PARAM-ATFLV is not initial and LS_PARAM-UNIT is not initial.
***       Add Conversion to M2 for Semi finished good
                append value #( MEINH = LS_PARAM-UNIT ) to RT_MATDAT_MARM assigning <S_MATDAT_MARM>.
                LS_CALCV-CALCV = LS_PARAM-ATFLV.
                LS_CALCV-CALCV_UOM = LS_PARAM-UNIT.
                ZCL_VCXI_XCS_SERVICE_UNIT=>CALC_UMREZ_UMREN( exporting IS_CALCV_BASE = value #( CALCV     = 1
                                                                                                CALCV_UOM = 'PC' )
                                                                       IS_CALCV_ALT  = LS_CALCV
                                                             importing EF_UMREZ      = <S_MATDAT_MARM>-UMREZ
                                                                       EF_UMREN      = <S_MATDAT_MARM>-UMREN ).

              endif.
            endif.
          catch CX_SY_MOVE_CAST_ERROR.
        endtry.
    endtry.

  endmethod.
  method GET_WEIGHTS.


    data: LS_SHTLE   type ZVCXI_P0SS_S_SHTLE,
          LS_SHTWI   type ZVCXI_P0SS_S_SHTWI,
          LS_CALCV_A type ZVCXI_XCS_S_CALCV,
          LS_CALCV_W type ZVCXI_XCS_S_CALCV,
          LS_PARAM   type /VCXI/VKSR_S_PARAM,
          LS_SURWT   type ZVCXI_PXS_S_SURWT.
    data: LR_CLSEA type ref to /VCXI/CL_VKSR_CLSEA,
          LR_SVCEL type ref to /VCXI/CL_VKSRA_SVC_ELEMENT.
    field-symbols: <S_MATDAT_MARM> type ZVCXI_XCM_MBQ_S_MATDAT_MARM.

***-------------------------------------------------------------------*
*** Try to get it from Area/Weight node
    LR_SVCEL = YCL_HP_SVC_SRV_VE=>GET_SVCEL_RELATED_CHILD( IF_SVCEL_ID = C_ISVCE_AREA_WGT
                                                           IR_SVCEL    = IR_SVCEL ).
    if LR_SVCEL is bound.

*** Net Blank Weight -> Net Weight
      EF_NTGEW = LR_SVCEL->GET_PARAM( IF_PARID = 'ZVCXI_PX_NBLWT' )-ATFLV.

*** Sold Blank Weight -> Gross Weight
      LS_PARAM = LR_SVCEL->GET_PARAM( IF_PARID = 'ZVCXI_PX_SBLWT' ).
      EF_BRGEW = LS_PARAM-ATFLV.
      EF_GEWEI = LS_PARAM-UNIT.

    else.

***-------------------------------------------------------------------*
*** No Area/Weight node --> MS Procurement
      LR_CLSEA = IR_SVCEL->GET_CLSEA( ).
      check LR_CLSEA is bound.

      try.

***     Get Sheet Length/Width + Surface Weight
          data(LR_EA_MS_CRPROC) = cast ZCL_VCXI_P1SS_EA_MS_CRPROC( LR_CLSEA ) .
          LR_EA_MS_CRPROC->GET_CRBPX( )->GET_SHTDIM( importing ES_SHTLE = LS_SHTLE
                                                               ES_SHTWI = LS_SHTWI ).
          LS_SURWT = LR_EA_MS_CRPROC->GET_SURWT( ).

          if LS_SHTLE-SHTLE_UOM is not initial and
             LS_SHTWI-SHTWI_UOM is not initial.

***         Calculate Area
            ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_SHTLE-SHTLE
                                                                IF_CALCV_UOM_1 = LS_SHTLE-SHTLE_UOM
                                                                IF_CALCV_2     = LS_SHTWI-SHTWI
                                                                IF_CALCV_UOM_2 = LS_SHTWI-SHTWI_UOM
                                                                IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_MULTIPLY
                                                      importing EF_CALCV       = LS_CALCV_A-CALCV
                                                                EF_CALCV_UOM   = LS_CALCV_A-CALCV_UOM ).

***         Calculate Weight of sheet
            ZCL_VCXI_XCS_SERVICE_CALC=>CALC_WITH_ANY( exporting IF_CALCV_1     = LS_CALCV_A-CALCV
                                                                IF_CALCV_UOM_1 = LS_CALCV_A-CALCV_UOM
                                                                IF_CALCV_2     = LS_SURWT-SURWT
                                                                IF_CALCV_UOM_2 = LS_SURWT-SURWT_UOM
                                                                IF_OPERA       = ZCL_VCXI_XCS_SERVICE_CALC=>C_OPERA_MULTIPLY
                                                      importing EF_CALCV       = LS_CALCV_W-CALCV
                                                                EF_CALCV_UOM   = LS_CALCV_W-CALCV_UOM ).

            move LS_CALCV_W-CALCV      to: EF_BRGEW, EF_NTGEW.
            move LS_CALCV_W-CALCV_UOM  to  EF_GEWEI.

          endif.
        catch CX_SY_MOVE_CAST_ERROR.
      endtry.
    endif.

  endmethod.
