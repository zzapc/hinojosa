
class YCL_HP_SVCS_UI_CFG definition
  public
  inheriting from ZCL_VCXI_P1SS_UI_CFG
  create public .

public section.

  data S_CFG_VIS_HP type YHP_SVCS_S_CFG_VIS .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
      !IF_TABPT type ZVCXI_XCS_TABPT default '0100S_CFG_TAB-TAB'
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
protected section.

  data R_PR_CFG_HP type ref to YCL_HP_SVCS_PR_CFG .

  methods INIT_VIS
    redefinition .
private section.
endclass. "YCL_HP_SVCS_UI_CFG definition
class YCL_HP_SVCS_UI_CFG implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).

    call function 'YHP_SVCS_UI_CFG_BIND'
      exporting
        IR_UI_CFG = ME
      importing
        ES_DYNP   = ME->S_DYNP
        EF_ACTIVE_TAB = ME->F_ACTIVE_TAB.

  endmethod.
  method CONSTRUCTOR.
    SUPER->CONSTRUCTOR( IR_UIFW   = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE   = IR_GATE
                        IF_TABPT  = IF_TABPT ).

*** Assign th RP Class to the UI Class
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_CFG_HP.
      catch CX_SY_MOVE_CAST_ERROR.

***     UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DO_UPDATE_VIS.

    data: LS_CFG_VIS  type YHP_SVCS_S_CFG_VIS.

    SUPER->DO_UPDATE_VIS( IS_VIS = IS_VIS ).

*** "Cast" Vis Structure
    move-corresponding IS_VIS to LS_CFG_VIS.

*** Set Product dimension display  option
    if ME->S_CFG_VIS_HP-PROPT ne LS_CFG_VIS-PROPT.
      ME->R_PR_CFG_HP->SET_PROPT( IF_PROPT = LS_CFG_VIS-PROPT
                                  IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Side flat for agriculture boxes
    if ME->S_CFG_VIS_HP-S_GUALDERA ne LS_CFG_VIS-S_GUALDERA.
      ME->R_PR_CFG_HP->SET_GUALDERA( IS_GUALDERA = LS_CFG_VIS-S_GUALDERA
                                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method INIT_VIS.
    data: LS_PRODIM type ZVCXI_PXS_S_PRODIM.

    SUPER->INIT_VIS( ).

    check ME->R_PR_CFG_HP is bound.

** Get product dimension display option
    ME->S_CFG_VIS_HP-PROPT = ME->R_PR_CFG_HP->GET_PROPT( ).

** Get side flap for architecture boxes
    ME->S_CFG_VIS_HP-S_GUALDERA = ME->R_PR_CFG_HP->GET_GUALDERA( ).

** Get product dimension Based on display option
    LS_PRODIM-S_PROLE = ME->S_CFG_VIS-S_PROLE.
    LS_PRODIM-S_PROWI = ME->S_CFG_VIS-S_PROWI.
    LS_PRODIM-S_PROHE = ME->S_CFG_VIS-S_PROHE.
    YCL_HP_X_SRV_VCDIM=>CONVERT_DIMENSION_TO_DISPLAY( EXPORTING IF_PR_CFG = ME->R_PR_CFG_HP
                                                      IF_FLUTE =  ME->S_CFG_VIS-FLUTE
                                                      IF_FEFCO =  ME->S_CFG_VIS-FEFCO
                                                      IS_CALIP =  ME->S_CFG_VIS-S_CALIP
                                            CHANGING  CF_PROPT =  ME->S_CFG_VIS_HP-PROPT
                                                      CS_PRODIM = LS_PRODIM ).
    MOVE-CORRESPONDING LS_PRODIM to ME->S_CFG_VIS.


  endmethod.
