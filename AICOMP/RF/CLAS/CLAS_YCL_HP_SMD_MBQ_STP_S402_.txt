
class YCL_HP_SMD_MBQ_STP_S402 definition
  public
  inheriting from ZCL_VCXI_XCM_MBQ_STP_S402
  final
  create public .

public section.

  methods EXECUTE
    redefinition .
protected section.
private section.
endclass. "YCL_HP_SMD_MBQ_STP_S402 definition
class YCL_HP_SMD_MBQ_STP_S402 implementation.
  method EXECUTE.

    data: LS_MATLI   type ZVCXI_XCM_MBQ_S_MATLI.
    data:LR_BOB_MAT type ref to ZCL_VCXI_XCI_BOB_MAT.
    data: LR_ACTDAT  type ref to DATA.
    field-symbols: <S_ACTDAT_CNF> type ZVCXI_XCM_MBQ_S_ACTDAT_CNF.
    data: LT_BAPIRET2 type BAPIRETTAB.

    try.
        SUPER->EXECUTE( ).

      catch /VCXI/CX_CKX into data(LR_CX_CKX).
***     Add Message to Message Log
        ME->R_RPSEG->R_MSG->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

    try.
***     Get Material List Entry of active Material List Entry
        LS_MATLI = ME->R_DCO_INPUT->T_MATLI[ MLCNT = ME->R_DCO_INPUT->F_MLCNT ].

***     Check Activity - Create/Update Materials Configuration
        if not LINE_EXISTS( LS_MATLI-T_ACTCON[ ACTID = ZCL_VCXI_XCM_MBQ_CUST=>C_ACTID_CNF ] ).
***       Set Status to CANCELED
          ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_CANCELED ).
          return.
        endif.

***     Load Activity Control Data
        LR_ACTDAT = LS_MATLI-T_ACTCON[ ACTID = ZCL_VCXI_XCM_MBQ_CUST=>C_ACTID_CNF ]-R_ACTDAT.
        assign LR_ACTDAT->* to <S_ACTDAT_CNF>.

***--------------------------------------------------------------------------------------
***     Load Material Business Object
        create object LR_BOB_MAT.
        LR_BOB_MAT->INIT_FROM_DB( exporting IF_MATNR    = LS_MATLI-MATNR
                                            IF_WERKS    = <S_ACTDAT_CNF>-WERKS
                                  importing ET_BAPIRET2 = LT_BAPIRET2 ).

*** Trigger DMS DOCUMENT Linking by MM Exit
        LR_BOB_MAT->SAVE( ).

*** Perform Save and Commit
        ME->R_RPSEG->SAVE_AND_COMMIT( ).
      catch /VCXI/CX_CKX into LR_CX_CKX.
***     Add Message to Message Log

        ME->R_RPSEG->R_MSG->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

  endmethod.
endclass. "YCL_HP_SMD_MBQ_STP_S402 implementation
