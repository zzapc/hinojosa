
class YCL_HP_SMD_MBQ_STP_S101 definition
  public
  inheriting from ZCL_VCXI_PXM_MBQ_STP_S101
  final
  create public .

public section.
protected section.

  methods ANALYZE_CONFIG
    redefinition .
private section.
endclass. "YCL_HP_SMD_MBQ_STP_S101 definition
class YCL_HP_SMD_MBQ_STP_S101 implementation.
  method ANALYZE_CONFIG.

    data: LF_FGCNT type I.
    data: LS_XCOMP_BUFFER type YS_XCOMP_BUFFER.
    data: LT_MATLI type ZVCXI_XCM_MBQ_TT_MATLI,
          LT_DMSLI type ZVCXI_XCM_MBQ_TT_DMSLI.
    data: LR_BL_XCOMP type ref to ZCL_VCXI_PXM_MBQ_BL_XCOMP.
    field-symbols: <S_MATLI> type ZVCXI_XCM_MBQ_S_MATLI,
                   <S_DMSLI> type ZVCXI_XCM_MBQ_S_DMSLI.


***--------------------------------------------------------------------------------------
*** Analyze Specification Overview (incl. Dispo Level for Components)
    ME->ANALYZE_SOV( IR_SVCEL_SOV = ME->GET_SVCEL_SOV( IR_SVC = IR_SVC ) ).

***--------------------------------------------------------------------------------------
*** Analyze X-Components
    sort ME->T_XCOMP_BUFFER by DISST descending.
    loop at ME->T_XCOMP_BUFFER into LS_XCOMP_BUFFER.
***   Trigger Analyzes in "Sub-Class"
      LR_BL_XCOMP = ZCL_VCXI_PXM_MBQ_BL_XCOMP=>FACTORY( IR_BOB_SDOC    = ME->R_DCO_INPUT->R_BOB_SDOC
                                                        IF_POSNR       = ME->R_DCO_INPUT->F_POSNR
                                                        IR_SVCEL_XCOMP = LS_XCOMP_BUFFER-R_SVCEL_XCOMP ).
      LR_BL_XCOMP->ANALYZE( exporting IF_DISST = LS_XCOMP_BUFFER-DISST
                            importing ET_MATLI = LT_MATLI
                                      ET_DMSLI = LT_DMSLI ).

***   Add to overall material list and assign unique counter
      loop at LT_MATLI assigning <S_MATLI>.
        <S_MATLI>-MLCNT = LINES( ET_MATLI ) + 1.
        insert <S_MATLI> into table ET_MATLI.
        if <S_MATLI>-DISST eq 0.
*{   REPLACE        HEDK918895                                        1
*\          LF_FGCNT += 1.
          LF_FGCNT = LF_FGCNT + 1.
*}   REPLACE
        endif.
      endloop.

***   Add to overall DMS Document list and assign unique counter
      loop at LT_DMSLI assigning <S_DMSLI>.
        <S_DMSLI>-DLCNT = LINES( ET_DMSLI ) + 1.
        insert <S_DMSLI> into table ET_DMSLI.
      endloop.
    endloop.

***--------------------------------------------------------------------------------------
*** Add Sales BOM if more than 1 Finished Good has been configured
    if LF_FGCNT gt 1.
***   Increase DISST by 1
      loop at ET_MATLI assigning <S_MATLI>.
*{   REPLACE        HEDK918895                                        2
*\        <S_MATLI>-DISST += 1.
        <S_MATLI>-DISST = <S_MATLI>-DISST + 1.
*}   REPLACE

      endloop.

***   Add Material List Entry for Sales BOM
      ME->ADD_MATLI_SB(
                        "exporting IF_SOKEY = 'PX_SB'      "Sales BOM
                        changing  CT_MATLI = ET_MATLI ).
    endif.

***--------------------------------------------------------------------------------------
*** Create Position for Main Materials
    loop at ET_MATLI assigning <S_MATLI> where DISST eq 0.
      append value #( ACTID = ZCL_VCXI_XCM_MBQ_CUST=>C_ACTID_SDPOS ) to <S_MATLI>-T_ACTCON.
    endloop.

  endmethod.
endclass. "YCL_HP_SMD_MBQ_STP_S101 implementation
