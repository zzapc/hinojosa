
class YCL_HP_SMD_MBQ_BOB_CMIR definition
  public
  inheriting from ZCL_VCXI_XCI_BOB_CMIR
  create public .

public section.

  data S_EXT type YHP_SMD_MBQ_S_ACTDAT_CMIR .

  methods SET_EXT
    importing
      !IS_EXT type YHP_SMD_MBQ_S_ACTDAT_CMIR
    raising
      /VCXI/CX_CKX .

  methods UPDATE
    redefinition .
protected section.
private section.
endclass. "YCL_HP_SMD_MBQ_BOB_CMIR definition
class YCL_HP_SMD_MBQ_BOB_CMIR implementation.
  method SET_EXT.
    ME->S_EXT = IS_EXT.
  endmethod.
  method UPDATE.
    data: LS_KNMT     type KNMT,
          LS_XKNMT    type VKNMT,
          LS_YKNMT    type VKNMT,
          LS_TCATALOG type TCATALOG.
    data: LT_XKNMT    type standard table of VKNMT,
          LT_YKNMT    type standard table of VKNMT,
          LT_TCATALOG type standard table of TCATALOG,    "Longtext, not update/created here
          LT_PRE01    type standard table of PRE01.

***--------------------------------------------------------------------------------------
*** Enqueue Customer-Material Info Record
    ME->ENQUEUE( ).

*** Get current Customer-Material Info Record
    LS_KNMT = ME->GET_KNMT( IF_KDMAT = IF_KDMAT ).
    if LS_KNMT-MATNR is not initial and
       LS_KNMT-MATNR ne IF_MATNR.
***   Customer material &1 has already been used in info &2
      /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( IF_MSGID = 'VS'
                                            IF_MSGTY = 'E'
                                            IF_MSGNO = '003'
                                            IF_MSGV1 = IF_KDMAT
                                            IF_MSGV2 = LS_KNMT-MATNR ).

    endif.

***--------------------------------------------------------------------------------------
*** Prepare parameters for customer info record save

    if LS_KNMT is not initial.
***   Old Entry
      move-corresponding LS_KNMT to LS_YKNMT.
      append LS_YKNMT to LT_YKNMT.
    endif.

    if LS_KNMT is initial.
***   New Entry
      LS_XKNMT-MANDT = SY-MANDT.
      LS_XKNMT-VKORG = ME->F_VKORG.
      LS_XKNMT-VTWEG = ME->F_VTWEG.
      LS_XKNMT-KUNNR = ME->F_KUNNR.
      LS_XKNMT-MATNR = IF_MATNR.
      LS_XKNMT-ERNAM = SY-UNAME.
      LS_XKNMT-ERDAT = SY-DATUM.

      LS_XKNMT-KDMAT = IF_KDMAT.
      LS_XKNMT-UPDKZ = 'I'.

*** Extension fields.
      move-corresponding ME->S_EXT to LS_XKNMT.
    else.
***   Update Entry
      move-corresponding LS_KNMT to LS_XKNMT.
      LS_XKNMT-UPDKZ = 'U'.
    endif.

*** Update supplied Values
    if IF_KDPTX is supplied.
      LS_XKNMT-POSTX = IF_KDPTX.
    endif.
    if IF_UNTTO is supplied.
      LS_XKNMT-UNTTO = IF_UNTTO.
    endif.
    if IF_UEBTO is supplied.
      LS_XKNMT-UEBTO = IF_UEBTO.
    endif.

    append LS_XKNMT to LT_XKNMT.

*** Customer Info-Record Save
    call function 'RV_CUSTOMER_MATERIAL_UPDATE'
      in update task
      tables
        XKNMT_TAB     = LT_XKNMT
        YKNMT_TAB     = LT_YKNMT
        TCATALOG_TAB  = LT_TCATALOG
      exceptions
        ERROR_MESSAGE = 1.
    case SY-SUBRC.
      when 1.
***     Error-Message from CALL FUNCTION
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
    endcase.

*** Commit Text
    loop at LT_TCATALOG into LS_TCATALOG where TDOBJECT = 'KNMT'.
      call function 'COMMIT_TEXT'
        exporting
          OBJECT = C_TDOBJECT
          NAME   = LS_TCATALOG-TDNAME
          KEEP   = 'X'.
    endloop.

    commit work and wait.

***--------------------------------------------------------------------------------------
*** Dequeue Customer-Material Info Record
    ME->DEQUEUE( ).

*** Refresh Buffer before checking Customer-Material Info Record
    LT_PRE01 = value #( ( MATNR = IF_MATNR ) ).
    call function 'RV_CUSTOMER_MATERIAL_PRE_READ'
      exporting
        CMP_KUNNR = ME->F_KUNNR
        CMP_VKORG = ME->F_VKORG
        CMP_VTWEG = ME->F_VTWEG
      tables
        MATNR_TAB = LT_PRE01.

    if ME->GET_KNMT( IF_KDMAT = IF_KDMAT )-MATNR ne IF_MATNR.
***   Customer-Material Info Record can't be saved.
      /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( IF_MSGID = 'ZVCXI_XCI'
                                            IF_MSGTY = 'E'
                                            IF_MSGNO = '401' ).
    endif.

  endmethod.
endclass. "YCL_HP_SMD_MBQ_BOB_CMIR implementation
