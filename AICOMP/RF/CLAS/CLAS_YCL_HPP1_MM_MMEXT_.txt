
class YCL_HPP1_MM_MMEXT definition
  public
  inheriting from YCL_HP_MM_MMEXT
  final
  create public .

public section.

  data S_MMEXT type YHPP1_MM_S_MMEXT read-only .

  methods SAVE
    redefinition .
  methods SET_EXTVAL
    redefinition .
  methods UPDATE_BY_XFG
    redefinition .
protected section.

  methods LOAD_FROM_DB
    redefinition .
private section.
endclass. "YCL_HPP1_MM_MMEXT definition
class YCL_HPP1_MM_MMEXT implementation.
  method LOAD_FROM_DB.

    data: LS_TD100 type YHPP1_MM_TD100.


***----------------------------------------------------------------------------
*** Load from SUPER
    SUPER->LOAD_FROM_DB( ).

***----------------------------------------------------------------------------
*** Load Material Master Extension
    select single *
           into LS_TD100
           from YHPP1_MM_TD100
          where MATNR eq ME->F_MATNR
           and  WERKS eq ME->F_WERKS.
    if SY-SUBRC eq 0.
***   Load current values
      ME->S_MMEXT = LS_TD100-S_MMEXT.
    endif.

  endmethod.
  method SAVE.

    data: LS_TD100 type YHPP1_MM_TD100.


*** Save SUPER
    SUPER->SAVE( ).

***----------------------------------------------------------------------------
*** Save basic Material Master Extension
    LS_TD100 = value #( MATNR   = ME->F_MATNR
                        WERKS   = ME->F_WERKS
                        S_MMEXT = ME->S_MMEXT ).
    modify YHPP1_MM_TD100 from LS_TD100.

  endmethod.
  method SET_EXTVAL.

    data: LS_EXTVAL type YHPP1_MM_S_EXTVAL.


    SUPER->SET_EXTVAL( IS_EXTVAL = IS_EXTVAL ).

*** "Cast" Extension Values
    LS_EXTVAL = corresponding #( IS_EXTVAL ).

*** Extension Header
    if ME->S_MMEXT ne LS_EXTVAL-S_MMEXT.
      ME->S_MMEXT = LS_EXTVAL-S_MMEXT.
      raise event /VCXI/IF_CKX_OBJECT~CHANGED.
    endif.

  endmethod.
  method UPDATE_BY_XFG.

    data: LR_EA_CFG     type ref to ZCL_VCXI_P1SS_EA_CFG,
          LR_BL_CRFPROP type ref to ZCL_VCXI_P1SS_BL_CRFPROP,
          LR_BL_CRBPROP type ref to ZCL_VCXI_P1SS_BL_CRBPROP.


***----------------------------------------------------------------------------
*** Get Element API Reference
    try.
        LR_EA_CFG ?= IR_SVCEL->GET_CLSEA( ).
      catch CX_SY_MOVE_CAST_ERROR into data(LR_CX_SY_MOVE_CAST_ERROR).
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( IR_PREVIOUS = LR_CX_SY_MOVE_CAST_ERROR ).
    endtry.
    check LR_EA_CFG is bound.

*** Get Corrugator Forward Property XML (Outbound)
    LR_BL_CRFPROP = LR_EA_CFG->GET_CRFPX( ).

*** Get Corrugator Backward Property XML (Outbound)
    LR_BL_CRBPROP = LR_EA_CFG->GET_CRBPX( ).


***----------------------------------------------------------------------------
*** Set Extension Values
    ME->SET_EXTVAL( IS_EXTVAL = value YHPP1_MM_S_EXTVAL( SABOA       = LR_BL_CRFPROP->F_SABOA
                                                         PRBOA       = LR_BL_CRFPROP->F_PRBOA
                                                         FLUTE       = LR_BL_CRFPROP->F_FLUTE
                                                         S_CALIP     = LR_BL_CRFPROP->S_CALIP
                                                         S_SURWT     = LR_EA_CFG->GET_SURWT( )
                                                         S_HNDDIM    = LR_BL_CRFPROP->S_HNDDIM
                                                         S_COUAR     = LR_BL_CRBPROP->S_COUAR
                                                         T_SCORE_COM = LR_BL_CRBPROP->T_SCORE
                                                         T_JOITY     = LR_BL_CRFPROP->T_JOITY
                                                         T_PRINT_COM = LR_BL_CRFPROP->T_PRINT ) ).

  endmethod.
endclass. "YCL_HPP1_MM_MMEXT implementation
