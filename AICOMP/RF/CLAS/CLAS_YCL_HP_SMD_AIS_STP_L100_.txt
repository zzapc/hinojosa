
class YCL_HP_SMD_AIS_STP_L100 definition
  public
  inheriting from YCL_HP_SMD_AIS_STP_L000
  final
  create public .

public section.

  methods CONSTRUCTOR
    importing
      !IR_RPSEG type ref to /VCXI/CL_CKMR_RPSEG
    raising
      /VCXI/CX_CKX .
protected section.

  data R_DCO_OUTPUT type ref to YCL_HP_SMD_AIS_DCO .
  data R_DCO_INPUT type ref to YCL_HP_SMD_AIS_DCO .

  methods EXECUTE_SUBPROCESS
    redefinition .
  methods GET_SUBPROCESS_DCO
    redefinition .
  methods ANALYZE_RESULT_DCO
    redefinition .
private section.
endclass. "YCL_HP_SMD_AIS_STP_L100 definition
class YCL_HP_SMD_AIS_STP_L100 implementation.
method ANALYZE_RESULT_DCO.

  data: LR_DCO_SUB type ref to YCL_HP_SMD_AIS_DCO.

  field-symbols: <S_WRKLI>     type YHP_SMD_AIS_S_QUOT_WRKLI,
                 <S_WRKLI_SUB> type YHP_SMD_AIS_S_QUOT_WRKLI.

*** Map Resulting DCO of SubProcess
  IR_RPROC->MAP_DCO( changing CR_DCO = LR_DCO_SUB ).
  if LR_DCO_SUB is not bound.
    return.
  endif.

*** Transfer Created SD Document & Item Numbers
  loop at ME->R_DCO_OUTPUT->T_WRKLI assigning <S_WRKLI>.
    read table LR_DCO_SUB->T_WRKLI assigning <S_WRKLI_SUB> with key GROUPING = <S_WRKLI>-GROUPING
                                                                    POSNR    = <S_WRKLI>-POSNR.
    if SY-SUBRC eq 0.
      <S_WRKLI>-VBELN_NEW = <S_WRKLI_SUB>-VBELN_NEW.
      <S_WRKLI>-POSNR_NEW = <S_WRKLI_SUB>-POSNR_NEW.
    endif.
  endloop.

endmethod.
method CONSTRUCTOR.

*** Super Constructor
  SUPER->CONSTRUCTOR( IR_RPSEG = IR_RPSEG ).

*** Map DCO to Required Input/Output
  ME->R_RPSEG->MAP_DCO( changing CR_DCO = ME->R_DCO_INPUT ).
  ME->R_RPSEG->MAP_DCO( changing CR_DCO = ME->R_DCO_OUTPUT ).

endmethod.
method EXECUTE_SUBPROCESS.

  IR_RPROC->/VCXI/IF_CKX_OBJECT_ACTION~HANDLE_ACTION( IF_FCODE = /VCXI/CL_CKMR_RPROC=>C_FCODE_EXECUTE_SUBMIT ).

endmethod.
method GET_SUBPROCESS_DCO.

  data: LT_WRKLI type YHP_SMD_AIS_TT_QUOT_WRKLI.

  data: LR_DCO type ref to YCL_HP_SMD_AIS_DCO.

  field-symbols: <S_WRKLI> type YHP_SMD_AIS_S_QUOT_WRKLI.

*** Sort by Old SD Number
  sort ME->R_DCO_INPUT->T_WRKLI by GROUPING POSNR ascending.

*** Create DCO per Old SD Document
  loop at ME->R_DCO_INPUT->T_WRKLI assigning <S_WRKLI>.

    append <S_WRKLI> to LT_WRKLI.

    at end of GROUPING.
      create object LR_DCO.
      move ME->R_DCO_INPUT->F_FSIDA to LR_DCO->F_FSIDA.
      move ME->R_DCO_INPUT->F_FCALC to LR_DCO->F_FCALC.
      append lines of LT_WRKLI to LR_DCO->T_WRKLI.
      append LR_DCO to RT_DCO.
      free   LR_DCO.
      clear  LT_WRKLI.
    endat.

  endloop.

endmethod.
