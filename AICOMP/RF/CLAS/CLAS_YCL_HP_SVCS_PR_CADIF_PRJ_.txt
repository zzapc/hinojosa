
class YCL_HP_SVCS_PR_CADIF_PRJ definition
  public
  inheriting from ZCL_VCXI_PXSS_PR_CADIF_PRJ
  create public .

public section.

  constants C_PARID_REVRF type /VCXI/VKS_PARID value 'YHP_SVC_REVRF' ##NO_TEXT.

  methods SET_REVRF
    importing
      !IF_REVRF type YHP_SVCS_REVRF
      !IF_SETBY type /VCXI/VKSR_SETBY default 'CALC'
    raising
      /VCXI/CX_CKX .
  methods GET_REVRF
    exporting
      !EF_SETBY type /VCXI/VKSR_SETBY
    returning
      value(RF_REVRF) type YHP_SVCS_REVRF
    raising
      /VCXI/CX_CKX .

  methods GET_XML
    redefinition .
  methods SET_XML
    redefinition .
protected section.
*  constants C_IEVNT_I_RECALC_CADPO type /VCXI/VKSC_IEVNT value 'RECALC_CADPO' ##NO_TEXT.
*  constants C_IEVNT_I_RECALC_FPRJR type /VCXI/VKSC_IEVNT value 'RECALC_FPRJR' ##NO_TEXT.
*  constants C_IEVNT_I_RECALC_FREVR type /VCXI/VKSC_IEVNT value 'RECALC_FREVR' ##NO_TEXT.
*  constants C_IEVNT_I_RECALC_KDMAT type /VCXI/VKSC_IEVNT value 'RECALC_KDMAT' ##NO_TEXT.
*  constants C_IEVNT_I_RECALC_KDPTX type /VCXI/VKSC_IEVNT value 'RECALC_KDPTX' ##NO_TEXT.
*  constants C_IEVNT_I_RECALC_MAKTX type /VCXI/VKSC_IEVNT value 'RECALC_MAKTX' ##NO_TEXT.
*  constants C_IEVNT_I_SET_DEFAULT type /VCXI/VKSC_IEVNT value 'SET_DEFAULT' ##NO_TEXT.
*  methods CHECK_CADDE
*    importing
*      !IR_MESSAGE type ref to /VCXI/CL_CKXM_MESSAGE optional
*    returning
*      value(RF_ELMST) type /VCXI/VKSR_ELMST
*    raising
*      /VCXI/CX_CKX .
*  methods CREATE_CADIF_IO
*    returning
*      value(RR_CADIF_IO) type ref to DATA
*    raising
*      /VCXI/CX_CKX .
*  methods GET_PRVDR_FG
*    exporting
*      !ER_FG type ref to ZIF_VCXI_PXSS_FG
*    returning
*      value(RT_GATE) type /VCXI/VKSR_TT_GATE
*    raising
*      /VCXI/CX_CKX .
*  methods HANDLE_RECALC_CADPO
*    raising
*      /VCXI/CX_CKX .
*  methods HANDLE_RECALC_FPRJR
*    raising
*      /VCXI/CX_CKX .
*  methods HANDLE_RECALC_FREVR
*    raising
*      /VCXI/CX_CKX .
*  methods HANDLE_RECALC_KDMAT
*    raising
*      /VCXI/CX_CKX .
*  methods HANDLE_RECALC_KDPTX
*    raising
*      /VCXI/CX_CKX .
*  methods HANDLE_RECALC_MAKTX
*    raising
**      /VCXI/CX_CKX .
*  methods HANDLE_SET_DEFAULT
*    raising
*      /VCXI/CX_CKX .
private section.
endclass. "YCL_HP_SVCS_PR_CADIF_PRJ definition
class YCL_HP_SVCS_PR_CADIF_PRJ implementation.
  method GET_REVRF.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_REVRF ).
    move LS_PARAM-ATWRT to RF_REVRF.
    move LS_PARAM-SETBY to EF_SETBY.

  endmethod.
  method GET_XML.

    data LS_CADIF_PRJ_IO      type ZVCXI_PXSS_S_CADIF_PRJ_IO.
    data LT_CADIF_ONEUP_IO_P1 type table of ZVCXI_P1SS_S_CADIF_ONEUP_IO.
    data LT_CADIF_MLTUP_IO    type table of ZVCXI_PXSS_S_CADIF_MLTUP_IO.

    RF_XXML = SUPER->GET_XML( ).

***------------------------------------------------------------------------------------------------
*** Extract XML
    try.
        call transformation ID
             source xml RF_XXML
             result S_CADIF_PRJ_IO   = LS_CADIF_PRJ_IO       "Project
                    S_CADIF_ONEUP_P1 = LT_CADIF_ONEUP_IO_P1  "OneUp Corrugate
                    S_CADIF_MLTUP    = LT_CADIF_MLTUP_IO.    "MultiUp
      catch CX_XSLT_SYSTEM_ERROR into data(LR_CX_XSLT_SYSTEM_ERROR).
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( LR_CX_XSLT_SYSTEM_ERROR ).
    endtry.

***------------------------------------------------------------------------------------------------
*** Get all Printing Inbound/Outbound Information
    loop at ME->R_GATE->GET_PRVDR( IF_IPRVT = YCL_HP_SVCS_PR_CADIF_PRINT=>C_IPRVT_HP_CADIF_PRINT
                                   IF_INUSE = ABAP_TRUE ) into data(LR_GATE).

      try.
          data(RT_PRINT) = cast YCL_HP_SVCS_PR_CADIF_PRINT( LR_GATE->R_CLSPR )->FILL_BY_PRINTING( ).
        catch CX_SY_MOVE_CAST_ERROR.
      endtry.

***   Insert CAD Printing
      read table LT_CADIF_ONEUP_IO_P1 into data(LS_CADIF_ONEUP_IO_P1) index 1.
      check SY-SUBRC is initial.

      append lines of RT_PRINT to LS_CADIF_ONEUP_IO_P1-T_PRINT.
      LT_CADIF_ONEUP_IO_P1[ 1 ] = LS_CADIF_ONEUP_IO_P1.

    endloop.

***------------------------------------------------------------------------------------------------
*** Create XML
    try.
        call transformation ID
             source S_CADIF_PRJ_IO   = LS_CADIF_PRJ_IO        "Project
                    S_CADIF_ONEUP_P1 = LT_CADIF_ONEUP_IO_P1   "OneUp Corrugate
                    S_CADIF_MLTUP    = LT_CADIF_MLTUP_IO      "MultiUp
             result xml RF_XXML.
      catch CX_XSLT_SYSTEM_ERROR into LR_CX_XSLT_SYSTEM_ERROR.
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( LR_CX_XSLT_SYSTEM_ERROR ).
    endtry.

  endmethod.
  method SET_REVRF.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.


    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = C_PARID_REVRF ).

    move IF_REVRF to LS_PARAM-ATWRT.
    move IF_SETBY to LS_PARAM-SETBY.

    LF_CHANGED = ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

    if LF_CHANGED eq ABAP_TRUE.
      "Send Event
      ME->R_GATE->SET_EVENT( IF_IEVNT = C_IEVNT_O_CADPO_CHANGED ).
    endif.

  endmethod.
  method SET_XML.

    SUPER->SET_XML( IF_XXML ).

    data LT_CADIF_ONEUP_IO_P1 type table of ZVCXI_P1SS_S_CADIF_ONEUP_IO.

***------------------------------------------------------------------------------------------------
*** Extract XML
    try.
        call transformation ID
             source xml IF_XXML
             result S_CADIF_ONEUP_P1 = LT_CADIF_ONEUP_IO_P1.  "OneUp Corrugate for Printing
      catch CX_XSLT_SYSTEM_ERROR into data(LR_CX_XSLT_SYSTEM_ERROR).
        /VCXI/CX_CKX=>RAISE_CKX_BY_PREVIOUS( LR_CX_XSLT_SYSTEM_ERROR ).
    endtry.

***------------------------------------------------------------------------------------------------
*** Set all Printing Inbound/Outbound Information

*** Get CAD OneUp Provider
    data(LT_GATE_ONEUP) = ME->R_GATE->GET_PRVDR( IF_IPRVT = ZCL_VCXI_PXSS_PR_CADIF_ONEUP=>C_IPRVT_PX_CADIF_ONEUP
                                                 IF_INUSE = ABAP_TRUE ).
    check LT_GATE_ONEUP is not initial.
    data(LR_GATE_ONEUP) = LT_GATE_ONEUP[ 1 ].
    check LR_GATE_ONEUP->R_CLSPR is bound.

*** Get OneUp GUID
    try.
        data(LF_GCADO) = cast ZCL_VCXI_PXSS_PR_CADIF_ONEUP( LR_GATE_ONEUP->R_CLSPR )->GET_GCADO( ).
      catch CX_SY_MOVE_CAST_ERROR.
    endtry.

*** Get CAD Printing Provider
    loop at ME->R_GATE->GET_PRVDR( IF_IPRVT = YCL_HP_SVCS_PR_CADIF_PRINT=>C_IPRVT_HP_CADIF_PRINT
                                   IF_INUSE = ABAP_TRUE ) into data(LR_GATE_PRINT).
      check LR_GATE_PRINT->R_CLSPR is bound.

      try.
***     Identify Inbound/Outbound Structure
          data(LS_CADIF_ONEUP_IO_P1) = value #( LT_CADIF_ONEUP_IO_P1[ GCADO = LF_GCADO ] optional ).
          check LS_CADIF_ONEUP_IO_P1-GCADO is not initial.

***     Set Inbound/Outbound Structure
          cast YCL_HP_SVCS_PR_CADIF_PRINT( LR_GATE_PRINT->R_CLSPR )->SET_CADIF_IO( IS_CADIF_IO = LS_CADIF_ONEUP_IO_P1-T_PRINT ).

        catch CX_SY_MOVE_CAST_ERROR.
      endtry.

    endloop.

  endmethod.
