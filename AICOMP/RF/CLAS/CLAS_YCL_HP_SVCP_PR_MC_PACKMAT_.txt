
class YCL_HP_SVCP_PR_MC_PACKMAT definition
  public
  inheriting from ZCL_VCXI_P0SP_PR_MC_PACKMAT
  final
  create public .

public section.

  interfaces YIF_HP_SVCP_PR_MC_PACKMAT .

  aliases C_PARID_CONSU_NORND_A
    for YIF_HP_SVCP_PR_MC_PACKMAT~C_PARID_CONSU_NORND_A .
  aliases C_PARID_CONSU_NORND_L
    for YIF_HP_SVCP_PR_MC_PACKMAT~C_PARID_CONSU_NORND_L .
  aliases C_PARID_CONSU_NORND_P
    for YIF_HP_SVCP_PR_MC_PACKMAT~C_PARID_CONSU_NORND_P .
  aliases C_PARID_CONSU_NORND_W
    for YIF_HP_SVCP_PR_MC_PACKMAT~C_PARID_CONSU_NORND_W .
  aliases F_PARID_CONSU_NORND
    for YIF_HP_SVCP_PR_MC_PACKMAT~F_PARID_CONSU_NORND .
  aliases GET_CONSU_NORND
    for YIF_HP_SVCP_PR_MC_PACKMAT~GET_CONSU_NORND .
  aliases SET_CONSU_NORND
    for YIF_HP_SVCP_PR_MC_PACKMAT~SET_CONSU_NORND .

  methods CONSTRUCTOR
    importing
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .

  methods CALC_CONSU
    redefinition .
protected section.

  methods INIT_DIM_PARAM
    redefinition .
private section.
endclass. "YCL_HP_SVCP_PR_MC_PACKMAT definition
class YCL_HP_SVCP_PR_MC_PACKMAT implementation.
  method CALC_CONSU.

*** Super not called, abstract method
    data: LS_AVGCO type ZVCXI_XCS_S_AVGCO,
          LS_AVGCP type ZVCXI_XCS_S_AVGCP,
          LS_CONSU type ZVCXI_XCSP_S_CONSU,
          LS_MQTYI type ZVCXI_XCSP_S_MQTYI.
    data: LS_CONSU_NORND type YHP_SVCP_S_CONSU_NORND.

    if IS_MQTYI is not initial.

***   Get Average Consumption Weight
      move ME->GET_AVGCO( ) to LS_AVGCO.
      move ME->GET_AVGCP( ) to LS_AVGCP.

***   Calculate Consumption
      if LS_AVGCP-AVGCP is not initial.

***     With Ceiling function
        LS_MQTYI-MQTYI   = CEIL( IS_MQTYI-MQTYI / LS_AVGCP-AVGCP ).
        LS_CONSU-CONSU   = CEIL( LS_MQTYI-MQTYI * LS_AVGCO-AVGCO ).

***     Without rounding
        clear LS_MQTYI-MQTYI.
        LS_MQTYI-MQTYI        = IS_MQTYI-MQTYI / LS_AVGCP-AVGCP.
        LS_CONSU_NORND-CONSU  = LS_MQTYI-MQTYI * LS_AVGCO-AVGCO.

      else.
        LS_CONSU-CONSU        = 0.
        LS_CONSU_NORND-CONSU  = 0.
      endif.

      LS_CONSU-CONSU_UOM        = LS_AVGCO-AVGCO_UOM.
      LS_CONSU_NORND-CONSU_UOM  = LS_AVGCO-AVGCO_UOM.
    endif.

*** Set Consumption
    ME->SET_CONSU( IS_CONSU = LS_CONSU ).

*** Set Consumption - without rounding
    ME->SET_CONSU_NORND( IS_CONSU_NORND = LS_CONSU_NORND ).

  endmethod.
  method CONSTRUCTOR.

    SUPER->CONSTRUCTOR( IR_GATE = IR_GATE ).

*** Initialize Specific Dimension Parameter
    ME->INIT_DIM_PARAM( ).

  endmethod.
  method INIT_DIM_PARAM.

    data:  LS_PARAM    type /VCXI/VKSR_S_PARAM.
    data:  LT_PARAM    type /VCXI/VKSR_TT_PARAM.

***-------------------------------------------------------------------*
*** Call Super
    SUPER->INIT_DIM_PARAM( ).

*** Get all parameters of gate
    LT_PARAM = ME->R_GATE->GET_PARAM_ALL( ).

*** Get Parameter ID for Consumption (no rounding)
    loop at LT_PARAM into LS_PARAM
                    where PARTY eq /VCXI/CL_VKSC_PARAM=>C_PARTY_NUMC
                     and  ( PARID eq YIF_HP_SVCP_PR_MC_PACKMAT~C_PARID_CONSU_NORND_A or
                            PARID eq YIF_HP_SVCP_PR_MC_PACKMAT~C_PARID_CONSU_NORND_P or
                            PARID eq YIF_HP_SVCP_PR_MC_PACKMAT~C_PARID_CONSU_NORND_L or
                            PARID eq YIF_HP_SVCP_PR_MC_PACKMAT~C_PARID_CONSU_NORND_W ).
      move LS_PARAM-PARID to ME->F_PARID_CONSU_NORND.
      exit.
    endloop.

  endmethod.
  method YIF_HP_SVCP_PR_MC_PACKMAT~GET_CONSU_NORND.

    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = ME->F_PARID_CONSU_NORND ).
    move LS_PARAM-ATFLV to RS_CONSU_NORND-CONSU.
    move LS_PARAM-UNIT  to RS_CONSU_NORND-CONSU_UOM.

  endmethod.
  method YIF_HP_SVCP_PR_MC_PACKMAT~SET_CONSU_NORND.

    data:  LF_CHANGED type ABAP_BOOL.
    data:  LS_PARAM   type /VCXI/VKSR_S_PARAM.

    LS_PARAM = ME->R_GATE->GET_PARAM( IF_PARID = ME->F_PARID_CONSU_NORND ).

    move IS_CONSU_NORND-CONSU                to LS_PARAM-ATFLV.
    move IS_CONSU_NORND-CONSU_UOM            to LS_PARAM-UNIT.
    move /VCXI/CL_VKSR_GATE=>C_SETBY_CALC    to LS_PARAM-SETBY.

*** Set new parameter value (no event raised)
    ME->R_GATE->SET_PARAM( IS_PARAM = LS_PARAM ).

  endmethod.
