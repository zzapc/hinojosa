
class YCL_HP_SMD_MBQ_STP_603 definition
  public
  inheriting from ZCL_VCXI_XCM_MBQ_STP_S000
  final
  create public .

public section.

  methods EXECUTE
    redefinition .
protected section.
private section.
endclass. "YCL_HP_SMD_MBQ_STP_603 definition
class YCL_HP_SMD_MBQ_STP_603 implementation.
  method EXECUTE.

    data LS_DMSLI    type ZVCXI_XCM_MBQ_S_DMSLI.
    data LS_BAPIRET2 type BAPIRET2.

    try.
***     Get DMS Document List Entry of active DMS Document List Entry
        LS_DMSLI = ME->R_DCO_INPUT->T_DMSLI[ DLCNT = ME->R_DCO_INPUT->F_DLCNT ].

***     Check Activity - Create DMS Document
        if not LINE_EXISTS( LS_DMSLI-T_ACTCON[ ACTID = ZCL_VCXI_XCM_MBQ_CUST=>C_ACTID_DMS ] ).
***       Set Status to CANCELED
          ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_CANCELED ).
          return.
        endif.

***--------------------------------------------------------------------------------------
***     Set DMS Document Status
        call function 'BAPI_DOCUMENT_SETSTATUS'
          exporting
            DOCUMENTTYPE    = LS_DMSLI-S_DOC_KEY-DOKAR
            DOCUMENTNUMBER  = LS_DMSLI-S_DOC_KEY-DOKNR
            DOCUMENTPART    = LS_DMSLI-S_DOC_KEY-DOKTL
            DOCUMENTVERSION = LS_DMSLI-S_DOC_KEY-DOKVR
            STATUSEXTERN    = 'FR'
            STATUSINTERN    = 'FR'
          importing
            RETURN          = LS_BAPIRET2
          exceptions
            others          = 1.

***--------------------------------------------------------------------------------------
***     Check for error messages when creating material
        if LS_BAPIRET2-TYPE eq 'E'.
          call function 'BAPI_TRANSACTION_ROLLBACK'.
***       Raise Exception from BAPI Message
          /VCXI/CX_CKX=>RAISE_CKX_BY_BAPIRET2( IS_BAPIRET2 = LS_BAPIRET2 ).
        else.
          call function 'BAPI_TRANSACTION_COMMIT'
            exporting
              WAIT = ABAP_TRUE.
***       Document &1 has been released
          ME->R_RPSEG->R_MSG->ADD_MESSAGE( IF_MSGID = 'UKM_DCD_MSG'
                                           IF_MSGTY = 'S'
                                           IF_MSGNO = '023'
                                           IF_MSGV1 = LS_DMSLI-S_DOC_KEY-DOKNR ).
        endif.

***     Set Status to Complete
        ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_COMPLETED ).

***--------------------------------------------------------------------------------------
      catch /VCXI/CX_CKX into data(LR_CX_CKX).
***     Set Status to Error
        ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_ERROR ).

***     Add Message to Message Log
        ME->R_RPSEG->R_MSG->ADD_MESSAGE_BY_CX_CKX( IR_CX_CKX = LR_CX_CKX ).
    endtry.

*** Perform Save and Commit
    ME->R_RPSEG->SAVE_AND_COMMIT( ).

  endmethod.
endclass. "YCL_HP_SMD_MBQ_STP_603 implementation
