
class YCL_HP_SMD_MBQ_STP_S110 definition
  public
  inheriting from ZCL_VCXI_XCM_MBQ_STP_S000
  create public .

public section.

  constants C_ZSTATP type ZSSD_STAT_POS value 'A7' ##NO_TEXT.

  methods GET_ITEM_STATUS
    importing
      !IF_POSNR type POSNR
    exporting
      value(EF_STSMA) type J_STSMA
      value(ET_STAT) type TT_JSTAT
      value(EF_ZSTATP) type ZSSD_STAT_POS
    raising
      /VCXI/CX_CKX .

  methods EXECUTE
    redefinition .
protected section.
private section.
endclass. "YCL_HP_SMD_MBQ_STP_S110 definition
class YCL_HP_SMD_MBQ_STP_S110 implementation.
  method EXECUTE.

    data: LF_ZSTATP type ZSSD_STAT_POS.
    data: LF_STSMA type J_STSMA,
          LF_OK    type ABAP_BOOL.
    data: LT_STAT  type TT_JSTAT.


*** Get Status and Profile
    ME->GET_ITEM_STATUS( exporting IF_POSNR   = ME->R_DCO_INPUT->F_POSNR
                         importing EF_STSMA   = LF_STSMA
                                   ET_STAT    = LT_STAT
                                   EF_ZSTATP  = LF_ZSTATP  ).

*** Check if the status is Ready for SmartMD
    case LF_STSMA.

***   Quotation
      when YCL_HP_SMD_MBQ_CUST=>C_STSMA_QUOTATION.
        loop at LT_STAT transporting no fields
                        where STAT = YCL_HP_SMD_MBQ_CUST=>C_STAT_READY_FOR_SMARTMD.
          LF_OK = ABAP_TRUE.
          exit.
        endloop.

      when others.
***   Status profile &1 not handled by SmartMD process.
        /VCXI/CX_CKX=>RAISE_CKX_WITH_MESSAGE( IF_MSGID = 'YHP_SMD_MBQ'
                                              IF_MSGTY = 'E'
                                              IF_MSGNO = '021'
                                              IF_MSGV1 = LF_STSMA ).
    endcase.

***-------------------------------------------------------------------*
*** Add messages after check

    if LF_OK eq ABAP_TRUE.
***   Valid Item &1; status is released for SmartMD.
      ME->R_RPSEG->R_MSG->ADD_MESSAGE( exporting IF_MSGID = 'YHP_SMD_MBQ'
                                                 IF_MSGTY = 'S'
                                                 IF_MSGNO = '013'
                                                 IF_MSGV1 = ME->R_DCO_INPUT->F_POSNR ).
      "Set Status to Complete
      ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_COMPLETED ).

    else.

***   Error Item &; status not released for SmartMD.
      ME->R_RPSEG->R_MSG->ADD_MESSAGE( exporting IF_MSGID = 'YHP_SMD_MBQ'
                                                 IF_MSGTY = 'E'
                                                 IF_MSGNO = '012'
                                                 IF_MSGV1 = ME->R_DCO_INPUT->F_POSNR ).
      "Set Status Error
      ME->R_RPSEG->SET_SRPSG( IF_SRPSG = /VCXI/CL_CKMR_RPSEG=>C_SRPSG_ERROR ).
    endif.

  endmethod.
  method GET_ITEM_STATUS.

    field-symbols: <LS_VBAP> type VBAP.

    read table ME->R_DCO_INPUT->R_BOB_SDOC->T_VBAP assigning <LS_VBAP> with key POSNR = IF_POSNR.
    if SY-SUBRC is initial.

***   Get Status of the Sales Document Item
      call function 'STATUS_READ'
        exporting
          CLIENT           = SY-MANDT
          OBJNR            = <LS_VBAP>-OBJNR
          ONLY_ACTIVE      = ABAP_TRUE
        importing
          STSMA            = EF_STSMA
        tables
          STATUS           = ET_STAT
        exceptions
          OBJECT_NOT_FOUND = 1
          others           = 2.

      if SY-SUBRC is not initial.
        /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
      endif.

*** Return VBAP status if available
      move <LS_VBAP>-ZZESTADO to EF_ZSTATP.

    endif.

  endmethod.
