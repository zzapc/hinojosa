
class YCL_HP_SVCS_UI_CSTEP_PRINT definition
  public
  inheriting from ZCL_VCXI_P1SS_UI_CSTEP_PRINT
  create public .

public section.

  data S_HP_CSTEP_PRINT_VIS type YHP_SVCS_S_PRINT .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE .
  methods DO_UPDATE_VIS_HP
    importing
      !IS_CSTEP_VIS type YHP_SVCS_S_PRINT
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_PBO
    redefinition .
  methods DO_UPDATE_VIS
    redefinition .
  methods DO_USER_COMMAND
    redefinition .
  methods SET_CURSOR
    redefinition .
  methods DEACTIVATE
    redefinition .
protected section.

  data R_HP_PR_CSTEP_PRINT type ref to YCL_HP_SVCS_PR_CSTEP_PRINT.
  data R_HP_GRID_MS_INK type ref to YCL_HP_SVCS_GRID_MS_INK .

  methods INIT_VIS
    redefinition .
private section.
endclass. "YCL_HP_SVCS_UI_CSTEP_PRINT definition
class YCL_HP_SVCS_UI_CSTEP_PRINT implementation.
  method ACTIVATE.

    SUPER->ACTIVATE( ).
*** Bind to customer-specific UI
    call function 'YHP_SVCS_UI_PRINT'
      exporting
        IR_UI_CSTEP_PRINT = ME
      importing
        ES_DYNP           = ME->S_DYNP.

***--------------------------------------------------------------------------------------
*** Create Grid for Inks
    create object ME->R_HP_GRID_MS_INK
      exporting
        IR_GATE           = ME->R_GATE
        IR_UIGATE         = ME->R_UIGATE
        IF_CONTAINER_NAME = '0100CC_GRID_MS_INK'.

  endmethod.
  method CONSTRUCTOR.
    SUPER->CONSTRUCTOR( IR_UIFW = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE = IR_GATE ).
*** Assign th RP Class to the UI Class
    try.

        move IR_GATE->R_CLSPR ?to ME->R_HP_PR_CSTEP_PRINT.
      catch CX_SY_MOVE_CAST_ERROR.
***   UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DEACTIVATE.

    SUPER->DEACTIVATE( ).

    if ME->R_HP_GRID_MS_INK is bound.
      ME->R_HP_GRID_MS_INK->DO_FREE( ).
      clear ME->R_HP_GRID_MS_INK.
    endif.

  endmethod.
  method DO_PBO.

***--------------------------------------------------------------------------------------
*** Super PBO
*    SUPER->DO_PBO( ).
***--------------------------------------------------------------------------------------
*** Initialize VIS
    ME->INIT_VIS( ).

***--------------------------------------------------------------------------------------
*** Get TextEdit Conversion Text
    if ME->R_TEXTEDIT_LTEXT is bound.
      move ABAP_TRUE to ME->R_TEXTEDIT_LTEXT->F_REFRESH_REQ.
      ME->R_TEXTEDIT_LTEXT->REFRESH_TEXTEDIT( ).
    endif.

***--------------------------------------------------------------------------------------
*** Initialize GRID
    if ME->R_HP_GRID_MS_INK is bound.
      ME->R_HP_GRID_MS_INK->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_HP_GRID_MS_INK->REFRESH_GRID( ).
    endif.

    if ME->R_GRID_MARK is bound.
      ME->R_GRID_MARK->F_REFRESH_REQ = ABAP_TRUE.
      ME->R_GRID_MARK->REFRESH_GRID( ).
    endif.

***--------------------------------------------------------------------------------------
*** Define DropDown Values for Design Document
    ME->SET_DDVAL_DESDO( ).

  endmethod.
  method DO_UPDATE_VIS.
data: LS_CSTEP_PRINT_VIS type ZVCXI_P1SS_S_CSTEP_PRINT_VIS.


*** Call SUPER
    SUPER->DO_UPDATE_VIS( IS_CSTEP_VIS = IS_CSTEP_VIS ).

    check ME->R_PR_CSTEP_PRINT is bound.

*** "Cast" Vis Structure
    move-corresponding IS_CSTEP_VIS to LS_CSTEP_PRINT_VIS.

***--------------------------------------------------------------------------------------
*** Set Print Type
    if ME->S_CSTEP_PRINT_VIS-TPRIN ne LS_CSTEP_PRINT_VIS-TPRIN.
      ME->R_PR_CSTEP_PRINT->SET_TPRIN( IF_TPRIN = LS_CSTEP_PRINT_VIS-TPRIN
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Print Side
    if ME->S_CSTEP_PRINT_VIS-PRSID ne LS_CSTEP_PRINT_VIS-PRSID.
      ME->R_PR_CSTEP_PRINT->SET_PRSID( IF_PRSID = LS_CSTEP_PRINT_VIS-PRSID
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Number of Colors
    if ME->S_CSTEP_PRINT_VIS-NOCOL ne LS_CSTEP_PRINT_VIS-NOCOL.
      ME->R_PR_CSTEP_PRINT->SET_NOCOL( IF_NOCOL = LS_CSTEP_PRINT_VIS-NOCOL ).
    endif.

*** Set Sheet Direction
    if ME->S_CSTEP_PRINT_VIS-SHTDI ne LS_CSTEP_PRINT_VIS-SHTDI.
      ME->R_PR_CSTEP_PRINT->SET_SHTDI( IF_SHTDI = LS_CSTEP_PRINT_VIS-SHTDI
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.


*** Update Printable Area if it is "Overriden"
    if ME->S_CSTEP_PRINT_VIS-FOVRR_PRARE eq ABAP_TRUE and
       ME->S_CSTEP_PRINT_VIS-S_PRARE ne LS_CSTEP_PRINT_VIS-S_PRARE.
      ME->R_PR_CSTEP_PRINT->SET_PRARE( IS_PRARE = LS_CSTEP_PRINT_VIS-S_PRARE
                                       IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Set Flag Override Printable Area
    if ME->S_CSTEP_PRINT_VIS-FOVRR_PRARE ne LS_CSTEP_PRINT_VIS-FOVRR_PRARE.
      ME->R_PR_CSTEP_PRINT->SET_PRARE( IF_FOVRR = LS_CSTEP_PRINT_VIS-FOVRR_PRARE ).
    endif.

  endmethod.
  method DO_UPDATE_VIS_HP.

    data: LS_CSTEP_PRINT_VIS type ZVCXI_P1SS_S_CSTEP_PRINT_VIS.

    LS_CSTEP_PRINT_VIS = corresponding #( IS_CSTEP_VIS ).

    ME->DO_UPDATE_VIS( IS_CSTEP_VIS = LS_CSTEP_PRINT_VIS  ).

    check ME->R_PR_CSTEP_PRINT is bound.

*** "Cast" Vis Structure
    move-corresponding IS_CSTEP_VIS to LS_CSTEP_PRINT_VIS.

*** Bar Code Control Quality on Machine
    if ME->S_HP_CSTEP_PRINT_VIS-BCCQM ne IS_CSTEP_VIS-BCCQM.
      ME->R_HP_PR_CSTEP_PRINT->SET_BCCQM( IF_BCCQM = IS_CSTEP_VIS-BCCQM
                                     IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Number of samples
    if ME->S_HP_CSTEP_PRINT_VIS-NOSAM ne IS_CSTEP_VIS-NOSAM.
      ME->R_HP_PR_CSTEP_PRINT->SET_NOSAM( IF_NOSAM = IS_CSTEP_VIS-NOSAM
                                          IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Printing Test
    if ME->S_HP_CSTEP_PRINT_VIS-PRSAM ne IS_CSTEP_VIS-PRSAM.
      ME->R_HP_PR_CSTEP_PRINT->SET_PRSAM( IF_PRSAM = IS_CSTEP_VIS-PRSAM
                                          IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method DO_USER_COMMAND.

    if ME->R_HP_GRID_MS_INK is bound.
      ME->R_HP_GRID_MS_INK->CHECK_CHANGED_DATA( ).
    endif.

    SUPER->DO_USER_COMMAND( IF_UCOMM = IF_UCOMM ).

  endmethod.
  method INIT_VIS.
    SUPER->INIT_VIS( ).

    free: ME->S_HP_CSTEP_PRINT_VIS.
    ME->S_HP_CSTEP_PRINT_VIS-S_CSTEP_VIS = ME->S_CSTEP_VIS.

***--------------------------------------------------------------------------------------
*** Print Type
    ME->S_HP_CSTEP_PRINT_VIS-TPRIN = ME->R_PR_CSTEP_PRINT->ZIF_VCXI_PXSS_PRINT~GET_TPRIN( ).

*** Print Side
    ME->S_HP_CSTEP_PRINT_VIS-PRSID = ME->R_PR_CSTEP_PRINT->GET_PRSID( ).

*** Number of Colors
    ME->S_HP_CSTEP_PRINT_VIS-NOCOL = ME->R_PR_CSTEP_PRINT->ZIF_VCXI_PXSS_PRINT~GET_NOCOL( ).

*** Printable Area
    ME->S_HP_CSTEP_PRINT_VIS-S_PRARE = ME->R_PR_CSTEP_PRINT->GET_PRARE( importing EF_FOVRR = ME->S_CSTEP_PRINT_VIS-FOVRR_PRARE ).

*** Sheet Direction
    ME->S_HP_CSTEP_PRINT_VIS-SHTDI = ME->R_PR_CSTEP_PRINT->GET_SHTDI( ).

****Bar Code Control Quality on Machine
    ME->S_HP_CSTEP_PRINT_VIS-BCCQM = ME->R_HP_PR_CSTEP_PRINT->GET_BCCQM( ).

*** Number os Samples
    ME->S_HP_CSTEP_PRINT_VIS-NOSAM = ME->R_HP_PR_CSTEP_PRINT->GET_NOSAM( ).

*** Printing Test
    ME->S_HP_CSTEP_PRINT_VIS-PRSAM = ME->R_HP_PR_CSTEP_PRINT->GET_PRSAM( ).

  endmethod.
  method SET_CURSOR.

    data:  LF_CURSOR type SCRFNAME.

    get cursor field LF_CURSOR.
    if LF_CURSOR is not initial.
      move LF_CURSOR to ME->F_CURSOR.
    endif.
  endmethod.
