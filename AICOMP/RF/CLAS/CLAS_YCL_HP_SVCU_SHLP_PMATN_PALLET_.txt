
class YCL_HP_SVCU_SHLP_PMATN_PALLET definition
  public
  final
  create public .

public section.

  types:
    begin of YS_KUNWE_PMATN.
    types: KUNNR   type KNVV-KUNNR,
           VKORG   type KNVV-VKORG,
           VTWEG   type KNVV-VTWEG,
           SPART   type KNVV-SPART.
           include type ZSD_HP_KNVV.
           types T_PMATN type ZVCXI_XCSU_TT_PMATN.
    types end of YS_KUNWE_PMATN .
  types:
    YT_KUNWE_PMATN type table of YS_KUNWE_PMATN .

  constants C_PMATY_PALLET type ZVCXI_XCSU_PMATY value 'PALLET' ##NO_TEXT.
  constants C_MATKL_CHEP type MATKL value '42110' ##NO_TEXT.
  constants C_MATKL_LPR type MATKL value '42111' ##NO_TEXT.
  constants C_MATKL_LOGIFRUIT type MATKL value '42112' ##NO_TEXT.
  constants C_MATKL_CONTRALOAD type MATKL value '42113' ##NO_TEXT.
  class-data T_KUNWE_PMATN type YT_KUNWE_PMATN read-only .
  class-data RG_MATKL_PALLETS_SPC type YHP_X_TT_MATKL .
  constants C_MVGR1_CHEP type MVGR1 value '001' ##NO_TEXT.
  constants C_MVGR1_LPR type MVGR1 value '002' ##NO_TEXT.
  constants C_MVGR1_LOGIFRUIT type MVGR1 value '003' ##NO_TEXT.
  constants C_MVGR1_CONTRALOAD type MVGR1 value '004' ##NO_TEXT.
  constants C_MVGR1_DEF1 type MVGR1 value '006' ##NO_TEXT.
  constants C_MVGR1_DEF2 type MVGR1 value '007' ##NO_TEXT.
  constants C_MVGR1_RETORNAB type MVGR1 value '008' ##NO_TEXT.
  constants C_MVGR1_COBRO type MVGR1 value '008' ##NO_TEXT.
  constants C_POOL type MVGR1 value '005' ##NO_TEXT.

  class-methods CLASS_CONSTRUCTOR .
  class-methods CALL_SHLP_PALRENT
    importing
      !IS_KNVV_WE type KNVV
      !IF_WERKS type WERKS_D optional
      !IF_PMATY type ZVCXI_XCSU_PMATY optional
      !IF_DISPLAY type ABAP_BOOL default ABAP_FALSE
      !IF_ALL type ABAP_BOOL default ABAP_FALSE
    returning
      value(RF_PMATN) type ZVCXI_XCSU_PMATN
    raising
      /VCXI/CX_CKX .
  class-methods CALL_SHLP
    importing
      !IS_KNVV_WE type KNVV
      !IF_WERKS type WERKS_D optional
      !IF_PMATY type ZVCXI_XCSU_PMATY
      !IF_DISPLAY type ABAP_BOOL default ABAP_FALSE
      !IF_ALL type ABAP_BOOL default ABAP_FALSE
    returning
      value(RF_PMATN) type ZVCXI_XCSU_PMATN
    raising
      /VCXI/CX_CKX .
  class-methods GET_VALID_PMATN_PALLET
    importing
      !IS_KNVV_WE type KNVV
      !IF_WERKS type WERKS_D
      !IF_PMATY type ZVCXI_XCSU_PMATY
    returning
      value(RT_PMATN) type ZVCXI_XCSU_TT_PMATN
    raising
      /VCXI/CX_CKX .
protected section.
private section.

  constants C_MVGR1_FONDOPERD type MVGR1 value '009' ##NO_TEXT.

  class-methods _ADJUST_PMATN_BY_MATKL
    importing
      !IS_KNVV_WE type KNVV
    changing
      !CT_PMATN type ZVCXI_XCSU_TT_PMATN
    raising
      /VCXI/CX_CKX .
  class-methods _ADJUST_PMATN_BY_MATKL_MVGR1
    importing
      !IS_KNVV_WE type KNVV
    changing
      !CT_PMATN type ZVCXI_XCSU_TT_PMATN .
endclass. "YCL_HP_SVCU_SHLP_PMATN_PALLET definition
class YCL_HP_SVCU_SHLP_PMATN_PALLET implementation.
  method _ADJUST_PMATN_BY_MATKL.

    data: LRG_MATKL_WE type YHP_X_TT_MATKL.
    data: LS_PMATN_BUF type ZVCXI_XCSU_S_PMATN.
    data: LT_PMATN_BUF type ZVCXI_XCSU_TT_PMATN.
    field-symbols: <LS_PMATN> type ZVCXI_XCSU_S_PMATN.

*** 1) Continue if we have a list of materials to search
    check CT_PMATN is not initial.

*** 2) Build Material Group filter based on customer attributes
    LRG_MATKL_WE = value #( SIGN = 'I' OPTION = 'EQ'
                          ( LOW = switch #( IS_KNVV_WE-ZZPALLET_CHEP       when ABAP_TRUE then C_MATKL_CHEP       else SPACE ) )
                          ( LOW = switch #( IS_KNVV_WE-ZZPALLET_LPR        when ABAP_TRUE then C_MATKL_LPR        else SPACE ) )
                          ( LOW = switch #( IS_KNVV_WE-ZZPALLET_LOGIFRUIT  when ABAP_TRUE then C_MATKL_LOGIFRUIT  else SPACE ) )
                          ( LOW = switch #( IS_KNVV_WE-ZZPALLET_CONTRALOAD when ABAP_TRUE then C_MATKL_CONTRALOAD else SPACE ) ) ).
    delete LRG_MATKL_WE where LOW is initial.

*** 3) Get special (SPC) packaging materials (based on customer attributes)
    if LRG_MATKL_WE is not initial.
      select MATNR, MATKL
        from MARA
        into table @data(LT_PMATN_VALID)
        for all entries in @CT_PMATN
        where MATNR eq @CT_PMATN-PMATN
          and MATKL in @LRG_MATKL_WE.
    endif.

*** 4) Get standard (STD) packaging materials
    select MATNR, MATKL
      from MARA
      appending table @LT_PMATN_VALID
      for all entries in @CT_PMATN
      where MATNR eq @CT_PMATN-PMATN
        and MATKL not in @RG_MATKL_PALLETS_SPC.

*** 4) Get buffered description for valid materials
    LT_PMATN_BUF[] = CT_PMATN[].
    clear CT_PMATN.

    loop at LT_PMATN_VALID into data(LS_PMATN_VALID).
      clear LS_PMATN_BUF.
      read table LT_PMATN_BUF into LS_PMATN_BUF with key PMATN = LS_PMATN_VALID-MATNR.
      check SY-SUBRC eq 0.
      append LS_PMATN_BUF to CT_PMATN.
    endloop.

  endmethod.
  method _ADJUST_PMATN_BY_MATKL_MVGR1.

    data: LRG_MVGR1_WE type YHP_X_TT_MVGR1.
    data: LS_PMATN_BUF type ZVCXI_XCSU_S_PMATN.
    data: LT_PMATN_BUF type ZVCXI_XCSU_TT_PMATN.
    field-symbols: <LS_PMATN> type ZVCXI_XCSU_S_PMATN.

*** 1) Continue if we have a list of materials to search
    check CT_PMATN is not initial.

*** 2) Build Material Group filter based on customer attributes
    LRG_MVGR1_WE = value #( SIGN = 'I' OPTION = 'EQ'
                          ( LOW = switch #( IS_KNVV_WE-ZZPALLET_CHEP       when ABAP_TRUE then C_MVGR1_CHEP       else SPACE ) )
                          ( LOW = switch #( IS_KNVV_WE-ZZPALLET_LPR        when ABAP_TRUE then C_MVGR1_LPR        else SPACE ) )
                          ( LOW = switch #( IS_KNVV_WE-ZZPALLET_LOGIFRUIT  when ABAP_TRUE then C_MVGR1_LOGIFRUIT  else SPACE ) )
                          ( LOW = switch #( IS_KNVV_WE-ZZPALLET_CONTRALOAD when ABAP_TRUE then C_MVGR1_CONTRALOAD else SPACE ) )
                          ( LOW = switch #( IS_KNVV_WE-ZZPALLET_POOL       when ABAP_TRUE then C_POOL             else SPACE ) )
                          ( LOW = switch #( IS_KNVV_WE-ZZPALLET_DEF1       when ABAP_TRUE then C_MVGR1_DEF1       else SPACE ) )
                          ( LOW = switch #( IS_KNVV_WE-ZZPALLET_DEF2       when ABAP_TRUE then C_MVGR1_DEF2       else SPACE ) )
                          ( LOW = switch #( IS_KNVV_WE-ZZPALLET_RETORNAB   when ABAP_TRUE then C_MVGR1_RETORNAB   else SPACE ) )
                          ( LOW = switch #( IS_KNVV_WE-ZZPALLET_COBRO      when ABAP_TRUE then C_MVGR1_COBRO      else SPACE ) )
                          ( LOW = switch #( IS_KNVV_WE-ZZPALLET_FONDOPERD  when ABAP_TRUE then C_MVGR1_FONDOPERD  else SPACE ) ) ).
    delete LRG_MVGR1_WE where LOW is initial.

*** 3) Get special (SPC) packaging materials (based on customer attributes)
    if LRG_MVGR1_WE is not initial.
      select MATNR, MVGR1 from MVKE into table @data(LT_PMATN_VALID)
        for all entries in @CT_PMATN
        where MATNR eq @CT_PMATN-PMATN
          and VKORG eq @IS_KNVV_WE-VKORG
          and VTWEG eq @IS_KNVV_WE-VTWEG
          and MVGR1 in @LRG_MVGR1_WE.
    endif.

*** 4) Get buffered description for valid materials
    LT_PMATN_BUF[] = CT_PMATN[].
    clear CT_PMATN.

    loop at LT_PMATN_VALID into data(LS_PMATN_VALID).
      clear LS_PMATN_BUF.
      read table LT_PMATN_BUF into LS_PMATN_BUF with key PMATN = LS_PMATN_VALID-MATNR.
      check SY-SUBRC eq 0.
      append LS_PMATN_BUF to CT_PMATN.
    endloop.

  endmethod.
method CALL_SHLP.

  data: LS_RETURN type DDSHRETVAL.
  data: LT_PMATN     type ZVCXI_XCSU_TT_PMATN,
        LT_RETURN    type table of DDSHRETVAL.

***-------------------------------------------------------------------*
*** Call Standard Material Searchhelp
  if IF_ALL eq ABAP_TRUE.
    call function 'F4IF_FIELD_VALUE_REQUEST'
      exporting
        TABNAME           = 'MARA'
        FIELDNAME         = 'MATNR'
        DISPLAY           = IF_DISPLAY
      tables
        RETURN_TAB        = LT_RETURN
      exceptions
        FIELD_NOT_FOUND   = 1
        NO_HELP_FOR_FIELD = 2
        INCONSISTENT_HELP = 3
        NO_VALUES_FOUND   = 4
        others            = 5.

*** Call Customizing Searchhelp (Only include valid materials based on material group)
  else.

    LT_PMATN = GET_VALID_PMATN_PALLET( exporting IS_KNVV_WE = IS_KNVV_WE
                                                 IF_WERKS   = IF_WERKS
                                                 IF_PMATY   = IF_PMATY ).

*** Call F4 Help function
    call function 'F4IF_INT_TABLE_VALUE_REQUEST'
      exporting
        RETFIELD        = 'PMATN'
        VALUE_ORG       = 'S'
        DISPLAY         = IF_DISPLAY
      tables
        VALUE_TAB       = LT_PMATN
        RETURN_TAB      = LT_RETURN
      exceptions
        PARAMETER_ERROR = 1
        NO_VALUES_FOUND = 2
        others          = 3.
  endif.

  if SY-SUBRC <> 0.
    /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
  endif.

  if SY-SUBRC eq 0.
    read table LT_RETURN index 1 into LS_RETURN.
    if SY-SUBRC eq 0.
      RF_PMATN = LS_RETURN-FIELDVAL.

      call function 'CONVERSION_EXIT_MATN1_INPUT'
        exporting
          INPUT        = RF_PMATN
        importing
          OUTPUT       = RF_PMATN
        exceptions
          LENGTH_ERROR = 1.
      if SY-SUBRC ne 0.
        clear RF_PMATN.
      endif.
    endif.
  endif.

endmethod.
method CALL_SHLP_PALRENT.

  data: LS_RETURN type DDSHRETVAL.
  data: LT_PMATN     type ZVCXI_XCSU_TT_PMATN,
        LT_RETURN    type table of DDSHRETVAL.

***-------------------------------------------------------------------*
*** Call Standard Material Searchhelp
  if IF_ALL eq ABAP_TRUE.
    call function 'F4IF_FIELD_VALUE_REQUEST'
      exporting
        TABNAME           = 'MARA'
        FIELDNAME         = 'MATNR'
        DISPLAY           = IF_DISPLAY
      tables
        RETURN_TAB        = LT_RETURN
      exceptions
        FIELD_NOT_FOUND   = 1
        NO_HELP_FOR_FIELD = 2
        INCONSISTENT_HELP = 3
        NO_VALUES_FOUND   = 4
        others            = 5.

*** Call Customizing Searchhelp (Only include valid materials based on material group)
  else.

    LT_PMATN = GET_VALID_PMATN_PALLET( exporting IS_KNVV_WE = IS_KNVV_WE
                                                 IF_WERKS   = IF_WERKS
                                                 IF_PMATY   = IF_PMATY ).

*** Call F4 Help function
    call function 'F4IF_INT_TABLE_VALUE_REQUEST'
      exporting
        RETFIELD        = 'PMATN'
        VALUE_ORG       = 'S'
        DISPLAY         = IF_DISPLAY
      tables
        VALUE_TAB       = LT_PMATN
        RETURN_TAB      = LT_RETURN
      exceptions
        PARAMETER_ERROR = 1
        NO_VALUES_FOUND = 2
        others          = 3.
  endif.

  if SY-SUBRC <> 0.
    /VCXI/CX_CKX=>RAISE_CKX_BY_SY( ).
  endif.

  if SY-SUBRC eq 0.
    read table LT_RETURN index 1 into LS_RETURN.
    if SY-SUBRC eq 0.
      RF_PMATN = LS_RETURN-FIELDVAL.

      call function 'CONVERSION_EXIT_MATN1_INPUT'
        exporting
          INPUT        = RF_PMATN
        importing
          OUTPUT       = RF_PMATN
        exceptions
          LENGTH_ERROR = 1.
      if SY-SUBRC ne 0.
        clear RF_PMATN.
      endif.
    endif.
  endif.

endmethod.
  method CLASS_CONSTRUCTOR.

***-------------------------------------------------------------------*
*** Build Range for Material Groups of Special Palletsfilter
    RG_MATKL_PALLETS_SPC = value #( OPTION = 'EQ' SIGN = 'I'  ( LOW = C_MATKL_CHEP )
                                                              ( LOW = C_MATKL_LPR  )
                                                              ( LOW = C_MATKL_CONTRALOAD )
                                                              ( LOW = C_MATKL_LOGIFRUIT  ) ).
  endmethod.
  METHOD get_valid_pmatn_pallet.

    DATA: ls_kunwe_pmatn TYPE ys_kunwe_pmatn.
    DATA: lr_cust_shipu TYPE REF TO   zcl_vcxi_xcsu_cust_shipu.

*** Get S&U customizing instance
    lr_cust_shipu = zcl_vcxi_xcsu_cust_shipu=>get_instance( ).
    CHECK lr_cust_shipu IS BOUND.

*** Get allowed pallet materials from customizing
    rt_pmatn = lr_cust_shipu->get_pmatn_for_pmaty( if_werks = if_werks
                                                   if_pmaty = if_pmaty ).


*INICIO----------------------------- MODIFICACIONHINOJOSA ------------------------------------------------------------------
    IF zcl_ap_exits=>exit_activa( 'AIC_GET_VALID_PMATN_PALLET' ).
      DATA(l_knvv_we) = is_knvv_we.

* Validamos que la organización de ventas recuperada sea coherente con el centro
      SELECT SINGLE vkorg FROM tvkwz
        INTO l_knvv_we-vkorg
       WHERE vkorg = l_knvv_we-vkorg
         AND vtweg = l_knvv_we-vtweg.
      IF sy-subrc = 0.
* Si no lo es, cambiamos los datos de organización para que correspondan con el centro indicado.
        SELECT knvv~* FROM knvv JOIN tvkwz ON tvkwz~vkorg = knvv~vkorg
                                          AND tvkwz~vtweg = knvv~vtweg
          INTO CORRESPONDING FIELDS OF @l_knvv_we
          UP TO 1 ROWS
         WHERE kunnr = @l_knvv_we-kunnr
           AND werks = @if_werks
           AND knvv~vtweg = @l_knvv_we-vtweg
           AND knvv~spart = @l_knvv_we-spart
         ORDER BY knvv~vkorg DESCENDING.
        ENDSELECT.
      ENDIF.


*** Filter materials based on material groups (from ship-to allowed pallets)
      TRY.
          "1) Check Buffer first
          ls_kunwe_pmatn = t_kunwe_pmatn[ kunnr = l_knvv_we-kunnr  vkorg = l_knvv_we-vkorg
                                          vtweg = l_knvv_we-vtweg  spart = l_knvv_we-spart ].
          MOVE ls_kunwe_pmatn-t_pmatn TO rt_pmatn.

        CATCH cx_sy_itab_line_not_found.
          "2) Do read since nothing was found in buffer
*        _ADJUST_PMATN_BY_MATKL( exporting IS_KNVV_WE = IS_KNVV_WE
*                                changing  CT_PMATN   = RT_PMATN ).
          _adjust_pmatn_by_matkl_mvgr1( EXPORTING is_knvv_we = l_knvv_we
                                         CHANGING  ct_pmatn   = rt_pmatn ).
***     Buffer data
          MOVE-CORRESPONDING l_knvv_we TO ls_kunwe_pmatn.
          MOVE rt_pmatn TO ls_kunwe_pmatn-t_pmatn.
          APPEND ls_kunwe_pmatn TO t_kunwe_pmatn.

      ENDTRY.

    ELSE.
*FIN----------------------------- MODIFICACIONHINOJOSA ------------------------------------------------------------------

*** Filter materials based on material groups (from ship-to allowed pallets)
      TRY.
          "1) Check Buffer first
          ls_kunwe_pmatn = t_kunwe_pmatn[ kunnr = is_knvv_we-kunnr  vkorg = is_knvv_we-vkorg
                                          vtweg = is_knvv_we-vtweg  spart = is_knvv_we-spart ].
          MOVE ls_kunwe_pmatn-t_pmatn TO rt_pmatn.

        CATCH cx_sy_itab_line_not_found.
          "2) Do read since nothing was found in buffer
*        _ADJUST_PMATN_BY_MATKL( exporting IS_KNVV_WE = IS_KNVV_WE
*                                changing  CT_PMATN   = RT_PMATN ).
          _adjust_pmatn_by_matkl_mvgr1( EXPORTING is_knvv_we = is_knvv_we
                                         CHANGING  ct_pmatn   = rt_pmatn ).
***     Buffer data
          MOVE-CORRESPONDING is_knvv_we TO ls_kunwe_pmatn.
          MOVE rt_pmatn TO ls_kunwe_pmatn-t_pmatn.
          APPEND ls_kunwe_pmatn TO t_kunwe_pmatn.

      ENDTRY.
    ENDIF.

  ENDMETHOD.
