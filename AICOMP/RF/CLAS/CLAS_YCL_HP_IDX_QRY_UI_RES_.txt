
class YCL_HP_IDX_QRY_UI_RES definition
  public
  create protected .

public section.

  methods DO_MODIFY_SCREEN
    raising
      /VCXI/CX_CKX .
  class-methods START_UI
    importing
      !IR_QUERY type ref to YCL_HP_IDX_QUERY
    raising
      /VCXI/CX_CKX .
  methods DO_PBO
    raising
      /VCXI/CX_CKX .
  methods DO_USER_COMMAND
    changing
      !CF_UCOMM type SYUCOMM
    raising
      /VCXI/CX_CKX .
protected section.

  data S_DYNP type /VCXI/CKX_S_DYNP_KEY .
  data R_QUERY type ref to YCL_HP_IDX_QUERY .
  constants C_UC_BACK type SYUCOMM value 'BACK' ##NO_TEXT.
  constants C_UC_CANCEL type SYUCOMM value 'CANCEL' ##NO_TEXT.
  constants C_UC_EXIT type SYUCOMM value 'EXIT' ##NO_TEXT.
  data R_GRID_RES type ref to YCL_HP_IDX_QRY_UI_GRID_RES .
  data F_REFRESH_REQ type ABAP_BOOL .
  data F_REFRESH_ALL_REQ type ABAP_BOOL .

  methods CONSTRUCTOR
    importing
      !IR_QUERY type ref to YCL_HP_IDX_QUERY
    raising
      /VCXI/CX_CKX .
  methods SET_PFSTATUS
    raising
      /VCXI/CX_CKX .
  methods SET_TITLE
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_BACK
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_EXIT
    raising
      /VCXI/CX_CKX .
  methods HANDLE_UC_CANCEL
    raising
      /VCXI/CX_CKX .
  methods DO_FREE
    raising
      /VCXI/CX_CKX .
  methods INIT_VIS
    raising
      /VCXI/CX_CKX .
private section.

endclass. "YCL_HP_IDX_QRY_UI_RES definition
class YCL_HP_IDX_QRY_UI_RES implementation.
method CONSTRUCTOR.

  move IR_QUERY to ME->R_QUERY.

*** Bind UI
  call function 'YHP_IDX_QRY_UI_RES_BIND'
    exporting
      IR_UI_RES = ME
    importing
      RS_DYNP   = ME->S_DYNP.

*** Init Vis Structure
  ME->INIT_VIS( ).

endmethod.
method DO_FREE.

*** Free Result Grid
  if ME->R_GRID_RES is bound.
    ME->R_GRID_RES->DO_FREE( ).
    clear ME->R_GRID_RES.
  endif.

endmethod.
method DO_MODIFY_SCREEN.
endmethod.
method DO_PBO.

  try.
***----------------------------------------------------------------------------
***   UI - Initialization
      ME->SET_PFSTATUS( ).
      ME->SET_TITLE( ).

***----------------------------------------------------------------------------
***   Refresh Grid
      if ME->R_GRID_RES is bound.
        if ME->F_REFRESH_REQ eq ABAP_TRUE.
          move ABAP_TRUE to ME->R_GRID_RES->F_REFRESH_REQ.
          move ABAP_TRUE to ME->R_GRID_RES->F_REFRESH_ALL_REQ.
        endif.
        ME->R_GRID_RES->REFRESH_GRID( ).
      endif.
      move ABAP_FALSE to ME->F_REFRESH_REQ.

    catch /VCXI/CX_CKX into data(LR_CX_CKX).
      LR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

endmethod.
method DO_USER_COMMAND.

  try.
      case CF_UCOMM.
        when YCL_HP_IDX_QRY_UI_RES=>C_UC_BACK.
          ME->HANDLE_UC_BACK( ).
        when YCL_HP_IDX_QRY_UI_RES=>C_UC_EXIT.
          ME->HANDLE_UC_EXIT( ).
        when YCL_HP_IDX_QRY_UI_RES=>C_UC_CANCEL.
          ME->HANDLE_UC_CANCEL( ).
      endcase.

    catch /VCXI/CX_CKX into data(LR_CX_CKX).
      LR_CX_CKX->SHOW_MESSAGE( ).
  endtry.

  clear CF_UCOMM.

endmethod.
method HANDLE_UC_BACK.

  ME->DO_FREE( ).
  leave to screen 0.

endmethod.
method HANDLE_UC_CANCEL.

  ME->DO_FREE( ).
  leave to screen 0.

endmethod.
method HANDLE_UC_EXIT.

  ME->DO_FREE( ).
  leave to screen 0.

endmethod.
method INIT_VIS.

  data LR_RESULT_DATA type ref to DATA.

*** initialize VIS structure
*** get query result data
  if ME->R_QUERY is bound.
    LR_RESULT_DATA = ME->R_QUERY->GET_RESULT( ).
  endif.

***----------------------------------------------------------------------------
*** initialize grid
  if ME->R_GRID_RES is not bound.
*** Create Grid for result
    create object ME->R_GRID_RES
      exporting
        IF_CONTAINER_NAME = '0100CC_GRID_RES'
        IR_DATA           = LR_RESULT_DATA
        IF_SELTY          = ME->R_QUERY->F_SELTY.
  endif.

endmethod.
method SET_PFSTATUS.

*** Set PF-Status
  set pf-status '0100' of program ME->S_DYNP-REPID.

endmethod.
method SET_TITLE.

*** Set GUI Title
  set titlebar '0100' of program ME->S_DYNP-REPID.

endmethod.
method START_UI.

  data LR_IDX_UI_RES type ref to YCL_HP_IDX_QRY_UI_RES.

*** Create Instance of UI
  create object LR_IDX_UI_RES
    exporting
      IR_QUERY = IR_QUERY.

**** Call Screen
  call function 'YHP_IDX_QRY_UI_RES_START'.

endmethod.
