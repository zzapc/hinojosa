
class YCL_HP_SVCR_BL_PRTE definition
  public
  inheriting from ZCL_VCXI_P1SR_BL_PRTE
  create public .

public section.

  interfaces YIF_HP_SVCR_HFLAG .
  interfaces YIF_HP_SVCR_AREA_WGT .

  aliases F_HFLAG
    for YIF_HP_SVCR_HFLAG~F_HFLAG .
  aliases S_OPQTY
    for YIF_HP_SVCR_AREA_WGT~S_OPQTY .
  aliases S_SBLAR
    for YIF_HP_SVCR_AREA_WGT~S_SBLAR .
  aliases S_SBLARO
    for YIF_HP_SVCR_AREA_WGT~S_SBLARO .

  methods COMPARE
    redefinition .
  methods COPY
    redefinition .
protected section.

  methods DESERIALIZE_INFO
    redefinition .
  methods SERIALIZE_INFO
    redefinition .
private section.
endclass. "YCL_HP_SVCR_BL_PRTE definition
class YCL_HP_SVCR_BL_PRTE implementation.
  method COMPARE.

    data:  LR_BL_PRTE_HP  type ref to YCL_HP_SVCR_BL_PRTE.

    check IR_BL_PRTE is bound.

    try.
        move IR_BL_PRTE ?to LR_BL_PRTE_HP.

***------------------------------------------------------------------------------------------------
*** Check same Homologation flag
        check ME->F_HFLAG  eq LR_BL_PRTE_HP->F_HFLAG.

****** Check same Sold Blank Area per Order and Operation Qty
***        check ME->S_SBLARO  eq LR_BL_PRTE_HP->S_SBLARO.
***        check ME->S_OPQTY   eq LR_BL_PRTE_HP->S_OPQTY.

*** Check same Sold Blank Area per Piece
        check ME->S_SBLAR  eq LR_BL_PRTE_HP->S_SBLAR.

      catch CX_SY_MOVE_CAST_ERROR.
    endtry.

    RF_SIMILAR = SUPER->COMPARE( IR_BL_PRTE = IR_BL_PRTE ).

  endmethod.
  method COPY.
    data: LR_BL_PRTE type ref to YCL_HP_SVCR_BL_PRTE.

***--------------------------------------------------------------------------------------
*** Super Copy
    RR_BL_PRTE = SUPER->COPY( ).

    move RR_BL_PRTE ?to LR_BL_PRTE.

***--------------------------------------------------------------------------------------
*** Copy Homologation flag
    move ME->F_HFLAG  to LR_BL_PRTE->F_HFLAG.

*** Copy Sold Blank Area per Order and Operation Qty
    move ME->S_SBLARO to LR_BL_PRTE->S_SBLARO.
    move ME->S_OPQTY  to LR_BL_PRTE->S_OPQTY.

*** Copy Sold Blank Area per Piece
    move ME->S_SBLAR  to LR_BL_PRTE->S_SBLAR.

  endmethod.
  method DESERIALIZE_INFO.

    data:  LF_XML   type STRING.

    check IF_XML is not initial.

*** Deserialize
    call transformation ID
         source xml IF_XML
         result SUPER_XML   = LF_XML
                HFLAG       = ME->F_HFLAG
                S_SBLARO    = ME->YIF_HP_SVCR_AREA_WGT~S_SBLARO
                S_SBLAR     = ME->YIF_HP_SVCR_AREA_WGT~S_SBLAR
                S_OPQTY     = ME->YIF_HP_SVCR_AREA_WGT~S_OPQTY.

*** Super Deserialize
    if LF_XML is not initial.
      SUPER->DESERIALIZE_INFO( IF_XML = LF_XML ).
    endif.

  endmethod.
  method SERIALIZE_INFO.

*** Super Serialize
    RF_XML = SUPER->SERIALIZE_INFO( ).

*** Serialize
    call transformation ID
         source SUPER_XML   = RF_XML
                HFLAG       = ME->F_HFLAG
                S_SBLARO    = ME->YIF_HP_SVCR_AREA_WGT~S_SBLARO
                S_SBLAR     = ME->YIF_HP_SVCR_AREA_WGT~S_SBLAR
                S_OPQTY     = ME->YIF_HP_SVCR_AREA_WGT~S_OPQTY
         result xml RF_XML.

  endmethod.
  method YIF_HP_SVCR_AREA_WGT~SET_OPQTY.

    ME->S_OPQTY = IS_OPQTY.

  endmethod.
  method YIF_HP_SVCR_AREA_WGT~SET_SBLAR.

    ME->S_SBLAR = IS_SBLAR.

  endmethod.
  method YIF_HP_SVCR_AREA_WGT~SET_SBLARO.

    ME->S_SBLARO = IS_SBLARO.

  endmethod.
  method YIF_HP_SVCR_HFLAG~SET_HFLAG.

    ME->YIF_HP_SVCR_HFLAG~F_HFLAG = IF_HFLAG.

  endmethod.
