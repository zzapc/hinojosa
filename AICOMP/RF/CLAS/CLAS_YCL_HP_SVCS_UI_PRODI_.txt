
class YCL_HP_SVCS_UI_PRODI definition
  public
  inheriting from ZCL_VCXI_XCSS_UI_PRODI
  create public .

public section.

  data S_HP_PRODI_VIS type YHP_SVCS_S_PRODI_VIS .
  data R_PR_XCOMP type ref to ZCL_VCXI_PXSS_PR_XCOMP .

  methods CONSTRUCTOR
    importing
      !IR_UIFW type ref to /VCXI/IF_VKSU_UIFW
      !IR_UIGATE type ref to /VCXI/CL_VKSRU_UIGATE
      !IR_GATE type ref to /VCXI/CL_VKSR_GATE
    raising
      /VCXI/CX_CKX .
  methods DO_UPDATE_PRODI_VIS_HP
    importing
      !IS_PRODI_VIS type YHP_SVCS_S_PRODI_VIS
    raising
      /VCXI/CX_CKX .
  methods CALL_SHLP_MRP
    returning
      value(RF_MRP) type YHP_SVCS_MRP
    raising
      /VCXI/CX_CKX .

  methods ACTIVATE
    redefinition .
  methods DO_MODIFY_SCREEN
    redefinition .
protected section.

  data R_PR_SVCS_PRODI type ref to YCL_HP_SVCS_PR_PRODI .

  methods INIT_VIS
    redefinition .
private section.
endclass. "YCL_HP_SVCS_UI_PRODI definition
class YCL_HP_SVCS_UI_PRODI implementation.
  method ACTIVATE.
    SUPER->ACTIVATE( ).

    call function 'YHP_SVCS_UI_PRODI_BIND'
      exporting
        IR_UI_PRODI = ME
      importing
        ES_DYNP   = ME->S_DYNP.
  endmethod.
  method CALL_SHLP_MRP.

    RF_MRP = ME->R_PR_SVCS_PRODI->CALL_SHLP_MRP( ).

  endmethod.
  method CONSTRUCTOR.
    SUPER->CONSTRUCTOR( IR_UIFW = IR_UIFW
                        IR_UIGATE = IR_UIGATE
                        IR_GATE = IR_GATE ).
*** Assign th RP Class to the UI Class
    try.
        move IR_GATE->R_CLSPR ?to ME->R_PR_SVCS_PRODI.
      catch CX_SY_MOVE_CAST_ERROR.
***   UI ABAP Class is not compatible with PR ABAP Class.
        raise exception type /VCXI/CX_CKX
          exporting
            F_MSGTY = 'E'
            F_MSGID = '/VCXI/VKSR'
            F_MSGNO = '801'.
    endtry.

  endmethod.
  method DO_MODIFY_SCREEN.
*try.
CALL METHOD SUPER->DO_MODIFY_SCREEN
  EXPORTING
    IF_REPID = if_repid
    IF_DYNNR = if_dynnr
    .
*endtry.
  loop at screen.
          case SCREEN-GROUP1.
            when 'PRH'.
              if ME->R_PR_PRODI->GET_FOVRR_PRODH( ) eq ABAP_TRUE.
                move 1 to SCREEN-INPUT.
              else.
                move 0 to SCREEN-INPUT.
              endif.
          endcase.

          modify screen.
        endloop.

  endmethod.
  method DO_UPDATE_PRODI_VIS_HP.
    data: LS_PRODI_VIS type ZVCXI_XCSS_S_PRODI_VIS.

    LS_PRODI_VIS = corresponding #( IS_PRODI_VIS ).

    ME->DO_UPDATE_PRODI_VIS( IS_PRODI_VIS = LS_PRODI_VIS ).

*** Production Information Client specification
    if ME->S_HP_PRODI_VIS-CSPEC ne IS_PRODI_VIS-CSPEC.
      ME->R_PR_SVCS_PRODI->SET_CSPEC( IF_CSPEC = IS_PRODI_VIS-CSPEC
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Production Information  FSC Type
    if ME->S_HP_PRODI_VIS-FSCTY ne IS_PRODI_VIS-FSCTY.
      ME->R_PR_SVCS_PRODI->SET_FSCTY( IF_FSCTY = IS_PRODI_VIS-FSCTY
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Production Information  Packaging Usage
    if ME->S_HP_PRODI_VIS-PACKU ne IS_PRODI_VIS-PACKU.
      ME->R_PR_SVCS_PRODI->SET_PACKU( IF_PACKU = IS_PRODI_VIS-PACKU
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Production Information Observations
    if ME->S_HP_PRODI_VIS-OBSER ne IS_PRODI_VIS-OBSER.
      ME->R_PR_SVCS_PRODI->SET_OBSER( IF_OBSER = IS_PRODI_VIS-OBSER
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Production Information Homologation
    if ME->S_HP_PRODI_VIS-HFLAG ne IS_PRODI_VIS-HFLAG.
      ME->R_PR_SVCS_PRODI->SET_HFLAG( IF_HFLAG = IS_PRODI_VIS-HFLAG
                                      IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

*** Production Information MRP
    if ME->S_HP_PRODI_VIS-MRP ne IS_PRODI_VIS-MRP.
      ME->R_PR_SVCS_PRODI->SET_MRP( IF_MRP   = IS_PRODI_VIS-MRP
                                    IF_SETBY = /VCXI/CL_VKSR_GATE=>C_SETBY_USER ).
    endif.

  endmethod.
  method INIT_VIS.

    SUPER->INIT_VIS( ).

    S_HP_PRODI_VIS = corresponding #( ME->S_PRODI_VIS ).

*** Client specification
    move ME->R_PR_SVCS_PRODI->GET_CSPEC( )       to ME->S_HP_PRODI_VIS-CSPEC.

*** FSC Type
    move ME->R_PR_SVCS_PRODI->GET_FSCTY( )       to ME->S_HP_PRODI_VIS-FSCTY.

*** Packaging Usage
    move ME->R_PR_SVCS_PRODI->GET_PACKU( )       to ME->S_HP_PRODI_VIS-PACKU.

*** Packaging Usage Description
    move ME->R_PR_SVCS_PRODI->GET_PACKU_DESCR( ) to ME->S_HP_PRODI_VIS-PACKU_DESCR.

*** Observations
    move ME->R_PR_SVCS_PRODI->GET_OBSER( )       to ME->S_HP_PRODI_VIS-OBSER.

*** Homologation
    move ME->R_PR_SVCS_PRODI->GET_HFLAG( )       to ME->S_HP_PRODI_VIS-HFLAG.

*** MRP
    move ME->R_PR_SVCS_PRODI->GET_MRP( ) to ME->S_HP_PRODI_VIS-MRP.

  endmethod.
