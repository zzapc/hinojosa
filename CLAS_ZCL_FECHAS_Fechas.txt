class ZCL_FECHAS definition
  public
  final
  create public .

public section.

  class-methods GET_NOMBRE_MES
    importing
      !MES type T247-MNR
    returning
      value(NOMBRE) type T247-LTX .
  class-methods GET_NOMBRE_MES_CORTO
    importing
      !MES type T247-MNR
    returning
      value(NOMBRE) type T247-LTX .
  class-methods GET_SEMANA
    importing
      !FECHA type SCAL-DATE
    returning
      value(SEMANA) type SCAL-WEEK .
  class-methods GET_NUMERO_DIA
    importing
      !FECHA type DATUM
    returning
      value(NUM_DIA) type NUMC1 .
  class-methods GET_PRIMER_DIA_SEMANA
    importing
      !SEMANA type SCAL-WEEK
    returning
      value(FECHA) type SCAL-DATE .
  class-methods GET_ULTIMO_DIA_SEMANA
    importing
      !SEMANA type SCAL-WEEK
    returning
      value(FECHA) type SCAL-DATE .
  class-methods GET_PERIODO_ANTERIOR
    importing
      !PER type SPMON optional
      !FECHA type DATUM default SY-DATUM
    returning
      value(PERIODO) type SPMON .
  class-methods SUMA_MESES
    importing
      !MESES type I
      !FECHA type DATUM
    returning
      value(NUEVA_FECHA) type DATUM .
  class-methods GET_PRIMER_DIA_MES
    importing
      !FECHA type DATUM default SY-DATUM
    returning
      value(NUEVA_FECHA) type DATUM .
  class-methods GET_ULTIMO_DIA_MES
    importing
      !FECHA type DATUM default SY-DATUM
    returning
      value(NUEVA_FECHA) type DATUM .
  type-pools ABAP .
  class-methods HAY_SOLAPAMIENTO_FECHAS
    importing
      !FINI1 type D
      !FFIN1 type D
      !HINI1 type T default '000000'
      !HFIN1 type T default '240000'
      !FINI2 type D
      !FFIN2 type D
      !HINI2 type T default '000000'
      !HFIN2 type T default '240000'
    returning
      value(HAY_SOLAPAMIENTO) type ABAP_BOOL .
  class-methods INTERSECCION_FECHAS
    importing
      !FINI1 type D
      !FFIN1 type D
      !HINI1 type T default '000000'
      !HFIN1 type T default '240000'
      !FINI2 type D
      !FFIN2 type D
      !HINI2 type T default '000000'
      !HFIN2 type T default '240000'
    exporting
      value(RHINI) type T
      value(RHFIN) type T
      value(RFINI) type D
      value(RFFIN) type D .
  class-methods UNION_FECHAS
    importing
      !FINI1 type D
      !FFIN1 type D
      !HINI1 type T default '000000'
      !HFIN1 type T default '240000'
      !FINI2 type D
      !FFIN2 type D
      !HINI2 type T default '000000'
      !HFIN2 type T default '240000'
    exporting
      value(RHINI) type T
      value(RHFIN) type T
      value(RFINI) type D
      value(RFFIN) type D .
  class-methods RESTA_FECHAS
    importing
      !FINI1 type D
      !FFIN1 type D
      !HINI1 type T default '000000'
      !HFIN1 type T default '240000'
      !FINI2 type D
      !FFIN2 type D
      !HINI2 type T default '000000'
      !HFIN2 type T default '240000'
    exporting
      value(RHINI) type T
      value(RHFIN) type T
      value(RFINI) type D
      value(RFFIN) type D .
  class-methods PRIMER_DIA_LAB_DESDE_FECHA
    importing
      !FECHA type DATUM
      !CALID type SCAL-FCALID default 'ES'
      !CORRECT_OPTION type SCAL-INDICATOR default '+'
    preferred parameter FECHA
    returning
      value(FECHA_SALIDA) type DATUM .
  class-methods PERIODOS_RESTA_FECHAS
    importing
      !FECHA1 type D
      !FECHA2 type D
      !OUTPUT_FORMAT type CHAR2 default '05'
    exporting
      !ANYOS type P0347-SCRYY
      !MESES type P0347-SCRMM
      !DIAS type P0347-SCRDD .
  class-methods RANGO2FECHAS
    importing
      !R_FECHAS type TPMY_R_DATE
    exporting
      !FECHA_DESDE type BEGDA
      !FECHA_HASTA type ENDDA .
  class-methods STRING2FECHA
    importing
      !CADENA type ANY
    preferred parameter CADENA
    returning
      value(FECHA) type D .
  class-methods GET_ULTIMO_DIA_MES_ANTERIOR
    importing
      !FECHA type DATUM default SY-DATUM
    returning
      value(NUEVA_FECHA) type DATUM .
  class-methods GET_PERIODO_SIGUIENTE
    importing
      !PER type SPMON optional
      !FECHA type DATUM default SY-DATUM
    returning
      value(PERIODO) type SPMON .
  class-methods ES_VALIDA
    importing
      !FECHA type SY-DATUM
    returning
      value(OK) type ABAP_BOOL .
  class-methods FECHA_A_LETRAS
    importing
      !FECHA type DATS default SY-DATUM
    returning
      value(LETRAS) type STRING .
  class-methods STRING2HORA
    importing
      !STRING type ANY
    returning
      value(HORA) type SY-UZEIT .
  class-methods GET_DURACION_INTERVALO
    importing
      !FINI type D
      !FFIN type D
      !HINI type T default '000000'
      !HFIN type T default '240000'
    returning
      value(HORAS) type EAUSZT .
  class-methods AAAAMMDD_2_DDMMAAAA
    importing
      !FECHA_AAAAMMDD type D
    returning
      value(FECHA_DDMMAAAA) type D .
  class-methods FECHA_HORA_EN_RANGO
    importing
      !R_FECHAS type TPMY_R_DATE
      !R_HORAS type JIT_ITIME_RANGE_TT
      !FECHA type SY-DATUM
      !HORA type SY-UZEIT
    returning
      value(SI) type ABAP_BOOL .
  class-methods RANGOSEM_2_RANGOFECHAS
    importing
      !R_SEMANAS type RSARCH_RT_CALWEEK
    returning
      value(R_FECHAS) type TPMY_R_DATE .
  class-methods ES_FESTIVO
    importing
      !FECHA type DATUM
      !CALID type ANY default 'ES'
    preferred parameter FECHA
    returning
      value(FESTIVO) type ABAP_BOOL .
protected section.
private section.
endclass. "ZCL_FECHAS definition
class ZCL_FECHAS implementation.
method AAAAMMDD_2_DDMMAAAA.

  fecha_ddmmaaaa(2) = fecha_aaaammdd+6(2).
  fecha_ddmmaaaa+2(2) = fecha_aaaammdd+4(2).
  fecha_ddmmaaaa+4(4) = fecha_aaaammdd(4).

endmethod.
METHOD es_festivo.
  DATA: l_sig_fecha TYPE d,
        l_calid TYPE scal-fcalid.

  CLEAR festivo.
  l_calid = calid.
  CALL FUNCTION 'DATE_CONVERT_TO_FACTORYDATE'
    EXPORTING
      correct_option               = '+'
      date                         = fecha
      factory_calendar_id          = l_calid
    IMPORTING
      date                         = l_sig_fecha
    EXCEPTIONS
      calendar_buffer_not_loadable = 1
      correct_option_invalid       = 2
      date_after_range             = 3
      date_before_range            = 4
      date_invalid                 = 5
      factory_calendar_not_found   = 6
      OTHERS                       = 7.

  IF sy-subrc = 0.
    IF fecha NE l_sig_fecha.
      festivo = 'X'.
    ENDIF.
  ENDIF.

ENDMETHOD.
method ES_VALIDA.

  CLEAR ok.
  CALL FUNCTION 'DATE_CHECK_PLAUSIBILITY'
    EXPORTING
      date                      = fecha
    EXCEPTIONS
      plausibility_check_failed = 1
      OTHERS                    = 2.
  IF sy-subrc = 0.
    ok = 'X'.
  ENDIF.

endmethod.
method FECHA_A_LETRAS.

  letras = get_nombre_mes( fecha+4(2) ).
  CONCATENATE fecha+6(2) 'de' letras ' de' fecha(4) into letras SEPARATED BY space.

endmethod.
METHOD fecha_hora_en_rango.
  DATA: l_rfecha TYPE tpms_r_date,
        l_rhora TYPE jit_itime_range,
        l_fini TYPE d,
        l_ffin TYPE d,
        l_hini TYPE sy-uzeit,
        l_hfin TYPE sy-uzeit.


  IF r_horas IS INITIAL.
    l_hini = '000000'.
    l_hfin = '235959'.
  ELSE.
    READ TABLE r_horas INTO l_rhora INDEX 1.
    l_hini = l_rhora-low.
    IF l_rhora-high IS INITIAL.
      l_hfin = l_rhora-low.
    ELSE.
      l_hfin = l_rhora-high.
    ENDIF.
  ENDIF.

  rango2fechas( EXPORTING r_fechas = r_fechas
                IMPORTING fecha_desde = l_fini
                          fecha_hasta = l_ffin ).

  si = hay_solapamiento_fechas( fini1 = l_fini
                                ffin1 = l_ffin
                                hini1 = l_hini
                                hfin1 = l_hfin
                                fini2 = fecha
                                ffin2 = fecha
                                hini2 = hora
                                hfin2 = hora ).

ENDMETHOD.
method GET_DURACION_INTERVALO.

  IF NOT fini IS INITIAL AND
     NOT ffin IS INITIAL.
    DATA: l_dias TYPE i,
          l_horas TYPE p DECIMALS 2.
    l_dias = ffin - fini.
    IF l_dias > 69000.
      l_horas = 99999.
    ELSE.
      IF l_dias >= 0.
        l_horas = ( hfin - hini ) / 3600.
        IF l_horas > 0 OR l_dias > 0.
          horas = 24 * l_dias + l_horas.
        ENDIF.
      ENDIF.
    ENDIF.
  ENDIF.
endmethod.
method GET_NOMBRE_MES.

  CLEAR nombre.
  SELECT SINGLE ltx FROM t247
    INTO nombre
  where spras = sy-langu
    and mnr = mes.

endmethod.
method GET_NOMBRE_MES_CORTO.

  CLEAR nombre.
  SELECT SINGLE KTX FROM t247
    INTO nombre
  where spras = sy-langu
    and mnr = mes.

endmethod.
method GET_NUMERO_DIA.
data l_WOTNR TYPE  P.

  call function 'DAY_IN_WEEK'
       exporting
             datum = fecha
       importing
             wotnr = l_WOTNR.
  num_dia = l_wotnr.

endmethod.
method GET_PERIODO_ANTERIOR.
  DATA l_fecha TYPE d.

  IF NOT per IS INITIAL.
    CONCATENATE per '01' INTO l_fecha.
  ELSE.
    l_fecha = fecha.
  ENDIF.
  l_fecha = suma_meses( meses = -1 fecha = l_fecha ).

  periodo = l_fecha(6).

endmethod.
method GET_PERIODO_SIGUIENTE.
  DATA l_fecha TYPE d.

  IF NOT per IS INITIAL.
    CONCATENATE per '01' INTO l_fecha.
  ELSE.
    l_fecha = fecha.
  ENDIF.
  l_fecha = suma_meses( meses = 1 fecha = l_fecha ).

  periodo = l_fecha(6).

endmethod.
method GET_PRIMER_DIA_MES.

  nueva_fecha = fecha.
  nueva_fecha+6(2) = '01'.

endmethod.
method GET_PRIMER_DIA_SEMANA.

  call function 'WEEK_GET_FIRST_DAY'
       exporting
             week = semana
       importing
             date = fecha
       exceptions
             week_invalid = 1
             others = 2.
endmethod.
method GET_SEMANA.

  CALL FUNCTION 'DATE_GET_WEEK'
    EXPORTING
      date         = fecha
    IMPORTING
      week         = semana
    EXCEPTIONS
      date_invalid = 1
      OTHERS       = 2.

endmethod.
method GET_ULTIMO_DIA_MES.

  CALL FUNCTION 'RP_LAST_DAY_OF_MONTHS'
    EXPORTING
      day_in            = fecha
    IMPORTING
      last_day_of_month = nueva_fecha
    EXCEPTIONS
      day_in_no_date    = 1
      OTHERS            = 2.

endmethod.
method GET_ULTIMO_DIA_MES_ANTERIOR.
  DATA l_fecha TYPE d.

  CONCATENATE fecha(6) '01' INTO l_fecha.
  NUEVA_FECHA = l_fecha - 1.

endmethod.
method GET_ULTIMO_DIA_SEMANA.

  fecha = get_primer_dia_semana( semana ).
  add 6 to fecha.

endmethod.
method HAY_SOLAPAMIENTO_FECHAS.
  data: l_ini1(14),
        l_fin1(14),
        l_ini2(14),
        l_fin2(14).

  concatenate fini1 hini1 into l_ini1.
  concatenate fini2 hini2 into l_ini2.
  concatenate ffin1 hfin1 into l_fin1.
  concatenate ffin2 hfin2 into l_fin2.

  clear: hay_solapamiento.
  if l_ini1 <= l_fin2 and
     l_fin1 >= l_ini2.

    hay_solapamiento = 'X'.

  endif.

endmethod.
method INTERSECCION_FECHAS.


  clear: rfini, rffin, rhini, rhfin.

  if HAY_SOLAPAMIENTO_FECHAS( fini1 = fini1 ffin1 = ffin1
                              fini2 = fini2 ffin2 = ffin2
                              hini1 = hini1 hfin1 = hfin1
                              hini2 = hini2 hfin2 = hfin2 ) = 'X'.

    if fini1 = fini2.
      rfini = fini1.
      if hini1 >= hini2.
        rhini = hini1.
      else.
        rhini = hini2.
      endif.
    elseif fini1 < fini2.
      rfini = fini2.
      rhini = hini2.
    else.
      rfini = fini1.
      rhini = hini1.
    endif.

    if ffin1 = ffin2.
      rffin = ffin1.
      if hfin1 >= hfin2.
        rhfin = hfin2.
      else.
        rhfin = hfin1.
      endif.
    elseif ffin1 < ffin2.
      rffin = ffin1.
      rhfin = hfin1.
    else.
      rffin = ffin2.
      rhfin = hfin2.
    endif.
  endif.

endmethod.
method PERIODOS_RESTA_FECHAS.

  CALL FUNCTION 'HR_HK_DIFF_BT_2_DATES'
    EXPORTING
      date1                   = fecha1
      date2                   = fecha2
      output_format           = output_format
    IMPORTING
      years                   = anyos
      months                  = meses
      days                    = dias
    EXCEPTIONS
      invalid_dates_specified = 1
      OTHERS                  = 2.

endmethod.
method PRIMER_DIA_LAB_DESDE_FECHA.

*       Si la fecha dada es laborable devuelve esa fecha,
*       sino devuelve el primer día laborable a partir de esa fecha

  CLEAR fecha_salida.
  CALL FUNCTION 'DATE_CONVERT_TO_FACTORYDATE'
    EXPORTING
      correct_option               = correct_option
      date                         = fecha
      factory_calendar_id          = calid
    IMPORTING
      date                         = fecha_salida
    EXCEPTIONS
      calendar_buffer_not_loadable = 1
      correct_option_invalid       = 2
      date_after_range             = 3
      date_before_range            = 4
      date_invalid                 = 5
      factory_calendar_not_found   = 6
      OTHERS                       = 7.


endmethod.
method RANGO2FECHAS.
  DATA: l_fecha TYPE tpms_r_date,
        r_fechas_aux TYPE tpmy_r_date.

  DESCRIBE TABLE r_fechas LINES sy-tfill.

  IF sy-tfill = 0.
    fecha_desde = '00000000'.
    fecha_hasta = '99991231'.
  ELSEIF sy-tfill = 1.
    READ TABLE r_fechas INTO l_fecha INDEX 1.
    fecha_desde = l_fecha-low.
    IF l_fecha-high IS INITIAL.
      fecha_hasta = fecha_desde.
    ELSE.
      fecha_hasta = l_fecha-high.
    ENDIF.
  ELSE.
    r_fechas_aux = r_fechas.
    SORT r_fechas_aux BY low.
    READ TABLE r_fechas_aux INTO l_fecha INDEX 1.
    fecha_desde = l_fecha-low.
    DESCRIBE TABLE r_fechas LINES sy-tfill.
    READ TABLE r_fechas_aux INTO l_fecha INDEX sy-tfill.
    IF l_fecha-high IS INITIAL.
      fecha_hasta = l_fecha-low.
    ELSE.
      fecha_hasta = l_fecha-high.
    ENDIF.
  ENDIF.

endmethod.
METHOD rangosem_2_rangofechas.
  DATA: l_fecha TYPE tpms_r_date,
        l_week TYPE rsarch_rs_calweek.

  CLEAR r_fechas.
  DESCRIBE TABLE r_semanas LINES sy-tfill.

  CHECK sy-tfill > 0.

  LOOP AT r_semanas INTO l_week.
    l_fecha-option = l_week-option.
    l_fecha-sign   = l_week-sign.
    l_fecha-low    = get_primer_dia_semana( l_week-low ).
    IF l_week-high IS INITIAL.
      l_fecha-high = get_ultimo_dia_semana( l_week-low ).
    ELSE.
      l_fecha-high = get_ultimo_dia_semana( l_week-high ).
    ENDIF.
    APPEND l_fecha TO r_fechas.
  ENDLOOP.

ENDMETHOD.
method RESTA_FECHAS.
  data: l_ini1(14),
        l_fin1(14),
        l_ini2(14),
        l_fin2(14).

  concatenate fini1 hini1 into l_ini1.
  concatenate fini2 hini2 into l_ini2.
  concatenate ffin1 hfin1 into l_fin1.
  concatenate ffin2 hfin2 into l_fin2.

  rfini = fini1. rffin = ffin1.
  rhini = hini1. rhfin = hfin1.

  if l_ini1 <= l_fin2 and
     l_fin1 >= l_ini2.

    if l_ini1 <= l_fin2.
      rfini = fini1.
      rhini = hini1.
    else.
      rfini = ffin2.
      rhini = hfin2.
    endif.

    if l_fin1 >= l_ini2.
      rffin = ffin2.
      rhfin = hfin2.
    else.
      rffin = fini2.
      rhfin = hini2.
    endif.

  endif.
endmethod.
method STRING2FECHA.
  DATA: l_cadena(20),
        l_dia(2),
        l_mesn(2),
        l_mes(20).

  l_cadena = cadena.
  TRANSLATE l_cadena TO UPPER CASE.
  IF l_cadena CS '/'.
    fecha(4) = l_cadena+6(4).
    fecha+4(2) = l_cadena+3(2).
    fecha+6(2) = l_cadena(2).
  ELSEIF l_cadena(3) = 'LUN' OR l_cadena(3) = 'MAR' OR l_cadena(3) = 'MIÉ' OR l_cadena(4) = 'JUE'
      OR l_cadena(3) = 'VIE' OR l_cadena(3) = 'SÁB' OR l_cadena(3) = 'DOM'.
    l_cadena = l_cadena+4.
    SPLIT l_cadena AT ' ' INTO l_dia l_mes.
    zcl_ap_string=>poner_ceros_c( CHANGING cadena = l_dia ).
    SELECT SINGLE mnr FROM t247
      INTO l_mesn
    WHERE spras = sy-langu
      AND ktx = l_mes.
    IF sy-subrc = 0.
      CONCATENATE sy-datum(4) l_mesn l_dia INTO fecha.
    ENDIF.
  ELSE.
    CALL FUNCTION 'CONVERT_DATE_TO_INTERNAL'
      EXPORTING
        date_external                  = l_cadena
*     ACCEPT_INITIAL_DATE            =
     IMPORTING
       date_internal                  = fecha
     EXCEPTIONS
       date_external_is_invalid       = 1
       OTHERS                         = 2.
  ENDIF.

endmethod.
method STRING2HORA.
  DATA: l_hh(10), l_mm(10), l_ss(10),
        l_h(2) TYPE n, l_m(2) TYPE n, l_s(2) TYPE n.

  SPLIT string AT ':' INTO l_hh l_mm.
  IF l_mm CS ':'.
    SPLIT l_mm AT ':' INTO l_mm l_ss.
    l_m = l_mm(2).
    l_s = l_ss(2).
  ELSE.
    l_m = l_mm(2).
    l_s = '00'.
  ENDIF.
  l_h = l_hh(2).

  zcl_ap_string=>poner_ceros_c( CHANGING cadena = l_h ).
  zcl_ap_string=>poner_ceros_c( CHANGING cadena = l_m ).
  zcl_ap_string=>poner_ceros_c( CHANGING cadena = l_s ).
  CONCATENATE l_h l_m l_s INTO hora.

endmethod.
method SUMA_MESES.

  CALL FUNCTION 'MONTH_PLUS_DETERMINE'
    EXPORTING
      months  = meses
      olddate = fecha
    IMPORTING
      newdate = nueva_fecha.

endmethod.
method UNION_FECHAS.


  clear: rfini, rffin, rhini, rhfin.


  if fini1 = fini2.
    rfini = fini1.
    if hini1 <= hini2.
      rhini = hini1.
    else.
      rhini = hini2.
    endif.
  elseif fini1 > fini2.
    rfini = fini2.
    rhini = hini2.
  else.
    rfini = fini1.
    rhini = hini1.
  endif.

  if ffin1 = ffin2.
    rffin = ffin1.
    if hfin1 <= hfin2.
      rhfin = hfin2.
    else.
      rhfin = hfin1.
    endif.
  elseif ffin1 > ffin2.
    rffin = ffin1.
    rhfin = hfin1.
  else.
    rffin = ffin2.
    rhfin = hfin2.
  endif.


endmethod.
