FUNCTION zoperacion_rollo.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     VALUE(I_INPUT) TYPE  ZDT_NOTRO_L
*"  EXPORTING
*"     VALUE(E_MBLNR) TYPE  MBLNR
*"     VALUE(E_MJAHR) TYPE  MJAHR
*"     VALUE(E_CHARG_PULPER) TYPE  CHARG_D
*"     VALUE(E_CHARG_ORIGEN) TYPE  CHARG_D
*"     VALUE(E_ERROR) TYPE  XFELD
*"  TABLES
*"      T_MESSAGES STRUCTURE  BAPIRET2 OPTIONAL
*"----------------------------------------------------------------------
* Realizar una conversión del material y lote del rollo (movimiento 309)
*"----------------------------------------------------------------------

* 07/04/2021 - 65397 - CMS sustituir 309 por mov.261 + 531

  DATA: lv_aufnr TYPE aufnr.

  DATA: ls_header        TYPE bapi2017_gm_head_01,
        lt_return        TYPE STANDARD TABLE OF bapiret2,
        lt_item          TYPE TABLE OF bapi2017_gm_item_create,
        ls_goodsmvt_item TYPE bapi2017_gm_item_create,
        ls_item_531      TYPE bapi2017_gm_item_create.

  DATA: lv_extnumber TYPE balnrext.

  CLEAR: lt_item[],
         lt_return[],
         ls_header,
         ls_goodsmvt_item.

* cabecera
  ls_header-pstng_date =  sy-datum.
  ls_header-doc_date   =  sy-datum.
  ls_header-pr_uname   =  sy-uname.
  ls_header-header_txt = 'Rollo a pulper MES'.

** posicion

  ls_goodsmvt_item-orderid = |{ i_input-ord_fab ALPHA = IN }|.

  SELECT SINGLE plnbez FROM afko
    INTO ls_goodsmvt_item-material
    WHERE aufnr =  ls_goodsmvt_item-orderid.

  SELECT SINGLE lgort INTO  ls_goodsmvt_item-stge_loc
   FROM afpo
   WHERE aufnr = ls_goodsmvt_item-orderid .

  SELECT SINGLE charg FROM mseg
    INTO ls_goodsmvt_item-batch
    WHERE matnr = ls_goodsmvt_item-material AND
          werks = i_input-centro  AND
          lgort = ls_goodsmvt_item-stge_loc AND
          sgtxt = i_input-id_rollo_mes.

* Obtener kg del lote (CMS - 22/02/202)
  SELECT SINGLE clabs
    INTO ls_goodsmvt_item-entry_qnt
    FROM mchb
    WHERE matnr = ls_goodsmvt_item-material   AND
          werks = i_input-centro              AND
          lgort = '1020'                      AND
          charg = ls_goodsmvt_item-batch.

  ls_goodsmvt_item-entry_uom    = 'KG'.
  ls_goodsmvt_item-plant        = i_input-centro.
  ls_goodsmvt_item-item_text    = i_input-id_rollo_mes.
  ls_goodsmvt_item-move_type    = '261'.
  ls_goodsmvt_item-stck_type    = ' '.

  APPEND ls_goodsmvt_item TO lt_item.

  CALL FUNCTION 'BAPI_GOODSMVT_CREATE'
    EXPORTING
      goodsmvt_header  = ls_header
      goodsmvt_code    = '06'
    IMPORTING
      materialdocument = e_mblnr
      matdocumentyear  = e_mjahr
    TABLES
      goodsmvt_item    = lt_item
      return           = lt_return.

  READ TABLE lt_return INTO DATA(ls_return) WITH KEY type = 'E'.
  IF sy-subrc <> 0.
    CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
      EXPORTING
        wait = 'X'.

  ELSE.

    e_error = 'X'.

    CLEAR lv_extnumber.

    lv_extnumber = i_input-id_rollo_mes.

    CALL FUNCTION 'ZMFPP_GUARDAR_LOG'
      EXPORTING
        objeto    = 'ZSAP_MES'
        subobjeto = 'ZOPERACION_ROLLO'
        mensajes  = lt_return[]
        extnumber = lv_extnumber.
    EXIT.
  ENDIF.

* Datos para mov. 531.
  CLEAR: lt_item[], lt_return[], ls_item_531 , e_mblnr , e_mjahr.
* cabecera
  ls_header-pstng_date =  sy-datum.
  ls_header-doc_date   =  sy-datum.
  ls_header-pr_uname   =  sy-uname.
  ls_header-header_txt = 'Rollo a pulper MES'.

  ls_item_531-stge_loc   = ls_goodsmvt_item-stge_loc.
  ls_item_531-orderid    = ls_goodsmvt_item-orderid.
  ls_item_531-material   = gc_matnr_bobpulper.
  ls_item_531-plant      = ls_goodsmvt_item-plant.
  ls_item_531-move_type  = '531'.
  ls_item_531-entry_qnt  = ls_goodsmvt_item-entry_qnt.
  ls_item_531-entry_uom  = 'KG'.

*  PERFORM obtener_reserva USING ls_item_531-orderid
*                       CHANGING ls_item_531-reserv_no
*                                ls_item_531-res_item.
  APPEND ls_item_531 TO lt_item.

  CALL FUNCTION 'BAPI_GOODSMVT_CREATE'
    EXPORTING
      goodsmvt_header  = ls_header
      goodsmvt_code    = '03'
    IMPORTING
      materialdocument = e_mblnr
      matdocumentyear  = e_mjahr
    TABLES
      goodsmvt_item    = lt_item
      return           = lt_return.

  IF line_exists( lt_return[ type = 'E' ] ).

    e_error = 'X'.

    CLEAR lv_extnumber.

    lv_extnumber = i_input-id_rollo_mes.

    CALL FUNCTION 'ZMFPP_GUARDAR_LOG'
      EXPORTING
        objeto    = 'ZSAP_MES'
        subobjeto = 'ZOPERACION_ROLLO'
        mensajes  = lt_return[]
        extnumber = lv_extnumber.
    EXIT.

  ELSE.

    CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
      EXPORTING
        wait = 'X'.

* Obtener rollo pulper creado a partir del doc.material creado en el mov.previo(531)
    SELECT SINGLE b~charg
      INTO @e_charg_pulper
      FROM mseg AS s
      INNER JOIN mchb AS b ON b~matnr = s~matnr AND
                              b~werks = s~werks AND
                              b~lgort = s~lgort AND
                              b~charg = s~charg
      WHERE mblnr = @e_mblnr AND
            mjahr = @e_mjahr.

    e_charg_origen = ls_goodsmvt_item-batch.

  ENDIF.

ENDFUNCTION.
