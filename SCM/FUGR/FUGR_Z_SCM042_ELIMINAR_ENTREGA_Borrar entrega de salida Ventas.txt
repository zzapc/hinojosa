FUNCTION z_scm042_eliminar_entrega.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     VALUE(I_VBELN) TYPE  VBELN_VL
*"  EXPORTING
*"     VALUE(ET_RETURN) TYPE  BAPIRET2_TT
*"----------------------------------------------------------------------
* JGM 02.2024 GAPCM042 Aplicativo Gestión de Desvíos:
* Borrar entrega de salida - Ventas.
*--------------------------------------------------------------------*
  DATA: lt_return TYPE bapiret2_tt.

  DATA: ls_header_data    TYPE bapiobdlvhdrchg,
        ls_header_control TYPE bapiobdlvhdrctrlchg.
*        lv_delivery       TYPE bapiobdlvhdrchg-deliv_numb.
  DATA: lr_type_error TYPE RANGE OF bapi_mtype.


  lr_type_error = VALUE #( sign = 'I' option = 'EQ' ( low = 'E' ) ( low = 'A' ) ( low = 'X' ) ).

  ls_header_data-deliv_numb = i_vbeln.
  ls_header_control-deliv_numb  = i_vbeln.
  ls_header_control-dlv_del     = 'X'.

  CALL FUNCTION 'BAPI_OUTB_DELIVERY_CHANGE'
    EXPORTING
      header_data    = ls_header_data
      header_control = ls_header_control
      delivery       = i_vbeln
    TABLES
      return         = lt_return.

  LOOP AT lt_return TRANSPORTING NO FIELDS WHERE type IN lr_type_error.
    EXIT.
  ENDLOOP.
  IF sy-subrc EQ 0.
    CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.

    "Error eliminando entrega:".
    PERFORM fill_return USING 'E' 'ZSCM042' '026' i_vbeln space space space CHANGING et_return.
  ELSE.
    CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
      EXPORTING
        wait = 'X'.

    "Eliminada entrega".
    PERFORM fill_return USING 'S' 'ZSCM042' '025' i_vbeln space space space CHANGING et_return.
  ENDIF.

  "Solo mensajes de error de la bapi.
  DELETE lt_return WHERE type NOT IN lr_type_error.

  APPEND LINES OF lt_return TO et_return.

ENDFUNCTION.
