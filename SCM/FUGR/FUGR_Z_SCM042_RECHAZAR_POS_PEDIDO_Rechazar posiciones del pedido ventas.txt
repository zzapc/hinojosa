FUNCTION z_scm042_rechazar_pos_pedido.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     VALUE(IT_PED_POS) TYPE  TDT_VBELN_POSNR
*"     VALUE(I_ABGRU) TYPE  ABGRU_VA DEFAULT 'Z5'
*"  EXPORTING
*"     VALUE(ET_RETURN) TYPE  BAPIRET2_TT
*"----------------------------------------------------------------------
* JGM 02.2024 GAPCM042 Aplicativo Gestión de Desvíos:
* Asignar rechazo a posición de pedido de ventas.
*--------------------------------------------------------------------*
  DATA: ls_order_header_in  TYPE bapisdh1,
        ls_order_header_inx TYPE bapisdh1x,
        ls_order_items_in   TYPE bapisditm,
        ls_order_items_inx  TYPE bapisditmx.

  DATA: lt_order_items_in  TYPE TABLE OF bapisditm,
        lt_order_items_inx TYPE TABLE OF  bapisditmx.

  DATA: lt_ped_pos TYPE tdt_vbeln_posnr,
        lt_return  TYPE bapiret2_tt.

  DATA: lv_salesdocument TYPE bapivbeln-vbeln.
  DATA: lr_type_error TYPE RANGE OF bapi_mtype.


  lr_type_error = VALUE #( sign = 'I' option = 'EQ' ( low = 'E' ) ( low = 'A' ) ).

  lt_ped_pos = it_ped_pos.

  SORT lt_ped_pos BY vbeln posnr.

  LOOP AT lt_ped_pos ASSIGNING FIELD-SYMBOL(<fs_ped_pos>).

    AT NEW vbeln.
      lv_salesdocument = <fs_ped_pos>-vbeln.

      "Cabecera:
      CLEAR ls_order_header_in.
      ls_order_header_inx-updateflag = 'U'.
    ENDAT.

    "Posiciones:
    ls_order_items_in-itm_number = <fs_ped_pos>-posnr.
    ls_order_items_in-reason_rej = i_abgru.

    APPEND ls_order_items_in TO lt_order_items_in.
    CLEAR ls_order_items_in.

    ls_order_items_inx-updateflag = 'U'.
    ls_order_items_inx-itm_number = <fs_ped_pos>-posnr.
    ls_order_items_inx-reason_rej = 'X'.

    APPEND ls_order_items_inx TO lt_order_items_inx.
    CLEAR ls_order_items_inx.

    AT END OF vbeln.
      CALL FUNCTION 'BAPI_SALESORDER_CHANGE'
        EXPORTING
          salesdocument    = lv_salesdocument
          order_header_inx = ls_order_header_inx
        TABLES
          return           = lt_return
          order_item_in    = lt_order_items_in
          order_item_inx   = lt_order_items_inx.

      LOOP AT lt_return TRANSPORTING NO FIELDS WHERE type IN lr_type_error.
        EXIT.
      ENDLOOP.
      IF sy-subrc EQ 0.
        CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
      ELSE.
        CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
          EXPORTING
            wait = 'X'.

        "Rechazadas posiciones pedido cliente".
        PERFORM fill_return USING 'S' 'ZSCM042' '042' lv_salesdocument space space space CHANGING et_return.
      ENDIF.

      "Mensajes de error de la bapi.
      DELETE lt_return WHERE type NOT IN lr_type_error.
      APPEND LINES OF lt_return TO et_return.

      FREE: lt_return,
            lt_order_items_in,
            lt_order_items_inx.
    ENDAT.
  ENDLOOP.

ENDFUNCTION.
