FUNCTION z_scm042_eliminar_pos_entrega.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     VALUE(IT_ENT_POS) TYPE  TDT_VBELN_POSNR
*"  EXPORTING
*"     VALUE(ET_RETURN) TYPE  BAPIRET2_TT
*"----------------------------------------------------------------------
* JGM 02.2024 GAPCM042 Aplicativo Gestión de Desvíos:
* Borrar posiciones de entrega de Salida
*--------------------------------------------------------------------*
  DATA: lt_ent_pos TYPE tdt_vbeln_posnr,
        lt_return  TYPE bapiret2_tt.
  DATA: lt_item_data    TYPE TABLE OF bapiobdlvitemchg,
        lt_item_control TYPE TABLE OF bapiobdlvitemctrlchg.

  DATA: ls_header_data    TYPE bapiobdlvhdrchg,
        ls_header_control TYPE bapiobdlvhdrctrlchg.
  DATA: ls_item_data    TYPE bapiobdlvitemchg,
        ls_item_control TYPE bapiobdlvitemctrlchg.

  DATA: lr_type_error TYPE RANGE OF bapi_mtype.


  lr_type_error = VALUE #( sign = 'I' option = 'EQ' ( low = 'E' ) ( low = 'A' ) ).

  lt_ent_pos = it_ent_pos.
  SORT lt_ent_pos BY vbeln posnr.

  LOOP AT lt_ent_pos ASSIGNING FIELD-SYMBOL(<fs_ent_pos>).

    AT NEW vbeln.
      "Cabecera:
      CLEAR: ls_header_data,
             ls_header_control.
      ls_header_data-deliv_numb     = <fs_ent_pos>-vbeln.
      ls_header_control-deliv_numb  = <fs_ent_pos>-vbeln.
    ENDAT.

    "Posiciones:
    ls_item_data-deliv_numb = <fs_ent_pos>-vbeln.
    ls_item_data-deliv_item = <fs_ent_pos>-posnr.
    APPEND ls_item_data TO lt_item_data.
    CLEAR ls_item_data.

    ls_item_control-deliv_numb = <fs_ent_pos>-vbeln.
    ls_item_control-deliv_item = <fs_ent_pos>-posnr.
    ls_item_control-del_item   = 'X'.   "Eliminar posición.
    APPEND ls_item_control TO lt_item_control.
    CLEAR ls_item_control.

    AT END OF vbeln.
      CALL FUNCTION 'BAPI_OUTB_DELIVERY_CHANGE'
        EXPORTING
          header_data    = ls_header_data
          header_control = ls_header_control
          delivery       = ls_header_data-deliv_numb
        TABLES
          item_data      = lt_item_data
          item_control   = lt_item_control
          return         = lt_return.

      LOOP AT lt_return TRANSPORTING NO FIELDS WHERE type IN lr_type_error.
        EXIT.
      ENDLOOP.
      IF sy-subrc EQ 0.
        CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
      ELSE.
        CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
          EXPORTING
            wait = 'X'.
      ENDIF.

      "Solo mensajes de error de la bapi.
      DELETE lt_return WHERE type NOT IN lr_type_error.
      APPEND LINES OF lt_return TO et_return.

      FREE: lt_return,
            lt_item_data,
            lt_item_control.
    ENDAT.
  ENDLOOP.

ENDFUNCTION.
