FUNCTION z_scm042_asignar_ent_transp.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     VALUE(I_VBELN) TYPE  VBELN_VL
*"     VALUE(I_TKNUM) TYPE  TKNUM
*"  EXPORTING
*"     VALUE(ET_RETURN) TYPE  BAPIRET2_TT
*"----------------------------------------------------------------------
* JGM 02.2024 GAPCM042 Aplicativo Gestión de Desvíos:
* Asignar entrega a transporte.
*--------------------------------------------------------------------*
  DATA: lt_return         TYPE bapiret2_tt,
        lt_itemdata       TYPE TABLE OF bapishipmentitem,
        lt_itemdataaction TYPE TABLE OF bapishipmentitemaction.

  DATA: ls_headerdata       TYPE bapishipmentheader,
        ls_headerdataaction TYPE bapishipmentheaderaction,
        ls_itemdata         TYPE bapishipmentitem,
        ls_itemdataaction   TYPE bapishipmentitemaction.

  DATA: lr_type_error TYPE RANGE OF bapi_mtype.


  SELECT a~tknum, a~tpnum, a~vbeln
    INTO TABLE @DATA(lt_transp_ent)
    FROM vttp AS a
    INNER JOIN likp AS b
    ON a~vbeln EQ b~vbeln
    WHERE a~tknum EQ @i_tknum
      AND b~vbeln EQ @i_vbeln.

  IF sy-subrc NE 0.
    lr_type_error = VALUE #( sign = 'I' option = 'EQ' ( low = 'E' ) ( low = 'A' ) ( low = 'X' ) ).

    CLEAR: ls_headerdata,
           ls_headerdataaction.

    ls_headerdata-shipment_num = i_tknum.

    ls_itemdata-delivery  = i_vbeln.
    ls_itemdata-itenerary = '0001'.
    APPEND ls_itemdata TO lt_itemdata.
    CLEAR ls_itemdata.

    ls_itemdataaction-delivery  = 'A'.
    ls_itemdataaction-itenerary = 'A'.
    APPEND ls_itemdataaction TO lt_itemdataaction.
    CLEAR ls_itemdataaction.

    CALL FUNCTION 'BAPI_SHIPMENT_CHANGE'
      EXPORTING
        headerdata       = ls_headerdata
        headerdataaction = ls_headerdataaction
      TABLES
        itemdata         = lt_itemdata
        itemdataaction   = lt_itemdataaction
        return           = lt_return.

    LOOP AT lt_return TRANSPORTING NO FIELDS WHERE type IN lr_type_error.
      EXIT.
    ENDLOOP.
    IF sy-subrc EQ 0.
      CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
    ELSE.
      CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
        EXPORTING
          wait = 'X'.

      "Mensaje asignación entrega (saldrán mensajes de todas las entregas asignadas + la nueva).
      et_return = VALUE #( FOR wa_ret IN lt_return WHERE ( type EQ 'S' AND id EQ 'VW' AND number EQ '482' AND
                                                           message_v1 EQ i_vbeln )  "Solo entrega nueva.
                         ( CORRESPONDING #( wa_ret ) ) ).
    ENDIF.

    "Y mensajes de error de la bapi.
    DELETE lt_return WHERE type NOT IN lr_type_error.

    APPEND LINES OF lt_return TO et_return.
  ELSE.
    "Entrega ya está asignada a transporte".
    PERFORM fill_return USING 'E' 'ZSCM042' '011' i_vbeln i_tknum space space CHANGING et_return.
  ENDIF.  "select check.

ENDFUNCTION.
