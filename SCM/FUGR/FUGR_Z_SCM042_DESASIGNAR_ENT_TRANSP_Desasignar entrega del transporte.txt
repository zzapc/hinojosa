FUNCTION z_scm042_desasignar_ent_transp.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     VALUE(I_VBELN) TYPE  VBELN_VL
*"     VALUE(I_TKNUM) TYPE  TKNUM
*"  EXPORTING
*"     VALUE(ET_RETURN) TYPE  BAPIRET2_TT
*"----------------------------------------------------------------------
* JGM 02.2024 GAPCM042 Aplicativo Gestión de Desvíos:
* Desasignar entrega del transporte.
*--------------------------------------------------------------------*
  DATA: lt_return         TYPE bapiret2_tt,
        lt_itemdata       TYPE TABLE OF bapishipmentitem,
        lt_itemdataaction TYPE TABLE OF bapishipmentitemaction.

  DATA: ls_headerdata       TYPE bapishipmentheader,
        ls_headerdataaction TYPE bapishipmentheaderaction,
        ls_itemdata         TYPE bapishipmentitem,
        ls_itemdataaction   TYPE bapishipmentitemaction.

  DATA: lr_type_error TYPE RANGE OF bapi_mtype.


  SELECT a~tknum, a~tpnum, a~vbeln
    INTO TABLE @DATA(lt_transp_ent)
    FROM vttp AS a
    INNER JOIN likp AS b
    ON a~vbeln EQ b~vbeln
    WHERE a~tknum EQ @i_tknum
      AND b~vbeln EQ @i_vbeln.

  IF sy-subrc EQ 0.
    lr_type_error = VALUE #( sign = 'I' option = 'EQ' ( low = 'E' ) ( low = 'A' ) ( low = 'X' ) ).

    CLEAR: ls_headerdata,
           ls_headerdataaction.

    ls_headerdata-shipment_num = i_tknum.

    ls_headerdataaction-status_plan         = 'D'.
    ls_headerdataaction-status_checkin      = 'D'.
    ls_headerdataaction-status_load_start   = 'D'.
    ls_headerdataaction-status_load_end     = 'D'.
    ls_headerdataaction-status_compl        = 'D'.
    ls_headerdataaction-status_shpmnt_start = 'D'.
    ls_headerdataaction-status_shpmnt_end   = 'D'.

    ls_itemdata-delivery  = i_vbeln.
    ls_itemdata-itenerary = '0001'.
    APPEND ls_itemdata TO lt_itemdata.
    CLEAR ls_itemdata.

    ls_itemdataaction-delivery  = 'D'.
    ls_itemdataaction-itenerary = 'D'.
    APPEND ls_itemdataaction TO lt_itemdataaction.
    CLEAR ls_itemdataaction.

    CALL FUNCTION 'BAPI_SHIPMENT_CHANGE'
      EXPORTING
        headerdata       = ls_headerdata
        headerdataaction = ls_headerdataaction
      TABLES
        itemdata         = lt_itemdata
        itemdataaction   = lt_itemdataaction
        return           = lt_return.

    LOOP AT lt_return TRANSPORTING NO FIELDS WHERE type IN lr_type_error.
      EXIT.
    ENDLOOP.
    IF sy-subrc EQ 0.
      CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
    ELSE.
      CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
        EXPORTING
          wait = 'X'.
    ENDIF.

    "Solo mensajes de error de la bapi.
    DELETE lt_return WHERE type NOT IN lr_type_error.

    APPEND LINES OF lt_return TO et_return.
  ELSE.
    "No existe entrega asignada a transporte".
    PERFORM fill_return USING 'E' 'ZSCM042' '010' i_vbeln i_tknum space space CHANGING et_return.
  ENDIF.  "select check.

ENDFUNCTION.
