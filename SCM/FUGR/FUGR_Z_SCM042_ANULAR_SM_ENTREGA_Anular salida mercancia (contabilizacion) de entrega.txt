FUNCTION z_scm042_anular_sm_entrega.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     VALUE(I_VBELN) TYPE  VBELN_VL
*"  EXPORTING
*"     VALUE(ET_RETURN) TYPE  BAPIRET2_TT
*"----------------------------------------------------------------------
* JGM 02.2024 GAPCM042 Aplicativo Gestión de Desvíos:
* Anular contabilización de entrega.
*--------------------------------------------------------------------*
  CONSTANTS:
    lc_vl09 TYPE sy-tcode VALUE 'VL09'.

  DATA: ls_return TYPE bapiret2.
  DATA: ls_emkpf TYPE emkpf.
  DATA: lt_mesg TYPE TABLE OF mesg.

  DATA: lr_type_error TYPE RANGE OF bapi_mtype.

  lr_type_error = VALUE #( sign = 'I' option = 'EQ' ( low = 'E' ) ( low = 'A' ) ( low = 'X' ) ).


  "Inicio anular SM entrega".
  PERFORM fill_return USING 'S' 'ZSCM042' '031' i_vbeln space space space CHANGING et_return.

  "Tipo doc. a anular (entrega) y comprobar estatus contab.SM.
  SELECT SINGLE a~vbtyp INTO @DATA(lv_vbtyp)
    FROM likp AS a
    INNER JOIN vbuk AS b
      ON a~vbeln EQ b~vbeln
    WHERE a~vbeln EQ @i_vbeln
      AND b~wbstk EQ 'C'.

  IF sy-subrc EQ 0.

    "Bloqueo entrega.
    CLEAR ls_return.
    PERFORM lock_obj_vl USING i_vbeln CHANGING ls_return.

    IF ls_return-type IN lr_type_error.
      APPEND ls_return TO et_return.
      RETURN.
    ENDIF.

    CALL FUNCTION 'WS_REVERSE_GOODS_ISSUE'
      EXPORTING
        i_vbeln                   = i_vbeln
        i_budat                   = sy-datum
        i_tcode                   = lc_vl09
        i_vbtyp                   = lv_vbtyp
      IMPORTING
        es_emkpf                  = ls_emkpf
      TABLES
        t_mesg                    = lt_mesg
      EXCEPTIONS
        error_reverse_goods_issue = 1
        OTHERS                    = 2.

    IF sy-subrc EQ 0.
      COMMIT WORK AND WAIT.
      "Anulada SM entrega".
      PERFORM fill_return USING 'S' 'ZSCM042' '009' i_vbeln space space space CHANGING et_return.
    ELSE.
      ROLLBACK WORK.
      PERFORM fill_return USING sy-msgty sy-msgid sy-msgno sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4
                          CHANGING et_return.
    ENDIF.

*    "Refrescar buffers y variables globales en funciones SAP de entregas y pedidos.
*    CALL FUNCTION 'RV_DELIVERY_INIT'.

    "El commit ya desbloquea el objeto, chequear de todos modos.
    PERFORM unlock_obj_vl USING i_vbeln.
  ELSE.
    "mensaje error estatus contab.sm
  ENDIF.

ENDFUNCTION.
