FUNCTION z_scm042_borrar_pos_po.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     VALUE(IT_PO_POS) TYPE  SEL_EBELN_EBELP_TTY
*"  EXPORTING
*"     VALUE(ET_RETURN) TYPE  BAPIRET2_TT
*"----------------------------------------------------------------------
* JGM 02.2024 GAPCM042 Aplicativo Gestión de Desvíos:
* Borrar (marcar borrado) de posiciones de pedido de traslado (PO).
*--------------------------------------------------------------------*
  DATA: lt_po_pos TYPE sel_ebeln_ebelp_tty,
        lt_return TYPE bapiret2_tt.
  DATA: lt_poitem  TYPE TABLE OF bapimepoitem,
        lt_poitemx TYPE TABLE OF bapimepoitemx.

  DATA: ls_poheader  TYPE bapimepoheader,
        ls_poheaderx TYPE bapimepoheaderx,
        ls_poitem    TYPE bapimepoitem,
        ls_poitemx   TYPE bapimepoitemx.

  DATA: lv_purchaseorder TYPE bapimepoheader-po_number.

  DATA: lr_type_error TYPE RANGE OF bapi_mtype.


  lr_type_error = VALUE #( sign = 'I' option = 'EQ' ( low = 'E' ) ( low = 'A' ) ).

  lt_po_pos = it_po_pos.
  SORT lt_po_pos BY ebeln ebelp.

  LOOP AT lt_po_pos ASSIGNING FIELD-SYMBOL(<fs_po_pos>).

    AT NEW ebeln.
      lv_purchaseorder = <fs_po_pos>-ebeln.

      "Cabecera:
      CLEAR: ls_poheader,
             ls_poheaderx.
    ENDAT.

    "Posiciones:
    ls_poitem-po_item    = <fs_po_pos>-ebelp.
    ls_poitem-delete_ind = 'X'.
    APPEND ls_poitem TO lt_poitem.
    CLEAR ls_poitem.

    ls_poitemx-po_item    = <fs_po_pos>-ebelp.
    ls_poitemx-delete_ind = 'X'.
    APPEND ls_poitemx TO lt_poitemx.
    CLEAR ls_poitemx.

    AT END OF ebeln.
      CALL FUNCTION 'BAPI_PO_CHANGE'
        EXPORTING
          purchaseorder = lv_purchaseorder
*         POHEADER      =
*         POHEADERX     =
        TABLES
          return        = lt_return
          poitem        = lt_poitem
          poitemx       = lt_poitemx.

      LOOP AT lt_return TRANSPORTING NO FIELDS WHERE type IN lr_type_error.
        EXIT.
      ENDLOOP.
      IF sy-subrc EQ 0.
        CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
      ELSE.
        CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
          EXPORTING
            wait = 'X'.

        "Borrado posiciones pedido traslado".
        PERFORM fill_return USING 'S' 'ZSCM042' '043' lv_purchaseorder space space space CHANGING et_return.
      ENDIF.

      "Mensajes de error de la bapi.
      DELETE lt_return WHERE type NOT IN lr_type_error.
      APPEND LINES OF lt_return TO et_return.

      FREE: lt_return,
            lt_poitem,
            lt_poitemx.
    ENDAT.
  ENDLOOP.

ENDFUNCTION.
