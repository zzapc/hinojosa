FUNCTION z_scm042_caso_desvio_transp.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     REFERENCE(I_TKNUM) TYPE  TKNUM
*"     REFERENCE(I_VBELN) TYPE  VBELN_VL
*"     REFERENCE(IT_SEL_DESVIOS) TYPE  ZSCM042_TT_DESVIOS
*"  EXPORTING
*"     REFERENCE(E_CASO) TYPE  ZSCM_DE_CASODESVIO
*"----------------------------------------------------------------------
* JGM 15.02.2024 GAPCM042 Aplicativo Gestión de Desvíos:
* Caso1: Camión Completo (todas sus entregas completas).
* Caso2: Algunas entregas completas.
* Caso3: Algunas posiciones de entrega.
* Caso4: Cant.parcial de posiciones de entrega.
*--------------------------------------------------------------------*
  DATA: lv_flag_parcial    TYPE flag,
        lv_flag_incompleto.
  DATA: lt_sel_desvios TYPE zscm042_tt_desvios.
  DATA: lr_mtart_pallet TYPE RANGE OF mtart.


  lr_mtart_pallet = VALUE #( ( sign = 'I' option = 'EQ' low = 'ZPAL' ) ).

  CLEAR e_caso.
  lt_sel_desvios = it_sel_desvios.

  SELECT b~tknum, b~vbeln,
         c~posnr, c~pstyv, c~lfimg, c~meins, c~vrkme, c~mtart
    INTO TABLE @DATA(lt_transp_bd)
    FROM vttk AS a
    INNER JOIN vttp AS b
    ON a~tknum EQ b~tknum
    INNER JOIN lips AS c
    ON b~vbeln EQ c~vbeln
    WHERE a~tknum EQ @i_tknum
      AND c~mtart NOT IN @lr_mtart_pallet.

  IF lt_sel_desvios IS NOT INITIAL AND lt_transp_bd IS NOT INITIAL.
    SORT lt_sel_desvios BY tknum vbeln posnr.
    SORT lt_transp_bd BY tknum vbeln posnr.

    "Chequeo Caso4: Para la entrega actual, basta con que haya una pos.seleccionada con cantidad de desvío modificada.
    CLEAR lv_flag_parcial.
    LOOP AT lt_sel_desvios INTO DATA(ls_sel_desvios) WHERE tknum EQ i_tknum
                                                       AND vbeln EQ i_vbeln
                                                       AND sel   EQ 'X'.
      IF ls_sel_desvios-cant_desvio NE ls_sel_desvios-lfimg.
        lv_flag_parcial = 'X'.
        EXIT.
      ENDIF.
    ENDLOOP.
    IF lv_flag_parcial EQ 'X'.
      e_caso = '4'.
      RETURN.
    ENDIF.

    "Chequeo Caso3: Se desvían algunas posiciones de la entrega actual, pero no todas.
    LOOP AT lt_sel_desvios INTO ls_sel_desvios WHERE tknum EQ i_tknum
                                                 AND vbeln EQ i_vbeln
                                                 AND sel   EQ space.
      EXIT.
    ENDLOOP.
    IF sy-subrc EQ 0.
      e_caso = '3'.
      RETURN.
    ELSE.
      "Chequeo Caso2: Entrega completa (implica que no hay mezclados casos anteriores 3 ni 4 de cant. reducida).
      "No salir, por chequear además si es Caso1.
      e_caso = '2'.
    ENDIF.

    "Chequeo Caso1: Camion completo (todas las entregas completas y sin cantidades parciales modificadas).
    "A efetos prácticos, los casos 1 y 2 implican el mismo tratamiento.
    CLEAR: lv_flag_incompleto.

    LOOP AT lt_transp_bd INTO DATA(ls_transp_bd) WHERE tknum EQ i_tknum.
      "-Que estén SELECCIONADAS las mismas entregas y posiciones de todo el transporte en la tabla de registros seleccionados por el usuario.
      "-Que esos registros además no tengan diferencias en la cantidad de desvío (Caso4) en cualquier otra entrega del
      "transporte DISTINTA a la que se chequea actualmente.
      "Cuando hay diferencias en la entrega actual, directamente cae en el chequeo del caso 4.
      IF NOT line_exists( lt_sel_desvios[ sel = 'X'
                                          tknum = ls_transp_bd-tknum
                                          vbeln = ls_transp_bd-vbeln
                                          posnr = ls_transp_bd-posnr
                                          cant_desvio = ls_transp_bd-lfimg ] ).
        lv_flag_incompleto = 'X'.
        EXIT.
      ENDIF.

    ENDLOOP.
    IF lv_flag_incompleto IS INITIAL.
      e_caso = '1.'.
    ENDIF.

  ENDIF.

ENDFUNCTION.
