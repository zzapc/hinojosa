FUNCTION z_aic_upd_material.
*"----------------------------------------------------------------------
*"*"Módulo funciones actualiz.
*"
*"*"Interfase local
*"  IMPORTING
*"     VALUE(MARA) TYPE  MARA
*"     VALUE(MARC) TYPE  MARC
*"----------------------------------------------------------------------

* Necesitemos asegurarnos de que se actualizan los valores del configurador en BBDD, para ello, ponemos una espera
* (este proceso va en fondo, nadie se va a enterar)
  IF zcl_ap_utils=>es_in_update_task( ).
    WAIT UP TO 10 SECONDS.
  ENDIF.

  SELECT yhp_idx_td310~price, eine~ekorg, eine~netpr, eine~waers, eine~peinh
    FROM marc JOIN yhp_idx_td310 ON yhp_idx_td310~cuobj = marc~cuobj
       LEFT OUTER JOIN eina ON  eina~matnr  = marc~matnr
                            AND eina~lifnr  = yhp_idx_td310~lifnr
                            AND eina~loekz  = ''
       LEFT OUTER JOIN eine ON  eine~infnr  = eina~infnr
                            AND eine~werks  = marc~werks
                            AND eine~esokz  = '3'
                            AND eine~loekz  = ''
    INTO @DATA(l_reg)
    UP TO 1 ROWS
    WHERE marc~matnr = @marc-matnr
      AND marc~werks = @marc-werks
      AND yhp_idx_td310~outso = 'X'
   ORDER BY cself DESCENDING, coper DESCENDING.
  ENDSELECT.
  IF sy-subrc = 0.
    IF l_reg-price = l_reg-netpr.
      zcl_ap_exits=>log_st( exit = 'MATERIAL_REGISTRO_INFO_SUBCO' clave = marc-matnr && '$' && marc-werks p1 = 'Coinciden precio AIC con el del registro info' p2 = l_reg-price msgty = 'S' ).
    ELSE.
      IF l_reg-ekorg IS INITIAL.
        zcl_ap_exits=>log_st( exit = 'MATERIAL_REGISTRO_INFO_SUBCO' clave = marc-matnr && '$' && marc-werks p1 = 'Se creará registro info' p2 = l_reg-netpr msgty = 'S' ).
      ELSE.
        zcl_ap_exits=>log_st( exit = 'MATERIAL_REGISTRO_INFO_SUBCO' clave = marc-matnr && '$' && marc-werks p1 = 'Difiere precio AIC' p2 = l_reg-price p3 = 'con el del registro info' p4 = l_reg-netpr msgty = 'S' ).
      ENDIF.

      CALL FUNCTION 'Z_LANZAR_JOB_AP'
        EXPORTING
          jobname              = 'ZAIC_REG_INFO'
          report               = 'ZRMM0032'
          retraso              = 10
          grabar_log           = ''
*         ORIGEN               = ''
          mail_si_error        = 'andres@sap4.com'
          parametros           = VALUE rsparams_tt( ( selname = 'S_MATNR' kind    = 'S' sign    = 'I' option  = 'EQ' low     = marc-matnr )
                                          ( selname = 'S_WERKS' kind    = 'S' sign    = 'I' option  = 'EQ' low     = marc-werks )
                                          ( selname = 'P_EJEC'  kind    = 'P' low     = 'X' )
                                        )
          espera_si_job_activo = 10
          clave                = |{ marc-matnr }${ marc-werks }|
          exit                 = 'MATERIAL_REGISTRO_INFO_SUBCO'
          new_task             = 'F'.
    ENDIF.
  ELSE.
    zcl_ap_log=>set_log( proceso = 'UPD_MAT' clave = marc-matnr && '$' && marc-werks p1 = 'No hay registro de subcontratación' msgty = 'S' ).
  ENDIF.



ENDFUNCTION.
