*$*$----------------------------------------------------------------$*$*
*$ Correction Inst.         0120061532 0000768074                     $*
*$--------------------------------------------------------------------$*
*$ Valid for       :                                                  $*
*$ Software Component   SAP_APPL   SAP Application                    $*
*$  Release 470          All Support Package Levels                   $*
*$  Release 600          Fm SAPKH60001                                $*
*$  Release 602          All Support Package Levels                   $*
*$  Release 603          All Support Package Levels                   $*
*$  Release 604          Fm SAPKH60401                                $*
*$  Release 605          All Support Package Levels                   $*
*$  Release 606          Fm SAPKH60601                                $*
*$  Release 616          All Support Package Levels                   $*
*$  Release 617          Fm SAPKH61701                                $*
*$  Release 618          All Support Package Levels                   $*
*$--------------------------------------------------------------------$*
*$ Changes/Objects Not Contained in Standard SAP System               $*
*$*$----------------------------------------------------------------$*$*
*&--------------------------------------------------------------------*
*& Object          REPS ZPICKFLO
*& Object Header   PROG ZPICKFLO
*&--------------------------------------------------------------------*
*>>>> START OF INSERTION <<<<
*&---------------------------------------------------------------------*
*& Report  ZPICKFLO                                                    *
*&                                                                     *
*&---------------------------------------------------------------------*

report zpickflo.

tables: vbuk, vbup, vbfa, ltap.

data: xvbuk like vbukvb occurs 0 with header line,
      xvbup like vbupvb occurs 0 with header line,
      xvbfa like vbfavb occurs 0 with header line,
      xltap like ltap   occurs 0 with header line,
      lf_later type c,
      begin of gt_ltap_select occurs 0,
        lgnum like ltap-lgnum,
        tanum like ltap-tanum,
      end of gt_ltap_select.

select-options: s_vbeln for vbuk-vbeln.
parameters: p_test as checkbox default 'X'.

start-of-selection.

  select * from vbuk
           into corresponding fields of table xvbuk
           where vbeln in s_vbeln
             and lvstk eq 'B'.
  if sy-subrc ne 0.
    message s001(vl) with 'No deliveries found'.
    exit.
  endif.
  sort xvbuk by mandt vbeln.

  select * from vbup
           into corresponding fields of table xvbup
           for all entries in xvbuk
           where vbeln eq xvbuk-vbeln.
  sort xvbup by mandt vbeln posnr.

  select * from vbfa
           into corresponding fields of table xvbfa
           for all entries in xvbup
           where vbelv eq xvbup-vbeln
             and posnv eq xvbup-posnr
             and vbtyp_n eq 'Q'.
  if sy-subrc ne 0.
    message s001(vl) with 'Document flow is inconsistent'.
    exit.
  endif.
  sort xvbfa by mandt vbelv posnv vbeln posnn vbtyp_n.
  loop at xvbfa.
    gt_ltap_select-tanum = xvbfa-vbeln.
    gt_ltap_select-lgnum = xvbfa-lgnum.
    collect gt_ltap_select.
  endloop.
  select * from ltap
           into table xltap
           for all entries in gt_ltap_select
           where lgnum = gt_ltap_select-lgnum
             and tanum = gt_ltap_select-tanum.
  sort xltap by mandt lgnum tanum tapos.

  loop at xvbup where lvsta eq 'B'.
    loop at xvbfa where vbelv eq xvbup-vbeln
                    and posnv eq xvbup-posnr.

      read table xltap with key lgnum = xvbfa-lgnum
                                tanum = xvbfa-vbeln
                                tapos = xvbfa-posnn
                         binary search.
      if sy-subrc ne 0.
        write:/   xvbup-vbeln, xvbup-posnr,
               40 'WM transfer order is missing'.
        continue.
      endif.

      select single later from ltak
      into lf_later
      where lgnum = xvbfa-lgnum and
            tanum = xvbfa-vbeln.

      if xltap-pquit eq 'X' and xvbfa-taqui eq space.

        if lf_later eq space or ( xltap-passd eq 'X' and lf_later ne
space ).
          xvbfa-rfmng     = xltap-nistm.
          xvbfa-rfmng_flt = xltap-nistm.
          xvbfa-taqui = 'X'.
          xvbfa-updkz = 'U'.
          modify xvbfa.
        else.
          write:/    xvbfa-vbelv, xvbfa-posnv, xvbfa-vbeln, xvbfa-posnn,
                  40 'Delayed Update of Outbound Delivery is enabled'.
        endif.
      endif.
    endloop.
    if sy-subrc ne 0.
      write:/ xvbup-vbeln, xvbup-posnr,'Document flow is missing'.
      continue.
    endif.
  endloop.

  delete xvbfa where updkz eq space.

  if not xvbfa[] is initial.
    write /.
    write: / 'Document flow entries to be corrected:'.
    write: /5 'VBELV',
           20 'POSNV',
           30 'VBELN',
           45 'POSNN'.

    loop at xvbfa.
      write: /5 xvbfa-vbelv,
             20 xvbfa-posnv,
             30 xvbfa-vbeln,
             45 xvbfa-posnn.
    endloop.
  else.
    write: / 'No incorrect deliveries found.'.
    exit.
  endif.
  uline.
  if p_test eq space.
    update vbfa from table xvbfa.
    commit work.
    write: / 'Database corrected.'.
  else.
    write: / 'Test run no database changes.'.
  endif.
*>>>> END OF INSERTION <<<<<<
...
*&--------------------------------------------------------------------*
