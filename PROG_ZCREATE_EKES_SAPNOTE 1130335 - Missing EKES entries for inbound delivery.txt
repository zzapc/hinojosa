*$*$----------------------------------------------------------------$*$*
*$ Correction Inst.         0120031469 0000591614                     $*
*$--------------------------------------------------------------------$*
*$ Valid for       :                                                  $*
*$ Software Component   SAP_APPL   SAP Application                    $*
*$  Release 46C          All Support Package Levels                   $*
*$  Release 470          All Support Package Levels                   $*
*$  Release 500          All Support Package Levels                   $*
*$--------------------------------------------------------------------$*
*$ Changes/Objects Not Contained in Standard SAP System               $*
*$*$----------------------------------------------------------------$*$*
*&--------------------------------------------------------------------*
*& Object          REPS ZCREATE_EKES
*& Object Header   PROG ZCREATE_EKES
*&--------------------------------------------------------------------*
*& REPORT ZCREATE_EKES
*&--------------------------------------------------------------------*
*>>>> START OF INSERTION <<<<
REPORT  ZCREATE_EKES.                                    "note 1130335
* recreation of missing EKES entries (with update of EKET-DABMG) with
* respect to an existing inbound delivery
* Attention: the conversion factors shall not have been changed in the
* inbound delivery!

tables: LIKP, LIPS, EKPO, EKET, EKES.
data: l_etens like ekes-etens,
      l_dabmg like ekes-dabmg,
      l_diff  like ekes-dabmg.

parameters: ibd_del like likp-vbeln,
            p_test  as checkbox     default 'X'.


select single * from likp where vbeln = ibd_del.
check sy-subrc = 0.
check likp-vbtyp = '7'.
select * from lips where vbeln = ibd_del.
  check not lips-vgbel is initial and
        not lips-vgpos is initial.
  select * from ekes where ebeln = lips-vgbel
                       and ebelp = lips-vgpos
                       and vbeln = lips-vbeln
                       and vbelp = lips-posnr.
  endselect.
  check sy-subrc <> 0.
* EKES does not exist
  select single * from ekpo where ebeln = lips-vgbel
                              and ebelp = lips-vgpos.
  check sy-subrc = 0.
* conversion factors must be the same
  check ekpo-umrez = lips-umvkz and ekpo-umren = lips-umvkn.

* determine current EKES-ETENS
  select * from ekes where ebeln = lips-vgbel
                       and ebelp = lips-vgpos.
  endselect.
  if sy-subrc = 0.
    l_etens = ekes-etens + 1.
  else.
    l_etens = '0001'.
  endif.
  clear ekes.

* create new EKES entry
  ekes-mandt = lips-mandt.
  ekes-ebeln = lips-vgbel.
  ekes-ebelp = lips-vgpos.
  ekes-etens = l_etens.
  ekes-ebtyp = 'LA'.
  ekes-eindt = likp-lfdat.
  ekes-lpein = '1'.
  ekes-uzeit = likp-lfuhr.
  ekes-erdat = lips-erdat.
  ekes-ezeit = lips-erzet.
  ekes-menge = lips-lfimg.
  ekes-estkz = '2'.
  ekes-kzdis = 'X'.
  ekes-vbeln = lips-vbeln.
  ekes-vbelp = lips-posnr.
  ekes-xblnr = likp-lifex.
  ekes-ematn = lips-ematn.
  ekes-charg = lips-charg.
  ekes-uecha = lips-uecha.

  if p_test is initial.
    insert ekes.
    write: / 'EKES entry for delivery item', lips-vbeln, lips-posnr,
           'inserted'.
  else.
    write: / 'EKES entry for delivery item', lips-vbeln, lips-posnr,
           'has to be inserted'.
  endif.
  check p_test is initial.

* update dabmg in EKET-DABMG's
  l_dabmg = ekes-menge.
  select * from eket where ebeln = lips-vgbel
                       and ebelp = lips-vgpos.
    if eket-dabmg < eket-menge.
      l_diff = eket-menge - eket-dabmg.
      if l_diff >= l_dabmg.
        eket-dabmg = eket-dabmg + l_dabmg.
        l_dabmg = 0.
        update eket.
        exit.
      else.
        eket-dabmg = eket-menge.
        update eket.
        l_dabmg = l_dabmg - l_diff.
      endif.
    endif.
  endselect.
* the rest comes into the last EKET entry
  if l_dabmg > 0.
    select * from eket where ebeln = lips-vgbel
                         and ebelp = lips-vgpos.
    endselect.
    if sy-subrc = 0.
      eket-dabmg = eket-dabmg + l_dabmg.
      update eket.
    endif.
  endif.

endselect.

*>>>> END OF INSERTION <<<<<<
...
*&--------------------------------------------------------------------*
