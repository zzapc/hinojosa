FUNCTION Z_ENVIO_CORREO.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     REFERENCE(VP_ASUNTO) TYPE  SO_OBJ_DES
*"     REFERENCE(VP_WITH_ERROR_SCREEN) TYPE  OS_BOOLEAN DEFAULT 'X'
*"     REFERENCE(VP_COMMIT) TYPE  OS_BOOLEAN DEFAULT 'X'
*"     REFERENCE(VP_TIPO) TYPE  SO_OBJ_TP DEFAULT 'RAW'
*"     REFERENCE(VP_EMAIL_SENDER) TYPE  AD_SMTPADR OPTIONAL
*"     REFERENCE(VP_IMPORTANCE) TYPE  BCS_DOCIMP OPTIONAL
*"  EXPORTING
*"     REFERENCE(VP_RESULT) TYPE  BOOLEAN
*"  CHANGING
*"     REFERENCE(TP_MESSAGE_BODY) TYPE  SOLI_TAB
*"     REFERENCE(TP_DESTINATARIOS) TYPE  ZEMAIL_T_MAILADDRESS
*"     REFERENCE(TP_DESTINATARIOS_CC) TYPE  ZEMAIL_T_MAILADDRESS
*"       OPTIONAL
*"     REFERENCE(TP_DESTINATARIOS_CCO) TYPE  ZEMAIL_T_MAILADDRESS
*"       OPTIONAL
*"     REFERENCE(TP_ANEXOS) TYPE  ZEMAIL_T_MAILATTACH OPTIONAL
*"----------------------------------------------------------------------


  DATA wl_destinatarios LIKE LINE OF tp_destinatarios.
  DATA wl_anexos        LIKE LINE OF tp_anexos.
  DATA lo_send_request  TYPE REF TO cl_bcs VALUE IS INITIAL.
  DATA: lo_sender       TYPE REF TO if_sender_bcs VALUE IS INITIAL.
  DATA: lo_recipient    TYPE REF TO if_recipient_bcs VALUE IS INITIAL.

  CLEAR vp_result.

  "Preparamos la clase para crear el objeto de envío
  CLASS cl_bcs DEFINITION LOAD.

  TRY.
      lo_send_request = cl_bcs=>create_persistent( ).

      "Creamos documento con el cuerpo y asunto
      DATA: lo_document TYPE REF TO cl_document_bcs VALUE IS INITIAL.
      IF vp_importance IS SUPPLIED.
        lo_document = cl_document_bcs=>create_document(
                                          i_type        = vp_tipo
                                          i_text        = tp_message_body[]
                                          i_importance  = vp_importance
                                          i_subject     = vp_asunto ).
      ELSE.
        lo_document = cl_document_bcs=>create_document(
                                          i_type        = vp_tipo
                                          i_text        = tp_message_body[]
                                          i_subject     = vp_asunto ).
      ENDIF.

      "Añadimos posibles anexos
      LOOP AT tp_anexos INTO wl_anexos.
        lo_document->add_attachment(
          EXPORTING
            i_attachment_type     = wl_anexos-attachment_type
            i_attachment_subject  = wl_anexos-attachment_subject
            i_attachment_size     = wl_anexos-attachment_size
            i_attachment_language = wl_anexos-attachment_language
            i_att_content_text    = wl_anexos-att_content_text
            i_att_content_hex     = wl_anexos-att_content_hex
            i_attachment_header   = wl_anexos-attachment_header
         ).
      ENDLOOP.

      "Cargamos el documento para mandar
      lo_send_request->set_document( lo_document ).

      "Indicar el remitente
      IF vp_email_sender IS NOT INITIAL.
        lo_sender ?= cl_cam_address_bcs=>create_internet_address( vp_email_sender ).
      ELSE.
        lo_sender = cl_sapuser_bcs=>create( sy-uname ).
      ENDIF.
      lo_send_request->set_sender( lo_sender ).

      "Indicar destinatarios
      LOOP AT tp_destinatarios INTO wl_destinatarios.
        lo_recipient = cl_cam_address_bcs=>create_internet_address( wl_destinatarios-email ).
        lo_send_request->add_recipient(
                              EXPORTING
                                i_recipient   = lo_recipient
                                i_express     = 'X'
                                i_no_forward  = ' ').
      ENDLOOP.

      LOOP AT tp_destinatarios_cc INTO wl_destinatarios.
        lo_recipient = cl_cam_address_bcs=>create_internet_address( wl_destinatarios-email ).
        lo_send_request->add_recipient(
                              EXPORTING
                                i_recipient   = lo_recipient
                                i_copy        = 'X'
                                i_no_forward  = ' ').
      ENDLOOP.

      LOOP AT tp_destinatarios_cco INTO wl_destinatarios.
        lo_recipient = cl_cam_address_bcs=>create_internet_address( wl_destinatarios-email ).
        lo_send_request->add_recipient(
                              EXPORTING
                                i_recipient   = lo_recipient
                                i_blind_copy  = 'X'
                                i_no_forward  = ' ').
      ENDLOOP.


      "Atributos de estado
      lo_send_request->set_status_attributes( i_requested_status = 'N' ).

      "Enviando correo....
      lo_send_request->set_send_immediately( abap_true ).
      lo_send_request->send(
                        EXPORTING
                            i_with_error_screen = vp_with_error_screen
                        RECEIVING
                            result = vp_result ).
      IF vp_commit IS NOT INITIAL.
        COMMIT WORK.
      ENDIF.

      WAIT UP TO 1 SECONDS.

    CATCH cx_send_req_bcs.
      vp_result = ''.
    CATCH cx_document_bcs.
      vp_result = ''.
    CATCH cx_address_bcs.
      vp_result = ''.
  ENDTRY.



ENDFUNCTION.
